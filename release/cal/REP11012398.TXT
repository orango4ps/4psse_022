OBJECT Report 11012398 Export Sales Credit Memos XML
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 114=m;
    CaptionML=[DEU=Export Verkaufsrechnungen XML;
               ENU=Export Sales Invoices XML;
               NLD=Export verkoopfacturen XML;
               NOR=Eksporter salgsfakturaer XML;
               SVE=Exportera f”rs„ljningsfakturor XML];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  Window.OPEN(Text000);
                END;

    OnPostReport=BEGIN
                   IF (NumOfExports <> 0) THEN
                     MESSAGE(Text001, NumOfExports)
                   ELSE
                     MESSAGE(Text002);
                 END;

  }
  DATASET
  {
    { 8098;    ;DataItem;                    ;
               DataItemTable=Table114;
               OnPreDataItem=BEGIN
                               IF NOT IncludeSentInvoices THEN BEGIN
                                 FILTERGROUP(10);
                                 SETFILTER("Exported on", '%1', 0D);
                                 FILTERGROUP(0);
                               END;

                               NumOfInvoices := COUNT;
                             END;

               OnAfterGetRecord=VAR
                                  CustGLNRelation@1100525003 : Record 11020562;
                                  TempBlob@1100525005 : Record 99008535;
                                  ThreeTierAutomationMgt@1100525004 : Codeunit 419;
                                  OutputFileName@1100525006 : Text[1024];
                                  Stream@1100525000 : OutStream;
                                  SalesCreditMemoHeader@1100525002 : Record 114;
                                BEGIN
                                  NumOfRec := NumOfRec + 1;
                                  Window.UPDATE(1,100 * ROUND(NumOfRec / NumOfInvoices * 100,1));

                                  IF NOT CustGLNRelation.GET("Sell-to Customer No.") THEN
                                    CurrReport.SKIP;

                                  IF gFromPost AND (NOT CustGLNRelation."Create XML while Posting") THEN
                                    CurrReport.SKIP;

                                  CustGLNRelation.TESTFIELD("Export Folder XML");

                                  SalesCreditMemoHeader.SETRANGE("No.", "No.");
                                  OutputFileName := STRSUBSTNO('%1\%2.xml', DELCHR(CustGLNRelation."Export Folder XML", '>', '\'), "No.");
                                  TempBlob.Blob.CREATEOUTSTREAM(Stream);
                                  XMLPORT.EXPORT(XMLPORT::"Export Sales Credit Memos", Stream, SalesCreditMemoHeader);
                                  ThreeTierAutomationMgt.BLOBExport4PS(TempBlob, OutputFileName, FALSE, TRUE);

                                  FIND;
                                  "Exported on" := TODAY;
                                  "Invoice Exported by XML" := TRUE;
                                  MODIFY;

                                  NumOfExports := NumOfExports + 1;
                                END;

               ReqFilterFields=No.,Exported on }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525001;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100525002;2;Field  ;
                  CaptionML=[DEU=Einschliesslich bereits versendete Rechnungen;
                             ENU=Include Already Sent Invoices;
                             NLD=Inclusief reeds verstuurde facturen;
                             NOR=Inkluder allerede sendte fakturaer;
                             SVE=Inklusive redan skickade fakturor];
                  SourceExpr=IncludeSentInvoices }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Window@1100525008 : Dialog;
      IncludeSentInvoices@1100525000 : Boolean;
      Text000@1100525001 : TextConst 'DEU=Rechnungen      @1@@@@@@@@\;ENU=Invoices            @1@@@@@@@@\;NLD=Facturen           @1@@@@@@@@\;NOR=Fakturaer            @1@@@@@@@@\;SVE=Fakturor            @1@@@@@@@@\';
      Text001@1100525002 : TextConst 'DEU=%1 Rechnungen wurden exportiert.;ENU=%1 Invoices are exported.;NLD=%1 facturen zijn ge‰xporteerd.;NOR=%1 fakturaer er eksportert.;SVE=%1 fakturor har exporterats.';
      Text002@1100525003 : TextConst 'DEU=Nichts exportiert;ENU=Nothing exported;NLD=Niets ge‰xporteerd;NOR=Ingenting er eksportert;SVE=Inget har exporterats';
      gFromPost@1100528200 : Boolean;
      NumOfInvoices@1100525004 : Integer;
      NumOfRec@1100525006 : Integer;
      NumOfExports@1100525005 : Integer;

    PROCEDURE SetReportSelection@1100528203(lFromPost@1100528200 : Boolean);
    BEGIN
      gFromPost := lFromPost;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

