OBJECT Report 11128524 Suggest Remittance Payments
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVNO10.00 (15000001),4PSSE.NO;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Suggest Remittance Payments;
               NOR=Remitteringsforslag];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 111280000;;DataItem;                   ;
               DataItemTable=Table11128090;
               DataItemTableView=SORTING(No.)
                                 WHERE(Remittance=CONST(Yes));
               ReqFilterFields=Remittance Account Code,No. }

    { 3182;1   ;DataItem;                    ;
               DataItemTable=Table23;
               OnPreDataItem=BEGIN
                               IF LastDueDateToPayReq = 0D THEN
                                 ERROR(Text000);
                               IF (PostingDate = 0D) AND NOT ReplacePostingDateWithDueDate THEN
                                 ERROR(Text001);

                               BankPmtType := GenJnlLine."Bank Payment Type";
                               BalAccType := GenJnlLine."Bal. Account Type";
                               BalAccNo := GenJnlLine."Bal. Account No.";
                               GenJnlLineInserted := FALSE;
                               SeveralCurrencies := FALSE;
                               MessageText := '';

                               IF BankPmtType = BankPmtType::"Manual Check" THEN
                                 ERROR(Text017,SELECTSTR(BankPmtType + 1,Text023));

                               IF ReplacePostingDateWithDueDate THEN
                                 PostingDate := 0D;

                               IF UsePaymentDisc AND (LastDueDateToPayReq < WORKDATE) THEN
                                 IF NOT CONFIRM(Text003,FALSE,WORKDATE) THEN
                                   ERROR(Text005);

                               //>>IME269
                               IF (GETFILTER(Blocked) = '') AND (NOT IncludeBlockedVendors) THEN
                                 SETRANGE(Blocked,Blocked::" ");
                               //<<IME269

                               Vend2.COPYFILTERS(Vendor);

                               OriginalAmtAvailable := AmountAvailable;
                               IF UsePriority THEN BEGIN
                                 SETCURRENTKEY(Priority);
                                 SETRANGE(Priority,1,2147483647);
                                 UsePriority := TRUE;
                               END;
                               Window.OPEN(Text006);

                               NextEntryNo := 1;
                             END;

               OnAfterGetRecord=BEGIN
                                  VendorExt.InitGetLine(Vendor);

                                  IF StopPayments THEN
                                    CurrReport.BREAK;
                                  Window.UPDATE(1,"No.");
                                  RemAccount.GET(VendorExt."Remittance Account Code");
                                  IF NOT OnlyInvoice THEN
                                    GetVendLedgEntries(TRUE,FALSE);
                                  GetVendLedgEntries(FALSE,FALSE);
                                  CheckAmounts(FALSE);
                                END;

               OnPostDataItem=BEGIN
                                IF UsePriority AND NOT StopPayments THEN BEGIN
                                  RESET;
                                  COPYFILTERS(Vend2);
                                  SETCURRENTKEY(Priority);
                                  SETRANGE(Priority,0);
                                  IF FIND('-') THEN
                                    REPEAT
                                      Window.UPDATE(1,"No.");
                                      GetVendLedgEntries(TRUE,FALSE);
                                      GetVendLedgEntries(FALSE,FALSE);
                                      CheckAmounts(FALSE);
                                    UNTIL (NEXT = 0) OR StopPayments;
                                END;

                                IF UsePaymentDisc AND NOT StopPayments THEN BEGIN
                                  RESET;
                                  COPYFILTERS(Vend2);
                                  Window.OPEN(Text007);
                                  IF FIND('-') THEN
                                    REPEAT
                                      Window.UPDATE(1,"No.");
                                      PayableVendLedgEntry.SETRANGE("Vendor No.","No.");
                                      GetVendLedgEntries(TRUE,TRUE);
                                      GetVendLedgEntries(FALSE,TRUE);
                                      CheckAmounts(TRUE);
                                    UNTIL (NEXT = 0) OR StopPayments;
                                END;

                                GenJnlLine.LOCKTABLE;
                                GenJnlTemplate.GET(GenJnlLine."Journal Template Name");
                                GenJnlBatch.GET(GenJnlLine."Journal Template Name",GenJnlLine."Journal Batch Name");
                                GenJnlLine.SETRANGE("Journal Template Name",GenJnlLine."Journal Template Name");
                                GenJnlLine.SETRANGE("Journal Batch Name",GenJnlLine."Journal Batch Name");
                                IF GenJnlLine.FINDLAST THEN BEGIN
                                  LastLineNo := GenJnlLine."Line No.";
                                  GenJnlLine.INIT;
                                END;

                                Window.OPEN(Text008);

                                PayableVendLedgEntry.RESET;
                                PayableVendLedgEntry.SETRANGE(Priority,1,2147483647);
                                MakeGenJnlLines;
                                PayableVendLedgEntry.RESET;
                                PayableVendLedgEntry.SETRANGE(Priority,0);
                                MakeGenJnlLines;
                                PayableVendLedgEntry.RESET;
                                PayableVendLedgEntry.DELETEALL;

                                Window.CLOSE;
                                ShowMessage(MessageText);
                              END;

               ReqFilterFields=No.,Payment Method Code;
               DataItemLink=No.=FIELD(No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF PostingDate = 0D THEN
                     PostingDate := WORKDATE;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Last Payment Date;
                             NOR=Siste betalingsdato;
                             SVE=Senaste betalningsdatum];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=LastDueDateToPayReq }

      { 2   ;2   ;Field     ;
                  CaptionML=[ENU=Find Payment Discounts;
                             NOR=Sok etter kontantrabatter;
                             SVE=Utnyttja kassarabatter];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=UsePaymentDisc;
                  MultiLine=Yes }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Use Vendor Priority;
                             NOR=Bruk leverandorprioritet;
                             SVE=AnvÑnd leverantîrsprioritet];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=UsePriority;
                  OnValidate=BEGIN
                               IF NOT UsePriority AND (AmountAvailable <> 0) THEN
                                 ERROR(Text011);
                             END;
                              }

      { 4   ;2   ;Field     ;
                  CaptionML=[ENU=Available Amount (LCY);
                             NOR=Disponibelt belop (NOK);
                             SVE=Disponibelt belopp (BVA)];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=AmountAvailable;
                  OnValidate=BEGIN
                               AmountAvailableOnAfterValidate;
                             END;
                              }

      { 5   ;2   ;Field     ;
                  CaptionML=[ENU=Posting Date;
                             NOR=Bokforingsdato;
                             SVE=Bokfîringsdatum];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=PostingDate;
                  OnValidate=BEGIN
                               ValidatePostingDate;
                             END;
                              }

      { 1080001;2;Field     ;
                  CaptionML=[ENU=Replace Posting Date with Due Date;
                             NOR=Erstatt bokforingsdato med forfallsdato;
                             SVE="ErsÑtt bokfîringsdatum med fîrfallodatum "];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=ReplacePostingDateWithDueDate }

      { 1080006;2;Field     ;
                  CaptionML=[ENU=Test Document Type;
                             NOR=Sjekk bilagstype;
                             SVE=Testa dokumenttyp];
                  OptionCaptionML=[ENU=All,Invoice/Credit memo;
                                   NOR=Alle,Faktura/Kreditnota;
                                   SVE=Alla,Faktura/Kreditnota];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=CheckLedgEntryType }

      { 1080002;2;Field     ;
                  CaptionML=[ENU=Invoice/Debit Vendor Ledger Entries only;
                             NOR=Bare faktura-/debetposter;
                             SVE=Endast faktura-/debitposter];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=OnlyInvoice }

      { 1100285500;2;Field  ;
                  CaptionML=[ENU=Include blocked Vendors;
                             NOR=Ta med sperret leverandorer;
                             SVE=Ta med spÑrrade leverantîrer];
                  SourceExpr=IncludeBlockedVendors }

      { 1000000000;2;Field  ;
                  CaptionML=[ENU=Include negative balance per Vendor;
                             NOR=Inkluder negativ saldo per Vendor;
                             SVE=Inkludera negativt saldo per leverantîr];
                  SourceExpr=InclNegBalance }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Please enter the last payment date.;NOR=Angi siste betalingsdato.';
      Text001@1001 : TextConst 'ENU=Please enter the posting date.;NOR=Angi Bokforingsdato.;SVE=Skriv in bokfîringsdatum.';
      Text003@1003 : TextConst 'ENU=The payment date is earlier than %1.\\Do you want to continue?;NOR=Betalingsdatoen er tidligere enn %1.\\Vil du fortsette?';
      Text005@1005 : TextConst 'ENU=The batch job was interrupted.;NOR=Kjorselen ble avbrutt.;SVE=Batch-jobbet blev avbrutet.';
      Text006@1006 : TextConst 'ENU=Processing vendors     #1##########;NOR=Behandler leverandorer #1##########;SVE=Behandlar leverantîrer                   #1##########';
      Text007@1007 : TextConst 'ENU=Processing vendors for payment discounts #1##########;NOR=Behandler leverandorer for kontantrabatt #1##########;SVE=Behandlar leverantîrers kassarabatter    #1##########';
      Text008@1008 : TextConst 'ENU=Inserting payment journal lines #1##########;NOR=Setter inn betalingskladdelinjer         #1##########;SVE=Infogar utbetalningsjnlrader             #1##########';
      Text011@1011 : TextConst 'ENU=Use Vendor Priority must be activated when the value in the Amount Available field is not 0.;NOR=Bruk leverandorprioritet mÜ aktiveres nÜr verdien i feltet Disponibelt belop ikke er 0.;SVE=AnvÑnd leverantîrsprioritet mÜste aktiveras nÑr vÑrdet i fÑltet disponibelt belopp inte Ñr noll.';
      Text016@1016 : TextConst '@@@=Parameter 1 - document type, 2 - document number, 3 - vendor number.;ENU=" is already applied to %1 %2 for vendor %3.";NOR=" er allerede utlignet mot %1 %2 for leverandor %3.";SVE=" Ñr redan kopplad till %1 %2 fîr leverantîr %3."';
      Text017@1017 : TextConst 'ENU="When Bank Payment Type = %1 and you have not selected in the Summarize per Vendor field,\then you must select the New Doc. No. per Line field.";NOR="NÜr bankbetalingstype = %1 og du ikke har valgt feltet Summer per leverandor,\mÜ du velge feltet Nytt bilagsnr. per linje."';
      Text019@1019 : TextConst 'ENU=You have only created suggested vendor payment lines for the Currency Code %1. There are, however, other open vendor ledger entries in currencies other than %2.;NOR=Du har bare opprettet foreslÜtte leverandorbetalingslinjer for valutakoden %1. Det finnes imidlertid andre Üpne leverandorposter i andre valutaer enn %2.';
      Text021@1021 : TextConst 'ENU=You have only created suggested vendor payment lines for the Currency Code %1. There are no other open vendor ledger entries in other currencies.;NOR=Du har bare opprettet foreslÜtte leverandorbetalingslinjer for valutakoden %1. Det finnes ingen andre Üpne leverandorposter i andre valutaer.';
      Text022@1022 : TextConst 'ENU=You have created suggested vendor payment lines for all currencies.;NOR=Du har opprettet foreslÜtte leverandorbetalingslinjer for alle valutaer.;SVE=Du har skapat betalningsfîrslagsrader (lev.) fîr alla valutor.';
      Vend2@1023 : Record 23;
      GenJnlTemplate@1024 : Record 80;
      GenJnlBatch@1025 : Record 232;
      GenJnlLine@1026 : Record 81;
      VendLedgEntry@1029 : Record 25;
      PayableVendLedgEntry@1032 : TEMPORARY Record 317;
      TempPaymentBuffer@1033 : TEMPORARY Record 372;
      OldTempPaymentBuffer@1034 : TEMPORARY Record 372;
      RemAccount@1080000 : Record 11128506;
      VendLedgEntry2@1080009 : Record 25;
      Vend3@1080010 : Record 23;
      DimMgt@1080017 : Codeunit 408;
      GenJnlPostLine@1062 : Codeunit 12;
      NoSeriesMgt@1036 : Codeunit 396;
      RemTools@1080001 : Codeunit 11128515;
      Window@1039 : Dialog;
      UsePaymentDisc@1040 : Boolean;
      PostingDate@1041 : Date;
      LastDueDateToPayReq@1042 : Date;
      NextDocNo@1043 : Code[20];
      AmountAvailable@1044 : Decimal;
      OriginalAmtAvailable@1045 : Decimal;
      UsePriority@1046 : Boolean;
      LastLineNo@1051 : Integer;
      NextEntryNo@1052 : Integer;
      StopPayments@1053 : Boolean;
      BankPmtType@1055 : ' ,Computer Check,Manual Check';
      BalAccType@1056 : 'G/L Account,Customer,Vendor,Bank Account';
      BalAccNo@1057 : Code[20];
      MessageText@1058 : Text[250];
      GenJnlLineInserted@1059 : Boolean;
      SeveralCurrencies@1060 : Boolean;
      Text023@1061 : TextConst 'ENU=" ,Computer Check,Manual Check";NOR=" ,Maskinell sjekk,Manuell sjekk";SVE=" ,Datorcheck,Handskriven check"';
      OnlyInvoice@1080002 : Boolean;
      StartText@1080003 : Text[30];
      CheckLedgEntryType@1080005 : 'All,Invoice/Credit Memo';
      Text15000000@1080006 : TextConst 'ENU=Refund;NOR=Refusjon;SVE=èterbetalning';
      Text15000001@1080007 : TextConst 'ENU=Payment;NOR=Betaling;SVE=Betalning';
      Text15000002@1080008 : TextConst '@@@=Parameter 1 - Refund or Payment, 2 - document type, 3 and 4 - document numbers.;ENU=%1 of %2 %3 (%4);NOR=%1 av %2 %3 (%4)';
      ReplacePostingDateWithDueDate@1080004 : Boolean;
      IncludeBlockedVendors@1100285500 : Boolean;
      InclNegBalance@1000000000 : Boolean;
      VendorExt@111280001 : Record 11128090;
      VendorExt3@111280002 : Record 11128090;
      GenJnLineExt@111280000 : Record 11128091;

    PROCEDURE SetGenJnlLine@1(NewGenJnlLine@1000 : Record 81);
    BEGIN
      GenJnlLine := NewGenJnlLine;
    END;

    LOCAL PROCEDURE ValidatePostingDate@7();
    BEGIN
      GenJnlBatch.GET(GenJnlLine."Journal Template Name",GenJnlLine."Journal Batch Name");
      IF GenJnlBatch."No. Series" = '' THEN
        NextDocNo := ''
      ELSE BEGIN
        NextDocNo := NoSeriesMgt.GetNextNo(GenJnlBatch."No. Series",PostingDate,FALSE);
        CLEAR(NoSeriesMgt);
      END;
    END;

    PROCEDURE GetVendLedgEntries@13(Positive@1000 : Boolean;Future@1001 : Boolean);
    BEGIN
      VendLedgEntry.RESET;
      VendLedgEntry.SETCURRENTKEY("Vendor No.",Open,Positive,"Due Date");
      VendLedgEntry.SETRANGE("Vendor No.",Vendor."No.");
      VendLedgEntry.SETRANGE(Open,TRUE);
      VendLedgEntry.SETRANGE(Positive,Positive);

      IF CheckLedgEntryType = CheckLedgEntryType::"Invoice/Credit Memo" THEN
        VendLedgEntry.SETFILTER(
          "Document Type",'%1|%2',VendLedgEntry."Document Type"::Invoice,VendLedgEntry."Document Type"::"Credit Memo");

      IF Future THEN BEGIN
        VendLedgEntry.SETRANGE("Due Date",LastDueDateToPayReq + 1,99991231D);
        VendLedgEntry.SETRANGE("Pmt. Discount Date",PostingDate,LastDueDateToPayReq);
        VendLedgEntry.SETFILTER("Remaining Pmt. Disc. Possible",'<>0');
      END ELSE
        VendLedgEntry.SETRANGE("Due Date",0D,LastDueDateToPayReq);
      VendLedgEntry.SETRANGE("On Hold",'');
      VendLedgEntry.SETFILTER("Global Dimension 1 Code",Vendor.GETFILTER("Global Dimension 1 Filter"));
      VendLedgEntry.SETFILTER("Global Dimension 2 Code",Vendor.GETFILTER("Global Dimension 2 Filter"));
      IF VendLedgEntry.FIND('-') THEN
        REPEAT
          SaveAmount;
        UNTIL VendLedgEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE SaveAmount@6();
    VAR
      PaymentToleranceMgt@1000 : Codeunit 426;
      lvVendor@1100285000 : Record 23;
    BEGIN
      WITH GenJnlLine DO BEGIN
        VALIDATE("Posting Date",CalcPostingdate(VendLedgEntry));
        IF VendLedgEntry.Positive THEN // Cr.Memo
          "Document Type" := "Document Type"::" "
        ELSE
          "Document Type" := "Document Type"::Payment;
        "Account Type" := "Account Type"::Vendor;
        VALIDATE("Account No.",VendLedgEntry."Vendor No.");
        VALIDATE("Currency Code",VendLedgEntry."Currency Code");
        VendLedgEntry.CALCFIELDS("Remaining Amount");
        //IF GenJnlPostLine.CheckCalcPmtDiscGenJnlVend(GenJnlLine,VendLedgEntry,0,FALSE)
        //IF GenJnlPostLine.CheckCalcPmtDiscGenJnlVend(GenJnlLine,VendLedgEntry,0,FALSE,0)
        IF PaymentToleranceMgt.CheckCalcPmtDiscGenJnlVend(GenJnlLine,VendLedgEntry,0,FALSE,0)
        THEN
          Amount := -(VendLedgEntry."Remaining Amount" - VendLedgEntry."Remaining Pmt. Disc. Possible")
        ELSE
          Amount := -VendLedgEntry."Remaining Amount";
        VALIDATE(Amount);

      //<<RFC-034
        lvVendor.GET(VendLedgEntry."Vendor No.");
        VendorExt.InitGetLine(lvVendor);
        GenJnLineExt.InitGetLine(GenJnlLine);
        RemAccount.GET(VendorExt."Remittance Account Code");
        IF (VendLedgEntry.KID <> '') AND (NOT RemAccount."No KID") THEN
          GenJnLineExt."BOLS Text Code" := GenJnLineExt."BOLS Text Code"::"KID transfer"
        ELSE
          IF VendLedgEntry."External Document No." <> '' THEN
            GenJnLineExt."BOLS Text Code" := GenJnLineExt."BOLS Text Code"::"Transfer without advice"
          ELSE
            GenJnLineExt."BOLS Text Code" := GenJnLineExt."BOLS Text Code"::"Transfer with advice";
        GenJnLineExt.SaveUpdateLine(GenJnlLine);
      //<<RFC-034


      END;

      IF UsePriority THEN
        PayableVendLedgEntry.Priority := Vendor.Priority
      ELSE
        PayableVendLedgEntry.Priority := 0;
      PayableVendLedgEntry."Vendor No." := VendLedgEntry."Vendor No.";
      PayableVendLedgEntry."Entry No." := NextEntryNo;
      PayableVendLedgEntry."Vendor Ledg. Entry No." := VendLedgEntry."Entry No.";
      PayableVendLedgEntry.Amount := GenJnlLine.Amount;
      PayableVendLedgEntry."Amount (LCY)" := GenJnlLine."Amount (LCY)";
      PayableVendLedgEntry.Positive := (PayableVendLedgEntry.Amount > 0);
      PayableVendLedgEntry.Future := (VendLedgEntry."Due Date" > LastDueDateToPayReq);
      PayableVendLedgEntry."Currency Code" := VendLedgEntry."Currency Code";
      //>>RCF-034
      IF NOT InclNegBalance THEN
        GenJnLineExt.InitGetLine(GenJnlLine);
        CASE GenJnLineExt."BOLS Text Code" OF
          GenJnLineExt."BOLS Text Code"::"Transfer without advice" :
            PayableVendLedgEntry."Currency Code" := VendLedgEntry."Currency Code" + '0';
          GenJnLineExt."BOLS Text Code"::"Transfer with advice":
            PayableVendLedgEntry."Currency Code" := VendLedgEntry."Currency Code" + '1';
          GenJnLineExt."BOLS Text Code"::"KID transfer":
            PayableVendLedgEntry."Currency Code" := VendLedgEntry."Currency Code" + '2';
        END;
      //<<RCF-034
      PayableVendLedgEntry.INSERT;
      NextEntryNo := NextEntryNo + 1;
    END;

    PROCEDURE CheckAmounts@10(Future@1000 : Boolean);
    VAR
      CurrencyBalance@1001 : Decimal;
      PrevCurrency@1002 : Code[10];
    BEGIN
      PayableVendLedgEntry.SETRANGE("Vendor No.",Vendor."No.");
      PayableVendLedgEntry.SETRANGE(Future,Future);

      IF PayableVendLedgEntry.FIND('-') THEN BEGIN
        PrevCurrency := PayableVendLedgEntry."Currency Code";
        REPEAT
          IF PayableVendLedgEntry."Currency Code" <> PrevCurrency THEN BEGIN
            IF CurrencyBalance < 0 THEN BEGIN
              PayableVendLedgEntry.SETRANGE("Currency Code",PrevCurrency);
              PayableVendLedgEntry.DELETEALL;
              PayableVendLedgEntry.SETRANGE("Currency Code");
            END ELSE
              AmountAvailable := AmountAvailable - CurrencyBalance;
            CurrencyBalance := 0;
            PrevCurrency := PayableVendLedgEntry."Currency Code";
          END;
          IF (OriginalAmtAvailable = 0) OR
             (AmountAvailable >= CurrencyBalance + PayableVendLedgEntry."Amount (LCY)")
          THEN
            CurrencyBalance := CurrencyBalance + PayableVendLedgEntry."Amount (LCY)"
          ELSE
            PayableVendLedgEntry.DELETE;
        UNTIL PayableVendLedgEntry.NEXT = 0;

        IF (CurrencyBalance < 0) AND
           ((NOT UsePaymentDisc) OR (UsePaymentDisc AND Future))
        THEN BEGIN
          PayableVendLedgEntry.SETRANGE("Currency Code",PrevCurrency);
          PayableVendLedgEntry.DELETEALL;
          PayableVendLedgEntry.SETRANGE("Currency Code");
        END ELSE
          IF OriginalAmtAvailable > 0 THEN
            AmountAvailable := AmountAvailable - CurrencyBalance;
        IF (OriginalAmtAvailable > 0) AND (AmountAvailable <= 0) THEN
          StopPayments := TRUE;
      END;
      PayableVendLedgEntry.RESET;
    END;

    LOCAL PROCEDURE MakeGenJnlLines@2();
    VAR
      GenJnlLine3@1000 : Record 81;
    BEGIN
      TempPaymentBuffer.RESET;
      TempPaymentBuffer.DELETEALL;

      IF BalAccType = BalAccType::"Bank Account" THEN BEGIN
        CheckCurrencies(BalAccType,BalAccNo,PayableVendLedgEntry);
        SetBankAccCurrencyFilter(BalAccType,BalAccNo,PayableVendLedgEntry);
      END;

      IF PayableVendLedgEntry.FIND('-') THEN
        REPEAT
          PayableVendLedgEntry.SETRANGE("Vendor No.",PayableVendLedgEntry."Vendor No.");
          PayableVendLedgEntry.FIND('-');
          REPEAT
            VendLedgEntry.GET(PayableVendLedgEntry."Vendor Ledg. Entry No.");
            TempPaymentBuffer."Vendor No." := VendLedgEntry."Vendor No.";
            TempPaymentBuffer."Currency Code" := VendLedgEntry."Currency Code";
            TempPaymentBuffer."Dimension Entry No." := 0;
            TempPaymentBuffer."Global Dimension 1 Code" := '';
            TempPaymentBuffer."Global Dimension 2 Code" := '';

            GenJnlLine3.RESET;
            GenJnlLine3.SETCURRENTKEY(
              "Account Type","Account No.","Applies-to Doc. Type","Applies-to Doc. No.");
            GenJnlLine3.SETRANGE("Account Type",GenJnlLine3."Account Type"::Vendor);
            GenJnlLine3.SETRANGE("Account No.",VendLedgEntry."Vendor No.");
            GenJnlLine3.SETRANGE("Applies-to Doc. Type",VendLedgEntry."Document Type");
            GenJnlLine3.SETRANGE("Applies-to Doc. No.",VendLedgEntry."Document No.");
            IF GenJnlLine3.FINDFIRST THEN
              GenJnlLine3.FIELDERROR(
                "Applies-to Doc. No.",
                STRSUBSTNO(
                  Text016,
                  VendLedgEntry."Document Type",VendLedgEntry."Document No.",
                  VendLedgEntry."Vendor No."));

            TempPaymentBuffer."Vendor Ledg. Entry Doc. Type" := VendLedgEntry."Document Type";
            TempPaymentBuffer."Vendor Ledg. Entry Doc. No." := VendLedgEntry."Document No.";
            TempPaymentBuffer."Global Dimension 1 Code" := VendLedgEntry."Global Dimension 1 Code";
            TempPaymentBuffer."Global Dimension 2 Code" := VendLedgEntry."Global Dimension 2 Code";
            TempPaymentBuffer."Dimension Set ID" := VendLedgEntry."Dimension Set ID";
            TempPaymentBuffer."Vendor Ledg. Entry No." := VendLedgEntry."Entry No.";
            TempPaymentBuffer.Amount := PayableVendLedgEntry.Amount;
            Window.UPDATE(1,VendLedgEntry."Vendor No.");
            TempPaymentBuffer.INSERT;
          UNTIL PayableVendLedgEntry.NEXT = 0;
          PayableVendLedgEntry.DELETEALL;
          PayableVendLedgEntry.SETRANGE("Vendor No.");
        UNTIL NOT PayableVendLedgEntry.FIND('-');

      CLEAR(OldTempPaymentBuffer);
      TempPaymentBuffer.SETCURRENTKEY("Document No.");
      IF TempPaymentBuffer.FIND('-') THEN
        REPEAT
          WITH GenJnlLine DO BEGIN
            INIT;
            Window.UPDATE(1,TempPaymentBuffer."Vendor No.");
            LastLineNo := LastLineNo + 10000;
            "Line No." := LastLineNo;

            VendLedgEntry2.GET(TempPaymentBuffer."Vendor Ledg. Entry No.");
            VALIDATE("Posting Date",CalcPostingdate(VendLedgEntry2));
            IF VendLedgEntry2.Positive THEN BEGIN // Cr.Memo
              "Document Type" := "Document Type"::" ";
              StartText := Text15000000;
            END ELSE BEGIN
              "Document Type" := "Document Type"::Payment;
              StartText := Text15000001;
            END;

            "Posting No. Series" := GenJnlBatch."Posting No. Series";
            IF (TempPaymentBuffer."Vendor No." = OldTempPaymentBuffer."Vendor No.") AND
               (TempPaymentBuffer."Currency Code" = OldTempPaymentBuffer."Currency Code")
            THEN
              "Document No." := OldTempPaymentBuffer."Document No."
            ELSE BEGIN
              "Document No." := NextDocNo;
              NextDocNo := INCSTR(NextDocNo);
              OldTempPaymentBuffer := TempPaymentBuffer;
              OldTempPaymentBuffer."Document No." := "Document No.";
            END;
            "Account Type" := "Account Type"::Vendor;
            VALIDATE("Account No.",TempPaymentBuffer."Vendor No.");
            "Bal. Account Type" := BalAccType;
            VALIDATE("Bal. Account No.",BalAccNo);
            VALIDATE("Currency Code",TempPaymentBuffer."Currency Code");
            "Bank Payment Type" := BankPmtType;

            VendLedgEntry2.GET(TempPaymentBuffer."Vendor Ledg. Entry No.");
            // Find VendLedg.Entry. Need "External Document No."
            Description :=
              COPYSTR(
                STRSUBSTNO(
                  Text15000002,
                  StartText,VendLedgEntry2."Document Type",VendLedgEntry2."Document No.",
                  VendLedgEntry2."External Document No."),1,MAXSTRLEN(Description));
            Vend3.GET(VendLedgEntry2."Vendor No.");
            VendorExt3.InitGetLine(Vend3);//4PSSE
            GenJnLineExt.InitGetLine(GenJnlLine);//4PSSE
            GenJnLineExt.VALIDATE("Remittance Account Code",VendorExt3."Remittance Account Code");
            GenJnLineExt.VALIDATE("Payment Due Date",VendLedgEntry2."Due Date");
            VALIDATE("External Document No.",VendLedgEntry2."External Document No.");
            GenJnLineExt.SaveUpdateLine(GenJnlLine);//4PSSE

            "Shortcut Dimension 1 Code" := TempPaymentBuffer."Global Dimension 1 Code";
            "Shortcut Dimension 2 Code" := TempPaymentBuffer."Global Dimension 2 Code";
            "Source Code" := GenJnlTemplate."Source Code";
            "Reason Code" := GenJnlBatch."Reason Code";
            VALIDATE(Amount,TempPaymentBuffer.Amount);
            "Applies-to Doc. Type" := TempPaymentBuffer."Vendor Ledg. Entry Doc. Type";
            "Applies-to Doc. No." := TempPaymentBuffer."Vendor Ledg. Entry Doc. No.";
            GenJnLineExt.InitGetLine(GenJnlLine);//4PSSE
            RemTools.CreateJournalData(GenJnlLine,GenJnLineExt,VendLedgEntry2);
            GenJnLineExt."Payment Type Code Abroad" := VendLedgEntry2."Payment Type Code Abroad";
            GenJnLineExt."Specification (Norges Bank)" := VendLedgEntry2."Specification (Norges Bank)";
            GenJnLineExt.SaveUpdateLine(GenJnlLine);//4PSSE

            "Dimension Set ID" := TempPaymentBuffer."Dimension Set ID";
            UpdateDimensions(GenJnlLine);
      //<<RFC-034
            RemAccount.GET(VendorExt3."Remittance Account Code");
            IF (GenJnLineExt.KID <> '') AND (NOT RemAccount."No KID") THEN
              GenJnLineExt."BOLS Text Code" := GenJnLineExt."BOLS Text Code"::"KID transfer"
            ELSE
              IF VendLedgEntry."External Document No." <> '' THEN
                GenJnLineExt."BOLS Text Code" := GenJnLineExt."BOLS Text Code"::"Transfer without advice"
              ELSE
                GenJnLineExt."BOLS Text Code" := GenJnLineExt."BOLS Text Code"::"Transfer with advice";
            GenJnLineExt.SaveUpdateLine(GenJnlLine);//4PSSE
      //<<RFC-034

            "Payment Reference" := VendLedgEntry2."Payment Reference"; //RFC155
            "Bal. Account Type" := "Bal. Account Type"::"G/L Account";
            "Bal. Account No." := '';

            INSERT;
            GenJnlLineInserted := TRUE;
          END;
        UNTIL TempPaymentBuffer.NEXT = 0;
    END;

    LOCAL PROCEDURE SetBankAccCurrencyFilter@11(BalAccType@1000 : 'G/L Account,Customer,Vendor,Bank Account';BalAccNo@1001 : Code[20];VAR TmpPayableVendLedgEntry@1002 : Record 317);
    VAR
      BankAcc@1003 : Record 270;
    BEGIN
      IF BalAccType = BalAccType::"Bank Account" THEN
        IF BalAccNo <> '' THEN BEGIN
          BankAcc.GET(BalAccNo);
          IF BankAcc."Currency Code" <> '' THEN
            TmpPayableVendLedgEntry.SETRANGE("Currency Code",BankAcc."Currency Code");
        END;
    END;

    LOCAL PROCEDURE ShowMessage@15(Text@1000 : Text[250]);
    BEGIN
      IF (Text <> '') AND GenJnlLineInserted THEN
        MESSAGE(Text);
    END;

    LOCAL PROCEDURE CheckCurrencies@4(BalAccType@1000 : 'G/L Account,Customer,Vendor,Bank Account';BalAccNo@1001 : Code[20];VAR TmpPayableVendLedgEntry@1002 : Record 317);
    VAR
      BankAcc@1003 : Record 270;
      TmpPayableVendLedgEntry2@1004 : TEMPORARY Record 317;
    BEGIN
      IF BalAccType = BalAccType::"Bank Account" THEN
        IF BalAccNo <> '' THEN BEGIN
          BankAcc.GET(BalAccNo);
          IF BankAcc."Currency Code" <> '' THEN BEGIN
            TmpPayableVendLedgEntry2.RESET;
            TmpPayableVendLedgEntry2.DELETEALL;
            IF TmpPayableVendLedgEntry.FIND('-') THEN
              REPEAT
                TmpPayableVendLedgEntry2 := TmpPayableVendLedgEntry;
                TmpPayableVendLedgEntry2.INSERT;
              UNTIL TmpPayableVendLedgEntry.NEXT = 0;

            TmpPayableVendLedgEntry2.SETFILTER("Currency Code",'<>%1',BankAcc."Currency Code");
            SeveralCurrencies := SeveralCurrencies OR TmpPayableVendLedgEntry2.FINDFIRST;

            IF SeveralCurrencies THEN
              MessageText := STRSUBSTNO(Text019,BankAcc."Currency Code")
            ELSE
              MessageText := STRSUBSTNO(Text021,BankAcc."Currency Code");
          END ELSE
            MessageText := Text022;
        END;
    END;

    LOCAL PROCEDURE CalcPostingdate@1080003(VendLedgEntry@1000000000 : Record 25) : Date;
    BEGIN
      IF ReplacePostingDateWithDueDate THEN BEGIN
        IF UsePaymentDisc AND
           (VendLedgEntry."Pmt. Discount Date" <> 0D) AND
           (VendLedgEntry."Pmt. Discount Date" <= LastDueDateToPayReq) AND
           (VendLedgEntry."Remaining Pmt. Disc. Possible" <> 0)
        THEN
          EXIT(VendLedgEntry."Pmt. Discount Date");

        EXIT(VendLedgEntry."Due Date");
      END;

      EXIT(PostingDate);
    END;

    LOCAL PROCEDURE AmountAvailableOnAfterValidate@19035272();
    BEGIN
      IF AmountAvailable <> 0 THEN
        UsePriority := TRUE;
    END;

    LOCAL PROCEDURE UpdateDimensions@17(VAR GenJnlLine@1005 : Record 81);
    VAR
      DimSetID@1003 : Integer;
      DimSetIDArr@1006 : ARRAY [10] OF Integer;
    BEGIN
      WITH GenJnlLine DO BEGIN
        DimSetID := "Dimension Set ID";
        CreateDim(
          DimMgt.TypeToTableID1("Account Type"),"Account No.",
          DimMgt.TypeToTableID1("Bal. Account Type"),"Bal. Account No.",
          DATABASE::Job,"Job No.",
          DATABASE::"Salesperson/Purchaser","Salespers./Purch. Code",
          DATABASE::Campaign,"Campaign No.",
          0,'',
          0,'',
          0,''
          );
        IF DimSetID <> "Dimension Set ID" THEN BEGIN
          DimSetIDArr[1] := "Dimension Set ID";
          DimSetIDArr[2] := DimSetID;
          "Dimension Set ID" :=
            DimMgt.GetCombinedDimensionSetID(DimSetIDArr,"Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
        END;
      END;
    END;

    BEGIN
    {
      150109 IME-269 ITERO.DL
      160929 ITERO.DL RFC155 handle Payment Reference
    }
    END.
  }
  RDLDATA
  {
  }
}

