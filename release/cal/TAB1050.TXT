OBJECT Table 1050 Additional Fee Setup
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Additional Fee Setup;
               NOR=Oppsett av tilleggsgebyr;
               SVE=Avgiftsinst„llningar];
    LookupPageID=Page1050;
    DrillDownPageID=Page1050;
  }
  FIELDS
  {
    { 1   ;   ;Charge Per Line     ;Boolean       ;CaptionML=[ENU=Charge Per Line;
                                                              NOR=Gebyr per linje;
                                                              SVE=Debitering per rad] }
    { 2   ;   ;Reminder Terms Code ;Code10        ;TableRelation="Reminder Terms".Code;
                                                   CaptionML=[ENU=Reminder Terms Code;
                                                              NOR=Purrebetingelseskode;
                                                              SVE=Betalningsp†minnelsekod];
                                                   NotBlank=Yes }
    { 3   ;   ;Reminder Level No.  ;Integer       ;TableRelation="Reminder Level".No.;
                                                   CaptionML=[ENU=Reminder Level No.;
                                                              NOR=Purregradnr.;
                                                              SVE=Betalningsp†minnelseniv†nr];
                                                   NotBlank=Yes }
    { 4   ;   ;Currency Code       ;Code10        ;TableRelation=Currency.Code;
                                                   CaptionML=[ENU=Currency Code;
                                                              NOR=Valutakode;
                                                              SVE=Valutakod] }
    { 5   ;   ;Threshold Remaining Amount;Decimal ;CaptionML=[ENU=Threshold Remaining Amount;
                                                              NOR=Terskel restbel›p;
                                                              SVE=Gr„nsv„rde f”r †terst†ende belopp];
                                                   MinValue=0 }
    { 6   ;   ;Additional Fee Amount;Decimal      ;OnValidate=VAR
                                                                ReminderLevel@1000 : Record 293;
                                                              BEGIN
                                                                IF "Currency Code" = '' THEN BEGIN
                                                                  ReminderLevel.GET("Reminder Terms Code","Reminder Level No.");
                                                                  IF "Charge Per Line" THEN
                                                                    ReminderLevel.VALIDATE("Add. Fee per Line Amount (LCY)","Additional Fee Amount")
                                                                  ELSE
                                                                    ReminderLevel.VALIDATE("Additional Fee (LCY)","Additional Fee Amount");
                                                                  ReminderLevel.MODIFY(TRUE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Additional Fee Amount;
                                                              NOR=Tilleggsgebyrbel›p;
                                                              SVE=Avgiftsbelopp];
                                                   MinValue=0 }
    { 7   ;   ;Additional Fee %    ;Decimal       ;CaptionML=[ENU=Additional Fee %;
                                                              NOR=Tilleggsgebyr %;
                                                              SVE=Avgift %];
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 8   ;   ;Min. Additional Fee Amount;Decimal ;OnValidate=BEGIN
                                                                IF ("Max. Additional Fee Amount" > 0) AND ("Min. Additional Fee Amount" > "Max. Additional Fee Amount") THEN
                                                                  ERROR(InvalidMaxAddFeeErr,FIELDCAPTION("Min. Additional Fee Amount"),FIELDCAPTION("Max. Additional Fee Amount"));
                                                              END;

                                                   CaptionML=[ENU=Min. Additional Fee Amount;
                                                              NOR=Min. tilleggsgebyrbel›p;
                                                              SVE=L„gsta avgiftsbelopp];
                                                   MinValue=0 }
    { 9   ;   ;Max. Additional Fee Amount;Decimal ;OnValidate=BEGIN
                                                                IF ("Max. Additional Fee Amount" > 0) AND ("Min. Additional Fee Amount" > "Max. Additional Fee Amount") THEN
                                                                  ERROR(InvalidMaxAddFeeErr,FIELDCAPTION("Min. Additional Fee Amount"),FIELDCAPTION("Max. Additional Fee Amount"));
                                                              END;

                                                   CaptionML=[ENU=Max. Additional Fee Amount;
                                                              NOR=Maks. tilleggsgebyrbel›p;
                                                              SVE=H”gsta avgiftsbelopp];
                                                   MinValue=0 }
  }
  KEYS
  {
    {    ;Reminder Terms Code,Reminder Level No.,Charge Per Line,Currency Code,Threshold Remaining Amount;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      InvalidMaxAddFeeErr@1001 : TextConst '@@@=%1 : Min. Additional Fee Amount,%2 : Max Additional Fee Amount;ENU=The value of the %1 field is greater than the value of the %2 field. You must change one of the values.;NOR=Verdien i feltet %1 er st›rre enn verdien i feltet %2. Du m† endre en av verdiene.;SVE=V„rdet i f„ltet %1 „r st”rre „n v„rdet i f„ltet %2. Du m†ste „ndra ett av v„rdena.';

    LOCAL PROCEDURE CalculateAddFeeSingleDynamic@1(VAR AdditionalFeeSetup@1000 : Record 1050;RemainingAmount@1002 : Decimal) : Decimal;
    VAR
      AdditionalFee@1001 : Decimal;
    BEGIN
      WITH AdditionalFeeSetup DO BEGIN
        IF NOT FINDSET THEN
          EXIT(0);
        REPEAT
          IF RemainingAmount >= "Threshold Remaining Amount" THEN BEGIN
            IF "Additional Fee Amount" > 0 THEN
              AdditionalFee := "Additional Fee Amount";

            IF "Additional Fee %" > 0 THEN
              AdditionalFee += RemainingAmount * "Additional Fee %" / 100;

            IF ("Max. Additional Fee Amount" > 0) AND (AdditionalFee > "Max. Additional Fee Amount") THEN
              AdditionalFee := "Max. Additional Fee Amount";

            IF AdditionalFee < "Min. Additional Fee Amount" THEN
              AdditionalFee := "Min. Additional Fee Amount";

            EXIT(AdditionalFee);
          END;
        UNTIL NEXT = 0;
        EXIT(0);
      END;
    END;

    LOCAL PROCEDURE CalculateAddFeeAccumulatedDynamic@1000(VAR AdditionalFeeSetup@1000 : Record 1050;RemainingAmount@1002 : Decimal) : Decimal;
    VAR
      AdditionalFee@1001 : Decimal;
      RangeAddFeeAmount@1004 : Decimal;
    BEGIN
      WITH AdditionalFeeSetup DO BEGIN
        IF NOT FINDSET THEN
          EXIT(0);
        REPEAT
          IF RemainingAmount >= "Threshold Remaining Amount" THEN BEGIN
            RangeAddFeeAmount := 0;

            IF "Additional Fee Amount" > 0 THEN
              RangeAddFeeAmount := "Additional Fee Amount";

            IF "Additional Fee %" > 0 THEN
              RangeAddFeeAmount += ((RemainingAmount - "Threshold Remaining Amount") * "Additional Fee %") / 100;

            IF "Max. Additional Fee Amount" > 0 THEN
              IF RangeAddFeeAmount > "Max. Additional Fee Amount" THEN
                RangeAddFeeAmount := "Max. Additional Fee Amount";

            IF RangeAddFeeAmount < "Min. Additional Fee Amount" THEN
              RangeAddFeeAmount := "Min. Additional Fee Amount";

            RemainingAmount := "Threshold Remaining Amount";
            AdditionalFee += RangeAddFeeAmount;
          END;
        UNTIL NEXT = 0;
      END;
      EXIT(AdditionalFee);
    END;

    [External]
    PROCEDURE GetAdditionalFeeFromSetup@5(ReminderLevel@1001 : Record 293;RemAmount@1002 : Decimal;CurrencyCode@1003 : Code[10];ChargePerLine@1004 : Boolean;AddFeeCalcType@1000 : Option;PostingDate@1008 : Date) : Decimal;
    VAR
      AdditionalFeeSetup@1010 : Record 1050;
      CurrExchRate@1007 : Record 330;
      FeeAmountInLCY@1006 : Decimal;
      RemAmountLCY@1009 : Decimal;
    BEGIN
      WITH AdditionalFeeSetup DO BEGIN
        ASCENDING(FALSE);
        SETRANGE("Charge Per Line",ChargePerLine);
        SETRANGE("Reminder Terms Code",ReminderLevel."Reminder Terms Code");
        SETRANGE("Reminder Level No.",ReminderLevel."No.");
        SETRANGE("Currency Code",CurrencyCode);
        IF FINDFIRST THEN BEGIN
          IF AddFeeCalcType = ReminderLevel."Add. Fee Calculation Type"::"Single Dynamic" THEN
            EXIT(CalculateAddFeeSingleDynamic(AdditionalFeeSetup,RemAmount));

          IF AddFeeCalcType = ReminderLevel."Add. Fee Calculation Type"::"Accumulated Dynamic" THEN
            EXIT(CalculateAddFeeAccumulatedDynamic(AdditionalFeeSetup,RemAmount));
        END ELSE
          IF CurrencyCode <> '' THEN BEGIN
            SETRANGE("Currency Code",'');
            IF FINDFIRST THEN BEGIN
              RemAmountLCY :=
                CurrExchRate.ExchangeAmtFCYToLCY(
      //          PostingDate,CurrencyCode,RemAmount,CurrExchRate.ExchangeRate(PostingDate,CurrencyCode)); //**4PS.o
                  0,'',PostingDate,CurrencyCode,RemAmount,CurrExchRate.ExchangeRate(0,'',PostingDate,CurrencyCode,TRUE),TRUE); //**4PS.n
              IF AddFeeCalcType = ReminderLevel."Add. Fee Calculation Type"::"Single Dynamic" THEN
                FeeAmountInLCY := CalculateAddFeeSingleDynamic(AdditionalFeeSetup,RemAmountLCY)
              ELSE
                IF AddFeeCalcType = ReminderLevel."Add. Fee Calculation Type"::"Accumulated Dynamic" THEN
                  FeeAmountInLCY := CalculateAddFeeAccumulatedDynamic(AdditionalFeeSetup,RemAmountLCY);
              EXIT(CurrExchRate.ExchangeAmtLCYToFCY(
                  0,'', //**4PS.n
                  PostingDate,CurrencyCode,
                  FeeAmountInLCY,
      //          CurrExchRate.ExchangeRate(PostingDate,CurrencyCode))); //**4PS.o
                  CurrExchRate.ExchangeRate(0,'',PostingDate,CurrencyCode,TRUE),TRUE)); //**4PS.n
            END;
            EXIT(0);
          END;
        EXIT(0);
      END;
    END;

    BEGIN
    END.
  }
}

