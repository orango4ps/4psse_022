OBJECT Page 11132121 Split Proj. Planning Activity
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Projektplanungsaktivit„t teilen;
               ENU=Split Proj. Planning Activity;
               NLD=Splits Projectplanningactivteit];
    SourceTable=Table11012001;
    PageType=NavigatePage;
    ActionList=ACTIONS
    {
      { 1100528809;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100528810;1 ;Action    ;
                      CaptionML=ENU=&Splitsen;
                      InFooterBar=Yes;
                      Image=SplitLines;
                      OnAction=BEGIN
                                 IF SplitQuantity < 2 THEN
                                   ERROR(Text000);
                                 SplitProjectPlanningActivity;
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100528800;0;Container;
                ContainerType=ContentArea }

    { 1100528801;1;Group  ;
                CaptionML=[DEU=Budgetzeile;
                           ENU=Budget Line;
                           NLD=Werkbegrotingsregel;
                           NOR=Budsjettrad;
                           SVE=Budgetrad];
                GroupType=Group }

    { 1100528802;2;Field  ;
                SourceExpr="Project No.";
                Editable=FALSE }

    { 1100528805;2;Field  ;
                SourceExpr="Project Description" }

    { 1100528807;2;Field  ;
                SourceExpr=Quantity;
                Editable=FALSE }

    { 1100528803;2;Field  ;
                SourceExpr=Description;
                Editable=FALSE }

    { 1100528804;2;Field  ;
                SourceExpr="Description 2";
                Editable=FALSE }

    { 1100528806;2;Field  ;
                SourceExpr="Project Planning Activity No.";
                Editable=FALSE;
                Style=Strong;
                StyleExpr=TRUE }

    { 1100528813;1;Group  ;
                CaptionML=[DEU=Optionen;
                           ENU=Options;
                           NLD=Opties;
                           NOR=Alternativer;
                           SVE=Alternativ];
                GroupType=Group }

    { 1100528808;2;Field  ;
                CaptionML=[DEU=Zeile(n) in (Menge) teilen;
                           ENU=Split Line(s) into (Quantity);
                           NLD=Splits regel(s) in (aantal)];
                SourceExpr=SplitQuantity;
                OnValidate=BEGIN
                             IF SplitQuantity < 2 THEN
                               ERROR(Text000);
                           END;
                            }

    { 1100528811;2;Field  ;
                CaptionML=[DEU=Alle Zeilen fr dieselbe Projektplanungsaktivit„tsnr. teilen;
                           ENU=Split All Lines for  same Project Planning Activity No.;
                           NLD=Splits alle regels met zelfde projectplanningsactiviteitnr.];
                SourceExpr=SplitAll;
                OnValidate=BEGIN
                             IF SplitAll THEN
                               UseExistingNo := TRUE;
                           END;
                            }

    { 1100528812;2;Field  ;
                CaptionML=[DEU=Vorhandene Projektplanungsaktivit„tsnummern (falls vorhanden) verwenden.;
                           ENU=Use Existing Project Planning Activity Numbers (If Present).;
                           NLD=Gebruik bestaande projectplanningactiviteitnr's (indien aanwezig).];
                SourceExpr=UseExistingNo }

  }
  CODE
  {
    VAR
      SplitQuantity@1100528800 : Integer;
      SplitAll@1100528801 : Boolean;
      Text000@1100528802 : TextConst 'DEU=Der Wert muss gr”áer als 1 sein.;ENU=Value must be greater then 1.;NLD=Waarde moet groter dan 1 zijn.';
      Text001@1100528803 : TextConst 'DEU=Es kann keine neue Projektplanungsaktivit„tsnummer erstellt werden. Die vorhandene Nummer ist zu lang.;ENU=Unable to Create New Project Planning Activity Number. Existing Number is too Long.;NLD=Aanmaken nieuw projectplanningactiviteitnr. niet mogelijk. Bestaand nummer is te lang.';
      UseExistingNo@1100528804 : Boolean;
      PreviousLineNo@1100528805 : Integer;

    LOCAL PROCEDURE SplitProjectPlanningActivity@1100528801();
    VAR
      BudgetLine@1100528801 : Record 11012001;
    BEGIN
      IF SplitAll THEN BEGIN
        BudgetLine.SETRANGE("Project No.", "Project No.");
        BudgetLine.SETRANGE(Adjustment, Adjustment);
        BudgetLine.SETRANGE("Extension Contract", "Extension Contract");
        BudgetLine.SETRANGE(Option, Option);
        BudgetLine.SETRANGE("Project Planning Activity No.", "Project Planning Activity No.");
        IF BudgetLine.FINDSET THEN
          REPEAT
            SplitLine(BudgetLine);
          UNTIL BudgetLine.NEXT = 0;
      END ELSE
        SplitLine(Rec);
    END;

    PROCEDURE SplitLine@1100528800(BudgetLine@1100528800 : Record 11012001);
    VAR
      BudgetLine2@1100528802 : Record 11012001;
      NewQuantity@1100528801 : Decimal;
      I@1100528803 : Integer;
      StepSizeLineNo@1100528805 : Integer;
      OriginalProjPlanningActivityNo@1100528806 : Code[30];
    BEGIN
      I := 1;
      StepSizeLineNo := GetLineNoStepSize(BudgetLine, SplitQuantity);
      PreviousLineNo := BudgetLine."Line No.";
      NewQuantity := 0;
      CalcSplitValues(BudgetLine, NewQuantity);
      OriginalProjPlanningActivityNo := BudgetLine."Project Planning Activity No.";

      WHILE I < SplitQuantity+1 DO BEGIN
        IF I = 1 THEN BEGIN
          BudgetLine.VALIDATE(Quantity, NewQuantity);
          BudgetLine."Project Planning Activity No." := GetCreateNewProjPlanningActivityNoByExisting(OriginalProjPlanningActivityNo, I);
          BudgetLine.MODIFY(TRUE);
        END ELSE BEGIN
          BudgetLine2.INIT;
          BudgetLine2.COPY(BudgetLine);
          BudgetLine2."Line No." := PreviousLineNo + StepSizeLineNo;
          BudgetLine2.INSERT;
          BudgetLine2.VALIDATE(Quantity, NewQuantity);
          BudgetLine2."Project Planning Activity No." := GetCreateNewProjPlanningActivityNoByExisting(OriginalProjPlanningActivityNo, I);
          BudgetLine2.MODIFY(TRUE);
          PreviousLineNo := BudgetLine2."Line No.";
        END;
          COMMIT;
          I += 1;
      END;
    END;

    PROCEDURE CalcSplitValues@1100528805(BugetLine@1100528800 : Record 11012001;VAR NewQuantity@1100528801 : Decimal);
    BEGIN
      NewQuantity := ROUND((BugetLine.Quantity / SplitQuantity), 0.01, '=');
    END;

    LOCAL PROCEDURE GetCreateNewProjPlanningActivityNoByExisting@1100528815(iProjPlanningActivityNo@1100528800 : Code[30];I@1100528806 : Integer) : Code[30];
    VAR
      ProjectPlanningActivity@1100528803 : Record 11012431;
      ProjectPlanningActivity2@1100528807 : Record 11012431;
      ProjectPlanningActivity3@1100528808 : Record 11012431;
      No@1100528801 : Integer;
      NewProjPlanningActivityNo@1100528802 : Code[30];
      NoExists@1100528804 : Boolean;
    BEGIN
      IF STRLEN(iProjPlanningActivityNo) > 27 THEN
        ERROR(Text001);

      IF UseExistingNo THEN
        No := I
      ELSE
        No := 1;

      REPEAT
        NewProjPlanningActivityNo := iProjPlanningActivityNo + '-';

        IF No IN [0..9] THEN
         NewProjPlanningActivityNo := NewProjPlanningActivityNo + '0';

        NewProjPlanningActivityNo := NewProjPlanningActivityNo + FORMAT(No);

        ProjectPlanningActivity.RESET;
        ProjectPlanningActivity.SETRANGE("No.", NewProjPlanningActivityNo);

        IF ProjectPlanningActivity.FINDFIRST THEN BEGIN
          NoExists := TRUE;
          IF UseExistingNo THEN
            EXIT(NewProjPlanningActivityNo)
          ELSE
            No += 1;
        END ELSE
         NoExists := FALSE;

      UNTIL NOT NoExists;

      IF ProjectPlanningActivity2.GET(iProjPlanningActivityNo) THEN BEGIN
        ProjectPlanningActivity3.INIT;
        ProjectPlanningActivity3.COPY(ProjectPlanningActivity2);
        ProjectPlanningActivity3."No." := NewProjPlanningActivityNo;
        ProjectPlanningActivity3.INSERT;
        EXIT(ProjectPlanningActivity3."No.");
      END;
    END;

    BEGIN
    END.
  }
}

