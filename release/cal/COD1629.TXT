OBJECT Codeunit 1629 Office Attachment Manager
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    SingleInstance=Yes;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      UrlOrContentString@1002 : Text;
      NameString@1001 : Text;
      Body@1003 : Text;
      Count@1000 : Integer;

    [External]
    PROCEDURE Add@2(FileUrlOrContent@1000 : Text;FileName@1001 : Text;BodyText@1002 : Text);
    BEGIN
      IF UrlOrContentString <> '' THEN BEGIN
        UrlOrContentString += '|';
        NameString += '|';
      END;

      UrlOrContentString += FileUrlOrContent;
      NameString += FileName;
      IF Body = '' THEN
        Body := BodyText;
      Count -= 1;
    END;

    [External]
    PROCEDURE Ready@3() : Boolean;
    BEGIN
      EXIT(Count < 1);
    END;

    [External]
    PROCEDURE Done@5();
    BEGIN
      Count := 0;
      UrlOrContentString := '';
      NameString := '';
      Body := '';
    END;

    [External]
    PROCEDURE GetFiles@8() : Text;
    BEGIN
      EXIT(UrlOrContentString);
    END;

    [External]
    PROCEDURE GetNames@9() : Text;
    BEGIN
      EXIT(NameString);
    END;

    [Internal]
    PROCEDURE GetBody@6() : Text;
    VAR
      MailMgt@1000 : Codeunit 9520;
    BEGIN
      EXIT(MailMgt.ImageBase64ToUrl(Body));
    END;

    [External]
    PROCEDURE IncrementCount@4(NewCount@1000 : Integer);
    BEGIN
      Count += NewCount;
    END;

    [EventSubscriber(Codeunit,80,OnSendSalesDocument)]
    LOCAL PROCEDURE OnSendSalesDocument@1(ShipAndInvoice@1000 : Boolean);
    BEGIN
      IF ShipAndInvoice THEN
        Count := 2;
    END;

    BEGIN
    END.
  }
}

