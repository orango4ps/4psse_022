OBJECT Report 11128041 Copy Proj. Cost Obj Sls Price
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PSSE;
  }
  PROPERTIES
  {
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   CostObjectCode := 'COSTOBJECT';
                 END;

  }
  DATASET
  {
    { 8019;    ;DataItem;                    ;
               DataItemTable=Table11072003;
               OnPreDataItem=BEGIN
                               NoOfUpdates := 0;

                               IF NewStartingDate = 0D THEN
                                 ERROR(Text003);

                               ToProjFilter := GETFILTER("No.");
                               IF (FromProjNo = '') AND (ToProjFilter = '') THEN
                                 ERROR(Text002);

                               IF FromProjNo = '' THEN
                                 UseBasePriceTable := TRUE
                               ELSE
                                 UseBasePriceTable := FALSE;

                               IF ToProjFilter = '' THEN
                                 SETRANGE("No.",FromProjNo);

                               CostObjectBuffer.DELETEALL;

                               IF UseBasePriceTable THEN BEGIN

                                 //////////////////////////////////////////////
                                 // From Base table
                                 IF CostObjectSalesPrice.FINDSET THEN REPEAT
                                   IF NOT CostObjectBuffer.GET(CostObjectCode,CostObjectSalesPrice."Cost Object") THEN BEGIN
                                     CostObjectBuffer.INIT;
                                     CostObjectBuffer."Dimension Code" := CostObjectCode;
                                     CostObjectBuffer.Code := CostObjectSalesPrice."Cost Object";
                                     CostObjectBuffer.INSERT;
                                   END;
                                 UNTIL CostObjectSalesPrice.NEXT = 0;


                               END ELSE BEGIN

                                 //////////////////////////////////////////////
                                 // From within same table
                                 IF From_ProjCostObjectSalesPrice.FINDSET THEN REPEAT

                                   IF NOT CostObjectBuffer.GET(CostObjectCode,From_ProjCostObjectSalesPrice."Cost Object") THEN BEGIN
                                     CostObjectBuffer.INIT;
                                     CostObjectBuffer."Dimension Code" := CostObjectCode;
                                     CostObjectBuffer.Code := From_ProjCostObjectSalesPrice."Cost Object";
                                     CostObjectBuffer.INSERT;
                                   END;
                                 UNTIL From_ProjCostObjectSalesPrice.NEXT = 0;

                               END;

                               ProjCostObjectSalesPriceBuffer.DELETEALL;
                             END;

               OnAfterGetRecord=BEGIN
                                  NewerLinesExists := FALSE;

                                  IF UseBasePriceTable THEN BEGIN

                                    //////////////////////////////////////////////
                                    // From Base price table
                                    CostObjectSalesPrice.RESET;
                                    CostObjectBuffer.RESET;

                                    IF CostObjectBuffer.FINDSET THEN REPEAT

                                      // Check if the combination of Cost Object and Project already exists with a later date
                                      To_ProjCostObjectSalesPrice.SETRANGE("Project No.","No.");
                                      To_ProjCostObjectSalesPrice.SETRANGE("Cost Object",CostObjectBuffer.Code);
                                      To_ProjCostObjectSalesPrice.SETFILTER("Starting Date",'>%1', NewStartingDate);
                                      IF To_ProjCostObjectSalesPrice.FINDFIRST THEN
                                        NewerLinesExists := TRUE;

                                      // Get the latest price
                                      CostObjectSalesPrice.SETRANGE("Cost Object",CostObjectBuffer.Code);
                                      IF CostObjectSalesPrice.FINDLAST THEN BEGIN

                                        // Insert new line with new date and price in the buffer
                                        ProjCostObjectSalesPriceBuffer.INIT;
                                        ProjCostObjectSalesPriceBuffer.VALIDATE("Project No.","No.");
                                        ProjCostObjectSalesPriceBuffer.VALIDATE("Cost Object",CostObjectBuffer.Code);
                                        ProjCostObjectSalesPriceBuffer.VALIDATE("Starting Date",NewStartingDate);

                                        CASE UpdateOption OF
                                        UpdateOption::"Fixed Amount":
                                          NewAmount := CostObjectSalesPrice."Sales Price" + NewValue;
                                        UpdateOption::Percent:
                                          NewAmount := CostObjectSalesPrice."Sales Price" * ((100 + NewValue) / 100);
                                        END;

                                        IF RoundAmount <> 0 THEN
                                          NewAmount := ROUND(NewAmount, RoundAmount)
                                        ELSE
                                          NewAmount := ROUND(NewAmount, 0.01);

                                        IF NewAmount < 0 THEN
                                          IF NOT CONFIRM(STRSUBSTNO(Text001,
                                            NewAmount,
                                            ProjCostObjectSalesPriceBuffer."Project No.",
                                            ProjCostObjectSalesPriceBuffer."Cost Object")) THEN
                                            ERROR(Text007);

                                        ProjCostObjectSalesPriceBuffer.VALIDATE("Sales Price",NewAmount);
                                        ProjCostObjectSalesPriceBuffer.INSERT;

                                      END;

                                    UNTIL CostObjectBuffer.NEXT = 0;

                                  END ELSE BEGIN

                                    //////////////////////////////////////////////
                                    // From within the same table
                                    From_ProjCostObjectSalesPrice.RESET;
                                    CostObjectBuffer.RESET;

                                    IF CostObjectBuffer.FINDSET THEN REPEAT

                                      // Check if the combination of Cost Object and Project already exists with a later date
                                      To_ProjCostObjectSalesPrice.SETRANGE("Project No.","No.");
                                      To_ProjCostObjectSalesPrice.SETRANGE("Cost Object",CostObjectBuffer.Code);
                                      To_ProjCostObjectSalesPrice.SETFILTER("Starting Date",'>%1', NewStartingDate);
                                      IF To_ProjCostObjectSalesPrice.FINDFIRST THEN
                                        NewerLinesExists := TRUE;

                                      // Get the latest price
                                      From_ProjCostObjectSalesPrice.SETRANGE("Project No.",FromProjNo);
                                      From_ProjCostObjectSalesPrice.SETRANGE("Cost Object",CostObjectBuffer.Code);
                                      IF From_ProjCostObjectSalesPrice.FINDLAST THEN BEGIN

                                        // Insert new line with new date and price in the buffer
                                        // The buffer is used so that no lines are inserting to the table in the loop
                                        // and interrupting the last find price
                                        ProjCostObjectSalesPriceBuffer.INIT;
                                        ProjCostObjectSalesPriceBuffer.VALIDATE("Project No.","No.");
                                        ProjCostObjectSalesPriceBuffer.VALIDATE("Cost Object",CostObjectBuffer.Code);
                                        ProjCostObjectSalesPriceBuffer.VALIDATE("Starting Date",NewStartingDate);

                                        CASE UpdateOption OF
                                        UpdateOption::"Fixed Amount":
                                          NewAmount := From_ProjCostObjectSalesPrice."Sales Price" + NewValue;
                                        UpdateOption::Percent:
                                          NewAmount := From_ProjCostObjectSalesPrice."Sales Price" * ((100 + NewValue) / 100);
                                        END;

                                        IF RoundAmount <> 0 THEN
                                          NewAmount := ROUND(NewAmount, RoundAmount)
                                        ELSE
                                          NewAmount := ROUND(NewAmount, 0.01);

                                        IF NewAmount < 0 THEN
                                          IF NOT CONFIRM(STRSUBSTNO(Text001,
                                            NewAmount,
                                            ProjCostObjectSalesPriceBuffer."Project No.",
                                            ProjCostObjectSalesPriceBuffer."Cost Object")) THEN
                                            ERROR(Text007);

                                        ProjCostObjectSalesPriceBuffer.VALIDATE("Sales Price",NewAmount);
                                        ProjCostObjectSalesPriceBuffer.INSERT;

                                      END;

                                    UNTIL CostObjectBuffer.NEXT = 0;

                                  END;

                                  // Check if newer lines were found
                                  IF NewerLinesExists THEN
                                    IF NOT CONFIRM(STRSUBSTNO(Text008,NewStartingDate)) THEN
                                      ERROR(Text007);
                                END;

               OnPostDataItem=BEGIN
                                // Update the real table from the buffer
                                ProjCostObjectSalesPriceBuffer.RESET;
                                IF ProjCostObjectSalesPriceBuffer.FINDSET THEN REPEAT
                                  To_ProjCostObjectSalesPrice.SETRANGE("Project No.",ProjCostObjectSalesPriceBuffer."Project No.");
                                  To_ProjCostObjectSalesPrice.SETRANGE("Cost Object",ProjCostObjectSalesPriceBuffer."Cost Object");
                                  To_ProjCostObjectSalesPrice.SETRANGE("Starting Date",ProjCostObjectSalesPriceBuffer."Starting Date");
                                  IF NOT To_ProjCostObjectSalesPrice.FINDFIRST THEN BEGIN

                                    To_ProjCostObjectSalesPrice.VALIDATE("Project No.",ProjCostObjectSalesPriceBuffer."Project No.");
                                    To_ProjCostObjectSalesPrice.VALIDATE("Cost Object",ProjCostObjectSalesPriceBuffer."Cost Object");
                                    To_ProjCostObjectSalesPrice.VALIDATE("Starting Date",ProjCostObjectSalesPriceBuffer."Starting Date");
                                    To_ProjCostObjectSalesPrice.VALIDATE("Sales Price",ProjCostObjectSalesPriceBuffer."Sales Price");
                                    To_ProjCostObjectSalesPrice.INSERT(TRUE);
                                    NoOfUpdates += 1;
                                  END;

                                UNTIL ProjCostObjectSalesPriceBuffer.NEXT = 0;

                                MESSAGE(Text006,NoOfUpdates);
                              END;

               ReqFilterFields=No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100285005;;Container;
                  CaptionML=[ENU=Options;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  ContainerType=ContentArea }

      { 1100285004;1;Field  ;
                  CaptionML=[ENU=Update Option;
                             NOR=Oppdater Alternativ;
                             SVE=Uppdateringsalternativ];
                  OptionCaptionML=[ENU=Fixed Amount,Percent;
                                   NOR=Fast bel›p,Prosent;
                                   SVE=Fast belopp,Procent];
                  SourceExpr=UpdateOption }

      { 1100285003;1;Field  ;
                  CaptionML=[ENU=From Proj. No.;
                             NOR=Fra prosjektnr.;
                             SVE=Fr†nprojektnr];
                  SourceExpr=FromProjNo;
                  TableRelation=Job;
                  OnValidate=BEGIN
                               IF FromProjNo <> '' THEN BEGIN
                                 From_ProjCostObjectSalesPrice.SETRANGE("Project No.",FromProjNo);
                                 IF NOT From_ProjCostObjectSalesPrice.FINDLAST THEN
                                   ERROR(STRSUBSTNO(Text005,FromProjNo));
                               END;
                             END;
                              }

      { 1100285002;1;Field  ;
                  CaptionML=[ENU=Update w. Amount/Percentage;
                             NOR=Oppdater med bel›p/Prosent;
                             SVE=Uppdatera med belopp/procent];
                  SourceExpr=NewValue }

      { 1100285001;1;Field  ;
                  CaptionML=[ENU=Round Amount;
                             NOR=Avrundet bel›p;
                             SVE=Avrundat belopp];
                  SourceExpr=RoundAmount }

      { 1100285000;1;Field  ;
                  CaptionML=[ENU=New Starting Date;
                             NOR=Ny Startdato;
                             SVE=Nytt startdatum];
                  SourceExpr=NewStartingDate }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      From_ProjCostObjectSalesPrice@1100285013 : Record 11020383;
      ProjCostObjectSalesPriceBuffer@1100285015 : TEMPORARY Record 11020383;
      To_ProjCostObjectSalesPrice@1100285000 : Record 11020383;
      CostObjectSalesPrice@1100285012 : Record 11020351;
      CostObjectBuffer@1100285011 : TEMPORARY Record 349;
      ToProjFilter@1100285010 : Text[250];
      FromProjNo@1100285009 : Code[20];
      CostObjectCode@1100285008 : Code[20];
      UpdateOption@1100285007 : 'Fixed Amount,Percent';
      NoOfUpdates@1100285006 : Integer;
      NewAmount@1100285005 : Decimal;
      NewValue@1100285004 : Decimal;
      RoundAmount@1100285003 : Decimal;
      NewStartingDate@1100285002 : Date;
      UseBasePriceTable@1100285001 : Boolean;
      Text001@1100285020 : TextConst 'ENU=There will be a negative amount (%1) for Project No. %2, Cost Object %3. Would you like to proceed anyway?;NOR=Bel›pet kommer til † bli negativt (%1) for prosjektnr. %2, Kostobjekt %3. Vil du fortsette likevel?;SVE=Beloppet kommer att bli negativt (%1) f”r projektnr %2, kostnadsobjekt %3. Vill du „nd† forts„tta?';
      Text002@1100285019 : TextConst 'ENU=From Project No. cannot be blank if To Project No. is blank.;NOR=Fra prosjektnr. kan ikke v‘re blankt hivs Til prosjektnr. er blankt.;SVE=Fr†nprojektnr f†r inte l„mnas blankt om Tillprojektnr inte anges.';
      Text003@1100285018 : TextConst 'ENU=New Starting Date must not be blank.;NOR=Ny Startdato kan ikke v‘re blank.;SVE=Nytt startdatum f†r inte vara blankt.';
      Text005@1100285017 : TextConst 'ENU=There is no information for Project No. %1 in the table.;NOR=Det finnes ingen informasjon i tabellen for prosjektnr. %1.;SVE=Det finns ingen information i tabellen f”r projektnr %1.';
      Text006@1100285016 : TextConst 'ENU=%1 new line(s) were inserted.;NOR=%1 ny(e) rad(er) ble satt inn.;SVE=%1 ny(a) rad(er) skapades.';
      Text007@1100285014 : TextConst 'ENU=The operation was aborted.;NOR=Operasjonen ble avbrutt.;SVE=Operationen avbr”ts.';
      NewerLinesExists@1100285021 : Boolean;
      Text008@1100285022 : TextConst 'ENU=There are lines on a later date than %1. Do you want to continue?;NOR=Det finnes rader p† en senere dato enn %1. Vil du fortsette?;SVE="Det finns rader med senare datum „n %1. Vill du forts„tta? "';

    BEGIN
    {
      4PSSE
      130909 ITERO.LP New ProcessingOnly report to copy data.
      130911 ITERO.DJ added Check if the combination of Cost Object and Project already exists with a later date
    }
    END.
  }
  RDLDATA
  {
  }
}

