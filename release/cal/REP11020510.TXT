OBJECT Report 11020510 Correct Transport Order Date
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Korrigieren Transportdatum;
               ENU=Correct Transport Date;
               NLD=Corrigeren transportdatum;
               NOR=Rette transportdato;
               SVE=R„tta transportdatum];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  gPostedTransOrderRec.GET(gPostedTransOrderRec."No.");
                  CheckMainTransportOrder();
                  CheckChangeOrderDateAllowed();
                  IF NewTransportDate = 0D THEN
                    ERROR(Text002);

                  IF NOT CONFIRM(Text000, FALSE, gPostedTransOrderRec."Transport Date", NewTransportDate) THEN
                    ERROR('');
                END;

  }
  DATASET
  {
    { 8514;    ;DataItem;                    ;
               DataItemTable=Table11020507;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=BEGIN
                               SETRANGE("No.", gPostedTransOrderRec."No.");
                             END;

               OnAfterGetRecord=BEGIN
                                  IF NewTransportDate <> "Transport Date" THEN BEGIN
                                    "Transport Date" := NewTransportDate;
                                    MODIFY;
                                  END;
                                END;
                                 }

    { 3168;1   ;DataItem;                    ;
               DataItemTable=Table11020515;
               DataItemTableView=SORTING(Transport Order No.,Linked Transport Order No.);
               OnAfterGetRecord=VAR
                                  lvPostedTransOrderRec@1100525000 : Record 11020507;
                                BEGIN
                                  IF lvPostedTransOrderRec.GET("Posted Linked Pl Trans. Orders"."Linked Transport Order No.") THEN BEGIN
                                    IF NewTransportDate <> lvPostedTransOrderRec."Transport Date" THEN BEGIN
                                      lvPostedTransOrderRec."Transport Date" := NewTransportDate;
                                      lvPostedTransOrderRec.MODIFY;
                                    END;
                                  END;
                                END;

               DataItemLink=Transport Order No.=FIELD(No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1100525004;2;Field  ;
                  CaptionML=[DEU=Transportauftragsnr.;
                             ENU=Transport Order No.;
                             NLD=Transportordernr.;
                             NOR=Transportordrenr.;
                             SVE=Transportordernr];
                  SourceExpr=gPostedTransOrderRec."No.";
                  Editable=FALSE }

      { 1100525000;2;Field  ;
                  CaptionML=[DEU=Transportdatum;
                             ENU=Transport Date;
                             NLD=Transportdatum;
                             NOR=Transportdato;
                             SVE=Transportdatum];
                  SourceExpr=gPostedTransOrderRec."Transport Date";
                  Editable=FALSE }

      { 1100525001;2;Field  ;
                  CaptionML=[DEU=Neues Transportdatum;
                             ENU=New Transport Date;
                             NLD=Nieuwe transportdatum;
                             NOR=Ny Transportdato;
                             SVE=Nytt transportdatum];
                  SourceExpr=NewTransportDate }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      gPostedTransOrderRec@1100525000 : Record 11020507;
      NewTransportDate@1100525001 : Date;
      Text000@1100525003 : TextConst 'DEU=M”chten Sie das Transportdatum ''%1'' in ''%2'' „ndern?;ENU=Do you want to modify the Transport Date ''%1'' in ''%2''?;NLD=Wilt de transportdatum ''%1'' wijzigen in ''%2''?;NOR=Vil du endre Transportdatoen ''%1'' i ''%2''?;SVE=Vill du „ndra transportdatumet ''%1'' i ''%2''?';
      Text001@1100525002 : TextConst 'DEU=Transportauftrag ''%1'' ist ein verbundener Auftrag, das Datum muss beim Haupttransportauftrag ''%2'' ge„ndert werden.;ENU=Transport Order ''%1'' is a linked order, date must be modified on the Main Transport Order ''%2''.;NLD=Transportorder ''%1'' is een gekoppelde order, datum moet bij de hoofdtransportorder ''%2'' gewijzigd worden.;NOR=Transportordre ''%1'' er en linket ordre, Dato m† endres i Hovedtransportordren ''%2''.;SVE=Transportorder ''%1'' „r en l„nkad order, datumet m†ste „ndras i huvudtransportordern ''%2''.';
      Text002@1100525004 : TextConst 'DEU=Geben Sie ein neues Datum ein.;ENU=Select a new date.;NLD=Vul een nieuwe datum in.;NOR=Velg en ny dato.;SVE=V„lj ett nytt datum.';

    PROCEDURE SetTransportOrder@1100525000(iTransOrderNo@1100525000 : Code[20]);
    BEGIN
      gPostedTransOrderRec.GET(iTransOrderNo);
      CheckMainTransportOrder();
      CheckChangeOrderDateAllowed();
    END;

    PROCEDURE CheckMainTransportOrder@1100525001();
    BEGIN
      gPostedTransOrderRec.CALCFIELDS("Invoice with Transport Order");
      IF gPostedTransOrderRec."Invoice with Transport Order" <> '' THEN
        ERROR(Text001, gPostedTransOrderRec."No.", gPostedTransOrderRec."Invoice with Transport Order");
    END;

    PROCEDURE CheckChangeOrderDateAllowed@1100525003();
    BEGIN
      //* Only allowed when not yet invoiced (Transport Costs: From/To Loc, Project, Internal).
      //* Note: The fields '... Invoiced' are also set to 'True' when there are no lines, so also check
      //* check if the 'Invoice No.' is filled (only filled when lines present and invoiced).

      IF (gPostedTransOrderRec."From Location Invoiced" AND (gPostedTransOrderRec."From Location Invoice No." <> '')) THEN
        gPostedTransOrderRec.TESTFIELD("From Location Invoiced", FALSE);

      IF (gPostedTransOrderRec."To Location Invoiced" AND (gPostedTransOrderRec."To Location Invoice No." <> '')) THEN
        gPostedTransOrderRec.TESTFIELD("To Location Invoiced", FALSE);

      IF (gPostedTransOrderRec."Project Invoiced"  AND (gPostedTransOrderRec."Project Invoice No." <> '')) THEN
        gPostedTransOrderRec.TESTFIELD("Project Invoiced", FALSE);

      IF (gPostedTransOrderRec."Internal Invoiced"  AND (gPostedTransOrderRec."Internal Invoice No." <> '')) THEN
        gPostedTransOrderRec.TESTFIELD("Internal Invoiced", FALSE);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

