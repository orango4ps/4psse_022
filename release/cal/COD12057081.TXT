OBJECT Codeunit 12057081 PDF Preveiw Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF502000;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      CloseDoc@1100285000 : TextConst 'ENU=!CLOSEDOC!;SVE=!CLOSEDOC!';

    PROCEDURE SetPDFFromExPurchDoc@1100285001(VAR ExPurchDoc@1100285000 : Record 12013587;VAR EncodedPDF@1100285003 : Text);
    VAR
      ExFlowBlob@1100285001 : Record 12013591;
      TempBlob@1100285002 : Record 99008535;
    BEGIN
      CLEAR(TempBlob);
      IF ExPurchDoc."Image Buffer ID" <> 0 THEN
        IF ExFlowBlob.GET(ExPurchDoc."Image Buffer ID") THEN
          IF ExtensionSupported(ExFlowBlob."File Extension") THEN BEGIN
            ExFlowBlob.CALCFIELDS(BLOB);
            TempBlob.Blob := ExFlowBlob.BLOB;
          END;

      ConvertPDF(TempBlob,EncodedPDF);
    END;

    PROCEDURE SetPDFFromExDoc@1100285000(VAR ExDoc@1100285000 : Record 12013608;VAR EncodedPDF@1100285003 : Text);
    VAR
      TempBlob@1100285002 : Record 99008535;
      ExDoc2@1100285004 : Record 12013612;
    BEGIN
      CLEAR(TempBlob);
      IF ExDoc2.RetrieveRecord(ExDoc2,ExDoc."Document Type",ExDoc."Document No.") THEN
        GetBlobFromExDoc2(ExDoc2,TempBlob);

      ConvertPDF(TempBlob,EncodedPDF);
    END;

    PROCEDURE SetPDFFromPostedExDoc@1100285005(VAR ExPostedDoc@1100285001 : Record 12013626;VAR EncodedPDF@1100285000 : Text);
    VAR
      TempBLOB@1100285002 : Record 99008535;
      ExDoc2@1100285003 : Record 12013612;
    BEGIN
      CLEAR(TempBLOB);
      IF ExDoc2.RetrieveRecord(ExDoc2,ExPostedDoc."Document Type",ExPostedDoc."Document No.") THEN
        GetBlobFromExDoc2(ExDoc2,TempBLOB);

      ConvertPDF(TempBLOB,EncodedPDF);
    END;

    LOCAL PROCEDURE ConvertPDF@1100285002(VAR TempBlob@1100285000 : Record 99008535;VAR EncodedPDF@1100285001 : Text);
    VAR
      TempBlobMgt@1100285002 : Codeunit 12057145;
    BEGIN
      IF TempBlob.Blob.HASVALUE THEN
        EncodedPDF := TempBlobMgt.ToBase64String(TempBlob)
      ELSE
        EncodedPDF := CloseDoc;
    END;

    LOCAL PROCEDURE ExtensionSupported@1100285003(Extension@1100285000 : Text) : Boolean;
    BEGIN
      IF (STRPOS(UPPERCASE(Extension),'PDF') = 0) THEN
        EXIT(FALSE)
      ELSE
        EXIT(TRUE);
    END;

    LOCAL PROCEDURE GetImageFile@1100285004(VAR ExDoc2@1100285000 : Record 12013612;VAR TempBlob@1100285001 : Record 99008535);
    VAR
      ExFlowSetup@1100285002 : Record 12013601;
      FileManagement@1100285003 : Codeunit 419;
      TempFileName@1100285004 : Text;
    BEGIN
      ExFlowSetup.GET;
      IF ExFlowSetup."Path to Used Invoices" = '' THEN
        EXIT;

      TempFileName := ExFlowSetup."Path to Used Invoices" + ExDoc2."Image Name";

      IF FileManagement.ServerFileExists(TempFileName) THEN
        FileManagement.BLOBImportFromServerFile(TempBlob,TempFileName)
      ELSE
        IF FileManagement.ClientFileExists(TempFileName) THEN
          FileManagement.BLOBImportFromServerFile(TempBlob,FileManagement.UploadFileSilent(TempFileName));
    END;

    LOCAL PROCEDURE GetBlobFromExDoc2@1100285006(VAR ExDoc2@1100285001 : Record 12013612;VAR TempBLOB@1100285000 : Record 99008535);
    BEGIN
      IF ExtensionSupported(ExDoc2."Image Name") THEN BEGIN
        IF ExDoc2."Image Blob".HASVALUE THEN BEGIN
          ExDoc2.CALCFIELDS("Image Blob");
          TempBLOB.Blob := ExDoc2."Image Blob"
        END ELSE
          GetImageFile(ExDoc2,TempBLOB);
      END;
    END;

    BEGIN
    END.
  }
}

