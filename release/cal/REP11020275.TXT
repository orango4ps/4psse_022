OBJECT Report 11020275 Copy Base Recipe
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Basisrezept kopieren;
               ENU=Copy Base Recipe;
               NLD=Standaard recept kopi‰ren];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 1100525000;;DataItem;Origin            ;
               DataItemTable=Table11020581;
               DataItemTableView=SORTING(Recipe Group,No.);
               OnPreDataItem=BEGIN
                               CheckSelection;
                             END;

               OnAfterGetRecord=BEGIN
                                  RecipeGrpFrom := "Recipe Group";
                                  RecipeFrom := "No.";
                                  StartUpdate;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100525004;;Container;
                  ContainerType=ContentArea }

      { 1100525003;1;Group  ;
                  GroupType=Group }

      { 1100525008;2;Field  ;
                  CaptionClass=Text998;
                  Style=Strong;
                  StyleExpr=TRUE }

      { 1100525007;2;Field  ;
                  Name=RecipeGrpFrom;
                  CaptionML=[DEU=Rezeptgruppe;
                             ENU=Recipe Group;
                             NLD=Receptgroep;
                             NOR=Maltype;
                             SVE=Malltyp];
                  SourceExpr=RecipeGrpFrom;
                  OnValidate=BEGIN
                               OnValidateRecipeGrpFrom;
                             END;

                  OnLookup=BEGIN
                             OnLookupRecipeGrpFrom;
                           END;
                            }

      { 1100525006;2;Field  ;
                  Name=RecipeFrom;
                  CaptionML=[DEU=Rezept;
                             ENU=Recipe;
                             NLD=Recept;
                             NOR=Mal;
                             SVE=Mall];
                  SourceExpr=RecipeFrom;
                  OnValidate=BEGIN
                               OnValidateRecipeFrom;
                             END;

                  OnLookup=BEGIN
                             OnLookupRecipeFrom;
                           END;
                            }

      { 1100525005;2;Field  ;
                  CaptionClass=Text999;
                  Style=Strong;
                  StyleExpr=TRUE }

      { 1100525002;2;Field  ;
                  Name=RecipeGrpTo;
                  CaptionML=[DEU=Rezeptgruppe;
                             ENU=Recipe Group;
                             NLD=Receptgroep;
                             NOR=Maltype;
                             SVE=Malltyp];
                  SourceExpr=RecipeGrpTo;
                  OnValidate=BEGIN
                               OnValidateRecipeGrpTo;
                             END;

                  OnLookup=BEGIN
                             OnLookupRecipeGrpTo;
                           END;
                            }

      { 1100525001;2;Field  ;
                  Name=RecipeTo;
                  CaptionML=[DEU=Rezept;
                             ENU=Recipe;
                             NLD=Recept;
                             NOR=Mal;
                             SVE=Mall];
                  SourceExpr=RecipeTo;
                  OnLookup=BEGIN
                             OnLookupRecipeTo;
                           END;
                            }

      { 1100525000;2;Field  ;
                  CaptionML=[DEU=Beschreibung Rezept;
                             ENU=Description Recipe;
                             NLD=Omschrijving recept];
                  SourceExpr=RecipeToTxt }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      RecipeGrpFrom@1100525009 : Code[20];
      RecipeGrpTo@1100525008 : Code[20];
      RecipeFrom@1100525007 : Code[20];
      RecipeTo@1100525006 : Code[20];
      RecipeGrpFromTxt@1100525005 : Text[50];
      RecipeGrpToTxt@1100525004 : Text[50];
      RecipeFromTxt@1100525003 : Text[100];
      RecipeToTxt@1100525002 : Text[100];
      RecipeRec@1100525001 : Record 11020581;
      RgrpRec@1100525000 : Record 11020580;
      Text000@1100525012 : TextConst 'DEU=Fehlende šbereinstimmung Ursprung-Bestimmung;ENU=Mismatch selection origin-destination;NLD=Selectie bron-kopie op elkaar afstemmen';
      Text006@1100525011 : TextConst 'DEU=Verarbeitung nur gestattet in Herkunftunternehmen Zentrale Artikelverwaltung;ENU=Transaction only allowed in company used as source for central itemmanagement;NLD=Verwerking alleen toegestaan in bronbedrijf centraal artikelbeheer;NOR=Transaksjonen er kun tillatt for selskap som er i bruk som kilde for sentral artikkelh†ndtering;SVE=Transaktionen „r endast till†ten f”r f”retag som anv„nds som k„lla f”r central artikelhantering';
      Text998@1100525013 : TextConst 'DEU=Kopieren von;ENU=Copy From;NLD=Kopieer van;NOR=Kopier fra;SVE=Kopiera fr†n';
      Text999@1100525010 : TextConst 'DEU=Kopieren in;ENU=Copy To;NLD=Kopieer naar;NOR=Kopier til;SVE=Kopiera till';

    PROCEDURE FillDefaults@1(lEstFrom@11012000 : Code[20];lSubEstFrom@11012001 : Code[20];lPartGrpFrom@11012002 : Code[20];lEstPartFrom@11012003 : Code[20];lLineFrom@11012004 : Integer;lRecipeGrpFrom@11012005 : Code[20];lRecipeFrom@11012006 : Code[20]);
    BEGIN
      RecipeGrpFrom := lRecipeGrpFrom;
      RecipeFrom  := lRecipeFrom;

      RecipeGrpTo := RecipeGrpFrom;
      RecipeTo := RecipeFrom;

      IF RgrpRec.GET(RecipeGrpFrom) THEN BEGIN
        RecipeGrpFromTxt := RgrpRec.Description;
        RecipeGrpToTxt := RgrpRec.Description;
      END;

      IF RecipeRec.GET(RecipeGrpFrom,RecipeFrom) THEN BEGIN
        RecipeFromTxt := RecipeRec.Description;
        RecipeToTxt := RecipeRec.Description;
      END;
    END;

    PROCEDURE StartUpdate@2();
    VAR
      BufferMgt@1100528900 : Codeunit 11012230;
      RcpRec2@1100528901 : Record 11020581;
      CompRel@1100528907 : Record 11012059;
      CentrManagementCU@1100528906 : Codeunit 11012010;
      NavBln@1100528905 : Boolean;
      DiscBln@1100528904 : Boolean;
      DiscGrpBln@1100528903 : Boolean;
      RcpBln@1100528902 : Boolean;
      TextRec@1100525000 : Record 11020595;
      TextRec2@1100525001 : Record 11020595;
      FPSLicenseManagement@1100527400 : Codeunit 11229289;
    BEGIN
      IF FPSLicenseManagement.LicenseAndReadPermissionForTable(CompRel) THEN BEGIN
        CentrManagementCU.ItemFormEditable(NavBln,DiscBln,DiscGrpBln,RcpBln);
        IF RcpBln THEN
          ERROR(Text006);
      END;
      RecipeRec.SETRANGE("Recipe Group",RecipeGrpFrom);
      IF RecipeFrom <> '' THEN
        RecipeRec.SETRANGE("No.",RecipeFrom);
      IF RecipeRec.FINDSET THEN BEGIN
        REPEAT
          RcpRec2.COPY(RecipeRec);
          RcpRec2."Recipe Group" := RecipeGrpTo;
          IF RecipeTo <> '' THEN BEGIN
            RcpRec2."No." := RecipeTo;
            RcpRec2.Description := RecipeToTxt;
          END;
          RcpRec2.Blocked := FALSE;
          RcpRec2.INSERT(TRUE);
          //db.sn, 09-06-15
          TextRec.SETRANGE("Table Name",TextRec."Table Name"::Estimate);
          TextRec.SETRANGE(Group, RecipeRec."Recipe Group");
          TextRec.SETRANGE("No.", RecipeRec."No.");
          IF TextRec.FINDSET THEN BEGIN
            REPEAT
              TextRec2 := TextRec;
              TextRec2.Group := RcpRec2."Recipe Group";
              TextRec2."No." := RcpRec2."No.";
              TextRec2.INSERT;
            UNTIL TextRec.NEXT = 0;
          END;
          //db.en, 09-06-15
        UNTIL RecipeRec.NEXT = 0;
      END ELSE BEGIN
        RcpRec2.INIT;
        RcpRec2."Recipe Group" := RecipeGrpTo;
        RcpRec2."No." := RecipeTo;
        RcpRec2.Description := RecipeToTxt;
        RcpRec2.Blocked := FALSE;
        RcpRec2.INSERT(TRUE);
      END;

      BufferMgt.CopyDetails(
        '', '', '', '', 0, RecipeGrpFrom, RecipeFrom, 0,
        '', '', '', '', 0, RecipeGrpTo, RecipeTo, 0,
        TRUE);
    END;

    PROCEDURE GetDestinination@1100528901(VAR DestGroup@1100528900 : Code[20];VAR DestRecipe@1100528901 : Code[20]);
    BEGIN
      DestGroup := RecipeGrpTo;
      DestRecipe := RecipeTo;
    END;

    PROCEDURE CheckSelection@1100528900();
    BEGIN
      //DP00686.sn
      Origin.SETFILTER("Recipe Group", RecipeGrpFrom);
      Origin.SETFILTER("No.", RecipeFrom);
      //DP00686.en
      IF ((Origin.GETFILTER("Recipe Group") = '') AND (RecipeGrpTo <> '')) OR
         ((Origin.GETFILTER("Recipe Group") <> '') AND (RecipeGrpTo = '')) OR
         ((Origin.GETFILTER("No.") = '') AND (RecipeTo <> '')) THEN
        ERROR(Text000);
    END;

    PROCEDURE "-CopyFrom-"@1100525019();
    BEGIN
    END;

    PROCEDURE OnValidateRecipeGrpFrom@1100525011();
    BEGIN
      RgrpRec.GET(RecipeGrpFrom);
      RecipeGrpFromTxt := RgrpRec.Description;
    END;

    PROCEDURE OnLookupRecipeGrpFrom@1100525009();
    BEGIN
      IF PAGE.RUNMODAL(0, RgrpRec) = ACTION::LookupOK THEN BEGIN
         RecipeGrpFrom := RgrpRec."No.";
         RecipeGrpFromTxt := RgrpRec.Description;
      END;
    END;

    PROCEDURE OnValidateRecipeFrom@1100525018();
    BEGIN
      RecipeRec.GET(RecipeGrpFrom,RecipeFrom);
      RecipeFromTxt := RecipeRec.Description;
      //DP00686.sn
      RecipeTo := RecipeFrom;
      RecipeToTxt := RecipeFromTxt;
      //DP00686.en
    END;

    PROCEDURE OnLookupRecipeFrom@1100525006();
    BEGIN
      RecipeRec.SETRANGE("Recipe Group", RecipeGrpFrom);
      IF PAGE.RUNMODAL(0, RecipeRec) = ACTION::LookupOK THEN BEGIN
         RecipeGrpFrom := RecipeRec."Recipe Group";
         RecipeFrom := RecipeRec."No.";
         OnValidateRecipeFrom;  //DP00686
      END;
    END;

    PROCEDURE "-CopyTo-"@1100525020();
    BEGIN
    END;

    PROCEDURE OnValidateRecipeGrpTo@1100525007();
    BEGIN
      RgrpRec.GET(RecipeGrpTo);
      RecipeGrpToTxt := RgrpRec.Description;
    END;

    PROCEDURE OnLookupRecipeGrpTo@1100525008();
    BEGIN
      IF PAGE.RUNMODAL(0, RgrpRec) = ACTION::LookupOK THEN BEGIN
         RecipeGrpTo := RgrpRec."No.";
         RecipeGrpToTxt := RgrpRec.Description;
      END;
    END;

    PROCEDURE OnLookupRecipeTo@1100525010();
    BEGIN
      RecipeRec.SETRANGE("Recipe Group", RecipeGrpTo);
      PAGE.RUNMODAL(0, RecipeRec);
    END;

    BEGIN
    {
      DP00686: From-Selection editable; overrule tablefilter by DataItemTableView to disable selection/filter DataItem of this report
    }
    END.
  }
  RDLDATA
  {
  }
}

