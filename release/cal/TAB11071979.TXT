OBJECT Table 11071979 Collective Plan Team
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Collective Plan Team;
    LookupPageID=Page11126470;
    DrillDownPageID=Page11126470;
  }
  FIELDS
  {
    { 10  ;   ;Resource No.        ;Code20        ;TableRelation="Resource (WOP)" WHERE (Type=CONST(Employee));
                                                   OnValidate=VAR
                                                                ResourceWOP@1100528601 : Record 11229278;
                                                                ResourceMgtWorkOrderPlan@1100528600 : Codeunit 11229277;
                                                              BEGIN
                                                                ResourceMgtWorkOrderPlan.TestResourceWithinCompanyPlanGroup("Resource No.");
                                                                IF "Resource No." <> '' THEN BEGIN
                                                                  ResourceWOP.GET("Resource No.");
                                                                  IF ResourceWOP."Link WO to WO Leading Resource" THEN
                                                                    "Team Leader" := FALSE;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Resource No.;
                                                              NOR=Ressursnr.;
                                                              SVE=Resursnr];
                                                   NotBlank=Yes }
    { 20  ;   ;Employee Name       ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Resource (WOP)".Name WHERE (No.=FIELD(Resource No.),
                                                                                                   Type=CONST(Employee)));
                                                   CaptionML=[ENU=Employee Name;
                                                              NOR=Navn p† ansatt;
                                                              SVE=Namn p† anst„lld];
                                                   Editable=No }
    { 30  ;   ;Team Leader         ;Boolean       ;CaptionML=[ENU=Team Leader;
                                                              NOR=Teamleder;
                                                              SVE=Teamledare] }
  }
  KEYS
  {
    {    ;Resource No.                            ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE GetResourceNoTeamLeader@1100528600(VAR ITmpCollectivePlanTeam@1100528600 : TEMPORARY Record 11071979) : Code[20];
    VAR
      TmpCollectivePlanTeam@1100528601 : TEMPORARY Record 11071979;
    BEGIN
      TmpCollectivePlanTeam.COPY(ITmpCollectivePlanTeam, TRUE);
      TmpCollectivePlanTeam.SETRANGE("Team Leader", TRUE);
      IF TmpCollectivePlanTeam.FINDFIRST THEN
        EXIT(TmpCollectivePlanTeam."Resource No.");
    END;

    PROCEDURE GetNoOfResourcesInTeam@1100528602(VAR ITmpCollectivePlanTeam@1100528600 : TEMPORARY Record 11071979) : Integer;
    VAR
      TmpCollectivePlanTeam@1100528601 : TEMPORARY Record 11071979;
    BEGIN
      TmpCollectivePlanTeam.COPY(ITmpCollectivePlanTeam, TRUE);
      EXIT(TmpCollectivePlanTeam.COUNT);
    END;

    PROCEDURE GetNoOfResourcesInTeamText@1100525001(VAR ITmpCollectivePlanTeam@1100525000 : TEMPORARY Record 11071979) : Text;
    VAR
      NoOfResourcesInTeam@1100525001 : Integer;
    BEGIN
      NoOfResourcesInTeam := GetNoOfResourcesInTeam(ITmpCollectivePlanTeam);
      CASE NoOfResourcesInTeam OF
        0: EXIT;
        1: BEGIN
             ITmpCollectivePlanTeam.FINDFIRST;
             EXIT(ITmpCollectivePlanTeam."Resource No.");
           END;
      ELSE
        EXIT(STRSUBSTNO('[%1]', NoOfResourcesInTeam));
      END;
    END;

    PROCEDURE GetResourceFilter@1100525000(VAR ITmpCollectivePlanTeam@1100525000 : TEMPORARY Record 11071979) ResourceFilter : Text;
    VAR
      TmpCollectivePlanTeam@1100525001 : TEMPORARY Record 11071979;
    BEGIN
      TmpCollectivePlanTeam.COPY(ITmpCollectivePlanTeam, TRUE);
      IF TmpCollectivePlanTeam.FINDSET THEN
        REPEAT
          IF ResourceFilter <> '' THEN
            ResourceFilter := ResourceFilter + '|';
          ResourceFilter := ResourceFilter + TmpCollectivePlanTeam."Resource No.";
        UNTIL TmpCollectivePlanTeam.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

