OBJECT Codeunit 5478 Graph Mgt - Journal Lines
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      GraphMgtJournal@1000 : Codeunit 5482;

    [External]
    PROCEDURE SetJournalLineTemplateAndBatch@1(VAR GenJournalLine@1001 : Record 81;JournalLineBatchName@1003 : Code[10]);
    BEGIN
      GenJournalLine.VALIDATE("Journal Template Name",GraphMgtJournal.GetDefaultJournalLinesTemplateName);
      GenJournalLine.SETRANGE("Journal Template Name",GenJournalLine."Journal Template Name");

      GenJournalLine.VALIDATE("Journal Batch Name",JournalLineBatchName);
      GenJournalLine.SETRANGE("Journal Batch Name",JournalLineBatchName);
    END;

    [External]
    PROCEDURE SetJournalLineFilters@3(VAR GenJournalLine@1000 : Record 81);
    BEGIN
      GenJournalLine.SETRANGE("Document Type",GenJournalLine."Document Type"::" ");
      GenJournalLine.SETRANGE("Account Type",GenJournalLine."Account Type"::"G/L Account");
      GenJournalLine.SETRANGE("Journal Template Name",GraphMgtJournal.GetDefaultJournalLinesTemplateName);
    END;

    [External]
    PROCEDURE SetJournalLineValues@25(VAR GenJournalLine@1000 : Record 81;TempGenJournalLine@1001 : TEMPORARY Record 81);
    VAR
      GenJournalBatch@1003 : Record 232;
      DummyDate@1002 : Date;
    BEGIN
      GenJournalLine.VALIDATE("Account Type",GenJournalLine."Account Type"::"G/L Account");
      GenJournalLine.VALIDATE("Account No.",TempGenJournalLine."Account No.");
      IF TempGenJournalLine."Posting Date" <> DummyDate THEN
        GenJournalLine.VALIDATE("Posting Date",TempGenJournalLine."Posting Date");
      GenJournalLine.VALIDATE("Document Type",GenJournalLine."Document Type"::" ");
      IF TempGenJournalLine."Document No." <> '' THEN
        GenJournalLine.VALIDATE("Document No.",TempGenJournalLine."Document No.");
      GenJournalLine.VALIDATE("External Document No.",TempGenJournalLine."External Document No.");
      GenJournalLine.VALIDATE(Amount,TempGenJournalLine.Amount);
      IF TempGenJournalLine.Description <> '' THEN
        GenJournalLine.VALIDATE(Description,TempGenJournalLine.Description);
      GenJournalLine.VALIDATE(Comment,TempGenJournalLine.Comment);
      GenJournalLine.VALIDATE("Bal. Account No.",'');
      IF GenJournalBatch.GET(GenJournalLine."Journal Template Name",GenJournalLine."Journal Batch Name") THEN BEGIN
        GenJournalLine.VALIDATE("Bal. Account Type",GenJournalBatch."Bal. Account Type");
        GenJournalLine.VALIDATE("Bal. Account No.",GenJournalBatch."Bal. Account No.");
      END
    END;

    LOCAL PROCEDURE EnableAccountODataWebService@1008();
    BEGIN
      UpdateIntegrationRecords(FALSE);
    END;

    [External]
    PROCEDURE UpdateIntegrationRecords@2(OnlyItemsWithoutId@1000 : Boolean);
    VAR
      GenJnlLine@1003 : Record 81;
      GraphMgtGeneralTools@1002 : Codeunit 5465;
      GenJnlLineRecordRef@1001 : RecordRef;
    BEGIN
      GenJnlLineRecordRef.OPEN(DATABASE::"Gen. Journal Line");
      GraphMgtGeneralTools.UpdateIntegrationRecords(GenJnlLineRecordRef,GenJnlLine.FIELDNO(Id),OnlyItemsWithoutId);
    END;

    [EventSubscriber(Codeunit,5465,ApiSetup)]
    LOCAL PROCEDURE HandleApiSetup@1003();
    BEGIN
      EnableAccountODataWebService;
      UpdateIds;
    END;

    [External]
    PROCEDURE UpdateIds@6();
    VAR
      GenJournalLine@1000 : Record 81;
    BEGIN
      WITH GenJournalLine DO BEGIN
        SETRANGE("Account Type","Account Type"::"G/L Account");

        IF FINDSET THEN
          REPEAT
            UpdateAccountID;
            UpdateJournalBatchID;
            MODIFY(FALSE);
          UNTIL NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}

