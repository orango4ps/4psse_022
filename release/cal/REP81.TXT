OBJECT Report 81 Import Budget from Excel
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Budget aus Excel importieren;
               ENU=Import Budget from Excel;
               NLD=Budget vanuit Excel importeren;
               NOR=Importer budsjett fra Excel];
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  BusUnit@1000 : Record 220;
                BEGIN
                  IF ToGLBudgetName = '' THEN
                    ERROR(Text000);

                  IF SheetName = '' THEN
                    SheetName := ExcelBuf.SelectSheetsName(ServerFileName);

                  BusUnitDimCode := 'BUSINESSUNIT_TAB220';
                  TempDim.INIT;
                  TempDim.Code := BusUnitDimCode;
                  TempDim."Code Caption" := UPPERCASE(BusUnit.TABLECAPTION);
                  TempDim.INSERT;

                  IF Dim.FIND('-') THEN BEGIN
                    REPEAT
                      TempDim.INIT;
                      TempDim := Dim;
                      TempDim."Code Caption" := UPPERCASE(TempDim."Code Caption");
                      TempDim.INSERT;
                    UNTIL Dim.NEXT = 0;
                  END;

                  IF GLAcc.FIND('-') THEN BEGIN
                    REPEAT
                      TempGLAcc.INIT;
                      TempGLAcc := GLAcc;
                      TempGLAcc.INSERT;
                    UNTIL GLAcc.NEXT = 0;
                  END;

                  ExcelBuf.LOCKTABLE;
                  BudgetBuf.LOCKTABLE;
                  GLBudgetEntry.SETRANGE("Budget Name",ToGLBudgetName);
                  IF NOT GLBudgetName.GET(ToGLBudgetName) THEN
                    CLEAR(GLBudgetName);

                  GLSetup.GET;
                  GlobalDim1Code := GLSetup."Global Dimension 1 Code";
                  GlobalDim2Code := GLSetup."Global Dimension 2 Code";
                  BudgetDim1Code := GLBudgetName."Budget Dimension 1 Code";
                  BudgetDim2Code := GLBudgetName."Budget Dimension 2 Code";
                  BudgetDim3Code := GLBudgetName."Budget Dimension 3 Code";
                  BudgetDim4Code := GLBudgetName."Budget Dimension 4 Code";

                  ExcelBuf.OpenBook(ServerFileName,SheetName);
                  ExcelBuf.ReadSheet;

                  AnalyzeData;
                END;

    OnPostReport=BEGIN
                   ExcelBuf.DELETEALL;
                   BudgetBuf.DELETEALL;
                 END;

  }
  DATASET
  {
    { 1   ;    ;DataItem;BudgetBuf           ;
               DataItemTable=Table371;
               DataItemTableView=SORTING(G/L Account No.,Dimension Value Code 1,Dimension Value Code 2,Dimension Value Code 3,Dimension Value Code 4,Dimension Value Code 5,Dimension Value Code 6,Dimension Value Code 7,Dimension Value Code 8,Date);
               OnPreDataItem=VAR
                               ConfirmManagement@1000 : Codeunit 27;
                             BEGIN
                               RecNo := 0;

                               IF NOT GLBudgetName.GET(ToGLBudgetName) THEN BEGIN
                                 IF NOT ConfirmManagement.ConfirmProcess(
                                      STRSUBSTNO(Text001,GLBudgetName.TABLECAPTION,ToGLBudgetName),TRUE)
                                 THEN
                                   CurrReport.BREAK;
                                 GLBudgetName.Name := ToGLBudgetName;
                                 GLBudgetName.INSERT;
                               END ELSE BEGIN
                                 IF GLBudgetName.Blocked THEN BEGIN
                                   MESSAGE(Text002,
                                     GLBudgetEntry.FIELDCAPTION("Budget Name"),ToGLBudgetName);
                                   CurrReport.BREAK;
                                 END;
                                 IF NOT ConfirmManagement.ConfirmProcess(
                                      STRSUBSTNO(Text003,LOWERCASE(FORMAT(SELECTSTR(ImportOption + 1,Text027))),ToGLBudgetName),TRUE)
                                 THEN
                                   CurrReport.BREAK;
                               END;

                               IF GLBudgetEntry3.FINDLAST THEN
                                 EntryNo := GLBudgetEntry3."Entry No." + 1
                               ELSE
                                 EntryNo := 1;
                               LastEntryNoBeforeImport := GLBudgetEntry3."Entry No.";
                             END;

               OnAfterGetRecord=BEGIN
                                  RecNo := RecNo + 1;

                                  IF ImportOption = ImportOption::"Replace entries" THEN BEGIN
                                    GLBudgetEntry.SETRANGE("G/L Account No.","G/L Account No.");
                                    GLBudgetEntry.SETRANGE(Date,Date);
                                    GLBudgetEntry.SETFILTER("Entry No.",'<=%1',LastEntryNoBeforeImport);
                                    IF DimCode[1] <> '' THEN
                                      SetBudgetDimFilter(DimCode[1],"Dimension Value Code 1",GLBudgetEntry);
                                    IF DimCode[2] <> '' THEN
                                      SetBudgetDimFilter(DimCode[2],"Dimension Value Code 2",GLBudgetEntry);
                                    IF DimCode[3] <> '' THEN
                                      SetBudgetDimFilter(DimCode[3],"Dimension Value Code 3",GLBudgetEntry);
                                    IF DimCode[4] <> '' THEN
                                      SetBudgetDimFilter(DimCode[4],"Dimension Value Code 4",GLBudgetEntry);
                                    IF DimCode[5] <> '' THEN
                                      SetBudgetDimFilter(DimCode[5],"Dimension Value Code 5",GLBudgetEntry);
                                    IF DimCode[6] <> '' THEN
                                      SetBudgetDimFilter(DimCode[6],"Dimension Value Code 6",GLBudgetEntry);
                                    IF DimCode[7] <> '' THEN
                                      SetBudgetDimFilter(DimCode[7],"Dimension Value Code 7",GLBudgetEntry);
                                    IF DimCode[8] <> '' THEN
                                      SetBudgetDimFilter(DimCode[8],"Dimension Value Code 8",GLBudgetEntry);
                                    IF NOT GLBudgetEntry.ISEMPTY THEN
                                      GLBudgetEntry.DELETEALL(TRUE);
                                  END;

                                  IF Amount = 0 THEN
                                    CurrReport.SKIP;
                                  IF NOT IsPostingAccount("G/L Account No.") THEN
                                    CurrReport.SKIP;
                                  GLBudgetEntry.INIT;
                                  GLBudgetEntry."Entry No." := EntryNo;
                                  GLBudgetEntry."Budget Name" := ToGLBudgetName;
                                  GLBudgetEntry."G/L Account No." := "G/L Account No.";
                                  GLBudgetEntry.Date := Date;
                                  GLBudgetEntry.Amount := ROUND(Amount);
                                  GLBudgetEntry.Description := Description;

                                  // Clear any entries in the temporary dimension set entry table
                                  IF NOT TempDimSetEntry.ISEMPTY THEN
                                    TempDimSetEntry.DELETEALL(TRUE);

                                  IF "Dimension Value Code 1" <> '' THEN
                                    InsertGLBudgetDim(DimCode[1],"Dimension Value Code 1",GLBudgetEntry);
                                  IF "Dimension Value Code 2" <> '' THEN
                                    InsertGLBudgetDim(DimCode[2],"Dimension Value Code 2",GLBudgetEntry);
                                  IF "Dimension Value Code 3" <> '' THEN
                                    InsertGLBudgetDim(DimCode[3],"Dimension Value Code 3",GLBudgetEntry);
                                  IF "Dimension Value Code 4" <> '' THEN
                                    InsertGLBudgetDim(DimCode[4],"Dimension Value Code 4",GLBudgetEntry);
                                  IF "Dimension Value Code 5" <> '' THEN
                                    InsertGLBudgetDim(DimCode[5],"Dimension Value Code 5",GLBudgetEntry);
                                  IF "Dimension Value Code 6" <> '' THEN
                                    InsertGLBudgetDim(DimCode[6],"Dimension Value Code 6",GLBudgetEntry);
                                  IF "Dimension Value Code 7" <> '' THEN
                                    InsertGLBudgetDim(DimCode[7],"Dimension Value Code 7",GLBudgetEntry);
                                  IF "Dimension Value Code 8" <> '' THEN
                                    InsertGLBudgetDim(DimCode[8],"Dimension Value Code 8",GLBudgetEntry);
                                  GLBudgetEntry."Dimension Set ID" := DimMgt.GetDimensionSetID(TempDimSetEntry);
                                  GLBudgetEntry.INSERT(TRUE);
                                  EntryNo := EntryNo + 1;
                                END;

               OnPostDataItem=BEGIN
                                IF RecNo > 0 THEN
                                  MESSAGE(Text004,GLBudgetEntry.TABLECAPTION,RecNo);

                                IF ImportOption = ImportOption::"Replace entries" THEN BEGIN
                                  AnalysisView.SETRANGE("Include Budgets",TRUE);
                                  IF AnalysisView.FINDSET(TRUE,FALSE) THEN
                                    REPEAT
                                      AnalysisView.AnalysisviewBudgetReset;
                                      AnalysisView.MODIFY;
                                    UNTIL AnalysisView.NEXT = 0;
                                END;
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   Description := Text005 + FORMAT(WORKDATE);
                 END;

      OnQueryClosePage=VAR
                         FileMgt@1001 : Codeunit 419;
                       BEGIN
                         IF CloseAction = ACTION::OK THEN BEGIN
                           IF ServerFileName = '' THEN
                             ServerFileName := FileMgt.UploadFile(Text006,ExcelFileExtensionTok);
                           IF ServerFileName = '' THEN
                             EXIT(FALSE);
                         END;
                       END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 9   ;2   ;Field     ;
                  CaptionML=[DEU=Budgetname;
                             ENU=Budget Name;
                             NLD=Budget;
                             NOR=Budsjettnavn;
                             SVE=Budgetnamn];
                  ToolTipML=[DEU=Gibt den Namen des Budgets an.;
                             ENU=Specifies the name of the budget.;
                             NLD=Hiermee wordt de naam van het budget opgegeven.;
                             NOR=Angir navnet pÜ budsjettet.;
                             SVE=Anger budgetens namn.];
                  ApplicationArea=#Suite;
                  SourceExpr=ToGLBudgetName;
                  TableRelation="G/L Budget Name" }

      { 7   ;2   ;Field     ;
                  CaptionML=[DEU=Option;
                             ENU=Option;
                             NLD=Optie;
                             NOR=Alternativ;
                             SVE=Alternativ];
                  ToolTipML=[DEU=Gibt an, ob die Budgetposten aus Excel den bereits bestehenden Budgetposten im System hinzugefÅgt werden oder ob die Posten in Business Central durch die Budgetposten aus Excel ersetzt werden sollen.;
                             ENU=Specifies if the budget entries are added from Excel to budget entries that are currently in the system or are replaced in Business Central with the budget entries from Excel.;
                             NLD=Hiermee wordt opgegeven of u de budgetposten uit Excel wilt toevoegen aan de huidige budgetposten in het systeem of de posten in Business Central wilt vervangen door de budgetposten uit Excel.;
                             NOR=Angir om budsjettpostene blir lagt til fra Excel i budsjettposter som for õyeblikket finnes i systemet, eller som erstattes i Business Central med budsjettpostene fra Excel.];
                  OptionCaptionML=[DEU=Posten ersetzen,Posten hinzufÅgen;
                                   ENU=Replace entries,Add entries;
                                   NLD=Posten vervangen,Posten toevoegen;
                                   NOR=Erstatt poster,Legg til poster];
                  ApplicationArea=#Suite;
                  SourceExpr=ImportOption }

      { 5   ;2   ;Field     ;
                  CaptionML=[DEU=Beschreibung;
                             ENU=Description;
                             NLD=Omschrijving;
                             NOR=Beskrivelse;
                             SVE=Beskrivning];
                  ToolTipML=[DEU=Gibt eine Beschreibung der importierten Budgetposten an, so dass diese problemlos von anderen Budgetposten unterschieden werden kînnen.;
                             ENU=Specifies a description of the imported budget entries, so that the entries can be easily identified among other budget entries.;
                             NLD=Hiermee wordt een omschrijving van de geãmporteerde budgetposten opgegeven, zodat u de posten gemakkelijk kunt terugvinden tussen de andere budgetposten.;
                             NOR=Angir en beskrivelse av de importerte budsjettpostene, slik at postene lett kan skilles fra andre budsjettposter.];
                  ApplicationArea=#Suite;
                  SourceExpr=Description }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Sie mÅssen einen Budgetnamen als Ziel fÅr den Import angeben.;ENU=You must specify a budget name to import to.;NLD=U moet de naam van het budget opgeven waarin u wilt importeren.;NOR=Du mÜ angi et budsjettnavn som det skal importeres til.';
      Text001@1001 : TextConst 'DEU=Mîchten Sie ein Element vom Typ ''%1'' mit dem Namen ''%2'' erstellen?;ENU=Do you want to create a %1 with the name %2?;NLD=Wilt u een %1 maken met de naam %2?;NOR=Vil du opprette %1 med navnet %2?';
      Text002@1002 : TextConst 'DEU=%1 %2 ist gesperrt. Sie kînnen keine Posten importieren.;ENU=%1 %2 is blocked. You cannot import entries.;NLD=%1 %2 is geblokkeerd. U kunt geen posten importeren.;NOR=%1 %2 er sperret. Du kan ikke importere poster.';
      Text003@1003 : TextConst 'DEU=Mîchten Sie fÅr den Budgetnamen ''%2'' wirklich die folgende Aktion ausfÅhren: %1?;ENU=Are you sure that you want to %1 for the budget name %2?;NLD=Weet u zeker dat u wilt %1 voor budget %2?;NOR=Er du sikker pÜ at du vil %1 for budsjettnavnet %2?';
      Text004@1004 : TextConst 'DEU=Die Tabelle %1 wurde erfolgreich mit %2 Posten aktualisiert.;ENU=%1 table has been successfully updated with %2 entries.;NLD=%1 tabel is bijgewerkt met %2 posten.;NOR=Tabell %1 er oppdatert med %2 poster.';
      Text005@1005 : TextConst 'DEU="Aus Excel importiert ";ENU="Imported from Excel ";NLD="Geãmporteerd vanuit Excel ";NOR="Importert fra Excel "';
      Text006@1006 : TextConst 'DEU=Exceldatei importieren;ENU=Import Excel File;NLD=Excel-bestand importeren;NOR=Importer Excel-fil;SVE=Importera Excel-fil';
      Text007@1007 : TextConst 'DEU=Daten werden analysiert ...\\;ENU=Analyzing Data...\\;NLD=Analyseren gegevens...\\;NOR=Analyserer data...\\;SVE=Analyserar data...\\';
      Text008@1008 : TextConst 'DEU=Sie kînnen in Ihrer Exceltabelle nicht mehr als acht Dimensionen festlegen.;ENU=You cannot specify more than 8 dimensions in your Excel worksheet.;NLD=U kunt niet meer dan 8 dimensies specificeren in uw Excel-werkblad.;NOR=Du kan ikke angi mer enn 8 dimensjoner i Excel-regnearket.';
      Text010@1010 : TextConst 'DEU=Sachkontonr.;ENU=G/L Account No.;NLD=Grootboekrekeningnr.;NOR=Finanskontonr.;SVE=Redovisningskontonr';
      Text011@1011 : TextConst 'DEU=Der Text Sachkontonr. kann in der Exceltabelle nur einmal angegeben werden.;ENU=The text G/L Account No. can only be specified once in the Excel worksheet.;NLD=De tekst Grootboekrekeningnr. kan slechts eenmaal in het Excel-werkblad opgegeven worden.;NOR=Teksten Finanskontonr. kan bare angis Çn gang i Excel-arket.';
      Text012@1012 : TextConst 'DEU=Die in der Exceltabelle angegebenen Dimensionen mÅssen in die Zeilen vor der Tabelle Åbertragen werden.;ENU=The dimensions specified by worksheet must be placed in the lines before the table.;NLD=De opgegeven dimensies per werkblad moeten op de regels voor de tabel geplaatst worden.;NOR=Dimensjonene som er angitt i regnearket, mÜ settes inn pÜ linjene fõr tabellen.';
      Text013@1013 : TextConst 'DEU="Dimension ";ENU="Dimension ";NLD="Dimensie ";NOR="Dimensjon ";SVE=Dimension';
      Text014@1014 : TextConst 'DEU=Datum;ENU=Date;NLD=Datum;NOR=Dato;SVE=Datum';
      Text015@1015 : TextConst 'DEU=Dimension 1;ENU=Dimension 1;NLD=Dimensie 1;NOR=Dimensjon 1;SVE=Dimension 1';
      Text016@1016 : TextConst 'DEU=Dimension 2;ENU=Dimension 2;NLD=Dimensie 2;NOR=Dimensjon 2;SVE=Dimension 2';
      Text017@1017 : TextConst 'DEU=Dimension 3;ENU=Dimension 3;NLD=Dimensie 3;NOR=Dimensjon 3';
      Text018@1018 : TextConst 'DEU=Dimension 4;ENU=Dimension 4;NLD=Dimensie 4;NOR=Dimensjon 4';
      Text019@1019 : TextConst 'DEU=Dimension 5;ENU=Dimension 5;NLD=Dimensie 5;NOR=Dimensjon 5';
      Text020@1020 : TextConst 'DEU=Dimension 6;ENU=Dimension 6;NLD=Dimensie 6;NOR=Dimensjon 6';
      Text021@1021 : TextConst 'DEU=Dimension 7;ENU=Dimension 7;NLD=Dimensie 7;NOR=Dimensjon 7';
      Text022@1022 : TextConst 'DEU=Dimension 8;ENU=Dimension 8;NLD=Dimensie 8;NOR=Dimensjon 8';
      Text023@1023 : TextConst 'DEU=Sie kînnen dieselben Daten nicht ein zweites Mal importieren.\;ENU=You cannot import the same information twice.\;NLD=U kunt dezelfde informatie niet tweemaal importeren.\;NOR=Du kan ikke importere samme informasjon to ganger.\';
      Text024@1024 : TextConst 'DEU=Die Kombination Sachkontonr. - Dimensionen - Datum muss eindeutig sein.;ENU=The combination G/L Account No. - Dimensions - Date must be unique.;NLD=De combinatie Grootboekrekeningnr. - Dimensies - Datum moet uniek zijn.;NOR=Kombinasjonen av Finanskontonr. - Dimensjoner - Dato mÜ vëre unik.';
      Text025@1025 : TextConst 'DEU=Es wurden keine Sachkonten in der Exceltabelle gefunden.;ENU=G/L Accounts have not been found in the Excel worksheet.;NLD=Er werden geen grootboekrekeningen in het Excel-werkblad gevonden.;NOR=Finner ingen finanskonti i Excel-arket.';
      Text026@1026 : TextConst 'DEU=Es wurden keine Daten in der Exceltabelle gefunden.;ENU=Dates have not been recognized in the Excel worksheet.;NLD=Er werden geen datums in het Excel-werkblad gevonden.;NOR=Datoene gjenkjennes ikke i Excel-arket.';
      ExcelBuf@1027 : Record 370;
      Dim@1028 : Record 348;
      TempDim@1030 : TEMPORARY Record 348;
      GLBudgetEntry@1032 : Record 96;
      TempDimSetEntry@1029 : TEMPORARY Record 480;
      GLSetup@1034 : Record 98;
      GLAcc@1035 : Record 15;
      TempGLAcc@1036 : TEMPORARY Record 15;
      GLBudgetName@1048 : Record 95;
      GLBudgetEntry3@1049 : Record 96;
      AnalysisView@1057 : Record 363;
      DimMgt@1061 : Codeunit 408;
      Window@1009 : Dialog;
      ServerFileName@1033 : Text;
      SheetName@1038 : Text[250];
      ToGLBudgetName@1039 : Code[10];
      DimCode@1040 : ARRAY [8] OF Code[20];
      EntryNo@1041 : Integer;
      LastEntryNoBeforeImport@1058 : Integer;
      GlobalDim1Code@1042 : Code[20];
      GlobalDim2Code@1043 : Code[20];
      TotalRecNo@1044 : Integer;
      RecNo@1045 : Integer;
      Description@1047 : Text[50];
      BusUnitDimCode@1031 : Code[20];
      BudgetDim1Code@1050 : Code[20];
      BudgetDim2Code@1051 : Code[20];
      BudgetDim3Code@1052 : Code[20];
      BudgetDim4Code@1053 : Code[20];
      ImportOption@1054 : 'Replace entries,Add entries';
      Text027@1055 : TextConst 'DEU=Posten ersetzen,Posten hinzufÅgen;ENU=Replace Entries,Add Entries;NLD=Posten vervangen,Posten toevoegen;NOR=Erstatt poster,Legg til poster';
      Text028@1056 : TextConst
        'DEU=Beim Exportieren des Budgets wurde ein Filter auf %1 verwendet. Wenn ein Filter auf einer Dimension verwendet wurde, muss in der importierten Tabelle eine Spalte mit derselben Dimension vorhanden sein.',
        'ENU=A filter has been used on the %1 when the budget was exported. When a filter on a dimension has been used, a column with the same dimension must be present in the worksheet imported. The column in the worksheet must specify the dimension value codes the program should use when importing the budget.',
        'NLD=Er is een filter op %1 gebruikt toen het budget werd geâxporteerd. Indien op een dimensie een filter wordt gebruikt, moet het werkblad een kolom met dezelfde dimensie bevatten. De kolom in het werkblad moet de dimensiewaardecodes bevatten die tijdens de import van het budget moeten worden gebruikt.',
        'NOR=Det ble brukt et filter pÜ %1 da budsjettet ble eksportert. NÜr det er brukt et filter pÜ en dimensjon, mÜ det importerte regnearket inneholde en kolonne som har samme dimensjon. Kolonnen i regnearket mÜ angi kodene for dimensjonsverdi som programmet skal bruke nÜr budsjettet importeres.';
      ExcelFileExtensionTok@1037 : TextConst '@@@={Locked};DEU=.xlsx;ENU=.xlsx;NLD=.xlsx;NOR=.xlsx';

    LOCAL PROCEDURE AnalyzeData@2();
    VAR
      TempExcelBuf@1000 : TEMPORARY Record 370;
      BudgetBuf@1001 : Record 371;
      DummyBudgetBuf@1002 : Record 371;
      HeaderRowNo@1003 : Integer;
      CountDim@1004 : Integer;
      TestDateTime@1005 : DateTime;
      OldRowNo@1006 : Integer;
      DimRowNo@1007 : Integer;
      DimCode3@1008 : Code[20];
    BEGIN
      Window.OPEN(
        Text007 +
        '@1@@@@@@@@@@@@@@@@@@@@@@@@@\');
      Window.UPDATE(1,0);
      TotalRecNo := ExcelBuf.COUNT;
      RecNo := 0;
      CountDim := 0;
      BudgetBuf.DELETEALL;

      IF ExcelBuf.FIND('-') THEN BEGIN
        REPEAT
          RecNo := RecNo + 1;
          Window.UPDATE(1,ROUND(RecNo / TotalRecNo * 10000,1));
          TempDim.SETRANGE("Code Caption",UPPERCASE(FormatData(ExcelBuf."Cell Value as Text")));
          CASE TRUE OF
            ExcelBuf."Cell Value as Text" = GLBudgetEntry.FIELDCAPTION("G/L Account No."):
              BEGIN
                IF HeaderRowNo = 0 THEN BEGIN
                  HeaderRowNo := ExcelBuf."Row No.";
                  TempExcelBuf := ExcelBuf;
                  TempExcelBuf.Comment := Text010;
                  TempExcelBuf.INSERT;
                END ELSE
                  ERROR(Text011);
              END;
            TempDim.FINDFIRST AND (ExcelBuf."Row No." <> HeaderRowNo):
              BEGIN
                IF HeaderRowNo <> 0 THEN
                  ERROR(Text012);

                IncreaseAndCheckCountDim(CountDim);
                DimCode[CountDim] := TempDim.Code;
                DimRowNo := ExcelBuf."Row No.";
                DimCode3 := TempDim.Code;
              END;
            (ExcelBuf."Row No." = DimRowNo) AND (ExcelBuf."Column No." > 1) AND (ImportOption = ImportOption::"Replace entries"):
              CASE DimCode3 OF
                BusUnitDimCode:
                  GLBudgetEntry.SETFILTER("Business Unit Code",ExcelBuf."Cell Value as Text");
                GlobalDim1Code:
                  GLBudgetEntry.SETFILTER("Global Dimension 1 Code",ExcelBuf."Cell Value as Text");
                GlobalDim2Code:
                  GLBudgetEntry.SETFILTER("Global Dimension 2 Code",ExcelBuf."Cell Value as Text");
                BudgetDim1Code:
                  GLBudgetEntry.SETFILTER("Budget Dimension 1 Code",ExcelBuf."Cell Value as Text");
                BudgetDim2Code:
                  GLBudgetEntry.SETFILTER("Budget Dimension 2 Code",ExcelBuf."Cell Value as Text");
                BudgetDim3Code:
                  GLBudgetEntry.SETFILTER("Budget Dimension 3 Code",ExcelBuf."Cell Value as Text");
                BudgetDim4Code:
                  GLBudgetEntry.SETFILTER("Budget Dimension 4 Code",ExcelBuf."Cell Value as Text");
              END;
            ExcelBuf."Row No." = HeaderRowNo:
              BEGIN
                TempExcelBuf := ExcelBuf;
                CASE TRUE OF
                  TempDim.FINDFIRST:
                    BEGIN
                      TempDim.MARK(FALSE);
                      IncreaseAndCheckCountDim(CountDim);
                      TempExcelBuf.Comment := Text013 + FORMAT(CountDim);
                      TempExcelBuf.INSERT;
                      DimCode[CountDim] := TempDim.Code;
                    END;
                  EVALUATE(TestDateTime,TempExcelBuf."Cell Value as Text"):
                    BEGIN
                      TempExcelBuf."Cell Value as Text" := FORMAT(DT2DATE(TestDateTime));
                      TempExcelBuf.Comment := Text014;
                      TempExcelBuf.INSERT;
                    END;
                END;
              END;
            (ExcelBuf."Row No." > HeaderRowNo) AND (HeaderRowNo > 0):
              BEGIN
                IF ExcelBuf."Row No." <> OldRowNo THEN BEGIN
                  OldRowNo := ExcelBuf."Row No.";
                  CLEAR(DummyBudgetBuf);
                END;

                TempExcelBuf.SETRANGE("Column No.",ExcelBuf."Column No.");
                IF TempExcelBuf.FINDFIRST THEN
                  CASE TempExcelBuf.Comment OF
                    Text010:
                      BEGIN
                        TempGLAcc.SETRANGE(
                          "No.",
                          COPYSTR(
                            ExcelBuf."Cell Value as Text",
                            1,MAXSTRLEN(DummyBudgetBuf."G/L Account No.")));
                        IF TempGLAcc.FINDFIRST THEN
                          DummyBudgetBuf."G/L Account No." :=
                            COPYSTR(
                              ExcelBuf."Cell Value as Text",
                              1,MAXSTRLEN(DummyBudgetBuf."G/L Account No."))
                        ELSE
                          DummyBudgetBuf."G/L Account No." := '';
                      END;
                    Text015:
                      DummyBudgetBuf."Dimension Value Code 1" :=
                        COPYSTR(
                          ExcelBuf."Cell Value as Text",
                          1,MAXSTRLEN(DummyBudgetBuf."Dimension Value Code 1"));
                    Text016:
                      DummyBudgetBuf."Dimension Value Code 2" :=
                        COPYSTR(
                          ExcelBuf."Cell Value as Text",
                          1,MAXSTRLEN(DummyBudgetBuf."Dimension Value Code 2"));
                    Text017:
                      DummyBudgetBuf."Dimension Value Code 3" :=
                        COPYSTR(
                          ExcelBuf."Cell Value as Text",
                          1,MAXSTRLEN(DummyBudgetBuf."Dimension Value Code 3"));
                    Text018:
                      DummyBudgetBuf."Dimension Value Code 4" :=
                        COPYSTR(
                          ExcelBuf."Cell Value as Text",
                          1,MAXSTRLEN(DummyBudgetBuf."Dimension Value Code 4"));
                    Text019:
                      DummyBudgetBuf."Dimension Value Code 5" :=
                        COPYSTR(
                          ExcelBuf."Cell Value as Text",
                          1,MAXSTRLEN(DummyBudgetBuf."Dimension Value Code 5"));
                    Text020:
                      DummyBudgetBuf."Dimension Value Code 6" :=
                        COPYSTR(
                          ExcelBuf."Cell Value as Text",
                          1,MAXSTRLEN(DummyBudgetBuf."Dimension Value Code 6"));
                    Text021:
                      DummyBudgetBuf."Dimension Value Code 7" :=
                        COPYSTR(
                          ExcelBuf."Cell Value as Text",
                          1,MAXSTRLEN(DummyBudgetBuf."Dimension Value Code 7"));
                    Text022:
                      DummyBudgetBuf."Dimension Value Code 8" :=
                        COPYSTR(
                          ExcelBuf."Cell Value as Text",
                          1,MAXSTRLEN(DummyBudgetBuf."Dimension Value Code 8"));
                    Text014:
                      IF DummyBudgetBuf."G/L Account No." <> '' THEN BEGIN
                        BudgetBuf := DummyBudgetBuf;
                        IF GLBudgetEntry.GETFILTER("Global Dimension 1 Code") <> '' THEN
                          EVALUATE(BudgetBuf."Dimension Value Code 1",GLBudgetEntry.GETFILTER("Global Dimension 1 Code"));
                        IF GLBudgetEntry.GETFILTER("Global Dimension 2 Code") <> '' THEN
                          EVALUATE(BudgetBuf."Dimension Value Code 2",GLBudgetEntry.GETFILTER("Global Dimension 2 Code"));
                        IF GLBudgetEntry.GETFILTER("Budget Dimension 1 Code") <> '' THEN
                          EVALUATE(BudgetBuf."Dimension Value Code 3",GLBudgetEntry.GETFILTER("Budget Dimension 1 Code"));
                        IF GLBudgetEntry.GETFILTER("Budget Dimension 2 Code") <> '' THEN
                          EVALUATE(BudgetBuf."Dimension Value Code 4",GLBudgetEntry.GETFILTER("Budget Dimension 2 Code"));
                        IF GLBudgetEntry.GETFILTER("Budget Dimension 3 Code") <> '' THEN
                          EVALUATE(BudgetBuf."Dimension Value Code 5",GLBudgetEntry.GETFILTER("Budget Dimension 3 Code"));
                        IF GLBudgetEntry.GETFILTER("Budget Dimension 4 Code") <> '' THEN
                          EVALUATE(BudgetBuf."Dimension Value Code 6",GLBudgetEntry.GETFILTER("Budget Dimension 4 Code"));
                        IF GLBudgetEntry.GETFILTER("Business Unit Code") <> '' THEN
                          EVALUATE(BudgetBuf."Dimension Value Code 7",GLBudgetEntry.GETFILTER("Business Unit Code"));
                        EVALUATE(BudgetBuf.Date,TempExcelBuf."Cell Value as Text");
                        EVALUATE(BudgetBuf.Amount,ExcelBuf."Cell Value as Text");
                        IF NOT BudgetBuf.FIND('=') THEN
                          BudgetBuf.INSERT
                        ELSE
                          ERROR(Text023 + Text024);
                      END;
                  END;
              END;
          END;
        UNTIL ExcelBuf.NEXT = 0;
      END;

      TempDim.SETRANGE("Code Caption");
      TempDim.MARKEDONLY(TRUE);
      IF TempDim.FINDFIRST THEN BEGIN
        Dim.GET(TempDim.Code);
        ERROR(Text028,Dim."Code Caption");
      END;

      Window.CLOSE;
      TempExcelBuf.RESET;
      TempExcelBuf.SETRANGE(Comment,Text010);
      IF NOT TempExcelBuf.FINDFIRST THEN
        ERROR(Text025);
      TempExcelBuf.SETRANGE(Comment,Text014);
      IF NOT TempExcelBuf.FINDFIRST THEN
        ERROR(Text026);
    END;

    LOCAL PROCEDURE IncreaseAndCheckCountDim@1(VAR CountDim@1000 : Integer);
    VAR
      MaxCountDim@1001 : Integer;
    BEGIN
      MaxCountDim := 8;
      CountDim := CountDim + 1;
      IF CountDim > MaxCountDim THEN
        ERROR(Text008);
    END;

    LOCAL PROCEDURE InsertGLBudgetDim@4(DimCode2@1000 : Code[20];DimValCode2@1001 : Code[20];VAR GLBudgetEntry2@1002 : Record 96);
    VAR
      DimValue@1003 : Record 349;
    BEGIN
      IF DimCode2 <> BusUnitDimCode THEN BEGIN
        DimValue.GET(DimCode2,DimValCode2);
        TempDimSetEntry.INIT;
        TempDimSetEntry.VALIDATE("Dimension Code",DimCode2);
        TempDimSetEntry.VALIDATE("Dimension Value Code",DimValCode2);
        TempDimSetEntry.VALIDATE("Dimension Value ID",DimValue."Dimension Value ID");
        TempDimSetEntry.INSERT;
      END;
      CASE DimCode2 OF
        BusUnitDimCode:
          GLBudgetEntry2."Business Unit Code" := COPYSTR(DimValCode2,1,MAXSTRLEN(GLBudgetEntry2."Business Unit Code"));
        GlobalDim1Code:
          GLBudgetEntry2."Global Dimension 1 Code" := DimValCode2;
        GlobalDim2Code:
          GLBudgetEntry2."Global Dimension 2 Code" := DimValCode2;
        BudgetDim1Code:
          GLBudgetEntry2."Budget Dimension 1 Code" := DimValCode2;
        BudgetDim2Code:
          GLBudgetEntry2."Budget Dimension 2 Code" := DimValCode2;
        BudgetDim3Code:
          GLBudgetEntry2."Budget Dimension 3 Code" := DimValCode2;
        BudgetDim4Code:
          GLBudgetEntry2."Budget Dimension 4 Code" := DimValCode2;
      END;
    END;

    [External]
    PROCEDURE IsPostingAccount@9(AccNo@1000 : Code[20]) : Boolean;
    VAR
      GLAccount@1102601001 : Record 15;
    BEGIN
      IF NOT GLAccount.GET(AccNo) THEN
        EXIT(FALSE);
      EXIT(GLAccount."Account Type" IN [GLAccount."Account Type"::Posting,GLAccount."Account Type"::"Begin-Total"]);
    END;

    LOCAL PROCEDURE FormatData@8(TextToFormat@1000 : Text[250]) : Text[250];
    VAR
      FormatInteger@1001 : Integer;
      FormatDecimal@1002 : Decimal;
      FormatDate@1003 : Date;
    BEGIN
      CASE TRUE OF
        EVALUATE(FormatInteger,TextToFormat):
          EXIT(FORMAT(FormatInteger));
        EVALUATE(FormatDecimal,TextToFormat):
          EXIT(FORMAT(FormatDecimal));
        EVALUATE(FormatDate,TextToFormat):
          EXIT(FORMAT(FormatDate));
        ELSE
          EXIT(TextToFormat);
      END;
    END;

    [External]
    PROCEDURE SetParameters@3(NewToGLBudgetName@1000 : Code[10];NewImportOption@1001 : Option);
    BEGIN
      ToGLBudgetName := NewToGLBudgetName;
      ImportOption := NewImportOption;
    END;

    [External]
    PROCEDURE SetBudgetDimFilter@1102601000(DimCode2@1102601002 : Code[20];DimValCode2@1102601001 : Code[20];VAR GLBudgetEntry2@1102601000 : Record 96);
    BEGIN
      CASE DimCode2 OF
        BusUnitDimCode:
          GLBudgetEntry2.SETRANGE("Business Unit Code",DimValCode2);
        GlobalDim1Code:
          GLBudgetEntry2.SETRANGE("Global Dimension 1 Code",DimValCode2);
        GlobalDim2Code:
          GLBudgetEntry2.SETRANGE("Global Dimension 2 Code",DimValCode2);
        BudgetDim1Code:
          GLBudgetEntry2.SETRANGE("Budget Dimension 1 Code",DimValCode2);
        BudgetDim2Code:
          GLBudgetEntry2.SETRANGE("Budget Dimension 2 Code",DimValCode2);
        BudgetDim3Code:
          GLBudgetEntry2.SETRANGE("Budget Dimension 3 Code",DimValCode2);
        BudgetDim4Code:
          GLBudgetEntry2.SETRANGE("Budget Dimension 4 Code",DimValCode2);
      END;
    END;

    [External]
    PROCEDURE SetFileName@5(NewFileName@1000 : Text);
    BEGIN
      ServerFileName := NewFileName;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

