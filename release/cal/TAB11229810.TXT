OBJECT Table 11229810 Project Portal Subsite
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnModify=BEGIN
               //IF ("Quote Request Subsite") THEN BEGIN
               //  IF (Description = xRec.Description) THEN
               //    TESTFIELD("Quote Request Subsite", FALSE);
               //END;
             END;

    OnDelete=VAR
               DocumentProperties@1100528305 : Record 11012746;
               SiteDocTypePPT@1100528304 : Record 11229811;
               DefaultRespPersPermPPT@1100528303 : Record 11229814;
               DefaultAuthorityPermPPT@1100528302 : Record 11229815;
               RespPersPermPPT@1100528301 : Record 11229812;
               AuthorityPermPPT@1100528300 : Record 11229813;
             BEGIN
               //TESTFIELD("Quote Request Subsite", FALSE);
               CheckIfDocsAreLinkedToPPT(TABLECAPTION);

               DocumentProperties.SETRANGE("Project Portal Subsite", Code);
               IF (NOT DocumentProperties.ISEMPTY) THEN
                 ERROR(Text000, TABLECAPTION);

               SiteDocTypePPT.SETRANGE("Project Portal Subsite", Code);
               SiteDocTypePPT.DELETEALL(TRUE);
               DefaultRespPersPermPPT.SETRANGE("Project Portal Subsite", Code);
               DefaultRespPersPermPPT.DELETEALL(TRUE);
               DefaultAuthorityPermPPT.SETRANGE("Project Portal Subsite", Code);
               DefaultAuthorityPermPPT.DELETEALL(TRUE);
               RespPersPermPPT.SETRANGE("Project Portal Subsite", Code);
               RespPersPermPPT.DELETEALL(TRUE);
               AuthorityPermPPT.SETRANGE("Project Portal Subsite", Code);
               AuthorityPermPPT.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               ERROR('');
             END;

    CaptionML=ENU=Document Portal Subsite;
    LookupPageID=Page11071794;
    DrillDownPageID=Page11071794;
  }
  FIELDS
  {
    { 5   ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Typ];
                                                   OptionCaptionML=ENU=Project,Service Location;
                                                   OptionString=Project,ServLoc }
    { 10  ;   ;Code                ;Code20        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 20  ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 30  ;   ;High Security Subsite;Boolean      ;OnValidate=BEGIN
                                                                ProcessSecurityTypeChange;
                                                              END;

                                                   CaptionML=ENU=High Security Site }
    { 40  ;   ;SharePoint Template ;Text20        ;CaptionML=[ENU=SharePoint Template;
                                                              NOR=SharePoint-mal;
                                                              SVE=SharePoint-mall] }
    { 50  ;   ;Quote Request Subsite;Boolean      ;CaptionML=ENU=Quote Request Subsite;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Type,Code                               ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100525002 : TextConst 'ENU=Change of %1 is not allowed.\There are already Documents linked to it.';

    PROCEDURE ProcessSecurityTypeChange@1100528300();
    VAR
      DefaultRespPersPermPPT@1100528303 : Record 11229814;
      DefaultAuthorityPermPPT@1100528302 : Record 11229815;
    BEGIN
      CheckIfDocsAreLinkedToPPT(FIELDCAPTION("High Security Subsite"));

      IF ("High Security Subsite") THEN BEGIN
        DefaultRespPersPermPPT.SETRANGE("Subsite Type", Type);
        DefaultRespPersPermPPT.SETRANGE(Type, DefaultRespPersPermPPT.Type::"Field Filter");
        DefaultRespPersPermPPT.SETRANGE("Project Portal Subsite", Code);
        DefaultRespPersPermPPT.DELETEALL(TRUE);

        DefaultAuthorityPermPPT.SETRANGE("Subsite Type", Type);
        DefaultAuthorityPermPPT.SETRANGE("Project Portal Subsite", Code);
        DefaultAuthorityPermPPT.DELETEALL(TRUE);
      END ELSE BEGIN
        DefaultRespPersPermPPT.SETRANGE("Subsite Type", Type);
        DefaultRespPersPermPPT.SETRANGE(Type, DefaultRespPersPermPPT.Type::Access);
        DefaultRespPersPermPPT.SETRANGE("Project Portal Subsite", Code);
        DefaultRespPersPermPPT.DELETEALL(TRUE);

        DefaultAuthorityPermPPT.SETRANGE("Subsite Type", Type);
        DefaultAuthorityPermPPT.SETRANGE(Type, DefaultAuthorityPermPPT.Type::Access);
        DefaultAuthorityPermPPT.SETRANGE("Project Portal Subsite", Code);
        DefaultAuthorityPermPPT.DELETEALL(TRUE);
      END;
    END;

    PROCEDURE CheckIfDocsAreLinkedToPPT@1100528301(ErrorMessage@1100525000 : Text[250]);
    VAR
      DocumentProperties@1100528300 : Record 11012746;
      DocumentLink@1100528301 : Record 11012747;
      Job@1100528302 : Record 11072003;
      ServiceLocation@1100528304 : Record 11012801;
      RecRef@1100528303 : RecordRef;
    BEGIN
      DocumentProperties.SETRANGE("Internal Company", COMPANYNAME);
      DocumentProperties.SETRANGE("Project Portal Subsite", Code);
      DocumentProperties.SETFILTER("Web link", '<>%1', '');
      IF (NOT DocumentProperties.FINDSET) THEN
        EXIT;

      REPEAT
        DocumentLink.SETRANGE("Document No.", DocumentProperties."No.");
        DocumentLink.SETFILTER("Table No.", '%1|%2', DATABASE::Job, DATABASE::"Service Location");
        IF (DocumentLink.FINDSET) THEN BEGIN
          REPEAT
            IF (RecRef.GET(DocumentLink."Record ID")) THEN BEGIN
              CASE DocumentLink."Table No." OF
                DATABASE::Job: BEGIN
                  RecRef.SETTABLE(Job);
                  IF (Job.IsJobInProjectPortal) THEN
                    ERROR(Text000, ErrorMessage);
                END;
                DATABASE::"Service Location": BEGIN
                  RecRef.SETTABLE(ServiceLocation);
                  IF (Job."SharePoint Site Generated") AND (Job."SharePoint Site URL" <> '') AND (Job."Project Portal Project") THEN
                    ERROR(Text000, ErrorMessage);
                END;
              END;
            END;
          UNTIL (DocumentLink.NEXT = 0);
        END;
      UNTIL (DocumentProperties.NEXT = 0);
    END;

    PROCEDURE InsertQuoteRequestSubsite@1100528303();
    VAR
      ProjectPortalSubsite@1100528300 : Record 11229810;
    BEGIN
      ProjectPortalSubsite.SETRANGE(Type, ProjectPortalSubsite.Type::Project);
      ProjectPortalSubsite.SETRANGE("Quote Request Subsite", TRUE);
      IF (ProjectPortalSubsite.FINDFIRST) THEN
        EXIT;

      ProjectPortalSubsite.INIT;
      ProjectPortalSubsite.Type := ProjectPortalSubsite.Type::Project;
      ProjectPortalSubsite.Code := 'Quote';
      ProjectPortalSubsite.Description := ProjectPortalSubsite.FIELDCAPTION("Quote Request Subsite");
      ProjectPortalSubsite."Quote Request Subsite" := TRUE;
      ProjectPortalSubsite.INSERT;
    END;

    BEGIN
    END.
  }
}

