OBJECT Table 11126014 SH Workplan Process Text
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               "Delete Allowed" := TRUE;
               "Standard Text" := FALSE;
               Value := '';
               "Value (Date)" := 0D;
               "Value (Value)" := 0;
               "Value (YesNo)" := FALSE;
             END;

    OnDelete=VAR
               SHWorkplanProcessTxtOption@1100528300 : Record 11126015;
             BEGIN
               TESTFIELD("Delete Allowed", TRUE);

               SHWorkplanProcessTxtOption.SETRANGE("Source Type", "Source Type");
               SHWorkplanProcessTxtOption.SETRANGE("Source Code", "Source Code");
               SHWorkplanProcessTxtOption.SETRANGE("Workplan Date", "Workplan Date");
               SHWorkplanProcessTxtOption.SETRANGE("Main Process Code", "Main Process Code");
               SHWorkplanProcessTxtOption.SETRANGE("Sub Process Code", "Sub Process Code");
               SHWorkplanProcessTxtOption.SETRANGE("Location Code", "Location Code");
               SHWorkplanProcessTxtOption.SETRANGE("Workplan Process Text Line No.", "Line No.");
               SHWorkplanProcessTxtOption.DELETEALL;
             END;

    CaptionML=ENU=Workplan Process Text (SH);
    LookupPageID=Page11130708;
    DrillDownPageID=Page11130708;
  }
  FIELDS
  {
    { 5   ;   ;Source Type         ;Option        ;CaptionML=[ENU=Source Type;
                                                              NOR=Kildetype;
                                                              SVE=Ursprungstyp];
                                                   OptionCaptionML=ENU=" ,Estimate,Project,Company";
                                                   OptionString=[ ,Estimate,Project,Company];
                                                   NotBlank=Yes }
    { 10  ;   ;Source Code         ;Code20        ;TableRelation=IF (Source Type=CONST(Estimate)) Estimate.No.
                                                                 ELSE IF (Source Type=CONST(Project)) Job.No.;
                                                   CaptionML=[ENU=Source Code;
                                                              NOR=Kildespor;
                                                              SVE=Ursprungskod];
                                                   NotBlank=Yes }
    { 20  ;   ;Workplan Date       ;Date          ;TableRelation="SH Workplan"."Workplan Date" WHERE (Source Type=FIELD(Source Type),
                                                                                                      Source Code=FIELD(Source Code));
                                                   CaptionML=ENU=Workplan Date }
    { 30  ;   ;Main Process Code   ;Code20        ;TableRelation="SH Workplan"."Main Process Code" WHERE (Source Type=FIELD(Source Type),
                                                                                                          Source Code=FIELD(Source Code),
                                                                                                          Workplan Date=FIELD(Workplan Date));
                                                   CaptionML=ENU=Main Process Code;
                                                   NotBlank=Yes }
    { 40  ;   ;Sub Process Code    ;Code20        ;TableRelation="SH Workplan"."Sub Process Code" WHERE (Source Type=FIELD(Source Type),
                                                                                                         Source Code=FIELD(Source Code),
                                                                                                         Workplan Date=FIELD(Workplan Date),
                                                                                                         Main Process Code=FIELD(Main Process Code));
                                                   CaptionML=ENU=Sub Process Code;
                                                   NotBlank=Yes }
    { 45  ;   ;Location Code       ;Code20        ;TableRelation="SH Workplan"."Location Code" WHERE (Source Type=FIELD(Source Type),
                                                                                                      Source Code=FIELD(Source Code),
                                                                                                      Workplan Date=FIELD(Workplan Date),
                                                                                                      Main Process Code=FIELD(Main Process Code),
                                                                                                      Sub Process Code=FIELD(Sub Process Code));
                                                   CaptionML=[ENU=Location Code;
                                                              NOR=Lokasjonskode;
                                                              SVE=Lagerst„llekod] }
    { 50  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 100 ;   ;Std Process Text Line No.;Integer  ;TableRelation="SH Standard Process Text"."Line No.";
                                                   OnValidate=BEGIN
                                                                IF ("Std Process Text Line No." <= 0) THEN
                                                                  ERROR('');
                                                                CopyStandardProcessText;
                                                              END;

                                                   CaptionML=ENU=Standard Process Text Line No. }
    { 110 ;   ;Text                ;Text100       ;CaptionML=[ENU=Text;
                                                              NOR=Tekst;
                                                              SVE=Ben„mning];
                                                   Editable=No }
    { 120 ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Typ];
                                                   OptionCaptionML=ENU=Text,Yes/No,Date,Value,Option,,,,,Header1,Header2,Header3;
                                                   OptionString=Text,YesNo,Date,Value,Option,,,,,Header1,Header2,Header3;
                                                   Editable=No }
    { 121 ;   ;Nr of Options       ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("SH Workplan Process Txt Option" WHERE (Source Type=FIELD(Source Type),
                                                                                                             Source Code=FIELD(Source Code),
                                                                                                             Workplan Date=FIELD(Workplan Date),
                                                                                                             Main Process Code=FIELD(Main Process Code),
                                                                                                             Sub Process Code=FIELD(Sub Process Code),
                                                                                                             Workplan Process Text Line No.=FIELD(Line No.)));
                                                   CaptionML=ENU=Nr of Options;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 130 ;   ;Unit Of Measure     ;Code10        ;TableRelation="Unit of Measure";
                                                   CaptionML=[ENU=Unit Of Measure;
                                                              NOR=Enhet;
                                                              SVE=Enhet];
                                                   Editable=No }
    { 131 ;   ;UoM Description     ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Unit of Measure".Description WHERE (Code=FIELD(Unit Of Measure)));
                                                   CaptionML=ENU=UoM Description;
                                                   Editable=No }
    { 140 ;   ;Value               ;Text80        ;OnValidate=BEGIN
                                                                EvaluateValue;
                                                              END;

                                                   OnLookup=BEGIN
                                                              HandleLookupValue;
                                                            END;

                                                   CaptionML=[ENU=Value;
                                                              NOR=Verdi;
                                                              SVE=V„rde] }
    { 150 ;   ;Value (Date)        ;Date          ;OnValidate=BEGIN
                                                                Value := FORMAT("Std Process Text Line No.");
                                                              END;

                                                   CaptionML=[ENU=Value (Date);
                                                              NOR=Verdi (Dato);
                                                              SVE=Valuteringsdag];
                                                   Editable=No }
    { 160 ;   ;Value (Value)       ;Decimal       ;CaptionML=[ENU=Value (Value);
                                                              NOR=Verdi (Verdi);
                                                              SVE=V„rde (v„rde)];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 170 ;   ;Value (YesNo)       ;Boolean       ;CaptionML=ENU=Value (Yes/No);
                                                   Editable=No }
    { 180 ;   ;Delete Allowed      ;Boolean       ;CaptionML=ENU=Delete Allowed;
                                                   Editable=No }
    { 190 ;   ;Standard Text       ;Boolean       ;CaptionML=[ENU=Standard Text;
                                                              NOR=Standardtekst;
                                                              SVE=Standardtext];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Source Type,Source Code,Workplan Date,Main Process Code,Sub Process Code,Location Code,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      TODAYTEXT@1100528300 : TextConst 'ENU=t;NOR=t;SVE=t';
      "YESNO-YESTEXT"@1100528303 : TextConst 'ENU=y;SVE=';
      Text000@1100528301 : TextConst 'ENU=Option Value %1 not found;NOR=Alternativverdi %1 ikke funnet;SVE=Alternativv„rde %1 hittades inte';

    PROCEDURE CopyStandardProcessText@1100528300();
    VAR
      SHStandardProcessText@1100528300 : Record 11126003;
    BEGIN
      IF ("Std Process Text Line No." <= 0) THEN
        EXIT;
      IF ("Line No." = 0) THEN
        "Line No." := GetLastLineNo + 10000;

      SHStandardProcessText.GET("Std Process Text Line No.");
      Text := SHStandardProcessText.Text;
      Type := SHStandardProcessText.Type;
      "Unit Of Measure" := SHStandardProcessText."Unit Of Measure";
      IF (Type = Type::Option) THEN
        CopyStandardProcessTextOptions;
    END;

    PROCEDURE EvaluateValue@1100528603();
    VAR
      DummyDate@1100528600 : Date;
      DummyDecimal@1100528601 : Decimal;
    BEGIN
      "Value (Date)" := 0D;
      "Value (Value)" := 0;
      "Value (YesNo)" := FALSE;
      IF (Value = '') THEN BEGIN
        EXIT;
      END;

      CASE Type OF
        Type::Date: BEGIN
          IF LOWERCASE(Value) = TODAYTEXT THEN BEGIN
            Value := FORMAT(TODAY);
            "Value (Date)" := TODAY;
          END ELSE BEGIN
            EVALUATE(DummyDate, Value);
            Value := FORMAT(DummyDate);
            "Value (Date)" := DummyDate;
          END;
        END;
        Type::Option:
          Value := CheckOptionValue;
        Type::Value: BEGIN
          EVALUATE(DummyDecimal, Value);
          Value := FORMAT(DummyDecimal, 0, 1);
          "Value (Value)" := DummyDecimal;
        END;
        Type::YesNo: BEGIN
          IF (LOWERCASE(COPYSTR(Value, 1, 1)) =  FORMAT("YESNO-YESTEXT")) THEN BEGIN
            "Value (YesNo)" := TRUE;
            Value := FORMAT("Value (YesNo)");
          END ELSE BEGIN
            "Value (YesNo)" := FALSE;
            Value := FORMAT("Value (YesNo)");
          END;
        END;
        Type::Header1,
        Type::Header2,
        Type::Header3:
          Value := '';
      END;
      MODIFY;
    END;

    PROCEDURE GetLastLineNo@1100525001() : Integer;
    VAR
      SHWorkplanProcessText@1100528300 : Record 11126014;
    BEGIN
      SHWorkplanProcessText.SETRANGE("Source Type", "Source Type");
      SHWorkplanProcessText.SETRANGE("Source Code", "Source Code");
      SHWorkplanProcessText.SETRANGE("Workplan Date", "Workplan Date");
      SHWorkplanProcessText.SETRANGE("Main Process Code", "Main Process Code");
      SHWorkplanProcessText.SETRANGE("Sub Process Code", "Sub Process Code");
      SHWorkplanProcessText.SETRANGE("Location Code", "Location Code");
      IF (SHWorkplanProcessText.FINDLAST) THEN
        EXIT(SHWorkplanProcessText."Line No.");
      EXIT(0);
    END;

    PROCEDURE HandleLookupValue@1100525000();
    VAR
      SHWorkplanProcessTxtOption@1100528600 : Record 11126015;
    BEGIN
      IF (Type <> Type::Option) THEN
        EXIT;

      SHWorkplanProcessTxtOption.SETRANGE("Source Type", "Source Type");
      SHWorkplanProcessTxtOption.SETRANGE("Source Code", "Source Code");
      SHWorkplanProcessTxtOption.SETRANGE("Workplan Date", "Workplan Date");
      SHWorkplanProcessTxtOption.SETRANGE("Main Process Code", "Main Process Code");
      SHWorkplanProcessTxtOption.SETRANGE("Sub Process Code", "Sub Process Code");
      SHWorkplanProcessTxtOption.SETRANGE("Location Code", "Location Code");
      SHWorkplanProcessTxtOption.SETRANGE("Workplan Process Text Line No.", "Line No.");
      IF (PAGE.RUNMODAL(0, SHWorkplanProcessTxtOption) = ACTION::LookupOK) THEN
        VALIDATE(Value, SHWorkplanProcessTxtOption.Description);
    END;

    LOCAL PROCEDURE "---Locals---"@1100525002();
    BEGIN
    END;

    LOCAL PROCEDURE CheckOptionValue@1100528604() : Text;
    VAR
      SHWorkplanProcessTxtOption@1100528600 : Record 11126015;
    BEGIN
      SHWorkplanProcessTxtOption.SETRANGE("Source Type", "Source Type");
      SHWorkplanProcessTxtOption.SETRANGE("Source Code", "Source Code");
      SHWorkplanProcessTxtOption.SETRANGE("Main Process Code", "Main Process Code");
      SHWorkplanProcessTxtOption.SETRANGE("Sub Process Code", "Sub Process Code");
      SHWorkplanProcessTxtOption.SETRANGE("Location Code", "Location Code");
      SHWorkplanProcessTxtOption.SETRANGE("Workplan Process Text Line No.", "Line No.");
      SHWorkplanProcessTxtOption.SETRANGE(Description, Value);
      IF (SHWorkplanProcessTxtOption.FINDFIRST) THEN
        EXIT(SHWorkplanProcessTxtOption.Description);
      SHWorkplanProcessTxtOption.SETFILTER(Description, '@' + Value + '*');
      IF (SHWorkplanProcessTxtOption.COUNT = 1) THEN BEGIN
        SHWorkplanProcessTxtOption.FINDFIRST;
        EXIT(SHWorkplanProcessTxtOption.Description);
      END;
      ERROR(Text000, Value);
    END;

    LOCAL PROCEDURE CopyStandardProcessTextOptions@1100528301();
    VAR
      SHStdProcessTextOption@1100528300 : Record 11126004;
      SHWorkplanProcessTxtOption@1100528301 : Record 11126015;
    BEGIN
      SHStdProcessTextOption.SETRANGE("Std Process Text Line No.", "Std Process Text Line No.");
      IF (NOT SHStdProcessTextOption.FINDSET) THEN
        EXIT;

      REPEAT
        SHWorkplanProcessTxtOption.INIT;
        SHWorkplanProcessTxtOption."Source Type" := "Source Type";
        SHWorkplanProcessTxtOption."Source Code" := "Source Code";
        SHWorkplanProcessTxtOption."Workplan Date" := "Workplan Date";
        SHWorkplanProcessTxtOption."Main Process Code" := "Main Process Code";
        SHWorkplanProcessTxtOption."Sub Process Code" := "Sub Process Code";
        SHWorkplanProcessTxtOption."Location Code" := "Location Code";
        SHWorkplanProcessTxtOption."Workplan Process Text Line No." := "Line No.";
        SHWorkplanProcessTxtOption.Code := SHStdProcessTextOption.Code;
        SHWorkplanProcessTxtOption.Description := SHStdProcessTextOption.Description;
        SHWorkplanProcessTxtOption."Delete Allowed" := TRUE;
        SHWorkplanProcessTxtOption.INSERT;
      UNTIL (SHStdProcessTextOption.NEXT = 0);
    END;

    BEGIN
    END.
  }
}

