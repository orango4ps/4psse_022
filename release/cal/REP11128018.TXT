OBJECT Report 11128018 SIE Import
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVSE11.00.00.19846,PE5.01 (11208);
  }
  PROPERTIES
  {
    CaptionML=[ENU=SIE Import;
               NOR=SIE-import;
               SVE=SIE-import];
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  FileMgt@1000 : Codeunit 419;
                BEGIN
                  ImportFile.TEXTMODE := TRUE;
                  ServerFileName := FileMgt.ServerTempFileName('se');
                  IF NOT UPLOAD(Text002,'',Text001,'',ServerFileName) THEN
                    ERROR('');
                  ImportFile.OPEN(ServerFileName);

                  DialogWindow.OPEN(Text003 + Text004);
                END;

    OnPostReport=BEGIN
                   ImportFile.CLOSE;
                   ERASE(ServerFileName);
                   MESSAGE(Text005);
                 END;

    UsageCategory=ReportsAndAnalysis;
  }
  DATASET
  {
    { 5444;    ;DataItem;                    ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number);
               OnPreDataItem=BEGIN
                               NextEntryNo := 1;
                               ValidateJnl;
                             END;

               OnAfterGetRecord=BEGIN
                                  DialogWindow.UPDATE(1,ROUND(ImportFile.POS / ImportFile.LEN * 10000,1.0));

                                  IF ImportFile.LEN = ImportFile.POS THEN
                                    CurrReport.BREAK;

                                  ImportFile.READ(FileText);
                                  FileText := ReplaceTab(FileText);
                                  FileText := Ansi2Ascii(FileText);

                                  TempSieBuffer.INIT;
                                  TempSieBuffer."Entry No." := NextEntryNo;
                                  FileText := DELCHR(FileText,'<',' ');
                                  Pos1 := GetPos1(FileText);
                                  IF Pos1 > 0 THEN
                                    TempSieBuffer."Import Field 1" := ClChr(COPYSTR(FileText,1,Pos1))
                                  ELSE
                                    TempSieBuffer."Import Field 1" := ClChr(COPYSTR(FileText,1,STRLEN(FileText)));

                                  Pos1 := STRLEN(TempSieBuffer."Import Field 1");
                                  Pos1 += 2;
                                  TempSieBuffer."Import Field 2" := ClChr(GetNextField(COPYSTR(FileText,Pos1,STRLEN(FileText))));

                                  Pos1 += i2;
                                  Pos1 += i;

                                  IF Pos1 < STRLEN(FileText) THEN
                                    TempSieBuffer."Import Field 3" := ClChr(GetNextField(COPYSTR(FileText,Pos1,STRLEN(FileText))));

                                  Pos1 += i2;
                                  Pos1 += i;

                                  IF Pos1 < STRLEN(FileText) THEN
                                    TempSieBuffer."Import Field 4" := ClChr(GetNextField(COPYSTR(FileText,Pos1,STRLEN(FileText))));

                                  Pos1 += i2;
                                  Pos1 += i;

                                  IF Pos1 < STRLEN(FileText) THEN
                                    TempSieBuffer."Import Field 5" := ClChr(GetNextField(COPYSTR(FileText,Pos1,STRLEN(FileText))));

                                  Pos1 += i2;
                                  Pos1 += i;

                                  IF Pos1 < STRLEN(FileText) THEN
                                    TempSieBuffer."Import Field 6" := ClChr(GetNextField(COPYSTR(FileText,Pos1,STRLEN(FileText))));

                                  Pos1 += i2;
                                  Pos1 += i;

                                  IF Pos1 < STRLEN(FileText) THEN
                                    TempSieBuffer."Import Field 7" := ClChr(GetNextField(COPYSTR(FileText,Pos1,STRLEN(FileText))));

                                  Pos1 += i2;
                                  Pos1 += i;

                                  IF Pos1 < STRLEN(FileText) THEN
                                    TempSieBuffer."Import Field 8" := ClChr(GetNextField(COPYSTR(FileText,Pos1,STRLEN(FileText))));

                                  TempSieBuffer.INSERT;
                                  NextEntryNo += 1;
                                END;
                                 }

    { 8164;    ;DataItem;                    ;
               DataItemTable=Table11128018;
               DataItemTableView=SORTING(Entry No.);
               OnPreDataItem=BEGIN
                               NextLineNo := 10000;
                               GenJnlLine.SETRANGE("Journal Template Name",GenJnlBatch."Journal Template Name");
                               GenJnlLine.SETRANGE("Journal Batch Name",GenJnlBatch.Name);
                               IF GenJnlLine.FIND('+') THEN
                                 NextLineNo := GenJnlLine."Line No." + 10000;
                             END;

               OnAfterGetRecord=BEGIN
                                  DialogWindow.UPDATE(2,ROUND("Entry No." / COUNT * 10000,1.0));

                                  CASE "Import Field 1" OF
                                    'FLAGGA':
                                      TESTFIELD("Import Field 2",'0');
                                    'TRANS':
                                      CreateGenJnlLine;
                                    'KONTO':
                                      IF InsertNewAccount THEN BEGIN
                                        GLAccount.INIT;
                                        GLAccount.VALIDATE("No.",DELCHR("Import Field 2",'=',' '));
                                        GLAccount.VALIDATE(Name,COPYSTR(DELCHR("Import Field 3",'=','"'),1,30));
                                        IF (COPYSTR(GLAccount."No.",1,1) = '1') OR (COPYSTR(GLAccount."No.",1,1) = '2') THEN
                                          GLAccount."Income/Balance" := GLAccount."Income/Balance"::"Balance Sheet"
                                        ELSE
                                          GLAccount."Income/Balance" := GLAccount."Income/Balance"::"Income Statement";
                                        GLAccount."Direct Posting" := TRUE;
                                        OK := GLAccount.INSERT;
                                      END;
                                    'VER':
                                      BEGIN
                                        IF UseSerie THEN
                                          DocNo := DELCHR("Import Field 2",'=','"') + DELCHR("Import Field 3",'=','"')
                                        ELSE
                                          DocNo := DELCHR("Import Field 3",'=','"');
                                        IF STRLEN(DocNo) = 0 THEN
                                          DocNo := NoSeriesMgt.GetNextNo(GenJnlBatch."No. Series",WORKDATE,FALSE);

                                        "Import Field 4" := DELCHR("Import Field 4",'=<>',DELCHR("Import Field 4",'=<>','0123456789'));
                                        // File format YYYYMMDD according to swedish standard
                                        EVALUATE(DD,COPYSTR("Import Field 4",7,2));
                                        EVALUATE(MM,COPYSTR("Import Field 4",5,2));
                                        EVALUATE(YYYY,COPYSTR("Import Field 4",1,4));
                                        PostingDate := DMY2DATE(DD,MM,YYYY);
                                        Description := COPYSTR("Import Field 5",1,50);
                                      END;
                                    ELSE
                                      EXIT;
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                DialogWindow.CLOSE;
                                DELETEALL;
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   OnActivateForm;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1070002;2;Field     ;
                  CaptionML=[ENU=Gen. Journal Template;
                             NOR=Regnskapsjournalmal;
                             SVE=Redovisningsjournalmall];
                  ToolTipML=[ENU=Specifies the name of the general journal to use during the import process.;
                             SVE=Anger namnet pÜ redovisningsjournalen som ska anvÑndas under importen.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=GenJnlLine."Journal Template Name";
                  TableRelation="Gen. Journal Template";
                  OnValidate=BEGIN
                               GenJnlLine."Journal Batch Name" := '';
                             END;
                              }

      { 1070004;2;Field     ;
                  Lookup=Yes;
                  CaptionML=[ENU=Gen. Journal Batch;
                             NOR=Regnskapsjournal;
                             SVE=Redovisningsjournal];
                  ToolTipML=[ENU=Specifies the name of the general journal batch to use during the import process.;
                             SVE=Anger namnet pÜ redovisningsjournalen som ska anvÑndas under importen.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=GenJnlLine."Journal Batch Name";
                  OnValidate=BEGIN
                               IF GenJnlLine."Journal Batch Name" <> '' THEN BEGIN
                                 GenJnlLine.TESTFIELD("Journal Template Name");
                                 GenJnlBatch.GET(GenJnlLine."Journal Template Name",GenJnlLine."Journal Batch Name");
                               END;
                               ValidateJnl;
                             END;

                  OnLookup=BEGIN
                             GenJnlLine.TESTFIELD("Journal Template Name");
                             GenJnlTemplate.GET(GenJnlLine."Journal Template Name");
                             GenJnlBatch.SETRANGE("Journal Template Name",GenJnlLine."Journal Template Name");
                             GenJnlBatch."Journal Template Name" := GenJnlLine."Journal Template Name";
                             GenJnlBatch.Name := GenJnlLine."Journal Batch Name";
                             IF PAGE.RUNMODAL(0,GenJnlBatch) = ACTION::LookupOK THEN BEGIN
                               GenJnlLine."Journal Batch Name" := GenJnlBatch.Name;
                               ValidateJnl;
                             END;
                           END;
                            }

      { 1070006;2;Field     ;
                  CaptionML=[ENU=Dimensions;
                             NOR=Dimensjoner;
                             SVE=Dimensioner];
                  ToolTipML=[ENU=Specifies the dimensions covered by the import process.;
                             SVE=Anger de dimensioner som omfattas av importen.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=ColumnDim;
                  Editable=FALSE;
                  OnAssistEdit=BEGIN
                                 CLEAR(SieDimensionPage);
                                 SieDimensionPage.LOOKUPMODE(TRUE);
                                 SieDimensionPage.RUN;
                                 ColumnDim := SieDimension.GetDimSelectionText;
                               END;
                                }

      { 1070008;2;Field     ;
                  CaptionML=[ENU=Insert G/L Account;
                             NOR=Sett inn hovedbokskonto;
                             SVE=Infoga redovisningskonto];
                  ToolTipML=[ENU=Specifies whether the general ledger account in the import file is missing in the chart of accounts, and must be set up during the import process.;
                             SVE=Anger om redovisningskontot i importfilen saknas i kontoplanen och mÜste skapas under importprocessen.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=InsertNewAccount }

      { 1070010;2;Field     ;
                  CaptionML=[ENU=Use Number Series for Doc. No.;
                             NOR=Bruk Nummerserie for Dokumentnr.;
                             SVE=AnvÑnd nummerserie fîr dok.nr];
                  ToolTipML=[ENU=Specifies whether to use the number series functionality if document numbers are not provided in the import file.;
                             SVE=Anger om du vill anvÑnda nummerseriefunktionen om dokumentnummer inte anges i importfilen.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=UseSerie }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      SieDimension@1070001 : Record 11128017;
      GenJnlTemplate@1070002 : Record 80;
      GenJnlBatch@1070003 : Record 232;
      GenJnlLine@1070004 : Record 81;
      GLAccount@1070005 : Record 15;
      TempSieBuffer@1070006 : Record 11128018;
      NoSeriesMgt@1001 : Codeunit 396;
      SieDimensionPage@1002 : Page 11128022;
      DialogWindow@1003 : Dialog;
      ImportFile@1000 : File;
      ServerFileName@1070007 : Text;
      ColumnDim@1070008 : Text;
      Description@1070009 : Text[100];
      i@1070011 : Integer;
      i2@1070012 : Integer;
      NextEntryNo@1070013 : Integer;
      Pos@1070014 : Integer;
      Pos1@1070015 : Integer;
      NextLineNo@1070016 : Integer;
      DocNo@1070017 : Code[20];
      Text001@1070028 : TextConst 'ENU=SIE files(*.se)|*.se|All files|*.*;NOR=SIE-filer (*.se)|*.se|Alle filer|*.*;SVE=SIE-filer (*.se)|*.se|Alla filer|*.*';
      Text002@1070029 : TextConst 'ENU=Import SIE File;NOR=Importer SIE-fil;SVE=Importera SIE-fil';
      FileText@1070019 : Text[250];
      Text003@1070030 : TextConst 'ENU=Reading SIE file           @1@@@@@@@@@@\;NOR=Leser SIE-fil           @1@@@@@@@@@@\;SVE=LÑser SIE-fil           @1@@@@@@@@@@\';
      InsertNewAccount@1070020 : Boolean;
      PostingDate@1070022 : Date;
      Text004@1070031 : TextConst 'ENU=Create journal             @2@@@@@@@@@@\;NOR=Opprette journal           @2@@@@@@@@@@\;SVE=Skapa journal           @2@@@@@@@@@@\';
      OK@1070023 : Boolean;
      UseSerie@1070024 : Boolean;
      DD@1070025 : Integer;
      MM@1070026 : Integer;
      YYYY@1070027 : Integer;
      Text005@1004 : TextConst 'ENU=The file was  imported successfully.;NOR=Filen er importert.;SVE=Filen har importerats.';

    LOCAL PROCEDURE ValidateJnl@1070000();
    BEGIN
      GenJnlBatch.GET(GenJnlLine."Journal Template Name",GenJnlLine."Journal Batch Name");
    END;

    PROCEDURE Ansi2Ascii@1070001(AnsiText@1070000 : Text[250]) : Text[250];
    VAR
      AsciiStr@1070006 : Text[30];
      AnsiStr@1070005 : Text[30];
      AE@1070004 : Char;
      UE@1070003 : Char;
      Lilla@1070002 : Char;
      Stora@1070001 : Char;
    BEGIN
      // ADJ0079
      EXIT(AnsiText);
      // 0079
      AsciiStr := 'ÜÑîÅèéôöÇàäÉÖá';
      AE := 196;
      UE := 220;
      Lilla := 229;
      Stora := 197;
      AnsiStr := FORMAT(Lilla) + '‰ˆ¸' + FORMAT(Stora) + FORMAT(AE) + '÷' + FORMAT(UE) + 'ÈÍË‚‡Á';

      EXIT(CONVERTSTR(AnsiText,AnsiStr,AsciiStr));
    END;

    PROCEDURE ClChr@1070002(String@1070000 : Text[250]) : Text[250];
    BEGIN
      String := DELCHR(String,'=','#{}');
      EXIT(String);
    END;

    PROCEDURE GetPos1@1070003(String@1070000 : Text[250]) : Integer;
    BEGIN
      EXIT(STRPOS(COPYSTR(String,1,STRLEN(String)),' ') - 1);
    END;

    PROCEDURE GetNextField@1070004(String@1070000 : Text[250]) : Text[250];
    BEGIN
      FOR i := 1 TO STRLEN(String) DO BEGIN
        IF COPYSTR(String,i,1) <> ' ' THEN BEGIN
          IF COPYSTR(String,i,1) = '"' THEN BEGIN
            FOR i2 := 1 TO STRLEN(String) DO BEGIN
              IF COPYSTR(String,i2 + i,1) = '"' THEN
                EXIT(DELCHR(COPYSTR(String,i,i2 + 1),'<',' '));
            END;
          END
          ELSE BEGIN
            IF COPYSTR(String,i,1) = '{' THEN BEGIN
              FOR i2 := 1 TO STRLEN(String) DO BEGIN
                IF COPYSTR(String,i2 + i,1) = '}' THEN
                  EXIT(DELCHR(COPYSTR(String,i,i2 + 1),'<',' '));
              END;
            END
            ELSE
              FOR i2 := 1 TO STRLEN(String) DO BEGIN
                IF COPYSTR(String,i2 + i,1) = ' ' THEN
                  EXIT(DELCHR(COPYSTR(String,i,i2 + 1),'<',' '));
                IF COPYSTR(String,i2 + i,1) = '{' THEN BEGIN
                  i := i - 1;
                  EXIT(DELCHR(COPYSTR(String,i,i2 + 1),'<',' '));
                END;
              END;
          END;
        END;
      END;
      EXIT(DELCHR(COPYSTR(String,1,STRLEN(String)),'<',' '));
    END;

    PROCEDURE CreateGenJnlLine@1070005();
    BEGIN
      IF DocNo = '' THEN
        DocNo := NoSeriesMgt.GetNextNo(GenJnlBatch."No. Series",WORKDATE,FALSE);

      GenJnlLine.INIT;
      GenJnlLine."Journal Template Name" := GenJnlBatch."Journal Template Name";
      GenJnlLine."Journal Batch Name" := GenJnlBatch.Name;
      GenJnlLine."Line No." := NextLineNo;
      GenJnlLine.INSERT;
      GenJnlLine.SetUpNewLine(GenJnlLine,GenJnlLine."Balance (LCY)",TRUE);
      GenJnlLine.VALIDATE("Account Type",GenJnlLine."Account Type"::"G/L Account");
      GenJnlLine."Posting Date" := PostingDate;
      GenJnlLine."Document Date" := PostingDate;
      GenJnlLine."Document No." := DocNo;
      GenJnlLine."Posting No. Series" := GenJnlBatch."Posting No. Series";
      // PEB0079
      //GenJnlLine.Description := Description;
      // 0079
      GenJnlLine.VALIDATE("Account No.","SIE Import Buffer"."Import Field 2");
      // PEB0079
      GenJnlLine.Description := Description;
      // 0079
      EVALUATE(GenJnlLine.Amount,CONVERTSTR("SIE Import Buffer"."Import Field 4",'.',','));
      GenJnlLine.VALIDATE(Amount);

      GenJnlLine."Gen. Posting Type" := GenJnlLine."Gen. Posting Type"::" ";
      GenJnlLine."Gen. Bus. Posting Group" := ' ';
      GenJnlLine."Gen. Prod. Posting Group" := ' ';
      GenJnlLine."VAT Bus. Posting Group" := ' ';
      GenJnlLine."VAT Prod. Posting Group" := ' ';

      IF STRLEN("SIE Import Buffer"."Import Field 3") > 2 THEN
        GetDimValue(GenJnlLine);

      GenJnlLine.MODIFY;
      NextLineNo += 10000;
    END;

    PROCEDURE GetDimValue@1070006(VAR DimGenJnl@1070000 : Record 81);
    VAR
      TempDimSetEntry@1070005 : TEMPORARY Record 480;
      DimensionValue@1070006 : Record 349;
      DimensionManagement@1070004 : Codeunit 408;
      Dim1@1070001 : Code[20];
      Dim2@1070002 : Code[20];
      SieNumber@1070003 : Integer;
    BEGIN
      Pos := 1;
      WHILE Pos < STRLEN("SIE Import Buffer"."Import Field 3") DO BEGIN
        Dim1 := GetDimValue1("SIE Import Buffer"."Import Field 3",Pos);
        IF Dim1 <> '' THEN
          EVALUATE(SieNumber,Dim1)
        ELSE
          EXIT;

        Pos += 1;
        Dim2 := GetDimValue1("SIE Import Buffer"."Import Field 3",Pos);
        SieDimension.SETRANGE(Selected,TRUE);
        SieDimension.SETRANGE("SIE Dimension",SieNumber);
        IF SieDimension.FIND('-') THEN
          CASE SieDimension.ShortCutDimNo OF
            1:
              GenJnlLine.VALIDATE("Shortcut Dimension 1 Code",Dim2);
            2:
              GenJnlLine.VALIDATE("Shortcut Dimension 2 Code",Dim2);
            3:
              GenJnlLine.ValidateShortcutDimCode(3,Dim2);
            4:
              GenJnlLine.ValidateShortcutDimCode(4,Dim2);
            5:
              GenJnlLine.ValidateShortcutDimCode(5,Dim2);
            6:
              GenJnlLine.ValidateShortcutDimCode(6,Dim2);
            7:
              GenJnlLine.ValidateShortcutDimCode(7,Dim2);
            8:
              GenJnlLine.ValidateShortcutDimCode(8,Dim2)
            ELSE BEGIN
              DimensionManagement.GetDimensionSet(TempDimSetEntry,GenJnlLine."Dimension Set ID");
              TempDimSetEntry.INIT;
              TempDimSetEntry."Dimension Code" := SieDimension."Dimension Code";
              TempDimSetEntry."Dimension Value Code" := Dim2;
              DimensionValue.GET(SieDimension."Dimension Code",Dim2);
              TempDimSetEntry."Dimension Value ID" := DimensionValue."Dimension Value ID";
              TempDimSetEntry.INSERT;
              GenJnlLine."Dimension Set ID" := DimensionManagement.GetDimensionSetID(TempDimSetEntry);
            END;
          END;
      END;
    END;

    PROCEDURE GetDimValue1@1070007(String@1070000 : Text[250];nPos@1070001 : Integer) : Text[20];
    BEGIN
      FOR i := nPos TO STRLEN(String) DO BEGIN
        IF COPYSTR(String,i,1) <> ' ' THEN BEGIN
          IF COPYSTR(String,i,1) = '"' THEN BEGIN
            FOR i2 := 1 TO STRLEN(String) DO BEGIN
              IF COPYSTR(String,i2 + i,1) = '"' THEN BEGIN
                Pos := i2 + 1 + i;
                EXIT(COPYSTR(String,i + 1,i2 - 1));
              END;
            END;
          END
          ELSE BEGIN
            FOR i2 := i TO STRLEN(String) DO BEGIN
              IF (COPYSTR(String,i2,1) = ' ') OR
                 (COPYSTR(String,i2,1) = '"')
              THEN BEGIN
                Pos := i2;
                EXIT(COPYSTR(String,i,i2 - i));
              END;
            END;
            Pos := i2;
            EXIT(COPYSTR(String,i - 1,i2));
          END;
        END;
      END;
      EXIT(' ');
    END;

    LOCAL PROCEDURE OnActivateForm@19002417();
    BEGIN
      ColumnDim := SieDimension.GetDimSelectionText;
    END;

    LOCAL PROCEDURE ReplaceTab@1070008(FileText@1070000 : Text[250]) : Text[250];
    VAR
      OldChar@1070001 : Char;
    BEGIN
      OldChar := 9;
      EXIT(CONVERTSTR(FileText,FORMAT(OldChar),' '));
    END;

    BEGIN
    {
      PEB *** Begin ***
      PEB0079 Miscellaneous adjusts for SIE (Bugs)
        - Fel validerings ordning fîr Beskrivning
        - Ansi 2 Ascii Ñr borttagen. filen skall vara Ascii
      PEB
      PEB *** End ***
      190212 ORANGO.DL upgrade to PE8.08
    }
    END.
  }
  RDLDATA
  {
  }
}

