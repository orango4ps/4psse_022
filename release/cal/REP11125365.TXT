OBJECT Report 11125365 IFC Object - Create Sv.Objects
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=IFC-Objekt - Serviceobjekte erstellen;
               ENU=IFC Object - Create Service Objects;
               NLD=IFC object - Serviceobjecten aanmaken];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   Selected := FALSE;
                 END;

    OnPreReport=BEGIN
                  IF ((CreateFor = CreateFor::Current) OR (CreateFor = CreateFor::Selection) OR (CreateFor = CreateFor::All)) THEN BEGIN
                    ValidateStandardObjectNo;
                    Selected := TRUE;
                  END;
                  IF (CreateFor = CreateFor::Link) THEN
                    Selected := TRUE;
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   IF CurrentLinked THEN
                     CreateFor := CreateFor::Selection;
                   SetEditable;
                   DeterminePropertyActions;
                 END;

      OnQueryClosePage=BEGIN
                         IF (CloseAction = ACTION::OK) AND ((CreateFor = CreateFor::Current) OR (CreateFor = CreateFor::Selection) OR (CreateFor = CreateFor::All)) THEN
                           ValidateStandardObjectNo;
                       END;

    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100525007;2;Field  ;
                  CaptionML=[DEU=Erstellen fÅr;
                             ENU=Create for;
                             NLD=Aanmaken voor];
                  OptionCaptionML=[DEU=Aktuell,Selektion,Verbinden,Alle;
                                   ENU=Current,Selection,Link,All;
                                   NLD=Huidige,Selectie,Koppelen,Alle];
                  SourceExpr=CreateFor;
                  Editable=CreateForEditable;
                  OnValidate=BEGIN
                               SetEditable;
                               IF CreateFor = CreateFor::All THEN BEGIN
                                 ObjectIfcType := '';
                                 PartOfIfcName := '';
                               END;
                             END;
                              }

      { 1100525001;2;Field  ;
                  CaptionML=[DEU=Standardobjekt;
                             ENU=Standard Object No.;
                             NLD=Standaard object;
                             NOR=Standardobjektnr;
                             SVE=Standardobjektsnr];
                  NotBlank=Yes;
                  SourceExpr=StandardObjectNo;
                  Editable=DefaultFieldsEditable;
                  OnValidate=BEGIN
                               ValidateStandardObjectNo;
                             END;

                  OnLookup=VAR
                             StandardObject@1100525000 : Record 11020334;
                           BEGIN
                             StandardObject.SETRANGE(Expired, FALSE);
                             StandardObject."No." := StandardObjectNo;
                             IF PAGE.RUNMODAL(0, StandardObject) = ACTION::LookupOK THEN BEGIN
                               IF StandardObject."No." <> '' THEN BEGIN
                                 StandardObjectNo := StandardObject."No.";
                                 ValidateStandardObjectNo;
                               END;
                             END;
                           END;
                            }

      { 1100525003;2;Field  ;
                  CaptionML=[DEU=(Teil des) Name(ns);
                             ENU=(Part of) Name;
                             NLD=(Deel van) naam];
                  SourceExpr=PartOfIfcName;
                  Editable=SelectionFieldsEditable }

      { 1100529002;2;Field  ;
                  CaptionML=[DEU=Eigenschaftenaktionen;
                             ENU=Property Actions;
                             NLD=Eigenschap acties];
                  OptionCaptionML=[DEU=Keine,Kopieren,Filtern,Kopieren und filtern;
                                   ENU=None,Copy,Filter,Copy & Filter;
                                   NLD=Geen,Kopiâren,Filteren,Kopiâren & filteren];
                  SourceExpr=PropertyActions;
                  Editable=PropertyActionsEditable;
                  OnAssistEdit=BEGIN
                                 CopyBaseIfcPropertyForServiceObject(TmpIfcObjectProperty);
                                 TmpIfcObjectProperty.Bool4 := TRUE;
                                 TmpIfcObjectProperty.Bool5 := (CreateFor <> CreateFor::Current);
                                 TmpIfcObjectProperty.Int1 := DATABASE::"Service-Object";
                                 PAGE.RUNMODAL(PAGE::"IFC Object Properties", TmpIfcObjectProperty);
                                 DeterminePropertyActions;
                               END;
                                }

      { 1100529400;2;Field  ;
                  CaptionML=[DEU=Bildpositionsfilter;
                             ENU=Picture Position Filter;
                             NLD=Afbeeldingpositiefilter];
                  SourceExpr=PicturePositionFilterCaption;
                  Editable=FALSE;
                  HideValue=PicturePositionFilterHideValue;
                  OnAssistEdit=VAR
                                 SelectPicturePositions@1100529400 : Page 11130594;
                               BEGIN
                                 IF PicturePositionFilterHideValue THEN
                                   EXIT;
                                 SelectPicturePositions.SetSelection(PicturePositions);
                                 IF SelectPicturePositions.RUNMODAL = ACTION::OK THEN BEGIN
                                   SelectPicturePositions.GetSelection(PicturePositions);
                                   PicturePositionFilterCaption := SelectPicturePositions.CreatePicturePositionFilterCaption(PicturePositions);
                                   IF STRPOS(PicturePositions, '1') = 0 THEN
                                     EXIT;
                                 END;
                               END;
                                }

      { 1100525005;1;Group  ;
                  CaptionML=[DEU=Aktuell;
                             ENU=Current;
                             NLD=Huidig;
                             SVE=Lîpande (innevarande)];
                  Editable=true;
                  GroupType=Group }

      { 1100525004;2;Field  ;
                  CaptionML=[DEU=IFC-Typ;
                             ENU=IFC Type;
                             NLD=IFC type];
                  SourceExpr=ObjectIfcType;
                  Editable=ObjectIfcTypeEditable;
                  QuickEntry=true }

      { 1100525006;2;Field  ;
                  Lookup=No;
                  CaptionML=[DEU=Servicestandort;
                             ENU=Service Location No.;
                             NLD=Servicelocatie;
                             NOR=Servicelokasjon Nr.;
                             SVE=Serviceplatsnr];
                  SourceExpr=ServiceLocationNo;
                  TableRelation="Service Location";
                  Editable=FALSE;
                  QuickEntry=FALSE }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TmpIfcObjectProperty@1100529004 : TEMPORARY Record 11020579;
      StandardObjectNo@1100525000 : Code[20];
      ServiceLocationNo@1100525004 : Code[20];
      ObjectIfcType@1100525002 : Text;
      PartOfIfcName@1100525003 : Text;
      CurrentLinked@1100529000 : Boolean;
      Selected@1100525001 : Boolean;
      CreateForEditable@1100529001 : Boolean INDATASET;
      DefaultFieldsEditable@1100529002 : Boolean INDATASET;
      SelectionFieldsEditable@1100525006 : Boolean INDATASET;
      PropertyActionsEditable@1100529010 : Boolean INDATASET;
      PicturePositionFilterHideValue@1100529402 : Boolean INDATASET;
      CreateFor@1100525005 : 'Current,Selection,Link,All';
      Text001@1100529003 : TextConst 'DEU=Kein %1 ausgewÑhlt;ENU=No %1 selected;NLD=Geen %1 geselecteerd';
      PropertyActions@1100529009 : 'None,Copy,Filter,Copy&Filter';
      PicturePositions@1100529401 : Code[10];
      PicturePositionFilterCaption@1100529400 : Text;
      ObjectIfcTypeEditable@1100527350 : Boolean INDATASET;

    PROCEDURE SetSelection@1100525000(IfcType@1100525001 : Text;IfcName@1100525000 : Text;ServiceLocationNo2@1100529000 : Code[20];CurrentLinked2@1100529001 : Boolean;VAR TmpIfcObjectProperty2@1100529002 : TEMPORARY Record 11020579);
    BEGIN
      IF CreateFor = CreateFor::All THEN BEGIN
        ObjectIfcType := '';
        PartOfIfcName := '';
      END ELSE BEGIN
        ObjectIfcType := IfcType;
        PartOfIfcName := IfcName;
      END;
      ServiceLocationNo := ServiceLocationNo2;
      CurrentLinked := CurrentLinked2;
      TmpIfcObjectProperty.COPY(TmpIfcObjectProperty2, TRUE);
    END;

    PROCEDURE GetSelection@1100525001(VAR CreateFor2@1100525000 : Integer;VAR PartOfName@1100525001 : Text;VAR StandardObjectNo2@1100525002 : Code[20];VAR PropertyName2@1100529001 : Text;VAR PropertyValue2@1100529000 : Text;VAR PicturePositions2@1100529400 : Code[10]);
    BEGIN
      CreateFor2 := 0;
      PartOfName := '';
      StandardObjectNo2 := '';
      PropertyName2 := '';
      PropertyValue2 := '';
      PicturePositions2 := PicturePositions;
      IF Selected THEN BEGIN
        CreateFor2 := CreateFor + 1;
        PartOfName := PartOfIfcName;
        StandardObjectNo2 := StandardObjectNo;
        IF CreateFor = CreateFor::Current THEN BEGIN
          TmpIfcObjectProperty.RESET;
          TmpIfcObjectProperty.MODIFYALL(Bool2, FALSE);  //Tmp table contains all properties of current IFC object, so no further filtering needed
        END ELSE BEGIN
          TmpIfcObjectProperty.RESET;
          TmpIfcObjectProperty.SETRANGE(Bool2, TRUE);
          TmpIfcObjectProperty.SETFILTER(Text5, '<>%1', '');
          IF TmpIfcObjectProperty.FINDFIRST THEN BEGIN
            PropertyName2 := TmpIfcObjectProperty.Text1;
            PropertyValue2 := TmpIfcObjectProperty.Text5;
          END ELSE BEGIN
            TmpIfcObjectProperty.SETRANGE(Text5);
            IF TmpIfcObjectProperty.FINDFIRST THEN
              PropertyName2 := TmpIfcObjectProperty.Text1;
          END;
          TmpIfcObjectProperty.RESET;
        END;
      END;
    END;

    LOCAL PROCEDURE ValidateStandardObjectNo@1100525002();
    VAR
      StandardObject@1100525000 : Record 11020334;
    BEGIN
      IF (StandardObjectNo = '') THEN
        ERROR(Text001, StandardObject.TABLECAPTION);
      StandardObject.GET(StandardObjectNo);
      StandardObject.TESTFIELD(Expired, FALSE);
    END;

    LOCAL PROCEDURE SetEditable@1100525003();
    BEGIN
      CreateForEditable := (NOT CurrentLinked);
      SelectionFieldsEditable := ((CreateFor = CreateFor::Selection) OR (CreateFor = CreateFor::Link) OR (CreateFor = CreateFor::All));
      DefaultFieldsEditable := ((CreateFor = CreateFor::Current) OR (CreateFor = CreateFor::Selection) OR (CreateFor = CreateFor::All));
      PropertyActionsEditable := FALSE; // Only editable via lookup if SelectionFieldsEditable
      PicturePositionFilterHideValue := (CreateFor = CreateFor::Link);
      ObjectIfcTypeEditable := (CreateFor = CreateFor::All);
    END;

    LOCAL PROCEDURE DeterminePropertyActions@1100529000();
    VAR
      TmpProperty@1100529002 : TEMPORARY Record 11020579;
      CopySelected@1100529001 : Boolean;
      FilterSelected@1100529000 : Boolean;
    BEGIN
      TmpProperty.COPY(TmpIfcObjectProperty, TRUE);
      TmpProperty.RESET;
      TmpProperty.SETRANGE(Bool1, TRUE);
      CopySelected := (NOT TmpProperty.ISEMPTY);
      TmpProperty.SETRANGE(Bool1);
      TmpProperty.SETRANGE(Bool2, TRUE);
      FilterSelected := (NOT TmpProperty.ISEMPTY);
      TmpProperty.SETRANGE(Bool2);
      PropertyActions := PropertyActions::None;
      IF CopySelected AND FilterSelected THEN
        PropertyActions := PropertyActions::"Copy&Filter"
      ELSE BEGIN
        IF CopySelected THEN
          PropertyActions := PropertyActions::Copy;
        IF FilterSelected THEN
          PropertyActions := PropertyActions::Filter;
      END;
    END;

    PROCEDURE GetIfcObjectTypeForAll@1100527350(VAR IfcObjectType2@1100527350 : Text[50]);
    BEGIN
      IF CreateFor = CreateFor::All THEN
        IfcObjectType2 :=  ObjectIfcType;
    END;

    LOCAL PROCEDURE CopyBaseIfcPropertyForServiceObject@1100527352(VAR TmpIfcObjectProperty@1100527351 : TEMPORARY Record 11020579);
    VAR
      BaseIfcPropertyForServiceObject@1100527350 : Record 11125828;
      TmpIfcObjectProperty2@1100527352 : TEMPORARY Record 11020579;
      KeyCode@1100527353 : Text[10];
    BEGIN
      TmpIfcObjectProperty2.COPY(TmpIfcObjectProperty, TRUE);
      BaseIfcPropertyForServiceObject.SETRANGE("Table No.", 11071691);
      IF BaseIfcPropertyForServiceObject.FINDSET THEN BEGIN
        REPEAT
          TmpIfcObjectProperty2.RESET;
          TmpIfcObjectProperty2.SETRANGE(Text1, BaseIfcPropertyForServiceObject.Property);
          IF NOT TmpIfcObjectProperty2.FINDFIRST THEN BEGIN
            KeyCode := FORMAT(TmpIfcObjectProperty.COUNT + 1);
            TmpIfcObjectProperty.INIT;
            TmpIfcObjectProperty."Key Code1" := COPYSTR('0000000000', 1, 10-STRLEN(TmpIfcObjectProperty."Key Code1")) + KeyCode;
            TmpIfcObjectProperty.Text1 := BaseIfcPropertyForServiceObject.Property;
            TmpIfcObjectProperty.Bool1 := TRUE;
            TmpIfcObjectProperty.Bool2 := FALSE;
            TmpIfcObjectProperty.Int1 := 11071691;
            TmpIfcObjectProperty.Int2 := 1;
            TmpIfcObjectProperty.INSERT;
            END;
          UNTIL BaseIfcPropertyForServiceObject.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

