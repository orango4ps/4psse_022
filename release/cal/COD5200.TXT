OBJECT Codeunit 5200 Employee/Resource Update
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    Permissions=TableData 156=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Res@1000 : Record 156;

    [External]
    PROCEDURE HumanResToRes@1(OldEmployee@1000 : Record 5200;Employee@1001 : Record 5200);
    BEGIN
      IF (Employee."Resource No." <> '') AND IsResourceUpdateNeeded(OldEmployee,Employee) THEN
        ResUpdate(Employee)
      ELSE
        EXIT;
    END;

    [External]
    PROCEDURE ResUpdate@4(Employee@1000 : Record 5200);
    BEGIN
      Res.GET(Employee."Resource No.");
      Res."Job Title" := Employee."Job Title";
      Res.Name := COPYSTR(Employee.FullName,1,MAXSTRLEN(Res.Name));
      Res.Address := Employee.Address;
      Res."Address 2" := Employee."Address 2";
      Res."Post Code" := Employee."Post Code";
      Res.County := Employee.County;
      Res.City := Employee.City;
      Res."Country/Region Code" := Employee."Country/Region Code";
      Res."Social Security No." := Employee."Social Security No.";
      Res."Employment Date" := Employee."Employment Date";
      OnAfterUpdateResource(Res);
      Res.MODIFY(TRUE)
    END;

    LOCAL PROCEDURE IsResourceUpdateNeeded@9(OldEmployee@1002 : Record 5200;Employee@1001 : Record 5200) : Boolean;
    VAR
      UpdateNeeded@1000 : Boolean;
    BEGIN
      UpdateNeeded :=
        (OldEmployee."Resource No." <> Employee."Resource No.") OR
        (OldEmployee."Job Title" <> Employee."Job Title") OR
        (OldEmployee."First Name" <> Employee."First Name") OR
        (OldEmployee."Last Name" <> Employee."Last Name") OR
        (OldEmployee.Address <> Employee.Address) OR
        (OldEmployee."Address 2" <> Employee."Address 2") OR
        (OldEmployee."Post Code" <> Employee."Post Code") OR
        (OldEmployee.County <> Employee.County) OR
        (OldEmployee.City <> Employee.City) OR
        (OldEmployee."Country/Region Code" <> Employee."Country/Region Code") OR
        (OldEmployee."Social Security No." <> Employee."Social Security No.") OR
        (OldEmployee."Employment Date" <> Employee."Employment Date");

      OnAfterCalculateResourceUpdateNeeded(Employee,OldEmployee,UpdateNeeded);

      EXIT(UpdateNeeded);
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterUpdateResource@5(VAR Resource@1000 : Record 156);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCalculateResourceUpdateNeeded@49(Employee@1000 : Record 5200;xEmployee@1001 : Record 5200;VAR UpdateNeeded@1002 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

