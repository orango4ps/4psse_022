OBJECT XMLport 11012276 Import RSU
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Import RSU file;
               SVE=Importera RSU-fil];
    Direction=Import;
    DefaultFieldsValidation=No;
    TextEncoding=WINDOWS;
    OnPreXMLport=VAR
                   lvFile@1100525500 : File;
                   TempFileName@1100525501 : Text[1024];
                 BEGIN
                   LastUpdateTime := TIME;
                   Window.OPEN(Text000);

                   TempFileName := FileMgt.UploadFileSilent(currXMLport.FILENAME);

                   IF lvFile.OPEN(TempFileName) THEN BEGIN
                     //95 is the length of tender lines in the rsu file, because the other lines have another length
                     //the calculation is not very exactly, but for big tenders it can reasonable calculated.
                     NoOfRecords :=  ROUND(lvFile.LEN /95, 1);
                     lvFile.CLOSE;
                   END ELSE BEGIN
                     NoOfRecords := 10;
                   END;

                   ERASE(TempFileName);
                 END;

    OnPostXMLport=VAR
                    lvPrinRec@1100525501 : Record 11012005;
                    lvRAWCU@1100525500 : Codeunit 11012164;
                  BEGIN
                    lvPrinRec.GET(gProjectNo,gPrincipal);
                    lvRAWCU.CalculateAKPerc(gProjectNo,gPrincipal,lvPrinRec."Surcharge %");
                    lvPrinRec.MODIFY;
                  END;

    Format=Fixed Text;
    UseRequestPage=No;
  }
  ELEMENTS
  {
    { [{B1F83BB3-9709-4C47-9C93-EBEAE6DE5DE8}];  ;Root                ;Element ;Text     }

    { [{311939BE-7B5C-436B-8CDF-BDFE571A506F}];1 ;Integer             ;Element ;Table   ;
                                                  SourceTable=Table2000000026;
                                                  SourceTableView=SORTING(Field1)
                                                                  WHERE(Field1=FILTER(1..));
                                                  Import::OnAfterInitRecord=BEGIN
                                                                              CLEAR(Line);
                                                                            END;

                                                  Import::OnBeforeInsertRecord=VAR
                                                                                 lvTempDec@1100525505 : Decimal;
                                                                                 lvLineNo@1100525504 : Integer;
                                                                                 lvDeficode@1100525503 : Integer;
                                                                                 lvTendElem@1100525501 : Code[20];
                                                                                 lvSkip@1100525500 : Boolean;
                                                                               BEGIN
                                                                                 Counter := Counter + 1;
                                                                                 UpdateProgressWindow(Counter);
                                                                                 IF COPYSTR(Line, 1, 2) = '23' THEN BEGIN
                                                                                   WITH TendElemRec DO BEGIN
                                                                                     lvSkip := FALSE;
                                                                                     INIT;
                                                                                     "Project No." := gProjectNo;
                                                                                     Principal := gPrincipal;
                                                                                     "Contract No." := gContractNo;
                                                                                     lvTendElem := DELCHR(COPYSTR(Line,3,6),'<>');
                                                                                     IF lvTendElem = '919990' THEN
                                                                                       lvSkip := TRUE;
                                                                                     IF NOT lvSkip THEN BEGIN
                                                                                       VALIDATE("No.", lvTendElem);
                                                                                       IF STRLEN("No.") = 6 THEN BEGIN
                                                                                         IF EVALUATE(lvDeficode, COPYSTR(Line,9,2)) THEN;
                                                                                         IF (COPYSTR("No.",6,1) <> '0') OR (lvDeficode <> 0) THEN
                                                                                           lvLineNo := 99
                                                                                   //this only indicated that the text should be added to the latest tender entry that ends with zero.
                                                                                         ELSE
                                                                                           IF EVALUATE(lvLineNo,COPYSTR(Line,11,2)) THEN;
                                                                                       END;
                                                                                       CASE lvLineNo OF
                                                                                         0:        //line 0 contains the res. commitment for fixed summary amount.
                                                                                           BEGIN
                                                                                             gLastTenderElem := "No.";
                                                                                             Description := DELCHR(COPYSTR(Line,15,50),'<>');
                                                                                             "Unit of Measure" := COPYSTR(Line,74,4);
                                                                                             IF COPYSTR(Line,78,9) <> '         ' THEN BEGIN
                                                                                               IF EVALUATE(Quantity,COPYSTR(Line,78,7)) THEN;
                                                                                               IF EVALUATE(lvTempDec,COPYSTR(Line,85,2)) THEN;
                                                                                               Quantity += lvTempDec/100;
                                                                                             END;
                                                                                             CASE COPYSTR(Line,87,1) OF
                                                                                               '  ':
                                                                                                 CLEAR("Settlement Type");
                                                                                               '0':
                                                                                                 "Settlement Type" := "Settlement Type"::Settling;
                                                                                               '1':
                                                                                                 "Settlement Type" := "Settlement Type"::"Non Settling";
                                                                                               '2':
                                                                                                 "Settlement Type" := "Settlement Type"::Reduction;
                                                                                               '3':
                                                                                                 "Settlement Type" := "Settlement Type"::Open;
                                                                                               '4':
                                                                                                 "Settlement Type" := "Settlement Type"::Approved;
                                                                                             END;
                                                                                             IF INSERT(TRUE) THEN;
                                                                                           END;
                                                                                         1:        //line 1 contains the result commitment for other entries
                                                                                           BEGIN
                                                                                             SETRANGE("Project No.", gProjectNo);
                                                                                             SETRANGE(Principal, gPrincipal);
                                                                                             SETRANGE("Contract No.", gContractNo);
                                                                                             SETRANGE("No.", DELCHR(COPYSTR(Line,3,6),'<>'));
                                                                                             IF FINDFIRST THEN BEGIN
                                                                                               "Unit of Measure" := COPYSTR(Line,74,4);
                                                                                               IF COPYSTR(Line,78,9) <> '         ' THEN BEGIN
                                                                                                 IF EVALUATE(Quantity,COPYSTR(Line,78,7)) THEN;
                                                                                                 IF EVALUATE(lvTempDec,COPYSTR(Line,85,2)) THEN;
                                                                                                 Quantity += lvTempDec/100;
                                                                                               END;
                                                                                               MODIFY(TRUE);
                                                                                             END;
                                                                                             WriteTextLine(gLastTenderElem);
                                                                                           END;
                                                                                         ELSE      //next lines will be imported as text
                                                                                           BEGIN
                                                                                             WriteTextLine(gLastTenderElem);
                                                                                           END;
                                                                                       END;
                                                                                     END;
                                                                                   END;
                                                                                 END;

                                                                                 currXMLport.SKIP;
                                                                               END;
                                                                                }

    { [{8B08D851-F35F-46E6-9BB3-2D09295B833B}];2 ;Line                ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=100 }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      TendElemRec@1100525511 : Record 11012470;
      TendElemTextRec@1100525510 : Record 11012475;
      FileMgt@1100525515 : Codeunit 419;
      gProjectNo@1100525509 : Code[20];
      gPrincipal@1100525508 : Code[20];
      gContractNo@1100525507 : Code[10];
      gLastTenderElem@1100525506 : Text[20];
      Window@1100525504 : Dialog;
      Progress@1100525503 : Integer;
      NoOfRecords@1100525502 : Integer;
      Counter@1100525501 : Integer;
      LastUpdateTime@1100525500 : Time;
      Text000@1100525514 : TextConst 'ENU=Import RSU-file @1@@@@@@@@@@@@@@@@@@@@@@\line #2#####;SVE=Importera RSU-fil @1@@@@@@@@@@@@@@@@@@@@@@\rad #2#####';

    PROCEDURE SetSelections@1100485000(IProjNo@1100485000 : Code[20];IPrincipal@1100485001 : Code[20];IContract@1100485003 : Code[10]);
    VAR
      ProjRec@1210190000 : Record 11072003;
    BEGIN
      BEGIN
        gProjectNo := IProjNo;
        ProjRec.GET(gProjectNo);
        ProjRec.TESTFIELD(RAW);
        gPrincipal := IPrincipal;
        gContractNo := IContract;
      END;
    END;

    PROCEDURE WriteTextLine@1100485001(iLastTenderElem@1100485001 : Text[20]);
    VAR
      lvText@1100485000 : Text[60];
    BEGIN
      lvText := FORMAT(TendElemRec."No.", 6) + ' ' + COPYSTR(Line,15,50);
      TendElemTextRec.INIT;
      TendElemTextRec.SETRANGE("Project No.", gProjectNo);
      TendElemTextRec.SETRANGE(Principal, gPrincipal);
      TendElemTextRec.SETRANGE("Contract No.", gContractNo);
      TendElemTextRec.SETRANGE("Tender Paragraph", iLastTenderElem);
      IF TendElemTextRec.FINDLAST THEN
        TendElemTextRec."Text Line No." += 10000
      ELSE
        TendElemTextRec."Text Line No." := 10000;
      TendElemTextRec."Project No." := gProjectNo;
      TendElemTextRec.Principal := gPrincipal;
      TendElemTextRec."Contract No." := gContractNo;
      TendElemTextRec."Tender Paragraph" := iLastTenderElem;
      TendElemTextRec.Text := lvText;
      TendElemTextRec.INSERT(TRUE);
    END;

    PROCEDURE UpdateProgressWindow@9(NewProgress@11012000 : Integer);
    BEGIN
      IF NoOfRecords > 0 THEN BEGIN
        Window.UPDATE(2,NewProgress);
        NewProgress := ROUND(NewProgress / NoOfRecords * 100,1);
        IF (NewProgress <> Progress) OR (TIME > LastUpdateTime + 5000) THEN BEGIN
          Window.UPDATE(1,100 * NewProgress);
          LastUpdateTime := TIME;
        END;
        Progress := NewProgress;
      END;
    END;

    BEGIN
    {
      CC: Dataport 11012276
      db, 29-05-17: ascii-interface (ZSU/RSU) expired since 01-01-2011

      Call with:
      ImpRSU.SetSelections("Project No.", Principal, "Contract No.");
      ImpRSU.RUN;
      CurrPage.UPDATE;
    }
    END.
  }
}

