OBJECT Codeunit 409 Dimension Value-Indent
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    TableNo=349;
    OnRun=BEGIN
            IF NOT
               CONFIRM(
                 STRSUBSTNO(
                   Text000 +
                   Text001 +
                   Text002 +
                   Text003,"Dimension Code"),TRUE)
            THEN
              EXIT;

            DimVal.SETRANGE("Dimension Code","Dimension Code");
            Indent;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU="This function updates the indentation of all the dimension values for dimension %1. ";NOR="Denne funksjonen oppdaterer innrykkingen av alle dimensjonsverdiene for dimensjon %1. ";SVE="Funktionen uppdaterar indenteringen p† alla dimensionsv„rden f”r dimension %1. "';
      Text001@1001 : TextConst 'ENU="All dimension values between a Begin-Total and the matching End-Total are indented by one level. ";NOR="Alle dimensjonsverdiene mellom en Fra-sum og tilh›rende Til-sum rykkes inn med ett niv†. ";SVE="Alla dimensionsv„rden mellan fr†n-summor och de korresponderande till-summorna „r indragna med en niv†. "';
      Text002@1002 : TextConst 'ENU=The Totaling field for each End-Total is also updated.\\;NOR=Sammentellingsfeltet for hver Til-sum oppdateres ogs†.\\;SVE=Summeringsf„lten f”r varje Till-summa „r ocks† uppdaterad.\\';
      Text003@1003 : TextConst 'ENU=Do you want to indent the dimension values?;NOR=Vil du rykke inn dimensjonsverdiene?;SVE=Vill du g”ra en indragning av dimensionsv„rdena?';
      Text004@1004 : TextConst 'ENU=Indenting Dimension Values @1@@@@@@@@@@@@@@@@@@;NOR=Rykker inn dimensjonsverdi @1@@@@@@@@@@@@@@@@@@;SVE=Indragning av dim.v„rden   @1@@@@@@@@@@@@@@@@@@';
      Text005@1005 : TextConst 'ENU=End-Total %1 is missing a matching Begin-Total.;NOR=Til-sum %1 mangler en tilh›rende Fra-sum.;SVE=Till-summa %1 saknar en korresponderande Fr†n-summa.';
      ArrayExceededErr@1010 : TextConst '@@@="%1 = A number bigger than 1";ENU=You can only indent %1 levels for dimension values of the type Begin-Total.;NOR=Du kan bare rykke inn %1 niv†er for dimensjonsverdier av typen Fra-sum.';
      DimVal@1006 : Record 349;
      Window@1007 : Dialog;
      DimValCode@1008 : ARRAY [10] OF Code[20];
      i@1009 : Integer;

    [External]
    PROCEDURE Indent@1();
    VAR
      NoOfDimVals@1000 : Integer;
      Progress@1001 : Integer;
    BEGIN
      Window.OPEN(Text004);

      NoOfDimVals := DimVal.COUNT;
      IF NoOfDimVals = 0 THEN
        NoOfDimVals := 1;
      WITH DimVal DO
        IF FIND('-') THEN
          REPEAT
            Progress := Progress + 1;
            Window.UPDATE(1,10000 * Progress DIV NoOfDimVals);

            IF "Dimension Value Type" = "Dimension Value Type"::"End-Total" THEN BEGIN
              IF i < 1 THEN
                ERROR(
                  Text005,
                  Code);
              Totaling := DimValCode[i] + '..' + Code;
              i := i - 1;
            END;

            Indentation := i;
            MODIFY;

            IF "Dimension Value Type" = "Dimension Value Type"::"Begin-Total" THEN BEGIN
              i := i + 1;
              IF i > ARRAYLEN(DimValCode) THEN
                ERROR(ArrayExceededErr,ARRAYLEN(DimValCode));
              DimValCode[i] := Code;
            END;
          UNTIL NEXT = 0;

      Window.CLOSE;
    END;

    BEGIN
    END.
  }
}

