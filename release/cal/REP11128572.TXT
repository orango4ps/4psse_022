OBJECT Report 11128572 Create DK Elec. Reminders
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVDK10.00 (13602);
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create Electronic Reminders;
               NOR=Opprette elektroniske betalingsp†minnelser;
               SVE=Skapa elektroniska betalningsp†minnelser];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF NOT CurrReport.USEREQUESTPAGE THEN
                    InitLogInteraction;
                END;

  }
  DATASET
  {
    { 5612;    ;DataItem;                    ;
               DataItemTable=Table297;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=VAR
                               IssuedReminderHeader@1101100000 : Record 297;
                             BEGIN
                               Counter := 0;

                               // Any electronic reminders?
                               IssuedReminderHeader.COPY("Issued Reminder Header");
                               IssuedReminderHeader.FILTERGROUP(8);
                               IssuedReminderHeader.SETFILTER("EAN No.",'<>%1','');
                               IF NOT IssuedReminderHeader.FINDFIRST THEN
                                 ERROR(Text003);

                               // All electronic reminders?
                               IssuedReminderHeader.SETRANGE("EAN No.",'');
                               IF IssuedReminderHeader.FINDFIRST THEN
                                 IF NOT CONFIRM(Text000,TRUE) THEN
                                   CurrReport.QUIT;
                               IssuedReminderHeader.SETRANGE("EAN No.");

                               // Some already sent?
                               IssuedReminderHeader.SETRANGE("Electronic Reminder Created",TRUE);
                               IF IssuedReminderHeader.FINDFIRST THEN
                                 IF NOT CONFIRM(Text001,TRUE) THEN
                                   CurrReport.QUIT;

                               SETFILTER("EAN No.",'<>%1','');
                             END;

               OnAfterGetRecord=BEGIN
                                  CODEUNIT.RUN(CODEUNIT::"OIOUBL Export Issued Reminder","Issued Reminder Header");

                                  IF LogInteraction THEN
                                    SegManagement.LogDocument(
                                      8,"No.",0,0,DATABASE::Customer,"Customer No.",'','',"Posting Description",'');

                                  COMMIT;
                                  Counter := Counter + 1;
                                END;

               OnPostDataItem=BEGIN
                                MESSAGE(Text002, Counter);
                              END;

               ReqFilterFields=No.,Customer No.,EAN No.,Electronic Reminder Created }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnInit=BEGIN
               LogInteractionEnable := TRUE;
             END;

      OnOpenPage=BEGIN
                   InitLogInteraction;
                   LogInteractionEnable := LogInteraction;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1101100001;2;Field  ;
                  Name=LogInteraction;
                  CaptionML=[ENU=Log Interaction;
                             NOR=Logg Interaksjon;
                             SVE=Logga interaktion];
                  ToolTipML=ENU=Specifies if you want to record the related interactions with the involved contact person in the Interaction Log Entry table.;
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=LogInteraction;
                  Enabled=LogInteractionEnable }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      SegManagement@1101100001 : Codeunit 5051;
      Counter@1101100002 : Integer;
      LogInteraction@1101100000 : Boolean;
      Text000@1101100006 : TextConst 'ENU=One or more issued reminders that match your filter criteria are not electronic reminders and will be skipped.\\Do you want to continue?;NOR=En eller flere p†minnelser som passer i ditt utvalg er ikke elektroniske p†minnelser og hoppes over.\\Vil du fortsette?;SVE=En eller flera p†minnelser som passar i ditt urval „r inte elektroniska p†minnelser och hoppas ”ver.\\Vill du forts„tta?';
      Text001@1101100005 : TextConst 'ENU=One or more electronic reminders that match your filter criteria have been created before.\\Do you want to continue?;NOR=En eller flere p†minnelser som passer i ditt utvalg har blitt opprettet tidligere.\\Vil du fortsette?;SVE=En eller flera p†minnelser som passar i ditt urval har skapats tidigare.\\Vill du forts„tta?';
      Text002@1101100004 : TextConst 'ENU=Successfully created %1 electronic reminders.;NOR=Opprettet %1 elektroniske betalingsp†minnelser;SVE=Skapade %1 elektroniska betalningsp†minnelser';
      Text003@1101100003 : TextConst 'ENU=There is nothing to create.;NOR=Det finnes ingenting † opprette.;SVE=Det finns inget att skapa.';
      LogInteractionEnable@19003940 : Boolean INDATASET;

    PROCEDURE InitLogInteraction@1101100002();
    BEGIN
      LogInteraction := SegManagement.FindInteractTmplCode(8) <> '';
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

