OBJECT Table 11126036 Project Result Forecast
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Project No.,Date,Description;
    OnInsert=BEGIN
               Fixed := FALSE; //kzwerver, 130104, n, #C001457
               "Select for Report" := TRUE; //#C021037
             END;

    OnModify=BEGIN
               IF (Fixed = xRec.Fixed) AND (Fixed) THEN
                 TESTFIELD(Fixed, FALSE); //kzwerver, 130104, n, #C001457
             END;

    OnDelete=BEGIN
               TESTFIELD(Fixed, FALSE); //kzwerver, 130104, n, #C001457
               DeleteLines;
             END;

    CaptionML=[ENU=Project Result Forecast;
               NOR=Prognose for prosjektresultat;
               SVE=Prognos f”r projektresultat];
    LookupPageID=Page11126037;
    DrillDownPageID=Page11126037;
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation=Job.No.;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Project Description");
                                                              END;

                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr];
                                                   NotBlank=Yes }
    { 20  ;   ;Date                ;Date          ;OnValidate=BEGIN
                                                                "Previous Forecast Date Filter" := '<' + FORMAT(Date);
                                                              END;

                                                   CaptionML=[ENU=Date;
                                                              NOR=Dato;
                                                              SVE=Datum];
                                                   NotBlank=Yes }
    { 30  ;   ;Project Description ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Job.Description WHERE (No.=FIELD(Project No.)));
                                                   CaptionML=[ENU=Project Description;
                                                              NOR=Prosjektbeskrivelse;
                                                              SVE=Projektbeskrivning];
                                                   Editable=No }
    { 40  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 50  ;   ;Previous Forecast Date Filter;Text30;
                                                   CaptionML=[ENU=Previous Forecast Date Filter;
                                                              NOR=Forrige prognosedatofilter;
                                                              SVE=F”reg†ende prognosdatumfilter];
                                                   Editable=No }
    { 60  ;   ;Previous Forecast Date;Date        ;FieldClass=FlowField;
                                                   CalcFormula=Max("Project Result Forecast".Date WHERE (Project No.=FIELD(Project No.),
                                                                                                         Date=FIELD(UPPERLIMIT(FILTER(Previous Forecast Date Filter)))));
                                                   CaptionML=[ENU=Previous Forecast Date;
                                                              NOR=Forrige prognosedato;
                                                              SVE=F”reg†ende prognosdatum];
                                                   Editable=No }
    { 70  ;   ;Actual Result Forecast;Decimal     ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Project Result Forecast Line"."Project Result Forecast" WHERE (Project No.=FIELD(Project No.),
                                                                                                                                   Date=FIELD(Date),
                                                                                                                                   Summary=CONST(No)));
                                                   CaptionML=[ENU=Actual Result Forecast;
                                                              NOR=Faktisk resultatprognose;
                                                              SVE=Faktisk resultatprognos];
                                                   Editable=No }
    { 80  ;   ;Total Purchase Budget;Decimal      ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Project Result Forecast Line"."Purchase Budget" WHERE (Project No.=FIELD(Project No.),
                                                                                                                           Date=FIELD(Date),
                                                                                                                           Summary=CONST(No)));
                                                   CaptionML=[ENU=Total Purchase Budget;
                                                              NOR=Totalt innkj›psbudsjett;
                                                              SVE=Totalt ink”psbudget];
                                                   Editable=No }
    { 90  ;   ;Total Budget        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Project Result Forecast Line"."Budget (Total)" WHERE (Project No.=FIELD(Project No.),
                                                                                                                          Date=FIELD(Date),
                                                                                                                          Summary=CONST(No)));
                                                   CaptionML=[ENU=Total Budget;
                                                              NOR=Budsjett Total;
                                                              SVE=Total budget];
                                                   Editable=No }
    { 200 ;   ;Total Actual Cost   ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Project Result Forecast Line"."Actual Cost" WHERE (Project No.=FIELD(Project No.),
                                                                                                                       Date=FIELD(Date),
                                                                                                                       Summary=CONST(No)));
                                                   CaptionML=[ENU=Total Actual Cost;
                                                              NOR=Total faktisk kost;
                                                              SVE=Total faktisk kostnad];
                                                   Editable=No }
    { 210 ;   ;Total Remaining Amount;Decimal     ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Project Result Forecast Line"."Remaining Amount" WHERE (Project No.=FIELD(Project No.),
                                                                                                                            Date=FIELD(Date),
                                                                                                                            Summary=CONST(No)));
                                                   CaptionML=[ENU=Total Remaining Amount;
                                                              NOR=Totalt restbel›p;
                                                              SVE=Totalt †terst. belopp];
                                                   Editable=No }
    { 220 ;   ;Select for Report   ;Boolean       ;CaptionML=[ENU=Select for Report;
                                                              NOR=Velg for rapport;
                                                              SVE=V„lj f”r rapport] }
    { 230 ;   ;Fixed               ;Boolean       ;CaptionML=[ENU=Fixed;
                                                              NOR=Fast;
                                                              SVE=Fast] }
    { 240 ;   ;Total Plot Price    ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Project Principal"."Plot Price" WHERE (Project No.=FIELD(Project No.)));
                                                   CaptionML=ENU=Total Plot Price;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 250 ;   ;Global Dimension 1 Code;Code20     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Job."Global Dimension 1 Code" WHERE (No.=FIELD(Project No.)));
                                                   CaptionML=[ENU=Global Dimension 1 Code;
                                                              NOR=Global dimensjon 1 kode;
                                                              SVE=Global dimension 1 kod];
                                                   Editable=No;
                                                   CaptionClass='1,1,1' }
  }
  KEYS
  {
    {    ;Project No.,Date                        ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DPA_Management@1100528500 : Codeunit 11012783;

    PROCEDURE DeleteLines@1100528701();
    VAR
      ProjectResultForecastLine@1100528700 : Record 11126037;
    BEGIN
      ProjectResultForecastLine.SETRANGE("Project No.", "Project No.");
      ProjectResultForecastLine.SETRANGE(Date, Date);
      ProjectResultForecastLine.DELETEALL(TRUE);
    END;

    PROCEDURE TotalBudgetAmount@1100528700() : Decimal;
    BEGIN
      CALCFIELDS("Total Purchase Budget", "Total Budget");
      EXIT("Total Purchase Budget" + "Total Budget");
    END;

    PROCEDURE TotalBudgAmntExlEstimateResult@1100528401() : Decimal;
    VAR
      ProjectResultForecastLine@1100528401 : Record 11126037;
      TotalBudget@1100528400 : Decimal;
    BEGIN
      CALCFIELDS("Total Purchase Budget");

      ProjectResultForecastLine.SETRANGE("Project No.", "Project No.");
      ProjectResultForecastLine.SETRANGE(Date, Date);
      ProjectResultForecastLine.SETRANGE(Summary, FALSE);
      IF ProjectResultForecastLine.FINDSET THEN
        REPEAT
          ProjectResultForecastLine.CALCFIELDS("Estimate Result Line");
          IF NOT ProjectResultForecastLine."Estimate Result Line" THEN
            TotalBudget := TotalBudget + ProjectResultForecastLine."Budget (Total)";
        UNTIL ProjectResultForecastLine.NEXT = 0;

      EXIT("Total Purchase Budget" + TotalBudget);
    END;

    PROCEDURE TotalContractAmount@1100528702() : Decimal;
    VAR
      Job@1100528700 : Record 11072003;
      ExtensionContract@1100525000 : Record 11012004;
    BEGIN
      IF Job.GET("Project No.") THEN BEGIN
        ExtensionContract.SETCURRENTKEY("Project No.", Principal, Status, "Version Date");
        ExtensionContract.SETRANGE("Project No.", "Project No.");
        ExtensionContract.SETFILTER("Version Date", '..%1', Date);
        ExtensionContract.CALCSUMS("Expected Contract Amount");
        Job.CALCFIELDS("Project Contract Amount", "Options Contract Amount", "Plot Price");
        EXIT(Job."Project Contract Amount" + ExtensionContract."Expected Contract Amount" + Job."Options Contract Amount" + Job."Plot Price");
      END;
    END;

    PROCEDURE CalcEstimateResult@1210190003(JobNo@1210190003 : Code[20];ReferenceDate@1210190002 : Date;VAR EstimateResult@1210190000 : Decimal;VAR EstimateResultExt@1210190001 : Decimal;VAR EstimateResultOpt@1100525000 : Decimal);
    VAR
      Job@1100528402 : Record 11072003;
      OriginalProjectBudget@1100528400 : Decimal;
      ExtensionProjectBudget@1100525001 : Decimal;
      OptionProjectBudget@1100525002 : Decimal;
    BEGIN
      // FG 19-11-14 C015609  EstimateResultOpt added...
      CalcResultBasedOnEstResCostGrp(JobNo, ReferenceDate, EstimateResult, EstimateResultExt, EstimateResultOpt); // FG 19-11-14

      //IF (EstimateResult = 0) AND (EstimateResultExt = 0) THEN //FG 19-11-14
      IF (EstimateResult = 0) AND (EstimateResultExt = 0) AND (EstimateResultOpt = 0) THEN //FG 21-11-14

        IF Job.GET(JobNo) THEN BEGIN
          Job.CALCFIELDS("Contract Amount", "Extension Contract Amount", "Options Contract Amount", Budget);
          OriginalProjectBudget := GetOriginalProjectBudget(JobNo);
          ExtensionProjectBudget :=GetExtensionProjectBudget(JobNo);// FG 26-11-14 C015609
          OptionProjectBudget :=GetOptionProjectBudget(JobNo);// FG 26-11-14 C015609

          EstimateResult := Job."Contract Amount" - OriginalProjectBudget;
          EstimateResultExt :=
            //(Job."Extension Contract Amount" + Job."Options Contract Amount") -   // FG 19-11-14
            //  (  Job.Budget - OriginalProjectBudget);
            Job."Extension Contract Amount" -
            //(Job.Budget - ExtensionProjectBudget); // FG 26-11-14 C015609
            ExtensionProjectBudget; //#C020195.c
          EstimateResultOpt :=
            Job."Options Contract Amount" -
            //(Job.Budget - OptionProjectBudget);  // FG 26-11-14 C015609
            OptionProjectBudget; //#C020195.c
        END;
    END;

    LOCAL PROCEDURE CalcResultBasedOnEstResCostGrp@1100528402(JobNo@1210190003 : Code[20];ReferenceDate@1210190004 : Date;VAR EstimateResult@1210190000 : Decimal;VAR EstimateResultExt@1210190001 : Decimal;VAR EstimateResultOpt@1100525002 : Decimal);
    VAR
      BudgetLine@1210190002 : Record 11012001;
      CostGroup@1100528400 : Record 11126038;
      CostObjectsByCostGroup@1100528401 : Record 11126039;
      ExtensionContract@1100525000 : Record 11012004;
      BudgetSummarySheet@1100525001 : Record 11012064;
    BEGIN
      EstimateResult := 0;
      EstimateResultExt := 0;
      EstimateResultOpt := 0;

      CostGroup.SETRANGE("Estimate Result", TRUE);
      IF CostGroup.FINDFIRST THEN BEGIN
        CostObjectsByCostGroup.SETRANGE("Cost Group Code", CostGroup.Code);
        IF CostObjectsByCostGroup.FINDSET THEN
          REPEAT
            BudgetLine.SETCURRENTKEY("Project No.", "Cost Object");
            BudgetLine.SETRANGE("Project No.", JobNo);
            BudgetLine.SETRANGE("Cost Object", CostObjectsByCostGroup."Cost Object Code");
            IF BudgetLine.FINDSET THEN
            REPEAT
              IF BudgetLine."Extension Contract" + BudgetLine.Option = '' THEN //#C014001.c
                EstimateResult += BudgetLine."Amount (LCY)"
              ELSE BEGIN
                IF (BudgetLine."Extension Contract" <> '') THEN BEGIN //#C014001.c
                  ExtensionContract.GET(BudgetLine."Project No.", BudgetLine."Extension Contract");
                  IF ((ExtensionContract.Status = ExtensionContract.Status::Order) OR
                     (ExtensionContract.Status = ExtensionContract.Status::"Invoicing Allowed"))
                  THEN BEGIN
                    {
                    //#T005082.so
                    BudgetSummarySheet.SETRANGE("Project No.", BudgetLine."Project No.");
                    BudgetSummarySheet.SETRANGE("Contract No.", BudgetLine."Extension Contract");
                    BudgetSummarySheet.SETFILTER("Version Date", '<=%1', ReferenceDate);
                    BudgetSummarySheet.SETRANGE("Cost Object", BudgetLine."Cost Object");
                    IF (BudgetSummarySheet.FINDSET) THEN BEGIN
                      REPEAT
                        EstimateResultExt += BudgetSummarySheet.Amount;
                      UNTIL (BudgetSummarySheet.NEXT = 0);
                    END ELSE BEGIN //#C014001.n
                    //#T005082.eo
                    }
                      IF (BudgetLine."Version Date" <= ReferenceDate) THEN
                        EstimateResultExt += BudgetLine."Amount (LCY)";
                    //END; //#T005082.o
                  END;
                END ELSE BEGIN
                  IF (BudgetLine.Option <> '') THEN BEGIN
                    IF (BudgetLine."Version Date" <= ReferenceDate) THEN
                      //kz.o EstimateResultExt += BudgetLine.Amount;
                      EstimateResultOpt += BudgetLine."Amount (LCY)";
                  END;
                END;
              END;
            UNTIL (BudgetLine.NEXT = 0);
          UNTIL CostObjectsByCostGroup.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE GetOriginalProjectBudget@1210190011(JobNo@1210190000 : Code[20]) : Decimal;
    VAR
      BudgetLine@1210190001 : Record 11012001;
    BEGIN
      BudgetLine.SETRANGE("Project No.", JobNo);
      BudgetLine.SETRANGE(Adjustment, '');
      BudgetLine.SETRANGE("Extension Contract", '');
      BudgetLine.SETRANGE(Option, '');
      BudgetLine.CALCSUMS("Amount (LCY)");
      EXIT(BudgetLine."Amount (LCY)");
    END;

    PROCEDURE CalcTotalEstimateResult@1100528400(JobNo@1100528400 : Code[20];ReferenceDate@1210190000 : Date) : Decimal;
    VAR
      EstimateResult@1100528401 : Decimal;
      EstimateResultExt@1100528402 : Decimal;
      EstimateResultOpt@1100525000 : Decimal;
    BEGIN
      // FG 19-11-14 C015609  EstimateResultOpt added...
      CalcEstimateResult(JobNo, ReferenceDate, EstimateResult, EstimateResultExt, EstimateResultOpt);
      EXIT(EstimateResult + EstimateResultExt + EstimateResultOpt);
    END;

    PROCEDURE GetExtensionProjectBudget@1100525000(JobNo@1100525000 : Code[20]) : Decimal;
    VAR
      BudgetLine@1100525001 : Record 11012001;
    BEGIN
      // FG 26-11-14 C015609 function added
      BudgetLine.SETRANGE("Project No.", JobNo);
      BudgetLine.SETRANGE(Adjustment, '');
      BudgetLine.SETFILTER("Extension Contract", '<>%1', '');
      BudgetLine.SETRANGE(Option, '');
      BudgetLine.CALCSUMS("Amount (LCY)");
      EXIT(BudgetLine."Amount (LCY)");
    END;

    PROCEDURE GetOptionProjectBudget@1100525001(JobNo@1100525000 : Code[20]) : Decimal;
    VAR
      BudgetLine@1100525001 : Record 11012001;
    BEGIN
      // FG 26-11-14 C015609 function added
      BudgetLine.SETRANGE("Project No.", JobNo);
      BudgetLine.SETRANGE(Adjustment, '');
      BudgetLine.SETRANGE("Extension Contract", '');
      BudgetLine.SETFILTER(Option,  '<>%1', '');
      BudgetLine.CALCSUMS("Amount (LCY)");
      EXIT(BudgetLine."Amount (LCY)");
    END;

    PROCEDURE DPA_GetFieldNoDepartmentFilter@1100528104() : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      EXIT(0);
    END;

    PROCEDURE DPA_SetFilters@1100528103(Context@1100525000 : Text[30]) : Boolean;
    VAR
      RecRef@1100525003 : RecordRef;
      i@1100525004 : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN
        EXIT;

      RecRef.GETTABLE(Rec);
      DPA_Management.SetDPAFilterFromContext(
        RecRef,
        Context,
        DPA_GetFieldNoDepartmentCode,
        DPA_GetFieldNoDepartmentFilter );

      FOR i := 10 DOWNTO 0 DO BEGIN
        RecRef.FILTERGROUP(i);
        FILTERGROUP(i);
        SETVIEW(RecRef.GETVIEW(FALSE));
      END;

      EXIT(DPA_Management.IsHardFilter);
    END;

    LOCAL PROCEDURE DPA_IsActive@1100528102() : Boolean;
    VAR
      DepartmentAuthorizObject@1100526000 : Record 11012466;
      FPSLicenseManagement@1100527400 : Codeunit 11229289;
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT FPSLicenseManagement.LicenseAndReadPermissionForTable(DepartmentAuthorizObject) THEN
        EXIT(FALSE);

      IF NOT DPA_Management.AuthorizationActive THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    PROCEDURE DPA_GetFieldNoDepartmentCode@1100528101() : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      EXIT(FIELDNO("Global Dimension 1 Code"));
    END;

    BEGIN
    END.
  }
}

