OBJECT Page 11012470 Tender Paragraphs by Principal
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Baubeschr.-Posten pro Projektauftraggeber;
               ENU=Tender Paragraphs by Project Principal;
               NLD=Besteksposten per project-opdrachtgever;
               NOR=Tilbudsavsnitt etter prosjektoppdragsgiver;
               SVE=AMA koder efter projektuppdragsgivare];
    SourceTable=Table11012470;
    DelayedInsert=Yes;
    SourceTableView=SORTING(Project No.,Principal,Contract No.,No.);
    DataCaptionFields=Project No.,Principal;
    PageType=List;
    OnInit=BEGIN
             SettleExcessEditable := TRUE;
             DeviantSettlementPriceEditable := TRUE;
             SettlementPriceEditable := TRUE;
             RevisionEditable := TRUE;
           END;

    OnOpenPage=BEGIN
                 CheckProjectPrincipal;  //DP01488
                 ShowAllTenderParagraphs := FALSE; //C036111.n
               END;

    OnClosePage=BEGIN
                  CheckFictAmtFilledForOpen();  //C001959
                END;

    OnAfterGetRecord=BEGIN
                       OnAfterGetCurrRecord;
                       DescriptionOnFormat;
                       GetTenderParPlanningActivitiesForDisplay(TenderParPlaningActivityNo,TenderParPlaningActivityDesc);  //UKR-C21179
                     END;

    OnNewRecord=BEGIN
                  FillDefaultSettleExcess();  //C-026862.n
                  OnAfterGetCurrRecord;
                  CLEAR(TenderParPlaningActivityNo); //UKR-C21179
                  CLEAR(TenderParPlaningActivityDesc); //UKR-C21179
                END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 1100485002;1 ;ActionGroup;
                      Name=Tender Paragraph;
                      CaptionML=[DEU=Baubeschreibungs&posten;
                                 ENU=Tender &Paragraph;
                                 NLD=Besteks&post;
                                 NOR=Tilbuds&avsnitt;
                                 SVE=Anbuds&stycke] }
      { 1100485005;2 ;Action    ;
                      CaptionML=[DEU=Text;
                                 ENU=Text;
                                 NLD=Tekst;
                                 NOR=Text;
                                 SVE=BenÑmning];
                      RunObject=Page 11012480;
                      RunPageView=SORTING(Project No.,Principal,Contract No.,Tender Paragraph,Text Line No.)
                                  ORDER(Ascending);
                      RunPageLink=Project No.=FIELD(Project No.),
                                  Principal=FIELD(Principal),
                                  Contract No.=FIELD(Contract No.),
                                  Tender Paragraph=FIELD(No.);
                      Image=Text }
      { 1100525000;2 ;Action    ;
                      CaptionML=[DEU=MwSt.-Spezifikation;
                                 ENU=VAT-Specification;
                                 NLD=Btw-specificatie;
                                 NOR=Mva.-spesifikasjon;
                                 SVE=Momsspecifikation];
                      RunObject=Page 11012481;
                      RunPageLink=Project No.=FIELD(Project No.),
                                  Principal=FIELD(Principal),
                                  Contract No.=FIELD(Contract No.),
                                  Tender Paragraph=FIELD(No.);
                      Image=VATEntries }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100485008;1 ;ActionGroup;
                      CaptionML=[DEU=F&unktion;
                                 ENU=F&unctions;
                                 NLD=F&uncties;
                                 NOR=Fu&nksjon;
                                 SVE=Fu&nktion] }
      { 1100525005;2 ;Action    ;
                      Name=ExchangeRate;
                      CaptionML=[DEU=WÑhrungswechselkurs aktualisieren;
                                 ENU=Update Currency Exchange Rate;
                                 NLD=Actualiseren valutawisselkoers];
                      Image=CurrencyExchangeRates;
                      OnAction=VAR
                                 ProjectPrincipal@1100525000 : Record 11012005;
                               BEGIN
                                 ProjectPrincipal.UpdateTenderElementCurrencyData("Project No.", Principal);  //DP00886
                               END;
                                }
      { 1100525002;2 ;Action    ;
                      Name=VatSpecAmountZero;
                      CaptionML=[DEU=MwSt.-Spez. mit Betrag Null entfernen;
                                 ENU=Remove Vat Specification with amount zero;
                                 NLD=Btw-specificatie met bedrag nul verwijderen];
                      Image=RefreshLines;
                      OnAction=BEGIN
                                 RemoveVatSpecificationAmountZero;  //db, 12-05-17
                               END;
                                }
      { 1100528900;2 ;Action    ;
                      Name=InitSummaryCost;
                      CaptionML=[DEU=AnfÑngliche Endblattkosten;
                                 ENU=Init Summary Cost;
                                 NLD=Staartkosten instellen];
                      Image=SetupLines;
                      OnAction=BEGIN
                                 UpdateSummaryCost;  //DP01488
                               END;
                                }
      { 1100528909;2 ;Action    ;
                      Name=CalculateSummaryCost;
                      CaptionML=[DEU=Endblattkosten berechnen;
                                 ENU=Calculate Summary Cost;
                                 NLD=Staartkosten doorrekenen];
                      Image=Calculate;
                      OnAction=BEGIN
                                 RecalculateSummaryCost("Project No.", Principal, '');  //DP01488
                               END;
                                }
      { 1100525006;2 ;ActionGroup;
                      CaptionML=[DEU=Kopieren;
                                 ENU=Copy;
                                 NLD=Kopiâren;
                                 NOR=KOPIER;
                                 SVE=KOPIA];
                      Image=Copy }
      { 1100485010;3 ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Standardbaubeschreibungsabschnitte kopieren;
                                 ENU=Copy &Standard Tender Paragraphs;
                                 NLD=Standaard besteksparagrafen kopiâren;
                                 NOR=Kopier &standard tilbudsavsnitt;
                                 SVE=Kopiera &standardanbudsstycken];
                      Image=Copy;
                      OnAction=VAR
                                 lvTenderParRec@1100485002 : Record 11012072;
                                 lvPrinTenderElemRec@1100485001 : Record 11012470;
                                 lvTendParFrm@1100485000 : Page 11012132;
                               BEGIN
                                 IF CONFIRM(Text002, FALSE) THEN BEGIN
                                   lvTenderParRec.SETRANGE(Type, lvTenderParRec.Type::Document);
                                   lvTenderParRec.SETRANGE("No.", '');
                                   lvTenderParRec.SETRANGE("Purchase Action", '');
                                   lvTenderParRec.SETRANGE("Document No.", '');
                                   IF lvTenderParRec.FIND('-') THEN BEGIN
                                     REPEAT
                                       IF NOT lvPrinTenderElemRec.GET("Project No.",Principal,'',lvTenderParRec."Tender Paragraph") THEN BEGIN
                                         lvPrinTenderElemRec.INIT;
                                         lvPrinTenderElemRec."Project No." := "Project No.";
                                         lvPrinTenderElemRec.Principal := Principal;
                                         lvPrinTenderElemRec."Contract No." := '';
                                         lvPrinTenderElemRec.FillDefaultSettleExcess();  //C-026862.n
                                         lvPrinTenderElemRec."No." := lvTenderParRec."Tender Paragraph";
                                         lvPrinTenderElemRec.Description := lvTenderParRec.Description;
                                         lvPrinTenderElemRec.INSERT(TRUE);
                                       END;
                                     UNTIL lvTenderParRec.NEXT = 0;
                                   END;
                                 END ELSE BEGIN
                                   lvTendParFrm.SetDefaultsProjPrincipal("Project No.",Principal,'');
                                   lvTendParFrm.RUNMODAL;
                                 END;
                                 CurrPage.UPDATE;
                               END;
                                }
      { 1100485016;3 ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Projektbaubeschreibungsabschnitt kopieren;
                                 ENU=Copy &Project Tender Paragraphs;
                                 NLD=Project besteksparagrafen kopiâren;
                                 NOR=Kopier tilbudsavsnitt for &prosjekt;
                                 SVE=Kopiera &projektanbudsstycken];
                      Image=CopyBudget;
                      OnAction=VAR
                                 lvTenderParRec@1100485002 : Record 11012072;
                                 lvPrinTenderElemRec@1100485001 : Record 11012470;
                                 lvProjTendParFrm@1100485000 : Page 11020346;
                               BEGIN
                                 TESTFIELD("Project No.");
                                 IF CONFIRM(Text003, FALSE) THEN BEGIN
                                   lvTenderParRec.SETRANGE(Type, lvTenderParRec.Type::Job);
                                   lvTenderParRec.SETRANGE("No.", "Project No.");
                                   lvTenderParRec.SETRANGE("Purchase Action", '');
                                   lvTenderParRec.SETRANGE("Document No.", '');
                                   IF lvTenderParRec.FIND('-') THEN BEGIN
                                     REPEAT
                                       IF NOT lvPrinTenderElemRec.GET("Project No.",Principal,'',lvTenderParRec."Tender Paragraph") THEN BEGIN
                                         lvPrinTenderElemRec.INIT;
                                         lvPrinTenderElemRec."Project No." := "Project No.";
                                         lvPrinTenderElemRec.Principal := Principal;
                                         lvPrinTenderElemRec."Contract No." := '';
                                         lvPrinTenderElemRec.FillDefaultSettleExcess();  //C-026862.n
                                         lvPrinTenderElemRec."No." := lvTenderParRec."Tender Paragraph";
                                         lvPrinTenderElemRec.Description := lvTenderParRec.Description;
                                         lvPrinTenderElemRec.INSERT(TRUE);
                                       END;
                                     UNTIL lvTenderParRec.NEXT = 0;
                                   END;
                                 END ELSE BEGIN
                                   lvTenderParRec.RESET;
                                   lvTenderParRec.FILTERGROUP(9);
                                   lvTenderParRec.SETRANGE("No.", "Project No.");
                                   lvTenderParRec.FILTERGROUP(0);
                                   lvProjTendParFrm.SETTABLEVIEW(lvTenderParRec);
                                   lvProjTendParFrm.SetDefaultsProjPrincipal("Project No.",Principal,'');
                                   lvProjTendParFrm.RUNMODAL;
                                 END;
                                 CurrPage.UPDATE;
                               END;
                                }
      { 1100525007;2 ;ActionGroup;
                      CaptionML=[DEU=Importieren;
                                 ENU=Import;
                                 NLD=Importeren;
                                 NOR=Importer;
                                 SVE=Importera];
                      Image=Import }
      { 1100485009;3 ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Baubeschreibungsposten (Messliste) importieren;
                                 ENU=Import Tender Paragraphs (Measurement List);
                                 NLD=Besteksposten (meetstaat) importeren;
                                 NOR=Importer tilbudsavsnitt (mÜlingsliste);
                                 SVE=Importera anbudsstycken (mÑtningslista)];
                      Image=Import;
                      OnAction=VAR
                                 ImportMeasurementList@1100485000 : XMLport 11012014;
                                 ListType@1100485001 : 'ProjectPrincipal,ExtContract,ProvWork';
                                 InStream@1100528500 : InStream;
                                 FromFile@1100528501 : Text;
                               BEGIN
                                 UPLOADINTOSTREAM(Text005, '', '', FromFile, InStream);

                                 ImportMeasurementList.SETSOURCE(InStream);
                                 ImportMeasurementList.InitXMLport(ListType::ProjectPrincipal, "Project No.", Principal, "Contract No.");
                                 ImportMeasurementList.IMPORT;
                                 CurrPage.UPDATE;
                               END;
                                }
      { 1100485018;3 ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=ENU=Import Tender (Bid Form);
                      Image=import;
                      OnAction=VAR
                                 ImportZSXXMLPort@1100485000 : XMLport 11012277;
                                 FromFile@1100529500 : Text;
                                 InStream@1100529501 : InStream;
                                 ClientTypeManagement@1100529502 : Codeunit 4030;
                                 ImportZSX@1100529503 : Codeunit 11125677;
                                 TempBlob@1100529504 : Record 99008535;
                                 OutStream@1100529505 : OutStream;
                               BEGIN
                                 IF NOT ClientTypeManagement.IsCommonWebClientType THEN BEGIN
                                   ImportZSXXMLPort.InitXMLport("Project No.", Principal, "Contract No.");
                                   ImportZSXXMLPort.RUN;
                                 END ELSE BEGIN
                                   TempBlob.Blob.CREATEOUTSTREAM(OutStream);
                                   UPLOADINTOSTREAM(Text005, '', '', FromFile, InStream);
                                   COPYSTREAM(OutStream, InStream);
                                   ImportZSX.SetBlobForImport(TempBlob);
                                   ImportZSX.ImportZSX("Project No.", Principal, "Contract No.", 'c:\dummy');
                                 END;

                                 CurrPage.UPDATE;
                               END;
                                }
      { 1100485011;2 ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Projekt&elemente erstellen;
                                 ENU=Create Project &Elements;
                                 NLD=Project&elementen aanmaken;
                                 NOR=Opprett prosjekt&element;
                                 SVE=Skapa projekt&element];
                      Image=CreateLinesFromJob;
                      OnAction=VAR
                                 lvPrinTenderElemRec@1100485005 : Record 11012470;
                                 lvProjectElement@1100485004 : Record 11012010;
                                 lvFormatElement@1100485003 : Codeunit 11012022;
                                 lvChapter@1100485002 : Code[20];
                                 lvParagraph@1100485001 : Code[20];
                                 lvLevel@1100485000 : Integer;
                               BEGIN
                                 IF CONFIRM(Text004, TRUE) THEN BEGIN
                                   lvPrinTenderElemRec.SETRANGE("Project No.", "Project No.");
                                   lvPrinTenderElemRec.SETRANGE(Principal, Principal);
                                   lvPrinTenderElemRec.SETRANGE("Contract No.", '');
                                   lvPrinTenderElemRec.SETFILTER(Element, '<>%1', '');
                                   IF lvPrinTenderElemRec.FIND('-') THEN BEGIN
                                     REPEAT
                                       IF NOT lvProjectElement.GET(lvPrinTenderElemRec."Project No.", lvPrinTenderElemRec.Element) THEN BEGIN
                                         lvFormatElement.FormatElemExtended(2, lvPrinTenderElemRec.Element, lvChapter, lvParagraph, lvLevel);
                                         IF lvLevel IN [1,2,3] THEN BEGIN
                                           IF NOT lvProjectElement.GET(lvPrinTenderElemRec."Project No.", lvChapter) THEN BEGIN
                                             lvProjectElement.INIT;
                                             lvProjectElement.VALIDATE("Project No.", lvPrinTenderElemRec."Project No.");
                                             lvProjectElement.VALIDATE(Element, lvChapter);
                                             lvProjectElement.Description := lvPrinTenderElemRec.Description;
                                             lvProjectElement."Unit of Measure" := lvPrinTenderElemRec."Unit of Measure";
                                             lvProjectElement.INSERT;
                                           END;
                                         END;
                                         IF lvLevel IN [2,3] THEN BEGIN
                                           IF NOT lvProjectElement.GET(lvPrinTenderElemRec."Project No.", lvParagraph) THEN BEGIN
                                             lvProjectElement.INIT;
                                             lvProjectElement.VALIDATE("Project No.", lvPrinTenderElemRec."Project No.");
                                             lvProjectElement.VALIDATE(Element, lvParagraph);
                                             lvProjectElement.Description := lvPrinTenderElemRec.Description;
                                             lvProjectElement."Unit of Measure" := lvPrinTenderElemRec."Unit of Measure";
                                             lvProjectElement.INSERT;
                                           END;
                                         END;
                                         IF lvLevel = 3 THEN BEGIN
                                           lvProjectElement.INIT;
                                           lvProjectElement.VALIDATE("Project No.", lvPrinTenderElemRec."Project No.");
                                           lvProjectElement.VALIDATE(Element, lvPrinTenderElemRec.Element);
                                           lvProjectElement.Description := lvPrinTenderElemRec.Description;
                                           lvProjectElement."Unit of Measure" := lvPrinTenderElemRec."Unit of Measure";
                                           lvProjectElement.INSERT;
                                         END;
                                       END;
                                     UNTIL lvPrinTenderElemRec.NEXT = 0;
                                   END;
                                 END;
                               END;
                                }
      { 1210190030;1 ;Action    ;
                      CaptionML=[DEU=D&rucken;
                                 ENU=&Print;
                                 NLD=&Afdrukken;
                                 NOR=Skriv &ut;
                                 SVE=Skriv &ut];
                      Promoted=Yes;
                      Image=Print;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ProjPrincipalRec@1210190000 : Record 11012005;
                                 PrintDoc@1210190002 : Codeunit 229;
                               BEGIN
                                 ProjPrincipalRec."Project No." :=  "Project No.";
                                 ProjPrincipalRec.Principal := Principal;
                                 PrintDoc.PrintTenderParagraphbyPrinc(ProjPrincipalRec);
                               END;
                                }
      { 1100529402;1 ;Action    ;
                      Name=CopyPlanningActivitiesToBudget;
                      CaptionML=[DEU=VerknÅpfte PlanungsaktivitÑten in Budget kopieren;
                                 ENU=Copy linked planning activities to budget;
                                 NLD=Kopieer planningsactiviteiten naar budget];
                      Promoted=Yes;
                      Image=CopyBudget;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 CopyPlanningActivitiesToBudget; //**4PS.s BI014 KD 28-07-16
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100529601;1;Group  ;
                GroupType=Group }

    { 1100529602;2;Field  ;
                Name=ShowAllTenderParagraphs;
                CaptionML=[DEU=Alle Bauplanposten des Auftraggebers anzeigen;
                           ENU=Show all tender paragraphs of the principal;
                           NLD=Toon alle besteksposten voor de opdrachtgever];
                Description=C036111;
                SourceExpr=ShowAllTenderParagraphs;
                OnValidate=BEGIN
                             IF ShowAllTenderParagraphs THEN
                               SETRANGE("Contract No.")
                             ELSE
                               SETRANGE("Contract No.", '');
                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 1210190000;1;Group  ;
                GroupType=Repeater }

    { 1100485019;2;Field  ;
                SourceExpr="No.";
                OnLookup=BEGIN
                           LookupTenderParagraph;
                         END;
                          }

    { 1210190003;2;Field  ;
                SourceExpr=Description;
                Style=Strong;
                StyleExpr=DescriptionEmphasize }

    { 1100485000;2;Field  ;
                SourceExpr=Element }

    { 1100529600;2;Field  ;
                Description=C036111;
                SourceExpr="Contract No.";
                Editable=FALSE }

    { 1210190005;2;Field  ;
                BlankZero=Yes;
                SourceExpr=Quantity;
                OnValidate=BEGIN
                             RefreshPage;
                           END;
                            }

    { 1210190007;2;Field  ;
                SourceExpr="Unit of Measure" }

    { 1210190011;2;Field  ;
                BlankZero=Yes;
                SourceExpr="Unit Price (LCY)";
                OnValidate=BEGIN
                             RefreshPage;
                           END;
                            }

    { 1100525001;2;Field  ;
                BlankZero=Yes;
                SourceExpr="Unit Price (FCY)";
                Visible=FALSE }

    { 1100525011;2;Field  ;
                BlankZero=Yes;
                SourceExpr="Basic Price Settl.Excess (LCY)" }

    { 1100525010;2;Field  ;
                BlankZero=Yes;
                SourceExpr="Basic Price Settl.Excess (FCY)";
                Visible=FALSE }

    { 1100485021;2;Field  ;
                BlankZero=Yes;
                SourceExpr="Settlement Price (LCY)";
                Visible=FALSE;
                Editable=SettlementPriceEditable;
                OnValidate=BEGIN
                             "Deviant Settlement Price" := TRUE;
                           END;
                            }

    { 1100525015;2;Field  ;
                BlankZero=Yes;
                SourceExpr="Settlement Price (FCY)";
                Visible=FALSE;
                Editable=SettlementPriceEditable }

    { 1100485031;2;Field  ;
                SourceExpr="Deviant Settlement Price";
                Visible=FALSE;
                Editable=DeviantSettlementPriceEditable }

    { 1210190013;2;Field  ;
                BlankZero=Yes;
                SourceExpr="Total Amount (LCY)" }

    { 1100525003;2;Field  ;
                BlankZero=Yes;
                SourceExpr="Total Amount (FCY)";
                Visible=FALSE }

    { 1100525004;2;Field  ;
                SourceExpr="Currency Code";
                Visible=FALSE }

    { 1210190009;2;Field  ;
                BlankZero=Yes;
                SourceExpr="Number of Points/Percentage";
                Editable=NumberofPointsEditable;
                OnValidate=BEGIN
                             RefreshPage;
                           END;
                            }

    { 1100485023;2;Field  ;
                SourceExpr=Finished;
                Visible=FALSE;
                OnValidate=BEGIN
                             RefreshPage;
                           END;
                            }

    { 1100485006;2;Field  ;
                SourceExpr="Settlement Type";
                Visible=FALSE }

    { 1100485029;2;Field  ;
                SourceExpr="Settle Excess";
                Visible=FALSE;
                Editable=SettleExcessEditable }

    { 1100485012;2;Field  ;
                SourceExpr=Text;
                Visible=FALSE;
                Editable=FALSE }

    { 1100528500;2;Field  ;
                SourceExpr="Tender Paragraph Text";
                Visible=FALSE;
                Editable=FALSE }

    { 1100485003;2;Field  ;
                SourceExpr=Revision;
                Editable=RevisionEditable;
                OnValidate=VAR
                             ProjRec@1100485000 : Record 11072003;
                             Text001@1100485001 : TextConst 'DEU=Warnung: Es gibt eine Preisverrechnung fÅr dieses Projekt!;ENU=Warning: A Revision Rule is specified for the project!;NLD=Waarschuwing: Er geldt een risico regeling voor het project!;NOR=Advarsel: En revisjonssregel er spesifisert for prosjektet!;SVE=Varning: En revideringsregel har inte specificerats fîr projektet!';
                           BEGIN
                             ProjRec.GET("Project No.");
                             IF ProjRec.Revision AND NOT Revision THEN
                               MESSAGE(Text001);
                           END;
                            }

    { 1100485025;2;Field  ;
                SourceExpr=Type;
                Visible=FALSE }

    { 1100485027;2;Field  ;
                SourceExpr=Layout;
                Visible=FALSE }

    { 1100528901;2;Field  ;
                SourceExpr="Apply Perc. (Discount)";
                Editable=ApplySummaryCost }

    { 1100528902;2;Field  ;
                SourceExpr="Apply Perc. (Surcharge)";
                Editable=ApplySummaryCost }

    { 1100528903;2;Field  ;
                SourceExpr="Apply Perc. (Profit/Risk)";
                Editable=ApplySummaryCost }

    { 1100528904;2;Field  ;
                SourceExpr="Apply Perc. (ExecutionCost)";
                Editable=ApplySummaryCost }

    { 1100525009;2;Field  ;
                SourceExpr="Apply Perc. (ContractorFee)";
                Visible=FALSE;
                Editable=ApplySummaryCost }

    { 1100528908;2;Field  ;
                SourceExpr="Percentage (Discount)";
                Editable=ApplySummaryCost }

    { 1100528907;2;Field  ;
                SourceExpr="Percentage (Surcharge)";
                Editable=ApplySummaryCost }

    { 1100528906;2;Field  ;
                SourceExpr="Percentage (Profit/Risk)";
                Editable=ApplySummaryCost }

    { 1100528905;2;Field  ;
                SourceExpr="Percentage (ExecutionCost)";
                Editable=ApplySummaryCost }

    { 1100525008;2;Field  ;
                SourceExpr="Percentage (ContractorFee)";
                Visible=FALSE;
                Editable=ApplySummaryCost }

    { 1100529401;2;Field  ;
                Name=TenderParPlaningActivityNo;
                CaptionML=[DEU=Projektplanungsaktionsnr.;
                           ENU=Project Planning Activity No.;
                           NLD=Projectplanningsactiviteitnr.;
                           NOR=Aktivitetsnummer for prosjektplanlegging;
                           SVE=Projektplaneringsaktivitetsnr];
                Description=UKR-C21177 (BI018);
                SourceExpr=TenderParPlaningActivityNo;
                OnValidate=BEGIN
                             TenderParPlanningActivityNoOnValidate(TenderParPlaningActivityNo);
                             GetTenderParPlanningActivitiesForDisplay(TenderParPlaningActivityNo,TenderParPlaningActivityDesc);
                           END;

                OnLookup=VAR
                           ProjectPlanningActivity@1100529400 : Record 11012431;
                         BEGIN
                           IF TenderParPlanningActivityNoOnLookup(ProjectPlanningActivity) THEN
                             IF ReplaceTenderParPlanningActivities(ProjectPlanningActivity) THEN
                               GetTenderParPlanningActivitiesForDisplay(TenderParPlaningActivityNo,TenderParPlaningActivityDesc);
                         END;

                OnAssistEdit=BEGIN
                               TenderParPlanningActivityNoOnAssistEdit;
                               GetTenderParPlanningActivitiesForDisplay(TenderParPlaningActivityNo,TenderParPlaningActivityDesc);
                             END;
                              }

    { 1100529400;2;Field  ;
                Name=TenderParPlaningActivityDesc;
                CaptionML=[DEU=Projektplanungsaktiviteitenbeschreibung;
                           ENU=Project Planning Activity Description;
                           NLD=Projectplanningsactiviteitomschrijving;
                           NOR=Beskrivelse av prosjektplanleggingsaktivitet;
                           SVE=Beskrivning av projektplaneringsaktivitet];
                Description=UKR-C21177 (BI017);
                SourceExpr=TenderParPlaningActivityDesc;
                Visible=FALSE;
                Editable=FALSE }

    { 1100529700;2;Field  ;
                SourceExpr="VAT Prod. Posting Group" }

    { 1210190001;0;Container;
                ContainerType=FactBoxArea }

    { 1210190002;1;Part   ;
                SubPageView=SORTING(Project No.,Principal,Contract No.,No.);
                SubPageLink=Project No.=FIELD(Project No.),
                            Principal=FIELD(Principal),
                            Contract No.=FILTER('');
                PagePartID=Page11130020;
                PartType=Page }

  }
  CODE
  {
    VAR
      Text000@1210190002 : TextConst 'DEU=Anzahl zugeweisener Punkte stimmt nicht mit der Summe der Punkte Åberein;ENU=Number of assigned points is not equal to Total Points;NLD=Aantal toegekende punten is ongelijk aan totaal punten;NOR="Antallet tilordnede poeng er ikke lik totalpoeng ";SVE=Antalet tilldelade punkter Ñr inte lika med totalpoÑngen';
      Text001@1210190003 : TextConst 'DEU=Summe Baubeschreibungsposten stimmt nicht mit dem Vertragspreis Åberein;ENU=Total Tender Paragraphs is not equal to the Contract Price;NLD=Totaal besteksposten is ongelijk aan de aanneemsom;NOR=Totale tilbudsavsnitt er ikke lik kontraktsprisen;SVE=Totalanbudsstyckena Ñr inte lika med kontraktspriset';
      ProjectPrincipal@1210190004 : Record 11012005;
      Message1@1210190001 : Text[100];
      Message2@1210190000 : Text[100];
      Text002@1210190005 : TextConst 'DEU=Alle Standardbaubeschreibungsabschnitte in Baubeschreibungsposten pro Sonderleistungsvertrag kopieren?;ENU=Copy all Standard Tender Paragraphs to Tender Paragraphs by Project Principal?;NLD=Alle standaard besteksparagrafen naar besteksposten per project-opdrachtgever kopiâren?;NOR=Kopiere alle standardtilbudsavsnitt til tilbudsavsnitt etter prosjektoppdragsgiver?;SVE=Kopiera alla standardanbudsstycken till anbudsstycken efter projektuppdragsgivare?';
      Text003@1100485001 : TextConst 'DEU=Alle Projektbaubeschreibungsabschnitte in Sonderleistungsvertrag kopieren?;ENU=Copy all Project Tender Paragraphs to Tender Paragraphs by Project Principal?;NLD=Alle projectbesteksparagrafen naar besteksposten per project-opdrachtgever kopiâren?;NOR=Kopier alle prosjekttilbudsavsnitt til tilbudsavsnitt etter prosjektoppdragsgiver?;SVE=Kopiera alla projektanbudsstycken till anbudsstycken efter projektuppdragsgivare?';
      Text004@1100485000 : TextConst 'DEU=Projektelemene erstellen?;ENU=Generate Project Elements?;NLD=Projectelementen aanmaken?;NOR=Generere prosjektelement?;SVE=Generera projektelement?';
      gProjRec@1100485002 : Record 11072003;
      TotalPointsPercentage@1100525000 : Decimal;
      RevisionEditable@19056319 : Boolean INDATASET;
      SettlementPriceEditable@19042059 : Boolean INDATASET;
      DeviantSettlementPriceEditable@19035422 : Boolean INDATASET;
      SettleExcessEditable@19041189 : Boolean INDATASET;
      DescriptionEmphasize@19010547 : Boolean INDATASET;
      NumberOfPointsEditable@1100525001 : Boolean INDATASET;
      ShowAllTenderParagraphs@1100529600 : Boolean;
      TenderParPlaningActivityNo@1100529400 : Code[30];
      TenderParPlaningActivityDesc@1100529401 : Text[50];
      ApplySummaryCost@1100528900 : Boolean INDATASET;
      Text005@1100528500 : TextConst 'DEU=AuswÑhlen Datei...;ENU=Select File...;NLD=Selecteerd bestand...';

    LOCAL PROCEDURE OnAfterGetCurrRecord@19077479();
    BEGIN
      xRec := Rec;
      CALCFIELDS("Contract Price (LCY)", "Total Quant.Points/Percentage", "Total Tender Paragraphs (LCY)", "Total Points/Percentage");

      //mg.sc, 01-02-11: M25239
      IF (ProjectPrincipal."Installm.Motivation Method" = ProjectPrincipal."Installm.Motivation Method"::Percentages) AND ("Contract Price (LCY)" <> 0)
      THEN
        TotalPointsPercentage := ROUND("Total Tender Paragraphs (LCY)" / "Contract Price (LCY)" * 100)
      ELSE
        TotalPointsPercentage := "Total Points/Percentage";
      //mg.ec, 01-02-11: M25239

      IF TotalPointsPercentage <> "Total Quant.Points/Percentage" THEN
        Message1 := STRSUBSTNO(Text000,"Total Points/Percentage","Total Quant.Points/Percentage")
      ELSE
        Message1 := '';

      IF "Total Tender Paragraphs (LCY)" <> "Contract Price (LCY)" THEN
        Message2 := STRSUBSTNO(Text001,"Total Tender Paragraphs (LCY)","Contract Price (LCY)")
      ELSE
        Message2 := '';

      gProjRec.GET("Project No.");
      RevisionEditable := (gProjRec.Revision AND NOT gProjRec.RAW);
      SettlementPriceEditable := (gProjRec.RAW);
      DeviantSettlementPriceEditable := (gProjRec.RAW);
      //SettleExcessEditable := (gProjRec.RAW);  //C-026862.o

      NumberOfPointsEditable :=
        ((ProjectPrincipal."Installm.Motivation Method" = ProjectPrincipal."Installm.Motivation Method"::Points) OR
         (ProjectPrincipal."Installm.Motivation Method" = ProjectPrincipal."Installm.Motivation Method"::Percentages));  //db, 10-03-16
    END;

    LOCAL PROCEDURE DescriptionOnFormat@19023855();
    BEGIN
      IF gProjRec.RAW THEN
        DescriptionEmphasize := (Layout <> Layout::Detail);
    END;

    LOCAL PROCEDURE RefreshPage@1100525001();
    BEGIN
      CurrPage.UPDATE;  //db, 10-03-16
    END;

    LOCAL PROCEDURE CopyPlanningActivitiesToBudget@1100529400();
    VAR
      BudgetLine@1100529402 : Record 11012001;
      BudgetPlanningActivity@1100529401 : Record 11229302;
      TenderParagraphByPrincipal@1100529403 : Record 11012470;
      TenderParagPlanActivity@1100529400 : Record 11229303;
    BEGIN
      //**4PS.sn BI014 KD 28-07-16
      IF ISEMPTY THEN
        EXIT;

      BudgetLine.SETRANGE("Project No.","Project No.");
      BudgetLine.SETFILTER("Tender Paragraph",'<>''''');
      IF BudgetLine.ISEMPTY THEN
        EXIT;

      TenderParagraphByPrincipal.SETRANGE("Project No.","Project No.");
      TenderParagraphByPrincipal.SETRANGE(Principal,Principal);

      TenderParagPlanActivity.SETRANGE("Project No.","Project No.");
      TenderParagPlanActivity.SETRANGE(Principal,Principal);

      TenderParagraphByPrincipal.FINDSET;
      REPEAT
        BudgetLine.SETRANGE("Tender Paragraph",TenderParagraphByPrincipal."No.");
        IF BudgetLine.FINDSET THEN BEGIN
          TenderParagPlanActivity.SETRANGE("Contract No.",TenderParagraphByPrincipal."Contract No.");
          TenderParagPlanActivity.SETRANGE("No.",TenderParagraphByPrincipal."No.");
          IF NOT TenderParagPlanActivity.ISEMPTY THEN
            REPEAT
              TenderParagPlanActivity.FINDSET;
              REPEAT
                IF NOT BudgetPlanningActivity.GET(BudgetLine."Project No.",BudgetLine.Adjustment,BudgetLine."Extension Contract",
                                                  BudgetLine.Option,BudgetLine."Line No.",TenderParagPlanActivity."Project Planning Activity No.")
                THEN BEGIN
                  BudgetPlanningActivity.INIT;
                  BudgetPlanningActivity."Project No." := BudgetLine."Project No.";
                  BudgetPlanningActivity.Adjustment := BudgetLine.Adjustment;
                  BudgetPlanningActivity."Extension Contract" := BudgetLine."Extension Contract";
                  BudgetPlanningActivity.Option := BudgetLine.Option;
                  BudgetPlanningActivity."Budget Line No." := BudgetLine."Line No.";
                  BudgetPlanningActivity."Project Planning Activity No." := TenderParagPlanActivity."Project Planning Activity No.";
                  BudgetPlanningActivity.VALIDATE(Percentage,TenderParagPlanActivity.Percentage);
                  BudgetPlanningActivity.INSERT;
                END;
              UNTIL TenderParagPlanActivity.NEXT = 0;
            UNTIL BudgetLine.NEXT = 0;
        END;
      UNTIL TenderParagraphByPrincipal.NEXT = 0;
      //**4PS.en BI014 KD 28-07-16
    END;

    LOCAL PROCEDURE RemoveVatSpecificationAmountZero@1100525000();
    VAR
      ElemVatSpecRec@1100525000 : Record 11012476;
    BEGIN
      //db, 12-05-17
      ElemVatSpecRec.SETFILTER("Project No.", "Project No.");
      ElemVatSpecRec.SETFILTER(Principal, Principal);
      ElemVatSpecRec.SETFILTER("Contract No.", "Contract No.");
      ElemVatSpecRec.SETRANGE(Amount, 0);
      ElemVatSpecRec.DELETEALL;
    END;

    LOCAL PROCEDURE UpdateSummaryCost@1100528900();
    VAR
      UpdateSummaryCostRAW@1100528900 : Page 11130027;
    BEGIN
      //DP01488
      UpdateSummaryCostRAW.SetSelection("Project No.", Principal, '');
      UpdateSummaryCostRAW.RUNMODAL;
      CheckProjectPrincipal;
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE CheckProjectPrincipal@1100528901();
    BEGIN
      //DP01488
      ProjectPrincipal.GET("Project No.",Principal);
      ApplySummaryCost := (ProjectPrincipal."Summary Cost (RAW)" = ProjectPrincipal."Summary Cost (RAW)"::"Percentage (Detail)");
    END;

    BEGIN
    {
      4PS 27-07-2016 UKR-C21179 OTRO: Link 1 Tender Paragraph line to mult planning act (DP01144 BI018)
      4PS 28-07-16 UKR-C21174 KD: Import planning activities from P6 (BI014) - Added trigger CopyPlanningActivitiesToBudget and action "Copy linked planning activities to budget"
      db, 29-05-17: ascii-interface (ZSU/RSU) expired since 01-01-2011
      db, 05-09-17: due to introduction of key-field Contract with filter to show all records, delayed insert is required when activating option import to avoid check on insert for NotBlank keyfields.
    }
    END.
  }
}

