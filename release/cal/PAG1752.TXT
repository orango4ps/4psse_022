OBJECT Page 1752 Data Classification Wizard
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Data Classification Assisted Setup Guide;
               NOR=Veiledning for assistert oppsett av dataklassifisering;
               SVE=Guiden fîr assisterad konfiguration av dataklassificering];
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table1180;
    PageType=NavigatePage;
    SourceTableTemporary=Yes;
    RefreshOnActivate=Yes;
    ShowFilter=No;
    OnInit=BEGIN
             LoadTopBanners;
           END;

    OnOpenPage=BEGIN
                 ResetControls;
                 ShowWorksheet := TRUE;
                 LedgerEntriesDefaultClassification := LedgerEntriesDefaultClassification::"Company Confidential";
                 TemplatesDefaultClassification := TemplatesDefaultClassification::Normal;
                 SetupTablesDefaultClassification := SetupTablesDefaultClassification::Normal;
               END;

    OnAfterGetRecord=BEGIN
                       IF Status = Status::"Review Needed" THEN
                         StatusStyle := 'UnFavorable'
                       ELSE
                         StatusStyle := 'Favorable';

                       IF "Status 2" = "Status 2"::"Review Needed" THEN
                         SimilarFieldsStatusStyle := 'UnFavorable'
                       ELSE
                         SimilarFieldsStatusStyle := 'Favorable';
                     END;

    ActionList=ACTIONS
    {
      { 16      ;0   ;ActionContainer;
                      ActionContainerType=NewDocumentItems }
      { 15      ;1   ;Action    ;
                      Name=ActionBack;
                      CaptionML=[ENU=Back;
                                 NOR=Tilbake;
                                 SVE=FîregÜende];
                      ApplicationArea=#All;
                      Enabled=BackEnabled;
                      InFooterBar=Yes;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 IF Step = Step::Verify THEN
                                   RESET;
                                 NextStep(TRUE);
                               END;
                                }
      { 14      ;1   ;Action    ;
                      Name=ActionNext;
                      CaptionML=[ENU=Next;
                                 NOR=Neste;
                                 SVE=NÑsta];
                      ApplicationArea=#All;
                      Enabled=NextEnabled;
                      InFooterBar=Yes;
                      Image=NextRecord;
                      OnAction=VAR
                                 DataSensitivity@1001 : Record 2000000159;
                                 DataClassificationMgt@1000 : Codeunit 1750;
                               BEGIN
                                 CASE Step OF
                                   Step::"Choose Mode":
                                     BEGIN
                                       IF ImportModeSelected THEN BEGIN
                                         DataClassificationMgt.ImportExcelSheet;
                                         Step := Step::Finish;
                                         ResetControls;
                                         EXIT;
                                       END;
                                       IF ExportModeSelected THEN BEGIN
                                         DataClassificationMgt.ExportToExcelSheet;
                                         Step := Step::Finish;
                                         ResetControls;
                                         EXIT;
                                       END;
                                       IF ExpertModeSelected THEN BEGIN
                                         DataSensitivity.SETRANGE("Company Name",COMPANYNAME);
                                         IF DataSensitivity.ISEMPTY THEN
                                           DataClassificationMgt.FillDataSensitivityTable;
                                       END;
                                     END;
                                   Step::Apply:
                                     BEGIN
                                       DataClassificationMgt.SetTableClassifications(Rec);
                                       SETRANGE(Include,TRUE);
                                     END;
                                   Step::"Set Rules":
                                     BEGIN
                                       DataSensitivity.SETRANGE("Company Name",COMPANYNAME);
                                       DataSensitivity.SETRANGE("Data Sensitivity",DataSensitivity."Data Sensitivity"::Unclassified);
                                       DataSensitivity.SETFILTER("Table No",DataClassificationMgt.GetTableNoFilterForTablesWhoseNameContains('Entry'));
                                       DataClassificationMgt.SetSensitivities(DataSensitivity,LedgerEntriesDefaultClassification);
                                       DataSensitivity.SETFILTER("Table No",DataClassificationMgt.GetTableNoFilterForTablesWhoseNameContains('Template'));
                                       DataClassificationMgt.SetSensitivities(DataSensitivity,TemplatesDefaultClassification);
                                       DataSensitivity.SETFILTER("Table No",DataClassificationMgt.GetTableNoFilterForTablesWhoseNameContains('Setup'));
                                       DataClassificationMgt.SetSensitivities(DataSensitivity,SetupTablesDefaultClassification);
                                     END;
                                 END;

                                 NextStep(FALSE);
                               END;
                                }
      { 13      ;1   ;Action    ;
                      Name=ActionFinish;
                      CaptionML=[ENU=Finish;
                                 NOR=Fullfõr;
                                 SVE=Slutfîr];
                      ApplicationArea=#All;
                      Enabled=FinishEnabled;
                      InFooterBar=Yes;
                      Image=Approve;
                      OnAction=BEGIN
                                 IF ShowWorksheet THEN
                                   PAGE.RUN(PAGE::"Data Classification Worksheet");
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 42  ;1   ;Group     ;
                Visible=TopBannerVisible AND NOT FinishEnabled;
                Editable=FALSE;
                GroupType=Group }

    { 41  ;2   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=MediaResourcesStandard."Media Reference";
                Editable=FALSE;
                ShowCaption=No }

    { 40  ;1   ;Group     ;
                Visible=TopBannerVisible AND FinishEnabled;
                Editable=FALSE;
                GroupType=Group }

    { 39  ;2   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=MediaResourcesDone."Media Reference";
                Editable=FALSE;
                ShowCaption=No }

    { 2   ;1   ;Group     ;
                Visible=Step = Step::Welcome;
                GroupType=Group }

    { 9   ;2   ;Group     ;
                CaptionML=[ENU=Welcome to the Data Classification Assisted Setup Guide;
                           NOR=Velkommen veiledningen for assistert oppsett av dataklassifisering;
                           SVE=VÑlkommen till guiden fîr assisterad konfiguration av dataklassificering];
                GroupType=Group;
                InstructionalTextML=[ENU="Data classification is an important part of protecting the privacy of personal and sensitive data, and is often required by data privacy laws. Classification can make it easier to retrieve personal data, for example, in response to a request, and it can add another layer of protection. This guide helps you classify the sensitivity of the data in tables and fields. ";
                                     NOR="Dataklassifiseringen er en viktig del av beskytte personlige og sensitive data, og den kreves ofte av datapersonvernlover. Klassifiseringen kan gjõre det enklere Ü hente personlige data, for eksempel som svar pÜ en forespõrsel, og den kan legge tilˇytterligere et lag med beskyttelse. Denne veiledningen hjelper deg med Ü klassifisere datasensitiviteten i tabeller og felt. ";
                                     SVE="Dataklassificering har stor betydelse nÑr det gÑller att skydda integriteten i personlig och kÑnslig information, vilket ofta krÑvs enligt lagar om datasekretess. Klassificeringen kan gîra det enklare att hÑmta personuppgifter, till exempel som svar pÜ en begÑran, och den kan tillfîra ytterligare en skyddsnivÜ. Den hÑr guiden hjÑlper dig att klassificera kÑnsligheten hos data i tabeller och fÑlt. "] }

    { 12  ;2   ;Group     ;
                CaptionML=[ENU=Classifications include:;
                           NOR=Klassifiseringer inkluderer:;
                           SVE=Klassificeringarna omfattar:];
                GroupType=Group;
                Layout=Rows }

    { 48  ;3   ;Field     ;
                CaptionML=[ENU=- Sensitive - Private data, such as political or religious beliefs.;
                           NOR=-Sensitive - Personlige data, for eksempel politiske eller religiõse meninger.;
                           SVE=-KÑnsligt - Privata data, exempelvis politisk eller religiîs ÜskÜdning.];
                ApplicationArea=#All }

    { 49  ;3   ;Field     ;
                CaptionML=[ENU=- Personal - Any data that can be used to identify someone.;
                           NOR=-Personlige - Data som kan brukes til Ü identifisere noen.;
                           SVE=-Personligt - Data som kan anvÑndas fîr att identifiera nÜgon.];
                ApplicationArea=#All }

    { 50  ;3   ;Field     ;
                CaptionML=[ENU=- Company Confidential - Business data that you do not want to expose. For example, ledger entries.;
                           NOR=-Selskapskonfidensielle - Forretningsdata du ikke õnsker Ü dele, for eksempel finansposter.;
                           SVE=-Konfidentiellt - Fîretagsdata som du inte vill exponera. Till exempel transaktioner.];
                ApplicationArea=#All }

    { 51  ;3   ;Field     ;
                CaptionML=[ENU=- Normal - Data that does not belong to other classifications.;
                           NOR=-Normale - Data som ikke tilhõrer andre klassifiseringer.;
                           SVE=-Normalt - Data som inte tillhîr andra klassificeringar.];
                ApplicationArea=#All }

    { 60  ;2   ;Group     ;
                CaptionML=[ENU=Legal disclaimer;
                           NOR=Ansvarsfraskrivelse;
                           SVE=Ansvarsfriskrivning];
                GroupType=Group;
                InstructionalTextML=[ENU=Microsoft is providing this Data Classification feature as a matter of convenience only. It's your responsibility to classify the data appropriately and comply with any laws and regulations that are applicable to you. Microsoft disclaims all responsibility towards any claims related to your classification of the data.;
                                     NOR=Microsoft tilbyr denne dataklassifiseringsfunksjonen bare som et praktisk hjelpemiddel. Det er ditt ansvar Ü klassifisere dataene riktig og overholde eventuelle lover og regler som gjelder for deg. Microsoft fraskriver seg ethvert ansvar mot eventuelle krav knyttet til din klassifisering av dataene.;
                                     SVE=Microsoft tillhandahÜller den hÑr dataklassificeringsfunktionen fîr att gîra det enklare fîr dig. Det Ñr ditt ansvar att klassificera data sÜ att de uppfyller kraven i de lagar och regler som gÑller fîr dig. Microsoft friskriver sig allt ansvar fîr eventuella krav som berîr klassificeringen av dina data.] }

    { 6   ;2   ;Field     ;
                DrillDown=Yes;
                ApplicationArea=#All;
                SourceExpr=HelpLbl;
                Editable=FALSE;
                Style=StandardAccent;
                StyleExpr=TRUE;
                OnDrillDown=BEGIN
                              HYPERLINK(HelpUrlTxt);
                            END;

                ShowCaption=No }

    { 3   ;1   ;Group     ;
                Visible=Step = Step::"Choose Mode";
                GroupType=Group }

    { 43  ;2   ;Group     ;
                CaptionML=[ENU=Let's Get Started;
                           NOR=La oss komme i gang;
                           SVE=Nu sÑtter vi igÜng];
                GroupType=Group;
                InstructionalTextML=[ENU=You can export data to an Excel worksheet, add the classifications, and then import the worksheet. For example, this is great for:;
                                     NOR=Du kan eksportere data til et Excel-regneark, legge til klassifiseringene og deretter importere regnearket. Dette er for eksempelˇpraktisk nÜr du vil gjõre fõlgende:;
                                     SVE=Du kan exportera data till ett Excel-kalkylblad, lÑgga till klassificeringar och sedan importera kalkylbladet. Det Ñr till exempel bra nÑr du vill:] }

    { 44  ;3   ;Field     ;
                CaptionML=[ENU=- Adding classifications in bulk.;
                           NOR=-Legge til masseklassifiseringer.;
                           SVE=- LÑgga till flera klassificeringar samtidigt.];
                ApplicationArea=#All;
                Importance=Additional;
                MultiLine=Yes }

    { 7   ;3   ;Field     ;
                CaptionML=[ENU=- Sharing data with a partner who is classifying data for you.;
                           NOR=-Dele data med en partner som klassifiserer data for deg.;
                           SVE=- Dela data med en partner som klassificerar data Üt dig.];
                ApplicationArea=#All }

    { 10  ;3   ;Field     ;
                CaptionML=[ENU=- Importing the classifications from another company.;
                           NOR=- Importere klassifiseringene fra et annet selskap.;
                           SVE=- Importera klassificeringar frÜn ett annat fîretag.];
                ApplicationArea=#All }

    { 47  ;3   ;Field     ;
                Name=<ExportModeSelected>;
                CaptionML=[ENU=Export Classification Data to Excel;
                           NOR=Eksporter klassifiseringsdata til Excel;
                           SVE=Exportera klassificeringsdata till Excel];
                ApplicationArea=#All;
                SourceExpr=ExportModeSelected;
                OnValidate=BEGIN
                             IF ExportModeSelected = TRUE THEN BEGIN
                               ExpertModeSelected := FALSE;
                               ImportModeSelected := FALSE;
                             END;

                             NextEnabled := ImportModeSelected OR ExpertModeSelected OR ExportModeSelected;
                           END;
                            }

    { 8   ;3   ;Field     ;
                CaptionML=[ENU=Import Classification Data from Excel;
                           NOR=Importer klassifiseringsdata fra Excel;
                           SVE=Importera klassificeringsdata frÜn Excel];
                ApplicationArea=#All;
                SourceExpr=ImportModeSelected;
                OnValidate=BEGIN
                             IF ImportModeSelected = TRUE THEN BEGIN
                               ExpertModeSelected := FALSE;
                               ExportModeSelected := FALSE;
                             END;

                             NextEnabled := ImportModeSelected OR ExpertModeSelected OR ExportModeSelected;
                           END;
                            }

    { 11  ;3   ;Group     ;
                GroupType=Group;
                InstructionalTextML=[ENU=You can also view lists of tables and fields and manually classify your data.;
                                     NOR=Du kan ogsÜ vise lister over tabeller og felt og klassifisere dataene dine manuelt.;
                                     SVE=Du kan ocksÜ visa listor med tabeller och fÑlt och manuellt klassificera dina data.] }

    { 17  ;4   ;Field     ;
                CaptionML=[ENU=Classify Data Manually;
                           NOR=Klassifiser data manuelt;
                           SVE=Klassificera data manuellt];
                ApplicationArea=#All;
                SourceExpr=ExpertModeSelected;
                OnValidate=BEGIN
                             IF ExpertModeSelected = TRUE THEN BEGIN
                               ImportModeSelected := FALSE;
                               ExportModeSelected := FALSE;
                             END;

                             NextEnabled := ImportModeSelected OR ExpertModeSelected OR ExportModeSelected;
                           END;
                            }

    { 33  ;1   ;Group     ;
                Visible=Step = Step::"Set Rules";
                GroupType=Group }

    { 61  ;2   ;Group     ;
                CaptionML=[ENU=Bulk-classify data based on its use;
                           NOR=Masseklassifisere data basert pÜ bruk;
                           SVE=Massklassificera data baserat pÜ dess anvÑndning];
                GroupType=Group }

    { 57  ;3   ;Group     ;
                CaptionML=[ENU=Examples:;
                           NOR=Eksempler:;
                           SVE=Exempel:];
                GroupType=Group;
                InstructionalTextML=[ENU=Data from posting includes G/L entries. Data on templates used to create customers, vendors, or items. Data on setup tables that configure functionality. These classifications apply only to fields that are currently Unclassified. We recommend that you review the fields before you apply the classifications.;
                                     NOR=Data fra bokfõring inkluderer finansposter. Dataˇi maler som brukes til Ü opprette kunder, leverandõrer eller varer. Data i oppsettstabeller som konfigurerer funksjonalitet. Disse klassifiseringene gjelder bare for felt som for õyeblikket er uklassifiserte. Det anbefales at du gÜr gjennom feltene fõr du bruker klassifiseringene.;
                                     SVE=Data frÜn bokfîring som omfattar redovisningstransaktioner. Data i mallar som anvÑnds fîr att skapa kunder, leverantîrer eller artiklar. Data i instÑllningstabeller som konfigurerar funktioner. Dessa klassificeringar gÑller bara fÑlt som fîr nÑrvarande Ñr oklassificerade. Vi rekommenderar att du granskar fÑlten innan du tillÑmpar klassificeringen.] }

    { 53  ;3   ;Group     ;
                GroupType=Group;
                Layout=Columns }

    { 54  ;4   ;Field     ;
                CaptionML=[ENU=Data from posting is:;
                           NOR=Data fra bokfõring er:;
                           SVE=Data frÜn bokfîring Ñr:];
                ApplicationArea=#All;
                SourceExpr=LedgerEntriesDefaultClassification }

    { 59  ;4   ;Field     ;
                DrillDown=Yes;
                ApplicationArea=#All;
                SourceExpr=ViewFieldsLbl;
                Editable=FALSE;
                Style=StrongAccent;
                StyleExpr=TRUE;
                OnDrillDown=VAR
                              DataSensitivity@1000 : Record 2000000159;
                              DataClassificationMgt@1001 : Codeunit 1750;
                            BEGIN
                              DataSensitivity.SETRANGE("Company Name",COMPANYNAME);
                              DataSensitivity.SETFILTER("Table No",DataClassificationMgt.GetTableNoFilterForTablesWhoseNameContains('Entry'));
                              PAGE.RUN(PAGE::"Data Classification Worksheet",DataSensitivity);
                            END;
                             }

    { 55  ;4   ;Field     ;
                CaptionML=[ENU=Data on templates is:;
                           NOR=Data i maler er:;
                           SVE=Data i mallar Ñr:];
                ApplicationArea=#All;
                SourceExpr=TemplatesDefaultClassification }

    { 38  ;4   ;Field     ;
                DrillDown=Yes;
                ApplicationArea=#All;
                SourceExpr=ViewFieldsLbl;
                Editable=FALSE;
                Style=StrongAccent;
                StyleExpr=TRUE;
                OnDrillDown=VAR
                              DataSensitivity@1001 : Record 2000000159;
                              DataClassificationMgt@1000 : Codeunit 1750;
                            BEGIN
                              DataSensitivity.SETRANGE("Company Name",COMPANYNAME);
                              DataSensitivity.SETFILTER("Table No",DataClassificationMgt.GetTableNoFilterForTablesWhoseNameContains('Template'));
                              PAGE.RUN(PAGE::"Data Classification Worksheet",DataSensitivity);
                            END;
                             }

    { 58  ;4   ;Field     ;
                CaptionML=[ENU=Data on setup tables is:;
                           NOR=Data i oppsettstabeller er:;
                           SVE=Data i instÑllningstabeller Ñr:];
                ApplicationArea=#All;
                SourceExpr=SetupTablesDefaultClassification }

    { 56  ;4   ;Field     ;
                DrillDown=Yes;
                ApplicationArea=#All;
                SourceExpr=ViewFieldsLbl;
                Editable=FALSE;
                Style=StrongAccent;
                StyleExpr=TRUE;
                OnDrillDown=VAR
                              DataSensitivity@1001 : Record 2000000159;
                              DataClassificationMgt@1000 : Codeunit 1750;
                            BEGIN
                              DataSensitivity.SETRANGE("Company Name",COMPANYNAME);
                              DataSensitivity.SETFILTER("Table No",DataClassificationMgt.GetTableNoFilterForTablesWhoseNameContains('Setup'));
                              PAGE.RUN(PAGE::"Data Classification Worksheet",DataSensitivity);
                            END;
                             }

    { 4   ;1   ;Group     ;
                Visible=Step = Step::Apply;
                GroupType=Group }

    { 18  ;2   ;Group     ;
                CaptionML=[ENU=Choose the tables that you want to classify;
                           NOR=Velg tabellene du vil klassifisere;
                           SVE=VÑlj vilka tabeller som du vill klassificera];
                GroupType=Group;
                InstructionalTextML=[ENU=When you classify a table, the classification applies to all fields in the table. You can choose a table to change classifications for individual fields.;
                                     NOR=NÜr du klassifiserer en tabell, gjelder klassifiseringen for alle feltene i tabellen. Du kan velge en tabell for Ü endre klassifiseringer for individuelle felt.;
                                     SVE=NÑr du klassificerar en tabell gÑller klassificeringen fîr alla fÑlt i tabellen. Du kan vÑlja en tabell om du vill Ñndra klassificeringen fîr enskilda fÑlt.] }

    { 19  ;3   ;Field     ;
                ApplicationArea=#All }

    { 20  ;3   ;Group     ;
                GroupType=Repeater }

    { 21  ;4   ;Field     ;
                Name=Include;
                ApplicationArea=#All;
                SourceExpr=Include }

    { 5   ;4   ;Field     ;
                Name=Entity;
                DrillDown=No;
                CaptionML=[ENU=Data Subject;
                           NOR=Dataemne;
                           SVE=Registrerade];
                ApplicationArea=#All;
                SourceExpr="Table Caption";
                Editable=FALSE }

    { 23  ;4   ;Field     ;
                Name=Default Data Sensitivity;
                ApplicationArea=#All;
                SourceExpr="Default Data Sensitivity" }

    { 24  ;1   ;Group     ;
                Visible=Step = Step::Verify;
                GroupType=Group }

    { 28  ;2   ;Group     ;
                CaptionML=[ENU=Good work! Now classify individual fields;
                           NOR=Bra jobba! NÜ skal du klassifisere individuelle felt;
                           SVE=Bra gjort! Nu ska du klassificera enskilda fÑlt.];
                GroupType=Group;
                InstructionalTextML=[ENU="The default classification has been added to the tables. Now you can classify individual fields in the tables, and  the entities that relate to the tables. ";
                                     NOR="Standardklassifiseringen er lagt til i tabellene. NÜ kan du klassifisere individuelle felt i tabellene og enhetene som erˇrelatert til tabellene. ";
                                     SVE="Standardklassificeringen har lagts till i tabellerna. Du kan nu klassificera enskilda fÑlt i tabellerna och enheterna som relaterar till tabellerna. "] }

    { 45  ;2   ;Group     ;
                CaptionML=[ENU=Review the classifications for all the entities before you continue!;
                           NOR=Se gjennom klassifiseringene for alle enhetene fõr du fortsetter!;
                           SVE=Granska klassificeringarna fîr alla enheter innan du fortsÑtter!];
                GroupType=Group }

    { 25  ;2   ;Group     ;
                GroupType=Repeater }

    { 26  ;3   ;Field     ;
                Name=Entity 2;
                DrillDown=No;
                CaptionML=[ENU=Data Subject;
                           NOR=Dataemne;
                           SVE=Registrerade];
                ApplicationArea=#All;
                SourceExpr="Table Caption";
                Editable=FALSE }

    { 32  ;3   ;Field     ;
                ApplicationArea=#All;
                SourceExpr=Fields;
                Editable=FALSE;
                Style=StandardAccent;
                StyleExpr=TRUE;
                OnDrillDown=VAR
                              DataSensitivity@1000 : Record 2000000159;
                            BEGIN
                              DataSensitivity.SETRANGE("Company Name",COMPANYNAME);
                              DataSensitivity.FILTERGROUP(2);
                              DataSensitivity.SETRANGE("Table No","Table No.");
                              PAGE.RUNMODAL(PAGE::"Data Classification Worksheet",DataSensitivity);

                              Reviewed := TRUE;
                              Status := Status::Reviewed;
                              CurrPage.UPDATE;
                            END;
                             }

    { 34  ;3   ;Field     ;
                ApplicationArea=#All;
                SourceExpr=Status;
                Editable=FALSE;
                StyleExpr=StatusStyle;
                OnDrillDown=VAR
                              DataSensitivity@1000 : Record 2000000159;
                            BEGIN
                              DataSensitivity.SETRANGE("Company Name",COMPANYNAME);
                              DataSensitivity.FILTERGROUP(2);
                              DataSensitivity.SETRANGE("Table No","Table No.");
                              PAGE.RUNMODAL(PAGE::"Data Classification Worksheet",DataSensitivity);

                              Reviewed := TRUE;
                              Status := Status::Reviewed;
                              CurrPage.UPDATE;
                            END;
                             }

    { 36  ;1   ;Group     ;
                Visible=Step = Step::"Verify Related Fields";
                GroupType=Group }

    { 37  ;2   ;Group     ;
                CaptionML=[ENU=We're getting there!;
                           NOR=Vi nërmer oss!;
                           SVE=Vi kommer till det!];
                GroupType=Group;
                InstructionalTextML=[ENU=Review the classifications for similar fields before you continue.;
                                     NOR=Se gjennom klassifiseringene for lignende felt fõr du fortsetter.;
                                     SVE=Granska klassificeringarna fîr liknande fÑlt innan du fortsÑtter.] }

    { 52  ;2   ;Group     ;
                GroupType=Repeater }

    { 35  ;3   ;Field     ;
                CaptionML=[ENU=Fields;
                           NOR=Felt;
                           SVE=FÑlt];
                ApplicationArea=#All;
                SourceExpr="Similar Fields Label";
                Editable=FALSE;
                OnDrillDown=VAR
                              DataSensitivity@1002 : Record 2000000159;
                              DataClassificationMgt@1000 : Codeunit 1750;
                            BEGIN
                              DataSensitivity.SETRANGE("Company Name",COMPANYNAME);
                              DataSensitivity.SETRANGE("Table No","Table No.");
                              DataSensitivity.SETFILTER("Data Sensitivity",STRSUBSTNO('%1|%2',
                                  DataSensitivity."Data Sensitivity"::Personal,
                                  DataSensitivity."Data Sensitivity"::Sensitive));
                              DataClassificationMgt.FindSimilarFields(DataSensitivity);
                              PAGE.RUNMODAL(PAGE::"Data Classification Worksheet",DataSensitivity);

                              "Similar Fields Reviewed" := TRUE;
                              "Status 2" := "Status 2"::Reviewed;
                              CurrPage.UPDATE;
                            END;
                             }

    { 22  ;3   ;Field     ;
                Name=Status 2;
                CaptionML=[ENU=Status;
                           NOR=Status;
                           SVE=Status];
                ApplicationArea=#All;
                SourceExpr="Status 2";
                Editable=FALSE;
                StyleExpr=SimilarFieldsStatusStyle;
                OnDrillDown=VAR
                              DataSensitivity@1000 : Record 2000000159;
                              DataClassificationMgt@1001 : Codeunit 1750;
                            BEGIN
                              DataSensitivity.SETRANGE("Company Name",COMPANYNAME);
                              DataSensitivity.SETRANGE("Table No","Table No.");
                              DataSensitivity.SETFILTER("Data Sensitivity",STRSUBSTNO('%1|%2',
                                  DataSensitivity."Data Sensitivity"::Personal,
                                  DataSensitivity."Data Sensitivity"::Sensitive));
                              DataClassificationMgt.FindSimilarFields(DataSensitivity);
                              PAGE.RUNMODAL(PAGE::"Data Classification Worksheet",DataSensitivity);

                              "Similar Fields Reviewed" := TRUE;
                              "Status 2" := "Status 2"::Reviewed;
                              CurrPage.UPDATE;
                            END;
                             }

    { 29  ;1   ;Group     ;
                Visible=(Step = Step::Finish) AND NOT ExportModeSelected;
                GroupType=Group }

    { 30  ;2   ;Group     ;
                CaptionML=[ENU=That's it;
                           NOR=Det var det;
                           SVE=Klart];
                GroupType=Group;
                InstructionalTextML=[ENU=We have applied the classifications to your data. If you want, you can review and update the classifications in the Data Classification Worksheet.;
                                     NOR=Vi har brukt klassifiseringene til datakilden. Hvis du vil, kan du kontrollere og oppdatere klassifiseringene i regnearket for dataklassifisering.;
                                     SVE=Vi har nu tillÑmpat klassificeringarna pÜ dina data. Om du vill kan du granska och uppdatera klassificeringarna i formulÑret fîr dataklassificeringarna.] }

    { 31  ;3   ;Field     ;
                Name=<Control30>;
                CaptionML=[ENU=Open Data Classification Worksheet;
                           NOR=èpne regneark for dataklassifisering;
                           SVE=ôppna formulÑret fîr dataklassificeringar];
                ApplicationArea=#All;
                SourceExpr=ShowWorksheet }

    { 27  ;1   ;Group     ;
                Visible=(Step = Step::Finish) AND ExportModeSelected;
                GroupType=Group }

    { 46  ;2   ;Group     ;
                CaptionML=[ENU=That's it;
                           NOR=Det var det;
                           SVE=Klart];
                GroupType=Group;
                InstructionalTextML=[ENU=The Excel worksheet is ready, and you can start classifying your data.  When you are done, run this guide again to import the updated Excel worksheet and apply the classifications.;
                                     NOR=Excel-regnearket er klart, og du kan begynne Ü klassifisere dataene. NÜr du er ferdig, kjõrer du denne veiledningen pÜ nytt for Ü importere det oppdaterte Excel-regnearket og ta i bruk klassifiseringene.;
                                     SVE=Excel-kalkylbladet Ñr klart och du kan bîrja klassificera dina data. NÑr du Ñr klar kîr du den hÑr guiden igen fîr att importera det uppdaterade Excel-kalkylbladet och tillÑmpa klassificeringarna.] }

  }
  CODE
  {
    VAR
      HelpUrlTxt@1000 : TextConst '@@@=Locked;ENU="https://go.microsoft.com/fwlink/?linkid=869249";NOR="https://go.microsoft.com/fwlink/?linkid=869249";SVE="https://go.microsoft.com/fwlink/?linkid=869249"';
      HelpLbl@1003 : TextConst 'ENU=Learn more;NOR=Finn ut mer;SVE=LÑs mer';
      ReviewFieldsErr@1015 : TextConst 'ENU=You must review the classifications for fields before you can continue.;NOR=Du mÜ se gjennom klassifiseringene for felt fõr du kan fortsette.;SVE=Du mÜste granska klassificeringarna fîr fÑlten innan du kan fortsÑtta.';
      ReviewSimilarFieldsErr@1016 : TextConst 'ENU=You must review the classifications for similar fields before you can continue.;NOR=Du mÜ se gjennom klassifiseringene for lignende felt fõr du kan fortsette.;SVE=Du mÜste granska klassificeringarna fîr liknande fÑlt innan du kan fortsÑtta.';
      MediaRepositoryStandard@1022 : Record 9400;
      MediaRepositoryDone@1021 : Record 9400;
      MediaResourcesStandard@1020 : Record 2000000182;
      MediaResourcesDone@1019 : Record 2000000182;
      ClientTypeManagement@1024 : Codeunit 4030;
      TopBannerVisible@1018 : Boolean;
      Step@1010 : 'Welcome,Choose Mode,Set Rules,Apply,Verify,Verify Related Fields,Finish';
      StatusStyle@1001 : Text;
      SimilarFieldsStatusStyle@1002 : Text;
      NextEnabled@1008 : Boolean;
      BackEnabled@1007 : Boolean;
      FinishEnabled@1006 : Boolean;
      ShowWorksheet@1005 : Boolean;
      ImportModeSelected@1027 : Boolean;
      ExpertModeSelected@1028 : Boolean;
      ExportModeSelected@1011 : Boolean;
      LedgerEntriesDefaultClassification@1009 : 'Unclassified,Sensitive,Personal,Company Confidential,Normal';
      SetupTablesDefaultClassification@1012 : 'Unclassified,Sensitive,Personal,Company Confidential,Normal';
      TemplatesDefaultClassification@1013 : 'Unclassified,Sensitive,Personal,Company Confidential,Normal';
      ViewFieldsLbl@1031 : TextConst 'ENU=View fields;NOR=Vis felt;SVE=Visa fÑlt';

    LOCAL PROCEDURE NextStep@18(Backward@1000 : Boolean);
    BEGIN
      IF NOT Backward THEN
        CheckMandatoryActions;

      IF Backward THEN BEGIN
        IF (Step = Step::Finish) AND (ImportModeSelected OR ExportModeSelected) THEN
          Step := Step::"Choose Mode"
        ELSE
          Step += -1;
      END ELSE
        Step += 1;
      ResetControls;
    END;

    LOCAL PROCEDURE ResetControls@25();
    VAR
      DataClassificationMgt@1000 : Codeunit 1750;
    BEGIN
      BackEnabled := TRUE;
      NextEnabled := TRUE;
      FinishEnabled := FALSE;
      RESET;

      IF ISEMPTY THEN
        DataClassificationMgt.GetPrivacyMasterTables(Rec);

      CASE Step OF
        Step::Welcome:
          BackEnabled := FALSE;
        Step::"Choose Mode":
          NextEnabled := ImportModeSelected OR ExpertModeSelected OR ExportModeSelected;
        Step::Verify,
        Step::"Verify Related Fields":
          SETRANGE(Include,TRUE);
        Step::Finish:
          BEGIN
            FinishEnabled := TRUE;
            NextEnabled := FALSE;
          END;
      END;
    END;

    LOCAL PROCEDURE CheckMandatoryActions@4();
    BEGIN
      IF Step = Step::"Verify Related Fields" THEN BEGIN
        SETRANGE("Similar Fields Reviewed",FALSE);
        IF FINDFIRST THEN
          ERROR(ReviewSimilarFieldsErr);
      END;
      IF Step = Step::Verify THEN BEGIN
        SETRANGE(Reviewed,FALSE);
        IF FINDFIRST THEN
          ERROR(ReviewFieldsErr);
      END;
    END;

    LOCAL PROCEDURE LoadTopBanners@40();
    BEGIN
      IF MediaRepositoryStandard.GET('AssistedSetup-NoText-400px.png',FORMAT(ClientTypeManagement.GetCurrentClientType)) AND
         MediaRepositoryDone.GET('AssistedSetupDone-NoText-400px.png',FORMAT(ClientTypeManagement.GetCurrentClientType))
      THEN
        IF MediaResourcesStandard.GET(MediaRepositoryStandard."Media Resources Ref") AND
           MediaResourcesDone.GET(MediaRepositoryDone."Media Resources Ref")
        THEN
          TopBannerVisible := MediaResourcesDone."Media Reference".HASVALUE;
    END;

    BEGIN
    END.
  }
}

