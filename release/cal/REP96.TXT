OBJECT Report 96 Copy G/L Budget
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Finanzbudget kopieren;
               ENU=Copy G/L Budget;
               NLD=Budget kopi‰ren;
               NOR=Kopier budsjett];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   IF AmountAdjustFactor = 0 THEN
                     AmountAdjustFactor := 1;

                   IF ToDateCompression = ToDateCompression::None THEN
                     ToDateCompression := ToDateCompression::Day;
                 END;

    OnPreReport=VAR
                  SelectedDim@1000 : Record 369;
                  GLSetup@1001 : Record 98;
                  GLBudgetName@1002 : Record 95;
                  ConfirmManagement@1004 : Codeunit 27;
                  Continue@1003 : Boolean;
                BEGIN
                  IF NOT NoMessage THEN
                    DimSelectionBuf.CompareDimText(3,REPORT::"Copy G/L Budget",'',ColumnDim,Text001);

                  IF (FromSource = FromSource::"G/L Budget Entry") AND (FromGLBudgetName = '') THEN
                    ERROR(Text002);

                  IF (FromSource = FromSource::"G/L Entry") AND (FromDate = '') THEN
                    ERROR(Text003);

                  IF ToGLBudgetName = '' THEN
                    ERROR(Text004);

                  Continue := TRUE;
                  GLBudgetName.SETRANGE(Name,ToGLBudgetName);
                  IF NOT GLBudgetName.FINDFIRST THEN BEGIN
                    IF NOT NoMessage THEN
                      IF NOT ConfirmManagement.ConfirmProcess(STRSUBSTNO(Text005,ToGLBudgetName),TRUE) THEN
                        Continue := FALSE;
                    IF Continue THEN BEGIN
                      GLBudgetName.INIT;
                      GLBudgetName.Name := ToGLBudgetName;
                      GLBudgetName.INSERT;
                      COMMIT;
                    END;
                  END ELSE BEGIN
                    BudgetDim1Code := GLBudgetName."Budget Dimension 1 Code";
                    BudgetDim2Code := GLBudgetName."Budget Dimension 2 Code";
                    BudgetDim3Code := GLBudgetName."Budget Dimension 3 Code";
                    BudgetDim4Code := GLBudgetName."Budget Dimension 4 Code";
                  END;

                  IF (NOT NoMessage) AND Continue THEN
                    IF NOT ConfirmManagement.ConfirmProcess(Text006,TRUE) THEN
                      Continue := FALSE;

                  IF Continue THEN BEGIN
                    SelectedDim.GetSelectedDim(USERID,3,REPORT::"Copy G/L Budget",'',TempSelectedDim);
                    IF TempSelectedDim.FIND('-') THEN
                      REPEAT
                        TempSelectedDim.Level := 0;
                        IF TempSelectedDim."Dimension Value Filter" <> '' THEN
                          IF FilterIncludesBlanks(TempSelectedDim."Dimension Value Filter") THEN
                            TempSelectedDim.Level := 1;
                        TempSelectedDim.MODIFY;
                      UNTIL TempSelectedDim.NEXT = 0;

                    ToGLBudgetEntry.LOCKTABLE;
                    IF ToGLBudgetEntry.FINDLAST THEN
                      GLBudgetEntryNo := ToGLBudgetEntry."Entry No." + 1
                    ELSE
                      GLBudgetEntryNo := 1;

                    GLSetup.GET;
                    GlobalDim1Code := GLSetup."Global Dimension 1 Code";
                    GlobalDim2Code := GLSetup."Global Dimension 2 Code";
                  END ELSE
                    CurrReport.QUIT;
                END;

    OnPostReport=VAR
                   FromGLBudgetEntry@1000 : Record 96;
                   FromGLEntry@1002 : Record 17;
                 BEGIN
                   WindowUpdateDateTime := CURRENTDATETIME;
                   Window.OPEN(Text007 + Text008 + Text009);

                   CASE FromSource OF
                     FromSource::"G/L Entry":
                       WITH FromGLEntry DO BEGIN
                         SETCURRENTKEY("G/L Account No.","Posting Date");
                         IF FromGLAccountNo <> '' THEN
                           SETFILTER("G/L Account No.",FromGLAccountNo);
                         SETFILTER("Posting Date",FromDate);
                         IF FIND('-') THEN BEGIN
                           REPEAT
                             ProcessRecord(
                               "G/L Account No.","Business Unit Code","Posting Date",Description,
                               "Dimension Set ID",Amount);
                           UNTIL NEXT = 0;
                         END;
                       END;
                     FromSource::"G/L Budget Entry":
                       WITH FromGLBudgetEntry DO BEGIN
                         SETRANGE("Budget Name",FromGLBudgetName);
                         IF FromGLAccountNo <> '' THEN
                           SETFILTER("G/L Account No.",FromGLAccountNo);
                         IF FromDate <> '' THEN
                           SETFILTER(Date,FromDate);
                         IF FINDLAST THEN
                           SETFILTER("Entry No.",'<=%1',"Entry No.");
                         SETCURRENTKEY("Budget Name","G/L Account No.",Description,Date);
                         IF FINDSET THEN
                           REPEAT
                             ProcessRecord(
                               "G/L Account No.","Business Unit Code",Date,Description,
                               "Dimension Set ID",Amount);
                           UNTIL NEXT = 0;
                       END;
                   END;
                   InsertGLBudgetEntry;
                   Window.CLOSE;

                   IF NOT NoMessage THEN
                     MESSAGE(Text010);
                 END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 12  ;2   ;Group     ;
                  CaptionML=[DEU=Kopieren von;
                             ENU=Copy from;
                             NLD=Kopi‰ren van;
                             NOR=Kopier fra;
                             SVE=Kopiera fr†n] }

      { 15  ;3   ;Field     ;
                  Name=Source;
                  CaptionML=[DEU=Herkunft;
                             ENU=Source;
                             NLD=Bron;
                             NOR=Kilde;
                             SVE=K„lla];
                  ToolTipML=[DEU=Gibt an, welche Art von Betr„gen Sie in ein neues Budget kopieren m”chten. Sie k”nnen Sachposten oder Finanzbudgetposten w„hlen.;
                             ENU=Specifies which kind of amounts that you want to copy to a new budget. You can select either general ledger entries or general ledger budget entries.;
                             NLD=Hiermee wordt opgegeven welk soort bedragen u naar een nieuw budget wilt kopi‰ren. U kunt grootboekposten of grootboekbudgetposten selecteren.;
                             NOR=Angir hvilke bel›p du vil kopiere til et nytt budsjett. Du kan velge enten finansposter eller finansbudsjettposter.];
                  OptionCaptionML=[DEU=Sachposten,Finanzbudgetposten;
                                   ENU=G/L Entry,G/L Budget Entry;
                                   NLD=Grootboekpost,Grootboekbudgetpost;
                                   NOR=Finanspost,Finansbudsjettpost];
                  ApplicationArea=#Suite;
                  SourceExpr=FromSource;
                  OnValidate=BEGIN
                               IF FromSource = FromSource::"G/L Entry" THEN
                                 FromGLBudgetName := '';
                             END;
                              }

      { 24  ;3   ;Field     ;
                  CaptionML=[DEU=Budgetname;
                             ENU=Budget Name;
                             NLD=Budget;
                             NOR=Budsjettnavn;
                             SVE=Budgetnamn];
                  ToolTipML=[DEU=Gibt den Namen des Budgets an.;
                             ENU=Specifies the name of the budget.;
                             NLD=Hiermee wordt de naam van het budget opgegeven.;
                             NOR=Angir navnet p† budsjettet.;
                             SVE=Anger budgetens namn.];
                  ApplicationArea=#Suite;
                  SourceExpr=FromGLBudgetName;
                  TableRelation="G/L Budget Name";
                  OnValidate=BEGIN
                               IF (FromGLBudgetName <> '') AND (FromSource = FromSource::"G/L Entry") THEN
                                 FromSource := FromSource::"G/L Budget Entry";
                             END;
                              }

      { 22  ;3   ;Field     ;
                  Name=GLAccountNo;
                  CaptionML=[DEU=Sachkontonr.;
                             ENU=G/L Account No.;
                             NLD=Grootboekrekeningnr.;
                             NOR=Finanskontonr.;
                             SVE=Redovisningskontonr];
                  ToolTipML=[DEU=Legt die Finanzbuchhaltungskonten fest, die von der Stapelverarbeitung bearbeitet werden.;
                             ENU=Specifies the G/L account or accounts that the batch job will process.;
                             NLD=Hiermee worden de grootboekrekeningen opgegeven die worden verwerkt door de batchverwerking.;
                             NOR=Angir finanskontoen eller -kontiene som kj›rselen skal behandle.];
                  ApplicationArea=#Suite;
                  SourceExpr=FromGLAccountNo;
                  TableRelation="G/L Account" }

      { 9   ;3   ;Field     ;
                  Name=Date;
                  CaptionML=[DEU=Datum;
                             ENU=Date;
                             NLD=Datum;
                             NOR=Dato;
                             SVE=Datum];
                  ToolTipML=[DEU=Gibt das Datum an.;
                             ENU=Specifies the date.;
                             NLD=Hiermee wordt de datum opgegeven.;
                             NOR=Angir datoen.;
                             SVE=Anger datumet.];
                  ApplicationArea=#Suite;
                  SourceExpr=FromDate;
                  OnValidate=VAR
                               GLAcc@1001 : Record 15;
                               TextManagement@1000 : Codeunit 41;
                             BEGIN
                               TextManagement.MakeDateFilter(FromDate);
                               GLAcc.SETFILTER("Date Filter",FromDate);
                               FromDate := GLAcc.GETFILTER("Date Filter");
                             END;
                              }

      { 26  ;3   ;Field     ;
                  CaptionML=[DEU=Ultimoposten;
                             ENU=Closing Entries;
                             NLD=Ultimoposten;
                             NOR=Avslutningsposter;
                             SVE=rsavslutstransaktioner];
                  ToolTipML=[DEU=Legt fest, ob im angezeigten Saldo die Ultimoposten enthalten sein sollen. Wenn Sie Betr„ge fr GuV Konten aus abgeschlossenen Gesch„ftsjahren einsehen m”chten, mssen Sie die Ultimoposten ausschlieáen.;
                             ENU=Specifies whether the balance shown will include closing entries. If you want to see the amounts on income statement accounts in closed years, you must exclude closing entries.;
                             NLD=Hiermee wordt opgegeven of in het weergegeven saldo ultimoposten moeten worden opgenomen. Als u de bedragen op de resultatenrekeningen van afgesloten jaren wilt zien, moet u de ultimoposten uitsluiten.;
                             NOR=Angir om saldoen som vises skal inkludere avslutningsposter. Hvis du vil se bel›pene i resultatkontiene i avsluttede †r, m† du utelukke avslutningsposter.;
                             SVE=Anger om saldot som visas ska inkludera †rsavslutstransaktioner eller inte. Om du vill visa beloppen f”r resultatr„kningskonton f”r avslutade †r ska du inte inkludera †rsavslutstransaktioner.];
                  OptionCaptionML=[DEU=Einschlieálich,Ausschlieálich;
                                   ENU=Include,Exclude;
                                   NLD=Inclusief,Exclusief;
                                   NOR=Inkluder,Ekskluder;
                                   SVE=Inkludera,Exkludera];
                  ApplicationArea=#Suite;
                  SourceExpr=FromClosingEntryFilter }

      { 1   ;3   ;Field     ;
                  CaptionML=[DEU=Dimensionen;
                             ENU=Dimensions;
                             NLD=Dimensies;
                             NOR=Dimensjoner;
                             SVE=Dimensioner];
                  ToolTipML=[DEU=Legt Dimensionen fest, wie etwa einen Bereich, ein Projekt oder eine Abteilung, die Sie Verkaufs- oder Einkaufsbelegen zuweisen k”nnen, um die Kosten zu verteilen und den Transaktionsverlauf zu analysieren.;
                             ENU=Specifies dimensions, such as area, project, or department, that you can assign to sales and purchase documents to distribute costs and analyze transaction history.;
                             NLD=Hiermee worden dimensies, zoals gebied, project of afdeling, opgegeven die u kunt toewijzen aan verkoop- en inkoopdocumenten om de kosten te verdelen en de transactiegeschiedenis te analyseren.;
                             NOR=Angir dimensjoner, for eksempel omr†de, prosjekt eller avdeling, som du kan tilordne til salgs- og kj›psdokumenter for † distribuere kostnader og analysere transaksjonshistorikk.];
                  ApplicationArea=#Dimensions;
                  SourceExpr=ColumnDim;
                  Editable=FALSE;
                  OnAssistEdit=VAR
                                 DimSelectionBuf@1001 : Record 368;
                               BEGIN
                                 DimSelectionBuf.SetDimSelectionChange(3,REPORT::"Copy G/L Budget",ColumnDim);
                               END;
                                }

      { 11  ;2   ;Group     ;
                  CaptionML=[DEU=Kopieren in;
                             ENU=Copy to;
                             NLD=Kopi‰ren naar;
                             NOR=Kopier til;
                             SVE=Kopiera till] }

      { 7   ;3   ;Field     ;
                  Name=BudgetName;
                  CaptionML=[DEU=Budgetname;
                             ENU=Budget Name;
                             NLD=Budget;
                             NOR=Budsjettnavn;
                             SVE=Budgetnamn];
                  ToolTipML=[DEU=Gibt den Namen des Budgets an.;
                             ENU=Specifies the name of the budget.;
                             NLD=Hiermee wordt de naam van het budget opgegeven.;
                             NOR=Angir navnet p† budsjettet.;
                             SVE=Anger budgetens namn.];
                  ApplicationArea=#Suite;
                  SourceExpr=ToGLBudgetName;
                  TableRelation="G/L Budget Name" }

      { 5   ;3   ;Field     ;
                  CaptionML=[DEU=Sachkontonr.;
                             ENU=G/L Account No.;
                             NLD=Grootboekrekeningnr.;
                             NOR=Finanskontonr.;
                             SVE=Redovisningskontonr];
                  ToolTipML=[DEU=Legt die Finanzbuchhaltungskonten fest, die von der Stapelverarbeitung bearbeitet werden.;
                             ENU=Specifies the G/L account or accounts that the batch job will process.;
                             NLD=Hiermee worden de grootboekrekeningen opgegeven die worden verwerkt door de batchverwerking.;
                             NOR=Angir finanskontoen eller -kontiene som kj›rselen skal behandle.];
                  ApplicationArea=#Suite;
                  SourceExpr=ToGLAccountNo;
                  TableRelation="G/L Account";
                  OnValidate=BEGIN
                               ToGLAccountNoOnAfterValidate;
                             END;
                              }

      { 13  ;2   ;Group     ;
                  CaptionML=[DEU=Anwenden von;
                             ENU=Apply;
                             NLD=Toepassen;
                             NOR=Utlign;
                             SVE=Verkst„ll] }

      { 18  ;3   ;Field     ;
                  CaptionML=[DEU=Korrekturfaktor;
                             ENU=Adjustment Factor;
                             NLD=Herwaarderingsfactor;
                             NOR=Justeringsfaktor];
                  ToolTipML=[DEU=Gibt einen Korrekturfaktor an, mit dem die Betr„ge, die Sie kopieren m”chten, multipliziert werden. Durch die Eingabe eines Korrekturfaktors k”nnen Sie die Betr„ge erh”hen oder verringern, die in das neue Budget kopiert werden.;
                             ENU=Specifies an adjustment factor to multiply the amounts that you want to copy. By entering an adjustment factor, you can increase or decrease the amounts that are copied to the new budget.;
                             NLD=Hiermee wordt een herwaarderingsfactor opgegeven om de bedragen te vermenigvuldigen die u wilt kopi‰ren. Als u een herwaarderingsfactor invoert, kunt u de bedragen die naar het nieuwe budget worden gekopieerd, verhogen of verlagen.;
                             NOR=Angir en justeringsfaktor for † multiplisere bel›pene du vil kopiere. Ved † angi en justeringsfaktor, kan du ›ke eller minske bel›pene som kopieres til det nye budsjettet.];
                  ApplicationArea=#Suite;
                  DecimalPlaces=0:5;
                  NotBlank=Yes;
                  SourceExpr=AmountAdjustFactor;
                  MinValue=0 }

      { 14  ;3   ;Field     ;
                  CaptionML=[DEU=Rundungsmethode;
                             ENU=Rounding Method;
                             NLD=Afrondingsmethode;
                             NOR=Avrundingsmetode;
                             SVE=Avrundningsmetod];
                  ToolTipML=[DEU=Gibt einen Code fr die Rundungsmethode an, die Sie auf die Posten anwenden m”chten, wenn Sie sie in das neue Budget kopieren.;
                             ENU=Specifies a code for the rounding method that you want to apply to entries when you copy them to a new budget.;
                             NLD=Hiermee wordt een code opgegeven voor de afrondingsmethode die u wilt toepassen op posten wanneer u ze naar een nieuw budget kopieert.;
                             NOR=Angir en kode for avrundingsmetoden du vil bruke p† poster n†r du kopierer dem til et nytt budsjett.];
                  ApplicationArea=#Suite;
                  SourceExpr=RoundingMethod.Code;
                  TableRelation="Rounding Method" }

      { 20  ;3   ;Field     ;
                  CaptionML=[DEU=Datumswechsel Formel;
                             ENU=Date Change Formula;
                             NLD=Datumformule;
                             NOR=Formel for datoendring;
                             SVE=Datum„ndringsformel];
                  ToolTipML=[DEU=Gibt an, wie die Datumsangaben der kopierten Posten ge„ndert werden. Verwenden Sie hierzu eine Datumsformel. Wenn Sie beispielsweise das Budget der letzten Woche in die laufende Woche kopieren m”chten, verwenden Sie die Formel 1W (eine Woche).;
                             ENU="Specifies how the dates on the entries that are copied will be changed. Use a date formula; for example, to copy last week's budget to this week, use the formula 1W (one week).";
                             NLD=Hiermee wordt opgegeven hoe de datums op de gekopieerde posten worden gewijzigd. Als u bijvoorbeeld het budget van de vorige week wilt kopi‰ren naar deze week, gebruikt u de datumformule 1W (‚‚n week).;
                             NOR=Angir hvordan datoene p† postene som kopieres, blir endret. Bruk en datoformel. Hvis du for eksempel vil kopiere siste ukes budsjett til denne uken, bruker du formelen 1U (‚n uke).];
                  ApplicationArea=#Suite;
                  SourceExpr=DateAdjustExpression }

      { 3   ;3   ;Field     ;
                  CaptionML=[DEU=Datumskomprimierung;
                             ENU=Date Compression;
                             NLD=Datumcompressie;
                             NOR=Datokomprimering;
                             SVE=Datumkomprimering];
                  ToolTipML=[DEU=Gibt die L„nge der Periode an, innerhalb der Posten zusammengefasst werden. W„hlen Sie das Feld, um die Optionen anzuzeigen.;
                             ENU=Specifies the length of the period whose entries are combined. To see the options, choose the field.;
                             NLD=Hiermee wordt de lengte opgegeven van de periode waarvoor de posten worden gecombineerd. Kies het veld om de opties weer te geven.;
                             NOR=Angir lengden p† perioden som har poster som kombineres. Velg feltet for † vise alternativene.];
                  OptionCaptionML=[DEU=Keine,Tag,Woche,Monat,Quartal,Jahr,Periode;
                                   ENU=None,Day,Week,Month,Quarter,Year,Period;
                                   NLD=Geen,Dag,Week,Maand,Kwartaal,Jaar,Periode;
                                   NOR=Ingen,Dag,Uke,M†ned,Kvartal,r,Periode;
                                   SVE=Ingen,Dag,Vecka,M†nad,Kvartal,r,Period];
                  ApplicationArea=#Suite;
                  SourceExpr=ToDateCompression }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'DEU=Dimensionen;ENU=Dimensions;NLD=Dimensies;NOR=Dimensjoner;SVE=Dimensioner';
      Text002@1001 : TextConst 'DEU=Sie mssen einen Budgetnamen angeben, von dem kopiert werden soll.;ENU=You must specify a budget name to copy from.;NLD=U moet de naam van het budget opgeven waarvan u wilt kopi‰ren.;NOR=Du m† angi hvilket budsjettnavn det skal kopieres fra.';
      Text003@1002 : TextConst 'DEU=Sie mssen ein Datumsintervall angeben, von dem kopiert werden soll.;ENU=You must specify a date interval to copy from.;NLD=U moet het datuminterval opgeven waaruit u wilt kopi‰ren.;NOR=Du m† angi hvilket datointervall det skal kopieres fra.';
      Text004@1003 : TextConst 'DEU=Sie mssen einen Budgetnamen angeben, in den kopiert werden soll.;ENU=You must specify a budget name to copy to.;NLD=U moet de naam van het budget opgeven waarnaar u wilt kopi‰ren.;NOR=Du m† angi hvilket budsjettnavn det skal kopieres til.';
      Text005@1004 : TextConst 'DEU=M”chten Sie den Finanzbudgetnamen %1 erstellen?;ENU=Do you want to create G/L Budget Name %1?;NLD=Wilt u budgetnaam %1 maken?;NOR=Vil du opprette finansbudsjettnavnet %1?';
      Text006@1005 : TextConst 'DEU=M”chten Sie mit dem Kopieren beginnen?;ENU=Do you want to start the copy?;NLD=Wilt u beginnen te kopi‰ren?;NOR=Vil du starte kopieringen?';
      Text007@1006 : TextConst 'DEU=Budget wird kopiert ...\\;ENU=Copying budget...\\;NLD=Kopi‰ren budget...\\;NOR=Kopierer budsjett...\\';
      Text008@1007 : TextConst 'DEU=Sachkontonr.    #1####################\;ENU=G/L Account No. #1####################\;NLD=Grootboekrek.   #1####################\;NOR=Finanskontonr.  #1####################\';
      Text009@1008 : TextConst 'DEU=Buchungsdatum   #2######;ENU=Posting Date    #2######;NLD=Boekingsdatum   #2######;NOR=Bokf›ringsdato  #2######';
      Text010@1009 : TextConst 'DEU=Das Budget wurde erfolgreich kopiert.;ENU=Budget has been successfully copied.;NLD=Budget is probleemloos gekopieerd.;NOR=Budsjettet er kopiert.';
      Text011@1075 : TextConst 'DEU=Es kann nur ein einzelnes Sachkonto definiert werden.;ENU=You can define only one G/L Account.;NLD=Er mag slechts ‚‚n grootboekrekening worden gedefinieerd.;NOR=Du kan bare definere ‚n finanskonto.';
      ToGLBudgetEntry@1010 : Record 96;
      TempGLBudgetEntry@1012 : TEMPORARY Record 96;
      TempSelectedDim@1015 : TEMPORARY Record 369;
      TempDimEntryBuffer@1044 : TEMPORARY Record 373;
      DimSetEntry@1045 : Record 480;
      TempDimSetEntry@1051 : TEMPORARY Record 480;
      RoundingMethod@1018 : Record 42;
      DimSelectionBuf@1019 : Record 368;
      DimMgt@1020 : Codeunit 408;
      Window@1013 : Dialog;
      FromDate@1021 : Text;
      FromSource@1022 : 'G/L Entry,G/L Budget Entry';
      FromGLBudgetName@1023 : Code[10];
      FromGLAccountNo@1024 : Code[250];
      FromClosingEntryFilter@1025 : 'Include,Exclude';
      ToGLBudgetName@1026 : Code[10];
      ToGLAccountNo@1027 : Code[20];
      ToBUCode@1028 : Code[20];
      ToDateCompression@1029 : 'None,Day,Week,Month,Quarter,Year,Period';
      ColumnDim@1030 : Text[250];
      AmountAdjustFactor@1031 : Decimal;
      DateAdjustExpression@1032 : DateFormula;
      GLBudgetEntryNo@1033 : Integer;
      GlobalDim1Code@1034 : Code[20];
      GlobalDim2Code@1035 : Code[20];
      BudgetDim1Code@1036 : Code[20];
      BudgetDim2Code@1037 : Code[20];
      BudgetDim3Code@1038 : Code[20];
      BudgetDim4Code@1039 : Code[20];
      NoMessage@1040 : Boolean;
      PrevPostingDate@1041 : Date;
      PrevCalculatedPostingDate@1042 : Date;
      OldGLAccountNo@1053 : Code[20];
      OldPostingDate@1052 : Date;
      OldPostingDescription@1011 : Text[100];
      OldBUCode@1050 : Code[20];
      WindowUpdateDateTime@1048 : DateTime;

    LOCAL PROCEDURE ProcessRecord@3(GLAccNo@1000 : Code[20];BUCode@1001 : Code[20];PostingDate@1002 : Date;PostingDescription@1003 : Text[100];DimSetID@1004 : Integer;Amount@1007 : Decimal);
    VAR
      NewDate@1008 : Date;
      NewDimSetID@1006 : Integer;
    BEGIN
      IF CURRENTDATETIME - WindowUpdateDateTime >= 750 THEN BEGIN
        Window.UPDATE(1,GLAccNo);
        Window.UPDATE(2,PostingDate);
        WindowUpdateDateTime := CURRENTDATETIME;
      END;
      NewDate := CalculatePeriodStart(PostingDate);
      IF (FromClosingEntryFilter = FromClosingEntryFilter::Exclude) AND (NewDate = CLOSINGDATE(NewDate)) THEN
        EXIT;

      IF (FromSource = FromSource::"G/L Entry") AND
         (ToDateCompression <> ToDateCompression::None)
      THEN
        PostingDescription := '';

      IF OldGLAccountNo = '' THEN BEGIN
        OldGLAccountNo := GLAccNo;
        OldPostingDate := NewDate;
        OldBUCode := BUCode;
        OldPostingDescription := PostingDescription;
      END;

      IF (GLAccNo <> OldGLAccountNo) OR
         (NewDate <> OldPostingDate) OR
         (BUCode <> OldBUCode) OR
         (PostingDescription <> OldPostingDescription) OR
         (ToDateCompression = ToDateCompression::None)
      THEN BEGIN
        OldGLAccountNo := GLAccNo;
        OldPostingDate := NewDate;
        OldBUCode := BUCode;
        OldPostingDescription := PostingDescription;
        InsertGLBudgetEntry;
      END;

      NewDimSetID := DimSetID;
      IF NOT IncludeFromEntry(NewDimSetID) THEN
        EXIT;

      UpdateTempGLBudgetEntry(GLAccNo,NewDate,Amount,PostingDescription,BUCode,NewDimSetID);
    END;

    LOCAL PROCEDURE UpdateTempGLBudgetEntry@6(GLAccNo@1000 : Code[20];PostingDate@1001 : Date;Amount@1002 : Decimal;Description@1003 : Text[100];BUCode@1004 : Code[20];DimSetID@1007 : Integer);
    BEGIN
      TempGLBudgetEntry.SETRANGE("G/L Account No.",GLAccNo);
      TempGLBudgetEntry.SETRANGE(Date,PostingDate);
      TempGLBudgetEntry.SETRANGE(Description,Description);
      TempGLBudgetEntry.SETRANGE("Business Unit Code",BUCode);
      TempGLBudgetEntry.SETRANGE("Dimension Set ID",DimSetID);

      IF TempGLBudgetEntry.FINDFIRST THEN BEGIN
        TempGLBudgetEntry.Amount := TempGLBudgetEntry.Amount + Amount;
        TempGLBudgetEntry.MODIFY;
        TempGLBudgetEntry.RESET;
      END ELSE BEGIN
        TempGLBudgetEntry.RESET;
        IF TempGLBudgetEntry.FINDLAST THEN
          TempGLBudgetEntry."Entry No." := TempGLBudgetEntry."Entry No." + 1
        ELSE
          TempGLBudgetEntry."Entry No." := 1;
        TempGLBudgetEntry."Dimension Set ID" := DimSetID;
        TempGLBudgetEntry."G/L Account No." := GLAccNo;
        TempGLBudgetEntry.Date := PostingDate;
        TempGLBudgetEntry.Amount := Amount;
        TempGLBudgetEntry.Description := Description;
        TempGLBudgetEntry."Business Unit Code" := BUCode;
        TempGLBudgetEntry.INSERT;
      END;
    END;

    LOCAL PROCEDURE InsertGLBudgetEntry@2();
    VAR
      Sign@1000 : Decimal;
    BEGIN
      IF TempGLBudgetEntry.FIND('-') THEN BEGIN
        REPEAT
          IF TempGLBudgetEntry.Amount <> 0 THEN BEGIN
            ToGLBudgetEntry := TempGLBudgetEntry;
            ToGLBudgetEntry."Entry No." := GLBudgetEntryNo;
            GLBudgetEntryNo := GLBudgetEntryNo + 1;
            ToGLBudgetEntry."Budget Name" := ToGLBudgetName;
            IF ToGLAccountNo <> '' THEN
              ToGLBudgetEntry."G/L Account No." := ToGLAccountNo;
            IF ToBUCode <> '' THEN
              ToGLBudgetEntry."Business Unit Code" := ToBUCode;
            ToGLBudgetEntry."User ID" := USERID;
            ToGLBudgetEntry."Last Date Modified" := TODAY;
            ToGLBudgetEntry.Date := TempGLBudgetEntry.Date;
            ToGLBudgetEntry.Amount := ROUND(TempGLBudgetEntry.Amount * AmountAdjustFactor);
            IF RoundingMethod.Code <> '' THEN BEGIN
              IF ToGLBudgetEntry.Amount >= 0 THEN
                Sign := 1
              ELSE
                Sign := -1;
              RoundingMethod."Minimum Amount" := ABS(ToGLBudgetEntry.Amount);
              IF RoundingMethod.FIND('=<') THEN BEGIN
                ToGLBudgetEntry.Amount :=
                  ToGLBudgetEntry.Amount + Sign * RoundingMethod."Amount Added Before";
                IF RoundingMethod.Precision > 0 THEN
                  ToGLBudgetEntry.Amount :=
                    Sign *
                    ROUND(
                      ABS(
                        ToGLBudgetEntry.Amount),RoundingMethod.Precision,COPYSTR('=><',
                        RoundingMethod.Type + 1,1));
                ToGLBudgetEntry.Amount :=
                  ToGLBudgetEntry.Amount + Sign * RoundingMethod."Amount Added After";
              END;
            END;
            DimSetEntry.RESET;
            DimSetEntry.SETRANGE("Dimension Set ID",TempGLBudgetEntry."Dimension Set ID");
            IF DimSetEntry.FIND('-') THEN BEGIN
              REPEAT
                IF DimSetEntry."Dimension Code" = GlobalDim1Code THEN
                  ToGLBudgetEntry."Global Dimension 1 Code" := DimSetEntry."Dimension Value Code";
                IF DimSetEntry."Dimension Code" = GlobalDim2Code THEN
                  ToGLBudgetEntry."Global Dimension 2 Code" := DimSetEntry."Dimension Value Code";
                IF DimSetEntry."Dimension Code" = BudgetDim1Code THEN
                  ToGLBudgetEntry."Budget Dimension 1 Code" := DimSetEntry."Dimension Value Code";
                IF DimSetEntry."Dimension Code" = BudgetDim2Code THEN
                  ToGLBudgetEntry."Budget Dimension 2 Code" := DimSetEntry."Dimension Value Code";
                IF DimSetEntry."Dimension Code" = BudgetDim3Code THEN
                  ToGLBudgetEntry."Budget Dimension 3 Code" := DimSetEntry."Dimension Value Code";
                IF DimSetEntry."Dimension Code" = BudgetDim4Code THEN
                  ToGLBudgetEntry."Budget Dimension 4 Code" := DimSetEntry."Dimension Value Code";
              UNTIL DimSetEntry.NEXT = 0;
            END;
            ToGLBudgetEntry.INSERT;
          END;
        UNTIL TempGLBudgetEntry.NEXT = 0;
      END;
      TempGLBudgetEntry.RESET;
      TempGLBudgetEntry.DELETEALL;
    END;

    [External]
    PROCEDURE Initialize@1(FromSource2@1000 : Option;FromGLBudgetName2@1001 : Code[10];FromGLAccountNo2@1002 : Code[250];FromDate2@1003 : Text[30];ToGlBudgetName2@1004 : Code[10];ToGLAccountNo2@1005 : Code[20];ToBUCode2@1006 : Code[20];AmountAdjustFactor2@1007 : Decimal;RoundingMethod2@1008 : Code[10];DateAdjustExpression2@1009 : DateFormula;NoMessage2@1010 : Boolean);
    BEGIN
      FromSource := FromSource2;
      FromGLBudgetName := FromGLBudgetName2;
      FromGLAccountNo := FromGLAccountNo2;
      FromDate := FromDate2;
      ToGLBudgetName := ToGlBudgetName2;
      ToGLAccountNo := ToGLAccountNo2;
      ToBUCode := ToBUCode2;
      AmountAdjustFactor := AmountAdjustFactor2;
      RoundingMethod.Code := RoundingMethod2;
      DateAdjustExpression := DateAdjustExpression2;
      NoMessage := NoMessage2;
    END;

    LOCAL PROCEDURE CalculatePeriodStart@4(PostingDate@1000 : Date) : Date;
    VAR
      AccountingPeriod@1001 : Record 50;
    BEGIN
      IF FORMAT(DateAdjustExpression) <> '' THEN
        IF PostingDate = CLOSINGDATE(PostingDate) THEN
          PostingDate := CLOSINGDATE(CALCDATE(DateAdjustExpression,NORMALDATE(PostingDate)))
        ELSE
          PostingDate := CALCDATE(DateAdjustExpression,PostingDate);
      IF PostingDate = CLOSINGDATE(PostingDate) THEN
        EXIT(PostingDate);

      CASE ToDateCompression OF
        ToDateCompression::Week:
          PostingDate := CALCDATE('<CW+1D-1W>',PostingDate);
        ToDateCompression::Month:
          PostingDate := CALCDATE('<CM+1D-1M>',PostingDate);
        ToDateCompression::Quarter:
          PostingDate := CALCDATE('<CQ+1D-1Q>',PostingDate);
        ToDateCompression::Year:
          PostingDate := CALCDATE('<CY+1D-1Y>',PostingDate);
        ToDateCompression::Period:
          BEGIN
            IF PostingDate <> PrevPostingDate THEN BEGIN
              PrevPostingDate := PostingDate;
              AccountingPeriod.SETRANGE("Starting Date",0D,PostingDate);
              IF AccountingPeriod.FINDLAST THEN BEGIN
                PrevCalculatedPostingDate := AccountingPeriod."Starting Date"
              END ELSE
                PrevCalculatedPostingDate := PostingDate;
            END;
            PostingDate := PrevCalculatedPostingDate;
          END;
      END;
      EXIT(PostingDate);
    END;

    LOCAL PROCEDURE FilterIncludesBlanks@17(TheFilter@1000 : Code[250]) : Boolean;
    VAR
      TempDimBuf2@1001 : TEMPORARY Record 360;
    BEGIN
      WITH TempDimBuf2 DO BEGIN
        DELETEALL; // Necessary because of C/SIDE error
        INIT;
        INSERT;
        SETFILTER("Dimension Code",TheFilter);
        EXIT(FINDFIRST);
      END;
    END;

    LOCAL PROCEDURE IncludeFromEntry@5(VAR DimSetID@1000 : Integer) : Boolean;
    VAR
      IncludeEntry@1001 : Boolean;
    BEGIN
      IF TempDimEntryBuffer.GET(DimSetID) THEN BEGIN
        DimSetID := TempDimEntryBuffer."Dimension Entry No.";
        EXIT(TRUE);
      END;
      TempDimEntryBuffer."No." := DimSetID;

      IncludeEntry := TRUE;
      DimSetEntry.SETRANGE("Dimension Set ID",DimSetID);
      TempDimSetEntry.RESET;
      TempDimSetEntry.DELETEALL;
      IF TempSelectedDim.FIND('-') THEN
        REPEAT
          DimSetEntry.INIT;
          DimSetEntry.SETRANGE("Dimension Code",TempSelectedDim."Dimension Code");
          IF TempSelectedDim."Dimension Value Filter" <> '' THEN
            DimSetEntry.SETFILTER("Dimension Value Code",TempSelectedDim."Dimension Value Filter");
          IF DimSetEntry.FINDFIRST THEN BEGIN
            TempDimSetEntry := DimSetEntry;
            TempDimSetEntry."Dimension Set ID" := 0;
            IF TempSelectedDim."New Dimension Value Code" <> '' THEN
              TempDimSetEntry.VALIDATE("Dimension Value Code",TempSelectedDim."New Dimension Value Code");
            TempDimSetEntry.INSERT(TRUE);
          END ELSE BEGIN
            IF TempSelectedDim."Dimension Value Filter" <> '' THEN
              IF TempSelectedDim.Level = 1 THEN BEGIN
                DimSetEntry.SETRANGE("Dimension Value Code");
                IncludeEntry := NOT DimSetEntry.FINDFIRST;
              END ELSE
                IncludeEntry := FALSE;
            IF IncludeEntry AND (TempSelectedDim."New Dimension Value Code" <> '') THEN BEGIN
              TempDimSetEntry."Dimension Set ID" := 0;
              TempDimSetEntry."Dimension Code" := COPYSTR(TempSelectedDim."Dimension Code",1,20);
              TempDimSetEntry.VALIDATE("Dimension Value Code",TempSelectedDim."New Dimension Value Code");
              TempDimSetEntry.INSERT(TRUE);
            END;
          END;
          DimSetEntry.SETRANGE("Dimension Code");
          DimSetEntry.SETRANGE("Dimension Value Code");
        UNTIL (TempSelectedDim.NEXT = 0) OR NOT IncludeEntry;
      IF IncludeEntry THEN BEGIN
        DimSetID := DimMgt.GetDimensionSetID(TempDimSetEntry);
        TempDimEntryBuffer."Dimension Entry No." := DimSetID;
        TempDimEntryBuffer.INSERT;
        EXIT(TRUE);
      END;
      EXIT(FALSE);
    END;

    [External]
    PROCEDURE InitializeRequest@7(FromSource2@1000 : Option;FromGLBudgetName2@1001 : Code[10];FromGLAccountNo2@1002 : Code[250];FromDate2@1003 : Text[30];FromClosingEntryFilter2@1011 : Option;DimensionText@1006 : Text[250];ToGlBudgetName2@1004 : Code[10];ToGLAccountNo2@1005 : Code[20];AmountAdjustFactor2@1007 : Decimal;RoundingMethod2@1008 : Code[10];DateAdjustExpression2@1009 : DateFormula;ToDateCompression2@1010 : Option);
    BEGIN
      FromSource := FromSource2;
      FromGLBudgetName := FromGLBudgetName2;
      FromGLAccountNo := FromGLAccountNo2;
      FromDate := FromDate2;
      FromClosingEntryFilter := FromClosingEntryFilter2;
      ColumnDim := DimensionText;
      ToGLBudgetName := ToGlBudgetName2;
      ToGLAccountNo := ToGLAccountNo2;
      AmountAdjustFactor := AmountAdjustFactor2;
      RoundingMethod.Code := RoundingMethod2;
      DateAdjustExpression := DateAdjustExpression2;
      ToDateCompression := ToDateCompression2;
    END;

    LOCAL PROCEDURE ToGLAccountNoOnAfterValidate@1130();
    VAR
      GLAccount@1110 : Record 15;
    BEGIN
      IF ToGLAccountNo <> '' THEN BEGIN
        GLAccount.GET(ToGLAccountNo);
        MESSAGE(Text011)
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

