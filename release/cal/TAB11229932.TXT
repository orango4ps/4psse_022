OBJECT Table 11229932 Plant Part (Plant Level)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               OnInsertNewPlantPart;
             END;

    OnDelete=VAR
               PlantPartProblem@1100529000 : Record 11229931;
             BEGIN
               CASE Level OF
                 Level::PlantType:
                   BEGIN
                     PlantPartProblem.SETRANGE(Level, PlantPartProblem.Level::PlantType);
                     PlantPartProblem.SETRANGE("Plant Type", "Plant Type");
                     PlantPartProblem.DELETEALL;
                   END;
                 Level::PlantNumber:
                   BEGIN
                     PlantPartProblem.SETRANGE(Level, PlantPartProblem.Level::PlantNumber);
                     PlantPartProblem.SETRANGE("Plant Type", "Plant Type");
                     PlantPartProblem.SETRANGE("Plant No.", "Plant No.");
                     PlantPartProblem.DELETEALL;
                   END;
               END;
             END;

    OnRename=BEGIN
               xRec.CALCFIELDS(Problems);
               IF xRec.Problems > 0 THEN
                 ERROR(Text001, FIELDCAPTION(Problems));
               OnInsertNewPlantPart;
             END;

    CaptionML=ENU=Plant Part (Plant Level);
  }
  FIELDS
  {
    { 10  ;   ;Level               ;Option        ;CaptionML=[ENU=Level;
                                                              NOR=Niv†;
                                                              SVE=Niv†];
                                                   OptionCaptionML=ENU=,Plant Type,Plant Number;
                                                   OptionString=,PlantType,PlantNumber }
    { 20  ;   ;Plant Type          ;Code20        ;TableRelation="Plant Type";
                                                   CaptionML=[ENU=Plant Type;
                                                              NOR=Maskintype;
                                                              SVE=Maskintyp] }
    { 30  ;   ;Plant No.           ;Code10        ;TableRelation="Plant Number".No. WHERE (Plant Type=FIELD(Plant Type));
                                                   CaptionML=[ENU=Plant No.;
                                                              NOR=Maskinnr.;
                                                              SVE=Maskinnr] }
    { 40  ;   ;Plant Part          ;Code30        ;TableRelation="Plant Part";
                                                   OnValidate=BEGIN
                                                                CALCFIELDS(Description);
                                                              END;

                                                   CaptionML=[ENU=Plant Part;
                                                              NOR=Maskindel;
                                                              SVE=Maskindel];
                                                   NotBlank=Yes }
    { 41  ;   ;Description         ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Part".Description WHERE (Code=FIELD(Plant Part)));
                                                   CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning];
                                                   Editable=No }
    { 50  ;   ;Problems            ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Plant Part Problem" WHERE (Level=FIELD(Level),
                                                                                                 Plant Type=FIELD(Plant Type),
                                                                                                 Plant No.=FIELD(Plant No.),
                                                                                                 Plant Part=FIELD(Plant Part)));
                                                   CaptionML=[ENU=Problems;
                                                              NOR=Problemer;
                                                              SVE=Problem];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Level,Plant Type,Plant No.,Plant Part   ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100529000 : TextConst 'ENU=Rename not allowed, already %1 present.';

    LOCAL PROCEDURE OnInsertNewPlantPart@1100529002();
    BEGIN
      CASE Level OF
        Level::PlantType:
          BEGIN
            TESTFIELD("Plant Type");
            TESTFIELD("Plant No.", '');
            CopyPlantPartProblems_GeneralToPlantType;
          END;
        Level::PlantNumber:
          BEGIN
            TESTFIELD("Plant Type");
            TESTFIELD("Plant No.");
            CopyPlantPartProblems_PlantTypeToPlanNumber;
          END;
      ELSE
        FIELDERROR(Level);
      END;
    END;

    PROCEDURE ShowPlantPartProblems@1100529000(EditableMode@1100529001 : Boolean);
    VAR
      PlantPartProblem@1100529000 : Record 11229931;
    BEGIN
      TESTFIELD("Plant Part");
      TESTFIELD("Plant Type");
      PlantPartProblem.FILTERGROUP(4);
      CASE Level OF
        Level::PlantType:
          BEGIN
            PlantPartProblem.SETRANGE(Level, PlantPartProblem.Level::PlantType);
            PlantPartProblem.SETRANGE("Plant Type", "Plant Type");
            PlantPartProblem.SETRANGE("Plant No.", '');
          END;
        Level::PlantNumber:
          BEGIN
            TESTFIELD("Plant No.");
            PlantPartProblem.SETRANGE(Level, PlantPartProblem.Level::PlantNumber);
            PlantPartProblem.SETRANGE("Plant Type", "Plant Type");
            PlantPartProblem.SETRANGE("Plant No.", "Plant No.");
          END;
      ELSE
        EXIT;
      END;
      PlantPartProblem.SETRANGE("Plant Part", "Plant Part");
      PlantPartProblem.FILTERGROUP(0);
      IF EditableMode THEN
        PAGE.RUNMODAL(PAGE::"Plant Part Problems", PlantPartProblem)
      ELSE BEGIN
        PAGE.RUNMODAL(PAGE::"Plant Part Problem List", PlantPartProblem);
      END;
    END;

    LOCAL PROCEDURE CopyPlantPartProblems_GeneralToPlantType@1100529001();
    VAR
      PlantPartProblem1@1100529001 : Record 11229931;
      PlantPartProblem2@1100529000 : Record 11229931;
    BEGIN
      PlantPartProblem1.SETRANGE(Level, PlantPartProblem1.Level::General);
      PlantPartProblem1.SETRANGE("Plant Part", "Plant Part");
      IF PlantPartProblem1.FINDSET THEN BEGIN
        REPEAT
          PlantPartProblem2.INIT;
          PlantPartProblem2.Level := PlantPartProblem1.Level::PlantType;
          PlantPartProblem2."Plant Type" := "Plant Type";
          PlantPartProblem2."Plant No." := '';
          PlantPartProblem2."Plant Part" := "Plant Part";
          PlantPartProblem2.Problem := PlantPartProblem1.Problem;
          PlantPartProblem2.INSERT;
        UNTIL PlantPartProblem1.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE CopyPlantPartProblems_PlantTypeToPlanNumber@1100529004();
    VAR
      PlantPartProblem1@1100529001 : Record 11229931;
      PlantPartProblem2@1100529000 : Record 11229931;
    BEGIN
      PlantPartProblem1.SETRANGE(Level, PlantPartProblem1.Level::PlantType);
      PlantPartProblem1.SETRANGE("Plant Type", "Plant Type");
      PlantPartProblem1.SETRANGE("Plant Part", "Plant Part");
      IF PlantPartProblem1.FINDSET THEN BEGIN
        REPEAT
          PlantPartProblem2.INIT;
          PlantPartProblem2.Level := PlantPartProblem1.Level::PlantNumber;
          PlantPartProblem2."Plant Type" := "Plant Type";
          PlantPartProblem2."Plant No." := "Plant No.";
          PlantPartProblem2."Plant Part" := "Plant Part";
          PlantPartProblem2.Problem := PlantPartProblem1.Problem;
          PlantPartProblem2.INSERT;
        UNTIL PlantPartProblem1.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}

