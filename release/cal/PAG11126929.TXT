OBJECT Page 11126929 Close Resource Requests
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Ressourcenanfragen fertigmelden;
               ENU=Close Resource Requests;
               NLD=Resourceaanvragen gereed melden];
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table11124901;
    PageType=Worksheet;
    UsageCategory=Tasks;
    SourceTableTemporary=Yes;
    OnOpenPage=BEGIN
                 CalculateDates;
                 GenericTempTable.DELETEALL;
               END;

    OnAfterGetRecord=BEGIN
                       IF (LastDateTime + 1000) < CURRENTDATETIME THEN
                         GetRequests;
                       IF GenericTempTable.GET("No.") THEN
                         Attention := TRUE
                       ELSE Attention := FALSE;
                     END;

    ActionList=ACTIONS
    {
      { 1100525014;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100525016;1 ;Action    ;
                      Name=OpenRequest;
                      CaptionML=[DEU=Offene Ressourcenanfrage;
                                 ENU=Open Resource Request;
                                 NLD=Open Resourceaanvraag];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Card;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ResourceRequest@1100525000 : Record 11124901;
                               BEGIN
                                 ResourceRequest.GET("No.");
                                 PAGE.RUNMODAL(PAGE::"Resource Request", ResourceRequest);
                                 IF GenericTempTable.GET("No.") THEN
                                   GenericTempTable.DELETE;
                                 GetRequests;
                               END;
                                }
      { 1100525015;1 ;Action    ;
                      Name=SetToFinish;
                      CaptionML=[DEU=Auf ³BeendetÊ setzen;
                                 ENU=Set to Finished;
                                 NLD=Gereed melden];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Approve;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ResourceRequest@1100525000 : Record 11124901;
                                 ResourceReqPlantLocation@1100525002 : Record 11124914;
                                 ResourceRequestStatusMgt@1100525001 : Codeunit 11124901;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(ResourceRequest);
                                 IF ResourceRequest.FINDSET THEN BEGIN
                                   REPEAT
                                     IF ResourceRequest.Status = ResourceRequest.Status::Open THEN BEGIN
                                       GenericTempTable."Key Code1" := ResourceRequest."No.";
                                       IF GenericTempTable.INSERT THEN BEGIN
                                         IF (ResourceRequest."No." <> '') AND
                                           (ResourceRequest."Source Type" IN [ResourceRequest."Source Type"::Project, ResourceRequest."Source Type"::ServiceOrder]) AND
                                           (ResourceRequest."Source No." <> '') THEN
                                           ResourceReqPlantLocation.UpdatePlantLocationsForOtherCompanies(ResourceRequest."No.");
                                         COMMIT;
                                         ResourceRequestStatusMgt.FinishRequest(ResourceRequest);
                                         COMMIT;
                                       END;
                                     END;
                                   UNTIL ResourceRequest.NEXT = 0;
                                 END;
                                 GetRequests();
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100525000;;Container;
                ContainerType=ContentArea }

    { 1100525004;1;Group  ;
                GroupType=Group }

    { 1100525002;2;Field  ;
                Name=StartDate;
                CaptionML=[DEU=Startdatum;
                           ENU=Start Date;
                           NLD=Startdatum];
                SourceExpr=StartDate;
                Editable=DatesEditable;
                OnValidate=BEGIN
                             GetRequests;
                           END;
                            }

    { 1100525003;2;Field  ;
                Name=EndDate;
                CaptionML=[DEU=Enddatum;
                           ENU=End Date;
                           NLD=Einddatum];
                SourceExpr=EndDate;
                Editable=DatesEditable;
                OnValidate=BEGIN
                             GetRequests;
                           END;
                            }

    { 1100525001;2;Field  ;
                Name=DateSelection;
                CaptionML=[DEU=Datumsauswahl;
                           ENU=Date Selection;
                           NLD=Datumselectie];
                OptionCaptionML=[DEU=Heute,Diese Woche,Vorige Woche,Dieser Monat,Voriger Monat,Angepasst;
                                 ENU=Today,This Week,Last Week,This Month,Last Month,Custom;
                                 NLD=Vandaag,Deze week,Vorige week,Deze maand,Vorige maand,Aangepast];
                SourceExpr=DateSelection;
                OnValidate=BEGIN
                             CalculateDates;
                           END;
                            }

    { 1100525005;1;Group  ;
                Name=Resource Requests;
                CaptionML=[DEU=Ressourcenanfrageliste;
                           ENU=Resource Requests;
                           NLD=Resourceaanvragen];
                Editable=False;
                GroupType=Repeater }

    { 1100525006;2;Field  ;
                SourceExpr="No.";
                Style=Unfavorable;
                StyleExpr=Attention }

    { 1100525007;2;Field  ;
                SourceExpr=Description }

    { 1100525012;2;Field  ;
                SourceExpr=Status }

    { 1100525008;2;Field  ;
                SourceExpr=Type }

    { 1100525009;2;Field  ;
                SourceExpr="Sub Type";
                Visible=False }

    { 1100525010;2;Field  ;
                SourceExpr="Source Type" }

    { 1100525011;2;Field  ;
                SourceExpr="Source No." }

    { 1100525013;2;Field  ;
                SourceExpr="Request Type";
                Visible=False }

  }
  CODE
  {
    VAR
      StartDate@1100525000 : Date;
      EndDate@1100525001 : Date;
      DateSelection@1100525002 : 'ToDay,ThisWeek,LastWeek,ThisMonth,LastMonth,Custom';
      DatesEditable@1100525003 : Boolean;
      LastDateTime@1100525004 : DateTime;
      Attention@1100525005 : Boolean;
      GenericTempTable@1100525006 : TEMPORARY Record 11020579;

    LOCAL PROCEDURE CalculateDates@1100525008();
    BEGIN
      DatesEditable := DateSelection = DateSelection::Custom;
      CASE DateSelection OF
        DateSelection::ToDay:
        BEGIN
          StartDate := TODAY;
          EndDate := TODAY;
        END;
        DateSelection::ThisWeek:
        BEGIN
          StartDate := CALCDATE('<-CW>', TODAY);
          EndDate := CALCDATE('<CW>', TODAY);
        END;
        DateSelection::LastWeek:
        BEGIN
          StartDate := CALCDATE('<-CW-1W>', TODAY);
          EndDate := CALCDATE('<CW-1W>', TODAY);
        END;
        DateSelection::ThisMonth:
        BEGIN
          StartDate := CALCDATE('<-CM>', TODAY);
          EndDate := CALCDATE('<CM>', TODAY);
        END;
        DateSelection::LastMonth:
        BEGIN
          StartDate := CALCDATE('<-CM-1M>', TODAY);
          EndDate := CALCDATE('<CM-1M>', TODAY);
        END;
      END;
      GetRequests();
    END;

    LOCAL PROCEDURE GetRequests@1100525010();
    VAR
      ResourceRequest@1100525000 : Record 11124901;
    BEGIN
      Rec.RESET;
      Rec.DELETEALL;
      ResourceRequest.SETRANGE(Status, ResourceRequest.Status::Open);
      IF ResourceRequest.FINDSET THEN BEGIN
        REPEAT
          IF CheckWorkorderDates(ResourceRequest) THEN BEGIN
            Rec := ResourceRequest;
            Rec.INSERT;
          END;
        UNTIL ResourceRequest.NEXT = 0;
      END;
      IF Rec.FINDFIRST THEN ;
      LastDateTime := CURRENTDATETIME;
    END;

    LOCAL PROCEDURE CheckWorkorderDates@1100525006(ResourceRequest@1100525000 : Record 11124901) : Boolean;
    VAR
      WorkOrder@1100525003 : Record 11229279;
      EndingDate@1100525004 : Date;
    BEGIN
      WorkOrder.SETRANGE("Source Type", WorkOrder."Source Type"::Request);
      WorkOrder.SETRANGE("Source Company", COMPANYNAME);
      WorkOrder.SETRANGE("Source No.", ResourceRequest."No.");
      IF WorkOrder.FINDSET THEN BEGIN
        REPEAT
          EndingDate := DT2DATE(WorkOrder."Ending Date/Time");
          IF (EndingDate < StartDate) OR (EndingDate > EndDate) THEN
            EXIT(FALSE);
        UNTIL WorkOrder.NEXT = 0;
        EXIT(TRUE);
      END;
      EXIT(FALSE);
    END;

    BEGIN
    END.
  }
}

