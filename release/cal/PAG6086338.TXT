OBJECT Page 6086338 Table Filter Field List EM
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.3.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Tabellenfilter;
               ENU=Table Filters;
               NLD=Tabelfilters;
               SVE=Tabellfilter];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table2000000041;
    OnAfterGetRecord=VAR
                       TableFilterField@161024012 : Record 6086348;
                     BEGIN
                       TableFilterField.SETRANGE("Table Filter GUID",TableGUID);
                       TableFilterField.SETRANGE("Field No.","No.");
                       IF NOT TableFilterField.FINDFIRST THEN
                         CLEAR(TableFilterField);
                       TableFilterField.GetValues(Value,FilterType);
                     END;

  }
  CONTROLS
  {
    { 1900000001;;Container;
                Name=ContentArea;
                ContainerType=ContentArea }

    { 161024012;1;Group   ;
                GroupType=Repeater }

    { 161024013;2;Field   ;
                Name=No.;
                CaptionML=[DEU=Nr.;
                           ENU=No.;
                           NLD=Nr.;
                           SVE=Nr];
                SourceExpr="No.";
                Editable=FALSE }

    { 161024025;2;Field   ;
                Name=Field Caption;
                CaptionML=[DEU=Caption;
                           ENU=Caption;
                           NLD=Bijschrift;
                           SVE=Rubrik];
                SourceExpr="Field Caption";
                Editable=FALSE }

    { 161024017;2;Field   ;
                Name=FilterType;
                CaptionML=[DEU=Filterart;
                           ENU=Filter Type;
                           NLD=Filtertype;
                           SVE=Filtertyp];
                OptionCaptionML=[DEU=Fester Filter,Feldart;
                                 ENU=Fixed Filter,Field Type;
                                 NLD=Vast filter,Veldtype];
                SourceExpr=FilterType;
                Visible=false;
                OnValidate=BEGIN
                             SetValue('');
                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 161024015;2;Field   ;
                Name=Filter;
                Lookup=Yes;
                CaptionML=[DEU=Filter;
                           ENU=Filter;
                           NLD=Filter;
                           SVE=Filter];
                SourceExpr=Value;
                OnValidate=BEGIN
                             SetValue(Value);
                             CurrPage.UPDATE(FALSE);
                           END;

                OnLookup=BEGIN
                           EXIT(LookupValue(Text));
                         END;
                          }

  }
  CODE
  {
    VAR
      FieldTypeFilter@1160040002 : Text[1024];
      Value@161024012 : Text[250];
      TableGUID@161024014 : GUID;
      FilterType@161024013 : 'FixedFilter,FieldType';
      FilterFieldAlreadyUsed@1160040003 : TextConst 'DEU=Der Filterwert %1 wird bereits gebraucht in %2 %3.;ENU=The filter value %1 is already used in %2 %3.;NLD=De filterwaarde %1 wordt al gebruikt in %2 %3.';
      OnlyOneFieldTypeFilterErr@1160040005 : TextConst 'DEU="Sie kînnen nur 1 Filter mit %1 = %2 haben.";ENU="You can only have one filter with %1 = %2.";NLD="U kunt slechts 1 filter hebben met %1 = %2."';
      Text001@1160040000 : TextConst 'DEU=Der PrimÑrschlÅssel der Tabelle darf nicht mehr als ein Feld enthalten.;ENU=The primary key of the table cannot contain more than one Field.;NLD=De primaire sleutel van de tabel mag maar 1 veld bevatten.';
      Text002@1160040001 : TextConst 'DEU=Lookup in diesem Feld ist nicht mîglich.;ENU=Lookup in this field is not possible.;NLD=Zoeken met dit veld is niet mogelijk.';

    PROCEDURE SetParam@161024012(NewGUID@1160040001 : GUID;NewFieldTypeFilter@1160040002 : Text[1024]);
    BEGIN
      TableGUID := NewGUID;
      FieldTypeFilter := NewFieldTypeFilter;
    END;

    LOCAL PROCEDURE SetValue@1160040003(NewValue@1160040000 : Text[250]);
    VAR
      FieldType@1160040003 : Record 6086345;
      TableFilterField@1160040001 : Record 6086348;
      TableFilterField2@1160040002 : Record 6086348;
    BEGIN
      TableFilterField.SETRANGE("Table Filter GUID",TableGUID);
      TableFilterField.SETRANGE("Field No.","No.");
      IF (NewValue = '') AND (FilterType = FilterType::FixedFilter) THEN BEGIN
        IF TableFilterField.FINDFIRST THEN
          TableFilterField.DELETE;
        EXIT;
      END;

      CASE FilterType OF
        FilterType::FixedFilter:
          BEGIN
            TableFilterField2.SETRANGE("Field No.","No.");
            TableFilterField2.SETRANGE("Value (Text)",NewValue);
            TableFilterField2.SETFILTER("Table Filter GUID",'<>%1',TableGUID);
            IF TableFilterField2.FINDFIRST THEN BEGIN
              FieldType.SETRANGE("Source Table Filter GUID",TableFilterField2."Table Filter GUID");
              FieldType.FINDFIRST;
              ERROR(FilterFieldAlreadyUsed,NewValue,FieldType.TABLECAPTION,FieldType.Code);
            END;
          END;

        FilterType::FieldType:
          BEGIN
            TableFilterField2.SETRANGE("Table Filter GUID",TableGUID);
            TableFilterField2.SETFILTER("Field No.",'<>%1',"No.");
            TableFilterField2.SETRANGE("Filter Type",TableFilterField."Filter Type"::"Field Type");
            IF TableFilterField2.FINDFIRST THEN
              ERROR(OnlyOneFieldTypeFilterErr,TableFilterField2.FIELDCAPTION("Filter Type"),TableFilterField2."Filter Type");
          END;
      END;

      IF NOT TableFilterField.FINDFIRST THEN BEGIN
        TableFilterField."Table Filter GUID" := TableGUID;
        TableFilterField."Table No." := TableNo;
        TableFilterField."Field No." := "No.";
        TableFilterField.INSERT;
      END;

      TableFilterField.SetValues(NewValue,FilterType);
      TableFilterField.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE GetTableNo@1160040000() : Integer;
    VAR
      RecRef@1160040000 : RecordRef;
      FieldRef@1160040002 : FieldRef;
      KeyRef@1160040001 : KeyRef;
    BEGIN
      IF FilterType = FilterType::FieldType THEN
        EXIT(DATABASE::"Field Type");

      IF RelationTableNo <> 0 THEN
        EXIT(RelationTableNo);

      RecRef.OPEN(TableNo);
      KeyRef := RecRef.KEYINDEX(RecRef.CURRENTKEYINDEX);
      FieldRef := KeyRef.FIELDINDEX(1);
      IF "No." = FieldRef.NUMBER THEN
        EXIT(TableNo)
      ELSE
        IF RelationTableNo > 0 THEN
          EXIT(RelationTableNo)
        ELSE
          ERROR(Text002);
    END;

    LOCAL PROCEDURE LookupValue@1160040001(VAR NewValue@1160040003 : Text[250]) : Boolean;
    VAR
      LookupRecID@161024012 : Record 6085760;
      FieldType@1160040004 : Record 6086345;
      RecIDMgt@1160040005 : Codeunit 6085604;
      RecRef@1160040000 : RecordRef;
      FieldRef@1160040001 : FieldRef;
      KeyRef@1160040002 : KeyRef;
    BEGIN
      IF FieldType.FieldIsOptionstring(TableNo,"No.") THEN
        EXIT(FieldType.LookupOptionString(TableNo,"No.",NewValue));

      IF FieldType.FieldIsBoolean(TableNo,"No.") THEN
        EXIT(FieldType.LookupBoolean(TableNo,"No.",NewValue));

      IF FilterType = 0 THEN BEGIN
        LookupRecID."Table No." := GetTableNo;

        LookupRecID."Record ID Tree ID" := RecIDMgt.GetRecIDTreeID2(LookupRecID."Table No.","No.",TableGUID,NewValue);

        CODEUNIT.RUN(CODEUNIT::"Record ID Lookup",LookupRecID);

        IF LookupRecID."Lookup Mode" = LookupRecID."Lookup Mode"::OK THEN BEGIN
          IF RecIDMgt.GetTableNoFromRecID(LookupRecID."Record ID Tree ID") = 0 THEN
            EXIT;
          RecRef.OPEN(RecIDMgt.GetTableNoFromRecID(LookupRecID."Record ID Tree ID"));
          KeyRef := RecRef.KEYINDEX(RecRef.CURRENTKEYINDEX);
          IF KeyRef.FIELDCOUNT > 1 THEN
            ERROR(Text001);
          FieldRef := KeyRef.FIELDINDEX(1);
          NewValue := RecIDMgt.GetKeyValue(LookupRecID."Record ID Tree ID",FieldRef.NUMBER);
          EXIT(TRUE);
        END;
      END ELSE BEGIN
        IF NewValue <> '' THEN
          IF FieldType.GET(NewValue) THEN;

        IF FieldTypeFilter <> '' THEN BEGIN
          FieldType.FILTERGROUP(2);
          FieldType.SETFILTER(Code,FieldTypeFilter);
          FieldType.FILTERGROUP(0);
        END;
        IF PAGE.RUNMODAL(0,FieldType) = ACTION::LookupOK THEN BEGIN
          NewValue := FieldType.Code;
          EXIT(TRUE);
        END;
      END;
    END;

    BEGIN
    END.
  }
}

