OBJECT Page 11126502 Prognosis Totals Extended FB
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[DEU=Prognose Gesamt Erweitert FB;
               ENU=Prognosis Totals Extended FB;
               NLD=Prognose totalen (uitgebreid);
               NOR=Prognoses Total Utvide FB;
               SVE=Prognosens total ut”kar FB];
    SourceTable=Table11012034;
    PageType=CardPart;
    OnAfterGetRecord=BEGIN
                       SetFilters("Project No.", "Prognosis Date", 0);
                       FillTempTable;
                     END;

  }
  CONTROLS
  {
    { 1210190009;;Container;
                ContainerType=ContentArea }

    { 1100529700;1;Field  ;
                CaptionML=[DEU=Gesamtbudget;
                           ENU=Total Budget;
                           NLD=Totaal budget;
                           NOR=Budsjett Total;
                           SVE=Total budget];
                SourceExpr=BudgetLine."Total Amount Actual" }

    { 1100525000;1;Field  ;
                CaptionML=[DEU=Ist-Kosten;
                           ENU=Actual Costs;
                           NLD=Besteed;
                           NOR=Faktiske koster;
                           SVE=Verkliga kostnader];
                SourceExpr=TempPrognosisLine."Actual Costs";
                Editable=false }

    { 1100528300;1;Field  ;
                CaptionML=[DEU=Eingegangen/Nicht fakturiert;
                           ENU=Received/Not Invoiced;
                           NLD=Ontvangen/niet gefactureerd;
                           NOR=Mottatt/ikke fakturert;
                           SVE=Mottaget/inte fakturerat];
                SourceExpr=TempPrognosisLine."Received/Not Invoiced";
                Editable=false }

    { 1100528301;1;Field  ;
                CaptionML=[DEU=Voriges prognostiziertes Endergebnis;
                           ENU=Prev Prognosis End Result;
                           NLD=Vorig geprognosticeerd eindresultaat;
                           NOR=Forrige prognose for Sluttresultat;
                           SVE=F”reg†ende prognos f”r slutresultat];
                SourceExpr=PrevPrognosisEndResult;
                Editable=false }

    { 1100528302;1;Field  ;
                CaptionML=[DEU=Einstandsbetrag;
                           ENU=Total Cost;
                           NLD=Totale kosten;
                           NOR=Total kost;
                           SVE=Totalkostnad];
                SourceExpr=TempPrognosisLine."Total Cost";
                Editable=false }

    { 1100528303;1;Field  ;
                CaptionML=[DEU=Zugelassen;
                           ENU=Allowed Costs;
                           NLD=Toegestaan;
                           NOR=Tillatte koster;
                           SVE=Till†tna kostnader];
                SourceExpr=TempPrognosisLine."Allowed Costs";
                Editable=false }

    { 1100528309;1;Field  ;
                CaptionML=[DEU=Verfgbar;
                           ENU=Available;
                           NLD=Beschikbaar;
                           NOR=Disponibelt;
                           SVE=Disponibelt];
                SourceExpr=AvailableDec }

    { 1100528304;1;Field  ;
                CaptionML=[DEU=Vorige Prognose;
                           ENU=Previous Prognosis;
                           NLD=Vorige prognose;
                           NOR=Forrige prognose;
                           SVE=F”reg†ende prognos];
                SourceExpr=PreviousPrognosis;
                Editable=false }

    { 1100528310;1;Field  ;
                CaptionML=[DEU=Endergebnis Differenz;
                           ENU=End Result Difference;
                           NLD=Eindresultaat verschil;
                           NOR=Sluttresultat, differanse;
                           SVE="Slutresultatsdifferens "];
                SourceExpr=CalcEndResult }

    { 1100528305;1;Field  ;
                CaptionML=[DEU=Prognostizierter Einstandsbetrag;
                           ENU=Prognosis Total Cost;
                           NLD=Geprognosticeerde totale kosten;
                           NOR=Prognose for totale kostnader;
                           SVE=Prognos totala kostnader];
                SourceExpr=TempPrognosisLine."Prognosis Total Cost";
                Editable=false }

    { 1100528306;1;Field  ;
                CaptionML=[DEU=Absicherung;
                           ENU=Risc Coverage;
                           NLD=Risicodekking;
                           NOR=Riskdekning;
                           SVE=Riskt„ckning];
                SourceExpr=TempPrognosisLine."Risc Coverage";
                Editable=false }

    { 1100528307;1;Field  ;
                CaptionML=[DEU=Ergebnis Bester Fall;
                           ENU=Best Case Result;
                           NLD=Beste scenario resultaat;
                           NOR=Beste scenario resultat;
                           SVE=B„sta scenario resultat];
                SourceExpr=BestCaseResult;
                Enabled=ShowCaseResult }

    { 1100528308;1;Field  ;
                CaptionML=[DEU=Ergebnis Schlechtester Fall;
                           ENU=Worst Case Result;
                           NLD=Slechtste scenario resultaat;
                           NOR=Verste scenario resultat;
                           SVE=V„rsta scenario resultat];
                SourceExpr=WorstCaseResult;
                Enabled=ShowCaseResult }

  }
  CODE
  {
    VAR
      ProjectNoFilter@1100528308 : Code[20];
      PrognosisDateFilter@1100528309 : Date;
      CostTypeFilter@1100528310 : Option;
      JobsSetup@1100528306 : Record 315;
      TempPrognosisLine@1100525000 : TEMPORARY Record 11012035;
      BudgetLine@1100529700 : Record 11012001;
      PrevPrognosisEndResult@1100528301 : Decimal;
      PreviousPrognosis@1100528300 : Decimal;
      BestCaseResult@1100528302 : Decimal;
      WorstCaseResult@1100528303 : Decimal;
      ShowCaseResult@1100528304 : Boolean INDATASET;
      AvailableDec@1100528305 : Decimal;
      CalcEndResult@1100528307 : Decimal;

    LOCAL PROCEDURE FillTempTable@19077479();
    BEGIN
      JobsSetup.GET;
      TempPrognosisLine.DELETEALL;
      AvailableDec := 0;
      PrevPrognosisEndResult := 0;
      PreviousPrognosis := 0;
      BestCaseResult := 0;
      WorstCaseResult := 0;
      ShowCaseResult := FALSE;
      AvailableDec := 0;
      CalcEndResult := 0;

      CASE TRUE OF
        ("Prognosis Level" = "Prognosis Level"::Detailed):
          FillTempTableFromPrognosisLine;
        ("Prognosis Level" = "Prognosis Level"::Element):
          FillTempTableFromPrognosisElement;
      END;
      IF (TempPrognosisLine.FINDFIRST) THEN;

      BudgetLine.SETRANGE("Project No.", ProjectNoFilter);
      IF BudgetLine.FINDFIRST THEN
        BudgetLine.CALCFIELDS("Total Amount Actual")
      ELSE
        BudgetLine."Total Amount Actual" := 0;

      TempPrognosisLine.CALCSUMS("Actual Costs", "Received/Not Invoiced", "Total Cost", "Allowed Costs",
        "Prognosis Total Cost", "Risc Coverage", "Prognosis End Result", "Budget Amount", "Open (Purchase)");
      CalcEndResult := PrevPrognosisEndResult - TempPrognosisLine."Prognosis End Result";
      IF (JobsSetup."Calculate Available" = JobsSetup."Calculate Available"::"Budget - Allowed Cost") THEN
        AvailableDec := TempPrognosisLine."Budget Amount" - TempPrognosisLine."Allowed Costs" - TempPrognosisLine."Open (Purchase)"
      ELSE
        AvailableDec := TempPrognosisLine."Budget Amount" - TempPrognosisLine."Total Cost";
    END;

    LOCAL PROCEDURE FillTempTableFromPrognosisLine@1100528300();
    VAR
      PrognosisLine@1100528300 : Record 11012035;
    BEGIN
      PrognosisLine.SETRANGE("Project No.", ProjectNoFilter);
      PrognosisLine.SETRANGE("Prognosis Date", PrognosisDateFilter);
      IF (CostTypeFilter > 0) THEN
        PrognosisLine.SETRANGE("Cost Type", CostTypeFilter - 1);
      IF (NOT PrognosisLine.FINDSET) THEN
        EXIT;

      REPEAT
        PrognosisLine.CALCFIELDS("Prev Prognosis End Result", "Previous Prognosis");
        PrevPrognosisEndResult += PrognosisLine."Prev Prognosis End Result";
        PreviousPrognosis += PrognosisLine."Previous Prognosis";

        TempPrognosisLine.COPY(PrognosisLine);
        TempPrognosisLine.INSERT;
      UNTIL (PrognosisLine.NEXT = 0);
    END;

    LOCAL PROCEDURE FillTempTableFromPrognosisElement@1100528301();
    VAR
      PrognosisPerElement@1100525001 : Record 11071893;
    BEGIN
      ShowCaseResult := TRUE;
      PrognosisPerElement.SETRANGE("Project No.", ProjectNoFilter);
      PrognosisPerElement.SETRANGE("Prognosis Date", PrognosisDateFilter);
      IF (NOT PrognosisPerElement.FINDSET) THEN
        EXIT;

      REPEAT
        PrognosisPerElement.CALCFIELDS("Prev Prognosis End Result", "Previous Prognosis");
        PrevPrognosisEndResult += PrognosisPerElement."Prev Prognosis End Result";
        PreviousPrognosis += PrognosisPerElement."Previous Prognosis";
        BestCaseResult += PrognosisPerElement."Best Case Result";
        WorstCaseResult += PrognosisPerElement."Worst Case Result";

        IF (NOT TempPrognosisLine.GET(PrognosisPerElement."Project No.", PrognosisPerElement."Prognosis Date", 99,
            PrognosisPerElement.Element, ''))
        THEN BEGIN
          TempPrognosisLine.INIT;
          TempPrognosisLine."Project No." := PrognosisPerElement."Project No.";
          TempPrognosisLine."Prognosis Date" := PrognosisPerElement."Prognosis Date";
          TempPrognosisLine."Cost Type" := 99;
          TempPrognosisLine.Element := PrognosisPerElement.Element;
          TempPrognosisLine."Cost Object" := '';
          TempPrognosisLine.INSERT;
        END;
        TempPrognosisLine."Actual Costs" += PrognosisPerElement."Actual Costs";
        TempPrognosisLine."Received/Not Invoiced" += PrognosisPerElement."Received/Not Invoiced";
        TempPrognosisLine."Total Cost" += PrognosisPerElement."Total Cost";
        TempPrognosisLine."Allowed Costs" += PrognosisPerElement."Allowed Costs";
        TempPrognosisLine."Prognosis Total Cost" += PrognosisPerElement."Prognosis Total Cost";
        TempPrognosisLine."Risc Coverage" += PrognosisPerElement."Risc Coverage";

        TempPrognosisLine."Prognosis End Result" += PrognosisPerElement."Prognosis End Result";
        TempPrognosisLine."Budget Amount" := PrognosisPerElement."Budget Amount";
        TempPrognosisLine."Open (Purchase)" += PrognosisPerElement."Open (Purchase)";

        TempPrognosisLine.MODIFY;
      UNTIL (PrognosisPerElement.NEXT = 0);
    END;

    PROCEDURE SetFilters@1100528302(ProjectNoFilterIn@1100528300 : Code[20];PrognosisDateFilterIn@1100528301 : Date;CostTypeFilterIn@1100528302 : Option);
    BEGIN
      ProjectNoFilter := ProjectNoFilterIn;
      PrognosisDateFilter := PrognosisDateFilterIn;
      IF (CostTypeFilterIn <> 0) THEN
        CostTypeFilter := CostTypeFilterIn;
    END;

    BEGIN
    END.
  }
}

