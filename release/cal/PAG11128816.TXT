OBJECT Page 11128816 LTMP Cost Chart
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=LTMP Kostendiagramm;
               ENU=LTMP Cost Chart;
               NLD=MJOP kostengrafiek];
    SourceTable=Table11128784;
    DataCaptionExpr=' ';
    OnInit=BEGIN
             PeriodType := PeriodType::Year;
             NoOfColumns := 20;
           END;

    OnOpenPage=BEGIN
                 Date."Period Start" := WORKDATE;
                 PeriodFormManagement.FindDate('=<', Date, PeriodType);
                 BaseDate := Date."Period Start";
               END;

    OnAfterGetCurrRecord=BEGIN
                           DetermineSelectedScenarios;
                           UpdateChart;
                         END;

    ActionList=ACTIONS
    {
      { 1100528606;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100528604;1 ;Action    ;
                      CaptionML=[DEU=Zurck;
                                 ENU=Previous;
                                 NLD=Vorige;
                                 NOR=Forrige;
                                 SVE=F”reg†ende];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=PreviousRecord;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 PeriodFormManagement.FindDate('<', Date, PeriodType);
                                 BaseDate := Date."Period Start";
                                 UpdateChart;
                               END;
                                }
      { 1100528603;1 ;Action    ;
                      CaptionML=[DEU=Weiter;
                                 ENU=Next;
                                 NLD=Volgende;
                                 NOR=Neste;
                                 SVE=N„sta];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=NextRecord;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 PeriodFormManagement.FindDate('>', Date, PeriodType);
                                 BaseDate := Date."Period Start";
                                 UpdateChart;
                               END;
                                }
      { 1100528605;1 ;Action    ;
                      CaptionML=[DEU=Szenarien anzeigen;
                                 ENU=Show Scenarios;
                                 NLD=Scenario's tonen];
                      Promoted=Yes;
                      Image=SelectChart;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 LTMPScenarioHeader@1100528600 : Record 11128791;
                                 NEN2767Management@1100528601 : Codeunit 11012863;
                               BEGIN
                                 NEN2767Management.GetSelectedScenarios("Location Type", "Location Code", "Survey No.", LTMPScenarioHeader);
                                 PAGE.RUNMODAL(0, LTMPScenarioHeader);
                                 //DetermineSelectedScenarios;
                                 //UpdateChart;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100528602;0;Container;
                ContainerType=ContentArea }

    { 1100528607;1;Group  ;
                CaptionML=[DEU=Optionen;
                           ENU=Options;
                           NLD=Opties;
                           NOR=Alternativer;
                           SVE=Alternativ];
                GroupType=Group }

    { 1100528609;2;Field  ;
                CaptionML=[DEU=Diagrammcode;
                           ENU=Chart Code;
                           NLD=Grafiekcode];
                SourceExpr=ChartCode;
                TableRelation="LTMP Chart Code".Code WHERE (Location Type=FIELD(Location Type),
                                                            Location Code=FIELD(Location Code),
                                                            Survey No.=FIELD(Survey No.));
                OnValidate=BEGIN
                             DetermineSelectedScenarios;
                             UpdateChart;
                           END;
                            }

    { 1100528610;2;Field  ;
                CaptionML=[DEU=Budget anzeigen;
                           ENU=Show Budget;
                           NLD=Budget tonen];
                SourceExpr=ShowBudget;
                OnValidate=BEGIN
                             UpdateChart;
                           END;
                            }

    { 1100528608;2;Field  ;
                CaptionML=[DEU=Kumulativ;
                           ENU=Cumulative;
                           NLD=Cumulatief;
                           NOR=Akkumulerte;
                           SVE=Ackumulerad];
                SourceExpr=AmountsCumulative;
                OnValidate=BEGIN
                             UpdateChart;
                           END;
                            }

    { 1100528601;1;Group  ;
                CaptionML=[DEU=Diagramm;
                           ENU=Chart;
                           NLD=Grafiek;
                           NOR=Diagram;
                           SVE=Diagram];
                GroupType=Group }

    { 1100528600;2;Field  ;
                Name=Chart;
                ControlAddIn=[Microsoft.Dynamics.Nav.Client.BusinessChart;PublicKeyToken=31bf3856ad364e35] }

  }
  CODE
  {
    VAR
      BusinessChartBuffer@1100528600 : Record 485;
      Date@1100528606 : Record 2000000007;
      SelectedLTMPScenarioHeader@1100528607 : Record 11128791;
      PeriodFormManagement@1100528605 : Codeunit 359;
      PeriodType@1100528601 : ',,Month,Quarter,Year';
      BaseDate@1100528602 : Date;
      NoOfColumns@1100528603 : Integer;
      Text000@1100528604 : TextConst 'DEU=Tag,Woche,Monat,Quartal,Jahr,Periode;ENU=Day,Week,Month,Quarter,Year,Period;NLD=Dag,Week,Maand,Kwartaal,Jaar,Periode;NOR=Dag,Uke,M†ned,Kvartal,r,Periode;SVE=Dag,Vecka,M†nad,Kvartal,r,Period';
      Text001@1100528608 : TextConst 'DEU=Anz. Szenarien (%1) berschreitet max. Anz. d. Maánahmen im Diagramm (%2);ENU=No. of Scenarios (%1) exceeds max. No. of Measures in Chart (%2);NLD=Aantal scenario''s (%1) is groter dan max. aantal series in grafiek (%2)';
      AmountsCumulative@1100528609 : Boolean;
      ChartCode@1100528610 : Code[10];
      ShowBudget@1100528611 : Boolean;
      Text002@1100528612 : TextConst 'DEU=Budget;ENU=Budget;NLD=Budget;NOR=Budsjett;SVE=Budget';
      NoOfChartMeasures@1100529400 : Integer;
      ChartAddInReady@1100529401 : Boolean;

    PROCEDURE SetChartCode@1100529400(IChartCode@1100529400 : Code[10]);
    BEGIN
      ChartCode := IChartCode;
      DetermineSelectedScenarios;
    END;

    PROCEDURE UpdateChart@1100528601();
    VAR
      Date@1100528604 : Record 2000000007;
      Year@1100528603 : Integer;
      Offset@1100528602 : Integer;
      ColumnName@1100528601 : Text[30];
      PeriodDate@1100528600 : Date;
      Index@1100528606 : Integer;
      CostAmount@1100528608 : Decimal;
      StartYear@1100528607 : Integer;
      PreviousAmounts@1100528609 : Decimal;
      BudgetAmount@1100528610 : Decimal;
    BEGIN
      WITH BusinessChartBuffer DO BEGIN
        IF SelectedLTMPScenarioHeader.COUNT > GetMaxNumberOfMeasures THEN
          MESSAGE(Text001, SelectedLTMPScenarioHeader.COUNT, GetMaxNumberOfMeasures);

        Initialize;

        Index := 0;
        IF SelectedLTMPScenarioHeader.FINDSET THEN
          REPEAT
            SelectedLTMPScenarioHeader.CALCFIELDS("Measurement Date");
            AddMeasure(
              STRSUBSTNO('%1/%2 %3 (%4)',
              SelectedLTMPScenarioHeader."Scenario No.", SelectedLTMPScenarioHeader."Version No.",
              SelectedLTMPScenarioHeader."Scenario Description", SelectedLTMPScenarioHeader."Measurement Date"),
              Index, "Data Type"::Decimal, BusinessChartBuffer."Chart Type"::Column);
            Index += 1;
          UNTIL (SelectedLTMPScenarioHeader.NEXT = 0) OR (Index = GetMaxNumberOfMeasures);
        IF ShowBudget THEN BEGIN
          AddMeasure(
            Text002,
            Index,
            "Data Type"::Decimal,
            BusinessChartBuffer."Chart Type"::Line);
          Index += 1;
        END;
        NoOfChartMeasures := Index;
        SetXAxis(GetPeriodTypeText(PeriodType), "Data Type"::String);

        Index := 0;
        IF SelectedLTMPScenarioHeader.FINDSET THEN BEGIN
          REPEAT
            PreviousAmounts := 0;
            SelectedLTMPScenarioHeader.CALCFIELDS("Measurement Date");
            StartYear := DATE2DMY(SelectedLTMPScenarioHeader."Measurement Date", 3);
            Date."Period Start" := BaseDate;
            PeriodFormManagement.FindDate('=<>', Date, PeriodType);
            FOR Offset := 0 TO NoOfColumns -1 DO BEGIN
              IF Offset > 0 THEN
                PeriodFormManagement.NextDate(1, Date, PeriodType);
              PeriodDate := Date."Period Start";
              Year := DATE2DMY(PeriodDate, 3);
              IF Index = 0 THEN BEGIN
                ColumnName := PeriodFormManagement.CreatePeriodFormat(PeriodType, Date."Period Start");
                AddColumn(ColumnName);
              END;
              IF AmountsCumulative THEN
                IF Offset = 0 THEN BEGIN
                  WHILE StartYear < Year DO BEGIN
                    PreviousAmounts += SelectedLTMPScenarioHeader.GetCostAmountByYear(StartYear);
                    StartYear += 1;
                  END;
                END;
              CostAmount := SelectedLTMPScenarioHeader.GetCostAmountByYear(Year);
              BusinessChartBuffer.SetValueByIndex(Index, Offset, CostAmount +  PreviousAmounts);
              IF AmountsCumulative THEN
                PreviousAmounts += CostAmount;
            END;
            Index += 1;
          UNTIL (SelectedLTMPScenarioHeader.NEXT = 0) OR (Index = GetMaxNumberOfMeasures);
        END;
        IF ShowBudget THEN BEGIN
          Date."Period Start" := BaseDate;
          PeriodFormManagement.FindDate('=<>', Date, PeriodType);
          FOR Offset := 0 TO NoOfColumns -1 DO BEGIN
            IF Offset > 0 THEN
              PeriodFormManagement.NextDate(1, Date, PeriodType);
              PeriodDate := Date."Period Start";
              Year := DATE2DMY(PeriodDate, 3);
              IF Index = 0 THEN BEGIN
                ColumnName := PeriodFormManagement.CreatePeriodFormat(PeriodType, Date."Period Start");
                AddColumn(ColumnName);
              END;
            BudgetAmount := GetBudgetAmount(Year, AmountsCumulative);
            BusinessChartBuffer.SetValueByIndex(Index, Offset, BudgetAmount);
          END;
          Index += 1;
        END;

        IF ChartAddInReady THEN
          Update(CurrPage.Chart);
      END;
    END;

    PROCEDURE GetPeriod@1100528604(IOffset@1100528600 : Integer;VAR ODate@1100528601 : Record 2000000007);
    BEGIN
      ODate."Period Start" := BaseDate;
      PeriodFormManagement.FindDate('=<>', ODate, PeriodType);
      IF IOffset <> 0 THEN
        PeriodFormManagement.NextDate(IOffset, ODate, PeriodType);
    END;

    PROCEDURE GetPeriodTypeText@1100528610(IPeriodType@1100528600 : Option) PeriodTypeText : Text;
    VAR
      OptionFieldTools@1100528601 : Codeunit 11012271;
    BEGIN
      OptionFieldTools.OptionValueToReadableOption(Text000, IPeriodType, PeriodTypeText);
    END;

    PROCEDURE DetermineSelectedScenarios@1100528602();
    VAR
      NEN2767Management@1100528603 : Codeunit 11012863;
    BEGIN
      NEN2767Management.GetScenariosToShow("Location Type", "Location Code", "Survey No.", ChartCode, SelectedLTMPScenarioHeader);
    END;

    LOCAL PROCEDURE GetBudgetAmount@1100528603(IYear@1100528600 : Integer;ICumulativeAmounts@1100528601 : Boolean) : Decimal;
    VAR
      LTMPBudget@1100528602 : Record 11128797;
    BEGIN
      LTMPBudget.SETRANGE("Location Type", "Location Type");
      LTMPBudget.SETRANGE("Location Code", "Location Code");
      LTMPBudget.SETRANGE("Survey No.", "Survey No.");
      IF NOT ICumulativeAmounts THEN
        LTMPBudget.SETRANGE(Year, IYear)
      ELSE
        LTMPBudget.SETRANGE(Year, 0, IYear);
      LTMPBudget.CALCSUMS(Amount);
      EXIT(LTMPBudget.Amount);
    END;

    EVENT Chart@-1100528600::DataPointClicked@1(point@1100525000 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartDataPoint");
    VAR
      LTMPBudget@1100525001 : Record 11128797;
      DrillDownIndex@1100525002 : Integer;
      DrillDownYear@1100525003 : Integer;
    BEGIN
      BusinessChartBuffer.SetDrillDownIndexes(point);
      DrillDownIndex := BusinessChartBuffer."Drill-Down X Index";
      IF ShowBudget THEN
        IF BusinessChartBuffer."Drill-Down Measure Index" = NoOfChartMeasures -1 THEN BEGIN
          LTMPBudget.SETRANGE("Location Type", "Location Type");
          LTMPBudget.SETRANGE("Location Code", "Location Code");
          LTMPBudget.SETRANGE("Survey No.", "Survey No.");
          PAGE.RUNMODAL(0, LTMPBudget);
          UpdateChart;
          EXIT;
        END;

      Date."Period Start" := BaseDate;
      PeriodFormManagement.FindDate('=<>', Date, PeriodType);
      IF DrillDownIndex > 0 THEN
        PeriodFormManagement.NextDate(DrillDownIndex, Date, PeriodType);
      DrillDownYear := DATE2DMY(Date."Period Start", 3);

      IF SelectedLTMPScenarioHeader.FINDSET THEN BEGIN
        IF BusinessChartBuffer."Drill-Down Measure Index" > 0 THEN
          SelectedLTMPScenarioHeader.NEXT(BusinessChartBuffer."Drill-Down Measure Index");
        PAGE.RUNMODAL(PAGE::"LTMP Policy Measures", SelectedLTMPScenarioHeader);
        UpdateChart;
      END;
    END;

    EVENT Chart@-1100528600::DataPointDoubleClicked@2(point@1100525000 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartDataPoint");
    BEGIN
    END;

    EVENT Chart@-1100528600::AddInReady@3();
    BEGIN
      ChartAddInReady := TRUE;
    END;

    EVENT Chart@-1100528600::Refresh@4();
    BEGIN
    END;

    BEGIN
    END.
  }
}

