OBJECT Page 11126602 BarCode Job Item
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=BarCode Job-Item;
               ENU=BarCode Job Item;
               NLD=Afgifte materiaal op project];
    PageType=NavigatePage;
    ShowFilter=No;
    OnOpenPage=VAR
                 ActiveSession@1100525000 : Record 2000000110;
               BEGIN
                 ActiveSession.SETRANGE("User ID", USERID);
                 IF ActiveSession.COUNT > 1 THEN
                   ERROR(Text001, USERID);

                 CompanyInfo.GET;
                 UserInfo.GET(USERID);
                 //Location from default
                 //Location from Company Information  (field 5700 from table 79)
                 //Or defined per login in
                 //UserSetup."Sales Resp. Ctr. Filter"
                 LocationNo := UserMgt.GetLocation(0,'',UserMgt.GetSalesFilter);
                 LocationRec.GET(LocationNo);
                 InvSetupRec.GET;
                 CompanyCode := COMPANYNAME;
                 ScannerRec.RESET;
                 ScannerRec.SETRANGE(ScannerID, COPYSTR(USERID, 1, MAXSTRLEN(ScannerRec.ScannerID)));
                 ScannerRec.SETRANGE("Receiving Company", COMPANYNAME);
                 ScannerRec.SETRANGE(Type, ScannerRec.Type::"Project Delivery");
                 ScannerRec.SETRANGE(Status, 0);
                 ScannerRec.SETFILTER("Project No.", '<>%1', '');
                 IF ScannerRec.FINDFIRST THEN BEGIN
                   LocationNo := ScannerRec."Location Code";
                   CompanyCode := ScannerRec."Receiving Company";
                   ElementCode := ScannerRec.Element;
                   JobNo := ScannerRec."Project No.";
                 END;
                 ScannerRec.RESET;
               END;

    ActionList=ACTIONS
    {
      { 1100525005;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100525007;1 ;Action    ;
                      Name=Lijst;
                      CaptionML=[DEU=Liste;
                                 ENU=Lijst;
                                 NLD=Lijst];
                      InFooterBar=Yes;
                      Image=List;
                      OnAction=VAR
                                 BarCodeItemList@1100409000 : Page 11126609;
                               BEGIN
                                 ScannerRec.RESET;
                                 ScannerRec.SETRANGE(ScannerID, COPYSTR(USERID, 1, MAXSTRLEN(ScannerRec.ScannerID)));
                                 ScannerRec.SETRANGE(Type, ScannerRec.Type::"Project Delivery");
                                 ScannerRec.SETRANGE(Status, 0);

                                 BarCodeItemList.SETTABLEVIEW(ScannerRec);
                                 BarCodeItemList.LOOKUPMODE(TRUE);
                                 BarCodeItemList.EDITABLE(TRUE);
                                 BarCodeItemList.RUNMODAL;

                                 //IF ACTION::LookupOK = PAGE.RUNMODAL(87913, ScannerRec) THEN;

                                 ScannerRec.RESET;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100525000;;Container;
                Name=top;
                ContainerType=ContentArea }

    { 1100525001;1;Field  ;
                Name=CtrlLocationNo;
                CaptionML=[DEU=Lagerort;
                           ENU=Location;
                           NLD=Magazijn;
                           NOR=Plassering;
                           SVE=LagerstÑlle];
                SourceExpr=LocationNo;
                OnValidate=BEGIN
                             IF NOT LocationRec.GET(LocationNo) THEN BEGIN
                               ERROR('Magazijn onbekend');
                             END;
                           END;

                OnLookup=BEGIN
                           //**4PS.AH Enter customer specific number for 'Location List'
                           IF PAGE.RUNMODAL(11126610, LocationRec) = ACTION::LookupOK THEN BEGIN
                             LocationNo := LocationRec.Code;
                             LocationRec.GET(LocationNo);
                           END;
                         END;

                QuickEntry=FALSE }

    { 1100525008;1;Field  ;
                Name=CtrlCompNo;
                CaptionML=[DEU=Mandant;
                           ENU=Company;
                           NLD=Ontv. bedrijf;
                           NOR=Selskap;
                           SVE=Fîretag];
                SourceExpr=CompanyCode;
                OnValidate=VAR
                             Company@1100525000 : Record 2000000006;
                           BEGIN
                             ScannerRec.RESET;
                             ScannerRec.SETRANGE(ScannerID, COPYSTR(USERID, 1, MAXSTRLEN(ScannerRec.ScannerID)));
                             ScannerRec.SETRANGE(Type, ScannerRec.Type::"Project Delivery");
                             ScannerRec.SETRANGE(Status, 0);
                             ScannerRec.SETFILTER("Receiving Company", '<>%1', CompanyCode);

                             IF ScannerRec.FINDFIRST THEN
                               ERROR(Text003, ScannerRec."Receiving Company");

                             ScannerRec.RESET;

                             Company.RESET;
                             Company.SETRANGE(Company.Name, CompanyCode);
                             IF NOT Company.FIND('-') THEN BEGIN
                               ERROR('Bedrijf onbekend');
                             END;

                             JobNo := '';
                             ElementCode := '';
                             JobRec.INIT;
                           END;

                OnLookup=VAR
                           Company@1100525000 : Record 2000000006;
                         BEGIN
                           ScannerRec.RESET;
                           ScannerRec.SETRANGE(ScannerID, COPYSTR(USERID, 1, MAXSTRLEN(ScannerRec.ScannerID)));
                           ScannerRec.SETRANGE(Type, ScannerRec.Type::"Project Delivery");
                           ScannerRec.SETRANGE(Status, 0);

                           IF ScannerRec.COUNT <> 0 THEN
                             EXIT;

                           ScannerRec.RESET;

                           Company.RESET;
                           IF Company.GET(CompanyCode) THEN;
                           //**4PS.AH Enter customer specific number for 'Company List'
                           IF PAGE.RUNMODAL(11126611, Company) = ACTION::LookupOK THEN BEGIN
                             CompanyCode := Company.Name ;
                           END;

                           JobNo := '';
                           ElementCode := '';
                           JobRec.INIT;
                         END;

                QuickEntry=FALSE }

    { 1100525002;1;Field  ;
                Name=CtrlJobNo;
                CaptionML=[DEU=Projekt;
                           ENU=Job;
                           NLD=Project;
                           NOR=Prosjekt;
                           SVE=Projekt];
                SourceExpr=JobNo;
                OnValidate=BEGIN
                             ScannerRec.RESET;
                             ScannerRec.SETRANGE(ScannerID, COPYSTR(USERID, 1, MAXSTRLEN(ScannerRec.ScannerID)));
                             ScannerRec.SETRANGE(Type, ScannerRec.Type::"Project Delivery");
                             ScannerRec.SETRANGE(Status, 0);
                             ScannerRec.SETFILTER("Project No.", '<>%1', JobNo);
                             IF ScannerRec.FINDFIRST THEN
                               ERROR(Text004, ScannerRec."Project No.");
                             ScannerRec.RESET;

                             JobRec.CHANGECOMPANY(CompanyCode);
                             JobRec.RESET;
                             JobRec.SETRANGE("Project Status", JobRec."Project Status"::Production, JobRec."Project Status"::"Technical Finished");
                             JobRec.SETRANGE("No.", JobNo);
                             IF NOT JobRec.FIND('-') THEN BEGIN
                               ERROR(Text006);
                             END;

                             JobRec.CALCFIELDS("Principal Name");
                             ElementCode := '';
                           END;

                OnLookup=BEGIN
                           ScannerRec.RESET;
                           ScannerRec.SETRANGE(ScannerID, COPYSTR(USERID, 1, MAXSTRLEN(ScannerRec.ScannerID)));
                           ScannerRec.SETRANGE(Type, ScannerRec.Type::"Project Delivery");
                           ScannerRec.SETRANGE(Status, 0);
                           IF ScannerRec.COUNT <> 0 THEN
                             EXIT;

                           ScannerRec.RESET;

                           JobRec.CHANGECOMPANY(CompanyCode);
                           JobRec.RESET;
                           JobRec.SETRANGE("Project Status", JobRec."Project Status"::Production, JobRec."Project Status"::"Technical Finished");
                           //**4PS.AH Enter customer specific number for 'Job List'
                           IF PAGE.RUNMODAL(11126612, JobRec) = ACTION::LookupOK THEN BEGIN
                             JobNo := JobRec."No." ;
                             JobRec.GET(JobNo);
                             JobRec.CALCFIELDS("Principal Name");
                           END;
                         END;

                QuickEntry=FALSE }

    { 1100525009;1;Field  ;
                CaptionML=[DEU=Auftraggeber;
                           ENU=Principal;
                           NLD=Opdrachtgever;
                           NOR=Oppdragsgiver;
                           SVE=Uppdragsgivare];
                SourceExpr=JobRec."Principal Name";
                Enabled=FALSE;
                Editable=FALSE;
                QuickEntry=FALSE }

    { 1100525012;1;Field  ;
                CaptionML=[DEU=Element;
                           ENU=Element;
                           NLD=Element;
                           NOR=Element;
                           SVE=Element];
                SourceExpr=ElementCode;
                OnValidate=VAR
                             ElementFormat@1100525004 : Codeunit 11012022;
                             Chapter@1100525003 : Code[20];
                             Paragraph@1100525002 : Code[20];
                             Level@1100525001 : Integer;
                             ElementRec@1100525000 : Record 11012010;
                           BEGIN
                             IF ElementCode <> '' THEN BEGIN
                               ElementFormat.CreateStandardElement(ElementCode,Chapter,Paragraph,Level);
                               IF ElementCode = '' THEN BEGIN
                                 ERROR(Text002);
                               END ELSE BEGIN
                                 ElementRec.RESET;
                                 ElementRec.CHANGECOMPANY(CompanyCode);
                                 ElementRec.SETRANGE("Project No.", JobNo);
                                 ElementRec.SETRANGE(Element, ElementCode);
                                 IF NOT ElementRec.FIND('-') THEN BEGIN
                                   ElementCode := '';
                                   ERROR(Text002);
                                 END;
                               END;
                             END;
                           END;

                OnLookup=VAR
                           ElementRec@1100525000 : Record 11012010;
                         BEGIN
                           ElementRec.RESET;
                           ElementRec.CHANGECOMPANY(CompanyCode);
                           ElementRec.SETRANGE("Project No.", JobNo);

                           ElementRec."Project No." := JobNo;
                           ElementRec.Element := ElementCode;
                           //**4PS.AH Enter customer specific number for 'Element List'
                           IF PAGE.RUNMODAL(11126613, ElementRec) = ACTION::LookupOK THEN
                             ElementCode := ElementRec.Element;
                         END;

                QuickEntry=FALSE }

    { 1100525003;1;Field  ;
                Name=CntrlItemCode;
                CaptionML=[DEU=Item-Code;
                           ENU=Item code;
                           NLD=Artikelcode];
                SourceExpr=ItemNo;
                OnValidate=BEGIN
                             IF ItemRec.GET(ItemNo) THEN
                                ItemDesc := ItemRec.Description
                             ELSE BEGIN
                               ItemDesc := '';
                               ItemNo := '';
                               ERROR('Artikel onbekend');
                             END;

                             IF (ItemRec.Blocked) AND (NOT ErrorSet) THEN BEGIN
                               ERROR(STRSUBSTNO('%1 is geblokkeerd', ItemNo));
                               ItemDesc := '';
                               ItemNo := '';
                             END;
                           END;

                OnLookup=BEGIN
                           ItemRec.RESET;
                           ItemRec.SETRANGE(Blocked, FALSE);
                           //**4PS.AH Enter customer specific number for 'Item List'
                           IF PAGE.RUNMODAL(11126614, ItemRec) = ACTION::LookupOK THEN
                             ItemNo := ItemRec."No." ;

                           ItemRec.RESET;

                           ItemRec.GET(ItemNo);
                           ItemDesc := ItemRec.Description;
                         END;

                QuickEntry=TRUE }

    { 1100525011;1;Field  ;
                CaptionML=[DEU=Beschreibung;
                           ENU=Description;
                           NLD=Omschrijving;
                           NOR=Beskrivelse;
                           SVE=Beskrivning];
                SourceExpr=ItemRec.Description;
                Visible=false;
                Enabled=FALSE;
                Editable=FALSE;
                QuickEntry=FALSE }

    { 1100525004;1;Field  ;
                Name=CntrlQuantity;
                CaptionML=[DEU=Menge;
                           ENU=Quantity;
                           NLD=Hoeveelheid;
                           NOR=Antall;
                           SVE=Antal];
                SourceExpr=Quantity;
                OnValidate=BEGIN
                             IF Quantity <> 0 THEN
                               AddScanLine;
                           END;
                            }

    { 1100525006;1;Field  ;
                CaptionML=[DEU=Basiseinheitencode;
                           ENU=Base Unit of Measure;
                           NLD=Basiseenheid;
                           NOR=Lagerenhet;
                           SVE=Basenhet];
                SourceExpr=ItemRec."Base Unit of Measure";
                Enabled=FALSE;
                Editable=FALSE;
                QuickEntry=FALSE }

  }
  CODE
  {
    VAR
      CompanyInfo@1100525029 : Record 79;
      UserInfo@1100525028 : Record 91;
      InvSetupRec@1100525027 : Record 313;
      LocationRec@1100525026 : Record 14;
      ScannerRec@1100525025 : Record 11012061;
      ItemRec@1100525024 : Record 27;
      JobRec@1100525023 : Record 11072003;
      UserMgt@1100525032 : Codeunit 5700;
      LocationNo@1100525017 : Code[20];
      ItemNo@1100525016 : Code[20];
      JobNo@1100525015 : Code[20];
      Quantity@1100525014 : Decimal;
      ItemDesc@1100525013 : Text[100];
      LineNumber@1100525012 : Integer;
      ErrorSet@1100525007 : Boolean;
      ElementCode@1100525006 : Code[20];
      JobSetup@1100525000 : Record 315;
      Text001@1100525030 : TextConst 'DEU=Mehr als eine Sitzung fÅr %1 geîffnet;ENU=More than one session for %1 open;NLD=Meerdere sessies voor %1 geopend';
      CompanyCode@1100525031 : Text[50];
      Text002@1100528900 : TextConst 'DEU=Element unbekannt.;ENU=Element unknown.;NLD=Element onbekend.';
      Text003@1100528901 : TextConst 'DEU=Die Zeilen von Projekt %1 zuerst verarbeiten.;ENU=Process the lines of project %1 first.;NLD=Eerst de regels op project %1 afhandelen.';
      Text004@1100528902 : TextConst 'DEU=Die Zeilen des empfangenden Unternehmens %1 zuerst verarbeiten.;ENU=Process the lines of receiving company %1 first.;NLD=Eerst de regels op ontvangend bedrijf %1 afhandelen.';
      Text005@1100528903 : TextConst 'DEU=Element muss eingegeben werden.;ENU=Element must be filled.;NLD=Element moet worden ingevuld.';
      Text006@1100528904 : TextConst 'DEU=Projekt unbekannt.;ENU=Project unknown.;NLD=Project onbekend.';

    PROCEDURE AddScanLine@1100409000();
    BEGIN
      IF NOT LocationRec.GET(LocationNo) THEN
        EXIT;

      IF (JobNo = '') OR
         (ItemNo = '') OR
         (Quantity = 0) THEN
        EXIT;

      JobSetup.GET;
      IF (ElementCode = '') AND (JobSetup."Posting Element Mandatory") THEN BEGIN
        ERROR(Text005);
      END;

      ScannerRec.INIT;
      ScannerRec.RESET;

      ScannerRec.ScannerID := COPYSTR(USERID, 1, MAXSTRLEN(ScannerRec.ScannerID));
      ScannerRec.Type := ScannerRec.Type::"Project Delivery";
      ScannerRec.Date := TODAY;
      ScannerRec.Time := TIME;
      ScannerRec."Line No." := '1';
      ScannerRec."Location Code" := LocationNo;
      ScannerRec."Receiving Company" := CompanyCode;
      ScannerRec."Project No." := JobNo;
      ScannerRec."Item No." := ItemNo;
      ScannerRec.Element := ElementCode;
      ScannerRec.Quantity := Quantity;

      REPEAT
        EVALUATE(LineNumber, ScannerRec."Line No.");
        LineNumber += 1;
        ScannerRec."Line No." := FORMAT(LineNumber);
      UNTIL ScannerRec.INSERT(TRUE);

      ItemNo := '';
      ItemDesc := '';
      Quantity := 0;

      CLEAR(ItemRec);
      CLEAR(ScannerRec);
      CLEAR(LocationRec);

      {
      IF ISCLEAR(WSHShell) THEN
        CREATE(WSHShell,FALSE,TRUE);
      WSHShell.SendKeys('+{TAB}');   // <SHIFT><TAB>
      WSHShell.SendKeys('+{TAB}');   // <SHIFT><TAB>  to itemcode
      CLEAR(WSHShell);
      }
    END;

    BEGIN
    END.
  }
}

