OBJECT Page 1816 Job Creation Wizard
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create New Job;
               NOR=Opprett nytt prosjekt;
               SVE=Skapa nytt projekt];
    SourceTable=Table11072003;
    DelayedInsert=Yes;
    PageType=NavigatePage;
    OnInit=BEGIN
             LoadTopBanners;
           END;

    OnOpenPage=BEGIN
                 INIT;

                 Step := Step::Start;
                 EnableControls;
               END;

    ActionList=ACTIONS
    {
      { 10      ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 15      ;1   ;Action    ;
                      Name=ActionBack;
                      CaptionML=[ENU=Back;
                                 NOR=Tilbake;
                                 SVE=Bak†t];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=BackActionEnabled;
                      InFooterBar=Yes;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 NextStep(TRUE);
                               END;
                                }
      { 14      ;1   ;Action    ;
                      Name=ActionNext;
                      CaptionML=[ENU=Next;
                                 NOR=Neste;
                                 SVE=N„sta];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=NextActionEnabled;
                      InFooterBar=Yes;
                      Image=NextRecord;
                      OnAction=BEGIN
                                 NextStep(FALSE);
                               END;
                                }
      { 11      ;1   ;Action    ;
                      Name=ActionFinish;
                      CaptionML=[ENU=Finish;
                                 NOR=Fullf›r;
                                 SVE=Slutf”r];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=FinishActionEnabled;
                      InFooterBar=Yes;
                      Image=Approve;
                      OnAction=BEGIN
                                 FinishAction;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 96  ;1   ;Group     ;
                Visible=TopBannerVisible AND NOT FinalStepVisible;
                Editable=FALSE;
                GroupType=Group }

    { 97  ;2   ;Field     ;
                ApplicationArea=#Jobs;
                SourceExpr=MediaResourcesStandard."Media Reference";
                Editable=FALSE;
                ShowCaption=No }

    { 98  ;1   ;Group     ;
                Visible=TopBannerVisible AND FinalStepVisible;
                Editable=FALSE;
                GroupType=Group }

    { 99  ;2   ;Field     ;
                ApplicationArea=#Jobs;
                SourceExpr=MediaResourcesDone."Media Reference";
                Editable=FALSE;
                ShowCaption=No }

    { 25  ;1   ;Group     ;
                Visible=FirstStepVisible;
                GroupType=Group }

    { 24  ;2   ;Group     ;
                CaptionML=[ENU=Welcome to Create New Job;
                           NOR=Velkommen til Opprett nytt prosjekt;
                           SVE=V„lkommen till Skapa nytt projekt];
                Visible=FirstStepVisible;
                GroupType=Group }

    { 23  ;3   ;Group     ;
                Visible=FirstStepVisible;
                GroupType=Group;
                InstructionalTextML=[ENU=Do you want to create a new job from an existing job?;
                                     NOR=Vil du opprette et nytt prosjekt fra et eksisterende prosjekt?;
                                     SVE=Vill du skapa ett nytt projekt utifr†n ett befintligt projekt?] }

    { 21  ;4   ;Field     ;
                Name=FromExistingJob;
                CaptionML=;
                ApplicationArea=#Jobs;
                SourceExpr=FromExistingJob;
                CaptionClass=FORMAT(FromExistingJob) }

    { 19  ;1   ;Group     ;
                Visible=CreationStepVisible;
                GroupType=Group }

    { 20  ;2   ;Group     ;
                CaptionML=[ENU=Welcome to Create New Job;
                           NOR=Velkommen til Opprett nytt prosjekt;
                           SVE=V„lkommen till Skapa nytt projekt];
                Visible=CreationStepVisible;
                GroupType=Group }

    { 18  ;3   ;Group     ;
                Visible=CreationStepVisible;
                GroupType=Group;
                InstructionalTextML=[ENU=Fill in the following fields for the new job.;
                                     NOR=Fyll ut f›lgende felt for det nye prosjektet.;
                                     SVE=Fyll i f”ljande f„lt f”r det nya projektet.] }

    { 16  ;4   ;Field     ;
                CaptionML=[ENU=No.;
                           NOR=Nr.;
                           SVE=Nr];
                ApplicationArea=#Jobs;
                SourceExpr="No.";
                OnAssistEdit=BEGIN
                               IF AssistEdit(xRec) THEN
                                 CurrPage.UPDATE;
                             END;
                              }

    { 26  ;4   ;Field     ;
                CaptionML=[ENU=Description;
                           NOR=Beskrivelse;
                           SVE=Beskrivning];
                ApplicationArea=#Jobs;
                SourceExpr=Description }

    { 27  ;4   ;Field     ;
                CaptionML=[ENU=Bill-to Customer No.;
                           NOR=Faktura til-kundenr.;
                           SVE=Faktureringskundnr];
                ApplicationArea=#Jobs;
                SourceExpr="Bill-to Customer No.";
                TableRelation=Customer }

    { 9   ;3   ;Group     ;
                GroupType=Group;
                InstructionalTextML=[ENU=To select the tasks to copy from an existing job, choose Next.;
                                     NOR=Hvis du vil velge oppgavene som skal kopieres fra et eksisterende prosjekt, velger du Neste.;
                                     SVE=V„lj N„sta om du vill v„lja uppgifter som ska kopieras fr†n ett befintligt projekt.] }

    { 8   ;1   ;Group     ;
                Visible=FinalStepVisible;
                GroupType=Group }

    { 12  ;2   ;Group     ;
                CaptionML=[ENU=That's it!;
                           NOR=Det var det!;
                           SVE=Klart!];
                GroupType=Group }

    { 4   ;3   ;Group     ;
                GroupType=Group;
                InstructionalTextML=[ENU=To view your new job, choose Finish.;
                                     NOR=Hvis du vil vise det nye prosjektet, velger du Fullf›r.;
                                     SVE=V„lj Slutf”r n„r du vill visa det nya projektet.] }

  }
  CODE
  {
    VAR
      MediaRepositoryStandard@1040 : Record 9400;
      MediaRepositoryDone@1041 : Record 9400;
      MediaResourcesStandard@1002 : Record 2000000182;
      MediaResourcesDone@1001 : Record 2000000182;
      ClientTypeManagement@1003 : Codeunit 4030;
      Step@1012 : 'Start,Creation,Finish';
      TopBannerVisible@1042 : Boolean;
      FirstStepVisible@1000 : Boolean;
      CreationStepVisible@1010 : Boolean;
      FinalStepVisible@1015 : Boolean;
      FinishActionEnabled@1009 : Boolean;
      BackActionEnabled@1013 : Boolean;
      NextActionEnabled@1014 : Boolean;
      FromExistingJob@1022 : Boolean;
      SelectJobNumberMsg@1026 : TextConst 'ENU=To continue, specify the job number that you want to copy.;NOR=Hvis du vil fortsette, angir du prosjektnummeret du vil kopiere.;SVE=Forts„tt genom att ange projektnumret som du vill kopiera.';
      SelectCustomerNumberMsg@1027 : TextConst 'ENU=To continue, specify the customer of the new job.;NOR=Hvis du vil fortsette, angir du kunden for det nye prosjektet.;SVE=Forts„tt genom att ange kunden f”r det nya projektet.';

    LOCAL PROCEDURE EnableControls@2();
    BEGIN
      IF Step = Step::Finish THEN BEGIN
        IF "No." = '' THEN BEGIN
          MESSAGE(SelectJobNumberMsg);
          Step := Step - 1;
          EXIT;
        END;

        IF "Bill-to Customer No." = '' THEN BEGIN
          MESSAGE(SelectCustomerNumberMsg);
          Step := Step - 1;
          EXIT;
        END;
      END;

      ResetControls;

      CASE Step OF
        Step::Start:
          ShowStartStep;
        Step::Creation:
          ShowCreationStep;
        Step::Finish:
          ShowFinalStep;
      END;
    END;

    LOCAL PROCEDURE FinishAction@12();
    BEGIN
      PAGE.RUN(PAGE::"Job Card",Rec);
      CurrPage.CLOSE;
    END;

    LOCAL PROCEDURE NextStep@41(Backwards@1000 : Boolean);
    BEGIN
      IF Backwards THEN
        Step := Step - 1
      ELSE
        Step := Step + 1;

      EnableControls;
    END;

    LOCAL PROCEDURE ShowStartStep@24();
    BEGIN
      FirstStepVisible := TRUE;
      FinishActionEnabled := FALSE;
      BackActionEnabled := FALSE;
      FromExistingJob := TRUE;
    END;

    LOCAL PROCEDURE ShowCreationStep@33();
    BEGIN
      CreationStepVisible := TRUE;
      FinishActionEnabled := FALSE;

      // If user clicked "Back", the Job will already exist, so don't try to create it again.
      IF "No." = '' THEN BEGIN
        INSERT(TRUE);
        COMMIT;
      END;

      IF NOT FromExistingJob THEN
        FinishAction;
    END;

    LOCAL PROCEDURE ShowFinalStep@34();
    VAR
      CopyJobTasks@1000 : Page 1041;
    BEGIN
      FinalStepVisible := TRUE;
      BackActionEnabled := FALSE;
      NextActionEnabled := FALSE;

      CopyJobTasks.SetToJob(Rec);
      CopyJobTasks.RUN;
    END;

    LOCAL PROCEDURE ResetControls@1();
    BEGIN
      FinishActionEnabled := TRUE;
      BackActionEnabled := TRUE;
      NextActionEnabled := TRUE;

      FirstStepVisible := FALSE;
      CreationStepVisible := FALSE;
      FinalStepVisible := FALSE;
    END;

    LOCAL PROCEDURE LoadTopBanners@40();
    BEGIN
      IF MediaRepositoryStandard.GET('AssistedSetup-NoText-400px.png',FORMAT(ClientTypeManagement.GetCurrentClientType)) AND
         MediaRepositoryDone.GET('AssistedSetupDone-NoText-400px.png',FORMAT(ClientTypeManagement.GetCurrentClientType))
      THEN
        IF MediaResourcesStandard.GET(MediaRepositoryStandard."Media Resources Ref") AND
           MediaResourcesDone.GET(MediaRepositoryDone."Media Resources Ref")
        THEN
          TopBannerVisible := MediaResourcesDone."Media Reference".HASVALUE;
    END;

    BEGIN
    END.
  }
}

