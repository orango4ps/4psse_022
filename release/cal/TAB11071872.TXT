OBJECT Table 11071872 Project Site Header
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    DataCaptionFields=Code;
    OnInsert=BEGIN
               ProjectSiteMgt.LogProjectSiteHeader(1, Rec); //**4PS.n
             END;

    OnModify=VAR
               ProjectSiteLine@1100528300 : Record 11071873;
             BEGIN
               ProjectSiteLine.SETRANGE("Seq. No.","Seq. No.");
               IF (Type <> 0) AND (NOT ProjectSiteLine.FINDFIRST) THEN BEGIN
                 ProjectSiteLine.INIT;
                 ProjectSiteLine."Seq. No." := "Seq. No.";
                 ProjectSiteLine."Line No." := 10000;
                 ProjectSiteLine.Type := Type;
                 ProjectSiteLine.Code := Code;
                 ProjectSiteLine."Company Name" := COMPANYNAME;
                 ProjectSiteLine.INSERT(TRUE);
               END;

               ProjectSiteMgt.LogProjectSiteHeader(2, Rec); //**4PS.n
             END;

    OnDelete=VAR
               ProjectSiteLine@1000000000 : Record 11071873;
             BEGIN
               TESTFIELD("SharePoint Status", "SharePoint Status"::New);

               ProjectSiteLine.SETRANGE("Seq. No.", "Seq. No.");
               ProjectSiteLine.DELETEALL(TRUE);

               ProjectSiteMgt.LogProjectSiteHeader(3, Rec); //**4PS.n
             END;

    CaptionML=[ENU=Project Site Header;
               NOR=Prosjektlokasjonshode;
               SVE=Projektplatsrubrik];
    LookupPageID=Page11125090;
    DrillDownPageID=Page11125090;
  }
  FIELDS
  {
    { 10  ;   ;Seq. No.            ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[ENU=Seq. No.;
                                                              NOR=Sekv. nr;
                                                              SVE=Sekv. nr];
                                                   Editable=No }
    { 20  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF Code <> '' THEN VALIDATE(Code);
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Typ];
                                                   OptionCaptionML=[ENU=" ,Project,Try-Out";
                                                                    NOR=" ,Prosjekt,Prospekt";
                                                                    SVE=" ,Projekt,Anbud"];
                                                   OptionString=,Project,Try-Out }
    { 30  ;   ;Code                ;Code20        ;TableRelation=IF (Type=CONST(Project)) Job
                                                                 ELSE IF (Type=CONST(Try-Out)) Try-out;
                                                   OnValidate=VAR
                                                                Project@1100528300 : Record 11072003;
                                                                TryOut@1100528301 : Record 11012750;
                                                                ProjectSiteSetup@1100528302 : Record 11071871;
                                                              BEGIN
                                                                ProjectSiteSetup.GET();
                                                                TESTFIELD("SharePoint Status", "SharePoint Status"::New);

                                                                IF STRLEN(Code + ProjectSiteSetup."Website URL") > 250 THEN
                                                                 ERROR(text003);

                                                                IF Type = Type::Project THEN BEGIN
                                                                  IF (Project.GET(Code)) THEN BEGIN
                                                                    IF (Name = '') THEN
                                                                      Name := Project.Description;
                                                                    IF (Description = '') THEN
                                                                      Description := Project."Description 2";
                                                                  END;
                                                                END;
                                                                IF Type = Type::"Try-Out" THEN BEGIN
                                                                  IF (TryOut.GET(Code)) THEN BEGIN
                                                                    IF (Name = '') THEN
                                                                      Name := TryOut.Description;
                                                                  END;
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              Project@1100528301 : Record 11072003;
                                                              TryOut@1100528300 : Record 11012750;
                                                            BEGIN
                                                              IF Type = Type::Project THEN BEGIN
                                                                Project."No." := Code;
                                                                IF PAGE.RUNMODAL(0, Project) = ACTION::LookupOK THEN
                                                                  VALIDATE(Code, Project."No.");
                                                              END;
                                                              IF Type = Type::"Try-Out" THEN BEGIN
                                                                TryOut.Code := Code;
                                                                IF PAGE.RUNMODAL(0, TryOut) = ACTION::LookupOK THEN
                                                                  VALIDATE(Code, TryOut.Code);
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   CharAllowed=AZaz09 }
    { 40  ;   ;Name                ;Text100       ;CaptionML=[ENU=Name;
                                                              NOR=Navn;
                                                              SVE=Namn];
                                                   NotBlank=Yes }
    { 50  ;   ;Description         ;Text80        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 55  ;   ;Template Code       ;Code20        ;TableRelation="Project Site Template";
                                                   CaptionML=[ENU=Template Code;
                                                              NOR=Malkode;
                                                              SVE=Mallkod] }
    { 56  ;   ;Template Description;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Site Template".Description WHERE (Code=FIELD(Template Code)));
                                                   CaptionML=[ENU=Template Description;
                                                              NOR=Malbeskrivelse;
                                                              SVE=Mallbeskrivning];
                                                   Editable=No }
    { 60  ;   ;SharePoint Status   ;Option        ;CaptionML=[ENU=SharePoint Status;
                                                              NOR=SharePoint-status;
                                                              SVE=SharePoint-status];
                                                   OptionCaptionML=[ENU=New,Added,Modified,Updated;
                                                                    NOR=Ny,Lagt til,Endret,Oppdatert;
                                                                    SVE=Ny,Tillagd,Žndrad,Uppdaterad];
                                                   OptionString=New,Added,Modified,Updated;
                                                   Editable=No }
    { 70  ;   ;Project Site Text   ;Text250       ;CaptionML=[ENU=Project Site Text;
                                                              NOR=Prosjektplass tekst;
                                                              SVE=Projektplats text] }
    { 80  ;   ;Project Site Text 2 ;Text250       ;CaptionML=[ENU=Project Site Text 2;
                                                              NOR=Prosjektplass tekst 2;
                                                              SVE=Projektplats text 2] }
    { 90  ;   ;Blocked             ;Boolean       ;CaptionML=[ENU=Blocked;
                                                              NOR=Sperret;
                                                              SVE=Sp„rrad] }
  }
  KEYS
  {
    {    ;Seq. No.                                ;Clustered=Yes }
    {    ;Type,Code                                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      text003@1000000004 : TextConst 'ENU=The maximum lenght of the URL (360) is exceeded.';
      ProjectSiteMgt@1100528300 : Codeunit 11012282;

    PROCEDURE CreateSite@1000000001();
    VAR
      ProjectSiteLine@1000000000 : Record 11071873;
    BEGIN
      TESTFIELD(Blocked, FALSE);
      IF ("SharePoint Status" < "SharePoint Status"::Added) THEN BEGIN
        "SharePoint Status" := "SharePoint Status"::Added;
      END;
      //kkleermaker.sn, 130103, C004200
      //When button CreateSite is pushed, always a changelog record for the project site header
      //should be written and no changelog record for the project site line.
      MODIFY(TRUE);
      //kkleermaker.en

      ProjectSiteLine.RESET;
      ProjectSiteLine.SETRANGE("Seq. No.","Seq. No.");
      ProjectSiteLine.SETRANGE("Site Created", FALSE);
      ProjectSiteLine.SETFILTER("Role Code", '<>%1', '');
      ProjectSiteLine.SETFILTER("Company Name", '<>%1', '');
      IF (ProjectSiteLine.FINDSET) THEN BEGIN
        REPEAT
          CreateSiteLine(ProjectSiteLine);
        UNTIL (ProjectSiteLine.NEXT = 0);
      END;
    END;

    PROCEDURE CreateSiteLine@1100409001(ProjectSiteLine@1100409000 : Record 11071873);
    VAR
      ProjectSiteHeader@1100409001 : Record 11071872;
    BEGIN
      ProjectSiteLine.TESTFIELD("Role Code");
      ProjectSiteHeader.GET(ProjectSiteLine."Seq. No.");
      ProjectSiteHeader.TESTFIELD(Blocked, FALSE);
      ProjectSiteHeader.TESTFIELD("SharePoint Status", ProjectSiteHeader."SharePoint Status"::Added);

      ProjectSiteLine."Site Created" := TRUE;
      ProjectSiteLine."Sharepoint No." := ProjectSiteLine.GetLastSharepointNo("Seq. No.") + 1;
      ProjectSiteLine.MODIFY;
      IF (ProjectSiteLine.Type = ProjectSiteLine.Type::"Try-Out") THEN
        UpdateTryOut(ProjectSiteLine.Code, ProjectSiteHeader.GetSiteURL);

      CreateSiteLineDocs(ProjectSiteLine);
    END;

    PROCEDURE CreateSiteLineDocs@1100409002(ProjectSiteLine@1100409002 : Record 11071873);
    VAR
      ProjectSiteHeader@1100409001 : Record 11071872;
    BEGIN
      ProjectSiteHeader.GET(ProjectSiteLine."Seq. No.");
      ProjectSiteHeader.TESTFIELD(Blocked, FALSE);
      ProjectSiteHeader.TESTFIELD("SharePoint Status", ProjectSiteHeader."SharePoint Status"::Added);

      CreateDocPropertiesLogging(ProjectSiteLine);
    END;

    PROCEDURE GetSiteURL@1100528328() SiteURL : Text[250];
    VAR
      ProjectSiteSetup@1100528300 : Record 11071871;
    BEGIN
      ProjectSiteSetup.GET;
      IF (Code = '') THEN
        SiteURL := ''
      ELSE
        SiteURL := ProjectSiteSetup."Website URL" + '/' + Code;
    END;

    PROCEDURE CreateDocPropertiesLogging@1100409000(ProjectSiteLine@1100409002 : Record 11071873);
    VAR
      DocumentLink@1100409004 : Record 11012747;
      DocumentProperties@1100409001 : Record 11012746;
      Job@1100409006 : Record 11072003;
      TryOut@1100409007 : Record 11012750;
      ProjectSiteMgt@1100409000 : Codeunit 11012282;
      RecRef@1100409005 : RecordRef;
    BEGIN
      Job.CHANGECOMPANY(ProjectSiteLine."Company Name");
      TryOut.CHANGECOMPANY(ProjectSiteLine."Company Name");

      CASE ProjectSiteLine.Type OF
        1: BEGIN
          Job.GET(ProjectSiteLine.Code);
          RecRef.GETTABLE(Job);
        END;
        2: BEGIN
          TryOut.GET(ProjectSiteLine.Code);
          RecRef.GETTABLE(TryOut);
        END;
      END;

      DocumentLink.SETCURRENTKEY("Record ID");
      DocumentLink.SETRANGE("Record ID", RecRef.RECORDID);
      IF (NOT DocumentLink.FINDSET) THEN
        EXIT;

      REPEAT
        IF (DocumentProperties.GET(DocumentLink."Document No.")) THEN BEGIN
          IF (NOT DocumentProperties.HasStorageURI) THEN
            ProjectSiteMgt.LogDocumentProperties(1, DocumentProperties);
        END;
      UNTIL (DocumentLink.NEXT = 0);
    END;

    PROCEDURE UpdateTryOut@1100409003(TryOutCode@1100409000 : Code[20];SiteURL@1100409001 : Text[100]);
    VAR
      TryOut@1100409002 : Record 11012750;
    BEGIN
      //C018525.n
      TryOut.GET(TryOutCode);
      TryOut."SharePoint Site URL" := SiteURL;
      TryOut.FillModificationData;
      TryOut.MODIFY;
    END;

    BEGIN
    END.
  }
}

