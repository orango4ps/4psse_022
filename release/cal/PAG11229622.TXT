OBJECT Page 11229622 DPT Fill DropZone Fields
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Dokumentenportal DropZone-Felder;
               ENU=Document Portal DropZone Fields;
               NLD=Documentenportaal DropZone velden];
    PopulateAllFields=Yes;
    PageType=StandardDialog;
    OnOpenPage=BEGIN
                 ProjectFieldsPresent := FALSE;
               END;

  }
  CONTROLS
  {
    { 1100525000;0;Container;
                ContainerType=ContentArea }

    { 1100528304;1;Field  ;
                CaptionML=[DEU=Belegportal - Subsite;
                           ENU=Document Portal Subsite;
                           NLD=Documentenportaal subsite];
                SourceExpr=SubsiteCode;
                OnValidate=VAR
                             DPTSubsiteProject@1100528300 : Record 11072330;
                             DPTSubsiteServLoc@1100525000 : Record 11072344;
                           BEGIN
                             CASE SourceType OF
                               SourceType::Project: DPTSubsiteProject.GET(SourceNo, SubsiteCode);
                               SourceType::ServiceLocation: DPTSubsiteServLoc.GET(SourceNo, SubsiteCode);
                             END;
                             DocumentType := '';
                           END;

                OnLookup=VAR
                           DPTSubsiteProject@1100528300 : Record 11072330;
                           DPTSubsiteServLoc@1100525001 : Record 11072344;
                           DPTSubsiteListProject@1100528301 : Page 11229653;
                           DPTSubsiteListServLoc@1100525002 : Page 11130628;
                         BEGIN
                           CASE SourceType OF
                             SourceType::Project: BEGIN
                               IF (DPTSubsiteProject.GET(SourceNo, SubsiteCode)) THEN;
                               DPTSubsiteProject.SETRANGE("Project No.", SourceNo);
                               DPTSubsiteProject.SETRANGE("Quote Request Subsite", FALSE);
                               DPTSubsiteListProject.LOOKUPMODE := TRUE;
                               DPTSubsiteListProject.SETTABLEVIEW(DPTSubsiteProject);
                               IF (DPTSubsiteListProject.RUNMODAL = ACTION::LookupOK) THEN BEGIN
                                 DPTSubsiteListProject.GETRECORD(DPTSubsiteProject);
                                 SubsiteCode := DPTSubsiteProject.Code;
                                 DocumentType := '';
                               END;
                             END;
                             SourceType::ServiceLocation: BEGIN
                               IF (DPTSubsiteServLoc.GET(SourceNo, SubsiteCode)) THEN;
                               DPTSubsiteServLoc.SETRANGE("Service Location No.", SourceNo);
                               DPTSubsiteListServLoc.LOOKUPMODE := TRUE;
                               DPTSubsiteListServLoc.SETTABLEVIEW(DPTSubsiteServLoc);
                               IF (DPTSubsiteListServLoc.RUNMODAL = ACTION::LookupOK) THEN BEGIN
                                 DPTSubsiteListServLoc.GETRECORD(DPTSubsiteServLoc);
                                 SubsiteCode := DPTSubsiteServLoc.Code;
                                 DocumentType := '';
                               END;
                             END;
                           END;
                         END;
                          }

    { 1100528305;1;Field  ;
                CaptionML=[DEU=Belegart;
                           ENU=Document Type;
                           NLD=Documentsoort;
                           NOR=Dokumenttype;
                           SVE=Dokumenttyp];
                SourceExpr=DocumentType;
                OnValidate=VAR
                             DPTSubsiteDocTypeProject@1100528300 : Record 11072331;
                             DPTSubsiteDocTypeServLoc@1100525000 : Record 11072345;
                           BEGIN
                             CASE SourceType OF
                               SourceType::Project: DPTSubsiteDocTypeProject.GET(SourceNo, SubsiteCode, DocumentType);
                               SourceType::ServiceLocation: DPTSubsiteDocTypeServLoc.GET(SourceNo, SubsiteCode, DocumentType);
                             END;
                           END;

                OnLookup=VAR
                           DPTSubsiteDocTypeProject@1100528301 : Record 11072331;
                           DPTSubsiteDocTypeServLoc@1100525001 : Record 11072345;
                           DPTDocTypeSubsiteListProj@1100528300 : Page 11229659;
                           DPTDocTypeSubsiteListSLoc@1100525002 : Page 11130620;
                         BEGIN
                           CASE SourceType OF
                             SourceType::Project: BEGIN
                               IF (DPTSubsiteDocTypeProject.GET(SourceNo, SubsiteCode, DocumentType)) THEN;
                               DPTSubsiteDocTypeProject.SETRANGE("Project No.", SourceNo);
                               DPTSubsiteDocTypeProject.SETRANGE("Subsite Code", SubsiteCode);
                               DPTDocTypeSubsiteListProj.LOOKUPMODE := TRUE;
                               DPTDocTypeSubsiteListProj.SETTABLEVIEW(DPTSubsiteDocTypeProject);
                               IF (DPTDocTypeSubsiteListProj.RUNMODAL = ACTION::LookupOK) THEN BEGIN
                                 DPTDocTypeSubsiteListProj.GETRECORD(DPTSubsiteDocTypeProject);
                                 DocumentType := DPTSubsiteDocTypeProject."Document Type";
                               END;
                             END;
                             SourceType::ServiceLocation: BEGIN
                               IF (DPTSubsiteDocTypeServLoc.GET(SourceNo, SubsiteCode, DocumentType)) THEN;
                               DPTSubsiteDocTypeServLoc.SETRANGE("Service Location No.", SourceNo);
                               DPTSubsiteDocTypeServLoc.SETRANGE("Subsite Code", SubsiteCode);
                               DPTDocTypeSubsiteListSLoc.LOOKUPMODE := TRUE;
                               DPTDocTypeSubsiteListSLoc.SETTABLEVIEW(DPTSubsiteDocTypeServLoc);
                               IF (DPTDocTypeSubsiteListSLoc.RUNMODAL = ACTION::LookupOK) THEN BEGIN
                                 DPTDocTypeSubsiteListSLoc.GETRECORD(DPTSubsiteDocTypeServLoc);
                                 DocumentType := DPTSubsiteDocTypeServLoc."Document Type";
                               END;
                             END;
                           END;
                         END;
                          }

    { 1100528306;1;Field  ;
                CaptionML=[DEU=Kontaktnr.;
                           ENU=Contact No.;
                           NLD=Relatienr.;
                           NOR=Kontaktnr;
                           SVE=Kontaktnr];
                SourceExpr=ContactNo;
                OnValidate=VAR
                             Contact@1100528300 : Record 5050;
                           BEGIN
                             Contact.GET(ContactNo);
                             ContactPersonNo := '';
                           END;

                OnLookup=VAR
                           Contact@1100528300 : Record 5050;
                           ContactList@1100528301 : Page 5052;
                         BEGIN
                           IF (Contact.GET(ContactNo)) THEN;
                           Contact.SETRANGE("Person No.", '');
                           ContactList.LOOKUPMODE := TRUE;
                           ContactList.EDITABLE := FALSE;
                           ContactList.SETTABLEVIEW(Contact);
                           IF (ContactList.RUNMODAL = ACTION::LookupOK) THEN BEGIN
                             ContactList.GETRECORD(Contact);
                             ContactNo := Contact."No.";
                             ContactPersonNo := '';
                           END;
                         END;
                          }

    { 1100528307;1;Field  ;
                CaptionML=[DEU=Kontaktnr.;
                           ENU=Contact Person No.;
                           NLD=Contactpersoonnr.;
                           NOR=Kontaktperson;
                           SVE=Kontaktperson];
                SourceExpr=ContactPersonNo;
                OnValidate=VAR
                             Contact@1100528300 : Record 5050;
                           BEGIN
                             IF (ContactNo <> '') THEN
                               Contact.GET(ContactPersonNo);
                           END;

                OnLookup=VAR
                           Contact@1100528300 : Record 5050;
                           ContactList@1100528301 : Page 5052;
                         BEGIN
                           IF (Contact.GET(ContactPersonNo)) THEN;
                           Contact.SETRANGE(Type, Contact.Type::Person);
                           Contact.SETRANGE("Company No.", ContactNo);
                           ContactList.LOOKUPMODE := TRUE;
                           ContactList.EDITABLE := FALSE;
                           ContactList.SETTABLEVIEW(Contact);
                           IF (ContactList.RUNMODAL = ACTION::LookupOK) THEN BEGIN
                             ContactList.GETRECORD(Contact);
                             ContactPersonNo := Contact."No.";
                           END;
                         END;
                          }

    { 1100528308;1;Field  ;
                CaptionML=[DEU=Belegportal Privatbeleg;
                           ENU=Document Portal Private Document;
                           NLD=Documentenportaal priv‚ document];
                SourceExpr=PrivateCompanyDocument }

    { 1100528309;1;Field  ;
                CaptionML=[DEU=Projektelement;
                           ENU=Project Element;
                           NLD=Project element;
                           NOR=Prosjektelement;
                           SVE=Projektelement];
                SourceExpr=ProjectElementCode;
                Visible=ProjectFieldsPresent;
                OnValidate=VAR
                             ProjectElement@1100528300 : Record 11012010;
                           BEGIN
                             ProjectElement.GET(SourceNo, ProjectElementCode);
                           END;

                OnLookup=VAR
                           ProjectElement@1100528300 : Record 11012010;
                           ProjectElements@1100528301 : Page 11012028;
                         BEGIN
                           IF (ProjectElement.GET(SourceNo, ProjectElementCode)) THEN;
                           ProjectElements.LOOKUPMODE := TRUE;
                           ProjectElements.EDITABLE := FALSE;
                           ProjectElements.SETTABLEVIEW(ProjectElement);
                           IF (ProjectElements.RUNMODAL = ACTION::LookupOK) THEN BEGIN
                             ProjectElements.GETRECORD(ProjectElement);
                             ProjectElementCode := ProjectElement.Element;
                           END;
                         END;
                          }

    { 1100525001;1;Field  ;
                Name=DPTPublish;
                CaptionML=[DEU=Dokumentversion;
                           ENU=Document version;
                           NLD=Documentversie];
                OptionCaptionML=[DEU=Prim„re Version,Sekund„re Version;
                                 ENU=Major version,Minor version;
                                 NLD=Primaire versie,Secundaire versie];
                SourceExpr=DPTPublish }

  }
  CODE
  {
    VAR
      SourceType@1100525000 : ',Project,ServiceLocation';
      SourceNo@1100528306 : Code[20];
      SubsiteCode@1100528300 : Code[20];
      DocumentType@1100528301 : Code[10];
      ContactNo@1100528302 : Code[20];
      ContactPersonNo@1100528303 : Code[20];
      PrivateCompanyDocument@1100528304 : Boolean;
      ProjectElementCode@1100528305 : Code[20];
      ProjectFieldsPresent@1100525001 : Boolean;
      DPTPublish@1100525002 : 'Primary,Secondary';

    PROCEDURE SetVars@1100528311(iSourceType@1100525000 : Option;iSourceNo@1100528306 : Code[20];iSubsiteCode@1100528300 : Code[20];iDocumentType@1100528301 : Code[10];iContactNo@1100528302 : Code[20];iContactPersonNo@1100528303 : Code[20];iPrivateCompanyDocument@1100528304 : Boolean;iProjectElement@1100528305 : Code[20];iDPTPublish@1100525001 : Boolean);
    BEGIN
      SourceType := iSourceType;
      SourceNo := iSourceNo;
      SubsiteCode := iSubsiteCode;
      DocumentType := iDocumentType;
      ContactNo := iContactNo;
      ContactPersonNo := iContactPersonNo;
      PrivateCompanyDocument := iPrivateCompanyDocument;
      ProjectElementCode := iProjectElement;

      ProjectFieldsPresent := (SourceType = SourceType::Project);

      IF (iDPTPublish) THEN
        DPTPublish := DPTPublish::Primary
      ELSE
        DPTPublish := DPTPublish::Secondary;
    END;

    PROCEDURE GetVars@1100528312(VAR iSubsiteCode@1100528305 : Code[20];VAR iDocumentType@1100528304 : Code[10];VAR iContactNo@1100528303 : Code[20];VAR iContactPersonNo@1100528302 : Code[20];VAR iPrivateCompanyDocument@1100528301 : Boolean;VAR iProjectElement@1100528300 : Code[20];VAR iDPTPublish@1100525000 : Boolean);
    BEGIN
      iSubsiteCode := SubsiteCode;
      iDocumentType := DocumentType;
      iContactNo := ContactNo;
      iContactPersonNo := ContactPersonNo;
      iPrivateCompanyDocument := PrivateCompanyDocument;
      iProjectElement := ProjectElementCode;
      iDPTPublish := (DPTPublish = DPTPublish::Primary);
    END;

    BEGIN
    END.
  }
}

