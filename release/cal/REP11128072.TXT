OBJECT Report 11128072 Approval reg. BG Autogiro A
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=PE7.02.01 (12047967);
  }
  PROPERTIES
  {
    CaptionML=[ENU=Approval reg. BG Auto Giro A;
               SVE=Medgiv.reg. BG Autogiro A];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF EXISTS(gFileName) THEN
                    ERASE(gFileName);
                  gFile.TEXTMODE(TRUE);
                  gFile.WRITEMODE(TRUE);
                  gServerTempFilename := gFileMgt.ServerTempFileName('');
                  gFile.CREATE(gServerTempFilename);

                  gCompanyInfo.GET;
                  gCompanyInfo.TESTFIELD("Giro No.");

                  IF STRLEN(gCompanyInfo."Payment Routing No.") > 6 THEN
                    ERROR(PEB006,gCompanyInfo.TABLECAPTION,gCompanyInfo.FIELDCAPTION("Payment Routing No."));
                  gFile.WRITE('01'+FORMAT(TODAY,0,'<Year4><Month,2><Day,2>')+'AUTOGIRO'+
                          PADSTR('',44,' ')+AdjustRight(gCompanyInfo."Payment Routing No.",6,'0')+
                          AdjustRight(gCompanyInfo."Giro No.",10,'0')+PADSTR('',2,' '));

                  gWindow.OPEN(PEB001);
                END;

    OnPostReport=BEGIN
                   gFile.CLOSE;
                   IF NOT gFileMgt.CanRunDotNetOnClient THEN
                     gFileMgt.DownloadHandler(gServerTempFilename,'','','','')
                   ELSE
                     gFileMgt.DownloadToFile(gServerTempFilename,gFileName);
                 END;

    UsageCategory=ReportsAndAnalysis;
  }
  DATASET
  {
    { 6836;    ;DataItem;                    ;
               DataItemTable=Table18;
               DataItemTableView=SORTING(No.)
                                 WHERE(Auto Giro Type=CONST(BG Sweden));
               OnPreDataItem=BEGIN
                               IF gFileName = '' THEN
                                 ERROR(PEB003);

                               Customer.SETFILTER("Approval Status",'%1|%2',Customer."Approval Status"::"Apply for approval",
                                              Customer."Approval Status"::"Apply for cancellation");
                               IF Customer.COUNT = 0 THEN
                                 ERROR(PEB004);
                             END;

               OnAfterGetRecord=BEGIN
                                  gWindow.UPDATE(1,"No.");
                                  TESTFIELD("Bank Giro No.");
                                  CLEAR(gTransferType);

                                  IF "Approval Status" = "Approval Status"::"Apply for approval" THEN
                                    gTransferType := '04';
                                  IF "Approval Status" = "Approval Status"::"Apply for cancellation" THEN
                                    gTransferType := '03';
                                  IF gTransferType = '' THEN
                                    ERROR(PEB005);

                                  gFile.WRITE(gTransferType+AdjustRight(gCompanyInfo."Giro No.",10,'0')+AdjustRight("Bank Giro No.",16,'0')+PADSTR('',52,' '));

                                  "Approval Status" := "Approval Status"::"Sent for approval";
                                  MODIFY;
                                END;

               ReqFilterFields=No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   gFileNameVisible := gFileMgt.CanRunDotNetOnClient;
                 END;

    }
    CONTROLS
    {
      { 1000000000;;Container;
                  Name=Cont;
                  ContainerType=ContentArea }

      { 1100570000;1;Group  ;
                  CaptionML=[ENU=Options;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1000000001;2;Field  ;
                  OptionCaptionML=[ENU=File Name;
                                   NOR=Filnavn;
                                   SVE=Filnamn];
                  SourceExpr=gFileName;
                  Visible=gFileNameVisible;
                  OnAssistEdit=BEGIN
                                 gFileName := gFileMgt.OpenFileDialog(PEB008,gFileName,PEB007);
                               END;
                                }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      gFileName@1000 : Text[1024];
      gFile@1001 : File;
      gCompanyInfo@1002 : Record 79;
      gTransferType@1003 : Text[2];
      gWindow@1004 : Dialog;
      PEB001@1006 : TextConst 'ENU=Customer #1#############;SVE=Kund #1#############';
      PEB002@1007 : TextConst 'ENU=Field content %1 is longer than %2, which is max length for field content;SVE=F„ltinneh†llet %1 „r l„ngre „n %2, vilket „r maxl„ngden p† f„ltinneh†llet.';
      PEB003@1008 : TextConst 'ENU=Fill in file name;SVE=Skriv in filnamn.';
      PEB004@1009 : TextConst 'ENU=No value exists.;SVE=Det finns inget att l„sa ut.';
      PEB005@1010 : TextConst 'ENU=Incorrect choice of approval status has been done!;SVE=Felaktigt urval av medgivandestatus har skett!';
      gServerTempFilename@1000000001 : Text[1024];
      PEB006@1000000002 : TextConst 'ENU=Max length for %1.%2 is 6 chr.;SVE=Max l„ngd p† %1.%2 „r 6 tecken.';
      PEB007@1100570002 : TextConst 'ENU=All files|*.*|TXT-files (*.txt)|*.txt|WRI-files (*.wri)|*.wri;SVE=Alla filer|*.*|TXT-filer (*.txt)|*.txt|WRI-filer (*.wri)|*.wri';
      PEB008@1100570001 : TextConst 'ENU=BG Autogiro;SVE=BG Autogiro';
      gFileMgt@12047958 : Codeunit 419;
      TextMgt@12047959 : Codeunit 11128088;
      gFileNameVisible@12047960 : Boolean INDATASET;

    PROCEDURE SumCheck@5(CheckNo@1000 : Code[20]) : Code[20];
    VAR
      ChSum@1001 : Integer;
      i@1002 : Integer;
      C@1003 : Integer;
    BEGIN
      CheckNo := ClearNo(CheckNo);
      CheckNo := COPYSTR(PADSTR('', 10 - STRLEN(CheckNo), '0') + CheckNo, 1, 9);
      CLEAR(ChSum);
      FOR i := 1 TO 9 DO BEGIN
        EVALUATE(C, COPYSTR(CheckNo, i, 1));
        ChSum := ChSum + ((C * (( i MOD 2) + 1)) MOD 10);
        ChSum := ChSum + ((C * (( i MOD 2) + 1)) DIV 10);
      END;
      ChSum := (10 - (ChSum MOD 10)) MOD 10;
      EXIT(FORMAT(ChSum));
    END;

    PROCEDURE ClearNo@2(TextIn@1000 : Text[250]) : Text[250];
    VAR
      NotNumeric@1001 : Text[250];
    BEGIN
      NotNumeric := DELCHR(TextIn,'=','0123456789');
      EXIT(DELCHR(TextIn,'=',NotNumeric));
    END;

    PROCEDURE AdjustRight@1(TextIn@1000 : Text[250];Length@1001 : Integer;FillCharacter@1002 : Text[1]) : Text[250];
    BEGIN
      TextIn := ClearNo(TextIn);
      IF STRLEN(TextIn) > Length THEN
        ERROR(PEB002,TextIn,Length);
      EXIT(PADSTR('', Length - STRLEN(TextIn), FillCharacter) + TextIn);
    END;

    BEGIN
    {
      PEB *** Begin ***
      PEB0006 Autogiro
      PEB
      PEB *** End ***
    }
    END.
  }
  RDLDATA
  {
  }
}

