OBJECT Page 9033 Invite External Accountant
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.01;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Invite External Accountant;
               NOR=Inviter ekstern regnskapsfõrer;
               SVE=Bjud in extern revisor];
    PageType=NavigatePage;
    OnInit=BEGIN
             DefineInitialEmailBody;
           END;

    OnOpenPage=VAR
                 PermissionManager@1000 : Codeunit 9002;
                 InviteExternalAccountant@1001 : Codeunit 9033;
                 NavUserAccountHelper@1004 : DotNet "'Microsoft.Dynamics.Nav.NavUserAccount, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.NavUserAccount.NavUserAccountHelper";
                 ProgressWindow@1002 : Dialog;
                 ErrorMessage@1003 : Text;
               BEGIN
                 OnInvitationStart;
                 IF NOT PermissionManager.SoftwareAsAService THEN
                   ERROR(SaaSOnlyErrorErr);

                 ProgressWindow.OPEN(WizardOpenValidationMsg);

                 IF NOT InviteExternalAccountant.VerifySMTPIsEnabledAndSetup THEN
                   ERROR(SMTPMustBeSetupErrorErr);

                 IF NOT InviteExternalAccountant.InvokeIsExternalAccountantLicenseAvailable(ErrorMessage,TargetLicense) THEN BEGIN
                   OnInvitationNoExternalAccountantLicenseFail;
                   ERROR(NoExternalAccountantLicenseAvailableErr);
                 END;

                 IF NOT InviteExternalAccountant.InvokeIsUserAdministrator THEN BEGIN
                   OnInvitationNoAADPermissionsFail;
                   ERROR(NoAADPermissionsErr);
                 END;

                 IF NOT (NavUserAccountHelper.IsSessionAdminSession OR NavUserAccountHelper.IsUserSuperInAllCompanies) THEN BEGIN
                   OnInvitationNoUserTablePermissionsFail;
                   ERROR(NoUserTableWritePermissionErr);
                 END;

                 ProgressWindow.CLOSE;
                 Step := Step::Start;
                 EnableControls;
               END;

    OnQueryClosePage=BEGIN
                       IF CloseAction = ACTION::OK THEN
                         IF NOT UserInvited THEN
                           IF NOT CONFIRM(SetupNotCompletedQst,FALSE) THEN
                             ERROR('');
                     END;

    ActionList=ACTIONS
    {
      { 2       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 3       ;1   ;Action    ;
                      Name=ActionBack;
                      CaptionML=[ENU=Back;
                                 NOR=Tilbake;
                                 SVE=FîregÜende];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=BackActionEnabled;
                      InFooterBar=Yes;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 NextStep(TRUE);
                               END;
                                }
      { 4       ;1   ;Action    ;
                      Name=ActionNext;
                      CaptionML=[ENU=Next;
                                 NOR=Neste;
                                 SVE=NÑsta];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=NextActionEnabled;
                      InFooterBar=Yes;
                      Image=NextRecord;
                      OnAction=VAR
                                 ErrorMessage@1010 : Text;
                               BEGIN
                                 IF Step = Step::DefineInformation THEN BEGIN
                                   IF (NewUserEmailAddress <> '') AND (NewFirstName <> '') AND (NewLastName <> '') AND (NewUserWelcomeEmail <> '') THEN BEGIN
                                     IF InviteExternalAccountant.InvokeEmailAddressIsAADAccount(NewUserEmailAddress,ErrorMessage) THEN BEGIN
                                       Invite;
                                       OnInvitationEnd(WasInvitationSuccessful,InvitationResult,TargetLicense);
                                       NextStep(FALSE);
                                     END ELSE
                                       ERROR(ErrorMessage);
                                   END ELSE
                                     ERROR(NotAllFieldsEnteredErrorErr);
                                 END ELSE
                                   NextStep(FALSE);
                               END;
                                }
      { 46      ;1   ;Action    ;
                      Name=ActionClose;
                      CaptionML=[ENU=Close;
                                 NOR=Lukk;
                                 SVE=Avsluta];
                      ApplicationArea=#Basic,#Suite;
                      Visible=CloseActionVisible;
                      Enabled=TRUE;
                      InFooterBar=Yes;
                      Image=NextRecord;
                      OnAction=BEGIN
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 10  ;1   ;Group     ;
                Visible=FirstStepVisible;
                GroupType=Group }

    { 11  ;2   ;Group     ;
                CaptionML=[ENU=Welcome to assisted setup for inviting an external accountant.;
                           NOR=Velkommen til assistert oppsett for invitasjon av ekstern regnskapsfõrer.;
                           SVE=VÑlkommen till en assisterad konfiguration fîr att bjuda in en extern revisor.];
                Visible=FirstStepVisible;
                GroupType=Group }

    { 12  ;3   ;Group     ;
                Visible=FirstStepVisible;
                GroupType=Group;
                InstructionalTextML=[ENU=This guide will help you invite an external accountant to login to your company.;
                                     NOR=Denne veiledningen hjelper deg med Ü invitere en ekstern regnskapsfõrer til Ü logge pÜ selskapet.;
                                     SVE=Den hÑr guiden hjÑlper dig att bjuda in en extern revisor sÜ att han eller hon kan logga in pÜ ditt fîretag.] }

    { 24  ;4   ;Group     ;
                CaptionML=[ENU=This Invite External Accountant feature allows your organization to share its data with an external party either through the use of a separate portal or through the external party's access to your organization's online services account. Microsoft has no control over the third-party's use of your data. You are responsible for ensuring that you have separate agreements in place with any such external user governing such external user's access to and use of your data.;
                           NOR=Med funksjonen Inviter ekstern regnskapsfõrer kan organisasjonen din dele data med en ekstern part ved Ü bruke en separat portal eller gjennom den eksterne partens tilgang til organisasjonens onlinetjenestekonto. Microsoft har ingen kontroll over hvordan tredjeparten bruker dataene dine. Du er selv ansvarlig for at det finnes separate avtaler med en slik ekstern bruker som styrer den eksterne brukerens tilgang til og bruk av dataene dine.;
                           SVE=Med funktionen Bjud in extern revisor kan din organisation dela sina data med en extern part genom att anvÑnda en separat portal eller genom att ge den externa parten Ütkomst till organisationens onlinetjÑnstkonto. Microsoft har ingen kontroll îver den hur den externa parten anvÑnder dina data. Du ansvarar sjÑlv fîr att det finns separata avtal med en sÜdan extern anvÑndare som reglerar den externa anvÑndarens tillgÜng till och anvÑndning av dina data.];
                Visible=FirstStepVisible;
                GroupType=Group }

    { 8   ;5   ;Group     ;
                CaptionML=[ENU=By clicking 'I Accept', you consent to share your organization's data with external parties you designate.;
                           NOR=Ved Ü klikke Godta samtykker du i Ü dele organisasjonens data med eksterne parter som du angir.;
                           SVE=Genom att klicka pÜ Jag accepterar samtycker du till att dela organisationens data med de externa parter som du anger.];
                Visible=FirstStepVisible;
                GroupType=Group }

    { 6   ;6   ;Field     ;
                Name=DataPrivacy;
                CaptionML=[ENU=I Accept;
                           NOR=Godta;
                           SVE=Jag accepterar];
                ToolTipML=[ENU=Specifies your consent to share your organization's data with external parties you designate.;
                           NOR=Angir at du samtykker i Ü dele organisasjonens data med eksterne parter som du angir.;
                           SVE=Anger att du samtycker till att dela organisationens data med de externa parter som du anger.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=DataPrivacyAccepted;
                OnValidate=BEGIN
                             NextActionEnabled := TRUE;
                           END;
                            }

    { 7   ;6   ;Group     ;
                Visible=FirstStepVisible;
                GroupType=Group;
                InstructionalTextML=[ENU=Choose Next to get started.;
                                     NOR=Velg Neste for Ü komme i gang.;
                                     SVE=VÑlj NÑsta fîr att komma igÜng.] }

    { 13  ;1   ;Group     ;
                Visible=DefineInformationStepVisible;
                GroupType=Group }

    { 20  ;2   ;Group     ;
                Visible=DefineInformationStepVisible;
                GroupType=Group;
                InstructionalTextML=[ENU=Please enter the email address of the accountant.;
                                     NOR=Angi e-postadressen for regnskapsfõreren.;
                                     SVE=Ange e-postadressen till revisorn.] }

    { 21  ;3   ;Field     ;
                Name=NewUserEmailAddress;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=NewUserEmailAddress;
                ShowMandatory=TRUE;
                ShowCaption=No }

    { 25  ;2   ;Group     ;
                Visible=DefineInformationStepVisible;
                GroupType=Group;
                InstructionalTextML=[ENU=Please enter the first name.;
                                     NOR=Angi fornavnet.;
                                     SVE=Skriv fîrnamnet.] }

    { 26  ;3   ;Field     ;
                Name=NewFirstName;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=NewFirstName;
                OnValidate=BEGIN
                             DefineInitialEmailBody;
                           END;

                ShowMandatory=TRUE;
                ShowCaption=No }

    { 15  ;2   ;Group     ;
                Visible=DefineInformationStepVisible;
                GroupType=Group;
                InstructionalTextML=[ENU=Please enter the last name.;
                                     NOR=Angi etternavnet.;
                                     SVE=Skriv efternamnet.] }

    { 5   ;3   ;Field     ;
                Name=NewLastName;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=NewLastName;
                ShowMandatory=TRUE;
                ShowCaption=No }

    { 16  ;2   ;Group     ;
                CaptionML=[ENU=Welcome Email;
                           NOR=E-postvelkomst;
                           SVE=VÑlkomstmeddelande];
                Visible=DefineInformationStepVisible;
                GroupType=Group }

    { 23  ;3   ;Field     ;
                Name=NewUserWelcomeEmail;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=NewUserWelcomeEmail;
                MultiLine=Yes;
                ShowCaption=No }

    { 17  ;1   ;Group     ;
                Visible=CloseActionVisible;
                GroupType=Group }

    { 18  ;2   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=InvitationResult;
                Enabled=FALSE;
                Editable=FALSE;
                Style=Strong;
                StyleExpr=TRUE;
                ShowCaption=No }

    { 19  ;2   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=InviteProgress;
                Enabled=FALSE;
                Editable=FALSE;
                MultiLine=Yes;
                ShowCaption=No }

  }
  CODE
  {
    VAR
      InviteExternalAccountant@1018 : Codeunit 9033;
      AzureADMgt@1001 : Codeunit 6300;
      Step@1002 : 'Start,DefineInformation,Finish';
      FirstStepVisible@1004 : Boolean;
      DefineInformationStepVisible@1005 : Boolean;
      BackActionEnabled@1008 : Boolean;
      NextActionEnabled@1009 : Boolean;
      SetupNotCompletedQst@1013 : TextConst 'ENU=The user was not yet invited. Are you sure that you want to exit?;NOR=Brukeren var ennÜ ikke invitert. Er du sikker pÜ at du vil avslutte?;SVE=AnvÑndaren har Ñnnu inte bjudits in. Vill du verkligen avsluta?';
      DataPrivacyAccepted@1000 : Boolean;
      CloseActionVisible@1021 : Boolean;
      NewUserEmailAddress@1010 : Text;
      NewFirstName@1053 : Text;
      NewLastName@1020 : Text;
      NewUserWelcomeEmail@1012 : Text;
      UserInvited@1015 : Boolean;
      EmailGreetingTxt@1024 : TextConst 'ENU="Hello ";NOR="Hei ";SVE="Hej "';
      EmailBodyTxt@1025 : TextConst '@@@=%1 - product name;ENU=Please accept this invitation to get access to my %1.;NOR=Godta denne invitasjonen for Ü fÜ tilgang til min forekomst av %1.;SVE=Acceptera denna inbjudan om du vill fÜ Ütkomst till min %1.';
      EmailClosingTxt@1026 : TextConst 'ENU=Best Regards,;NOR=Med vennlig hilsen;SVE=Med vÑnlig hÑlsning,';
      SaaSOnlyErrorErr@1014 : TextConst 'ENU=This functionality is not intended for on premises.;NOR=Denne funksjonen er ikke men for lokal bruk.;SVE=Den hÑr funktionen Ñr inte tÑnkt att anvÑndas lokalt.';
      InviteProgress@1022 : Text;
      InvitationErrorTxt@1027 : TextConst '@@@="%1=part of the invite process, e.g. invite, profile update, license assignment.  %2 =the error message.";ENU=Inviting external accountant failed while doing the %1.  %2;NOR=Invitasjon av ekstern regnskapsfõrer mislyktes under utfõring av %1. %2;SVE=Det gick inte att bjuda en den externa revisorn medan du hîll pÜ att %1. %2';
      InviteTxt@1028 : TextConst 'ENU=invite;NOR=invitasjon;SVE=bjuda in';
      ProfileUpdateTxt@1029 : TextConst 'ENU=profile update;NOR=profiloppdatering;SVE=uppdatera profil';
      LicenseAssignmentTxt@1030 : TextConst 'ENU=license assignment;NOR=lisensavtale;SVE=tilldela licens';
      EmailTxt@1031 : TextConst 'ENU=email;NOR=e-post;SVE=skicka e-post';
      InvitationSuccessTxt@1032 : TextConst '@@@="%1=first name.  %2 =last name.";ENU=%1 %2 was successfully invited!;NOR=%1 %2 ble invitert!;SVE=%1 %2 har bjudits in!';
      NoExternalAccountantLicenseAvailableErr@1033 : TextConst 'ENU=No External Accountant license available. Contact your administrator.;NOR=Ingen lisens for ekstern regnskapsfõrer er tilgjengelig. Kontakt systemansvarlig.;SVE=Det finns ingen licens fîr extern revisor. Kontakta administratîren.';
      NoAADPermissionsErr@1034 : TextConst 'ENU=You do not have permission to invite the user. You must either be a global administrator or a user administrator in Azure AD. Please contact your administrator.;NOR=Du har ikke tillatelser til Ü invitere brukeren. Du mÜ vëre global administrator eller brukeradministrator i Azure AD. Kontakt systemansvarlig.;SVE=Du har inte behîrighet att bjuda in anvÑndaren. Du mÜste antingen vara en global administratîr eller en anvÑndaradministratîr i Azure AD. Kontakta administratîren.';
      WizardOpenValidationMsg@1035 : TextConst 'ENU=Verifying permissions and license availability.;NOR=Verifiserer tillatelser og lisenstilgjengelighet.;SVE=Kontrollerar om det finns behîrigheter och licens.';
      InviteProgressWindowMsg@1036 : TextConst 'ENU=Inviting external accountant.  This process could take a little while.;NOR=Inviterer ekstern regnskapsfõrer. Denne prosessen kan ta en stund.;SVE=Bjuder in extern revisor. Detta kan ta en liten stund.';
      EmailSubjectTxt@1037 : TextConst '@@@=%1 - product name;ENU=You have been invited to %1;NOR=Du har blitt invitert til %1;SVE=Du har blivit inbjuden till %1';
      OpenTheFollowingLinkTxt@1039 : TextConst 'ENU=Open the following link to verify that you can log in.;NOR=èpne koblingen nedenfor for Ü bekrefte at du kan logge pÜ.;SVE=ôppna fîljande lÑnk om du vill kontrollera att du kan logga in.';
      ToAddMyCompanyTxt@1007 : TextConst '@@@="%1=Link for creating a client in the Accountant Hub.";ENU="To add my company to your Accountant Hub, click <a href=""%1"">here</a>.";NOR="Hvis du vil legge til mitt selskap i din forekomst av Accountant Hub, klikker du <a href=""%1"">her</a>.";SVE="Om du vill lÑgga till mitt fîretag i din Accountant Hub klickar du <a href=""%1"">hÑr</a>."';
      EmailNotUsingAccountantPortalTxt@1040 : TextConst '@@@={Do not translate html portion.};ENU="Not using the Accountant Hub?  Click <a href=""https://dynamics.microsoft.com/en-us/business-central/accountants"">here</a> to learn more.";NOR="Bruker du ikke Accountant Hub? Klikk <a href=""https://dynamics.microsoft.com/en-us/business-central/accountants"">her</a> for Ü finne ut mer.";SVE="AnvÑnder du inte Accountant Hub? Klicka <a href=""https://dynamics.microsoft.com/en-us/business-central/accountants"">hÑr</a> om du vill ha mer information."';
      InvitationResult@1023 : Text;
      FailureTxt@1041 : TextConst 'ENU=Failure;NOR=Mislykket;SVE=Misslyckades';
      SuccessTxt@1042 : TextConst 'ENU=Success;NOR=Vellykket;SVE=Lyckades';
      EmailErrorTxt@1043 : TextConst 'ENU=Error occurred while sending email.;NOR=Det oppstod en feil under sending av e-post.;SVE=Ett fel uppstod nÑr e-postmeddelandet skickades.';
      NotAllFieldsEnteredErrorErr@1044 : TextConst 'ENU=To continue, enter all required fields.;NOR=Fyll ut alle obligatoriske felt for Ü fortsette.;SVE=Ange alla obligatoriska fÑlt innan du fortsÑtter.';
      WasInvitationSuccessful@1060 : Boolean;
      SMTPMustBeSetupErrorErr@1045 : TextConst 'ENU=SMTP must be configured prior to inviting external accountant. Contact your administrator.;NOR=SMTP mÜ vëre konfigurert fõr invitasjon av en ekstern regnskapsfõrer. Kontakt systemansvarlig.;SVE=SMTP mÜste konfigureras innan du bjuder in extern revisor. Kontakta systemadministratîren.';
      NoUserTableWritePermissionErr@1046 : TextConst 'ENU=This step adds a user to your company, and only your administrator can do that. Please contact your administrator.;NOR=Dette trinnet legger til en bruker i firmaet, og det kan bare systemansvarlig gjõre. Kontakt systemansvarlig.;SVE=Det hÑr steget lÑgger till en anvÑndare i ditt fîretag, och det kan bara systemadministratîren kan gîra. Kontakta systemadministratîren.';
      AccountantHubPRODTxt@1003 : TextConst '@@@={Locked};ENU=https://accountants.dynamics.com;NOR=https://accountants.dynamics.com;SVE=https://accountants.dynamics.com';
      AccountantHubPPETxt@1006 : TextConst '@@@={Locked};ENU=https://accountants.dynamics-tie.com;NOR=https://accountants.dynamics-tie.com;SVE=https://accountants.dynamics-tie.com';
      UrlPageModeFilterTxt@1017 : TextConst '@@@="%1=Link to client.  %2 =client name.  {Locked}";ENU="/?page=21&mode=Edit&filter=Customer.''Client Link'' IS ''''''%1'''''' AND Customer.Name IS ''''''%2''''''";NOR="/?page=21&mode=Edit&filter=Customer.''Client Link'' IS ''''''%1'''''' AND Customer.Name IS ''''''%2''''''";SVE="/?page=21&mode=Edit&filter=Customer.''Client Link'' IS ''''''%1'''''' AND Customer.Name IS ''''''%2''''''"';
      TargetLicense@1061 : Text;

    LOCAL PROCEDURE EnableControls@1();
    BEGIN
      ResetControls;

      CASE Step OF
        Step::Start:
          ShowStartStep;
        Step::DefineInformation:
          ShowDefineInformationStep;
        Step::Finish:
          ShowFinishStep;
      END;
    END;

    LOCAL PROCEDURE NextStep@3(Backwards@1000 : Boolean);
    BEGIN
      IF Backwards THEN
        Step := Step - 1
      ELSE
        Step := Step + 1;

      EnableControls;
    END;

    LOCAL PROCEDURE ShowStartStep@4();
    BEGIN
      FirstStepVisible := TRUE;

      BackActionEnabled := FALSE;
    END;

    LOCAL PROCEDURE ShowDefineInformationStep@5();
    BEGIN
      DefineInformationStepVisible := TRUE;
    END;

    LOCAL PROCEDURE ShowFinishStep@6();
    BEGIN
      NextActionEnabled := FALSE;
      CloseActionVisible := TRUE;
    END;

    LOCAL PROCEDURE Invite@1045();
    VAR
      SMTPMail@1011 : Codeunit 400;
      MailManagement@1012 : Codeunit 9520;
      GuestGraphUser@1013 : DotNet "'Microsoft.Dynamics.Nav.LicensingService.Model, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.LicensingService.Model.UserInfo";
      Graph@1014 : DotNet "'Microsoft.Dynamics.Nav.AzureADGraphClient, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.AzureADGraphClient.GraphQuery";
      ProgressWindow@1015 : Dialog;
      InvitedUserId@1016 : GUID;
      InviteRedeemUrl@1017 : Text;
      ErrorMessage@1018 : Text;
    BEGIN
      UserInvited := TRUE;
      ProgressWindow.OPEN(InviteProgressWindowMsg);

      IF NOT InviteExternalAccountant.InvokeInvitationsRequest(NewFirstName + NewLastName,
           NewUserEmailAddress,GetWebClientUrl,InvitedUserId,InviteRedeemUrl,ErrorMessage)
      THEN BEGIN
        InvitationResult := FailureTxt;
        InviteProgress := STRSUBSTNO(InvitationErrorTxt,InviteTxt,ErrorMessage);
        ProgressWindow.CLOSE;
        EXIT;
      END;
      Graph := Graph.GraphQuery;

      IF NOT InviteExternalAccountant.TryGetGuestGraphUser(InvitedUserId,GuestGraphUser) THEN BEGIN
        InvitationResult := FailureTxt;
        InviteProgress := STRSUBSTNO(InvitationErrorTxt,InviteTxt,ErrorMessage);
        ProgressWindow.CLOSE;
        EXIT;
      END;

      IF NOT InviteExternalAccountant.InvokeUserProfileUpdateRequest(GuestGraphUser,
           Graph.GetTenantDetail().CountryLetterCode,ErrorMessage)
      THEN BEGIN
        InvitationResult := FailureTxt;
        InviteProgress := STRSUBSTNO(InvitationErrorTxt,ProfileUpdateTxt,ErrorMessage);
        ProgressWindow.CLOSE;
        EXIT;
      END;

      IF NOT InviteExternalAccountant.InvokeUserAssignLicenseRequest(GuestGraphUser,TargetLicense,ErrorMessage) THEN BEGIN
        InvitationResult := FailureTxt;
        InviteProgress := STRSUBSTNO(InvitationErrorTxt,LicenseAssignmentTxt,ErrorMessage);
        ProgressWindow.CLOSE;
        EXIT;
      END;

      SMTPMail.CreateMessage('',MailManagement.GetSenderEmailAddress,NewUserEmailAddress,
        STRSUBSTNO(EmailSubjectTxt,PRODUCTNAME.MARKETING),DefineFullEmailBody(NewUserWelcomeEmail),TRUE);
      IF NOT SMTPMail.TrySend THEN BEGIN
        InvitationResult := FailureTxt;
        InviteProgress := STRSUBSTNO(InvitationErrorTxt,EmailTxt,EmailErrorTxt);
        ProgressWindow.CLOSE;
        EXIT;
      END;

      ProgressWindow.CLOSE;

      InvitationResult := SuccessTxt;
      WasInvitationSuccessful := TRUE;
      InviteProgress := STRSUBSTNO(InvitationSuccessTxt,NewFirstName,NewLastName);
      CurrPage.UPDATE(FALSE);
      InviteExternalAccountant.UpdateAssistedSetup;
      InviteExternalAccountant.CreateNewUser(InvitedUserId);
    END;

    LOCAL PROCEDURE ResetControls@7();
    BEGIN
      FirstStepVisible := FALSE;
      DefineInformationStepVisible := FALSE;
      NextActionEnabled := FALSE;

      BackActionEnabled := TRUE;

      IF DataPrivacyAccepted THEN
        NextActionEnabled := TRUE;

      CloseActionVisible := FALSE;
    END;

    LOCAL PROCEDURE DefineInitialEmailBody@1046();
    VAR
      User@1010 : Record 2000000120;
      Company@1000 : Record 2000000006;
      EmailGreeting@1011 : Text;
      EmailBody@1012 : Text;
      EmailClosing@1013 : Text;
    BEGIN
      User.GET(USERSECURITYID);
      Company.GET(COMPANYNAME);
      EmailGreeting := EmailGreetingTxt + NewFirstName + ',' + NewLineForTextControl;
      EmailBody := STRSUBSTNO(EmailBodyTxt,PRODUCTNAME.MARKETING) + NewLineForTextControl + NewLineForTextControl;
      EmailClosing := EmailClosingTxt + NewLineForTextControl + User."User Name" + NewLineForTextControl + Company."Display Name";
      NewUserWelcomeEmail := EmailGreeting + EmailBody + EmailClosing;
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE DefineFullEmailBody@1047(InitialEmailMessage@1010 : Text) : Text;
    VAR
      EmailBody@1012 : Text;
    BEGIN
      EmailBody := ReplaceNewLinesWithHtmlLineBreak(InitialEmailMessage);
      EmailBody := EmailBody + LineBreakForEmail + LineBreakForEmail;
      EmailBody := EmailBody + OpenTheFollowingLinkTxt + LineBreakForEmail;
      EmailBody := EmailBody + GetWebClientUrl + LineBreakForEmail;
      EmailBody := EmailBody + LineBreakForEmail + LineBreakForEmail;
      EmailBody := EmailBody + STRSUBSTNO(ToAddMyCompanyTxt,GetCreateClientUrl) + LineBreakForEmail;
      EmailBody := EmailBody + LineBreakForEmail + LineBreakForEmail;
      EmailBody := EmailBody + EmailNotUsingAccountantPortalTxt + LineBreakForEmail;
      EXIT(EmailBody)
    END;

    LOCAL PROCEDURE NewLineForTextControl@1048() Newline : Text;
    BEGIN
      Newline[1] := 13;
      Newline[2] := 10;
    END;

    LOCAL PROCEDURE LineBreakForEmail@1050() : Text;
    BEGIN
      EXIT('</br>');
    END;

    LOCAL PROCEDURE ReplaceNewLinesWithHtmlLineBreak@1051(InputText@1010 : Text) : Text;
    VAR
      String@1011 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.String";
      TextToReplace@1012 : Text;
    BEGIN
      String := InputText;
      TextToReplace[1] := 10;
      EXIT(String.Replace(TextToReplace,LineBreakForEmail));
    END;

    LOCAL PROCEDURE GetWebClientUrl@1052() : Text;
    VAR
      Graph@1004 : DotNet "'Microsoft.Dynamics.Nav.AzureADGraphClient, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.AzureADGraphClient.GraphQuery";
      ClientUrl@1010 : Text;
      TenantDomainName@1000 : Text;
      TenantObjectId@1001 : Text;
    BEGIN
      IF InviteExternalAccountant.IsPPE THEN
        ClientUrl := 'https://businesscentral.dynamics-tie.com/'
      ELSE
        ClientUrl := 'https://businesscentral.dynamics.com/';

      TenantDomainName := AzureADMgt.GetInitialTenantDomainName;
      Graph := Graph.GraphQuery;
      TenantObjectId := Graph.GetTenantDetail.ObjectId;

      IF STRLEN(TenantDomainName) > 0 THEN
        ClientUrl := ClientUrl + TenantDomainName
      ELSE
        ClientUrl := ClientUrl + TenantObjectId;

      ClientUrl := ClientUrl + '?redirectedfromsignup=1';

      EXIT(ClientUrl);
    END;

    LOCAL PROCEDURE GetAccountantHubUrl@1000() : Text;
    VAR
      AccountantHubUrl@1010 : Text;
    BEGIN
      IF InviteExternalAccountant.IsPPE THEN
        AccountantHubUrl := AccountantHubPPETxt
      ELSE
        AccountantHubUrl := AccountantHubPRODTxt;

      EXIT(AccountantHubUrl);
    END;

    LOCAL PROCEDURE GetCreateClientUrl@1002() : Text;
    VAR
      TypeHelper@1001 : Codeunit 10;
      Graph@1004 : DotNet "'Microsoft.Dynamics.Nav.AzureADGraphClient, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.AzureADGraphClient.GraphQuery";
      CreateClientUrl@1010 : Text;
      WebClientUrl@1002 : Text;
      TenantDisplayName@1000 : Text;
    BEGIN
      Graph := Graph.GraphQuery;
      TenantDisplayName := Graph.GetTenantDetail.DisplayName;
      WebClientUrl := GetWebClientUrl;

      CreateClientUrl := GetAccountantHubUrl +
        STRSUBSTNO(UrlPageModeFilterTxt,TypeHelper.UrlEncode(WebClientUrl),TypeHelper.UrlEncode(TenantDisplayName));

      EXIT(CreateClientUrl);
    END;

    [Integration]
    LOCAL PROCEDURE OnInvitationStart@20();
    BEGIN
      // This event is called the invitation process is started.
    END;

    [Integration]
    LOCAL PROCEDURE OnInvitationNoExternalAccountantLicenseFail@21();
    BEGIN
      // This event is called when the invitation process can not proceed due to a lack of external accountant licenses.
    END;

    [Integration]
    LOCAL PROCEDURE OnInvitationNoAADPermissionsFail@22();
    BEGIN
      // This event is called when the invitation process can not proceed due to a lack of user AAD permissions.
    END;

    [Integration]
    LOCAL PROCEDURE OnInvitationEnd@23(WasInvitationSuccessful@1001 : Boolean;Result@1002 : Text;Progress@1003 : Text);
    BEGIN
      // This event is called when the invitation process is finished.
    END;

    [Integration]
    LOCAL PROCEDURE OnInvitationNoUserTablePermissionsFail@24();
    BEGIN
      // This event is called when the invitation process can not proceed because session is not admin or user is not super in all companies.
    END;

    BEGIN
    END.
  }
}

