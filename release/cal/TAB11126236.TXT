OBJECT Table 11126236 Project Raw Material Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TestStatusOpen;
             END;

    OnModify=BEGIN
               TestStatusOpen;
               UpdateRemainderLine("Project No.");
             END;

    OnDelete=BEGIN
               TestStatusOpen;
               DELETE;
               UpdateRemainderLine("Project No.");
               INSERT;
             END;

    CaptionML=[ENU=Project Raw Material Line;
               NOR=Prosjekt, r†materialerad;
               SVE=Projekt, r†materialrad];
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation="Project Raw Material Header";
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr.;
                                                              SVE=Projektnr];
                                                   NotBlank=Yes }
    { 20  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 30  ;   ;Skip Type           ;Code10        ;TableRelation="Skip Type";
                                                   OnValidate=BEGIN
                                                                CalcSkipQuantity;
                                                                CalcCostPrice;
                                                                CalcTransportPrice;
                                                              END;

                                                   CaptionML=[ENU=Skip Type;
                                                              NOR=Type overhopp;
                                                              SVE=Typ ”verhopp] }
    { 40  ;   ;Raw Material Type   ;Code10        ;TableRelation="Raw Material Type";
                                                   OnValidate=BEGIN
                                                                CalcRawMaterialTypePercentage;
                                                                CalcTotalVolume;
                                                                CalcCostPrice;
                                                              END;

                                                   CaptionML=[ENU=Raw Material Type;
                                                              NOR=Type r†material;
                                                              SVE=R†materialtyp] }
    { 50  ;   ;Raw Material Type Percentage;Decimal;
                                                   OnValidate=BEGIN
                                                                CalcTotalVolume;
                                                                UpdateRemainderLine("Project No.");
                                                              END;

                                                   CaptionML=[ENU=Raw Material Type Percentage;
                                                              NOR=Prosent type r†materiale;
                                                              SVE=Procent r†materialtyp] }
    { 60  ;   ;Total Volume        ;Decimal       ;CaptionML=[ENU=Total Volume;
                                                              NOR=Totalt volum;
                                                              SVE=Total volym];
                                                   Editable=No }
    { 70  ;   ;Skip Quantity       ;Decimal       ;CaptionML=[ENU=Skip Quantity;
                                                              NOR=Hopp over antall;
                                                              SVE=Antal ”verhopp];
                                                   DecimalPlaces=0:0;
                                                   Editable=No }
    { 80  ;   ;Cost Price          ;Decimal       ;OnValidate=VAR
                                                                RawMaterialType@1210190000 : Record 11126232;
                                                              BEGIN
                                                                IF RawMaterialType.GET("Raw Material Type") THEN
                                                                  RawMaterialType.TESTFIELD("Prices Editable", TRUE);

                                                                CalcTotalCostPrice;
                                                              END;

                                                   CaptionML=[ENU=Cost Price;
                                                              NOR=Selvkost;
                                                              SVE=Sj„lvkostnadspris];
                                                   AutoFormatType=2 }
    { 90  ;   ;Transport Price     ;Decimal       ;CaptionML=[ENU=Transport Price;
                                                              NOR=Transportpris;
                                                              SVE=Transportpris];
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 100 ;   ;Total Cost Price    ;Decimal       ;CaptionML=[ENU=Total Cost Price;
                                                              NOR=Total selvkost;
                                                              SVE=Totalt sj„lvkostnadspris];
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 110 ;   ;Line Amount         ;Decimal       ;CaptionML=[ENU=Line Amount;
                                                              NOR=Linjebel›p;
                                                              SVE=Radbelopp];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 120 ;   ;Subcontracted       ;Boolean       ;CaptionML=[ENU=Subcontracted;
                                                              NOR=Underleveranse;
                                                              SVE=Underentreprenad] }
    { 130 ;   ;Comment             ;Text50        ;CaptionML=[ENU=Comment;
                                                              NOR=Merknad;
                                                              SVE=Kommentar] }
    { 200 ;   ;Total Skip Quantity ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Project Raw Material Line"."Skip Quantity" WHERE (Project No.=FIELD(Project No.)));
                                                   CaptionML=[ENU=Total Skip Quantity;
                                                              NOR=Totalt antall overhopp;
                                                              SVE=Totalt antal ”verhopp];
                                                   DecimalPlaces=0:0;
                                                   Editable=No }
    { 201 ;   ;Private Skip Quantity;Decimal      ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Project Raw Material Line"."Skip Quantity" WHERE (Project No.=FIELD(Project No.),
                                                                                                                      Subcontracted=CONST(No)));
                                                   CaptionML=[ENU=Private Skip Quantity;
                                                              NOR=Privat antall overhopp;
                                                              SVE=Privat antal ”verhopp];
                                                   DecimalPlaces=0:0;
                                                   Editable=No }
    { 202 ;   ;Subcontracted Skip Quantity;Decimal;FieldClass=FlowField;
                                                   CalcFormula=Sum("Project Raw Material Line"."Skip Quantity" WHERE (Project No.=FIELD(Project No.),
                                                                                                                      Subcontracted=CONST(Yes)));
                                                   CaptionML=[ENU=Subcontracted Skip Quantity;
                                                              NOR=Underentrepren›r antall overhopp;
                                                              SVE=Underentreprenad, antal ”verhopp];
                                                   DecimalPlaces=0:0;
                                                   Editable=No }
    { 210 ;   ;Total Amount        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Project Raw Material Line"."Line Amount" WHERE (Project No.=FIELD(Project No.)));
                                                   CaptionML=[ENU=Total Amount;
                                                              NOR=Totalt bel›p;
                                                              SVE=Total Amount];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 220 ;   ;Private Amount      ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Project Raw Material Line"."Line Amount" WHERE (Project No.=FIELD(Project No.),
                                                                                                                    Subcontracted=CONST(No)));
                                                   CaptionML=[ENU=Private Amount;
                                                              NOR=Privat bel›p;
                                                              SVE=Privat belopp];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 230 ;   ;Subcontracted Amount;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Project Raw Material Line"."Line Amount" WHERE (Project No.=FIELD(Project No.),
                                                                                                                    Subcontracted=CONST(Yes)));
                                                   CaptionML=[ENU=Subcontracted Amount;
                                                              NOR=Underentrepren›r bel›p;
                                                              SVE=Underentreprenadsbelopp];
                                                   Editable=No;
                                                   AutoFormatType=1 }
  }
  KEYS
  {
    {    ;Project No.,Line No.                    ;Clustered=Yes }
    {    ;Project No.,Subcontracted               ;SumIndexFields=Line Amount,Skip Quantity }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      SustainableRawMaterialMgtSetup@1100528500 : Record 11126223;

    PROCEDURE CalcRawMaterialTypePercentage@1210190005();
    VAR
      ProjectRawMaterialHeader@1100528500 : Record 11126235;
      BuildingCharacterRawMatType@1210190000 : Record 11126233;
    BEGIN
      IF "Line No." = ReminderLineNo THEN
        VALIDATE("Raw Material Type Percentage", "Raw Material Type Percentage")
      ELSE BEGIN
        IF ProjectRawMaterialHeader.GET("Project No.")THEN;
        IF BuildingCharacterRawMatType.GET(ProjectRawMaterialHeader."Building Character", "Raw Material Type") THEN;

        VALIDATE("Raw Material Type Percentage", BuildingCharacterRawMatType.Percentage);
      END;
    END;

    PROCEDURE CalcTotalVolume@1210190001();
    VAR
      ProjectRawMaterialHeader@1100528501 : Record 11126235;
      BuildingCharacter@1100528500 : Record 11126224;
      BuildingMethod@1210190000 : Record 11126229;
      ProjectTypeRawMaterial@1210190001 : Record 11126226;
      LaboriousnessCategory@1210190002 : Record 11126227;
    BEGIN
      IF ProjectRawMaterialHeader.GET("Project No.") THEN;
      IF BuildingCharacter.GET(ProjectRawMaterialHeader."Building Character") THEN;
      IF BuildingMethod.GET(ProjectRawMaterialHeader."Building Method") THEN;
      IF ProjectTypeRawMaterial.GET(ProjectRawMaterialHeader."Project Type") THEN;
      IF LaboriousnessCategory.GET(ProjectRawMaterialHeader."Laboriousness Category") THEN;

      "Total Volume" :=
        BuildingCharacter."Average Raw Material Quantity"/100 *
        ProjectRawMaterialHeader."Total Volume (m3)" *
        BuildingMethod.Factor *
        ProjectTypeRawMaterial.Factor *
        LaboriousnessCategory.Factor *
        "Raw Material Type Percentage"/100;

      CalcSkipQuantity;
    END;

    PROCEDURE CalcSkipQuantity@1210190000();
    VAR
      SkipType@1210190000 : Record 11126231;
    BEGIN
      IF SkipType.GET("Skip Type") THEN;

      IF SkipType."Size (m3)" = 0 THEN
        "Skip Quantity" := 0
      ELSE
        "Skip Quantity" := ROUND("Total Volume" / SkipType."Size (m3)", 1, '>');

      CalcLineAmount;
    END;

    PROCEDURE CalcCostPrice@1210190006();
    VAR
      RawMaterialTypeSkipType@1210190000 : Record 11126234;
    BEGIN
      IF RawMaterialTypeSkipType.GET("Raw Material Type", "Skip Type") THEN
        "Cost Price" := RawMaterialTypeSkipType."Cost Price"
      ELSE
        "Cost Price" := 0;

      CalcTotalCostPrice;
    END;

    PROCEDURE CalcTransportPrice@1100528502();
    VAR
      SkipType@1100528500 : Record 11126231;
    BEGIN
      IF SkipType.GET("Skip Type") THEN;

      "Transport Price" := SkipType."Transport Price";

      CalcTotalCostPrice;
    END;

    PROCEDURE CalcTotalCostPrice@1100528503();
    BEGIN
      "Total Cost Price" := "Cost Price" + "Transport Price";

      CalcLineAmount;
    END;

    PROCEDURE CalcLineAmount@1100528504();
    BEGIN
      "Line Amount" := ROUND("Skip Quantity" * "Total Cost Price");
    END;

    PROCEDURE DiscountAmount@1100528500() : Decimal;
    VAR
      ProjectRawMaterialHeader@1100528501 : Record 11126235;
    BEGIN
      IF NOT ProjectRawMaterialHeader.GET("Project No.") THEN
        EXIT;

      CALCFIELDS("Private Amount");
      EXIT(ROUND(ProjectRawMaterialHeader."Discount %"/100 * "Private Amount"));
    END;

    PROCEDURE TotalRawMaterialCost@1100528501() : Decimal;
    BEGIN
      CALCFIELDS("Total Amount");

      EXIT("Total Amount" - DiscountAmount);
    END;

    PROCEDURE HandleHeaderChanges@1100528505(ProjectRawMaterialHeader@1100528501 : Record 11126235;ChangedFieldNo@1100528500 : Integer);
    BEGIN
      IF ChangedFieldNo = ProjectRawMaterialHeader.FIELDNO(ProjectRawMaterialHeader."Building Character") THEN
        CalcRawMaterialTypePercentage
      ELSE
        CalcTotalVolume;
    END;

    LOCAL PROCEDURE TestStatusOpen@37();
    VAR
      ProjectRawMaterialHeader@1100528500 : Record 11126235;
    BEGIN
      IF NOT ProjectRawMaterialHeader.GET("Project No.") THEN
        EXIT;

      IF ProjectRawMaterialHeader.Status = ProjectRawMaterialHeader.Status::Closed THEN
        ProjectRawMaterialHeader.FIELDERROR(Status);
    END;

    PROCEDURE UpdateRemainderLine@1100528506(ProjectNo@1100528500 : Code[20]);
    VAR
      ProjectRawMaterialLine@1100528501 : Record 11126236;
      ProjectRawMaterialLine2@1100528502 : Record 11126236;
      RemainderLineExists@1100528503 : Boolean;
    BEGIN
      IF "Line No." = ReminderLineNo THEN
        EXIT;

      SustainableRawMaterialMgtSetup.GET;
      SustainableRawMaterialMgtSetup.TESTFIELD("Skip Type Remaining Raw Mat.");
      SustainableRawMaterialMgtSetup.TESTFIELD("Raw Mat. Type Remaining Mat.");

      //Update DB before calculating total Raw Material Percentage
      ProjectRawMaterialLine := Rec;
      IF ProjectRawMaterialLine.FIND('=') THEN
        MODIFY; //

      //Calc Total Specified Raw Material Percentage
      ProjectRawMaterialLine.SETRANGE("Project No.", ProjectNo);
      ProjectRawMaterialLine.SETFILTER("Line No.", '<>%1', ReminderLineNo);
      ProjectRawMaterialLine.CALCSUMS("Raw Material Type Percentage");

      //Write Remainder Raw Material Percentage
      RemainderLineExists := ProjectRawMaterialLine2.GET("Project No.", ReminderLineNo);

      IF ProjectRawMaterialLine."Raw Material Type Percentage" = 100 THEN BEGIN
        IF RemainderLineExists THEN
          ProjectRawMaterialLine2.DELETE(TRUE);
      END ELSE BEGIN
        IF NOT RemainderLineExists THEN BEGIN
          ProjectRawMaterialLine2."Project No." := ProjectNo;
          ProjectRawMaterialLine2."Line No." := ReminderLineNo;
          ProjectRawMaterialLine2.INSERT(TRUE);
        END;
        ProjectRawMaterialLine2.VALIDATE("Skip Type", SustainableRawMaterialMgtSetup."Skip Type Remaining Raw Mat.");
        ProjectRawMaterialLine2.VALIDATE("Raw Material Type", SustainableRawMaterialMgtSetup."Raw Mat. Type Remaining Mat.");
        ProjectRawMaterialLine2.VALIDATE("Raw Material Type Percentage", 100 - ProjectRawMaterialLine."Raw Material Type Percentage");
        ProjectRawMaterialLine2.MODIFY(TRUE);
      END;
    END;

    PROCEDURE InitRecord@1100528507();
    BEGIN
      SustainableRawMaterialMgtSetup.GET;
      IF SustainableRawMaterialMgtSetup."Default Skip Type" <> '' THEN
        VALIDATE("Skip Type", SustainableRawMaterialMgtSetup."Default Skip Type");
    END;

    PROCEDURE ReminderLineNo@1100528508() : Integer;
    BEGIN
      EXIT(10000);
    END;

    BEGIN
    END.
  }
}

