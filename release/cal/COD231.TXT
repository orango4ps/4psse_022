OBJECT Codeunit 231 Gen. Jnl.-Post
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.01,4PS14.00,4PSSE;
  }
  PROPERTIES
  {
    TableNo=81;
    EventSubscriberInstance=Manual;
    OnRun=VAR
            GenJnlLine@1000 : Record 81;
          BEGIN
            GenJnlLine.COPY(Rec);
            Code(GenJnlLine);
            COPY(GenJnlLine);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot be filtered when posting recurring journals;NOR=kan ikke filtreres n†r du bokf›rer gjentakelseskladder;SVE=kan inte filtreras n„r †terkommande journaler bokf”rs';
      Text001@1001 : TextConst 'ENU=Do you want to post the journal lines?;NOR=Vil du bokf›re kladdelinjene?;SVE=Vill du bokf”ra journalraderna?';
      Text002@1002 : TextConst 'ENU=There is nothing to post.;NOR=Det finnes ingenting † bokf›re.;SVE=Det finns inget att bokf”ra.';
      Text003@1003 : TextConst 'ENU=The journal lines were successfully posted.;NOR=Kladdelinjene ble bokf›rt uten problemer.;SVE=Journalraderna „r bokf”rda.';
      Text004@1004 : TextConst 'ENU=The journal lines were successfully posted. You are now in the %1 journal.;NOR=Kladdelinjene ble bokf›rt p† riktig m†te. Du er n† i kladd %1.;SVE=Journalraderna „r bokf”rda. Du „r nu i journal %1.';
      Text005@1009 : TextConst 'ENU=Using %1 for Declining Balance can result in misleading numbers for subsequent years. You should manually check the postings and correct them if necessary. Do you want to continue?;NOR=Hvis du bruker %1 for saldo, kan det f›re til villedende tall for etterf›lgende †r. Du b›r kontrollere bokf›ringene manuelt og korrigere dem hvis det er n›dvendig. Vil du fortsette?;SVE=Om %1 anv„nds f”r degressivt saldo kan det leda till missvisande siffror f”r de f”ljande †ren. Kontrollera bokf”ringen manuellt och korrigera om det „r n”dv„ndigt. Vill du forts„tta?';
      Text006@1010 : TextConst '@@@=Source Code in Genenral Journal Template must not be equal to Job G/L WIP in Source Code Setup.;ENU=%1 in %2 must not be equal to %3 in %4.;NOR=%1 i %2 kan ikke v‘re lik %3 i %4.;SVE=%1 i %2 f†r inte vara lika med %3 i %4.';
      PreviewMode@1011 : Boolean;
      gBlnBackground@1210190000 : Boolean;
      gBlnNoCommit@1210190001 : Boolean;
      gReplacePostingDatePeriodic@1100285100 : Boolean;

    LOCAL PROCEDURE Code@1(VAR GenJnlLine@1004 : Record 81);
    VAR
      GenJnlTemplate@1005 : Record 80;
      FALedgEntry@1000 : Record 5601;
      SourceCodeSetup@1001 : Record 242;
      GenJnlPostBatch@1002 : Codeunit 13;
      ConfirmManagement@1006 : Codeunit 27;
      TempJnlBatchName@1003 : Code[10];
      HideDialog@1007 : Boolean;
      GenJournalTemplatePerRole@1100525000 : Record 11020419;
    BEGIN
      HideDialog := FALSE;
      OnBeforeCode(GenJnlLine,HideDialog);

      WITH GenJnlLine DO BEGIN
        GenJournalTemplatePerRole.CheckPermission(TABLECAPTION, "Journal Template Name",TRUE); //**4PS.n

        GenJnlTemplate.GET("Journal Template Name");
        IF GenJnlTemplate.Type = GenJnlTemplate.Type::Jobs THEN BEGIN
          SourceCodeSetup.GET;
          IF GenJnlTemplate."Source Code" = SourceCodeSetup."Job G/L WIP" THEN
            ERROR(Text006,GenJnlTemplate.FIELDCAPTION("Source Code"),GenJnlTemplate.TABLECAPTION,
              SourceCodeSetup.FIELDCAPTION("Job G/L WIP"),SourceCodeSetup.TABLECAPTION);
        END;
        GenJnlTemplate.TESTFIELD("Force Posting Report",FALSE);
        IF GenJnlTemplate.Recurring AND (GETFILTER("Posting Date") <> '') THEN
          FIELDERROR("Posting Date",Text000);

        IF (NOT gBlnBackground) THEN  //**4PS.n
          IF NOT (PreviewMode OR HideDialog) THEN
            IF NOT ConfirmManagement.ConfirmProcess(Text001,TRUE) THEN
              EXIT;

        IF "Account Type" = "Account Type"::"Fixed Asset" THEN BEGIN
          FALedgEntry.SETRANGE("FA No.","Account No.");
          FALedgEntry.SETRANGE("FA Posting Type","FA Posting Type"::"Acquisition Cost");
          IF FALedgEntry.FINDFIRST AND "Depr. Acquisition Cost" AND NOT HideDialog THEN
            IF NOT ConfirmManagement.ConfirmProcess(STRSUBSTNO(Text005,FIELDCAPTION("Depr. Acquisition Cost")),TRUE) THEN
              EXIT;
        END;

        TempJnlBatchName := "Journal Batch Name";

        GenJnlPostBatch.ReplacePostingDatePeriodic(gReplacePostingDatePeriodic);  //RFC135
        GenJnlPostBatch.SetPreviewMode(PreviewMode);
        GenJnlPostBatch.SetSuppressCommit(gBlnNoCommit);  //**4PS.n
        GenJnlPostBatch.RUN(GenJnlLine);

        IF PreviewMode THEN
          EXIT;

        IF (NOT gBlnBackground) THEN BEGIN  //**4PS.n
          IF "Line No." = 0 THEN
            MESSAGE(Text002)
          ELSE
            IF TempJnlBatchName = "Journal Batch Name" THEN
              MESSAGE(Text003)
            ELSE
              MESSAGE(
                Text004,
                "Journal Batch Name");
        END;  //**4PS.n

        IF NOT FIND('=><') OR (TempJnlBatchName <> "Journal Batch Name") THEN BEGIN
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          OnGenJnlLineSetFilter(GenJnlLine);
          FILTERGROUP(0);
          "Line No." := 1;
        END;
      END;
    END;

    [External]
    PROCEDURE Preview@2(VAR GenJournalLineSource@1003 : Record 81);
    VAR
      GenJnlPostPreview@1002 : Codeunit 19;
      GenJnlPost@1001 : Codeunit 231;
    BEGIN
      BINDSUBSCRIPTION(GenJnlPost);
      GenJnlPostPreview.Preview(GenJnlPost,GenJournalLineSource);
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCode@3(VAR GenJournalLine@1000 : Record 81;VAR HideDialog@1001 : Boolean);
    BEGIN
    END;

    [EventSubscriber(Codeunit,19,OnRunPreview)]
    LOCAL PROCEDURE OnRunPreview@4(VAR Result@1000 : Boolean;Subscriber@1001 : Variant;RecVar@1002 : Variant);
    VAR
      GenJournalLine@1004 : Record 81;
      GenJnlPost@1003 : Codeunit 231;
    BEGIN
      GenJnlPost := Subscriber;
      GenJournalLine.COPY(RecVar);
      PreviewMode := TRUE;
      Result := GenJnlPost.RUN(GenJournalLine);
    END;

    [Integration]
    LOCAL PROCEDURE OnGenJnlLineSetFilter@5(VAR GenJournalLine@1000 : Record 81);
    BEGIN
    END;

    PROCEDURE Background@1210190001(iBlnBackground@1210190000 : Boolean);
    BEGIN
      //**4PS
      // If true, no messages to- nor confirmations from- the user will be generated
      gBlnBackground := iBlnBackground;
    END;

    PROCEDURE NoCommitAllowed@1210190000(iBlnNoCommit@1210190000 : Boolean);
    BEGIN
      //**4PS
      // If true, no commit allowed during the process (post-batch)
      gBlnNoCommit := iBlnNoCommit;
    END;

    PROCEDURE ReplacePostingDatePeriodic@1100285100(lv_replace@1100285100 : Boolean);
    BEGIN
      //RFC135
      gReplacePostingDatePeriodic := lv_replace;
    END;

    BEGIN
    {
      4PS MVOS 20100810: Check permission Journal Template before posting.
      160923 ITERO.DL RFC135 Supplier invoices with accrual entries in a closed period should be booked in open period, passover variable to next function
    }
    END.
  }
}

