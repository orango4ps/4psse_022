OBJECT Codeunit 249 VAT Registration Log Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.01,4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 249=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      ValidPathTxt@1002 : TextConst '@@@={Locked};ENU=descendant::vat:valid;NOR=descendant::vat:valid;SVE=descendant::vat:valid';
      NamePathTxt@1001 : TextConst '@@@={Locked};ENU=descendant::vat:traderName;NOR=descendant::vat:traderName;SVE=descendant::vat:traderName';
      AddressPathTxt@1000 : TextConst '@@@={Locked};ENU=descendant::vat:traderAddress;NOR=descendant::vat:traderAddress;SVE=descendant::vat:traderAddress';
      RequestIdPathTxt@1003 : TextConst '@@@={Locked};ENU=descendant::vat:requestIdentifier;NOR=descendant::vat:requestIdentifier;SVE=descendant::vat:requestIdentifier';
      PostcodePathTxt@1004 : TextConst '@@@={Locked};ENU=descendant::vat:traderPostcode;NOR=descendant::vat:traderPostcode;SVE=descendant::vat:traderPostcode';
      StreetPathTxt@1005 : TextConst '@@@={Locked};ENU=descendant::vat:traderStreet;NOR=descendant::vat:traderStreet;SVE=descendant::vat:traderStreet';
      CityPathTxt@1006 : TextConst '@@@={Locked};ENU=descendant::vat:traderCity;NOR=descendant::vat:traderCity;SVE=descendant::vat:traderCity';
      DataTypeManagement@1008 : Codeunit 701;
      ValidVATNoQst@1011 : TextConst 'ENU=The VAT registration number is valid. Do you want us to update the name and address?;NOR=Organisasjonsnummeret er gyldig. Vil du at vi skal oppdatere navnet og adressen?;SVE=Momsregistreringsnumret „r giltigt. Vill du att vi uppdaterar namnet och adressen?';
      ValidVATNoNoAddressQst@1013 : TextConst 'ENU=The VAT registration number is valid. Do you want us to update the name?;NOR=Organisasjonsnummeret er gyldig. Vil du at vi skal oppdatere navnet?;SVE=Momsregistreringsnumret „r giltigt. Vill du uppdatera namnet?';
      InvalidVatRegNoMsg@1010 : TextConst 'ENU=We didn''t find a match for this number. Verify that you entered the correct number.;NOR=Finner ikke dette nummeret. Kontroller at du har angitt riktig nummer.;SVE=Det finns ingen matchning f”r det h„r numret. Kontrollera att du angav r„tt nummer.';
      NotVerifiedVATRegMsg@1009 : TextConst 'ENU=We couldn''t verify the VAT registration number. Try again later.;NOR=Vi kan ikke bekrefte organisasjonsnummeret. Pr›v p† nytt senere.;SVE=Vi kunde inte verifiera momsregistreringsnumret. F”rs”k igen senare.';
      VATSrvDisclaimerUrlTok@1007 : TextConst '@@@={Locked};ENU="https://go.microsoft.com/fwlink/?linkid=841741";NOR="https://go.microsoft.com/fwlink/?linkid=841741";SVE="https://go.microsoft.com/fwlink/?linkid=841741"';
      DescriptionLbl@1012 : TextConst 'ENU=EU VAT Reg. No. Validation Service Setup;NOR=Oppsett av tjeneste for validering av EU-organisasjonsnr.;SVE=Konfigurera valideringstj„nst f”r EU momsreg.nr.';
      CompName@1100529600 : Text[30];

    [External]
    PROCEDURE LogCustomer@1(Customer@1000 : Record 18);
    VAR
      VATRegistrationLog@1001 : Record 249;
      CountryCode@1002 : Code[10];
    BEGIN
      CountryCode := GetCountryCode(Customer."Country/Region Code");
      IF NOT IsEUCountry(CountryCode) THEN
        EXIT;

      InsertVATRegistrationLog(
        Customer."VAT Registration No.",CountryCode,VATRegistrationLog."Account Type"::Customer,Customer."No.");
    END;

    [External]
    PROCEDURE LogVendor@2(Vendor@1002 : Record 23);
    VAR
      VATRegistrationLog@1001 : Record 249;
      CountryCode@1000 : Code[10];
    BEGIN
      CountryCode := GetCountryCode(Vendor."Country/Region Code");
      IF NOT IsEUCountry(CountryCode) THEN
        EXIT;

      InsertVATRegistrationLog(
        Vendor."VAT Registration No.",CountryCode,VATRegistrationLog."Account Type"::Vendor,Vendor."No.");
    END;

    [External]
    PROCEDURE LogContact@8(Contact@1000 : Record 5050);
    VAR
      VATRegistrationLog@1001 : Record 249;
      CountryCode@1002 : Code[10];
    BEGIN
      CountryCode := GetCountryCode(Contact."Country/Region Code");
      IF NOT IsEUCountry(CountryCode) THEN
        EXIT;

      InsertVATRegistrationLog(
        Contact."VAT Registration No.",CountryCode,VATRegistrationLog."Account Type"::Contact,Contact."No.");
    END;

    [Internal]
    PROCEDURE LogVerification@3(VAR VATRegistrationLog@1000 : Record 249;XMLDoc@1001 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";Namespace@1003 : Text);
    VAR
      XMLDOMMgt@1002 : Codeunit 6224;
      FoundXmlNode@1004 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      IF NOT XMLDOMMgt.FindNodeWithNamespace(XMLDoc.DocumentElement,ValidPathTxt,'vat',Namespace,FoundXmlNode) THEN
        EXIT;

      CASE LOWERCASE(FoundXmlNode.InnerText) OF
        'true':
          BEGIN
            VATRegistrationLog."Entry No." := 0;
            VATRegistrationLog.Status := VATRegistrationLog.Status::Valid;
            VATRegistrationLog."Verified Date" := CURRENTDATETIME;
            VATRegistrationLog."User ID" := USERID;

            VATRegistrationLog."Verified Name" := COPYSTR(ExtractValue(NamePathTxt,XMLDoc,Namespace),1,
                MAXSTRLEN(VATRegistrationLog."Verified Name"));
            VATRegistrationLog."Verified Address" := COPYSTR(ExtractValue(AddressPathTxt,XMLDoc,Namespace),1,
                MAXSTRLEN(VATRegistrationLog."Verified Address"));
            VATRegistrationLog."Request Identifier" := COPYSTR(ExtractValue(RequestIdPathTxt,XMLDoc,Namespace),1,
                MAXSTRLEN(VATRegistrationLog."Request Identifier"));
            VATRegistrationLog."Verified Postcode" := COPYSTR(ExtractValue(PostcodePathTxt,XMLDoc,Namespace),1,
                MAXSTRLEN(VATRegistrationLog."Verified Postcode"));
            VATRegistrationLog."Verified Street" := COPYSTR(ExtractValue(StreetPathTxt,XMLDoc,Namespace),1,
                MAXSTRLEN(VATRegistrationLog."Verified Street"));
            VATRegistrationLog."Verified City" := COPYSTR(ExtractValue(CityPathTxt,XMLDoc,Namespace),1,
                MAXSTRLEN(VATRegistrationLog."Verified City"));

            VATRegistrationLog.INSERT(TRUE);
          END;
        'false':
          BEGIN
            VATRegistrationLog."Entry No." := 0;
            VATRegistrationLog."Verified Date" := CURRENTDATETIME;
            VATRegistrationLog.Status := VATRegistrationLog.Status::Invalid;
            VATRegistrationLog."User ID" := USERID;
            VATRegistrationLog."Verified Name" := '';
            VATRegistrationLog."Verified Address" := '';
            VATRegistrationLog."Request Identifier" := '';
            VATRegistrationLog."Verified Postcode" := '';
            VATRegistrationLog."Verified Street" := '';
            VATRegistrationLog."Verified City" := '';
            VATRegistrationLog.INSERT(TRUE);
          END;
      END;
    END;

    LOCAL PROCEDURE LogUnloggedVATRegistrationNumbers@10();
    VAR
      Customer@1000 : Record 18;
      Vendor@1001 : Record 23;
      Contact@1002 : Record 5050;
      VATRegistrationLog@1003 : Record 249;
    BEGIN
      Customer.SETFILTER("VAT Registration No.",'<>%1','');
      IF Customer.FINDSET THEN
        REPEAT
          VATRegistrationLog.SETRANGE("VAT Registration No.",Customer."VAT Registration No.");
          IF VATRegistrationLog.ISEMPTY THEN
            LogCustomer(Customer);
        UNTIL Customer.NEXT = 0;

      Vendor.SETFILTER("VAT Registration No.",'<>%1','');
      IF Vendor.FINDSET THEN
        REPEAT
          VATRegistrationLog.SETRANGE("VAT Registration No.",Vendor."VAT Registration No.");
          IF VATRegistrationLog.ISEMPTY THEN
            LogVendor(Vendor);
        UNTIL Vendor.NEXT = 0;

      Contact.SETFILTER("VAT Registration No.",'<>%1','');
      IF Contact.FINDSET THEN
        REPEAT
          VATRegistrationLog.SETRANGE("VAT Registration No.",Contact."VAT Registration No.");
          IF VATRegistrationLog.ISEMPTY THEN
            LogContact(Contact);
        UNTIL Contact.NEXT = 0;

      COMMIT;
    END;

    LOCAL PROCEDURE InsertVATRegistrationLog@16(VATRegNo@1002 : Text[20];CountryCode@1000 : Code[10];AccountType@1001 : Option;AccountNo@1003 : Code[20]);
    VAR
      VATRegistrationLog@1004 : Record 249;
    BEGIN
      WITH VATRegistrationLog DO BEGIN
        INIT;
        "VAT Registration No." := VATRegNo;
        "Country/Region Code" := CountryCode;
        "Account Type" := AccountType;
        "Account No." := AccountNo;
        "User ID" := USERID;
        INSERT(TRUE);
      END;
    END;

    [External]
    PROCEDURE DeleteCustomerLog@4(Customer@1000 : Record 18);
    VAR
      VATRegistrationLog@1001 : Record 249;
    BEGIN
      WITH VATRegistrationLog DO BEGIN
        //**4PS.sn
        IF (CompName <> '') AND (CompName <> COMPANYNAME) THEN
          CHANGECOMPANY(CompName);
        //**4PS.en
        SETRANGE("Account Type","Account Type"::Customer);
        SETRANGE("Account No.",Customer."No.");
        IF NOT ISEMPTY THEN
          DELETEALL;
      END;
    END;

    [External]
    PROCEDURE DeleteVendorLog@5(Vendor@1000 : Record 23);
    VAR
      VATRegistrationLog@1001 : Record 249;
    BEGIN
      WITH VATRegistrationLog DO BEGIN
        SETRANGE("Account Type","Account Type"::Vendor);
        SETRANGE("Account No.",Vendor."No.");
        IF NOT ISEMPTY THEN
          DELETEALL;
      END;
    END;

    [External]
    PROCEDURE DeleteContactLog@9(Contact@1000 : Record 5050);
    VAR
      VATRegistrationLog@1001 : Record 249;
    BEGIN
      WITH VATRegistrationLog DO BEGIN
        SETRANGE("Account Type","Account Type"::Contact);
        SETRANGE("Account No.",Contact."No.");
        IF NOT ISEMPTY THEN
          DELETEALL;
      END;
    END;

    [External]
    PROCEDURE AssistEditCustomerVATReg@7(Customer@1000 : Record 18);
    VAR
      VATRegistrationLog@1001 : Record 249;
    BEGIN
      WITH VATRegistrationLog DO BEGIN
        SETRANGE("Account Type","Account Type"::Customer);
        SETRANGE("Account No.",Customer."No.");
        IF ISEMPTY THEN
          LogUnloggedVATRegistrationNumbers;
        PAGE.RUNMODAL(PAGE::"VAT Registration Log",VATRegistrationLog);
      END;
    END;

    [External]
    PROCEDURE AssistEditVendorVATReg@6(Vendor@1001 : Record 23);
    VAR
      VATRegistrationLog@1000 : Record 249;
    BEGIN
      WITH VATRegistrationLog DO BEGIN
        SETRANGE("Account Type","Account Type"::Vendor);
        SETRANGE("Account No.",Vendor."No.");
        IF ISEMPTY THEN
          LogUnloggedVATRegistrationNumbers;
        PAGE.RUNMODAL(PAGE::"VAT Registration Log",VATRegistrationLog);
      END;
    END;

    [External]
    PROCEDURE AssistEditContactVATReg@11(Contact@1001 : Record 5050);
    VAR
      VATRegistrationLog@1000 : Record 249;
    BEGIN
      WITH VATRegistrationLog DO BEGIN
        SETRANGE("Account Type","Account Type"::Contact);
        SETRANGE("Account No.",Contact."No.");
        IF ISEMPTY THEN
          LogUnloggedVATRegistrationNumbers;
        PAGE.RUNMODAL(PAGE::"VAT Registration Log",VATRegistrationLog);
      END;
    END;

    LOCAL PROCEDURE IsEUCountry@12(CountryCode@1000 : Code[10]) : Boolean;
    VAR
      CountryRegion@1001 : Record 9;
      CompanyInformation@1002 : Record 79;
    BEGIN
      IF (CountryCode = '') AND CompanyInformation.GET THEN
        CountryCode := CompanyInformation."Country/Region Code";

      IF CountryCode <> '' THEN
        IF CountryRegion.GET(CountryCode) THEN
          EXIT(CountryRegion."EU Country/Region Code" <> '');

      EXIT(FALSE);
    END;

    LOCAL PROCEDURE GetCountryCode@13(CountryCode@1000 : Code[10]) : Code[10];
    VAR
      CompanyInformation@1001 : Record 79;
    BEGIN
      IF CountryCode <> '' THEN
        EXIT(CountryCode);

      CompanyInformation.GET;
      EXIT(CompanyInformation."Country/Region Code");
    END;

    LOCAL PROCEDURE ExtractValue@14(Xpath@1000 : Text;XMLDoc@1003 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";Namespace@1004 : Text) : Text;
    VAR
      XMLDOMMgt@1002 : Codeunit 6224;
      FoundXmlNode@1001 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      IF NOT XMLDOMMgt.FindNodeWithNamespace(XMLDoc.DocumentElement,Xpath,'vat',Namespace,FoundXmlNode) THEN
        EXIT('');
      EXIT(FoundXmlNode.InnerText);
    END;

    [External]
    PROCEDURE CheckVIESForVATNo@550(VAR RecordRef@1004 : RecordRef;VAR VATRegistrationLog@1040 : Record 249;RecordVariant@1001 : Variant;EntryNo@1010 : Code[20];CountryCode@1013 : Code[10];AccountType@1000 : Option);
    VAR
      Customer@1002 : Record 18;
      VATRegNoSrvConfig@1007 : Record 248;
      CountryRegion@1003 : Record 9;
      VatRegNoFieldRef@1009 : FieldRef;
      VATRegNo@1012 : Text[20];
    BEGIN
      RecordRef.GETTABLE(RecordVariant);
      IF NOT CountryRegion.IsEUCountry(CountryCode) THEN
        EXIT; // VAT Reg. check Srv. is only available for EU countries.

      IF VATRegNoSrvConfig.VATRegNoSrvIsEnabled THEN BEGIN
        DataTypeManagement.GetRecordRef(RecordVariant,RecordRef);
        IF NOT DataTypeManagement.FindFieldByName(RecordRef,VatRegNoFieldRef,Customer.FIELDNAME("VAT Registration No.")) THEN
          EXIT;
        VATRegNo := VatRegNoFieldRef.VALUE;

        VATRegistrationLog.InitVATRegLog(VATRegistrationLog,CountryCode,AccountType,EntryNo,VATRegNo);
        CODEUNIT.RUN(CODEUNIT::"VAT Lookup Ext. Data Hndl",VATRegistrationLog);
      END;
    END;

    [External]
    PROCEDURE UpdateRecordFromVATRegLog@370(VAR RecordRef@1004 : RecordRef;RecordVariant@1001 : Variant;VATRegistrationLog@1040 : Record 249);
    VAR
      ConfirmQst@1000 : Text;
    BEGIN
      RecordRef.GETTABLE(RecordVariant);
      CASE VATRegistrationLog.Status OF
        VATRegistrationLog.Status::Valid:
          BEGIN
            IF HasAddress(VATRegistrationLog) THEN
              ConfirmQst := ValidVATNoQst
            ELSE
              ConfirmQst := ValidVATNoNoAddressQst;

            IF CONFIRM(ConfirmQst) THEN
              PopulateFieldsFromVATRegLog(RecordRef,RecordVariant,VATRegistrationLog);
          END;
        VATRegistrationLog.Status::Invalid:
          MESSAGE(InvalidVatRegNoMsg);
        ELSE
          MESSAGE(NotVerifiedVATRegMsg);
      END;
    END;

    LOCAL PROCEDURE PopulateFieldsFromVATRegLog@26(VAR RecordRef@1004 : RecordRef;RecordVariant@1001 : Variant;VATRegistrationLog@1000 : Record 249);
    VAR
      Customer@1002 : Record 18;
      FieldRef@1003 : FieldRef;
    BEGIN
      DataTypeManagement.GetRecordRef(RecordVariant,RecordRef);

      IF DataTypeManagement.FindFieldByName(RecordRef,FieldRef,Customer.FIELDNAME(Name)) THEN
        FieldRef.VALUE(COPYSTR(VATRegistrationLog."Verified Name",1,FieldRef.LENGTH));

      IF VATRegistrationLog."Verified Postcode" <> '' THEN
        IF DataTypeManagement.FindFieldByName(RecordRef,FieldRef,Customer.FIELDNAME("Post Code")) THEN
          FieldRef.VALUE(COPYSTR(VATRegistrationLog."Verified Postcode",1,FieldRef.LENGTH));

      IF VATRegistrationLog."Verified Street" <> '' THEN
        IF DataTypeManagement.FindFieldByName(RecordRef,FieldRef,Customer.FIELDNAME(Address)) THEN
          FieldRef.VALUE(COPYSTR(VATRegistrationLog."Verified Street",1,FieldRef.LENGTH));

      IF VATRegistrationLog."Verified City" <> '' THEN
        IF DataTypeManagement.FindFieldByName(RecordRef,FieldRef,Customer.FIELDNAME(City)) THEN
          FieldRef.VALUE(COPYSTR(VATRegistrationLog."Verified City",1,FieldRef.LENGTH));
    END;

    [External]
    PROCEDURE InitServiceSetup@22();
    VAR
      VATRegNoSrvConfig@1000 : Record 248;
      VATLookupExtDataHndl@1001 : Codeunit 248;
    BEGIN
      IF NOT VATRegNoSrvConfig.FINDFIRST THEN BEGIN
        VATRegNoSrvConfig.INIT;
        VATRegNoSrvConfig."Service Endpoint" := VATLookupExtDataHndl.GetVATRegNrValidationWebServiceURL;
        VATRegNoSrvConfig.Enabled := FALSE;
        VATRegNoSrvConfig.INSERT;
      END;
    END;

    [External]
    PROCEDURE SetupService@18();
    VAR
      VATRegNoSrvConfig@1000 : Record 248;
    BEGIN
      IF VATRegNoSrvConfig.FINDFIRST THEN
        EXIT;
      InitServiceSetup;
    END;

    [External]
    PROCEDURE EnableService@15();
    VAR
      VATRegNoSrvConfig@1000 : Record 248;
    BEGIN
      IF NOT VATRegNoSrvConfig.FINDFIRST THEN BEGIN
        InitServiceSetup;
        VATRegNoSrvConfig.FINDFIRST;
      END;

      VATRegNoSrvConfig.Enabled := TRUE;
      VATRegNoSrvConfig.MODIFY;
    END;

    [External]
    PROCEDURE ValidateVATRegNoWithVIES@560(VAR RecordRef@1004 : RecordRef;RecordVariant@1001 : Variant;EntryNo@1010 : Code[20];AccountType@1011 : Option;CountryCode@1013 : Code[10]);
    VAR
      VATRegistrationLog@1008 : Record 249;
    BEGIN
      CheckVIESForVATNo(RecordRef,VATRegistrationLog,RecordVariant,EntryNo,CountryCode,AccountType);

      IF VATRegistrationLog.FIND THEN // Only update if the log was created
        UpdateRecordFromVATRegLog(RecordRef,RecordVariant,VATRegistrationLog);
    END;

    [External]
    PROCEDURE GetServiceDisclaimerUR@17() : Text;
    BEGIN
      EXIT(VATSrvDisclaimerUrlTok);
    END;

    [EventSubscriber(Table,1400,OnRegisterServiceConnection)]
    [External]
    PROCEDURE HandleViesRegisterServiceConnection@20(VAR ServiceConnection@1003 : Record 1400);
    VAR
      VATRegNoSrvConfig@1001 : Record 248;
      RecRef@1002 : RecordRef;
    BEGIN
      SetupService;
      VATRegNoSrvConfig.FINDFIRST;

      RecRef.GETTABLE(VATRegNoSrvConfig);

      IF VATRegNoSrvConfig.Enabled THEN
        ServiceConnection.Status := ServiceConnection.Status::Enabled
      ELSE
        ServiceConnection.Status := ServiceConnection.Status::Disabled;
      WITH VATRegNoSrvConfig DO
        ServiceConnection.InsertServiceConnection(
          ServiceConnection,RecRef.RECORDID,DescriptionLbl,"Service Endpoint",PAGE::"VAT Registration Config");
    END;

    LOCAL PROCEDURE HasAddress@21(VATRegistrationLog@1000 : Record 249) : Boolean;
    BEGIN
      IF (VATRegistrationLog."Verified Postcode" = '') AND
         (VATRegistrationLog."Verified Street" = '') AND
         (VATRegistrationLog."Verified City" = '')
      THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    PROCEDURE SetCompany@1100529600(CompNameNew@1100529600 : Text[30]);
    BEGIN
      //**4PS
      CompName := CompNameNew;
    END;

    BEGIN
    END.
  }
}

