OBJECT Codeunit 6085705 Purch. Doc. - Validation
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.5.00.07,4PS14.00;
  }
  PROPERTIES
  {
    TableNo=6085590;
    OnRun=VAR
            DCSetup@1160040002 : Record 6085573;
            PurchSetup@1160040010 : Record 312;
            DocumentLine@1000000000 : TEMPORARY Record 6085596;
            Template@1000000010 : Record 6085579;
            Field@1000000004 : Record 6085580;
            DocumentComment@1000000008 : Record 6085594;
            DocPage@1160040006 : Record 6085591;
            Vendor@161024012 : Record 23;
            PurchHeader@161024014 : Record 38;
            VendLedgEntry@161024015 : Record 25;
            PaymentTerms@161024020 : Record 3;
            GLSetup@161024022 : Record 98;
            DocVal@1160040003 : Record 6085593;
            Currency@1160040008 : Record 4;
            DCDocument@6085573 : Record 6085590;
            CaptureMgt@1000000009 : Codeunit 6085576;
            DCAppMgt@1160040007 : Codeunit 6085722;
            IsValid@1160040001 : Boolean;
            IsInvoice@1160040004 : Boolean;
            UsePayTo@1160040000 : Boolean;
            VendDocNo@161024016 : Code[250];
            DocCurrCode@161024023 : Code[20];
            CurrCode@1160040009 : Code[10];
            PayToVendNo@1160040012 : Code[20];
            DocDate@161024019 : Date;
            DocDueDate@161024017 : Date;
            CalcDueDate@161024018 : Date;
            AmountExclVAT@1000000002 : Decimal;
            VATAmount@1000000005 : Decimal;
            AmountInclVAT@1000000006 : Decimal;
            SubtractedAmountExclVAT@161024013 : Decimal;
            TotalLineAmount@1000000003 : Decimal;
            DocValue@1100528200 : Record 6085593;
            CompanyInformation@1100528203 : Record 79;
            CompanyVATRegNo@1100528208 : Code[30];
            OurVATRegNo@1100528204 : Code[30];
            BankAccNo@1100528205 : Code[50];
            VendBankAccNo@1100528600 : Code[50];
            VendBankAccount@1100528206 : Record 288;
            BankAccFound@1100528207 : Boolean;
            BAccNo@1100527000 : Code[50];
            VendBAccNo@1100527001 : Code[50];
            BAccFound@1100527002 : Boolean;
            MaxVATDiffAllowed@1160040011 : Decimal;
            Text@1160040005 : Text[250];
            VendDocNoCaption@1160040013 : Text[80];
            CommentText@1160040014 : Text[250];
            FldName@6085574 : Text[80];
          BEGIN
            CODEUNIT.RUN(CODEUNIT::"Doc. - Field Validation",Rec);

            Template.GET("Template No.");

            IsValid := OK;

            Vendor.GET(GetSourceID);
            IF Vendor.Blocked = Vendor.Blocked::All THEN BEGIN
              DocumentComment.Add(Rec,Field,0,DocumentComment.Area::Validation,DocumentComment."Comment Type"::Error,
                STRSUBSTNO(Text007,Vendor.FIELDCAPTION(Blocked),Vendor.Blocked,Vendor.TABLECAPTION,Vendor."No."));
              IsValid := FALSE;
            END;

            AmountExclVAT := PurchDocMgt.GetAllAmountsExclVAT(Rec);
            VATAmount := PurchDocMgt.GetVATAmount(Rec);
            SubtractedAmountExclVAT := PurchDocMgt.GetSubtractedAmountExclVAT(Rec);
            AmountInclVAT := PurchDocMgt.GetAmountInclVAT(Rec);
            IsInvoice := PurchDocMgt.GetIsInvoice(Rec);

            // *********************************************************************************************************************************
            // CHECK EXTERNAL DOCUMENT NO.
            // *********************************************************************************************************************************
            VendDocNo := COPYSTR(PurchDocMgt.GetDocumentNo(Rec),1,MAXSTRLEN(VendDocNo));
            IF VendDocNo <> '' THEN BEGIN
              IF Vendor."Pay-to Vendor No." = '' THEN BEGIN
                PayToVendNo := Vendor."No.";
                UsePayTo := FALSE;
              END ELSE BEGIN
                PayToVendNo := Vendor."Pay-to Vendor No.";
                UsePayTo := TRUE;
              END;

              VendLedgEntry.SETCURRENTKEY("External Document No.");
              IF IsInvoice THEN BEGIN
                VendLedgEntry.SETRANGE("Document Type",VendLedgEntry."Document Type"::Invoice);
                VendDocNoCaption := PurchHeader.FIELDCAPTION("Vendor Invoice No.");
              END ELSE BEGIN
                VendLedgEntry.SETRANGE("Document Type",VendLedgEntry."Document Type"::"Credit Memo");
                VendDocNoCaption := PurchHeader.FIELDCAPTION("Vendor Cr. Memo No.");
              END;

              VendLedgEntry.SETRANGE("External Document No.",COPYSTR(VendDocNo,1,MAXSTRLEN(VendLedgEntry."External Document No.")));
              VendLedgEntry.SETRANGE("Vendor No.",PayToVendNo);

              IF VendLedgEntry.FINDFIRST THEN BEGIN
                IF UsePayTo THEN
                  DocumentComment.Add(Rec,Field,0,DocumentComment.Area::Validation,DocumentComment."Comment Type"::Error,
                    STRSUBSTNO(DocNoExistToOnEntryForPayMsg,VendDocNoCaption,VendDocNo,VendLedgEntry.TABLECAPTION,
                    VendLedgEntry.FIELDCAPTION("Entry No."),VendLedgEntry."Entry No.",Vendor.FIELDCAPTION("Pay-to Vendor No."),
                    Vendor."Pay-to Vendor No."))
                ELSE
                  DocumentComment.Add(Rec,Field,0,DocumentComment.Area::Validation,DocumentComment."Comment Type"::Error,
                    STRSUBSTNO(DocNoExistOnEntryMsg,VendDocNoCaption,VendDocNo,VendLedgEntry.TABLECAPTION,
                    VendLedgEntry.FIELDCAPTION("Entry No."),VendLedgEntry."Entry No."));
                IsValid := FALSE;
              END ELSE BEGIN
                // Key only exists in NAV 7.00.00 and forward
                IF PurchHeader.SETCURRENTKEY("Document Type","Pay-to Vendor No.") THEN;

                IF IsInvoice THEN BEGIN
                  PurchHeader.SETRANGE("Document Type",PurchHeader."Document Type"::Invoice);
                  PurchHeader.SETRANGE("Vendor Invoice No.",COPYSTR(VendDocNo,1,MAXSTRLEN(PurchHeader."Vendor Invoice No.")));
                END ELSE BEGIN
                  PurchHeader.SETRANGE("Document Type",PurchHeader."Document Type"::"Credit Memo");
                  PurchHeader.SETRANGE("Vendor Cr. Memo No.",COPYSTR(VendDocNo,1,MAXSTRLEN(PurchHeader."Vendor Cr. Memo No.")));
                END;

                PurchHeader.SETRANGE("Pay-to Vendor No.",PayToVendNo);

                IF PurchHeader.FINDFIRST THEN BEGIN
                  IF UsePayTo THEN BEGIN
                    IF PurchHeader."Document Type" = PurchHeader."Document Type"::Invoice THEN
                      CommentText := InvDocNoExistForPayToMsg
                    ELSE
                      CommentText := CrMemoDocNoExistForPayToMsg;

                    DocumentComment.Add(Rec,Field,0,DocumentComment.Area::Validation,DocumentComment."Comment Type"::Error,
                      STRSUBSTNO(CommentText,VendDocNoCaption,VendDocNo,PurchHeader."No.",
                      Vendor.FIELDCAPTION("Pay-to Vendor No."),Vendor."Pay-to Vendor No."))
                  END ELSE BEGIN
                    IF PurchHeader."Document Type" = PurchHeader."Document Type"::Invoice THEN
                      CommentText := InvDocNoExistMsg
                    ELSE
                      CommentText := CrMemoDocNoExistMsg;

                    DocumentComment.Add(Rec,Field,0,DocumentComment.Area::Validation,DocumentComment."Comment Type"::Error,
                      STRSUBSTNO(CommentText,VendDocNoCaption,VendDocNo,PurchHeader."No."));
                  END;
                  IsValid := FALSE;
                END;
              END;
            END;

            // *********************************************************************************************************************************
            // WARN IF DIFFERENT DATE FORMATS ARE USED ON FIELDS
            // *********************************************************************************************************************************
            Field.SETRANGE("Template No.","Template No.");
            Field.SETRANGE("Data Type",Field."Data Type"::Date);
            IF Field.FINDFIRST THEN BEGIN
              Field.SETFILTER("Date Format",'<>%1',Field."Date Format");
              IF NOT Field.ISEMPTY THEN
                DocumentComment.Add(Rec,Field,0,DocumentComment.Area::Validation,DocumentComment."Comment Type"::Warning,Text016);
            END;
            Field.RESET;

            //**4PS.sn
            IF Template."Use E-mail Date for Doc. Date" AND ("E-Mail Date" <> 0DT) THEN
              DocDate := DT2DATE("E-Mail Date")
            ELSE
            //**4PS.en
              DocDate := PurchDocMgt.GetDocumentDate(Rec);

            //**4PS.sn
            //Bhaskar: Retained this change in Continia 4.50 upgrade - though not sure what the intent was.
            //         Continia 4.50 code does not do this - but not sure if change was this is specific to 4PS.
            IF NOT PurchDocMgt.GetIsInvoice(Rec) THEN BEGIN
              DocValue.SETRANGE("Document No.", Rec."No.");
              DocValue.SETRANGE(Code, 'DUEDATE');
              IF DocValue.FINDFIRST THEN BEGIN
                DocValue."Value (Date)" := DocDate;
                DocValue.MODIFY;
              END;
            END;
            //**4PS.en

            DocDueDate := PurchDocMgt.GetDueDate(Rec);

            IF IsInvoice THEN BEGIN
              IF DocDate <> 0D THEN
                IF PaymentTerms.GET(Vendor."Payment Terms Code") AND (FORMAT(PaymentTerms."Due Date Calculation") <> '') THEN
                  CalcDueDate := CALCDATE(PaymentTerms."Due Date Calculation",DocDate);

              IF Field.GET("Template No.",Field.Type::Header,'DUEDATE') THEN BEGIN
                // *****************************************************************************************************************************
                // WARN IF DUE DATE = DOCUMENT DATE
                // *****************************************************************************************************************************
                IF Field."Search for Value" THEN
                  IF (DocDueDate = DocDate) AND (DocDueDate <> CalcDueDate) AND (DocDate <> 0D) THEN
                    DocumentComment.Add(Rec,Field,0,DocumentComment.Area::Validation,DocumentComment."Comment Type"::Warning,
                      STRSUBSTNO(Text008,PurchDocMgt.GetDueDateDesc(Rec),PurchDocMgt.GetDocDateDesc(Rec)));

                // *****************************************************************************************************************************
                // WARN IF DUE DATE <> CALCULATE DUE DATE (BASED ON THE DOCUMENT DATE AND VENDOR PAYMENT TERMS)
                // *****************************************************************************************************************************
                IF (DocDate <> 0D) AND (DocDueDate <> 0D) AND (CalcDueDate <> 0D) AND (DocDueDate <> CalcDueDate) THEN
                  DocumentComment.Add(Rec,Field,0,DocumentComment.Area::Validation,DocumentComment."Comment Type"::Warning,
                    STRSUBSTNO(Text009,Vendor."Payment Terms Code"));
              END;
            END ELSE BEGIN
            // *********************************************************************************************************************************
            // WARN IF THIS IS A CREDIT MEMO AND DUE DATE IS IN THE FUTURE
            // *********************************************************************************************************************************
              IF DocDueDate > TODAY THEN
                DocumentComment.Add(Rec,Field,0,DocumentComment.Area::Validation,DocumentComment."Comment Type"::Warning,
                  STRSUBSTNO(Text017,PurchDocMgt.GetDueDateDesc(Rec)));
            END;

            IF Field.GET("Template No.",Field.Type::Header,'CURRCODE') THEN
              FldName := Field."Field Name"
            ELSE
              FldName := PurchHeader.FIELDCAPTION("Currency Code");

            DocCurrCode := PurchDocMgt.GetCurrencyCode(Rec);
            DCSetup.GET;
            GLSetup.GET;

            // *********************************************************************************************************************************
            // CHECK CURRENCY CODE
            // *********************************************************************************************************************************
            IF (DocCurrCode = '') AND DCSetup."Fill-out LCY" THEN
              DocumentComment.Add(Rec,Field,0,DocumentComment.Area::Validation,DocumentComment."Comment Type"::Error,
                STRSUBSTNO(MissingCurrencyCodeMsg,FldName,DCSetup.FIELDCAPTION("Fill-out LCY"),DCSetup.TABLECAPTION));

            // *********************************************************************************************************************************
            // WARN IF CURRENCY CODE <> VENDOR CARD CURRENCY CODE
            // *********************************************************************************************************************************
            IF (NOT ((Vendor."Currency Code" IN ['',GLSetup."LCY Code"]) AND (DocCurrCode IN ['',GLSetup."LCY Code"])) AND
              (Vendor."Currency Code" <> DocCurrCode))
            THEN
              DocumentComment.Add(Rec,Field,0,DocumentComment.Area::Validation,DocumentComment."Comment Type"::Warning,
                STRSUBSTNO(Text011,DocCurrCode,Vendor."Currency Code"));

            // *********************************************************************************************************************************
            // WARN IF TOTAL AMOUNTS ARE NEGATIVE WHEN THIS IS AN INVOICE (LIKELY THAT THE DOCUMENT TYPE SHOULD BE A CREDIT MEMO)
            // *********************************************************************************************************************************
            IF IsInvoice THEN
              IF AmountExclVAT < 0 THEN
                DocumentComment.Add(Rec,Field,0,DocumentComment.Area::Validation,DocumentComment."Comment Type"::Error,
                  STRSUBSTNO(FieldIsNegativeMsg,DCDocument.FIELDCAPTION("Amount Excl. VAT")));

            // *********************************************************************************************************************************
            // CHECK VAT AMOUNT
            // *********************************************************************************************************************************
            PurchSetup.GET;

            IF (DocCurrCode = '') OR ((DocCurrCode <> '') AND (DocCurrCode = GLSetup."LCY Code") AND NOT DCSetup."Fill-out LCY") THEN BEGIN
              Currency.InitRoundingPrecision;
              CurrCode := GLSetup."LCY Code";

              IF PurchSetup."Invoice Rounding" THEN
                GLSetup.TESTFIELD("Inv. Rounding Precision (LCY)");
            END ELSE BEGIN
              Currency.GET(DocCurrCode);
              CurrCode := DocCurrCode;

              IF PurchSetup."Invoice Rounding" THEN
                Currency.TESTFIELD("Invoice Rounding Precision");
            END;

            IF PurchSetup."Invoice Rounding" THEN
              MaxVATDiffAllowed := Currency."Invoice Rounding Precision"
            ELSE
              MaxVATDiffAllowed := 0;

            IF Template."Purch. Validate VAT Calc." THEN BEGIN
              IF ABS((AmountExclVAT + VATAmount) - AmountInclVAT) > MaxVATDiffAllowed THEN BEGIN
                DocumentComment.Add(Rec,Field,0,DocumentComment.Area::Validation,DocumentComment."Comment Type"::Error,Text001);
                IsValid := FALSE;
              END;
            END;

            //**4PS.sn
            CompanyInformation.GET;
            CompanyVATRegNo := CompanyInformation."VAT Registration No.";
            CompanyVATRegNo := DELCHR(CompanyVATRegNo);
            OurVATRegNo := PurchDocMgt.GetOurVATNo(Rec);
            OurVATRegNo := DELCHR(OurVATRegNo);
            IF OurVATRegNo <> '' THEN BEGIN
              IF STRLEN(CompanyVATRegNo) <= STRLEN(OurVATRegNo) THEN BEGIN
                IF STRPOS(CompanyVATRegNo, OurVATRegNo) = 0 THEN
                  DocumentComment.Add(Rec, Field, 0, DocumentComment.Area::Validation, DocumentComment."Comment Type"::Warning, Text11012001);
              END ELSE BEGIN
                IF STRPOS(OurVATRegNo, CompanyVATRegNo) = 0 THEN
                  DocumentComment.Add(Rec, Field, 0, DocumentComment.Area::Validation, DocumentComment."Comment Type"::Warning, Text11012001);
              END;
            END;

            BankAccNo := PurchDocMgt.GetBankAccountNo(Rec);
            BankAccNo := DELCHR(BankAccNo, '=', ' .');
            VendBankAccount.SETRANGE("Vendor No.", Vendor."No.");
            VendBankAccount.SETRANGE("B-Account", FALSE);
            IF VendBankAccount.FINDSET THEN
              REPEAT
                IF VendBankAccount.IBAN <> '' THEN BEGIN
                  VendBankAccNo := DELCHR(VendBankAccount.IBAN, '=', ' .');
                  IF BankAccNo = VendBankAccNo THEN
                    BankAccFound := TRUE;
                END ELSE BEGIN
                  VendBankAccNo := DELCHR(VendBankAccount."Bank Account No.", '=', 'P .');
                  IF BankAccNo = VendBankAccNo THEN
                    BankAccFound := TRUE;
                END;
              UNTIL (VendBankAccount.NEXT = 0) OR BankAccFound;

            IF NOT BankAccFound THEN BEGIN
              DocumentComment.Add(Rec, Field, 0, DocumentComment.Area::Validation, DocumentComment."Comment Type"::Error, Text11012002);
              IsValid := FALSE;
            END;

            BAccNo := PurchDocMgt.GetBAccountNo(Rec);
            BAccNo := DELCHR(BAccNo, '=', ' .');
            IF PurchDocMgt.GetAmountBlockedAcc(Rec) <> 0 THEN BEGIN
              VendBankAccount.SETRANGE("Vendor No.", Vendor."No.");
              VendBankAccount.SETRANGE("B-Account", TRUE);
              IF VendBankAccount.FINDSET THEN
                REPEAT
                  IF VendBankAccount.IBAN <> '' THEN BEGIN
                    VendBAccNo := DELCHR(VendBankAccount.IBAN, '=', ' .');
                    IF BAccNo = VendBAccNo THEN
                      BAccFound:= TRUE;
                  END ELSE BEGIN
                    VendBAccNo := DELCHR(VendBankAccount."Bank Account No.", '=', 'P .');
                    IF BAccNo = VendBAccNo THEN
                      BAccFound:= TRUE;
                  END;
                UNTIL (VendBankAccount.NEXT = 0) OR BAccFound;

              IF NOT BAccFound THEN BEGIN
                DocumentComment.Add(Rec, Field, 0, DocumentComment.Area::Validation, DocumentComment."Comment Type"::Error, Text11012003);
                IsValid := FALSE;
              END;
            END;
            //**4PS.en
            IF PurchSetup."Invoice Rounding" THEN
              IF ROUND(AmountInclVAT,Currency."Invoice Rounding Precision",Currency.InvoiceRoundingDirection) <> AmountInclVAT THEN BEGIN
                Field.GET("Template No.",Field.Type::Header,'AMOUNTINCLVAT');

                DocumentComment.Add(Rec,Field,0,DocumentComment.Area::Validation,DocumentComment."Comment Type"::Warning,
                  STRSUBSTNO(InvRoundMsg,Field."Field Name",AmountInclVAT,
                  ROUND(AmountInclVAT,Currency."Invoice Rounding Precision",Currency.InvoiceRoundingDirection),
                  Currency.FIELDCAPTION("Invoice Rounding Precision"),Currency.FIELDCAPTION("Invoice Rounding Type"),CurrCode));
              END;

            // *********************************************************************************************************************************
            // BUILD LINES TABLE AND CHECK THE LINES
            // *********************************************************************************************************************************
            BuildTempLinesTable(DocumentLine);
            IF DocumentLine.ISEMPTY THEN BEGIN
              IF Template."Recognize Lines" = Template."Recognize Lines"::Yes THEN
                DocumentComment.Add(Rec,Field,0,DocumentComment.Area::Validation,DocumentComment."Comment Type"::Warning,Text018);
            END ELSE BEGIN
              DocumentLine.SETRANGE(OK,FALSE);
              IF NOT DocumentLine.ISEMPTY THEN BEGIN
                DocumentComment.Add(Rec,Field,0,DocumentComment.Area::Validation,DocumentComment."Comment Type"::Error,Text002);
                IsValid := FALSE;
              END;

              IF Template."Validate Line Totals" THEN BEGIN
                // Sum all the lines and compare it to the headerfield, Amount Excl. VAT
                DocumentLine.SETRANGE(OK);
                IF DocumentLine.FINDSET(FALSE,FALSE) THEN
                  REPEAT
                    TotalLineAmount := TotalLineAmount + PurchDocMgt.GetLineAmount(Rec,DocumentLine."Line No.");
                  UNTIL DocumentLine.NEXT = 0;

                IF TotalLineAmount <> SubtractedAmountExclVAT THEN BEGIN
                  DocumentComment.Add(Rec,Field,0,DocumentComment.Area::Validation,DocumentComment."Comment Type"::Error,
                    STRSUBSTNO(Text003,DCAppMgt.FormatAmount(TotalLineAmount,DocCurrCode),
                    DCAppMgt.FormatAmount(SubtractedAmountExclVAT,DocCurrCode)));
                  IsValid := FALSE;
                END;
              END;

              // *****************************************************************************************************************************
              // VALIDATE MATCH
              // *****************************************************************************************************************************
              IF "Match Status" <> "Match Status"::"Manually Matched" THEN BEGIN
                IF IsInvoice THEN BEGIN
                  IF Template."Purch. Inv. Reg. Step 1" = Template."Purch. Inv. Reg. Step 1"::"Match Order & Create Invoice" THEN
                    IsValid := IsValid AND ValidateLineMatch(Rec,DocumentLine);
                END ELSE BEGIN
                  IF Template."Purch. Cr.Memo Reg. Step 1" =
                    Template."Purch. Cr.Memo Reg. Step 1"::"Match Return Order & Create Credit Memo"
                  THEN
                    IsValid := IsValid AND ValidateLineMatch(Rec,DocumentLine);
                END;
              END;
            END;

            IsValid := IsValid AND ValidateMatch(Rec,Template);
            IsValid := IsValid AND ValidateLines(Template,Rec,DocumentLine);
            IsValid := IsValid AND ValidateAmtAccounts(Rec,Template);

            IF IsValid <> OK THEN BEGIN
              OK := IsValid;
              MODIFY;
            END;
          END;

  }
  CODE
  {
    VAR
      Text001@1000000000 : TextConst 'DEU=BetrÑge stimmen nicht Åberein.;ENU=Amounts do not match.;NLD=Bedragen komen niet overeen.';
      Text002@1000000001 : TextConst 'DEU=In einer oder mehreren Zeilen sind Fehler vorhanden.;ENU=One or more lines have errors.;NLD=Een of meerdere regels bevatten een fout.';
      Text003@1000000002 : TextConst 'DEU=Die Summer aller Zeilen stimmt nicht mit dem Gesamtbetrag Åberein: %1 - %2;ENU=The sum of all lines (%1) does not reconcile to the total amount (%2).;NLD=De som van alle regels (%1) komt niet overeen met het totaalbedrag (%2).';
      Text004@6085580 : TextConst 'DEU=%1 (%2) in Zeile %3 muss umgerechnet werden.;ENU=%1 (%2) on line %3 must be translated.;NLD=%1 (%2) op regel %3 moet vertaald worden.';
      Text005@1000000004 : TextConst 'DEU=Keine Zeilen erkannt.;ENU=No lines recognized.;NLD=Geen regels herkend.';
      Text006@1000000005 : TextConst 'DEU=Eine oder mehrere Zeilen sind einmalig.;ENU=One or more lines are unmatched.;NLD=EÇn of meer regels hebben geen match.';
      PurchDocMgt@1000000006 : Codeunit 6085709;
      Text007@161024012 : TextConst 'DEU=%1 kann nicht ''%2'' auf %3 %4 sein;ENU=%1 cannot be ''%2'' on %3 %4;NLD=%1 kan niet ''%2'' zijn op %3 %4';
      Text008@161024013 : TextConst 'DEU="WARNUNG: %1 = %2";ENU="WARNING: %1 = %2";NLD="WAARSCHUWING: %1 = %2"';
      Text009@161024015 : TextConst 'DEU=WARNUNG: Zahlungsbedingungen (%1) sind nicht korrekt.;ENU=WARNING: Payment Terms (%1) not correct.;NLD=WAARSCHUWING: Betalingscondities (%1) zijn niet correct.';
      DocNoExistOnEntryMsg@6085573 : TextConst 'DEU="%1 %2 besteht bereits (auf %3, %4 = %5).";ENU="%1 %2 already exists (on %3, %4 = %5).";NLD="%1 %2 bestaat reeds (op %3, %4 = %5)."';
      DocNoExistToOnEntryForPayMsg@6085574 : TextConst 'DEU="%1 %2 besteht bereits (auf %3, %4 = %5 fÅr %6 = %7).";ENU="%1 %2 already exists (on %3, %4 = %5 for %6 = %7).";NLD="%1 %2 bestaat reeds (op %3, %4 = %5 voor %6 = %7)."';
      InvDocNoExistMsg@6085575 : TextConst 'DEU=%1 %2 besteht bereits (auf nicht gebuchter Rechnung %3).;ENU=%1 %2 already exists (on unposted invoice %3).;NLD=%1 %2 bestaat reeds (op ongeboekte factuur %3).';
      CrMemoDocNoExistMsg@6085576 : TextConst 'DEU=%1 %2 besteht bereits (auf nicht gebuchter Gutschrift %3).;ENU=%1 %2 already exists (on unposted credit memo %3).;NLD=%1 %2 bestaat reeds (op ongeboekte creditnota %3).';
      InvDocNoExistForPayToMsg@6085577 : TextConst 'DEU="%1 %2 besteht bereits (auf nicht gebuchter Rechnung %3 fÅr %4 = %5).";ENU="%1 %2 already exists (on unposted invoice %3 for %4 = %5).";NLD="%1 %2 bestaat reeds (op ongeboekte factuur %3 voor %4 = %5)."';
      CrMemoDocNoExistForPayToMsg@6085578 : TextConst 'DEU="%1 %2 besteht bereits (auf nicht gebuchter Gutschrift %3 fÅr %4 = %5).";ENU="%1 %2 already exists (on unposted credit memo %3 for %4 = %5).";NLD="%1 %2 bestaat reeds (op ongeboekte creditnota %3 voor %4 = %5)."';
      Text011@161024016 : TextConst 'DEU="WARNUNG: unterschiedlicher WÑhrungscode. Beleg = ''%1'', Kreditor = ''%2''";ENU="WARNING: Currency Code is different. Document = ''%1'', Vendor = ''%2''";NLD="WAARSCHUWING: Valutacode wijkt af. Document = ''%1'', Leverancier = ''%2''"';
      SumMatchedLinesMsg@161024017 : TextConst 'DEU=Die Summe aller Zeilen stimmt nicht mit dem Gesamtbetrag Åberein.;ENU=The sum of all matched lines (%1) does not reconcile to the total amount (%2).;NLD=De som van alle overeenkomende regels (%1) komt niet overeen met het totaalbedrag (%2).';
      WarningTxtMsg@1160040004 : TextConst 'DEU=WARNUNG:;ENU=WARNING:;NLD=WAARSCHUWING:;SVE=Varning:';
      Text013@161024018 : TextConst 'DEU=Es wurden keine Zeilen zugeordnet und keine zu Åberweisenden BetrÑge konfiguriert.;ENU=No lines have been matched and no amounts has been configured to be transfered.;NLD=Er zijn geen regels gematcht en er zijn geen bedragen geconfigureerd om te verplaatsen.';
      Text014@161024019 : TextConst 'DEU=%1 muss in der Vorlage angegeben werden.;ENU=%1 must be specified on the template.;NLD=%1 moet in het sjabloon voorkomen.';
      Text015@161024020 : TextConst 'DEU=Es wurde kein Konto erstellt fÅr %1.;ENU=No Account has been configured for %1.;NLD=Geen rekening geconfigureerd voor %1.';
      Text016@161024021 : TextConst 'DEU=WARNUNG: in den Feldern werden unterschiedliche Datenformate verwendet.;ENU=WARNING: Different date formats used on fields.;NLD=WAARSCHUWING: Verschillende datumformaten worden gebruikt.';
      Text017@161024022 : TextConst 'DEU=WARNUNG: %1 grî·er als heute auf Gutschrift.;ENU=WARNING: %1 greater than today on credit note.;NLD=WAARSCHUWING: %1 is later dan vandaag op creditnota.';
      Text11012001@1100528201 : TextConst 'DEU=Umsatzsteuernummer ist mîglicherweise nicht korrekt.;ENU=VAT No. is possibly not correct;NLD=BTW-nr. is mogelijk niet juist.';
      Text11012002@1100528200 : TextConst 'DEU=Kontonummer ist nicht korrekt.;ENU=Bank Account No. is not correct;NLD=Bankrekeningnr. is niet correct.';
      Text11012003@1100527000 : TextConst 'DEU=Kontonummer ist nicht korrekt;ENU=B-Account No. is not correct;NLD=G-rekeningnr. is niet correct.';
      Text018@1160040000 : TextConst 'DEU=WARNUNG: Keine Zeilen erkannt.;ENU=WARNING: No lines recognized.;NLD=WAARSCHUWING: Geen regels herkend.';
      MissingCurrencyCodeMsg@1160040002 : TextConst 'DEU=Sie mÅssen %1 angeben, wenn %2 in %3 aktiviert ist.;ENU=You must specify %1 when %2 is activated in %3.;NLD=U moet %1 opgeven wanneer %2 is geactiveerd in %3.';
      FieldIsNegativeMsg@1160040001 : TextConst 'DEU=%1 ist negativ auf der Rechnung.;ENU=%1 is negative on invoice.;NLD=%1 is negatief op de factuur.';
      InvRoundMsg@1160040003 : TextConst 'DEU=WARNUNG: %1 (%2) stimmt nicht Åberein mit dem gerundeten Betrag (%3), berechnet auf %4 und %5, konfiguriert fÅr %6.;ENU=WARNING: %1 (%2) does not match the rounded amount (%3) calculated using %4 and %5 configured for %6.;NLD=WAARSCHUWING: %1 (%2) komt niet overeen met het afgeronde bedrag (%3) berekend op basis van %4 en %5 geconfigureerd voor %6.';
      FoundAccountToLongMsg@6085579 : TextConst 'DEU=Das Account (%1), gefunden von %2 fÅr %3 ist %4 Zeichen lang, aber das Account darf niet mehr als %5 Zeichen enthalten.;ENU=The Account (%1) found from %2 for %3 is %4 characters long but the account must not be more than %5 characters.;NLD=Het account (%1) gevonden van %2 voor %3 is %4 tekens lang, maar het account mag niet meer dan %5 tekens zijn.';

    PROCEDURE ValidateLineMatch@1000000000(VAR Document@1000000004 : Record 6085590;VAR DocumentLine@1000000002 : TEMPORARY Record 6085596) : Boolean;
    VAR
      PurchDocMatch@1000000000 : Record 6085700;
      CaptureMgnt@1000000001 : Codeunit 6085576;
      Field@1000000005 : Record 6085580;
      DocumentComment@1000000009 : Record 6085594;
      Quantity@1000000003 : Decimal;
      UnitCost@1000000006 : Decimal;
      LineAmount@1000000007 : Decimal;
      MatchRequired@1000000008 : Boolean;
    BEGIN
      IF DocumentLine.FINDSET THEN
        REPEAT
          Quantity := PurchDocMgt.GetLineQuantity(Document,DocumentLine."Line No.");
          UnitCost := PurchDocMgt.GetLineUnitCost(Document,DocumentLine."Line No.");
          LineAmount := PurchDocMgt.GetLineAmount(Document,DocumentLine."Line No.");

          MatchRequired := (Quantity <> 0) OR (UnitCost <> 0) OR (LineAmount <> 0);

          IF MatchRequired THEN BEGIN
            PurchDocMatch.SETRANGE("Document No.",DocumentLine."Document No.");
            PurchDocMatch.SETRANGE("Document Line No.",DocumentLine."Line No.");
            IF PurchDocMatch.ISEMPTY THEN BEGIN
              DocumentComment.Add(Document,Field,0,DocumentComment.Area::Validation,DocumentComment."Comment Type"::Error,Text006);
              EXIT(FALSE);
            END;
          END;
        UNTIL DocumentLine.NEXT = 0;

      EXIT(TRUE);
    END;

    PROCEDURE ValidateMatch@161024019(VAR Document@1000000004 : Record 6085590;Template@161024014 : Record 6085579) : Boolean;
    VAR
      CaptureMgnt@1000000001 : Codeunit 6085576;
      DCAppMgt@1160040000 : Codeunit 6085722;
      Field@1000000005 : Record 6085580;
      DocumentComment@1000000009 : Record 6085594;
      AmountToMatch@161024015 : Decimal;
      MatchedAmount@161024017 : Decimal;
      IsInvalid@161024012 : Boolean;
      DocCurrCode@1160040001 : Code[20];
    BEGIN
      IF PurchDocMgt.IsDocMatched(Document) THEN BEGIN
        MatchedAmount := PurchDocMgt.GetDocMatchedAmount(Document);
        AmountToMatch := PurchDocMgt.GetAmountToMatch(Document);
        DocCurrCode := PurchDocMgt.GetCurrencyCode(Document);

        IF NOT PurchDocMgt.MatchWithinTolerance(Template,PurchDocMgt.GetPurchPostingDate(Document,Template),DocCurrCode,0,AmountToMatch,
          MatchedAmount)
        THEN BEGIN
          IF Document."Match Status" <> Document."Match Status"::"Manually Matched" THEN BEGIN
            DocumentComment.Add(Document,Field,0,DocumentComment.Area::Validation,DocumentComment."Comment Type"::Error,
              STRSUBSTNO(SumMatchedLinesMsg,DCAppMgt.FormatAmount(MatchedAmount,DocCurrCode),
              DCAppMgt.FormatAmount(AmountToMatch,DocCurrCode)));
            IsInvalid := TRUE;
          END ELSE
            DocumentComment.Add(Document,Field,0,DocumentComment.Area::Validation,DocumentComment."Comment Type"::Warning,
              STRSUBSTNO('%1 %2',WarningTxtMsg,STRSUBSTNO(SumMatchedLinesMsg,DCAppMgt.FormatAmount(MatchedAmount,DocCurrCode),
              DCAppMgt.FormatAmount(AmountToMatch,DocCurrCode))));
        END ELSE
          IF AmountToMatch <> MatchedAmount THEN BEGIN
            IF Template."Variance Posting Account" = '' THEN BEGIN
              DocumentComment.Add(Document,Field,0,DocumentComment.Area::Validation,DocumentComment."Comment Type"::Error,
                STRSUBSTNO(Text014,Template.FIELDCAPTION("Variance Posting Account")));
              IsInvalid := TRUE;
            END;
          END;
      END;

      EXIT(NOT IsInvalid);
    END;

    PROCEDURE ValidateLines@6085573(Template@1160040005 : Record 6085579;VAR Document@1000000004 : Record 6085590;VAR DocumentLine@1160040000 : TEMPORARY Record 6085596) : Boolean;
    VAR
      Field@1000000005 : Record 6085580;
      DocumentComment@1000000009 : Record 6085594;
      LineTransl@1160040003 : Record 6085584;
      CaptureMgnt@1160040002 : Codeunit 6085576;
      No@1160040001 : Code[50];
      Quantity@1000000003 : Decimal;
      UnitCost@1000000006 : Decimal;
      LineAmount@1000000007 : Decimal;
      MatchRequired@1000000008 : Boolean;
      IsValid@1160040007 : Boolean;
    BEGIN
      IF PurchDocMgt.IsDocMatched(Document) THEN
        EXIT(TRUE);

      IsValid := TRUE;

      IF DocumentLine.FINDSET THEN BEGIN
        Field.GET(Template."No.",Field.Type::Line,'NO');
        REPEAT
          No := PurchDocMgt.GetLineAccountNo(Document,DocumentLine."Line No.");
          Quantity := PurchDocMgt.GetLineQuantity(Document,DocumentLine."Line No.");
          UnitCost := PurchDocMgt.GetLineUnitCost(Document,DocumentLine."Line No.");
          LineAmount := PurchDocMgt.GetLineAmount(Document,DocumentLine."Line No.");

          MatchRequired := (Quantity <> 0) OR (UnitCost <> 0) OR (LineAmount <> 0);

          IF MatchRequired THEN
            IF NOT PurchDocMgt.GetTranslLineInfo2(Document,DocumentLine."Line No.",LineTransl) THEN BEGIN
              DocumentComment.Add(Document,Field,DocumentLine."Line No.",DocumentComment.Area::Validation,
                DocumentComment."Comment Type"::Error,STRSUBSTNO(Text004,Field."Field Name",No,DocumentLine."Line No."));
              IsValid := FALSE;
            END;
        UNTIL DocumentLine.NEXT = 0;
      END;
      EXIT(IsValid);
    END;

    PROCEDURE ValidateAmtAccounts@161024021(VAR Document@1000000004 : Record 6085590;Template@161024014 : Record 6085579) : Boolean;
    VAR
      PurchDocMatch@1000000000 : Record 6085700;
      DataTransl@161024019 : Record 6085584;
      Field@1000000005 : Record 6085580;
      TemplField@161024013 : Record 6085580;
      DocumentComment@1000000009 : Record 6085594;
      Value@161024020 : Record 6085593;
      CaptureMgnt@161024021 : Codeunit 6085576;
      Amount@161024016 : Decimal;
      MatchedAmount@161024017 : Decimal;
      IsInvalid@161024012 : Boolean;
      AccountNo@161024018 : Code[20];
      TempAccountNo@6085573 : Code[1024];
      LinesCaptured@161024015 : Boolean;
    BEGIN
      Value.SETCURRENTKEY("Document No.","Is Value",Type,"Line No.");
      Value.SETRANGE("Document No.",Document."No.");
      Value.SETRANGE("Is Value",TRUE);
      Value.SETRANGE(Type,Value.Type::Line);
      LinesCaptured := NOT Value.ISEMPTY;

      PurchDocMatch.SETRANGE("Document No.",Document."No.");

      Field.SETRANGE("Template No.",Document."Template No.");
      Field.SETRANGE(Type,Field.Type::Header);
      Field.SETRANGE("Data Type",Field."Data Type"::Number);
      IF (PurchDocMatch.ISEMPTY) AND (NOT LinesCaptured) THEN
        Field.SETFILTER("Transfer Amount to Document",'<>%1',Field."Transfer Amount to Document"::" ")
      ELSE
        Field.SETRANGE("Transfer Amount to Document",Field."Transfer Amount to Document"::Always);
      Field.SETRANGE("Subtract from Amount Field",'');

      IF NOT Template."Allow Register without Amounts" THEN BEGIN
        IF (NOT Field.FINDFIRST) AND (PurchDocMatch.ISEMPTY) AND (NOT LinesCaptured) THEN BEGIN
          DocumentComment.Add(Document,Field,0,DocumentComment.Area::Validation,DocumentComment."Comment Type"::Error,Text013);
          IsInvalid := TRUE;
        END;
      END;

      REPEAT
        Amount := CaptureMgnt.GetDecimal(Document,Field.Type,Field.Code,0);
        IF Amount <> 0 THEN BEGIN
          AccountNo := '';

          IF Field."G/L Account Field Code" <> '' THEN
            IF (TemplField.GET(Field."Template No.",Field.Type,Field."G/L Account Field Code")) THEN BEGIN
              TempAccountNo := CaptureMgnt.GetValueAsText(Document."No.",0,TemplField);
              IF STRLEN(TempAccountNo) <= MAXSTRLEN(AccountNo) THEN
                AccountNo := TempAccountNo
              ELSE
                DocumentComment.Add(Document,Field,0,DocumentComment.Area::Validation,DocumentComment."Comment Type"::Error,
                  STRSUBSTNO(FoundAccountToLongMsg,TempAccountNo,TemplField."Field Name",Field."Field Name",STRLEN(TempAccountNo),
                    MAXSTRLEN(AccountNo)));
            END;

          IF AccountNo = '' THEN
            IF DataTransl.GET(Document."Template No.",Field.Type,Field.Code) THEN
              AccountNo := DataTransl."Translate to No.";

          IF (AccountNo = '') AND (NOT Template."Allow Register without Amounts") THEN BEGIN
            DocumentComment.Add(Document,Field,0,DocumentComment.Area::Validation,DocumentComment."Comment Type"::Error,
              STRSUBSTNO(Text015,Field."Field Name"));
            IsInvalid := TRUE;
          END;
        END;
      UNTIL Field.NEXT = 0;

      EXIT(NOT IsInvalid);
    END;

    BEGIN
    {
      This codeunit validates the full purchase document
    }
    END.
  }
}

