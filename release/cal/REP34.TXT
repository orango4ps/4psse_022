OBJECT Report 34 Change Payment Tolerance
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    Permissions=TableData 4=rm,
                TableData 21=rm,
                TableData 25=rm,
                TableData 98=rm;
    CaptionML=[DEU=Zahlungstoleranz Ñndern;
               ENU=Change Payment Tolerance;
               NLD=Betalingstolerantie wijzigen;
               NOR=Endre betalingstoleranse];
    ProcessingOnly=Yes;
    OnPostReport=VAR
                   ConfirmManagement@1000 : Codeunit 27;
                 BEGIN
                   IF AllCurrencies THEN BEGIN
                     IF Currency.FIND('-') THEN
                       REPEAT
                         IF Currency."Payment Tolerance %" <> PaymentTolerancePct THEN
                           Currency."Payment Tolerance %" := PaymentTolerancePct;
                         IF Currency."Max. Payment Tolerance Amount" <> MaxPmtToleranceAmount THEN
                           Currency."Max. Payment Tolerance Amount" := MaxPmtToleranceAmount;
                         Currency."Max. Payment Tolerance Amount" := ROUND(
                             Currency."Max. Payment Tolerance Amount",Currency."Amount Rounding Precision");
                         Currency.MODIFY;
                       UNTIL Currency.NEXT = 0;
                     GLSetup.GET;
                     IF GLSetup."Payment Tolerance %" <> PaymentTolerancePct THEN
                       GLSetup."Payment Tolerance %" := PaymentTolerancePct;
                     IF GLSetup."Max. Payment Tolerance Amount" <> MaxPmtToleranceAmount THEN
                       GLSetup."Max. Payment Tolerance Amount" := MaxPmtToleranceAmount;
                     GLSetup."Max. Payment Tolerance Amount" := ROUND(
                         GLSetup."Max. Payment Tolerance Amount",GLSetup."Amount Rounding Precision");
                     GLSetup.MODIFY;
                   END ELSE
                     IF CurrencyCode = '' THEN BEGIN
                       GLSetup.GET;
                       AmountRoundingPrecision := GLSetup."Amount Rounding Precision";
                       IF GLSetup."Payment Tolerance %" <> PaymentTolerancePct THEN
                         GLSetup."Payment Tolerance %" := PaymentTolerancePct;
                       IF GLSetup."Max. Payment Tolerance Amount" <> MaxPmtToleranceAmount THEN
                         GLSetup."Max. Payment Tolerance Amount" := MaxPmtToleranceAmount;
                       GLSetup."Max. Payment Tolerance Amount" := ROUND(
                           GLSetup."Max. Payment Tolerance Amount",GLSetup."Amount Rounding Precision");
                       GLSetup.MODIFY;
                     END ELSE
                       IF CurrencyCode <> '' THEN BEGIN
                         Currency.GET(CurrencyCode);
                         AmountRoundingPrecision := Currency."Amount Rounding Precision";
                         IF Currency."Payment Tolerance %" <> PaymentTolerancePct THEN
                           Currency."Payment Tolerance %" := PaymentTolerancePct;
                         IF Currency."Max. Payment Tolerance Amount" <> MaxPmtToleranceAmount THEN
                           Currency."Max. Payment Tolerance Amount" := MaxPmtToleranceAmount;
                         Currency."Max. Payment Tolerance Amount" := ROUND(
                             Currency."Max. Payment Tolerance Amount",Currency."Amount Rounding Precision");
                         Currency.MODIFY;
                       END;

                   IF AllCurrencies THEN BEGIN
                     IF ConfirmManagement.ConfirmProcess(Text001,TRUE) THEN BEGIN
                       IF Currency.FIND('-') THEN
                         REPEAT
                           AmountRoundingPrecision := Currency."Amount Rounding Precision";
                           CurrencyCode := Currency.Code;
                           ChangeCustLedgEntries;
                           ChangeVendLedgEntries;
                         UNTIL Currency.NEXT = 0;
                       CurrencyCode := '';
                       GLSetup.GET;
                       AmountRoundingPrecision := GLSetup."Amount Rounding Precision";
                       ChangeCustLedgEntries;
                       ChangeVendLedgEntries;
                     END;
                   END ELSE
                     IF ConfirmManagement.ConfirmProcess(Text001,TRUE) THEN BEGIN
                       ChangeCustLedgEntries;
                       ChangeVendLedgEntries;
                     END;
                 END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnInit=BEGIN
               CurrencyCodeEnable := TRUE;
             END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 7   ;2   ;Field     ;
                  CaptionML=[DEU=Alle WÑhrungen;
                             ENU=All Currencies;
                             NLD=Alle valuta's;
                             NOR=Alle valutaer];
                  ToolTipML=[DEU=Gibt an, ob Sie die Einrichtung der Toleranz sowohl fÅr die LandeswÑhrung als auch fÅr FremdwÑhrungen Ñndern mîchten.;
                             ENU=Specifies if you want to change the tolerance setup for both local and all foreign currencies.;
                             NLD=Hiermee wordt opgegeven of u de tolerantie-instellingen voor zowel de lokale als alle vreemde valuta's wilt wijzigen.;
                             NOR=Angir om du vil endre toleranseoppsett for bÜde lokal og fremmed valuta.];
                  ApplicationArea=#Suite;
                  SourceExpr=AllCurrencies;
                  OnValidate=BEGIN
                               IF AllCurrencies THEN BEGIN
                                 CurrencyCode := '';
                                 PaymentTolerancePct := 0;
                                 MaxPmtToleranceAmount := 0;
                                 CurrencyCodeEnable := FALSE;
                               END ELSE BEGIN
                                 CurrencyCodeEnable := TRUE;
                                 CurrencyCode := '';
                                 PaymentTolerancePct := GLSetup."Payment Tolerance %";
                                 MaxPmtToleranceAmount := GLSetup."Max. Payment Tolerance Amount";
                                 DecimalPlaces := CheckApplnRounding(GLSetup."Amount Decimal Places");
                               END;
                             END;
                              }

      { 1   ;2   ;Field     ;
                  Name=Currency Code;
                  CaptionML=[DEU=WÑhrungscode;
                             ENU=Currency Code;
                             NLD=Valutacode;
                             NOR=Valutakode;
                             SVE=Valutakod];
                  ToolTipML=[DEU=Gibt den Code fÅr die WÑhrung an, in der die BetrÑge angezeigt werden.;
                             ENU=Specifies the code for the currency that amounts are shown in.;
                             NLD=Hiermee wordt de valutacode opgegeven waarin bedragen worden weergegeven.;
                             NOR=Angir koden for valutaen som belõpene vises i.;
                             SVE=Anger i vilken valuta beloppen visas.];
                  ApplicationArea=#Suite;
                  SourceExpr=CurrencyCode;
                  TableRelation=Currency;
                  Enabled=CurrencyCodeEnable;
                  OnValidate=BEGIN
                               IF NOT AllCurrencies THEN
                                 IF CurrencyCode = '' THEN BEGIN
                                   GLSetup.GET;
                                   PaymentTolerancePct := GLSetup."Payment Tolerance %";
                                   MaxPmtToleranceAmount := GLSetup."Max. Payment Tolerance Amount";
                                   DecimalPlaces := CheckApplnRounding(GLSetup."Amount Decimal Places");
                                 END ELSE BEGIN
                                   Currency.GET(CurrencyCode);
                                   PaymentTolerancePct := Currency."Payment Tolerance %";
                                   MaxPmtToleranceAmount := Currency."Max. Payment Tolerance Amount";
                                   DecimalPlaces := CheckApplnRounding(Currency."Amount Decimal Places");
                                 END;
                             END;

                  OnLookup=BEGIN
                             Currencies.LOOKUPMODE := TRUE;
                             IF Currencies.RUNMODAL = ACTION::LookupOK THEN
                               Currencies.GetCurrency(CurrencyCode);
                             CLEAR(Currencies);
                             IF CurrencyCode = '' THEN BEGIN
                               GLSetup.GET;
                               PaymentTolerancePct := GLSetup."Payment Tolerance %";
                               MaxPmtToleranceAmount := GLSetup."Max. Payment Tolerance Amount";
                             END ELSE BEGIN
                               Currency.GET(CurrencyCode);
                               PaymentTolerancePct := Currency."Payment Tolerance %";
                               MaxPmtToleranceAmount := Currency."Max. Payment Tolerance Amount";
                             END;
                           END;
                            }

      { 3   ;2   ;Field     ;
                  CaptionML=[DEU=Zahlungstoleranz %;
                             ENU=Payment Tolerance %;
                             NLD=Betalingstolerantie %;
                             NOR=Betalingstoleranseprosent;
                             SVE=Betalningstolerans %];
                  ToolTipML=[DEU=Gibt den Prozentsatz an, um den die Zahlung oder die Erstattung von dem Betrag auf der Rechnung oder der Gutschrift abweichen darf.;
                             ENU=Specifies the percentage by which the payment or refund is allowed to be less than the amount on the invoice or credit memo.;
                             NLD=Hiermee wordt het percentage opgegeven dat de betaling of terugbetaling lager mag zijn dan het bedrag op de factuur of creditnota.;
                             NOR=Angir prosentsatsen som betalingen eller refusjonen kan vëre mindre enn, i forhold til belõpet pÜ fakturaen eller kreditnotaen.];
                  ApplicationArea=#Basic,#Suite;
                  DecimalPlaces=0:5;
                  SourceExpr=PaymentTolerancePct;
                  Enabled=TRUE }

      { 5   ;2   ;Field     ;
                  Name=Max. Pmt. Tolerance Amount;
                  CaptionML=[DEU=Max. Zahlungstoleranzbetrag;
                             ENU=Max. Pmt. Tolerance Amount;
                             NLD=Max. betalingstolerantiebedrag;
                             NOR=Maks. betalingstoleransebelõp];
                  ToolTipML=[DEU=Gibt den maximalen Betrag an, um den die Zahlung oder die Erstattung von dem Betrag auf der Rechnung oder der Gutschrift abweichen darf.;
                             ENU=Specifies the maximum allowed amount by which the payment or refund can differ from the amount on the invoice or credit memo.;
                             NLD=Hiermee wordt het maximaal toegestane bedrag opgegeven dat de betaling of terugbetaling mag verschillen van het bedrag op de factuur of creditnota.;
                             NOR=Angir det maksimalt tillatte belõpet som betalingen eller refusjonen kan avvike fra belõpet pÜ fakturaen eller kreditnotaen.];
                  ApplicationArea=#Basic,#Suite;
                  DecimalPlaces=0:5;
                  SourceExpr=MaxPmtToleranceAmount;
                  Enabled=TRUE;
                  OnValidate=BEGIN
                               IF AllCurrencies THEN BEGIN
                                 DecimalPlaces := 5;
                                 FormatString := Text002 + '0:5' + Text003;
                               END ELSE
                                 IF Currency.Code <> '' THEN BEGIN
                                   Currency.GET(Currency.Code);
                                   DecimalPlaces := CheckApplnRounding(Currency."Amount Decimal Places");
                                   FormatString := Text002 + Currency."Amount Decimal Places" + Text003;
                                 END ELSE BEGIN
                                   GLSetup.GET;
                                   DecimalPlaces := CheckApplnRounding(GLSetup."Amount Decimal Places");
                                   FormatString := Text002 + GLSetup."Amount Decimal Places" + Text003;
                                 END;
                               TextFormat := FORMAT(MaxPmtToleranceAmount,0,FormatString);
                               TextInput := FORMAT(MaxPmtToleranceAmount);
                               IF STRLEN(TextFormat) < STRLEN(TextInput) THEN
                                 ERROR(Text004,DecimalPlaces);
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Currency@1003 : Record 4;
      GLSetup@1004 : Record 98;
      Currencies@1006 : Page 5;
      CurrencyCode@1007 : Code[10];
      PaymentTolerancePct@1001 : Decimal;
      MaxPmtToleranceAmount@1000 : Decimal;
      Text001@1005 : TextConst 'DEU=Mîchten Sie fÅr jeden nicht gesperrten Debitor und Kreditor alle offenen Posten Ñndern?;ENU=Do you want to change all open entries for every customer and vendor that are not blocked?;NLD=Wilt u alle openstaande posten wijzigen voor elke klant en leverancier die niet geblokkeerd is?;NOR=Vil du endre alle de Üpne postene for hver kunde og leverandõr som ikke er sperret?;SVE=Vill du Ñndra alla îppna transaktioner fîr varje kund och leverantîr som inte Ñr spÑrrad?';
      AmountRoundingPrecision@1008 : Decimal;
      DecimalPlaces@1010 : Integer;
      AllCurrencies@1009 : Boolean;
      FormatString@1014 : Text[80];
      TextFormat@1015 : Text[250];
      TextInput@1016 : Text[250];
      Text002@1012 : TextConst 'DEU=<Precision,;ENU=<Precision,;NLD=<Precision,;NOR=<Precision,;SVE=<Precision,';
      Text003@1013 : TextConst 'DEU=><Standard Format,0>;ENU=><Standard Format,0>;NLD=><Standard Format,0>;NOR=><Standard Format,0>;SVE=><Standard Format,0>';
      Text004@1017 : TextConst 'DEU=In diesem Feld kînnen Sie maximal %1 Nachkommastellen eingeben.;ENU=The field can have a maximum of %1 decimal places.;NLD=Het veld mag maximaal %1 decimalen bevatten.;NOR=Feltet kan ha opptil %1 desimaler.';
      CurrencyCodeEnable@19025310 : Boolean INDATASET;

    LOCAL PROCEDURE CheckApplnRounding@4(AmountDecimalPlaces@1000 : Text[5]) : Integer;
    VAR
      ColonPlace@1001 : Integer;
      ReturnNumber@1002 : Integer;
      OK@1003 : Boolean;
      TempAmountDecimalPlaces@1004 : Text[5];
    BEGIN
      ColonPlace := STRPOS(AmountDecimalPlaces,':');

      IF ColonPlace = 0 THEN BEGIN
        OK := EVALUATE(ReturnNumber,AmountDecimalPlaces);
        IF OK THEN
          EXIT(ReturnNumber);
      END ELSE BEGIN
        TempAmountDecimalPlaces := COPYSTR(AmountDecimalPlaces,ColonPlace + 1,ColonPlace + 1);
        OK := EVALUATE(ReturnNumber,TempAmountDecimalPlaces);
        IF OK THEN
          EXIT(ReturnNumber);
      END;
    END;

    LOCAL PROCEDURE ChangeCustLedgEntries@1();
    VAR
      Customer@1001 : Record 18;
      CustLedgEntry@1000 : Record 21;
      NewPaymentTolerancePct@1003 : Decimal;
      NewMaxPmtToleranceAmount@1002 : Decimal;
    BEGIN
      Customer.SETCURRENTKEY("No.");
      Customer.LOCKTABLE;
      IF NOT Customer.FIND('-')THEN
        EXIT;

      REPEAT
        IF NOT Customer."Block Payment Tolerance" THEN BEGIN
          CustLedgEntry.SETCURRENTKEY("Customer No.",Open);
          CustLedgEntry.SETRANGE("Customer No.",Customer."No.");
          CustLedgEntry.SETRANGE(Open,TRUE);

          CustLedgEntry.SETFILTER("Document Type",'%1|%2',
            CustLedgEntry."Document Type"::Invoice,
            CustLedgEntry."Document Type"::"Credit Memo");

          CustLedgEntry.SETRANGE("Currency Code",CurrencyCode);
          NewPaymentTolerancePct := PaymentTolerancePct;
          NewMaxPmtToleranceAmount := MaxPmtToleranceAmount;

          CustLedgEntry.LOCKTABLE;
          IF CustLedgEntry.FIND('-') THEN BEGIN
            REPEAT
              CustLedgEntry.CALCFIELDS("Remaining Amount");
              CustLedgEntry."Max. Payment Tolerance" :=
                ROUND(NewPaymentTolerancePct * CustLedgEntry."Remaining Amount" / 100,AmountRoundingPrecision);
              IF (CustLedgEntry."Max. Payment Tolerance" = 0) AND
                 (NewMaxPmtToleranceAmount <> 0) OR
                 ((ABS(CustLedgEntry."Max. Payment Tolerance") > NewMaxPmtToleranceAmount) AND
                  (CustLedgEntry."Max. Payment Tolerance" <> 0) AND
                  (NewMaxPmtToleranceAmount <> 0))
              THEN BEGIN
                IF CustLedgEntry."Document Type" = CustLedgEntry."Document Type"::Invoice THEN
                  CustLedgEntry."Max. Payment Tolerance" :=
                    ROUND(NewMaxPmtToleranceAmount,AmountRoundingPrecision)
                ELSE
                  CustLedgEntry."Max. Payment Tolerance" :=
                    ROUND(-NewMaxPmtToleranceAmount,AmountRoundingPrecision);
              END;
              IF ABS(CustLedgEntry."Remaining Amount") < ABS(CustLedgEntry."Max. Payment Tolerance") THEN
                CustLedgEntry."Max. Payment Tolerance" := CustLedgEntry."Remaining Amount";
              CustLedgEntry.MODIFY;
            UNTIL CustLedgEntry.NEXT = 0;
          END;
        END;
      UNTIL Customer.NEXT = 0;
    END;

    LOCAL PROCEDURE ChangeVendLedgEntries@2();
    VAR
      Vendor@1001 : Record 23;
      VendLedgEntry@1000 : Record 25;
      NewPaymentTolerancePct@1003 : Decimal;
      NewMaxPmtToleranceAmount@1002 : Decimal;
    BEGIN
      Vendor.SETCURRENTKEY("No.");
      Vendor.LOCKTABLE;
      IF NOT Vendor.FIND('-')THEN
        EXIT;
      REPEAT
        IF NOT Vendor."Block Payment Tolerance" THEN BEGIN
          VendLedgEntry.SETCURRENTKEY("Vendor No.",Open);
          VendLedgEntry.SETRANGE("Vendor No.",Vendor."No.");

          VendLedgEntry.SETRANGE(Open,TRUE);

          VendLedgEntry.SETFILTER("Document Type",'%1|%2',
            VendLedgEntry."Document Type"::Invoice,
            VendLedgEntry."Document Type"::"Credit Memo");

          VendLedgEntry.SETRANGE("Currency Code",CurrencyCode);
          NewPaymentTolerancePct := PaymentTolerancePct;
          NewMaxPmtToleranceAmount := MaxPmtToleranceAmount;

          VendLedgEntry.LOCKTABLE;
          IF VendLedgEntry.FIND('-') THEN BEGIN
            REPEAT
              VendLedgEntry.CALCFIELDS("Remaining Amount");
              VendLedgEntry."Max. Payment Tolerance" :=
                ROUND(NewPaymentTolerancePct * VendLedgEntry."Remaining Amount" / 100,AmountRoundingPrecision);
              IF (VendLedgEntry."Max. Payment Tolerance" = 0) AND
                 (NewMaxPmtToleranceAmount <> 0) OR
                 ((ABS(VendLedgEntry."Max. Payment Tolerance") > NewMaxPmtToleranceAmount) AND
                  (VendLedgEntry."Max. Payment Tolerance" <> 0) AND
                  (NewMaxPmtToleranceAmount <> 0))
              THEN BEGIN
                IF VendLedgEntry."Document Type" = VendLedgEntry."Document Type"::Invoice THEN
                  VendLedgEntry."Max. Payment Tolerance" :=
                    ROUND(-NewMaxPmtToleranceAmount,AmountRoundingPrecision)
                ELSE
                  VendLedgEntry."Max. Payment Tolerance" :=
                    ROUND(NewMaxPmtToleranceAmount,AmountRoundingPrecision);
              END;
              IF ABS(VendLedgEntry."Remaining Amount") < ABS(VendLedgEntry."Max. Payment Tolerance") THEN
                VendLedgEntry."Max. Payment Tolerance" := VendLedgEntry."Remaining Amount";
              VendLedgEntry.MODIFY;
            UNTIL VendLedgEntry.NEXT = 0;
          END;
        END;
      UNTIL Vendor.NEXT = 0;
    END;

    [External]
    PROCEDURE SetCurrency@3(NewCurrency@1000 : Record 4);
    BEGIN
      PageSetCurrency(NewCurrency);
      EXIT;
    END;

    LOCAL PROCEDURE PageSetCurrency@19033490(NewCurrency@1000 : Record 4);
    BEGIN
      Currency := NewCurrency;

      IF Currency.Code <> ''THEN BEGIN
        Currency.GET(Currency.Code);
        CurrencyCode := Currency.Code;
        PaymentTolerancePct := Currency."Payment Tolerance %";
        MaxPmtToleranceAmount := Currency."Max. Payment Tolerance Amount";
        DecimalPlaces := CheckApplnRounding(Currency."Amount Decimal Places");
      END ELSE BEGIN
        GLSetup.GET;
        PaymentTolerancePct := GLSetup."Payment Tolerance %";
        MaxPmtToleranceAmount := GLSetup."Max. Payment Tolerance Amount";
        DecimalPlaces := CheckApplnRounding(GLSetup."Amount Decimal Places");
      END;
    END;

    [External]
    PROCEDURE InitializeRequest@5(AllCurrenciesFrom@1000 : Boolean;CurrencyCodeFrom@1001 : Code[10];PaymentTolerancePctFrom@1002 : Decimal;MaxPmtToleranceAmountFrom@1003 : Decimal);
    BEGIN
      AllCurrencies := AllCurrenciesFrom;
      CurrencyCode := CurrencyCodeFrom;
      PaymentTolerancePct := PaymentTolerancePctFrom;
      MaxPmtToleranceAmount := MaxPmtToleranceAmountFrom;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

