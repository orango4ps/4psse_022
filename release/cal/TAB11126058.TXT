OBJECT Table 11126058 Proj. Hr. StMng. Empl. Sel.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ERROR('');  //* Only indirect insert allowed (so no manual insert by the user)
             END;

    OnRename=BEGIN
               ERROR('');
             END;

    CaptionML=[ENU=Project Hour Acc. Site Manager - Employee Selection;
               NOR=Prosjekttimekonto byggeleder - Valg av ansatte;
               SVE=Arbetsledare, ack. projekttimmar Õ Val av anstÑlld];
  }
  FIELDS
  {
    { 10  ;   ;Year                ;Integer       ;CaptionML=[ENU=Year;
                                                              NOR=èr;
                                                              SVE=èr];
                                                   Editable=No }
    { 20  ;   ;Week                ;Integer       ;CaptionML=[ENU=Week;
                                                              NOR=Uke;
                                                              SVE=Vecka];
                                                   MinValue=1;
                                                   MaxValue=53;
                                                   Editable=No }
    { 25  ;   ;Receiving Company   ;Text50        ;TableRelation="Site Mngmnt. Hours Companies";
                                                   CaptionML=[ENU=Receiving Company;
                                                              NOR=Mottakselskap;
                                                              SVE=Mottagningsfîretag] }
    { 30  ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr.;
                                                              SVE=Projektnr];
                                                   Editable=No }
    { 40  ;   ;Company             ;Text50        ;TableRelation=Company.Name;
                                                   CaptionML=[ENU=Company;
                                                              NOR=Selskap;
                                                              SVE=Fîretag];
                                                   Editable=No }
    { 50  ;   ;Employee No.        ;Code20        ;TableRelation="Site Mngmnt. Hours Employee".No. WHERE (Company=FIELD(Company));
                                                   CaptionML=[ENU=Employee No.;
                                                              NOR=Ansattnr;
                                                              SVE=AnstÑllningsnr];
                                                   Editable=No }
    { 60  ;   ;Employee Name       ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Site Mngmnt. Hours Employee"."Full Name" WHERE (Company=FIELD(Company),
                                                                                                                       No.=FIELD(Employee No.)));
                                                   CaptionML=[ENU=Employee Name;
                                                              NOR=Navn pÜ ansatt;
                                                              SVE=Namn pÜ anstÑlld];
                                                   Editable=No }
    { 61  ;   ;Search Name         ;Code100       ;CaptionML=[ENU=Search Name;
                                                              NOR=Sõkenavn;
                                                              SVE=Sîknamn];
                                                   Editable=No }
    { 62  ;   ;External            ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Site Mngmnt. Hours Employee".External WHERE (Company=FIELD(Company),
                                                                                                                    No.=FIELD(Employee No.)));
                                                   CaptionML=[ENU=External;
                                                              NOR=Eksternt;
                                                              SVE=Externt];
                                                   Editable=No }
    { 80  ;   ;Select              ;Boolean       ;CaptionML=[ENU=Select;
                                                              NOR=Velg;
                                                              SVE=VÑlj] }
  }
  KEYS
  {
    {    ;Year,Week,Receiving Company,Project No.,Company,Employee No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1210190000 : TextConst 'ENU=Building %1 #2###############;NOR=Bygning %1 #2###############;SVE=Byggnad  %1 #2###############';
      Text002@1210190001 : TextConst 'ENU=In week %1-%2 already booked on %3 ''%4'' of %5 ''%6'', this must be modified/deleted first.;NOR=I uke %1-%2 er det allerede bestilt for %3''%4'' av %5 ''%6'', dette mÜ modifiseres/slettes fõrst.;SVE=I vecka %1-%2 redan bokad fîr %3 ''%4'' av %5 ''%6''. Detta mÜste fîrst Ñndras/tas bort.';
      SiteMgtHrSetup@1210190002 : Record 11012355;

    PROCEDURE BuildAllowedEmployees@1100525003(YearNo@1100525002 : Integer;WeekNo@1100525003 : Integer;ReceivingCompany@1210190001 : Text[50];ProjectNo@1100525001 : Code[20];EmplCompany@1210190000 : Text[50]);
    VAR
      SiteMgtProjHrsHead@1210190002 : Record 11012362;
      SiteMgtEmpl@1100525000 : Record 11012357;
      ProjHrEmplSel@1100525004 : Record 11126058;
      Window@1100525007 : Dialog;
    BEGIN
      //* Build in a temporary table, so in the calling process (form) 'Rec' must be temporary.
      Window.OPEN(STRSUBSTNO(Text001, FIELDCAPTION("Employee No.")));
      RESET;
      DELETEALL;
      IF (YearNo = 0) OR (WeekNo = 0) OR (ReceivingCompany = '') OR (ProjectNo = '') THEN
        EXIT;
      IF NOT SiteMgtProjHrsHead.GET(YearNo, WeekNo, ReceivingCompany, ProjectNo) THEN
        EXIT;
      IF EmplCompany = '' THEN
        EmplCompany := COMPANYNAME;

      IF SiteMgtHrSetup.CheckPostingInWeekAllowed(YearNo, WeekNo, ReceivingCompany, FALSE) THEN BEGIN
        SiteMgtEmpl.RESET;
        SiteMgtEmpl.SETRANGE(Company, EmplCompany);
        SiteMgtEmpl.SETFILTER("No.", '<>%1', '');
        SiteMgtEmpl.SETRANGE("Account Hours",
          SiteMgtEmpl."Account Hours"::"Required according to Norm", SiteMgtEmpl."Account Hours"::Optional);
        IF SiteMgtEmpl.FINDSET THEN BEGIN
          REPEAT
            Window.UPDATE(2, SiteMgtEmpl."No.");
            IF NOT GET(YearNo, WeekNo, ReceivingCompany, ProjectNo, SiteMgtEmpl.Company, SiteMgtEmpl."No.") THEN BEGIN
              INIT;
              Year := YearNo;
              Week := WeekNo;
              "Receiving Company" := ReceivingCompany;
              "Project No." := ProjectNo;
              Company := SiteMgtEmpl.Company;
              "Employee No." := SiteMgtEmpl."No.";
              "Search Name" := SiteMgtEmpl."Search Name";
              IF ProjHrEmplSel.GET(Year, Week, "Receiving Company", "Project No.", Company, "Employee No.") THEN
                Select := TRUE;
              INSERT;
            END;
          UNTIL SiteMgtEmpl.NEXT = 0;
        END;
      END;

      //* Insert already selected employees (if not present yet), also if from another company then the selected company.
      ProjHrEmplSel.RESET;
      ProjHrEmplSel.SETRANGE(Year, YearNo);
      ProjHrEmplSel.SETRANGE(Week, WeekNo);
      ProjHrEmplSel.SETRANGE("Receiving Company", ReceivingCompany);
      ProjHrEmplSel.SETRANGE("Project No.", ProjectNo);
      IF ProjHrEmplSel.FINDSET THEN BEGIN
        REPEAT
          Window.UPDATE(2, ProjHrEmplSel."Employee No.");
          IF NOT GET(YearNo, WeekNo, ReceivingCompany, ProjectNo, ProjHrEmplSel.Company, ProjHrEmplSel."Employee No.") THEN BEGIN
            INIT;
            Year := YearNo;
            Week := WeekNo;
            "Receiving Company" := ReceivingCompany;
            "Project No." := ProjectNo;
            Company := ProjHrEmplSel.Company;
            "Employee No." :=  ProjHrEmplSel."Employee No.";
            "Search Name" := ProjHrEmplSel."Search Name";
            Select := TRUE;
            INSERT;
          END;
        UNTIL ProjHrEmplSel.NEXT = 0;
      END;

      Window.CLOSE;
    END;

    PROCEDURE OnSelect@1210190002();
    VAR
      ProjHrEmplSel@1210190000 : Record 11126058;
      SiteMgtProjHrLine@1210190001 : Record 11012363;
      EmptyLine@1210190002 : Boolean;
    BEGIN
      //* Update from temporary table to 'normal' table
      IF Select THEN BEGIN
        IF NOT ProjHrEmplSel.GET(Year, Week, "Receiving Company", "Project No.", Company, "Employee No.") THEN BEGIN
          ProjHrEmplSel := Rec;
          ProjHrEmplSel.INSERT;
        END ELSE BEGIN
          IF NOT ProjHrEmplSel.Select THEN BEGIN
            ProjHrEmplSel.Select := TRUE;
            ProjHrEmplSel.MODIFY;
          END;
        END;
      END ELSE BEGIN
        SiteMgtProjHrLine.SETRANGE(Year, Year);
        SiteMgtProjHrLine.SETRANGE(Week, Week);
        SiteMgtProjHrLine.SETRANGE("Receiving Company", "Receiving Company");
        SiteMgtProjHrLine.SETRANGE("Project No.", "Project No.");
        SiteMgtProjHrLine.SETRANGE("Supplying Company", Company);
        SiteMgtProjHrLine.SETRANGE("Employee No.", "Employee No.");
        SiteMgtProjHrLine.SETFILTER("Total Line", '<>%1', 0);
        EmptyLine := SiteMgtProjHrLine.ISEMPTY;
        IF EmptyLine THEN BEGIN
          SiteMgtProjHrLine.SETRANGE("Total Line");  //* Reset filter
          SiteMgtProjHrLine.SETFILTER(Amount, '<>%1', 0);
          EmptyLine := SiteMgtProjHrLine.ISEMPTY;
        END;
        IF NOT EmptyLine THEN
          ERROR(Text002, Year, Week, FIELDCAPTION("Employee No."), Company + '/' + "Employee No.",
            FIELDCAPTION("Project No."), "Project No.");
        IF ProjHrEmplSel.GET(Year, Week, "Receiving Company", "Project No.", Company, "Employee No.") THEN
          ProjHrEmplSel.DELETE;
      END;
    END;

    BEGIN
    END.
  }
}

