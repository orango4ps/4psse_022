OBJECT Codeunit 130402 CAL Command Line Test Runner
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    Subtype=TestRunner;
    TestIsolation=Codeunit;
    OnRun=VAR
            CALTestEnabledCodeunit@1000 : Record 130403;
            CALTestResult@1001 : Record 130405;
            CodeCoverageMgt@1003 : Codeunit 9990;
            CALTestMgt@1004 : Codeunit 130401;
          BEGIN
            SELECTLATESTVERSION;
            TestRunNo := CALTestResult.LastTestRunNo + 1;
            CompanyWorkDate := WORKDATE;

            IF CALTestEnabledCodeunit.FINDSET THEN
              REPEAT
                IF CALTestMgt.DoesTestCodeunitExist(CALTestEnabledCodeunit."Test Codeunit ID") THEN BEGIN
                  CodeCoverageMgt.Start(TRUE);
                  CODEUNIT.RUN(CALTestEnabledCodeunit."Test Codeunit ID");
                  CodeCoverageMgt.Stop;
                  CALTestMgt.ExtendTestCoverage(CALTestEnabledCodeunit."Test Codeunit ID");
                END;
              UNTIL CALTestEnabledCodeunit.NEXT = 0
          END;

  }
  CODE
  {
    VAR
      CALTestRunnerPublisher@1002 : Codeunit 130403;
      TestRunNo@1001 : Integer;
      CompanyWorkDate@1000 : Date;

    PROCEDURE OnBeforeTestRun@1(CodeunitID@1000 : Integer;CodeunitName@1001 : Text[30];FunctionName@1002 : Text[128];FunctionTestPermissions@1003 : TestPermissions) : Boolean;
    VAR
      CALTestResult@1004 : Record 130405;
    BEGIN
      IF FunctionName = '' THEN
        EXIT(TRUE);

      CALTestRunnerPublisher.SetSeed(1);
      APPLICATIONAREA('');

      WORKDATE := CompanyWorkDate;

      CLEARLASTERROR;
      CALTestResult.Initialize(TestRunNo,CodeunitID,FunctionName,CURRENTDATETIME);
      EXIT(TRUE)
    END;

    PROCEDURE OnAfterTestRun@2(CodeunitID@1002 : Integer;CodeunitName@1001 : Text[30];FunctionName@1000 : Text[128];FunctionTestPermissions@1005 : TestPermissions;Success@1003 : Boolean);
    VAR
      CALTestResult@1004 : Record 130405;
    BEGIN
      IF FunctionName = '' THEN
        EXIT;

      CALTestResult.FINDLAST;
      CALTestResult.Update(Success,CURRENTDATETIME);

      APPLICATIONAREA('');
    END;

    BEGIN
    END.
  }
}

