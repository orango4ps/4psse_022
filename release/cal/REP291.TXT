OBJECT Report 291 Delete Invd Blnkt Sales Orders
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03,4PS14.00;
  }
  PROPERTIES
  {
    AccessByPermission=TableData 36=RD;
    CaptionML=[DEU=Erledigte Rahmenauftr„ge l”schen;
               ENU=Delete Invoiced Blanket Sales Orders;
               NLD=Gefactureerde afroepcontracten verwijderen;
               NOR=Slett fakturerte rammeordrer;
               SVE=Ta bort fakturerade f”rs„ljningsavropsorder];
    ApplicationArea=#Suite;
    ProcessingOnly=Yes;
    UsageCategory=Tasks;
  }
  DATASET
  {
    { 6640;    ;DataItem;                    ;
               DataItemTable=Table36;
               DataItemTableView=SORTING(Document Type,No.)
                                 WHERE(Document Type=CONST(Blanket Order));
               ReqFilterHeadingML=[DEU=Verkauf Rahmenauftrag;
                                   ENU=Blanket Sales Order;
                                   NLD=Verkoopraamcontract;
                                   NOR=Rammeordre;
                                   SVE=Avropsorder, f”rs„ljning];
               OnPreDataItem=BEGIN
                               Window.OPEN(Text000);
                             END;

               OnAfterGetRecord=VAR
                                  ATOLink@1002 : Record 904;
                                  ApprovalsMgmt@1000 : Codeunit 1535;
                                BEGIN
                                  Window.UPDATE(1,"No.");

                                  SalesLine.RESET;
                                  SalesLine.SETRANGE("Document Type","Document Type");
                                  SalesLine.SETRANGE("Document No.","No.");
                                  SalesLine.SETFILTER("Quantity Invoiced",'<>0');
                                  IF SalesLine.FINDFIRST THEN BEGIN
                                    SalesLine.SETRANGE("Quantity Invoiced");
                                    SalesLine.SETFILTER("Outstanding Quantity",'<>0');
                                    IF NOT SalesLine.FINDFIRST THEN BEGIN
                                      SalesLine.SETRANGE("Outstanding Quantity");
                                      SalesLine.SETFILTER("Qty. Shipped Not Invoiced",'<>0');
                                      IF NOT SalesLine.FINDFIRST THEN BEGIN
                                        SalesLine.LOCKTABLE;
                                        IF NOT SalesLine.FINDFIRST THEN BEGIN
                                          SalesLine.SETRANGE("Qty. Shipped Not Invoiced");
                                          SalesLine2.SETRANGE("Blanket Order No.","No.");
                                          IF NOT SalesLine2.FINDFIRST THEN BEGIN
                                            ArchiveManagement.AutoArchiveSalesDocument("Sales Header");
                                            SalesLine.SETFILTER("Qty. to Assemble to Order",'<>0');
                                            IF SalesLine.FINDSET THEN
                                              REPEAT
                                                ATOLink.DeleteAsmFromSalesLine(SalesLine);
                                              UNTIL SalesLine.NEXT = 0;
                                            SalesLine.SETRANGE("Qty. to Assemble to Order");
                                            OnBeforeDeleteSalesLines(SalesLine);
                                            SalesLine.DELETEALL;

                                            SalesCommentLine.SETRANGE("Document Type","Document Type");
                                            SalesCommentLine.SETRANGE("No.","No.");
                                            SalesCommentLine.DELETEALL;

                                            ApprovalsMgmt.DeleteApprovalEntries(RECORDID);

                                            OnBeforeDeleteSalesHeader("Sales Header");
                                            DELETE;

                                            COMMIT;
                                          END;
                                        END;
                                      END;
                                    END;
                                  END;
                                END;

               ReqFilterFields=No.,Sell-to Customer No.,Bill-to Customer No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Verkauf Auftr„ge werden durchgelesen #1##########;ENU=Processing sales orders #1##########;NLD=Verwerken verk.-orders  #1##########;NOR=Behandler ordrer        #1##########';
      SalesLine@1001 : Record 37;
      SalesLine2@1002 : Record 37;
      SalesCommentLine@1003 : Record 44;
      ArchiveManagement@1007 : Codeunit 5063;
      Window@1005 : Dialog;

    [Integration]
    LOCAL PROCEDURE OnBeforeDeleteSalesHeader@1(VAR SalesHeader@1000 : Record 36);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeDeleteSalesLines@2(VAR SalesLine@1000 : Record 37);
    BEGIN
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

