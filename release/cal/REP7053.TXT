OBJECT Report 7053 Implement Price Change
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Preisvorschlag Åbernehmen;
               ENU=Implement Price Change;
               NLD=Prijswijziging doorvoeren;
               NOR=Implementer prisendring];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 5995;    ;DataItem;                    ;
               DataItemTable=Table7023;
               DataItemTableView=SORTING(Starting Date,Ending Date,Sales Type,Sales Code,Currency Code,Item No.,Variant Code,Unit of Measure Code,Minimum Quantity);
               OnPreDataItem=BEGIN
                               Window.OPEN(
                                 Text000 +
                                 Text007 +
                                 Text008 +
                                 Text009 +
                                 Text010 +
                                 Text011);
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(1,"Item No.");
                                  Window.UPDATE(2,"Sales Type");
                                  Window.UPDATE(3,"Sales Code");
                                  Window.UPDATE(4,"Currency Code");
                                  Window.UPDATE(5,"Starting Date");

                                  SalesPrice.INIT;
                                  SalesPrice.VALIDATE("Item No.","Item No.");
                                  SalesPrice.VALIDATE("Sales Type","Sales Type");
                                  SalesPrice.VALIDATE("Sales Code","Sales Code");
                                  SalesPrice.VALIDATE("Unit of Measure Code","Unit of Measure Code");
                                  SalesPrice.VALIDATE("Variant Code","Variant Code");
                                  SalesPrice.VALIDATE("Starting Date","Starting Date");
                                  SalesPrice.VALIDATE("Ending Date","Ending Date");
                                  SalesPrice."Minimum Quantity" := "Minimum Quantity";
                                  SalesPrice."Currency Code" := "Currency Code";
                                  SalesPrice."Unit Price" := "New Unit Price";
                                  SalesPrice."Price Includes VAT" := "Price Includes VAT";
                                  SalesPrice."Allow Line Disc." := "Allow Line Disc.";
                                  SalesPrice."Allow Invoice Disc." := "Allow Invoice Disc.";
                                  SalesPrice."VAT Bus. Posting Gr. (Price)" := "VAT Bus. Posting Gr. (Price)";
                                  OnAfterCopyToSalesPrice(SalesPrice,"Sales Price Worksheet");
                                  IF SalesPrice."Unit Price" <> 0 THEN
                                    IF NOT SalesPrice.INSERT(TRUE) THEN
                                      SalesPrice.MODIFY(TRUE);
                                END;

               OnPostDataItem=VAR
                                ConfirmManagement@1000 : Codeunit 27;
                              BEGIN
                                COMMIT;
                                IF NOT DeleteWhstLine THEN
                                  DeleteWhstLine := ConfirmManagement.ConfirmProcess(Text005,TRUE);
                                IF DeleteWhstLine THEN
                                  DELETEALL;
                                COMMIT;
                                IF SalesPrice.FINDFIRST THEN;
                              END;

               ReqFilterFields=Item No.,Sales Type,Sales Code,Unit of Measure Code,Currency Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Aktualisieren von VK-Preisen...\\;ENU=Updating Unit Prices...\\;NLD=Bijwerken eenheidsprijzen...\\;NOR=Oppdaterer salgspriser...\\';
      Text005@1001 : TextConst 'DEU=Die VK-Preise wurden aufgrund der vorgeschlagenen PreisÑnderungen aktualisiert.\\Mîchten Sie die vorgeschlagenen PreisÑnderungen lîschen?;ENU=The item prices have now been updated in accordance with the suggested price changes.\\Do you want to delete the suggested price changes?;NLD=De artikelprijzen zijn nu bijgewerkt op basis van de voorgestelde prijswijzigingen.\\Wilt u de voorgestelde prijswijzigingen verwijderen?;NOR=Vareprisene er oppdatert i henhold til foreslÜtte prisendringer.\\Vil du slette de foreslÜtte prisendringene?';
      Text007@1003 : TextConst 'DEU=Artikelnr.             #1##########\;ENU=Item No.               #1##########\;NLD=Artikelnr.             #1##########\;NOR=Varenr.                #1##########\';
      Text008@1004 : TextConst 'DEU=Verkaufsart            #2##########\;ENU=Sales Type             #2##########\;NLD=Verkoopsoort           #2##########\;NOR=Salgstype              #2##########\';
      Text009@1005 : TextConst 'DEU=Verkaufscode           #3##########\;ENU=Sales Code             #3##########\;NLD=Verkoopcode            #3##########\;NOR=Salgskode              #3##########\';
      Text010@1006 : TextConst 'DEU=WÑhrungscode           #4##########\;ENU=Currency Code          #4##########\;NLD=Valutacode             #4##########\;NOR=Valutakode             #4##########\';
      Text011@1007 : TextConst 'DEU=Startdatum             #5######;ENU=Starting Date          #5######;NLD=Begindatum             #5######;NOR=Startdato              #5######';
      SalesPrice@1008 : Record 7002;
      Window@1009 : Dialog;
      DeleteWhstLine@1010 : Boolean;

    [External]
    PROCEDURE InitializeRequest@1(NewDeleteWhstLine@1000 : Boolean);
    BEGIN
      DeleteWhstLine := NewDeleteWhstLine;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyToSalesPrice@2(VAR SalesPrice@1000 : Record 7002;SalesPriceWorksheet@1001 : Record 7023);
    BEGIN
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

