OBJECT Codeunit 1313 Correct Posted Purch. Invoice
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03;
  }
  PROPERTIES
  {
    TableNo=122;
    Permissions=TableData 122=rm,
                TableData 124=rm;
    OnRun=VAR
            PurchaseHeader@1001 : Record 38;
            ItemJnlPostLine@1002 : Codeunit 22;
            RedoApplications@1003 : Boolean;
          BEGIN
            RedoApplications := UnapplyCostApplication(ItemJnlPostLine,"No.");
            CreateCopyDocument(Rec,PurchaseHeader,PurchaseHeader."Document Type"::"Credit Memo",FALSE);
            PurchaseHeader."Vendor Cr. Memo No." := PurchaseHeader."No.";
            OnAfterCreateCorrectivePurchCrMemo(Rec,PurchaseHeader,CancellingOnly);

            CODEUNIT.RUN(CODEUNIT::"Purch.-Post",PurchaseHeader);
            SetTrackInfoForCancellation(Rec);
            IF RedoApplications THEN
              ItemJnlPostLine.RedoApplications;
            COMMIT;
          END;

  }
  CODE
  {
    VAR
      PostedInvoiceIsPaidCorrectErr@1000 : TextConst 'ENU=You cannot correct this posted purchase invoice because it is fully or partially paid.\\To reverse a paid purchase invoice, you must manually create a purchase credit memo.;NOR=Du kan ikke korrigere denne bokf›rte kj›psfakturaen fordi den er fullt eller delvis betalt.\\N†r du skal tilbakef›re en betalt kj›psfaktura, m† du opprette en kj›pskreditnota manuelt.;SVE=Det g†r inte att korrigera den h„r bokf”rda ink”psfakturan eftersom den „r helt eller delvis betalad.\\Du m†ste skapa en ink”pskreditnota manuellt f”r att kunna †terf”ra en betalad ink”psfaktura.';
      PostedInvoiceIsPaidCCancelErr@1019 : TextConst 'ENU=You cannot cancel this posted purchase invoice because it is fully or partially paid.\\To reverse a paid purchase invoice, you must manually create a purchase credit memo.;NOR=Du kan ikke annullere denne bokf›rte kj›psfakturaen fordi den er fullt eller delvis betalt.\\N†r du skal tilbakef›re en betalt kj›psfaktura, m† du opprette en kj›pskreditnota manuelt.;SVE=Det g†r inte att annullera den h„r bokf”rda ink”psfakturan eftersom den „r helt eller delvis betalad.\\Du m†ste skapa en ink”pskreditnota manuellt f”r att kunna †terf”ra en betalad ink”psfaktura.';
      AlreadyCorrectedErr@1001 : TextConst 'ENU=You cannot correct this posted purchase invoice because it has been canceled.;NOR=Du kan ikke korrigere denne bokf›rte kj›psfakturaen fordi den er annullert.;SVE=Det g†r inte att korrigera den h„r bokf”rda ink”psfakturan eftersom den har annullerats.';
      AlreadyCancelledErr@1005 : TextConst 'ENU=You cannot cancel this posted purchase invoice because it has already been canceled.;NOR=Du kan ikke annullere denne bokf›rte kj›psfakturaen fordi den allerede er annullert.;SVE=Det g†r inte att annullera den h„r bokf”rda ink”psfakturan eftersom den redan har annullerats.';
      CorrCorrectiveDocErr@1045 : TextConst 'ENU=You cannot correct this posted purchase invoice because it represents a correction of a credit memo.;NOR=Du kan ikke korrigere denne bokf›rte kj›psfakturaen fordi den representerer en korrigering av en kreditnota.;SVE=Det g†r inte att korrigera den h„r bokf”rda ink”psfakturan eftersom den representerar en r„ttelse av en kreditnota.';
      CancelCorrectiveDocErr@1044 : TextConst 'ENU=You cannot cancel this posted purchase invoice because it represents a correction of a credit memo.;NOR=Du kan ikke annullere denne bokf›rte kj›psfakturaen fordi den representerer en korrigering av en kreditnota.;SVE=Det g†r inte att annullera den h„r bokf”rda ink”psfakturan eftersom den representerar en r„ttelse av en kreditnota.';
      VendorIsBlockedCorrectErr@1002 : TextConst '@@@="%1 = Customer name";ENU=You cannot correct this posted purchase invoice because vendor %1 is blocked.;NOR=Du kan ikke korrigere denne bokf›rte kj›psfakturaen fordi leverand›ren %1 er sperret.;SVE=Det g†r inte att korrigera den h„r bokf”rda ink”psfakturan eftersom leverant”ren %1 „r sp„rrad.';
      VendorIsBlockedCancelErr@1016 : TextConst '@@@="%1 = Customer name";ENU=You cannot cancel this posted purchase invoice because vendor %1 is blocked.;NOR=Du kan ikke annullere denne bokf›rte kj›psfakturaen fordi leverand›ren %1 er sperret.;SVE=Det g†r inte att annullera den h„r bokf”rda ink”psfakturan eftersom leverant”ren %1 „r sp„rrad.';
      ItemIsBlockedCorrectErr@1003 : TextConst '@@@="%1 = Item No. %2 = Item Description";ENU=You cannot correct this posted purchase invoice because item %1 %2 is blocked.;NOR=Du kan ikke korrigere denne bokf›rte kj›psfakturaen fordi varen %1 %2 er sperret.;SVE=Det g†r inte att korrigera den h„r bokf”rda ink”psfakturan eftersom artikeln %1 %2 „r sp„rrad.';
      ItemIsBlockedCancelErr@1017 : TextConst '@@@="%1 = Item No. %2 = Item Description";ENU=You cannot cancel this posted purchase invoice because item %1 %2 is blocked.;NOR=Du kan ikke annullere denne bokf›rte kj›psfakturaen fordi varen %1 %2 er sperret.;SVE=Det g†r inte att annullera den h„r bokf”rda ink”psfakturan eftersom artikeln %1 %2 „r sp„rrad.';
      AccountIsBlockedCorrectErr@1018 : TextConst '@@@="%1 = Table Caption %2 = Account number.";ENU=You cannot correct this posted purchase invoice because %1 %2 is blocked.;NOR=Du kan ikke korrigere denne bokf›rte kj›psfakturaen fordi %1 %2 er sperret.;SVE=Det g†r inte att korrigera den h„r bokf”rda ink”psfakturan eftersom %1 %2 „r sp„rrad.';
      AccountIsBlockedCancelErr@1025 : TextConst '@@@="%1 = Table Caption %2 = Account number.";ENU=You cannot cancel this posted purchase invoice because %1 %2 is blocked.;NOR=Du kan ikke annullere denne bokf›rte kj›psfakturaen fordi %1 %2 er sperret.;SVE=Det g†r inte att annullera den h„r bokf”rda ink”psfakturan eftersom %1 %2 „r sp„rrad.';
      NoFreeInvoiceNoSeriesCorrectErr@1004 : TextConst 'ENU=You cannot correct this posted purchase invoice because no unused invoice numbers are available. \\You must extend the range of the number series for purchase invoices.;NOR=Du kan ikke korrigere denne bokf›rte kj›psfakturaen fordi det ikke finnes noen ubrukte fakturanumre tilgjengelig. \\Du m† utvide nummerserien for kj›psfakturaer.;SVE=Det g†r inte att korrigera den h„r bokf”rda ink”psfakturan eftersom inga oanv„nda fakturanummer „r tillg„ngliga. \\Du m†ste ut”ka nummerserien f”r ink”psfakturor.';
      NoFreeInvoiceNoSeriesCancelErr@1026 : TextConst 'ENU=You cannot cancel this posted purchase invoice because no unused invoice numbers are available. \\You must extend the range of the number series for purchase invoices.;NOR=Du kan ikke annullere denne bokf›rte kj›psfakturaen fordi det ikke finnes noen ubrukte fakturanumre tilgjengelig. \\Du m† utvide nummerserien for kj›psfakturaer.;SVE=Det g†r inte att annullera den h„r bokf”rda ink”psfakturan eftersom inga oanv„nda fakturanummer „r tillg„ngliga. \\Du m†ste ut”ka nummerserien f”r ink”psfakturor.';
      NoFreeCMSeriesCorrectErr@1013 : TextConst 'ENU=You cannot correct this posted purchase invoice because no unused credit memo numbers are available. \\You must extend the range of the number series for credit memos.;NOR=Du kan ikke korrigere denne bokf›rte kj›psfakturaen fordi det ikke finnes noen ubrukte kreditnotanumre tilgjengelig. \\Du m† utvide nummerserien for kreditnotaer.;SVE=Det g†r inte att korrigera den h„r bokf”rda ink”psfakturan eftersom inga oanv„nda kreditnotanummer „r tillg„ngliga. \\Du m†ste ut”ka nummerserien f”r kreditnotor.';
      NoFreeCMSeriesCancelErr@1027 : TextConst 'ENU=You cannot cancel this posted purchase invoice because no unused credit memo numbers are available. \\You must extend the range of the number series for credit memos.;NOR=Du kan ikke annullere denne bokf›rte kj›psfakturaen fordi det ikke finnes noen ubrukte kreditnotanumre tilgjengelig. \\Du m† utvide nummerserien for kreditnotaer.;SVE=Det g†r inte att annullera den h„r bokf”rda ink”psfakturan eftersom inga oanv„nda kreditnotanummer „r tillg„ngliga. \\Du m†ste ut”ka nummerserien f”r kreditnotor.';
      NoFreePostCMSeriesCorrectErr@1028 : TextConst 'ENU=You cannot correct this posted purchase invoice because no unused posted credit memo numbers are available. \\You must extend the range of the number series for posted credit memos.;NOR=Du kan ikke korrigere denne bokf›rte kj›psfakturaen fordi det ikke finnes noen ubrukte bokf›rte kreditnotanumre tilgjengelig. \\Du m† utvide nummerserien for bokf›rte kreditnotaer.;SVE=Det g†r inte att korrigera den h„r bokf”rda ink”psfakturan eftersom inga oanv„nda bokf”rda kreditnotanummer „r tillg„ngliga. \\Du m†ste ut”ka nummerserien f”r bokf”rda kreditnotor.';
      NoFreePostCMSeriesCancelErr@1029 : TextConst 'ENU=You cannot cancel this posted purchase invoice because no unused posted credit memo numbers are available. \\You must extend the range of the number series for posted credit memos.;NOR=Du kan ikke annullere denne bokf›rte kj›psfakturaen fordi det ikke finnes noen ubrukte bokf›rte kreditnotanumre tilgjengelig. \\Du m† utvide nummerserien for bokf›rte kreditnotaer.;SVE=Det g†r inte att annullera den h„r bokf”rda ink”psfakturan eftersom inga oanv„nda bokf”rda kreditnotanummer „r tillg„ngliga. \\Du m†ste ut”ka nummerserien f”r bokf”rda kreditnotor.';
      PurchaseLineFromOrderCorrectErr@1006 : TextConst '@@@="%1 = Item no. %2 = Item description";ENU=You cannot correct this posted purchase invoice because item %1 %2 is used on a purchase order.;NOR=Du kan ikke korrigere denne bokf›rte kj›psfakturaen fordi varen %1 %2 brukes i en bestilling.;SVE=Det g†r inte att korrigera den h„r bokf”rda ink”psfakturan eftersom artikeln %1 %2 anv„nds i en ink”psorder.';
      PurchaseLineFromOrderCancelErr@1030 : TextConst '@@@="%1 = Item no. %2 = Item description";ENU=You cannot cancel this posted purchase invoice because item %1 %2 is used on a purchase order.;NOR=Du kan ikke annullere denne bokf›rte kj›psfakturaen fordi varen %1 %2 brukes i en bestilling.;SVE=Det g†r inte att annullera den h„r bokf”rda ink”psfakturan eftersom artikeln %1 %2 anv„nds i en ink”psorder.';
      ShippedQtyReturnedCorrectErr@1007 : TextConst '@@@="%1 = Item no. %2 = Item description.";ENU=You cannot correct this posted purchase invoice because item %1 %2 has already been fully or partially returned.;NOR=Du kan ikke korrigere denne bokf›rte kj›psfakturaen fordi varen %1 %2 allerede er fullt eller delvis returnert.;SVE=Det g†r inte att korrigera den h„r bokf”rda ink”psfakturan eftersom artikeln %1 %2 redan „r helt eller delvis returnerad.';
      ShippedQtyReturnedCancelErr@1031 : TextConst '@@@="%1 = Item no. %2 = Item description.";ENU=You cannot cancel this posted purchase invoice because item %1 %2 has already been fully or partially returned.;NOR=Du kan ikke annullere denne bokf›rte kj›psfakturaen fordi varen %1 %2 allerede er fullt eller delvis returnert.;SVE=Det g†r inte att annullera den h„r bokf”rda ink”psfakturan eftersom artikel %1 %2 redan „r helt eller delvis returnerad.';
      UsedInJobCorrectErr@1008 : TextConst '@@@="%1 = Item no. %2 = Item description.";ENU=You cannot correct this posted purchase invoice because item %1 %2 is used in a job.;NOR=Du kan ikke korrigere denne bokf›rte kj›psfakturaen fordi varen %1 %2 brukes i et prosjekt.;SVE=Det g†r inte att korrigera den h„r bokf”rda ink”psfakturan eftersom artikeln %1 %2 anv„nds i ett projekt.';
      UsedInJobCancelErr@1032 : TextConst '@@@="%1 = Item no. %2 = Item description.";ENU=You cannot cancel this posted purchase invoice because item %1 %2 is used in a job.;NOR=Du kan ikke annullere denne bokf›rte kj›psfakturaen fordi varen %1 %2 brukes i et prosjekt.;SVE=Det g†r inte att annullera den h„r bokf”rda ink”psfakturan eftersom artikeln %1 %2 anv„nds i ett projekt.';
      PostingNotAllowedCorrectErr@1009 : TextConst 'ENU=You cannot correct this posted purchase invoice because it was posted in a posting period that is closed.;NOR=Du kan ikke korrigere denne bokf›rte kj›psfakturaen fordi den ble bokf›rt i en bokf›ringsperiode som er lukket.;SVE=Det g†r inte att korrigera den h„r bokf”rda ink”psfakturan eftersom den bokf”rdes i en st„ngd bokf”ringsperiod.';
      PostingNotAllowedCancelErr@1033 : TextConst 'ENU=You cannot cancel this posted purchase invoice because it was posted in a posting period that is closed.;NOR=Du kan ikke annullere denne bokf›rte kj›psfakturaen fordi den ble bokf›rt i en bokf›ringsperiode som er lukket.;SVE=Det g†r inte att annullera den h„r bokf”rda ink”psfakturan eftersom den bokf”rdes i en st„ngd bokf”ringsperiod.';
      InvoiceIsBasedOnOrderCorrectErr@1010 : TextConst 'ENU=You cannot correct this posted purchase invoice because the invoice is based on a purchase order.;NOR=Du kan ikke korrigere denne bokf›rte kj›psfakturaen fordi fakturaen er basert p† en bestilling.;SVE=Det g†r inte att korrigera den h„r bokf”rda ink”psfakturan eftersom fakturan „r baserad p† en ink”psorder.';
      InvoiceIsBasedOnOrderCancelErr@1034 : TextConst 'ENU=You cannot cancel this posted purchase invoice because the invoice is based on a purchase order.;NOR=Du kan ikke annullere denne bokf›rte kj›psfakturaen fordi fakturaen er basert p† en bestilling.;SVE=Det g†r inte att annullera den h„r bokf”rda ink”psfakturan eftersom fakturan „r baserad p† en ink”psorder.';
      LineTypeNotAllowedCorrectErr@1012 : TextConst '@@@="%1 = Item no. %2 = Item description %3 = Item type.";ENU=You cannot correct this posted purchase invoice because the purchase invoice line for %1 %2 is of type %3, which is not allowed on a simplified purchase invoice.;NOR=Du kan ikke korrigere denne bokf›rte kj›psfakturaen fordi kj›psfakturalinjen for %1 %2 er av typen %3, som ikke er tillatt p† en forenklet kj›psfaktura.;SVE=Det g†r inte att korrigera den h„r bokf”rda ink”psfakturan eftersom ink”psfakturaraden f”r %1 %2 „r av typen %3, som inte till†ts f”r en f”renklad ink”psfaktura.';
      LineTypeNotAllowedCancelErr@1036 : TextConst '@@@="%1 = Item no. %2 = Item description %3 = Item type.";ENU=You cannot cancel this posted purchase invoice because the purchase invoice line for %1 %2 is of type %3, which is not allowed on a simplified purchase invoice.;NOR=Du kan ikke annullere denne bokf›rte kj›psfakturaen fordi kj›psfakturalinjen for %1 %2 er av typen %3, som ikke er tillatt p† en forenklet kj›psfaktura.;SVE=Det g†r inte att annullera den h„r bokf”rda ink”psfakturan eftersom ink”psfakturaraden f”r %1 %2 „r av typen %3, som inte till†ts f”r en f”renklad ink”psfaktura.';
      PurchasesPayablesSetup@1046 : Record 312;
      CancellingOnly@1014 : Boolean;
      InvalidDimCodeCorrectErr@1037 : TextConst '@@@="%1 = Table caption %2 = Account number %3 = Item no. %4 = Item description.";ENU=You cannot correct this posted purchase invoice because the dimension rule setup for account ''%1'' %2 prevents %3 %4 from being canceled.;NOR=Du kan ikke korrigere denne bokf›rte kj›psfakturaen fordi dimensjonsregeloppsettet for kontoen %1 %2 forhindrer at %3 %4 kan annulleres.;SVE=Det g†r inte att korrigera den h„r bokf”rda ink”psfakturan eftersom dimensionsregelinst„llningen f”r kontot %1 %2 f”rhindrar att %3 %4 annulleras.';
      InvalidDimCodeCancelErr@1038 : TextConst '@@@="%1 = Table caption %2 = Account number %3 = Item no. %4 = Item description.";ENU=You cannot cancel this posted purchase invoice because the dimension rule setup for account ''%1'' %2 prevents %3 %4 from being canceled.;NOR=Du kan ikke annullere denne bokf›rte kj›psfakturaen fordi dimensjonsregeloppsettet for kontoen %1 %2 forhindrer at %3 %4 kan annulleres.;SVE=Det g†r inte att annullera den h„r bokf”rda ink”psfakturan eftersom dimensionsregelinst„llningen f”r kontot %1 %2 f”rhindrar att %3 %4 annulleras.';
      InvalidDimCombinationCorrectErr@1023 : TextConst '@@@="%1 = Item no. %2 = Item description.";ENU=You cannot correct this posted purchase invoice because the dimension combination for item %1 %2 is not allowed.;NOR=Du kan ikke korrigere denne bokf›rte kj›psfakturaen fordi dimensjonskombinasjonen for varen %1 %2 ikke er tillatt.;SVE=Det g†r inte att korrigera den h„r bokf”rda ink”psfakturan eftersom dimensionskombinationen f”r artikeln %1 %2 inte „r till†ten.';
      InvalidDimCombinationCancelErr@1039 : TextConst '@@@="%1 = Item no. %2 = Item description.";ENU=You cannot cancel this posted purchase invoice because the dimension combination for item %1 %2 is not allowed.;NOR=Du kan ikke annullere denne bokf›rte kj›psfakturaen fordi dimensjonskombinasjonen for varen %1 %2 ikke er tillatt.;SVE=Det g†r inte att annullera den h„r bokf”rda ink”psfakturan eftersom dimensionskombinationen f”r artikel %1 %2 inte „r till†ten.';
      InvalidDimCombHeaderCorrectErr@1011 : TextConst 'ENU=You cannot correct this posted purchase invoice because the combination of dimensions on the invoice is blocked.;NOR=Du kan ikke korrigere denne bokf›rte kj›psfakturaen fordi kombinasjonen av dimensjoner p† fakturaen er sperret.;SVE=Det g†r inte att korrigera den h„r bokf”rda ink”psfakturan eftersom kombinationen av dimensioner p† fakturan „r sp„rrad.';
      InvalidDimCombHeaderCancelErr@1035 : TextConst 'ENU=You cannot cancel this posted purchase invoice because the combination of dimensions on the invoice is blocked.;NOR=Du kan ikke annullere denne bokf›rte kj›psfakturaen fordi kombinasjonen av dimensjoner p† fakturaen er sperret.;SVE=Det g†r inte att annullera den h„r bokf”rda ink”psfakturan eftersom kombinationen av dimensioner p† fakturan „r sp„rrad.';
      ExternalDocCorrectErr@1015 : TextConst 'ENU=You cannot correct this posted purchase invoice because the external document number is required on the invoice.;NOR=Du kan ikke korrigere denne bokf›rte kj›psfakturaen fordi eksterndokumentnummeret er obligatorisk p† fakturaen.;SVE=Det g†r inte att korrigera den h„r bokf”rda ink”psfakturan eftersom det externa dokumentnumret kr„vs p† fakturan.';
      ExternalDocCancelErr@1040 : TextConst 'ENU=You cannot cancel this posted purchase invoice because the external document number is required on the invoice.;NOR=Du kan ikke annullere denne bokf›rte kj›psfakturaen fordi eksterndokumentnummeret er obligatorisk p† fakturaen.;SVE=Det g†r inte att annullera den h„r bokf”rda ink”psfakturan eftersom det externa dokumentnumret kr„vs p† fakturan.';
      InventoryPostClosedCorrectErr@1041 : TextConst 'ENU=You cannot correct this posted purchase invoice because the posting inventory period is already closed.;NOR=Du kan ikke korrigere denne bokf›rte kj›psfakturaen fordi lagerperioden for bokf›ring allerede er lukket.;SVE=Det g†r inte att korrigera den h„r bokf”rda ink”psfakturan eftersom bokf”ringslagerperioden redan „r st„ngd.';
      InventoryPostClosedCancelErr@1042 : TextConst 'ENU=You cannot cancel this posted purchase invoice because the posting inventory period is already closed.;NOR=Du kan ikke annullere denne bokf›rte kj›psfakturaen fordi lagerperioden for bokf›ring allerede er lukket.;SVE=Det g†r inte att annullera den h„r bokf”rda ink”psfakturan eftersom bokf”ringslagerperioden redan „r st„ngd.';
      PostingCreditMemoFailedOpenPostedCMQst@1020 : TextConst 'ENU=Canceling the invoice failed because of the following error: \\%1\\A credit memo is posted. Do you want to open the posted credit memo?;NOR=Annullering av fakturaen mislyktes p† grunn av f›lgende feil: \\%1\\En kreditnota er bokf›rt. Vil du †pne den bokf›rte kreditnotaen?;SVE=Det gick inte att annullera fakturan p† grund av f”ljande fel: \\%1\\En kreditnota har bokf”rts. Vill du ”ppna den bokf”rda kreditnotan?';
      PostingCreditMemoFailedOpenCMQst@1021 : TextConst 'ENU=Canceling the invoice failed because of the following error: \\%1\\A credit memo is created but not posted. Do you want to open the credit memo?;NOR=Annullering av fakturaen mislyktes p† grunn av f›lgende feil: \\%1\\En kreditnota er opprettet, men ikke bokf›rt. Vil du †pne kreditnotaen?;SVE=Det gick inte att annullera fakturan p† grund av f”ljande fel: \\%1\\En kreditnota har skapats men inte bokf”rts. Vill du ”ppna kreditnotan?';
      CreatingCreditMemoFailedNothingCreatedErr@1022 : TextConst 'ENU=Canceling the invoice failed because of the following error: \\%1.;NOR=Annullering av fakturaen mislyktes p† grunn av f›lgende feil: \\%1.;SVE=Det gick inte att annullera fakturan p† grund av f”ljande fel: \\%1.';
      ErrorType@1024 : 'IsPaid,VendorBlocked,ItemBlocked,AccountBlocked,IsCorrected,IsCorrective,SerieNumInv,SerieNumCM,SerieNumPostCM,ItemIsReturned,FromOrder,PostingNotAllowed,LineFromOrder,WrongItemType,LineFromJob,DimErr,DimCombErr,DimCombHeaderErr,ExtDocErr,InventoryPostClosed';
      WrongDocumentTypeForCopyDocumentErr@1043 : TextConst 'ENU=You cannot correct or cancel this type of document.;NOR=Du kan ikke rette opp eller avbryte denne typen dokument.;SVE=Du kan inte korrigera eller avbryta den h„r typen av dokument.';
      InvoicePartiallyPaidMsg@1047 : TextConst '@@@=%1 - invoice no.;ENU=Invoice %1 is partially paid or credited. The corrective credit memo may not be fully closed by the invoice.;NOR=Faktura %1 er delvis betalt eller kreditert. Det er ikke sikkert at korrigerende kreditnota blir helt lukket av fakturaen.';
      InvoiceClosedMsg@1048 : TextConst '@@@=%1 - invoice no.;ENU=Invoice %1 is closed. The corrective credit memo will not be applied to the invoice.;NOR=Faktura %1 er lukket. Korrigerende kreditnota blir ikke utlignet i fakturaen.';
      SkipLbl@1049 : TextConst 'ENU=Skip;NOR=Hopp over';
      CreateCreditMemoLbl@1050 : TextConst 'ENU=Create credit memo anyway;NOR=Opprett kreditnota likevel';
      ShowEntriesLbl@1051 : TextConst 'ENU=Show applied entries;NOR=Vis utlignede poster';

    [External]
    PROCEDURE CancelPostedInvoice@4(VAR PurchInvHeader@1000 : Record 122) : Boolean;
    BEGIN
      CancellingOnly := TRUE;
      EXIT(CreateCreditMemo(PurchInvHeader));
    END;

    LOCAL PROCEDURE CreateCreditMemo@13(VAR PurchInvHeader@1000 : Record 122) : Boolean;
    VAR
      PurchaseHeader@1004 : Record 38;
      PurchCrMemoHdr@1005 : Record 124;
    BEGIN
      TestCorrectInvoiceIsAllowed(PurchInvHeader,CancellingOnly);
      IF NOT CODEUNIT.RUN(CODEUNIT::"Correct Posted Purch. Invoice",PurchInvHeader) THEN BEGIN
        PurchCrMemoHdr.SETRANGE("Applies-to Doc. No.",PurchInvHeader."No.");
        IF PurchCrMemoHdr.FINDFIRST THEN BEGIN
          IF CONFIRM(STRSUBSTNO(PostingCreditMemoFailedOpenPostedCMQst,GETLASTERRORTEXT)) THEN
            PAGE.RUN(PAGE::"Posted Purchase Credit Memo",PurchCrMemoHdr);
        END ELSE BEGIN
          PurchaseHeader.SETRANGE("Applies-to Doc. No.",PurchInvHeader."No.");
          IF PurchaseHeader.FINDFIRST THEN BEGIN
            IF CONFIRM(STRSUBSTNO(PostingCreditMemoFailedOpenCMQst,GETLASTERRORTEXT)) THEN
              PAGE.RUN(PAGE::"Purchase Credit Memo",PurchaseHeader);
          END ELSE
            ERROR(CreatingCreditMemoFailedNothingCreatedErr,GETLASTERRORTEXT);
        END;
        EXIT(FALSE);
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE CreateCopyDocument@16(VAR PurchInvHeader@1002 : Record 122;VAR PurchaseHeader@1001 : Record 38;DocumentType@1000 : Option;SkipCopyFromDescription@1004 : Boolean);
    VAR
      CopyDocMgt@1003 : Codeunit 6620;
    BEGIN
      CLEAR(PurchaseHeader);
      PurchaseHeader."Document Type" := DocumentType;
      PurchaseHeader."No." := '';
      PurchaseHeader.SetAllowSelectNoSeries;
      OnBeforePurchaseHeaderInsert(PurchaseHeader,PurchInvHeader);
      PurchaseHeader.INSERT(TRUE);

      CASE DocumentType OF
        PurchaseHeader."Document Type"::"Credit Memo":
          CopyDocMgt.SetPropertiesForCreditMemoCorrection;
        PurchaseHeader."Document Type"::Invoice:
          CopyDocMgt.SetPropertiesForInvoiceCorrection(SkipCopyFromDescription);
        ELSE
          ERROR(WrongDocumentTypeForCopyDocumentErr);
      END;

      CopyDocMgt.CopyPurchaseDocForInvoiceCancelling(PurchInvHeader."No.",PurchaseHeader);
    END;

    [External]
    PROCEDURE CreateCreditMemoCopyDocument@11(VAR PurchInvHeader@1001 : Record 122;VAR PurchaseHeader@1000 : Record 38) : Boolean;
    BEGIN
      IF NOT PurchInvHeader.IsFullyOpen THEN BEGIN
        ShowInvoiceAppliedNotification(PurchInvHeader);
        EXIT(FALSE);
      END;
      CreateCopyDocument(PurchInvHeader,PurchaseHeader,PurchaseHeader."Document Type"::"Credit Memo",FALSE);
      EXIT(TRUE);
    END;

    [External]
    PROCEDURE CreateCorrectiveCreditMemo@48(VAR InvoiceNotification@1000 : Notification);
    VAR
      PurchHeader@1002 : Record 38;
      PurchInvHeader@1001 : Record 122;
    BEGIN
      PurchInvHeader.GET(InvoiceNotification.GETDATA(PurchInvHeader.FIELDNAME("No.")));
      InvoiceNotification.RECALL;

      CreateCopyDocument(PurchInvHeader,PurchHeader,PurchHeader."Document Type"::"Credit Memo",FALSE);
      PAGE.RUN(PAGE::"Purchase Credit Memo",PurchHeader);
    END;

    [External]
    PROCEDURE ShowAppliedEntries@43(VAR InvoiceNotification@1000 : Notification);
    VAR
      VendLedgerEntry@1002 : Record 25;
      PurchInvHeader@1001 : Record 122;
    BEGIN
      PurchInvHeader.GET(InvoiceNotification.GETDATA(PurchInvHeader.FIELDNAME("No.")));
      VendLedgerEntry.GET(PurchInvHeader."Vendor Ledger Entry No.");
      PAGE.RUNMODAL(PAGE::"Applied Vendor Entries",VendLedgerEntry);
    END;

    [External]
    PROCEDURE SkipCorrectiveCreditMemo@50(VAR InvoiceNotification@1000 : Notification);
    BEGIN
      InvoiceNotification.RECALL;
    END;

    [External]
    PROCEDURE CancelPostedInvoiceStartNewInvoice@1(VAR PurchInvHeader@1000 : Record 122;VAR PurchaseHeader@1002 : Record 38);
    BEGIN
      CancellingOnly := FALSE;

      IF CreateCreditMemo(PurchInvHeader) THEN BEGIN
        CreateCopyDocument(PurchInvHeader,PurchaseHeader,PurchaseHeader."Document Type"::Invoice,TRUE);
        COMMIT;
      END;
    END;

    [External]
    PROCEDURE TestCorrectInvoiceIsAllowed@5(VAR PurchInvHeader@1000 : Record 122;Cancelling@1001 : Boolean);
    BEGIN
      CancellingOnly := Cancelling;

      PurchInvHeader.CALCFIELDS(Amount);
      PurchInvHeader.TESTFIELD(Amount);
      TestIfPostingIsAllowed(PurchInvHeader);
      TestIfInvoiceIsCorrectedOnce(PurchInvHeader);
      TestIfInvoiceIsNotCorrectiveDoc(PurchInvHeader);
      TestIfInvoiceIsPaid(PurchInvHeader);
      TestIfVendorIsBlocked(PurchInvHeader,PurchInvHeader."Buy-from Vendor No.");
      TestIfVendorIsBlocked(PurchInvHeader,PurchInvHeader."Pay-to Vendor No.");
      TestVendorDimension(PurchInvHeader,PurchInvHeader."Pay-to Vendor No.");
      TestDimensionOnHeader(PurchInvHeader);
      TestPurchaseLines(PurchInvHeader);
      TestIfAnyFreeNumberSeries(PurchInvHeader);
      TestExternalDocument(PurchInvHeader);
      TestInventoryPostingClosed(PurchInvHeader);

      OnAfterTestCorrectInvoiceIsAllowed(PurchInvHeader,Cancelling);
    END;

    LOCAL PROCEDURE ShowInvoiceAppliedNotification@42(PurchInvHeader@1000 : Record 122);
    VAR
      NotificationLifecycleMgt@1001 : Codeunit 1511;
      InvoiceNotification@1002 : Notification;
      NotificationText@1004 : Text;
    BEGIN
      InvoiceNotification.ID := CREATEGUID;
      InvoiceNotification.SCOPE(NOTIFICATIONSCOPE::LocalScope);
      InvoiceNotification.SETDATA(PurchInvHeader.FIELDNAME("No."),PurchInvHeader."No.");
      PurchInvHeader.CALCFIELDS(Closed);
      IF PurchInvHeader.Closed THEN
        NotificationText := STRSUBSTNO(InvoiceClosedMsg,PurchInvHeader."No.")
      ELSE
        NotificationText := STRSUBSTNO(InvoicePartiallyPaidMsg,PurchInvHeader."No.");
      InvoiceNotification.MESSAGE(NotificationText);
      InvoiceNotification.ADDACTION(ShowEntriesLbl,CODEUNIT::"Correct Posted Purch. Invoice",'ShowAppliedEntries');
      InvoiceNotification.ADDACTION(SkipLbl,CODEUNIT::"Correct Posted Purch. Invoice",'SkipCorrectiveCreditMemo');
      InvoiceNotification.ADDACTION(CreateCreditMemoLbl,CODEUNIT::"Correct Posted Purch. Invoice",'CreateCorrectiveCreditMemo');
      NotificationLifecycleMgt.SendNotification(InvoiceNotification,PurchInvHeader.RECORDID);
    END;

    LOCAL PROCEDURE SetTrackInfoForCancellation@2(VAR PurchInvHeader@1000 : Record 122);
    VAR
      PurchCrMemoHdr@1001 : Record 124;
      CancelledDocument@1002 : Record 1900;
    BEGIN
      PurchCrMemoHdr.SETRANGE("Applies-to Doc. No.",PurchInvHeader."No.");
      IF PurchCrMemoHdr.FINDLAST THEN
        CancelledDocument.InsertPurchInvToCrMemoCancelledDocument(PurchInvHeader."No.",PurchCrMemoHdr."No.");
    END;

    LOCAL PROCEDURE TestDimensionOnHeader@15(PurchInvHeader@1000 : Record 122);
    VAR
      DimensionManagement@1001 : Codeunit 408;
    BEGIN
      IF NOT DimensionManagement.CheckDimIDComb(PurchInvHeader."Dimension Set ID") THEN
        ErrorHelperHeader(ErrorType::DimCombHeaderErr,PurchInvHeader);
    END;

    LOCAL PROCEDURE TestIfVendorIsBlocked@9(PurchInvHeader@1000 : Record 122;VendNo@1005 : Code[20]);
    VAR
      Vendor@1001 : Record 23;
    BEGIN
      Vendor.GET(VendNo);
      IF Vendor.Blocked IN [Vendor.Blocked::All] THEN
        ErrorHelperHeader(ErrorType::VendorBlocked,PurchInvHeader);
    END;

    LOCAL PROCEDURE TestVendorDimension@3(PurchInvHeader@1000 : Record 122;VendNo@1005 : Code[20]);
    VAR
      Vendor@1001 : Record 23;
      DimensionManagement@1004 : Codeunit 408;
      TableID@1002 : ARRAY [10] OF Integer;
      No@1003 : ARRAY [10] OF Code[20];
    BEGIN
      Vendor.GET(VendNo);
      TableID[1] := DATABASE::Vendor;
      No[1] := Vendor."No.";
      IF NOT DimensionManagement.CheckDimValuePosting(TableID,No,PurchInvHeader."Dimension Set ID") THEN
        ErrorHelperAccount(ErrorType::DimErr,Vendor.TABLECAPTION,Vendor."No.",Vendor."No.",Vendor.Name);
    END;

    LOCAL PROCEDURE TestPurchaseLines@6(PurchInvHeader@1000 : Record 122);
    VAR
      PurchRcptLine@1004 : Record 121;
      PurchInvLine@1001 : Record 123;
      Item@1002 : Record 27;
      DimensionManagement@1007 : Codeunit 408;
      ReceivedQtyNoReturned@1005 : Decimal;
      RevUnitCostLCY@1006 : Decimal;
      TableID@1013 : ARRAY [10] OF Integer;
      No@1014 : ARRAY [10] OF Code[20];
    BEGIN
      PurchInvLine.SETRANGE("Document No.",PurchInvHeader."No.");
      IF PurchInvLine.FIND('-') THEN
        REPEAT
          IF NOT IsCommentLine(PurchInvLine) THEN BEGIN
            IF PurchRcptLine.GET(PurchInvLine."Receipt No.",PurchInvLine."Receipt Line No.") THEN BEGIN
              IF PurchRcptLine."Order No." <> '' THEN
                ErrorHelperLine(ErrorType::LineFromOrder,PurchInvLine);
            END;

            IF (NOT PurchInvLine.IsCancellationSupported) AND NotInvRndAccount(PurchInvHeader."Vendor Posting Group",PurchInvLine) THEN
              ErrorHelperLine(ErrorType::WrongItemType,PurchInvLine);

            IF PurchInvLine.Type = PurchInvLine.Type::Item THEN BEGIN
              Item.GET(PurchInvLine."No.");

              IF Item.IsInventoriableType THEN
                IF (PurchInvLine.Quantity > 0) AND (PurchInvLine."Job No." = '') AND WasNotCancelled(PurchInvHeader."No.") THEN BEGIN
                  PurchInvLine.CalcReceivedPurchNotReturned(ReceivedQtyNoReturned,RevUnitCostLCY,FALSE);
                  IF PurchInvLine.Quantity <> ReceivedQtyNoReturned THEN
                    ErrorHelperLine(ErrorType::ItemIsReturned,PurchInvLine);
                END;

              IF Item.Blocked THEN
                ErrorHelperLine(ErrorType::ItemBlocked,PurchInvLine);

              TableID[1] := DATABASE::Item;
              No[1] := PurchInvLine."No.";
              IF NOT DimensionManagement.CheckDimValuePosting(TableID,No,PurchInvLine."Dimension Set ID") THEN
                ErrorHelperAccount(ErrorType::DimErr,Item.TABLECAPTION,No[1],Item."No.",Item.Description);

              IF Item.IsInventoriableType THEN
                TestInventoryPostingSetup(PurchInvLine);

              TestGenPostingSetup(PurchInvLine);
              TestVendorPostingGroup(PurchInvLine,PurchInvHeader."Vendor Posting Group");
              TestVATPostingSetup(PurchInvLine);

              IF NOT DimensionManagement.CheckDimIDComb(PurchInvLine."Dimension Set ID") THEN
                ErrorHelperLine(ErrorType::DimCombErr,PurchInvLine);
            END;
          END;
        UNTIL PurchInvLine.NEXT = 0;
    END;

    LOCAL PROCEDURE TestGLAccount@21(AccountNo@1000 : Code[20];PurchInvLine@1005 : Record 123);
    VAR
      GLAccount@1001 : Record 15;
      Item@1006 : Record 27;
      DimensionManagement@1004 : Codeunit 408;
      TableID@1002 : ARRAY [10] OF Integer;
      No@1003 : ARRAY [10] OF Code[20];
    BEGIN
      GLAccount.GET(AccountNo);
      IF GLAccount.Blocked THEN
        ErrorHelperAccount(ErrorType::AccountBlocked,GLAccount.TABLECAPTION,AccountNo,'','');
      TableID[1] := DATABASE::"G/L Account";
      No[1] := AccountNo;
      Item.GET(PurchInvLine."No.");
      IF NOT DimensionManagement.CheckDimValuePosting(TableID,No,PurchInvLine."Dimension Set ID") THEN
        ErrorHelperAccount(ErrorType::DimErr,GLAccount.TABLECAPTION,AccountNo,Item."No.",Item.Description);
    END;

    LOCAL PROCEDURE TestIfInvoiceIsPaid@7(PurchInvHeader@1000 : Record 122);
    BEGIN
      PurchInvHeader.CALCFIELDS("Amount Including VAT");
      PurchInvHeader.CALCFIELDS("Remaining Amount");
      IF PurchInvHeader."Amount Including VAT" <> PurchInvHeader."Remaining Amount" THEN
        ErrorHelperHeader(ErrorType::IsPaid,PurchInvHeader);
    END;

    LOCAL PROCEDURE TestIfInvoiceIsCorrectedOnce@10(PurchInvHeader@1000 : Record 122);
    VAR
      CancelledDocument@1001 : Record 1900;
    BEGIN
      IF CancelledDocument.FindPurchCancelledInvoice(PurchInvHeader."No.") THEN
        ErrorHelperHeader(ErrorType::IsCorrected,PurchInvHeader);
    END;

    LOCAL PROCEDURE TestIfInvoiceIsNotCorrectiveDoc@32(PurchInvHeader@1000 : Record 122);
    VAR
      CancelledDocument@1001 : Record 1900;
    BEGIN
      IF CancelledDocument.FindPurchCorrectiveInvoice(PurchInvHeader."No.") THEN
        ErrorHelperHeader(ErrorType::IsCorrective,PurchInvHeader);
    END;

    LOCAL PROCEDURE TestIfPostingIsAllowed@12(PurchInvHeader@1000 : Record 122);
    VAR
      GenJnlCheckLine@1001 : Codeunit 11;
    BEGIN
      IF GenJnlCheckLine.DateNotAllowed(PurchInvHeader."Posting Date") THEN
        ErrorHelperHeader(ErrorType::PostingNotAllowed,PurchInvHeader);
    END;

    LOCAL PROCEDURE TestIfAnyFreeNumberSeries@14(PurchInvHeader@1003 : Record 122);
    VAR
      PurchasesPayablesSetup@1001 : Record 312;
      NoSeriesManagement@1002 : Codeunit 396;
      PostingDate@1000 : Date;
    BEGIN
      PostingDate := WORKDATE;
      PurchasesPayablesSetup.GET;

      IF NoSeriesManagement.TryGetNextNo(PurchasesPayablesSetup."Credit Memo Nos.",PostingDate) = '' THEN
        ErrorHelperHeader(ErrorType::SerieNumCM,PurchInvHeader);

      IF NoSeriesManagement.TryGetNextNo(PurchasesPayablesSetup."Posted Credit Memo Nos.",PostingDate) = '' THEN
        ErrorHelperHeader(ErrorType::SerieNumPostCM,PurchInvHeader);

      IF (NOT CancellingOnly) AND (NoSeriesManagement.TryGetNextNo(PurchasesPayablesSetup."Invoice Nos.",PostingDate) = '') THEN
        ErrorHelperHeader(ErrorType::SerieNumInv,PurchInvHeader);
    END;

    LOCAL PROCEDURE TestExternalDocument@8(PurchInvHeader@1000 : Record 122);
    VAR
      PurchasesPayablesSetup@1001 : Record 312;
    BEGIN
      PurchasesPayablesSetup.GET;
      IF (PurchInvHeader."Vendor Invoice No." = '') AND PurchasesPayablesSetup."Ext. Doc. No. Mandatory" THEN
        ErrorHelperHeader(ErrorType::ExtDocErr,PurchInvHeader);
    END;

    LOCAL PROCEDURE TestInventoryPostingClosed@26(PurchInvHeader@1000 : Record 122);
    VAR
      InventoryPeriod@1001 : Record 5814;
    BEGIN
      InventoryPeriod.SETRANGE(Closed,TRUE);
      InventoryPeriod.SETFILTER("Ending Date",'>=%1',PurchInvHeader."Posting Date");
      IF InventoryPeriod.FINDFIRST THEN
        ErrorHelperHeader(ErrorType::InventoryPostClosed,PurchInvHeader);
    END;

    LOCAL PROCEDURE TestGenPostingSetup@20(PurchInvLine@1000 : Record 123);
    VAR
      GenPostingSetup@1001 : Record 252;
    BEGIN
      PurchasesPayablesSetup.GetRecordOnce;

      WITH GenPostingSetup DO BEGIN
        GET(PurchInvLine."Gen. Bus. Posting Group",PurchInvLine."Gen. Prod. Posting Group");
        TESTFIELD("Purch. Account");
        TestGLAccount("Purch. Account",PurchInvLine);
        TESTFIELD("Purch. Credit Memo Account");
        TestGLAccount("Purch. Credit Memo Account",PurchInvLine);
        TESTFIELD("Direct Cost Applied Account");
        TestGLAccount("Direct Cost Applied Account",PurchInvLine);
        IF PurchasesPayablesSetup."Discount Posting" <> PurchasesPayablesSetup."Discount Posting"::"No Discounts" THEN BEGIN
          TESTFIELD("Purch. Line Disc. Account");
          TestGLAccount("Purch. Line Disc. Account",PurchInvLine);
        END;
      END;
    END;

    LOCAL PROCEDURE TestVendorPostingGroup@27(PurchInvLine@1000 : Record 123;VendorPostingGr@1002 : Code[20]);
    VAR
      VendorPostingGroup@1001 : Record 93;
    BEGIN
      WITH VendorPostingGroup DO BEGIN
        GET(VendorPostingGr);
        TESTFIELD("Payables Account");
        TestGLAccount("Payables Account",PurchInvLine);
      END;
    END;

    LOCAL PROCEDURE TestVATPostingSetup@29(PurchInvLine@1000 : Record 123);
    VAR
      VATPostingSetup@1001 : Record 325;
    BEGIN
      WITH VATPostingSetup DO BEGIN
        GET(PurchInvLine."VAT Bus. Posting Group",PurchInvLine."VAT Prod. Posting Group");
        IF "VAT Calculation Type" <> "VAT Calculation Type"::"Sales Tax" THEN BEGIN
          TESTFIELD("Purchase VAT Account");
          TestGLAccount("Purchase VAT Account",PurchInvLine);
        END;
      END;
    END;

    LOCAL PROCEDURE TestInventoryPostingSetup@39(PurchInvLine@1000 : Record 123);
    VAR
      InventoryPostingSetup@1001 : Record 5813;
    BEGIN
      WITH InventoryPostingSetup DO BEGIN
        GET(PurchInvLine."Location Code",PurchInvLine."Posting Group");
        TESTFIELD("Inventory Account");
        TestGLAccount("Inventory Account",PurchInvLine);
      END;
    END;

    LOCAL PROCEDURE IsCommentLine@18(PurchInvLine@1000 : Record 123) : Boolean;
    BEGIN
      EXIT((PurchInvLine.Type = PurchInvLine.Type::" ") OR (PurchInvLine."No." = ''));
    END;

    LOCAL PROCEDURE WasNotCancelled@31(InvNo@1000 : Code[20]) : Boolean;
    VAR
      PurchCrMemoHdr@1001 : Record 124;
    BEGIN
      PurchCrMemoHdr.SETRANGE("Applies-to Doc. Type",PurchCrMemoHdr."Applies-to Doc. Type"::Invoice);
      PurchCrMemoHdr.SETRANGE("Applies-to Doc. No.",InvNo);
      EXIT(PurchCrMemoHdr.ISEMPTY);
    END;

    LOCAL PROCEDURE NotInvRndAccount@19(VendorPostingGroupCode@1000 : Code[20];PurchInvLine@1003 : Record 123) : Boolean;
    VAR
      VendorPostingGroup@1001 : Record 93;
    BEGIN
      IF PurchInvLine.Type <> PurchInvLine.Type::"G/L Account" THEN
        EXIT(TRUE);

      VendorPostingGroup.GET(VendorPostingGroupCode);
      EXIT((VendorPostingGroup."Invoice Rounding Account" <> PurchInvLine."No.") OR (NOT PurchInvLine."System-Created Entry"));
    END;

    LOCAL PROCEDURE UnapplyCostApplication@25(VAR ItemJnlPostLine@1001 : Codeunit 22;InvNo@1000 : Code[20]) : Boolean;
    VAR
      TempItemLedgEntry@1003 : TEMPORARY Record 32;
      TempItemApplicationEntry@1002 : TEMPORARY Record 339;
    BEGIN
      FindItemLedgEntries(TempItemLedgEntry,InvNo);
      IF FindAppliedInbndEntries(TempItemApplicationEntry,TempItemLedgEntry) THEN BEGIN
        REPEAT
          ItemJnlPostLine.UnApply(TempItemApplicationEntry);
        UNTIL TempItemApplicationEntry.NEXT = 0;
        EXIT(TRUE);
      END;
    END;

    LOCAL PROCEDURE FindItemLedgEntries@30(VAR ItemLedgEntry@1002 : Record 32;InvNo@1000 : Code[20]);
    VAR
      PurchInvLine@1001 : Record 123;
    BEGIN
      WITH PurchInvLine DO BEGIN
        SETRANGE("Document No.",InvNo);
        SETRANGE(Type,Type::Item);
        IF FINDSET THEN
          REPEAT
            GetItemLedgEntries(ItemLedgEntry,FALSE);
          UNTIL NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE FindAppliedInbndEntries@28(VAR TempItemApplicationEntry@1000 : TEMPORARY Record 339;VAR ItemLedgEntry@1001 : Record 32) : Boolean;
    VAR
      ItemApplicationEntry@1002 : Record 339;
    BEGIN
      TempItemApplicationEntry.RESET;
      TempItemApplicationEntry.DELETEALL;
      IF ItemLedgEntry.FINDSET THEN
        REPEAT
          IF ItemApplicationEntry.AppliedOutbndEntryExists(ItemLedgEntry."Entry No.",TRUE,FALSE) THEN
            REPEAT
              TempItemApplicationEntry := ItemApplicationEntry;
              IF NOT TempItemApplicationEntry.FIND THEN
                TempItemApplicationEntry.INSERT;
            UNTIL ItemApplicationEntry.NEXT = 0;
        UNTIL ItemLedgEntry.NEXT = 0;
      EXIT(TempItemApplicationEntry.FINDSET);
    END;

    LOCAL PROCEDURE ErrorHelperHeader@22(ErrorOption@1000 : Option;PurchInvHeader@1001 : Record 122);
    VAR
      Vendor@1003 : Record 23;
    BEGIN
      IF CancellingOnly THEN
        CASE ErrorOption OF
          ErrorType::IsPaid:
            ERROR(PostedInvoiceIsPaidCCancelErr);
          ErrorType::VendorBlocked:
            BEGIN
              Vendor.GET(PurchInvHeader."Pay-to Vendor No.");
              ERROR(VendorIsBlockedCancelErr,Vendor.Name);
            END;
          ErrorType::IsCorrected:
            ERROR(AlreadyCancelledErr);
          ErrorType::IsCorrective:
            ERROR(CancelCorrectiveDocErr);
          ErrorType::SerieNumInv:
            ERROR(NoFreeInvoiceNoSeriesCancelErr);
          ErrorType::SerieNumCM:
            ERROR(NoFreeCMSeriesCancelErr);
          ErrorType::SerieNumPostCM:
            ERROR(NoFreePostCMSeriesCancelErr);
          ErrorType::FromOrder:
            ERROR(InvoiceIsBasedOnOrderCancelErr);
          ErrorType::PostingNotAllowed:
            ERROR(PostingNotAllowedCancelErr);
          ErrorType::ExtDocErr:
            ERROR(ExternalDocCancelErr);
          ErrorType::InventoryPostClosed:
            ERROR(InventoryPostClosedCancelErr);
          ErrorType::DimCombHeaderErr:
            ERROR(InvalidDimCombHeaderCancelErr);
        END
      ELSE
        CASE ErrorOption OF
          ErrorType::IsPaid:
            ERROR(PostedInvoiceIsPaidCorrectErr);
          ErrorType::VendorBlocked:
            BEGIN
              Vendor.GET(PurchInvHeader."Pay-to Vendor No.");
              ERROR(VendorIsBlockedCorrectErr,Vendor.Name);
            END;
          ErrorType::IsCorrected:
            ERROR(AlreadyCorrectedErr);
          ErrorType::IsCorrective:
            ERROR(CorrCorrectiveDocErr);
          ErrorType::SerieNumInv:
            ERROR(NoFreeInvoiceNoSeriesCorrectErr);
          ErrorType::SerieNumPostCM:
            ERROR(NoFreePostCMSeriesCorrectErr);
          ErrorType::SerieNumCM:
            ERROR(NoFreeCMSeriesCorrectErr);
          ErrorType::FromOrder:
            ERROR(InvoiceIsBasedOnOrderCorrectErr);
          ErrorType::PostingNotAllowed:
            ERROR(PostingNotAllowedCorrectErr);
          ErrorType::ExtDocErr:
            ERROR(ExternalDocCorrectErr);
          ErrorType::InventoryPostClosed:
            ERROR(InventoryPostClosedCorrectErr);
          ErrorType::DimCombHeaderErr:
            ERROR(InvalidDimCombHeaderCorrectErr);
        END;
    END;

    LOCAL PROCEDURE ErrorHelperLine@23(ErrorOption@1000 : Option;PurchInvLine@1001 : Record 123);
    VAR
      Item@1002 : Record 27;
    BEGIN
      IF CancellingOnly THEN
        CASE ErrorOption OF
          ErrorType::ItemBlocked:
            BEGIN
              Item.GET(PurchInvLine."No.");
              ERROR(ItemIsBlockedCancelErr,Item."No.",Item.Description);
            END;
          ErrorType::ItemIsReturned:
            BEGIN
              Item.GET(PurchInvLine."No.");
              ERROR(ShippedQtyReturnedCancelErr,Item."No.",Item.Description);
            END;
          ErrorType::LineFromOrder:
            ERROR(PurchaseLineFromOrderCancelErr,PurchInvLine."No.",PurchInvLine.Description);
          ErrorType::WrongItemType:
            ERROR(LineTypeNotAllowedCancelErr,PurchInvLine."No.",PurchInvLine.Description,PurchInvLine.Type);
          ErrorType::LineFromJob:
            ERROR(UsedInJobCancelErr,PurchInvLine."No.",PurchInvLine.Description);
          ErrorType::DimCombErr:
            ERROR(InvalidDimCombinationCancelErr,PurchInvLine."No.",PurchInvLine.Description);
        END
      ELSE
        CASE ErrorOption OF
          ErrorType::ItemBlocked:
            BEGIN
              Item.GET(PurchInvLine."No.");
              ERROR(ItemIsBlockedCorrectErr,Item."No.",Item.Description);
            END;
          ErrorType::ItemIsReturned:
            BEGIN
              Item.GET(PurchInvLine."No.");
              ERROR(ShippedQtyReturnedCorrectErr,Item."No.",Item.Description);
            END;
          ErrorType::LineFromOrder:
            ERROR(PurchaseLineFromOrderCorrectErr,PurchInvLine."No.",PurchInvLine.Description);
          ErrorType::WrongItemType:
            ERROR(LineTypeNotAllowedCorrectErr,PurchInvLine."No.",PurchInvLine.Description,PurchInvLine.Type);
          ErrorType::LineFromJob:
            ERROR(UsedInJobCorrectErr,PurchInvLine."No.",PurchInvLine.Description);
          ErrorType::DimCombErr:
            ERROR(InvalidDimCombinationCorrectErr,PurchInvLine."No.",PurchInvLine.Description);
        END;
    END;

    LOCAL PROCEDURE ErrorHelperAccount@24(ErrorOption@1000 : Option;AccountNo@1001 : Code[20];AccountCaption@1002 : Text;No@1003 : Code[20];Name@1004 : Text);
    BEGIN
      IF CancellingOnly THEN
        CASE ErrorOption OF
          ErrorType::AccountBlocked:
            ERROR(AccountIsBlockedCancelErr,AccountCaption,AccountNo);
          ErrorType::DimErr:
            ERROR(InvalidDimCodeCancelErr,AccountCaption,AccountNo,No,Name);
        END
      ELSE
        CASE ErrorOption OF
          ErrorType::AccountBlocked:
            ERROR(AccountIsBlockedCorrectErr,AccountCaption,AccountNo);
          ErrorType::DimErr:
            ERROR(InvalidDimCodeCorrectErr,AccountCaption,AccountNo,No,Name);
        END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCreateCorrectivePurchCrMemo@34(PurchInvHeader@1000 : Record 122;VAR PurchaseHeader@1001 : Record 38;VAR CancellingOnly@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterTestCorrectInvoiceIsAllowed@33(VAR PurchInvHeader@1000 : Record 122;Cancelling@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePurchaseHeaderInsert@17(VAR PurchaseHeader@1000 : Record 38;PurchInvHeader@1001 : Record 122);
    BEGIN
    END;

    BEGIN
    END.
  }
}

