OBJECT Report 11125706 Update Proj. Activ. Cap. Plan.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Aktualisieren Projektaktivit„t-Kapazit„tsplanung;
               ENU=Update Project Activity Capacity Planning;
               NLD=Bijwerken projectactiviteit-capaciteitsplanning;
               NOR=Oppdater kapasitetsplanlegging for prosjektaktivitet;
               SVE=Uppdatera kapacitetsplanering f”r projektaktivitet];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  Window.OPEN(STRSUBSTNO('%1 #3##########\%2 #4##########',
                    "Project Planning Activity".FIELDCAPTION("Project No."), "Project Planning Activity".FIELDCAPTION(Code)));
                END;

    OnPostReport=BEGIN
                   Window.CLOSE;
                 END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 8019;    ;DataItem;                    ;
               DataItemTable=Table11072003;
               DataItemTableView=SORTING(No.)
                                 WHERE(Project Status=FILTER(Preparation|Production));
               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(3, "No.");
                                  Window.UPDATE(4, '');
                                END;

               ReqFilterFields=No. }

    { 2271;1   ;DataItem;                    ;
               DataItemTable=Table11012431;
               DataItemTableView=SORTING(Project No.,Code);
               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(4, Code);
                                END;

               DataItemLink=Project No.=FIELD(No.) }

    { 6269;2   ;DataItem;                    ;
               DataItemTable=Table11125708;
               DataItemTableView=SORTING(Project No.,Activity Code,Line No.);
               OnPreDataItem=BEGIN
                               UpdateActivity := FALSE;
                             END;

               OnAfterGetRecord=BEGIN
                                  DeleteRecord := FALSE;
                                  CASE "Resource Type" OF
                                    "Resource Type"::TeamType:
                                      IF NOT TeamTypeRec.GET("Resource Code") THEN
                                        DeleteRecord := TRUE;
                                    "Resource Type"::Discipline:
                                      IF NOT DisciplineRec.GET("Resource Code") THEN
                                        DeleteRecord := TRUE;
                                    "Resource Type"::Plant:
                                      IF NOT PlantTypeRec.GET("Resource Code") THEN
                                        DeleteRecord := TRUE;
                                  END;

                                  ReqActCapRec.COPY("Requested Activity Capacity");
                                  IF DeleteRecord THEN
                                    ReqActCapRec.DELETE(TRUE)   //* TRUE !
                                  ELSE BEGIN
                                    ReqActCapMgt.UpdateReqActCapByDay(ReqActCapRec, TRUE);
                                    ReqActCapRec.MODIFY(FALSE)  //* FALSE !
                                  END;
                                  IF NOT UpdateActivity THEN
                                    UpdateActivity := TRUE;
                                END;

               OnPostDataItem=BEGIN
                                IF UpdateActivity THEN BEGIN
                                  CLEAR(ReqActCapRec);
                                  ReqActCapRec."Project No." := "Project Planning Activity"."Project No.";
                                  ReqActCapRec."Activity Code" := "Project Planning Activity".Code;
                                  ReqActCapRec."Line No." := 0;
                                  ReqActCapMgt.UpdateProjActivityPeriod(ReqActCapRec, 0);  //* 0: AfterUpdate (for this mode only project and activity needed)
                                END;
                              END;

               DataItemLink=Project No.=FIELD(Project No.),
                            Activity Code=FIELD(Code) }

    { 1168;2   ;DataItem;                    ;
               DataItemTable=Table11125710;
               DataItemTableView=SORTING(Project No.,Activity Code,Line No. Request,Line No.);
               OnAfterGetRecord=BEGIN
                                  //Do not process records that are not linked to a Requested Activity Capacity
                                  //record, because these records do not belong to the capacity planning.
                                  IF "Line No. Request" = 0 THEN
                                    CurrReport.SKIP;

                                  DeleteRecord := FALSE;
                                  CASE "Resource Type" OF
                                    "Resource Type"::Team:
                                      IF NOT TeamRec.GET("Resource Code") THEN
                                        DeleteRecord := TRUE;
                                    "Resource Type"::Employee:
                                      IF NOT EmplRec.GET("Resource Code") THEN
                                        DeleteRecord := TRUE;
                                    "Resource Type"::Plant:
                                      IF NOT PlantNoRec.GET("Resource Code", "Plant No.") THEN
                                        DeleteRecord := TRUE;
                                  END;
                                  IF NOT DeleteRecord THEN BEGIN
                                    IF NOT ReqActCapRec.GET("Project No.", "Activity Code", "Line No. Request") THEN
                                      DeleteRecord := TRUE;
                                  END;

                                  AssignActCapRec.COPY("Assigned Activity Capacity");
                                  IF DeleteRecord THEN
                                    AssignActCapRec.DELETE(TRUE)   //* TRUE !
                                  ELSE BEGIN
                                    AssignActCapMgt.UpdateAssignActCapByDay(AssignActCapRec);
                                    AssignActCapRec.MODIFY(FALSE)  //* FALSE !
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                COMMIT;
                              END;

               DataItemLink=Project No.=FIELD(Project No.),
                            Activity Code=FIELD(Code) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ReqActCapRec@1100525006 : Record 11125708;
      AssignActCapRec@1100525010 : Record 11125710;
      TeamTypeRec@1100525003 : Record 11125706;
      TeamRec@1100525014 : Record 11012437;
      DisciplineRec@1100525002 : Record 11020204;
      EmplRec@1100525012 : Record 5200;
      PlantTypeRec@1100525001 : Record 11012551;
      PlantNoRec@1100525013 : Record 11012552;
      ReqActCapMgt@1100525000 : Codeunit 11125707;
      AssignActCapMgt@1100525011 : Codeunit 11125708;
      Window@1100525007 : Dialog;
      UpdateActivity@1100525005 : Boolean;
      DeleteRecord@1100525004 : Boolean;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

