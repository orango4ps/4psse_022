OBJECT Page 11132112 Project Document Relations
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Belegbeziehungen;
               ENU=Document Relations;
               NLD=Document relaties;
               NOR=Dokumentrelasjoner;
               SVE=Dokumentrelationer];
    SourceTable=Table11071833;
    DataCaptionFields=Source Type,No.;
    PageType=List;
    OnNewRecord=BEGIN
                  "Send by E-Mail" := TRUE;
                  Print := TRUE;
                END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 1100525000;1 ;ActionGroup;
                      CaptionML=[DEU=Beleg;
                                 ENU=Document;
                                 NLD=Document;
                                 NOR=Dokument;
                                 SVE=Dokument] }
      { 1100525001;2 ;Action    ;
                      ShortCutKey=Shift+F7;
                      CaptionML=[DEU=Karte;
                                 ENU=Card;
                                 NLD=Kaart;
                                 NOR=Kort;
                                 SVE=Kort];
                      Promoted=Yes;
                      Image=EditLines;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 DocumentProperties@1100525000 : Record 11012746;
                                 PageWordDocument@1210190002 : Page 11012406;
                                 PageExternalDocument@1210190001 : Page 11012417;
                               BEGIN
                                 DocumentProperties.SETRANGE("No.", "Related Document No.");
                                 DocumentProperties.FINDFIRST;
                                 IF NOT DocumentProperties."External Document" THEN BEGIN
                                   CLEAR(PageWordDocument);
                                   PageWordDocument.SETTABLEVIEW(DocumentProperties);
                                   PageWordDocument.SETRECORD(DocumentProperties);
                                   PageWordDocument.RUNMODAL;
                                 END ELSE BEGIN
                                   CLEAR(PageExternalDocument);
                                   PageExternalDocument.SETTABLEVIEW(DocumentProperties);
                                   PageExternalDocument.SETRECORD(DocumentProperties);
                                   PageExternalDocument.RUNMODAL;
                                 END;
                               END;
                                }
      { 1100525002;2 ;Action    ;
                      ShortCutKey=Return;
                      CaptionML=[DEU=ôffnen;
                                 ENU=Open;
                                 NLD=Open;
                                 NOR=èpne;
                                 SVE=ôppna];
                      Promoted=Yes;
                      Image=ViewPage;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 DocumentProperties@1100525000 : Record 11012746;
                               BEGIN
                                 DocumentProperties.GET("Related Document No.");
                                 IF DocumentProperties.IsConfidentialOwner() THEN
                                   DocumentProperties.OpenDocument();
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100529601;1 ;Action    ;
                      CaptionML=[DEU=Alle verknÅpften Belege hinzufÅgen;
                                 ENU=Add all linked documents;
                                 NLD=Voeg alle gekoppelde documenten toe];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Add;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 AddAllLinkedDocuments;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;;Container;
                ContainerType=ContentArea }

    { 1210190000;1;Group  ;
                GroupType=Repeater }

    { 1210190001;2;Field  ;
                SourceExpr="Related Document No.";
                OnLookup=VAR
                           ProjDocumentRelationRecRef@1100528705 : RecordRef;
                           DocumentList@1100525001 : Page 11012407;
                           DummyRID@1100525005 : RecordID;
                         BEGIN
                           ProjDocumentRelationRecRef.GETTABLE(Rec);

                           DocumentList.SetVars(DummyRID, '', TRUE, TRUE);
                           DocumentList.SetContact('', '');
                           DocumentList.SetDocumentRelationRecRef(ProjDocumentRelationRecRef);
                           DocumentList.RUNMODAL;

                           CurrPage.UPDATE;
                         END;
                          }

    { 1210190003;2;Field  ;
                SourceExpr=Description }

    { 1210190005;2;Field  ;
                SourceExpr="Description 2" }

    { 1210190007;2;Field  ;
                DrillDown=No;
                SourceExpr="Document Description" }

    { 1210190018;2;Field  ;
                BlankZero=Yes;
                SourceExpr=Quantity }

    { 1100525003;2;Field  ;
                SourceExpr="Send by E-Mail" }

    { 1100409000;2;Field  ;
                SourceExpr=Print }

    { 1210190009;2;Field  ;
                Lookup=No;
                DrillDown=No;
                SourceExpr="Document No." }

    { 1210190021;2;Field  ;
                DrillDown=No;
                SourceExpr="Document Date" }

    { 1210190011;2;Field  ;
                DrillDown=No;
                SourceExpr="Creation date" }

    { 1210190023;2;Field  ;
                SourceExpr="Modification Date" }

    { 1199197400;2;Field  ;
                SourceExpr="Seq. No.";
                Visible=FALSE }

  }
  CODE
  {

    LOCAL PROCEDURE AddAllLinkedDocuments@1100529601();
    VAR
      ExtensionContract@1100529603 : Record 11012004;
      Plot@1100529618 : Record 11012500;
      DocumentProperties@1100529600 : Record 11012746;
      ProjectDocumentRelation@1100529601 : Record 11071833;
      DocumentLinkManagement@1100529604 : Codeunit 11012401;
      RecRef@1100529602 : RecordRef;
    BEGIN
      DocumentProperties.RESET;

      CASE "Source Type" OF
        "Source Type"::"Extension Contract":
          BEGIN
            IF NOT ExtensionContract.GET("Project No.", "No.") THEN
              EXIT;
            RecRef.GETTABLE(ExtensionContract);
          END;
        "Source Type"::Plot:
          BEGIN
            IF NOT Plot.GET("Project No.", "No.") THEN
              EXIT;
            RecRef.GETTABLE(Plot);
          END;
        ELSE
          EXIT;
      END;

      DocumentLinkManagement.GetDocuments(DocumentProperties, RecRef);

      DocumentProperties.RemovePrevVersions(DocumentProperties);
      DocumentProperties.MARKEDONLY(TRUE);
      IF DocumentProperties.ISEMPTY THEN
        EXIT;

      DocumentProperties.FINDSET;
      REPEAT
        IF NOT ProjectDocumentRelation.GET("Source Type", "Project No.", "No.", DocumentProperties."No.") THEN BEGIN
          ProjectDocumentRelation.INIT;
          ProjectDocumentRelation."Source Type" := "Source Type";
          ProjectDocumentRelation."Project No." := "Project No.";
          ProjectDocumentRelation."No." := "No.";
          ProjectDocumentRelation.VALIDATE("Related Document No.", DocumentProperties."No.");
          ProjectDocumentRelation."Document No." := DocumentProperties."Document No.";
          ProjectDocumentRelation."Send by E-Mail" := TRUE;
          IF DocumentProperties.FileCanBeConvertedToPDF THEN
            ProjectDocumentRelation.Print := TRUE;
          ProjectDocumentRelation.INSERT;
        END;
      UNTIL DocumentProperties.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

