OBJECT Codeunit 11229309 Consumed Web Service Runtime
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            IF (IsDsp) THEN
              RunDsp
            ELSE
              RunNormal;
          END;

  }
  CODE
  {
    VAR
      ConsumedWebService@1100528304 : Record 11229797;
      ConsumedWebServiceLine@1100528303 : Record 11229798;
      ConsumedWebServiceMgt@1100528308 : Codeunit 11229310;
      HttpWebRequestMgt@1100528309 : Codeunit 1297;
      IsDsp@1100528306 : Boolean;
      GlobalUrl@1100528307 : Text;
      RequestXML@1100528301 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      ResponseXML@1100528300 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      HTTPWebRequest@1100528302 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpWebRequest";
      HTTPWebResponse@1100528305 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpWebResponse";
      HttpStatusCode@1100528310 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpStatusCode";
      ResponseHeaders@1100528311 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.Specialized.NameValueCollection";

    [External]
    PROCEDURE GetRuntime@1100528300(VAR ResponseXMLOut@1100528301 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";VAR HTTPWebResponseOut@1100528300 : DotNet "'System, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpWebResponse");
    BEGIN
      ResponseXMLOut := ResponseXML;
      HTTPWebResponseOut := HTTPWebResponse;
    END;

    [External]
    PROCEDURE SetDspRuntime@1100528303(IsDspIn@1100528300 : Boolean;ConsumedWebServiceLineIn@1100528301 : Record 11229798;RequestXMLIn@1100528302 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument");
    BEGIN
      IsDsp := IsDspIn;
      RequestXML := RequestXMLIn;
      ConsumedWebService.GET(ConsumedWebServiceLineIn."Web Service Code");
      ConsumedWebServiceLine := ConsumedWebServiceLineIn;
    END;

    [External]
    PROCEDURE SetRuntime@1100528301(ConsumedWebServiceIn@1100528306 : Record 11229797;ConsumedWebServiceLineIn@1100528307 : Record 11229798;RequestXMLIn@1100528305 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument");
    BEGIN
      RequestXML := RequestXMLIn;
      ConsumedWebService.COPY(ConsumedWebServiceIn);
      ConsumedWebServiceLine.COPY(ConsumedWebServiceLineIn);
    END;

    [External]
    PROCEDURE SetUrl@1100528307(UrlIn@1100528300 : Text);
    BEGIN
      GlobalUrl := UrlIn;
    END;

    LOCAL PROCEDURE "---Locals---"@1100528304();
    BEGIN
    END;

    LOCAL PROCEDURE CreateCustomHeader@1100528302();
    VAR
      TempBlob@1100528301 : TEMPORARY Record 99008535;
      FileManagement@1100528300 : Codeunit 419;
      XMLDOMManagement4PS@1100528304 : Codeunit 11020220;
      ClientFileName@1100528302 : Text;
      CustomHeaderXML@1100528303 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      RequestRootNode@1100528305 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      FoundNode@1100528306 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      ConsumedWebService.CALCFIELDS("Custom SOAP Header XML");
      IF (NOT ConsumedWebService."Custom SOAP Header XML".HASVALUE) THEN
        EXIT;

      TempBlob.INIT;
      TempBlob.Blob := ConsumedWebService."Custom SOAP Header XML";
      ClientFileName := FileManagement.ClientTempFileName('xml');
      FileManagement.BLOBExport4PS(TempBlob, ClientFileName, FALSE, FALSE);
      IF (ISNULL(CustomHeaderXML)) THEN
        CustomHeaderXML := CustomHeaderXML.XmlDocument;
      XMLDOMManagement4PS.LoadXMLFromClientFile(CustomHeaderXML, ClientFileName, TRUE);

      //XMLDOMManagement4PS.FindNode(RequestXML, './/*[local-name()="."]', RequestRootNode);
      XMLDOMManagement4PS.FindNode(RequestXML, '.', RequestRootNode); //RequestRootNode.Type = XML.Document
      RequestRootNode := RequestRootNode.FirstChild; //RequestRootNode.Type = XML.Node


      //message('RequestXML: %1', RequestXML.OuterXml);
      //MESSAGE('1 RequestRootNode: \%1', RequestRootNode.OuterXml);

      XMLDOMManagement4PS.FindNode(CustomHeaderXML, '.', FoundNode);
      FoundNode := FoundNode.FirstChild;
      RequestRootNode.AppendChild(RequestXML.ImportNode(FoundNode, TRUE));

      {
      XMLDOMManagement4PS.FindNode(CustomHeaderXML, './/*[local-name()="CustomInfo"]', FoundNode);
      RequestRootNode.AppendChild(RequestXML.ImportNode(FoundNode, TRUE));
      XMLDOMManagement4PS.FindNode(CustomHeaderXML, './/*[local-name()="AuthenticationInfo"]', FoundNode);
      RequestRootNode.AppendChild(RequestXML.ImportNode(FoundNode, TRUE));
      }

      //MESSAGE('2 RequestRootNode: \%1', RequestRootNode.OuterXml);
    END;

    LOCAL PROCEDURE RunDsp@1100528305();
    VAR
      TempBlob@1100528301 : TEMPORARY Record 99008535;
      AuthorizationString@1100528302 : Text;
      ResponseStream@1100528304 : InStream;
      Url@1100528300 : Text;
    BEGIN
      IF (GlobalUrl = '') THEN
        Url := STRSUBSTNO('%1/%2',ConsumedWebService."Web Service URL",ConsumedWebServiceLine."Method Name")
      ELSE
        Url := GlobalUrl;

      HttpWebRequestMgt.Initialize(Url);
      HttpWebRequestMgt.AddBasicAuthentication(ConsumedWebService.Login,ConsumedWebService.Password);
      HttpWebRequestMgt.DisableUI;
      HttpWebRequestMgt.SetMethod('POST');
      HttpWebRequestMgt.SetProxy(ConsumedWebService."Web Proxy Address");//credentials??
      HttpWebRequestMgt.AddHeader('Accept-Encoding','gzip, deflate');
      HttpWebRequestMgt.AddHeader('Accept','');
      HttpWebRequestMgt.AddHeader('Content-Type',ConsumedWebService."Content Type");
      HttpWebRequestMgt.AddHeader('SOAPAction', ConsumedWebService."SOAP Action");
      TempBlob.WriteAsText(STRSUBSTNO('%1:%2',ConsumedWebService.Login,ConsumedWebService.Password),TEXTENCODING::Windows);
      AuthorizationString := STRSUBSTNO('Basic %1',TempBlob.ToBase64String);
      HttpWebRequestMgt.AddHeader('Authorization', AuthorizationString);
      HttpWebRequestMgt.AddBodyAsAsciiText(RequestXML.OuterXml);

      HttpWebRequestMgt.CreateInstream(ResponseStream);
      IF (NOT HttpWebRequestMgt.GetResponse(ResponseStream, HttpStatusCode, ResponseHeaders)) THEN
        HttpWebRequestMgt.ProcessFaultXMLResponse(ConsumedWebService."Support Info",
          ConsumedWebService."Fault Node", ConsumedWebService."Prefix Fault Namespace",
          ConsumedWebService."Fault Namespace");
      HttpWebRequestMgt.TryLoadXMLResponse(ResponseStream, ResponseXML);
    END;

    LOCAL PROCEDURE RunNormal@1100528306();
    VAR
      NetworkCredential@1100528303 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.NetworkCredential";
      NetworkCredentialCache@1100528302 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.CredentialCache";
      WebProxy@1100528301 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.WebProxy";
      MemoryStream@1100528300 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.MemoryStream";
    BEGIN
      CLEAR(HTTPWebRequest);
      CLEAR(HTTPWebResponse);
      CLEAR(ResponseXML);

      HTTPWebRequest := HTTPWebRequest.HttpWebRequest;
      HTTPWebResponse := HTTPWebResponse.HttpWebResponse;
      HTTPWebRequest := HTTPWebRequest.Create(ConsumedWebServiceMgt.GetWebServiceUrl(ConsumedWebService."Web Service URL", COMPANYNAME));
      ResponseXML := ResponseXML.XmlDocument;
      NetworkCredentialCache := NetworkCredentialCache.CredentialCache;

      HTTPWebRequest.Timeout := 30000;
      HTTPWebRequest.Method := 'post';
      IF (ConsumedWebService."Content Type" <> '') THEN
        HTTPWebRequest.ContentType := ConsumedWebService."Content Type";
      IF (ConsumedWebService."SOAP Action" <> '') THEN
        HTTPWebRequest.Headers.Add('SOAPAction', ConsumedWebService."SOAP Action" + ConsumedWebServiceLine."Method Name");
      IF (ConsumedWebService."Web Proxy Address" <> '') THEN BEGIN
        WebProxy := WebProxy.WebProxy(ConsumedWebService."Web Proxy Address");
        WebProxy.Credentials := NetworkCredentialCache.DefaultNetworkCredentials;
        HTTPWebRequest.Proxy := WebProxy;
      END;
      IF (NOT ConsumedWebService."Use Custom SOAP Header") THEN BEGIN
        IF (ConsumedWebService.Login <> '') THEN BEGIN
          NetworkCredential := NetworkCredential.NetworkCredential(ConsumedWebService.Login, ConsumedWebService.Password);
          HTTPWebRequest.Credentials := NetworkCredential;
        END;
      END;
      MemoryStream := HTTPWebRequest.GetRequestStream;
      RequestXML.Save(MemoryStream);
      MemoryStream.Flush;
      MemoryStream.Close;

      HTTPWebResponse := HTTPWebRequest.GetResponse;
      IF (NOT ISNULL(HTTPWebResponse)) THEN BEGIN
        MemoryStream := HTTPWebResponse.GetResponseStream;
        ResponseXML.Load(MemoryStream);
        MemoryStream.Flush;
        MemoryStream.Close;
      END ELSE
        HTTPWebResponse := HTTPWebResponse.HttpWebResponse;
    END;

    EVENT RequestXML@1100528301::NodeInserting@94(sender@1100528301 : Variant;e@1100528300 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT RequestXML@1100528301::NodeInserted@95(sender@1100528301 : Variant;e@1100528300 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT RequestXML@1100528301::NodeRemoving@96(sender@1100528301 : Variant;e@1100528300 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT RequestXML@1100528301::NodeRemoved@97(sender@1100528301 : Variant;e@1100528300 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT RequestXML@1100528301::NodeChanging@98(sender@1100528301 : Variant;e@1100528300 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT RequestXML@1100528301::NodeChanged@99(sender@1100528301 : Variant;e@1100528300 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT ResponseXML@1100528300::NodeInserting@94(sender@1100528301 : Variant;e@1100528300 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT ResponseXML@1100528300::NodeInserted@95(sender@1100528301 : Variant;e@1100528300 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT ResponseXML@1100528300::NodeRemoving@96(sender@1100528301 : Variant;e@1100528300 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT ResponseXML@1100528300::NodeRemoved@97(sender@1100528301 : Variant;e@1100528300 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT ResponseXML@1100528300::NodeChanging@98(sender@1100528301 : Variant;e@1100528300 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT ResponseXML@1100528300::NodeChanged@99(sender@1100528301 : Variant;e@1100528300 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    BEGIN
    END.
  }
}

