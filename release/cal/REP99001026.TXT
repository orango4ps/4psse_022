OBJECT Report 99001026 Replan Production Order
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04,4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=FA neu planen;
               ENU=Replan Production Order;
               NLD=Productieorder herplannen;
               NOR=Planlegg prod.ordre p† nytt;
               SVE=Omplanera produktionsorder];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   Direction := Direction::Backward;
                 END;

    OnPreReport=BEGIN
                  MfgSetup.GET;
                END;

  }
  DATASET
  {
    { 4824;    ;DataItem;                    ;
               DataItemTable=Table5405;
               DataItemTableView=SORTING(Status,No.)
                                 WHERE(Status=FILTER(..Released));
               OnPreDataItem=BEGIN
                               First := TRUE;
                               MfgSetup.GET;

                               Window.OPEN(
                                 Text000 +
                                 Text001 +
                                 Text002);
                             END;

               OnAfterGetRecord=BEGIN
                                  IF (CalcMethod = CalcMethod::"One level") AND NOT First THEN
                                    CurrReport.BREAK;

                                  Window.UPDATE(1,Status);
                                  Window.UPDATE(2,"No.");

                                  IF "Replan Ref. No." = '' THEN BEGIN
                                    "Replan Ref. No." := "No.";
                                    "Replan Ref. Status" := Status;
                                    MODIFY;
                                  END;
                                  IF First THEN BEGIN
                                    RESET;
                                    SETRANGE("Replan Ref. No.","Replan Ref. No.");
                                    SETRANGE("Replan Ref. Status","Replan Ref. Status");
                                    First := FALSE;
                                    IF CalcMethod <> CalcMethod::"No Levels" THEN
                                      DeleteProdOrders("Production Order","Low-Level Code",CalcMethod = CalcMethod::"All levels");
                                  END;

                                  DeleteUnreservedLowLevelProdOrderLines("Production Order");

                                  CreateProdOrderLines.CheckStructure(
                                    Status,"No.",Direction,TRUE,TRUE);
                                END;
                                 }

    { 3581;1   ;DataItem;                    ;
               DataItemTable=Table5406;
               DataItemTableView=SORTING(Status,Prod. Order No.,Planning Level Code);
               OnAfterGetRecord=VAR
                                  ProdOrderRouteMgt@1000 : Codeunit 99000772;
                                BEGIN
                                  BlockDynamicTracking(TRUE);
                                  IF "Routing No." = '' THEN BEGIN
                                    CalcProdOrder.BlockDynamicTracking(TRUE);
                                    CalcProdOrder.Recalculate("Prod. Order Line",Direction,TRUE);

                                    MODIFY;
                                  END ELSE
                                    ProdOrderRouteMgt.Calculate("Prod. Order Line");
                                END;

               DataItemLink=Status=FIELD(Status),
                            Prod. Order No.=FIELD(No.) }

    { 4877;2   ;DataItem;                    ;
               DataItemTable=Table5409;
               DataItemTableView=SORTING(Status,Prod. Order No.,Routing Reference No.,Routing No.,Operation No.);
               OnPreDataItem=BEGIN
                               IF Direction = Direction::Forward THEN
                                 SETCURRENTKEY(Status,"Prod. Order No.","Routing Reference No.","Routing No.","Sequence No. (Forward)")
                               ELSE
                                 SETCURRENTKEY(Status,"Prod. Order No.","Routing Reference No.","Routing No.","Sequence No. (Backward)");
                             END;

               OnAfterGetRecord=VAR
                                  ProdOrderLine@1000 : Record 5406;
                                  CalcProdOrderRtngLine@1001 : Codeunit 99000774;
                                  PlanLevel@1002 : Integer;
                                BEGIN
                                  IF "Routing Status" = "Routing Status"::Finished THEN
                                    CurrReport.SKIP;

                                  PlanLevel := "Prod. Order Line"."Planning Level Code";

                                  ProdOrderLine.SETCURRENTKEY(Status,"Prod. Order No.","Routing No.");
                                  ProdOrderLine.SETRANGE(Status,Status);
                                  ProdOrderLine.SETRANGE("Prod. Order No.","Prod. Order No.");
                                  ProdOrderLine.SETRANGE("Routing No.","Routing No.");
                                  IF ProdOrderLine.FIND('-') THEN
                                    REPEAT
                                      IF PlanLevel < ProdOrderLine."Planning Level Code" THEN
                                        PlanLevel := ProdOrderLine."Planning Level Code";
                                    UNTIL (ProdOrderLine.NEXT = 0) OR (PlanLevel > "Prod. Order Line"."Planning Level Code");

                                  ProdOrderLine.RESET;

                                  IF PlanLevel = "Prod. Order Line"."Planning Level Code" THEN BEGIN
                                    IF Direction = Direction::Forward THEN BEGIN
                                      "Starting Date" := "Prod. Order Line"."Starting Date";
                                      "Starting Time" := "Prod. Order Line"."Starting Time";
                                      MODIFY;
                                    END ELSE BEGIN
                                      "Ending Date" := "Prod. Order Line"."Ending Date";
                                      "Ending Time" := "Prod. Order Line"."Ending Time";
                                      MODIFY;
                                    END;
                                    CalcProdOrderRtngLine.CalculateRoutingLine("Prod. Order Routing Line",Direction,TRUE);
                                  END;
                                  MODIFY;
                                END;

               OnPostDataItem=BEGIN
                                CalcProdOrder.CalculateProdOrderDates("Prod. Order Line",TRUE);
                                OnAfterLastProdOrderRtngLine("Prod. Order Line");
                              END;

               DataItemLink=Status=FIELD(Status),
                            Prod. Order No.=FIELD(Prod. Order No.),
                            Routing No.=FIELD(Routing No.) }

    { 7771;2   ;DataItem;                    ;
               DataItemTable=Table5407;
               DataItemTableView=SORTING(Status,Prod. Order No.,Prod. Order Line No.,Line No.);
               OnPreDataItem=BEGIN
                               SETFILTER("Item No.",'<>%1','');
                             END;

               OnAfterGetRecord=VAR
                                  SKU@1001 : TEMPORARY Record 5700;
                                  StockkeepingUnit@1009 : Record 5700;
                                  CompItem@1000 : Record 27;
                                  MainProdOrder@1002 : Record 5405;
                                  ProdOrder@1003 : Record 5405;
                                  ProdOrderLine@1004 : Record 5406;
                                  AvailabilityMgt@1005 : Codeunit 5400;
                                  CreateProdOrderLines@1006 : Codeunit 99000787;
                                  GetPlanningParameters@1011 : Codeunit 99000855;
                                  InvtProfileOffsetting@1010 : Codeunit 99000854;
                                  ReqQty@1007 : Decimal;
                                  WithInventory@1008 : Boolean;
                                  IsHandled@1012 : Boolean;
                                BEGIN
                                  BlockDynamicTracking(TRUE);
                                  VALIDATE("Routing Link Code");
                                  MODIFY;

                                  CALCFIELDS("Reserved Qty. (Base)");
                                  IF "Reserved Qty. (Base)" = "Remaining Qty. (Base)" THEN
                                    EXIT;

                                  CompItem.GET("Item No.");

                                  IF CalcMethod = CalcMethod::"No Levels" THEN
                                    CurrReport.BREAK;

                                  CompItem.SETRANGE("Variant Filter","Variant Code");
                                  CompItem.SETRANGE("Location Filter","Location Code");
                                  CompItem.SETRANGE("Date Filter",0D,"Due Date");

                                  IF StockkeepingUnit.GET("Location Code","Item No.","Variant Code") THEN
                                    WithInventory := StockkeepingUnit."Include Inventory"
                                  ELSE
                                    WithInventory := CompItem."Include Inventory";

                                  IF WithInventory THEN
                                    CompItem.CALCFIELDS(Inventory);

                                  OnProdOrderCompOnBeforeCalcExpectedQtyOnHand("Prod. Order Component",CompItem,WithInventory);
                                  AvailabilityMgt.ExpectedQtyOnHand(CompItem,TRUE,0,ReqQty,"Due Date");

                                  IF ReqQty >= 0 THEN
                                    CurrReport.SKIP;

                                  ReqQty := ABS(ReqQty);
                                  IF ReqQty > "Remaining Qty. (Base)" THEN
                                    ReqQty := "Remaining Qty. (Base)";

                                  GetPlanningParameters.AtSKU(SKU,"Item No.","Variant Code","Location Code");
                                  ReqQty += InvtProfileOffsetting.AdjustReorderQty(ReqQty,SKU,0,0);

                                  IF ReqQty = 0 THEN
                                    CurrReport.SKIP;

                                  MainProdOrder.GET("Production Order".Status,"Prod. Order No.");

                                  IF CompItem."Replenishment System" = CompItem."Replenishment System"::"Prod. Order" THEN BEGIN
                                    ProdOrder.Status := MainProdOrder.Status;
                                    ProdOrder."Replan Ref. No." := MainProdOrder."Replan Ref. No.";
                                    ProdOrder."Replan Ref. Status" := MainProdOrder."Replan Ref. Status";
                                    ProdOrder.INSERT(TRUE);

                                    ProdOrder."Starting Date" := WORKDATE;
                                    ProdOrder."Creation Date" := WORKDATE;
                                    ProdOrder."Starting Time" := MfgSetup."Normal Starting Time";
                                    ProdOrder."Ending Time" := MfgSetup."Normal Ending Time";
                                    ProdOrder."Due Date" := "Due Date";
                                    ProdOrder."Ending Date" := "Due Date";
                                    ProdOrder."Low-Level Code" := MainProdOrder."Low-Level Code" + 1;
                                    ProdOrder."Source Type" := ProdOrder."Source Type"::Item;
                                    ProdOrder.VALIDATE("Source No.","Item No.");
                                    ProdOrder.VALIDATE(Quantity,ReqQty);
                                    //ProdOrder."Location Code" := "Location Code"; //**4PS.o
                                    ProdOrder.VALIDATE("Location Code","Location Code"); //**4PS.n
                                    OnProdOrderCompOnAfterGetRecordOnBeforeProdOrderModify(ProdOrder,MainProdOrder,"Prod. Order Component");

                                    ProdOrder.MODIFY;

                                    IsHandled := FALSE;
                                    OnProdOrderCompOnAfterGetRecordOnBeforeCreateProdOrderLines(ProdOrder,IsHandled);
                                    IF NOT IsHandled THEN
                                      CreateProdOrderLines.Copy(ProdOrder,1,"Variant Code",TRUE);
                                    ProdOrderLine.SETRANGE(Status,ProdOrder.Status);
                                    ProdOrderLine.SETRANGE("Prod. Order No.",ProdOrder."No.");
                                    ProdOrderLine.FIND('-');

                                    MODIFY;
                                    ProdOrderLine.MODIFY;

                                    ProdOrderLine.SETRANGE(Status,Status);
                                    ProdOrderLine.SETRANGE("Prod. Order No.",ProdOrder."No.");

                                    IF ProdOrderLine.FIND('-') THEN
                                      REPEAT
                                        CalcProdOrder.BlockDynamicTracking(TRUE);
                                        CalcProdOrder.Recalculate(ProdOrderLine,1,TRUE);
                                      UNTIL ProdOrderLine.NEXT = 0;

                                    MODIFY;
                                  END;
                                  ReservMgt.SetProdOrderComponent("Prod. Order Component");
                                  ReservMgt.AutoTrack("Remaining Qty. (Base)");
                                END;

               DataItemLink=Status=FIELD(Status),
                            Prod. Order No.=FIELD(Prod. Order No.),
                            Prod. Order Line No.=FIELD(Line No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 6   ;2   ;Field     ;
                  CaptionML=[DEU=Planungsrichtung;
                             ENU=Scheduling Direction;
                             NLD=Planningsrichting;
                             NOR=Planleggingsretning;
                             SVE=Planeringsriktning];
                  ToolTipML=[DEU=Gibt an, ob die Planung vorw„rts oder rckw„rts verschoben werden soll.;
                             ENU=Specifies if you want the scheduling to be replanned forward or backward.;
                             NLD=Hiermee wordt opgegeven of de planning voorwaarts of achterwaarts moet worden herpland.;
                             NOR=Angir om du vil at planleggingen skal planlegges p† nytt fremover eller bakover.;
                             SVE=Anger om schemal„ggningen ska omplaneras fram†t eller bak†t.];
                  OptionCaptionML=[DEU=Vorw„rts,Zurck;
                                   ENU=Forward,Back;
                                   NLD=Voorwaarts,Terug;
                                   NOR=Fremover,Bakover;
                                   SVE=Fram†t,Bak†t];
                  ApplicationArea=#Manufacturing;
                  SourceExpr=Direction }

      { 1   ;2   ;Field     ;
                  CaptionML=[DEU=Planen;
                             ENU=Plan;
                             NLD=Plannen;
                             NOR=Planlegging;
                             SVE=Plan];
                  ToolTipML=[DEU=Gibt an, ob die Planung eine oder alle Ebenen der Fertigungsstckliste enthalten muss.;
                             ENU=Specifies whether the planning is to take place over one or all levels of the production BOM.;
                             NLD=Hiermee wordt opgegeven of de planning ‚‚n of alle niveaus van de productiestuklijst moet omvatten.;
                             NOR=Angir om planleggingen skal skje over ett eller alle niv†ene i produksjonsstykklisten.;
                             SVE=Anger om planeringen ska g”ras f”r en eller samtliga niv†er i produktionsstrukturen.];
                  OptionCaptionML=[DEU=Keine Ebene,Eine Ebene,Alle Ebenen;
                                   ENU=No Levels,One Level,All Levels;
                                   NLD=Geen niveaus,E‚n niveau,Alle niveaus;
                                   NOR=Ingen niv†er,Ett niv†,Alle niv†er;
                                   SVE=Inga niv†er,En niv†,Alla niv†er];
                  ApplicationArea=#Manufacturing;
                  SourceExpr=CalcMethod }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Fertigungsauftr„ge neu planen...\\;ENU=Replanning Production Orders...\\;NLD=Herplannen prod.-orders...\\;NOR=Planlegger prod.ordrer p† nytt...\\;SVE=Omplanerar produktionsorder...\\';
      Text001@1001 : TextConst 'DEU=Status         #1##########\;ENU=Status         #1##########\;NLD=Status         #1##########\;NOR=Status         #1##########\;SVE=Status         #1##########\';
      Text002@1002 : TextConst 'DEU=Nr.            #2##########;ENU=No.            #2##########;NLD=Nr.            #2##########;NOR=Nr.            #2##########;SVE=Nr             #2##########';
      MfgSetup@1003 : Record 99000765;
      CalcProdOrder@1005 : Codeunit 99000773;
      CreateProdOrderLines@1006 : Codeunit 99000787;
      ReservMgt@1007 : Codeunit 99000845;
      UOMMgt@1004 : Codeunit 5402;
      Window@1010 : Dialog;
      Direction@1011 : 'Forward,Backward';
      CalcMethod@1012 : 'No Levels,One level,All levels';
      First@1013 : Boolean;

    [External]
    PROCEDURE InitializeRequest@1(NewDirection@1000 : Option;NewCalcMethod@1001 : Option);
    BEGIN
      Direction := NewDirection;
      CalcMethod := NewCalcMethod;
    END;

    LOCAL PROCEDURE DeleteProdOrders@2(ProdOrder@1000 : Record 5405;LowLevelCode@1001 : Integer;AllLevels@1002 : Boolean);
    VAR
      ProdOrderComponent@1003 : Record 5407;
    BEGIN
      IF LowLevelCode > 99 THEN
        EXIT;

      ProdOrder.SETCURRENTKEY("Low-Level Code");
      ProdOrder.SETRANGE("Replan Ref. No.",ProdOrder."Replan Ref. No.");
      ProdOrder.SETRANGE("Replan Ref. Status",ProdOrder."Replan Ref. Status");
      ProdOrder.SETRANGE("Low-Level Code",LowLevelCode + 1);

      ProdOrderComponent.SETRANGE(Status,ProdOrder.Status);
      ProdOrderComponent.SETRANGE("Prod. Order No.",ProdOrder."No.");
      IF ProdOrderComponent.FINDSET THEN
        REPEAT
          ProdOrder.SETRANGE("Source No.",ProdOrderComponent."Item No.");
          IF ProdOrder.FINDFIRST THEN BEGIN
            IF AllLevels THEN
              DeleteProdOrders(ProdOrder,LowLevelCode + 1,AllLevels);
            ProdOrder.DELETE(TRUE);
          END;
        UNTIL ProdOrderComponent.NEXT = 0;
    END;

    LOCAL PROCEDURE DeleteUnreservedLowLevelProdOrderLines@3(ProdOrder@1002 : Record 5405);
    VAR
      ProdOrderLine@1001 : Record 5406;
      ExtReservedQtyBase@1000 : Decimal;
    BEGIN
      WITH ProdOrderLine DO BEGIN
        SetCalledFromComponent(TRUE);
        LOCKTABLE;
        SETRANGE(Status,ProdOrder.Status);
        SETRANGE("Prod. Order No.",ProdOrder."No.");
        IF FIND('-') THEN
          REPEAT
            IF "Planning Level Code" > 0 THEN BEGIN
              CALCFIELDS("Reserved Qty. (Base)");
              IF "Reserved Qty. (Base)" = 0 THEN
                DELETE(TRUE)
              ELSE BEGIN
                ExtReservedQtyBase := CalcQtyReservedFromExternalDemand(ProdOrderLine,DATABASE::"Prod. Order Component");
                VALIDATE(Quantity,UOMMgt.CalcQtyFromBase(ExtReservedQtyBase,"Qty. per Unit of Measure"));
                IF Quantity > 0 THEN
                  MODIFY(TRUE)
                ELSE
                  DELETE(TRUE);
              END;
            END;
          UNTIL NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE CalcQtyReservedFromExternalDemand@13(ProdOrderLine@1008 : Record 5406;SourceType@1006 : Integer) ReservedQtyBase : Decimal;
    VAR
      ReservEntry@1007 : Record 337;
      ReservEntryFrom@1001 : Record 337;
      ProdOrderLineReserve@1000 : Codeunit 99000837;
    BEGIN
      ReservedQtyBase := 0;

      WITH ReservEntry DO BEGIN
        SETCURRENTKEY("Source ID","Source Ref. No.","Source Type","Source Subtype","Source Batch Name");
        SETRANGE("Reservation Status","Reservation Status"::Reservation);
        ProdOrderLineReserve.FilterReservFor(ReservEntry,ProdOrderLine);

        IF FINDSET THEN
          REPEAT
            ReservEntryFrom.GET("Entry No.",NOT Positive);
            IF (ReservEntryFrom."Source Type" <> SourceType) OR (ReservEntryFrom."Source ID" <> ProdOrderLine."Prod. Order No.") OR
               (ReservEntryFrom."Source Subtype" <> ProdOrderLine.Status)
            THEN
              ReservedQtyBase += "Quantity (Base)";
          UNTIL NEXT = 0;
      END;
    END;

    [Integration(DEFAULT,TRUE)]
    LOCAL PROCEDURE OnAfterLastProdOrderRtngLine@1001(VAR ProdOrderLine@1000 : Record 5406);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnProdOrderCompOnAfterGetRecordOnBeforeProdOrderModify@4(VAR ProdOrder@1000 : Record 5405;MainProdOrder@1001 : Record 5405;ProdOrderComp@1002 : Record 5407);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnProdOrderCompOnAfterGetRecordOnBeforeCreateProdOrderLines@5(VAR ProdOrder@1000 : Record 5405;VAR IsHandled@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnProdOrderCompOnBeforeCalcExpectedQtyOnHand@6(ProdOrderComponent@1000 : Record 5407;VAR CompItem@1001 : Record 27;WithInventory@1002 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

