OBJECT Report 11125354 Send Manager Time Sh Reminders
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Time Sheet-Erinnerungen an Manager senden;
               ENU=Send Managers Time Sheet Reminders;
               NLD=Managers herinnering urenverantwoording sturen];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   WeekNo := DATE2DWY(TODAY, 2) -1;
                   Year := DATE2DWY(TODAY, 3);
                 END;

    OnPreReport=BEGIN
                  IF (Year = 0) OR (WeekNo = 0) THEN
                    ERROR(Text000);
                END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 1100527000;;DataItem;                  ;
               DataItemTable=Table5200;
               DataItemTableView=WHERE(Status=CONST(Active));
               OnPreDataItem=BEGIN
                               SETFILTER("Account Hours",'%1',Employee."Account Hours"::"Required according to Norm");
                               SETFILTER("Manager No.", '<>%1', '');
                             END;

               OnAfterGetRecord=BEGIN
                                  FillTempTable(Employee."Manager No.");
                                END;

               OnPostDataItem=VAR
                                HourAccounting@1100527002 : Record 11012038;
                              BEGIN
                                IF GenTempTableTMP.FINDSET THEN BEGIN
                                  REPEAT
                                    SetHourAccounting(HourAccounting,Year,WeekNo,GenTempTableTMP."Key Code1");
                                    IF STRLEN(HourAccounting.GetEmployeeNamesReportingDelay) > 0 THEN
                                      SendMessage(GenTempTableTMP."Key Code1");
                                  UNTIL GenTempTableTMP.NEXT = 0;
                                END;
                              END;

               ReqFilterFields=No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100527005;;Container;
                  ContainerType=ContentArea }

      { 1100527004;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100527003;2;Field  ;
                  CaptionML=[DEU=Jahr;
                             ENU=Year;
                             NLD=Jaar;
                             NOR=èr;
                             SVE=èr];
                  SourceExpr=Year }

      { 1100527002;2;Field  ;
                  CaptionML=[DEU=Woche;
                             ENU=Week;
                             NLD=Week;
                             NOR=Uke;
                             SVE=Vecka];
                  SourceExpr=WeekNo }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      GenTempTableTMP@1100527000 : TEMPORARY Record 11020579;
      MailTemplateManagement@1100525000 : Codeunit 11012622;
      WeekNo@1100527004 : Integer;
      Year@1100527003 : Integer;
      Text000@1100527001 : TextConst 'DEU=Bitte geben Sie eine Wochennummer und ein Jahr an.;ENU=Please specify a week no. and a year.;NLD=Geef a.u.b. een weeknr. en een jaar in.';
      Text001@1100527005 : TextConst 'DEU=Erinnerung: Time Sheet fÅr Woche %1.%2 ist fÑllig.;ENU=Reminder: Time Sheet for week %1.%2 is due.;NLD=Urenverantwoording werknemers voor week %1.%2 worden verwacht.';

    PROCEDURE SendMessage@1100527005(EmployeeNo@1100527001 : Code[20]);
    VAR
      DocumentMailing@1100527004 : Codeunit 260;
      HourAccounting@1100527002 : Record 11012038;
      Employee@1100527003 : Record 5200;
      MailTemplate@1100525002 : Record 11229450;
      MailTemplateNo@1100525000 : Integer;
      Subject@1100525001 : Text;
    BEGIN
      Employee.GET(EmployeeNo);
      SetHourAccounting(HourAccounting,Year,WeekNo,EmployeeNo);
      MailTemplateNo := 112;
      Subject := STRSUBSTNO(Text001, WeekNo, Year);
      MailTemplateManagement.GetMailTemplate(MailTemplate, MailTemplateNo, Employee."Global Dimension 1 Code");
      IF (MailTemplate."Use Mail Template Subject Text") THEN
        Subject := MailTemplateManagement.GetLayoutTextSubstitutedSubjectOnly(MailTemplateNo, '', HourAccounting);

      DocumentMailing.EmailFile4PS(
        '',
        '',
        '',
        Employee."No.",
        Employee."Company E-Mail",
        MailTemplateNo,
        Employee."Global Dimension 1 Code",
        '',
        HourAccounting,
        Subject,
        '',
        TRUE);
    END;

    LOCAL PROCEDURE FillTempTable@1100527000(ManagerNo@1100527000 : Code[20]);
    BEGIN
      GenTempTableTMP.INIT;
      GenTempTableTMP."Key Code1" := ManagerNo;
      IF GenTempTableTMP.INSERT THEN;
    END;

    LOCAL PROCEDURE SetHourAccounting@1100527001(VAR HourAccounting@1100527003 : Record 11012038;Year@1100527000 : Integer;Week@1100527001 : Integer;EmployeeNo@1100527002 : Code[20]);
    BEGIN
      HourAccounting.Year := Year;
      HourAccounting.Week := Week;
      HourAccounting."Employee No." := EmployeeNo;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

