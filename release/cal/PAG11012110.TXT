OBJECT Page 11012110 Copy General Data
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Allgemeine Daten kopieren;
               ENU=Copy General Data;
               NLD=Kopi‰ren standaardgegevens];
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    PageType=StandardDialog;
    OnOpenPage=BEGIN
                 ProjRec.GET(ProjectCde);
               END;

    OnQueryClosePage=BEGIN
                       CloseActionOK := CloseAction IN [ACTION::OK,ACTION::LookupOK];
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 8   ;1   ;Field     ;
                CaptionML=[DEU=Projekt;
                           ENU=Project;
                           NLD=Project;
                           NOR=Prosjekt;
                           SVE=Projekt];
                SourceExpr=ProjRec."No.";
                Editable=FALSE }

    { 10  ;1   ;Field     ;
                CaptionML=[DEU=Beschreibung;
                           ENU=Description;
                           NLD=Omschrijving;
                           NOR=Beskrivelse;
                           SVE=Beskrivning];
                SourceExpr=ProjRec.Description;
                Editable=FALSE }

    { 1100525000;1;Field  ;
                CaptionClass=Text19043260;
                Style=Strong;
                StyleExpr=TRUE }

    { 1   ;1   ;Field     ;
                CaptionML=[DEU=&Wohnungsmodelle;
                           ENU=House Types;
                           NLD=Woningmodellen;
                           NOR=Hustyper;
                           SVE=Hustyper];
                SourceExpr=SelectHouseModel }

    { 3   ;1   ;Field     ;
                CaptionML=[DEU=Elemente;
                           ENU=Elements;
                           NLD=Elementen;
                           NOR=Elementer;
                           SVE=Element];
                SourceExpr=SelectElement }

    { 5   ;1   ;Field     ;
                CaptionML=[DEU=Einkaufsmarkierungen;
                           ENU=Purchase Actions;
                           NLD=Inkoopacties;
                           NOR=Innkj›pshandlinger;
                           SVE=Ink”ps†tg„rder];
                SourceExpr=SelectPurchaseAction }

    { 1100485000;1;Field  ;
                CaptionML=[DEU=Budgettarife;
                           ENU=Budget Rates;
                           NLD=Begrotingstarieven;
                           NOR=Budsjettsatser;
                           SVE=Budgetavgifter];
                SourceExpr=SelectBudgetRate }

    { 1100528400;1;Field  ;
                CaptionML=[DEU=Elementphasen;
                           ENU=Element Phases;
                           NLD=Elementfasen];
                SourceExpr=SelectElementPhase;
                OnValidate=BEGIN
                             IF NOT SelectElementPhase THEN
                               SelectElementPhaseLabel := FALSE;
                           END;
                            }

    { 1100528401;1;Field  ;
                CaptionML=[DEU=Elementphasenlabels;
                           ENU=Element Phase Labels;
                           NLD=Elementfaselabels];
                SourceExpr=SelectElementPhaseLabel;
                OnValidate=BEGIN
                             IF NOT SelectElementPhase AND SelectElementPhaseLabel THEN
                               ERROR(Text007, TRUE);
                           END;
                            }

  }
  CODE
  {
    VAR
      Text000@11012017 : TextConst 'DEU=Die folgenden Daten wurden kopiert:\\;ENU=The following data has been copied:\\;NLD=De volgende gegevens zijn gekopieerd:\\;NOR=F›lgende data ble kopiert:\\;SVE=F”ljande data har kopierats:\\';
      Text001@11012018 : TextConst 'DEU=%1 von den %2 Standardwohnungsmodellen\;ENU=%1 of the %2 standard House Models\;NLD=%1 van de %2 standaard woningmodellen\;NOR=%1 av %2 Standardhusmodeller\;SVE=%1 av %2 standardhusmodeller\';
      Text002@11012019 : TextConst 'DEU=%1 von den %2 Standardelementen\;ENU=%1 of the %2 standard Elements\;NLD=%1 van de %2 standaard elementen\;NOR=%3 av %4 Standardelement\;SVE=%3 av %4 standardelement\';
      Text003@11012020 : TextConst 'DEU=%1 von %2 Standardeinkaufsmarkierungen\;ENU=%1 of the %2 standard Purchase Actions\;NLD=%1 van de %2 standaard inkoopacties\;NOR=%5 av %6 Standardinnkj›pshandlinger\;SVE=%5 av %6 standardink”ps†tg„rder\';
      ProjRec@11012000 : Record 11072003;
      StandModelRec@11012001 : Record 11012501;
      ProjModelRec@11012002 : Record 11012501;
      StandElemRec@11012003 : Record 11012060;
      ProjElemRec@11012004 : Record 11012010;
      StandPurchActionRec@11012005 : Record 11012025;
      ProjPurchActionRec@11012006 : Record 11012026;
      PurchaseTextRecOrig@1100485006 : Record 11012067;
      PurchaseTextRecDest@1100485007 : Record 11012067;
      StandBudgetRateRec@1100485003 : Record 11012000;
      ProjBudgetRateRec@1100485004 : Record 11020231;
      FeatByPurchActionRec@1100485008 : Record 11012069;
      FeatByPurchActionRec2@1100485009 : Record 11012069;
      ProjectCde@11012007 : Code[20];
      SelectHouseModel@11012008 : Boolean;
      SelectElement@11012009 : Boolean;
      SelectPurchaseAction@11012010 : Boolean;
      SelectBudgetRate@1100485000 : Boolean;
      SelectElementPhase@1100528401 : Boolean;
      SelectElementPhaseLabel@1100528402 : Boolean;
      HouseModelCounter@11012011 : Integer;
      HouseModelCounterOK@11012012 : Integer;
      ElemCounter@11012013 : Integer;
      ElemCounterOk@11012014 : Integer;
      PurchActionCounter@11012015 : Integer;
      PurchActionCounterOK@11012016 : Integer;
      BudgetRateCounter@1100485001 : Integer;
      BudgetRateCounterOK@1100485002 : Integer;
      Text004@1100485005 : TextConst 'DEU=%1 von den %2  Standardbudgettarifen\;ENU=%1 of the %2 standard Budget Rates\;NLD=%1 van de %2 standaard begrotingstarieven\;NOR=%7 av %8 Standard budsjettsatser;SVE=%7 av %8 standardbudgetavgifter';
      Text005@1100528407 : TextConst 'DEU=%1 von %2 Standardelementphasen\;ENU=%1 of the %2 standard Element Phases\;NLD=%1 van de %2 standaard elementfasen\';
      Text006@1100528408 : TextConst 'DEU=%1 von %2 Standardelementphasenlabels;ENU=%1 of the %2 standard Element Phase Labels;NLD=%1 van de %2 standaard elementfaselabels';
      Text007@1100528409 : TextConst 'DEU=Elementphasen muss %1 sein.;ENU=Element Phases should be %1.;NLD=Elementfasen moet %1 zijn.';
      Text19043260@1100525000 : TextConst 'DEU=Kopieren;ENU=Copy;NLD=Kopieer;NOR=KOPIER;SVE=Kopiera';
      ElementPhaseCounter@1100528403 : Integer;
      ElementPhaseCounterOK@1100528404 : Integer;
      ElementPhaseLabelCounter@1100528405 : Integer;
      ElementPhaseLabelCounterOK@1100528406 : Integer;
      CloseActionOK@1100528400 : Boolean;

    PROCEDURE SetProject@4(lProjectCde@11012000 : Code[20]);
    BEGIN
      ProjectCde := lProjectCde;
    END;

    PROCEDURE CopyHouseModels@1();
    VAR
      StandUnitRec@1210190001 : Record 11012508;
      ProjUnitRec@1210190000 : Record 11012508;
      FPSLicenseManagement@1100527400 : Codeunit 11229289;
    BEGIN
      StandModelRec.SETFILTER("Project No.",'%1','');
      HouseModelCounter := 0;
      IF FPSLicenseManagement.LicenseAndReadPermissionForTable(StandModelRec) THEN
        HouseModelCounter := StandModelRec.COUNT;
      HouseModelCounterOK := 0;
      IF NOT SelectHouseModel THEN
        EXIT;

      IF StandModelRec.FIND('-') THEN
        REPEAT
          IF NOT ProjModelRec.GET(ProjectCde,StandModelRec.Model) THEN BEGIN
            ProjModelRec.INIT;
            ProjModelRec."Project No." := ProjectCde;
            ProjModelRec.Model := StandModelRec.Model;
            ProjModelRec.TRANSFERFIELDS(StandModelRec,FALSE);
            ProjModelRec.INSERT;

            StandUnitRec.SETFILTER("Project No.",'%1','');
            StandUnitRec.SETRANGE(Model,StandModelRec.Model);
            IF StandUnitRec.FIND('-') THEN
            REPEAT
              IF NOT ProjUnitRec.GET(ProjectCde,StandModelRec.Model, StandUnitRec.Unit) THEN BEGIN
                ProjUnitRec.INIT;
                ProjUnitRec."Project No." := ProjectCde;
                ProjUnitRec.Model := StandModelRec.Model;
                ProjUnitRec.Unit := StandUnitRec.Unit;
                ProjUnitRec.TRANSFERFIELDS(StandUnitRec,FALSE);
                ProjUnitRec.INSERT;
              END;
            UNTIL StandUnitRec.NEXT = 0;
            HouseModelCounterOK := HouseModelCounterOK + 1;
          END;
        UNTIL StandModelRec.NEXT = 0;
    END;

    PROCEDURE CopyElements@2();
    BEGIN
      ElemCounter := StandElemRec.COUNT;
      ElemCounterOk := 0;
      IF NOT SelectElement THEN
        EXIT;

      IF StandElemRec.FIND('-') THEN
        REPEAT
          IF NOT ProjElemRec.GET(ProjectCde,StandElemRec.Code) THEN BEGIN
            ProjElemRec.INIT;
            ProjElemRec."Project No." := ProjectCde;
            ProjElemRec.Element := StandElemRec.Code;
            ProjElemRec.Description := StandElemRec.Description;
            ProjElemRec.Chapter := StandElemRec.Chapter;
            ProjElemRec.Paragraph := StandElemRec.Paragraph;
            ProjElemRec.Level := StandElemRec.Level;
            ProjElemRec.Quantity := StandElemRec.Quantity;
            ProjElemRec."Unit of Measure" := StandElemRec."Unit of Measure";
            ProjElemRec."Element Type" := StandElemRec."Element Type"; //kzwerver, 110525, n, #27630
            ProjElemRec."Planning Activity Type" := StandElemRec."Planning Activity Type";
            ProjElemRec."Search Code" := StandElemRec."Search Code";  // 29353.n
            ProjElemRec."Previous Element" := StandElemRec."Previous Element";
            ProjElemRec."Lead Time (Days)" := StandElemRec."Lead Time (Days)";
            ProjElemRec."Responsible Employee" := StandElemRec."Responsible Employee";
            ProjElemRec."Publish in Empoly App" := StandElemRec."Publish in Empoly App";
            ProjElemRec.Discipline := StandElemRec.Discipline;
            ProjElemRec."Standard Object" := StandElemRec."Standard Object";
            ProjElemRec.INSERT(TRUE); //kzwerver, 140110, c, #C011589
            ElemCounterOk := ElemCounterOk + 1;
          END;
        UNTIL StandElemRec.NEXT = 0;
    END;

    PROCEDURE CopyPurchaseActions@3();
    BEGIN
      PurchActionCounter := StandPurchActionRec.COUNT;
      PurchActionCounterOK := 0;
      IF NOT SelectPurchaseAction THEN
        EXIT;

      IF StandPurchActionRec.FIND('-') THEN
        REPEAT
          IF NOT ProjPurchActionRec.GET(ProjectCde,StandPurchActionRec.Code) THEN BEGIN
            ProjPurchActionRec.INIT;
            ProjPurchActionRec."Project No." := ProjectCde;
            ProjPurchActionRec.TRANSFERFIELDS(StandPurchActionRec);
            ProjPurchActionRec."Changed by" := USERID;
            ProjPurchActionRec."Changed on" := TODAY;
            ProjPurchActionRec."Requested Quote Receipt Date" := ProjRec."Requested Quote Receipt Date";
            ProjPurchActionRec."Vendors react before" := ProjRec."Vendors React before";
            ProjPurchActionRec.INSERT;

      //**4PS.sn
            FeatByPurchActionRec.RESET;
            FeatByPurchActionRec.SETRANGE(Source,FeatByPurchActionRec.Source::Project); // Call 22524
            FeatByPurchActionRec.SETRANGE("Estimate No.",'');                           // Call 22524
            FeatByPurchActionRec.SETRANGE("Project No.",'');
            FeatByPurchActionRec.SETRANGE("Purchase Action",StandPurchActionRec.Code);
            IF FeatByPurchActionRec.FIND('-') THEN
              REPEAT
                FeatByPurchActionRec2.INIT;
                FeatByPurchActionRec2."Project No." := ProjectCde;
                FeatByPurchActionRec2."Purchase Action" := FeatByPurchActionRec."Purchase Action";
                FeatByPurchActionRec2."Contact Feature Group" := FeatByPurchActionRec."Contact Feature Group";
                FeatByPurchActionRec2."Contact Feature" := FeatByPurchActionRec."Contact Feature";
                FeatByPurchActionRec2."Contact Feature Description" := FeatByPurchActionRec."Contact Feature Description";
                FeatByPurchActionRec2.INSERT;
              UNTIL FeatByPurchActionRec.NEXT = 0;

      //**4PS.en

            PurchaseTextRecOrig.SETRANGE(PurchaseTextRecOrig."Line Type", PurchaseTextRecOrig."Line Type"::"Purchase Action");
            PurchaseTextRecOrig.SETRANGE("Purchase Action", StandPurchActionRec.Code);
            IF PurchaseTextRecOrig.FIND('-') THEN
              REPEAT
                PurchaseTextRecDest.TRANSFERFIELDS(PurchaseTextRecOrig);
                PurchaseTextRecDest."Line Type" := PurchaseTextRecDest."Line Type"::"Project Purchase Action";
                PurchaseTextRecDest."No." := ProjectCde;
                PurchaseTextRecDest.INSERT;
              UNTIL PurchaseTextRecOrig.NEXT = 0;
            PurchActionCounterOK := PurchActionCounterOK + 1;
          END;
        UNTIL StandPurchActionRec.NEXT = 0;
    END;

    PROCEDURE CopyBudgetRates@1100485001();
    BEGIN
      BudgetRateCounter := StandBudgetRateRec.COUNT;
      BudgetRateCounterOK := 0;
      IF NOT SelectBudgetRate THEN
        EXIT;

      IF StandBudgetRateRec.FIND('-') THEN
        REPEAT
          ProjBudgetRateRec.INIT;
          ProjBudgetRateRec."Project No." := ProjectCde;
          ProjBudgetRateRec.TRANSFERFIELDS(StandBudgetRateRec);
          ProjBudgetRateRec."Rate (LCY)" := StandBudgetRateRec.Rate;
          IF ProjBudgetRateRec.INSERT THEN
            BudgetRateCounterOK := BudgetRateCounterOK + 1;
        UNTIL StandBudgetRateRec.NEXT = 0;
    END;

    LOCAL PROCEDURE CopyElementPhases@1100528403();
    VAR
      BaseElementPhase@1100528400 : Record 11229465;
      ProjectElementPhase@1100528401 : Record 11229446;
    BEGIN
      ElementPhaseCounter := BaseElementPhase.COUNT;
      ElementPhaseCounterOK := 0;
      IF NOT SelectElementPhase THEN
        EXIT;

      IF BaseElementPhase.FINDSET THEN
        REPEAT
          ProjectElementPhase.INIT;
          ProjectElementPhase."Project No." := ProjectCde;
          ProjectElementPhase.TRANSFERFIELDS(BaseElementPhase);
          IF ProjectElementPhase.INSERT THEN
            ElementPhaseCounterOK := ElementPhaseCounterOK + 1;
        UNTIL BaseElementPhase.NEXT = 0;
    END;

    LOCAL PROCEDURE CopyElementPhaseLabels@1100528405();
    VAR
      BaseElementPhaseLabel@1100528400 : Record 11229466;
      ProjectElementPhaseLabel@1100528401 : Record 11229463;
    BEGIN
      ElementPhaseLabelCounter := BaseElementPhaseLabel.COUNT;
      ElementPhaseLabelCounterOK := 0;
      IF NOT SelectElementPhaseLabel THEN
        EXIT;

      IF BaseElementPhaseLabel.FINDSET THEN
        REPEAT
          ProjectElementPhaseLabel.INIT;
          ProjectElementPhaseLabel."Project No." := ProjectCde;
          ProjectElementPhaseLabel.TRANSFERFIELDS(BaseElementPhaseLabel);
          IF ProjectElementPhaseLabel.INSERT THEN
            ElementPhaseLabelCounterOK := ElementPhaseLabelCounterOK + 1;
        UNTIL BaseElementPhaseLabel.NEXT = 0;
    END;

    LOCAL PROCEDURE StdDialogConfirm@1100528400() : Boolean;
    BEGIN
      CurrPage.RUNMODAL;
      EXIT(CloseActionOK);
    END;

    LOCAL PROCEDURE StdDialogDoAction@1100528401();
    VAR
      MessageText@1100528400 : Text;
    BEGIN
      CopyHouseModels;
      CopyElements;
      CopyPurchaseActions;
      CopyBudgetRates;
      CopyElementPhases;
      CopyElementPhaseLabels;

      MessageText := Text000;
      MessageText := MessageText + STRSUBSTNO(Text001, HouseModelCounterOK, HouseModelCounter);
      MessageText := MessageText + STRSUBSTNO(Text002, ElemCounterOk, ElemCounter);
      MessageText := MessageText + STRSUBSTNO(Text003, PurchActionCounterOK, PurchActionCounter);
      MessageText := MessageText + STRSUBSTNO(Text004, BudgetRateCounterOK, BudgetRateCounter);
      MessageText := MessageText + STRSUBSTNO(Text005, ElementPhaseCounterOK, ElementPhaseCounter);
      MessageText := MessageText + STRSUBSTNO(Text006, ElementPhaseLabelCounterOK, ElementPhaseLabelCounter);
      MESSAGE(MessageText);
    END;

    PROCEDURE StdDialogRunModal@1100528404();
    BEGIN
      IF StdDialogConfirm THEN
        StdDialogDoAction;
    END;

    BEGIN
    END.
  }
}

