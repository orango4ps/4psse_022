OBJECT Codeunit 1546 Workflow Webhook Notify Task
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    TableNo=1504;
    EventSubscriberInstance=Static-Automatic;
    OnRun=VAR
            WorkflowWebhookSubscriptionTable@1002 : Record 469;
            WorkflowWebhookEntryTable@1001 : Record 467;
            UserTable@1003 : Record 2000000120;
            WorkflowWebhookNotification@1000 : Codeunit 1545;
            ContactEmail@1004 : Text;
            RetryCount@1005 : Integer;
            WaitTime@1006 : Integer;
            InitHandled@1007 : Boolean;
          BEGIN
            // Fetch Subscription
            WorkflowWebhookSubscriptionTable.SETFILTER("WF Definition Id","Workflow Code");
            WorkflowWebhookSubscriptionTable.SETFILTER(Enabled,'%1',TRUE);
            IF NOT WorkflowWebhookSubscriptionTable.FINDFIRST THEN
              EXIT;

            // Fetch Entry
            WorkflowWebhookEntryTable.SETRANGE("Workflow Step Instance ID",ID);
            IF NOT WorkflowWebhookEntryTable.FINDFIRST THEN
              EXIT;

            UserTable.SETRANGE("User Name",WorkflowWebhookEntryTable."Initiated By User ID");
            IF UserTable.FINDFIRST THEN
              ContactEmail := UserTable."Contact Email";

            // Send notification
            OnFetchWorkflowWebhookNotificationInitParams(RetryCount,WaitTime,InitHandled);

            // If parameters not initialized by subscribers (mock subscriber for testing)
            IF NOT InitHandled THEN BEGIN
              // Set defaults
              BINDSUBSCRIPTION(WorkflowWebhookNotification);
              RetryCount := 5;
              WaitTime := 12000;
            END;

            WorkflowWebhookNotification.Initialize(RetryCount,WaitTime);
            WorkflowWebhookNotification.SendNotification(WorkflowWebhookEntryTable."Data ID",
              WorkflowWebhookEntryTable."Workflow Step Instance ID",WorkflowWebhookSubscriptionTable.GetNotificationUrl,ContactEmail);
          END;

  }
  CODE
  {

    [Integration]
    LOCAL PROCEDURE OnFetchWorkflowWebhookNotificationInitParams@1(VAR RetryCount@1000 : Integer;VAR WaitTime@1001 : Integer;VAR InitHandled@1002 : Boolean);
    BEGIN
    END;

    BEGIN
    {
      // Intended to be called using TaskScheduler
    }
    END.
  }
}

