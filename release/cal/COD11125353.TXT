OBJECT Codeunit 11125353 NAS Management 4PS
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE NASHandler@1(NASID@1000 : Text[260]);
    VAR
      FPSMobileSetup@1100527000 : Record 11012676;
      Parameter@1008 : Text[260];
      ParamStr@1007 : Text[260];
      SepPosition@1006 : Integer;
      DocumentMgtSetup@1100409003 : Record 11071831;
      NASAutomaticExchangeFSA@1100409000 : Codeunit 11020229;
      LIPInterfaceStartup@1100409001 : Codeunit 11012139;
      SharepointInterfaceStartup@1100409002 : Codeunit 11012086;
      NASSendSMTPSOStatusMail@1100409004 : Codeunit 11012824;
      DPTEventLogNAS@1100528300 : Codeunit 11012452;
      CodeUnitToRun@1100409005 : Integer;
    BEGIN
      ParamStr := UPPERCASE(NASID);
      REPEAT
        SepPosition := STRPOS(ParamStr,',');
        IF SepPosition > 0 THEN
          Parameter := COPYSTR(ParamStr,1,SepPosition - 1)
        ELSE
          Parameter := ParamStr;

        IF (Parameter = '2BA') THEN
          CODEUNIT.RUN(CODEUNIT::"Process Item Batch");

        IF (Parameter = '4PS-MOBILE') THEN BEGIN
          StartMSMQHandler();
          StartProcessDebrief();
        END;

        IF (Parameter = 'MSMQ-HANDLER-MOBILE') THEN
          StartMSMQHandler();

        IF (Parameter = 'PROCESS-DEBRIEF-MOBILE') THEN
          StartProcessDebrief();

        IF (Parameter = 'DWH') THEN //* Datawarehouse Replication
          CODEUNIT.RUN(CODEUNIT::"DataWarehouse - Projects v3");

        IF (Parameter = 'FSA') THEN //* Field Service (buitendienst)
          NASAutomaticExchangeFSA.RUN;

        IF (Parameter = 'DONAU') THEN
          CODEUNIT.RUN(CODEUNIT::"Request Handler (Donau)");

        IF (Parameter = 'DOCU') THEN  //* Document beheersysteem Sharepoint of LIP
          IF DocumentMgtSetup.GET THEN
            CASE DocumentMgtSetup."Documentation System" OF
              DocumentMgtSetup."Documentation System"::LIP:
                LIPInterfaceStartup.RUN;
              DocumentMgtSetup."Documentation System"::Sharepoint:
                SharepointInterfaceStartup.RUN;
            END;

        IF (Parameter = 'SO-SMTPMAIL') THEN
          NASSendSMTPSOStatusMail.RUN;

        IF (COPYSTR(Parameter,1,6) = 'CUSTOM') THEN BEGIN
          EVALUATE(CodeUnitToRun,COPYSTR(Parameter,7));
          CODEUNIT.RUN(CodeUnitToRun);
        END;

        ParamStr := COPYSTR(ParamStr,SepPosition + 1);
      UNTIL SepPosition = 0;
    END;

    LOCAL PROCEDURE StartMSMQHandler@1100528701();
    VAR
      MobileSetup@1100528702 : Record 11012676;
      Id@1100528701 : Integer;
    BEGIN
      IF MobileSetup.GET THEN
        IF (MobileSetup."Receive Message Queue" <> '') AND (MobileSetup."Reply Message Queue" <> '') THEN
          STARTSESSION(Id, CODEUNIT::"Request Handler (4PS-Mobile)");
    END;

    LOCAL PROCEDURE StartProcessDebrief@1100528700();
    VAR
      Company@1100528700 : Record 2000000006;
      MobileSetup@1100528702 : Record 11012676;
      Id@1100528701 : Integer;
    BEGIN
      IF MobileSetup.GET THEN
        IF MobileSetup."4PS-Mobile Active" THEN
          STARTSESSION(Id, CODEUNIT::"NAS Process Debrief 4PS-Mobile");
    END;

    BEGIN
    END.
  }
}

