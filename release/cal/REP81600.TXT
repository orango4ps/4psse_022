OBJECT Report 81600 Import Installments
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PSSESB.001.I022;
  }
  PROPERTIES
  {
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 5136;    ;DataItem;                    ;
               DataItemTable=Table11012018;
               DataItemTableView=SORTING(Project No.,Principal,Plot No.,Installment No.);
               OnPreDataItem=BEGIN
                               ProjectNo := GETFILTER("Project No.");
                               PrincipalNo := GETFILTER(Principal);
                               PlotNo := GETFILTER("Plot No.");
                               ExtensionNo := GETFILTER("Extension Contract");

                               IF ProjectNo = '' THEN
                                 ERROR(Text003);

                               Window.OPEN(Text004,LineNo);
                               CSVfile.TEXTMODE(TRUE);
                               CSVfile.WRITEMODE(FALSE);
                               IF NOT CSVfile.OPEN(ImportFileName) THEN
                                 ERROR(Text005);
                               ProjInstallment.SetSuspendAmountCheck;
                               REPEAT
                                 CLEAR(InString);
                                 CSVfile.READ(InString);

                                 ProjInstallment.INIT;
                                 ProjInstallment."Project No." := ProjectNo;
                                 ProjInstallment.Principal := PrincipalNo;
                                 //>>130904 Itero
                                 ProjInstallment.DetermineVATPPG("VAT Prod. Posting Group" = '');
                                 //<<130904
                                 ProjInstallment."Plot No." := PlotNo;
                                 ProjInstallment."Installment No." :=
                                   GenProjInstallments.GetFreeInstallmentNo(ProjectNo,PrincipalNo,PlotNo);
                                 ProjInstallment."Extension Contract" := ExtensionNo;

                                 // Col A field "Description"
                                 ProjInstallment.Description := COPYSTR(InString,1,STRPOS(InString,';')-1);
                                 InString := DELSTR(InString, 1, STRPOS(InString,';'));

                                 // Col B field "Installment Amount"
                                 IF NOT EVALUATE(InstallmentAmt,COPYSTR(InString,1,STRPOS(InString,';')-1)) THEN
                                   InstallmentAmt := 0;
                                 ProjInstallment.VALIDATE("Installment Amount",InstallmentAmt);
                                 InString := DELSTR(InString, 1, STRPOS(InString,';'));

                                 // Col C field "Expected Invoice Date"
                                 IF NOT EVALUATE(ExpectedInvoiceDate,COPYSTR(InString,1,STRPOS(InString,';')-1)) THEN
                                   ExpectedInvoiceDate := 0D;
                                 ProjInstallment.VALIDATE("Expected Invoice Date",ExpectedInvoiceDate);
                                 InString := DELSTR(InString, 1, STRPOS(InString,';'));

                                 // Col D field "Cost Object"
                                 ProjInstallment.VALIDATE("Cost Object",COPYSTR(InString,1,20));

                                 ProjInstallment.INSERT(TRUE);
                                 LineNo += 1;
                                 Window.UPDATE;
                               UNTIL CSVfile.POS = CSVfile.LEN;

                               // SESB.I022 KD 08.08.2013 >>
                               IF (PrincipalNo <> '') AND (ExtensionNo = '') THEN
                                 GenProjInstallments.CalcFinalInstalmentPrincipal(ProjectNo,PrincipalNo);
                               IF (ExtensionNo <> '') THEN
                                 GenProjInstallments.CalcFinalInstalmentExtension(ProjectNo,ExtensionNo);
                               // SESB.I022 KD 08.08.2013 <<

                               ProjInstallment.CheckContractAmount;

                               Window.CLOSE;
                             END;

               ReqFilterFields=Project No.,Principal,Plot No.,Extension Contract }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100294000;;Container;
                  ContainerType=ContentArea }

      { 1100294001;1;Group  ;
                  CaptionML=[ENU=Options;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100294002;2;Field  ;
                  Name=File Name;
                  CaptionML=[ENU=File Name;
                             NOR=Filnavn;
                             SVE=Filnamn];
                  SourceExpr=ImportFileName;
                  OnAssistEdit=BEGIN
                                 AssistSelectFile;
                                 ValidateFileName;
                               END;
                                }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ProjInstallment@1100294006 : Record 11012018;
      Project@1100294007 : Record 11072003;
      GenProjInstallments@1100294009 : Codeunit 11012007;
      CSVfile@1100294003 : File;
      Window@1100294013 : Dialog;
      ImportFileName@1100294000 : Text[250];
      Text001@1100294001 : TextConst 'ENU=File Type must be ''.csv''.;NLD=Bestandstype moet ''.csv'' zijn.;NOR=Filtype m† v‘re ''.csv''.;SVE=Filtypen m†ste vara ''.csv''.';
      Text002@1100294002 : TextConst 'ENU=CSV files (*.csv)|*.csv|All Files (*.*)|*.*;NOR=CSV-filer (*.csv)|*.csv|Alle filer (*.*)|*.*;SVE=CSV filer (*.csv)|*.csv|Alla filer (*.*)|*.*';
      ProjectNo@1100294004 : Code[20];
      Text003@1100294005 : TextConst 'ENU=Report can''t be run separately.;NOR=Rapportern kan ikke kj›res separat;SVE=Rapporten kan inte k”ras separat';
      PrincipalNo@1100294015 : Code[20];
      PlotNo@1100294016 : Code[10];
      ExtensionNo@1100294017 : Code[10];
      LineNo@1100294008 : Integer;
      InString@1100294010 : Text[1000];
      InstallmentAmt@1100294011 : Decimal;
      ExpectedInvoiceDate@1100294012 : Date;
      Text004@1100294014 : TextConst 'ENU=Lines processed #1############;NOR=Prosseserte rader #1############;SVE=Behandlade rader #1#############';
      Text005@1100294018 : TextConst 'ENU=File %1 not found.;NOR=Filen %1 ble ikke funnet.;SVE=HIttar inte filen %1';
      Text006@1100294019 : TextConst 'ENU=Please Select the CSV File;NOR=Velg CSV-fil;SVE=V„lj CSV fil';

    PROCEDURE AssistSelectFile@1210190001();
    VAR
      lvFileName@1210190000 : Text[250];
    BEGIN
      lvFileName := ImportFileName;

      //IF NOT ISSERVICETIER THEN
      //  lvFileName := FileDialog.OpenFile(Text006,lvFileName,4,Text002,0)
      //ELSE
        IF NOT(UPLOAD(Text006, '', Text002, lvFileName, lvFileName)) THEN
          lvFileName := '';

      IF lvFileName <> '' THEN
        ImportFileName := lvFileName;
    END;

    PROCEDURE ValidateFileName@1210190000();
    BEGIN
      IF ImportFileName = '' THEN
        EXIT;
      IF (STRLEN(ImportFileName) < 4) OR
        (COPYSTR(ImportFileName, (STRLEN(ImportFileName) - 3)) <> '.csv')
      THEN BEGIN
        CLEAR(ImportFileName);
        ERROR(Text001);
      END;
    END;

    BEGIN
    {
      SESB.001 Importing Instalments
         ASH 18.07.2013 Object created

      SESB.I022 Final Instalment
         KD 08.08.2013 Trigger "Project Installment - OnPreDataItem" changed

      >>130904 Itero
      Call IME-017
    }
    END.
  }
  RDLDATA
  {
  }
}

