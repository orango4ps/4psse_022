OBJECT Page 11012131 Filter Features for Purch.Act.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Filtern Mermale fÅr Einkaufsmarkierungen;
               ENU=Filter Features for Purch.Act.;
               NLD=Filter op kenmerken voor inkoopacties;
               NOR=Filterfunksjoner for innkjõpshandling.;
               SVE=Filterfunktioner fîr inkîpsÜtg.];
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table11012070;
    PageType=NavigatePage;
    OnInit=BEGIN
             DateFromTBVisible := TRUE;
             "Minimum ScoreVisible" := TRUE;
             "Minimum ScoreEditable" := TRUE;
           END;

    OnOpenPage=VAR
                 VendorAssessment@1100485000 : Record 11012292;
                 FPSLicenseManagement@1100527400 : Codeunit 11229289;
               BEGIN
                 FILTERGROUP(9);
                 SETRANGE(User, USERID);
                 FILTERGROUP(0);

                 "Minimum ScoreVisible" := (FPSLicenseManagement.LicenseAndReadPermissionForTable(VendorAssessment));
                 DateFromTBVisible := (FPSLicenseManagement.LicenseAndReadPermissionForTable(VendorAssessment));

                 CertDateFilter := STRSUBSTNO('%1%2%3', FORMAT(WORKDATE), '..' , '|''''');

                 Step1Visible := TRUE;
                 Step2Visible := FALSE;
                 Step3Visible := FALSE;
                 WizardStep := 1;
                 ShowStep(TRUE);
               END;

    OnAfterGetRecord=BEGIN
                       OnAfterGetCurrRecord;
                     END;

    OnNewRecord=BEGIN
                  OnAfterGetCurrRecord;
                END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100528703;1 ;Action    ;
                      Name=Back;
                      CaptionML=[DEU=< &ZurÅck;
                                 ENU=< &Back;
                                 NLD=< &Vorige;
                                 NOR=< &Tilbake;
                                 SVE=< &Tillbaka];
                      Enabled=BackEnable;
                      InFooterBar=Yes;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 ShowStep(FALSE);
                                 IF WizardStep > 1 THEN
                                   WizardStep -= 1;
                                 ShowStep(TRUE);
                                 CurrPage.UPDATE(TRUE);
                               END;
                                }
      { 1100528702;1 ;Action    ;
                      Name=Next;
                      CaptionML=[DEU=&Weiter >;
                                 ENU=&Next >;
                                 NLD=Vo&lgende >;
                                 NOR=&Neste >;
                                 SVE=&NÑsta >];
                      Enabled=NextEnable;
                      InFooterBar=Yes;
                      Image=NextRecord;
                      OnAction=BEGIN
                                 ShowStep(FALSE);
                                 IF WizardStep < 3 THEN
                                   WizardStep += 1;
                                 ShowStep(TRUE);
                                 CurrPage.UPDATE(TRUE);
                               END;
                                }
      { 1100528701;1 ;Action    ;
                      CaptionML=[DEU=B&eenden;
                                 ENU=&Finish;
                                 NLD=&Voltooien;
                                 NOR=&Fullfõr;
                                 SVE=&Slutfîr];
                      Enabled=FinishEnable;
                      InFooterBar=Yes;
                      Image=Completed;
                      OnAction=BEGIN
                                 ProcessType := STRMENU(Text002, 1);
                                 IF ProcessType > 0 THEN BEGIN
                                   CurrPage.CertificateSubform.PAGE.GetCertificateSelection(TempFilterCertForPurchAct);
                                   ProcessGetVendors(
                                     TempFilterCertForPurchAct, ProcessType, gSource, gEstNoCde, gProjNoCde, gPurchActCde,
                                     DateFilter, CertDateFilter, ContractTypeFilter, CombFeatBln, CombCertificates);
                                   CurrPage.CLOSE;
                                 END;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1210190002;1;Group  ;
                Name=Step1;
                Visible=Step1Visible;
                GroupType=Group }

    { 1210190009;2;Field  ;
                Name=DateFromTB;
                CaptionML=[DEU=Datumfilter Beurteilungen;
                           ENU=Date Filter Assessment;
                           NLD=Datumfilter beoordelingen;
                           NOR=Datofiltervurdering;
                           SVE=Datumfilterbedîmning];
                SourceExpr=DateFilter;
                Visible=DateFromTBVisible;
                OnValidate=VAR
                             VendorAssessmentLineNew@1100485001 : Record 11072420;
                           BEGIN
                             TextManagement.MakeDateFilter(DateFilter);
                             VendorAssessmentLineNew.SETFILTER(Date, DateFilter);
                             DateFilter := VendorAssessmentLineNew.GETFILTER(Date);
                           END;
                            }

    { 1210190008;2;Field  ;
                CaptionML=[DEU=FÑlligkeitsdatumsfilter Zertifikate;
                           ENU=Due Date Filter Certificates;
                           NLD=Vervaldatumfilter certificaten;
                           NOR=Forfallsdatofilter, sertifikater;
                           SVE=Fîrfallodatumfilter, certifikat];
                SourceExpr=CertDateFilter;
                OnValidate=VAR
                             VendorAssessmentLineNew@1100525000 : Record 11072420;
                           BEGIN
                             TextManagement.MakeDateFilter(DateFilter);
                             VendorAssessmentLineNew.SETFILTER(Date, DateFilter);
                             DateFilter := VendorAssessmentLineNew.GETFILTER(Date);
                           END;
                            }

    { 1210190114;2;Field  ;
                CaptionML=[DEU=Vertragsart;
                           ENU=Contract Type;
                           NLD=Contractvorm;
                           NOR=Kontraktstype;
                           SVE=Kontraktstyp];
                OptionCaptionML=[DEU=" ,Kein Vertrag,Einkaufsbedingungen,Rabattbedingungen";
                                 ENU=" ,No Contract,Contract,Contract with Discount Terms";
                                 NLD=" ,Geen contract,Inkoopvoorwaarden,Kortingscondities";
                                 NOR=" ,Ingen Kontrakt,Kontrakt,Kontrakt med rabattvilkÜr";
                                 SVE=" ,Inget kontrakt,Kontrakt,Kontrakt med rabattvillkor"];
                SourceExpr=ContractTypeFilter;
                OnValidate=BEGIN
                             ContractTypeFilterOnAfterValid;
                           END;
                            }

    { 1100485002;2;Field  ;
                CaptionML=[DEU=Merkmale kombinieren;
                           ENU=Combine features;
                           NLD=Kenmerken combineren;
                           NOR=Kombiner funksjoner;
                           SVE=Kombinera funktioner];
                SourceExpr=CombFeatBln }

    { 1210190004;2;Field  ;
                CaptionML=[DEU=Zertifikate kombinieren;
                           ENU=Combine certificates;
                           NLD=Certificaten combineren;
                           NOR=Kombiner sertifikater;
                           SVE=Kombinera certifikat];
                SourceExpr=CombCertificates }

    { 1100525000;1;Group  ;
                Name=Step2;
                Visible=Step2Visible }

    { 1210190000;2;Group  ;
                GroupType=Repeater }

    { 1210190001;3;Field  ;
                SourceExpr="Contact Feature Group";
                Editable=FALSE }

    { 1210190003;3;Field  ;
                SourceExpr="Contact Feature";
                Editable=FALSE }

    { 1210190005;3;Field  ;
                SourceExpr="Contact Feature Description" }

    { 1210190007;3;Field  ;
                SourceExpr=Filter }

    { 1100485000;3;Field  ;
                BlankZero=Yes;
                SourceExpr="Minimum Score";
                Visible="Minimum ScoreVisible";
                Editable="Minimum ScoreEditable" }

    { 1100528700;1;Group  ;
                Name=Step3;
                Visible=Step3Visible;
                GroupType=Group }

    { 1100528704;2;Part   ;
                Name=CertificateSubform;
                PagePartID=Page11124915;
                PartType=Page }

  }
  CODE
  {
    VAR
      TextManagement@1100525000 : Codeunit 41;
      TempFilterCertForPurchAct@1100528707 : TEMPORARY Record 11020593;
      CombFeatBln@1100525002 : Boolean;
      gSource@1100525004 : Option;
      ProcessType@1100525006 : Integer;
      gProjNoCde@1210190006 : Code[20];
      gEstNoCde@1100485003 : Code[20];
      gPurchActCde@1210190008 : Code[10];
      DateFilter@1100485001 : Text[30];
      Text002@1100525005 : TextConst 'DEU=Kreditoren,Kontakte,Beide;ENU=Vendors,Contacts,Both;NLD=Leveranciers,Relaties,Beide;NOR=Leverandõrer,Kontakter,Begge;SVE=Leverantîrer,Kontakter,BÜda';
      CertDateFilter@1100525008 : Text[30];
      CombCertificates@1100525007 : Boolean;
      ContractTypeFilter@1210190000 : ' ,No Contract,Contract,Contract with Discount Terms';
      "Minimum ScoreEditable"@19077802 : Boolean INDATASET;
      "Minimum ScoreVisible"@19079098 : Boolean INDATASET;
      DateFromTBVisible@19037905 : Boolean INDATASET;
      Step1Visible@1100528706 : Boolean INDATASET;
      Step2Visible@1100528705 : Boolean INDATASET;
      Step3Visible@1100528704 : Boolean INDATASET;
      FinishEnable@1100528703 : Boolean INDATASET;
      BackEnable@1100528702 : Boolean INDATASET;
      NextEnable@1100528701 : Boolean INDATASET;
      WizardStep@1100528700 : Integer;

    PROCEDURE SetProjectNo@1210190000(lProjNoCde@1210190000 : Code[20];lPurchActCde@1210190001 : Code[10]);
    VAR
      lvPurchActVendRec@1100525000 : Record 11012068;
    BEGIN
      gProjNoCde := lProjNoCde;
      gPurchActCde := lPurchActCde;
      gEstNoCde := '';
      gSource := lvPurchActVendRec.Source::Project;
    END;

    PROCEDURE SetEstNo@1100485000(lEstNoCde@1210190000 : Code[20];lPurchActCde@1210190001 : Code[10]);
    VAR
      lvPurchActVendRec@1100525000 : Record 11012068;
    BEGIN
      gEstNoCde := lEstNoCde;
      gPurchActCde := lPurchActCde;
      gProjNoCde := '';
      gSource := lvPurchActVendRec.Source::Estimate;
    END;

    LOCAL PROCEDURE ShowStep@2(Visible@1001 : Boolean);
    VAR
      FilterCertForPurchAct@1100528700 : Record 11020593;
    BEGIN
      CASE WizardStep OF
        1:
          BEGIN
            Step1Visible := Visible;
            IF Visible THEN BEGIN
              FinishEnable := FALSE;
              BackEnable := FALSE;
              NextEnable := TRUE;
            END;
          END;
        2:
          BEGIN
            Step2Visible := Visible;
            IF Visible THEN BEGIN
              FinishEnable := FALSE;
              BackEnable := TRUE;
              NextEnable := TRUE;
            END;
          END;
        3:
          BEGIN
            Step3Visible := Visible;
            IF Visible THEN BEGIN
              // When step 3 is showed for the 2nd time there are problemen with marking the filter field.
              // This is a standard Nav bug. Serveral scenarios did not solve the problem.
              IF TempFilterCertForPurchAct.ISEMPTY THEN BEGIN
                FilterCertForPurchAct.GetCertificates(TempFilterCertForPurchAct);
                CurrPage.CertificateSubform.PAGE.SetCertificates(TempFilterCertForPurchAct);
                CurrPage.CertificateSubform.PAGE.UpdatePage;
              END;

              FinishEnable := TRUE;
              BackEnable := TRUE;
              NextEnable := FALSE;
            END;
          END;
      END;
    END;

    LOCAL PROCEDURE ContractTypeFilterOnAfterValid@19064339();
    BEGIN
      CurrPage.UPDATE(TRUE); //**4PS.n
    END;

    LOCAL PROCEDURE OnAfterGetCurrRecord@19077479();
    VAR
      Feature@1100485000 : Record 11012752;
    BEGIN
      xRec := Rec;
      IF (Feature.GET("Contact Feature Group","Contact Feature")) AND
         (Feature.Department <> '') THEN
        "Minimum ScoreEditable" := TRUE
      ELSE
        "Minimum ScoreEditable" := FALSE;
    END;

    BEGIN
    {
      4PS MVOS 20100920 : Added combine certificates functionality and certificate dateiflter.
    }
    END.
  }
}

