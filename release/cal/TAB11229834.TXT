OBJECT Table 11229834 Feature (Project Element)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               IF IsInUse THEN
                 ERROR(Text002);
             END;

    CaptionML=ENU=Feature (Project Element);
    LookupPageID=Page11126484;
    DrillDownPageID=Page11126484;
  }
  FIELDS
  {
    { 10  ;   ;Code                ;Code20        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod] }
    { 20  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 30  ;   ;Value Type          ;Option        ;OnValidate=VAR
                                                                ProjectElementFeatureOption@1100525000 : Record 11229836;
                                                              BEGIN
                                                                UpdateProjectElementFeature;
                                                                IF (xRec."Value Type" IN ["Value Type"::Option, "Value Type"::"Yes/No"]) AND (xRec."Value Type" <> "Value Type") THEN BEGIN
                                                                  IF IsInUse THEN
                                                                    ERROR(Text003);
                                                                  ProjectElementFeatureOption.SETRANGE(Feature, Code);
                                                                  ProjectElementFeatureOption.DELETEALL(TRUE);
                                                                END;

                                                                IF "Value Type" = "Value Type"::"Yes/No" THEN BEGIN
                                                                  ProjectElementFeatureOption.INIT;
                                                                  ProjectElementFeatureOption.Feature := Code;
                                                                  ProjectElementFeatureOption.Code := 'YES';
                                                                  ProjectElementFeatureOption.Description := Text000;
                                                                  ProjectElementFeatureOption.INSERT(TRUE);

                                                                  ProjectElementFeatureOption.Feature := Code;
                                                                  ProjectElementFeatureOption.Code := 'NO';
                                                                  ProjectElementFeatureOption.Description := Text001;
                                                                  ProjectElementFeatureOption.INSERT(TRUE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Field Type;
                                                              NOR=Felttype;
                                                              SVE=Field Type];
                                                   OptionCaptionML=[ENU=Date,Value,Text,Option,Yes/No;
                                                                    SVE=Datum,V„rde,Text,Alternativ,Ja/Nej];
                                                   OptionString=Date,Value,Text,Option,Yes/No }
    { 40  ;   ;Unit Of Measure     ;Code10        ;TableRelation="Unit of Measure";
                                                   CaptionML=[ENU=Unit Of Measure;
                                                              NOR=Enhet;
                                                              SVE=Enhet] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528801 : TextConst 'ENU=Yes;NOR=Ja;SVE=Ja';
      Text001@1100528800 : TextConst 'ENU=No;NOR=Nr.;SVE=Nej';
      Text002@1100528803 : TextConst 'ENU=Feature in use, removing not allowed';
      Text003@1100528802 : TextConst 'ENU=Feature in use, modifying not allowed';

    PROCEDURE IsInUse@1100528800() : Boolean;
    VAR
      ProjectElementFeature@1100528800 : Record 11229835;
    BEGIN
      ProjectElementFeature.SETRANGE(Feature, Code);
      EXIT(NOT ProjectElementFeature.ISEMPTY);
    END;

    PROCEDURE UpdateProjectElementFeature@1100528803();
    VAR
      ProjectElementFeature@1100528800 : Record 11229835;
    BEGIN
      IF ((xRec."Value Type" IN ["Value Type"::Option, "Value Type"::"Yes/No"]) OR
          ("Value Type" IN ["Value Type"::Option, "Value Type"::"Yes/No"]) AND
         (xRec."Value Type" <> "Value Type"))
      THEN BEGIN
        ProjectElementFeature.SETRANGE(Feature, Code);
        IF ProjectElementFeature.FINDSET THEN
          REPEAT
            ProjectElementFeature."Value Type" := "Value Type";
            ProjectElementFeature.MODIFY(TRUE);
          UNTIL ProjectElementFeature.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}

