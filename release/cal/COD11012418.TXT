OBJECT Codeunit 11012418 Complaint Management
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1100485000 : TextConst 'DEU=Neue Beschwerde fÅr %1 ''%2'' erstellen?;ENU=Create new Complaint for %1 ''%2''?;NLD=Nieuwe klacht aanmaken voor %1 ''%2''?;NOR=Opprette ny klage for %1 ''%2''?;SVE=Skapa nytt klagomÜl fîr %1 ''%2''?';
      Text001@1100485001 : TextConst 'DEU=Beschwerdennummer kann nicht bestimmt werden. Leeres Feld unzulÑssig.;ENU=Complaint No. can not be determined, empty not allowed.;NLD=Klachtnummer kan niet bepaald worden, leeg niet toegestaan.;NOR=Klagenr kan ikke bestemmes, tomt ikke tillatt.;SVE=KlagomÜlsnr kan inte faststÑllas och fÜr inte vara tomt.';

    PROCEDURE CreateNewComplaintOnSource@1100485000(ISourceType@1100485000 : Option;ISourceNo@1100485001 : Code[20];ISourceNo2@1100485002 : Code[20]);
    VAR
      lvComplaintRec@1100485003 : Record 11012450;
      lvCustRec@1100485005 : Record 18;
      lvVendRec@1100485006 : Record 23;
      lvDimValRec@1100485009 : Record 349;
      lvPlantTypeRec@1100485007 : Record 11012551;
      lvPlantNoRec@1100485008 : Record 11012552;
      lvPlantLocRec@1100485015 : Record 11012554;
      lvRentalUnitRec@1100485014 : Record 11012940;
      lvDimMgtCU@1100485010 : Codeunit 408;
      lvSourceContactNo@1100485011 : Code[20];
      lvSourceContactName@1100485012 : Text[100];
      lvSourceContactPhone@1100485013 : Text[100];
      lvText@1100485004 : Text[100];
    BEGIN
      WITH lvComplaintRec DO BEGIN
        IF (ISourceNo = '') THEN
          EXIT;

        lvText := ISourceNo;
        CASE ISourceType OF
          "Source Type"::Cust:
            BEGIN
              lvCustRec.GET(ISourceNo);
              lvSourceContactName := lvCustRec.Contact;
              lvSourceContactPhone := lvCustRec."Phone No.";
            END;
          "Source Type"::Vend:
            BEGIN
              lvVendRec.GET(ISourceNo);
              lvSourceContactName := lvVendRec.Contact;
              lvSourceContactPhone := lvVendRec."Phone No.";
            END;
          "Source Type"::Dept:
             lvDimMgtCU.GetDimValueRec(1, ISourceNo, lvDimValRec, TRUE, '');
          "Source Type":: Plant:
            BEGIN
              IF (ISourceNo2 = '') THEN
                lvPlantTypeRec.GET(ISourceNo)
              ELSE BEGIN
                lvText := lvText + '-' + ISourceNo2;
                lvPlantNoRec.GET(ISourceNo, ISourceNo2);
              END;
            END;
          "Source Type":: PlantLoc:
            lvPlantLocRec.GET(ISourceNo);
          "Source Type"::RentalUnit:
            BEGIN
              lvText := lvText + '-' + ISourceNo2;
              lvRentalUnitRec.GET(ISourceNo, ISourceNo2);
            END;
          ELSE
            EXIT;
        END;

        "Source Type" := ISourceType;
        IF NOT CONFIRM(Text000, TRUE, "Source Type", lvText) THEN
          EXIT;

        INIT;
        "No." := '';
        Status := Status::Received;
        VALIDATE("Source Type", ISourceType);
        VALIDATE("Source No.", ISourceNo);
        IF (ISourceNo2 <> '') AND (ISourceType IN ["Source Type"::Plant,"Source Type"::RentalUnit]) THEN
          VALIDATE("Source No. 2", ISourceNo2);
        "Source Contact No." := lvSourceContactNo;
        "Source Contact Name" := COPYSTR(lvSourceContactName, 1, MAXSTRLEN("Source Contact Name"));
        "Source Phone No." := COPYSTR(lvSourceContactPhone, 1, MAXSTRLEN("Source Phone No."));
        INSERT(TRUE);
        IF ("No." = '') THEN
          ERROR(Text001);

        COMMIT;
        SETCURRENTKEY("Source Type" , "Source No." , "Source No. 2");
        SETRANGE("No.", "No.");
        PAGE.RUNMODAL(PAGE::"Complaint Management", lvComplaintRec);
      END;
    END;

    PROCEDURE CreateNewComplaintOnDocument@1100485002(IDocumentType@1100485000 : Option;IDocumentNo@1100485001 : Code[20]);
    VAR
      lvComplaintRec@1100485003 : Record 11012450;
      lvComplaintDocRec@1100485002 : Record 11012457;
      lvSalesHeadRec@1100485010 : Record 36;
      lvSalesInvoiceHeadRec@1100485009 : Record 112;
      lvSalesCredMemHeadRec@1100485008 : Record 114;
      lvPurchHeadRec@1100485007 : Record 38;
      lvPlantOrderRec@1100485015 : Record 11012556;
      lvPostedPlantTransportOrderRec@1100485016 : Record 11020507;
      ExitOrder@1100525000 : Record 11012559;
      RentalCorrection@1100525001 : Record 11012565;
      lvContRec@1100485014 : Record 5050;
      lvSourceType@1100485006 : Option;
      lvSourceNo@1100485005 : Code[20];
      lvSourceContactNo@1100485013 : Code[20];
      lvSourceContactName@1100485012 : Text[100];
      lvSourceContactPhone@1100485011 : Text[100];
    BEGIN
      WITH lvComplaintDocRec DO BEGIN
        IF (IDocumentNo = '') THEN
          EXIT;

        CASE IDocumentType OF
          "Document Type"::"Q-S":
            BEGIN
              lvSalesHeadRec.GET(lvSalesHeadRec."Document Type"::Quote, IDocumentNo);
              lvSourceType := lvComplaintRec."Source Type"::Cust;
              lvSourceNo := lvSalesHeadRec."Sell-to Customer No.";
              lvSourceContactNo := lvSalesHeadRec."Sell-to Contact No.";
              lvSourceContactName := lvSalesHeadRec."Sell-to Contact";
            END;
          "Document Type"::"O-S":
            BEGIN
              lvSalesHeadRec.GET(lvSalesHeadRec."Document Type"::Order, IDocumentNo);
              lvSourceType := lvComplaintRec."Source Type"::Cust;
              lvSourceNo := lvSalesHeadRec."Sell-to Customer No.";
              lvSourceContactNo := lvSalesHeadRec."Sell-to Contact No.";
              lvSourceContactName := lvSalesHeadRec."Sell-to Contact";
            END;
          "Document Type"::"PI-S":
            BEGIN
              lvSalesInvoiceHeadRec.GET(IDocumentNo);
              lvSourceType := lvComplaintRec."Source Type"::Cust;
              lvSourceNo := lvSalesInvoiceHeadRec."Sell-to Customer No.";
              lvSourceContactNo := lvSalesInvoiceHeadRec."Sell-to Contact No.";
              lvSourceContactName := lvSalesInvoiceHeadRec."Sell-to Contact";
            END;
          "Document Type"::"PM-S":
            BEGIN
              lvSalesCredMemHeadRec.GET(IDocumentNo);
              lvSourceType := lvComplaintRec."Source Type"::Cust;
              lvSourceNo := lvSalesCredMemHeadRec."Sell-to Customer No.";
              lvSourceContactName := lvSalesCredMemHeadRec."Sell-to Contact";
            END;
          "Document Type"::"Q-P":
            BEGIN
              lvPurchHeadRec.GET(lvPurchHeadRec."Document Type"::Quote, IDocumentNo);
              lvSourceType := lvComplaintRec."Source Type"::Vend;
              lvSourceNo := lvPurchHeadRec."Buy-from Vendor No.";
              lvSourceContactNo := lvPurchHeadRec."Buy-from Contact No.";
              lvSourceContactName := lvPurchHeadRec."Buy-from Contact";
            END;
          "Document Type"::"O-P":
            BEGIN
              lvPurchHeadRec.GET(lvPurchHeadRec."Document Type"::Order, IDocumentNo);
              lvSourceType := lvComplaintRec."Source Type"::Vend;
              lvSourceNo := lvPurchHeadRec."Buy-from Vendor No.";
              lvSourceContactNo := lvPurchHeadRec."Buy-from Contact No.";
              lvSourceContactName := lvPurchHeadRec."Buy-from Contact";
            END;
          "Document Type"::"PO-Pl":
            BEGIN
              lvPlantOrderRec.GET(IDocumentNo);
              IF (lvPlantOrderRec.Type <> lvPlantOrderRec.Type::Removal) THEN
                lvSourceContactName := lvPlantOrderRec."To Location Contact"
              ELSE
                lvSourceContactName := lvPlantOrderRec."From Location Contact";
            END;
          "Document Type"::"TO-Pl":
            BEGIN
              lvPostedPlantTransportOrderRec.GET(IDocumentNo);
              IF (lvPostedPlantTransportOrderRec.Type <> lvPostedPlantTransportOrderRec.Type::Removal) THEN
                lvSourceContactName := lvPostedPlantTransportOrderRec."To Location Contact"
              ELSE
                lvSourceContactName := lvPostedPlantTransportOrderRec."From Location Contact";
            END;
          "Document Type"::"EO-Pl":
            BEGIN
              ExitOrder.GET(IDocumentNo);
              lvSourceContactName := ExitOrder.Contact;
            END;
          "Document Type"::"RC-Pl":
            BEGIN
              RentalCorrection.GET(IDocumentNo);
            END;
          ELSE
            EXIT;
        END;

        IF (lvSourceContactNo <> '') THEN BEGIN
          IF NOT lvContRec.GET(lvSourceContactNo) THEN
            lvSourceContactNo := ''
          ELSE BEGIN
            IF (lvSourceContactName = '') THEN
              lvSourceContactName := lvContRec.Name
            ELSE BEGIN
              IF (lvContRec.Name <> lvSourceContactName) THEN
                lvSourceContactNo := '';
            END;
            IF (lvSourceContactPhone = '') AND (lvSourceContactNo <> '') THEN
              lvSourceContactPhone := lvContRec."Phone No.";
          END;
        END;

        "Document Type" := IDocumentType;
        IF NOT CONFIRM(Text000, TRUE, "Document Type", IDocumentNo) THEN
          EXIT;
      END;

      WITH lvComplaintRec DO BEGIN
        INIT;
        "No." := '';
        Status := Status::Received;
        "Source Type" := lvSourceType;
        "Source No." := lvSourceNo;
        "Source Contact No." := lvSourceContactNo;
        "Source Contact Name" := COPYSTR(lvSourceContactName, 1, MAXSTRLEN("Source Contact Name"));
        "Source Phone No." := COPYSTR(lvSourceContactPhone, 1, MAXSTRLEN("Source Phone No."));
        INSERT(TRUE);
        IF ("No." = '') THEN
          ERROR(Text001);

        lvComplaintDocRec.INIT;
        lvComplaintDocRec."Complaint No." := "No.";
        lvComplaintDocRec."Line No." := 10000;
        lvComplaintDocRec."Document Type" := IDocumentType;
        lvComplaintDocRec."Document No." := IDocumentNo;
        lvComplaintDocRec.INSERT;

        COMMIT;
        SETCURRENTKEY("Source Type" , "Source No." , "Source No. 2");
        SETRANGE("No.", "No.");
        PAGE.RUNMODAL(PAGE::"Complaint Management", lvComplaintRec);
      END;
    END;

    PROCEDURE ShowComplaintOnDocument@1100485001(IDocumentType@1100485001 : Option;IDocumentNo@1100485000 : Code[20];IOpenComplaints@1100485004 : Boolean);
    VAR
      lvComplaintRec@1100485003 : Record 11012450;
      lvComplaintDocRec@1100485002 : Record 11012457;
      lvSourceType@1100485006 : Option;
      lvFirst@1100485007 : Boolean;
      lvSourceNo@1100485005 : Code[20];
      lvComplNo@1100485008 : Code[20];
    BEGIN
      WITH lvComplaintRec DO BEGIN
        CASE IDocumentType OF
          lvComplaintDocRec."Document Type"::"Q-S",
          lvComplaintDocRec."Document Type"::"O-S",
          lvComplaintDocRec."Document Type"::"PI-S",
          lvComplaintDocRec."Document Type"::"PM-S":
            lvSourceType := "Source Type"::Cust;
          lvComplaintDocRec."Document Type"::"Q-P",
          lvComplaintDocRec."Document Type"::"O-P":
            lvSourceType := "Source Type"::Vend;
          lvComplaintDocRec."Document Type"::"PO-Pl",
          lvComplaintDocRec."Document Type"::"TO-Pl",
          lvComplaintDocRec."Document Type"::"EO-Pl",
          lvComplaintDocRec."Document Type"::"RC-Pl":
            lvSourceType := 0;
          ELSE
            lvSourceType := -1;
        END;

        IF (lvSourceType >= 0) AND (IDocumentNo <> '') THEN BEGIN
          lvFirst := TRUE;
          lvComplaintDocRec.SETRANGE("Document Type", IDocumentType);
          lvComplaintDocRec.SETRANGE("Document No.", IDocumentNo);
          IF lvComplaintDocRec.FIND('-') THEN BEGIN
            REPEAT
              IF GET(lvComplaintDocRec."Complaint No.") THEN BEGIN
                IF (NOT IOpenComplaints) OR (Status <> Status::Finished) THEN BEGIN
                  MARK(TRUE);
                  IF lvFirst THEN BEGIN
                    lvFirst := FALSE;
                    lvSourceNo := "Source No.";
                    lvComplNo := "No.";
                  END;
                  IF ("Source Type" <> lvSourceType) THEN
                    lvSourceType := -1;  //* More than one Source Type
                  IF ("Source No." <> lvSourceNo) THEN
                    lvSourceNo := '';  //* More than one Source No
                END;
              END;
            UNTIL lvComplaintDocRec.NEXT = 0;
          END;
        END;

        SETCURRENTKEY("Source Type", "Source No.");
        IF (lvSourceType >= 0) THEN
          SETRANGE("Source Type", lvSourceType);
        IF (lvSourceNo <> '') OR (lvSourceType = 0) THEN
          SETRANGE("Source No.", lvSourceNo);
        IF IOpenComplaints THEN
          SETFILTER(Status, '<>%1', Status::Finished);
        lvComplaintRec.SETRANGE("Document Type Filter", IDocumentType);
        lvComplaintRec.SETRANGE("Doc. Type Filter On Complaint", TRUE);
        MARKEDONLY(TRUE);
        "No." := lvComplNo;
        PAGE.RUNMODAL(PAGE::"Complaint List", lvComplaintRec);
      END;
    END;

    BEGIN
    END.
  }
}

