OBJECT Codeunit 23 Item Jnl.-Post Batch
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04,4PS14.00,4PSSE;
  }
  PROPERTIES
  {
    TableNo=83;
    Permissions=TableData 27=m,
                TableData 233=imd,
                TableData 7313=r,
                TableData 11020316=rimd,
                TableData 11020317=rimd;
    OnRun=BEGIN
            ItemJnlLine.COPY(Rec);
            Code;
            Rec := ItemJnlLine;
            //**4PS.sn
            IF (ICInventoryPostingSuccessful) THEN
              "Line No." := 1;
            //**4PS.en
          END;

  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=Journal Batch Name    #1##########\\;NOR=Kladdenavn            #1##########\\;SVE=Journalnamn           #1##########\\';
      Text002@1002 : TextConst 'ENU=Checking lines        #2######\;NOR=Kontrollerer linjer   #2######\;SVE=Kontrollerar rader    #2######\';
      Text003@1003 : TextConst 'ENU=Posting lines         #3###### @4@@@@@@@@@@@@@\;NOR=Bokf›rer linjer       #3###### @4@@@@@@@@@@@@@\;SVE=Bokf”ring rader       #3###### @4@@@@@@@@@@@@@\';
      Text004@1004 : TextConst 'ENU=Updating lines        #5###### @6@@@@@@@@@@@@@;NOR=Oppdaterer linjer     #5###### @6@@@@@@@@@@@@@;SVE=Uppdatering rader     #5###### @6@@@@@@@@@@@@@';
      Text005@1005 : TextConst 'ENU=Posting lines         #3###### @4@@@@@@@@@@@@@;NOR=Bokf›rer linjer       #3###### @4@@@@@@@@@@@@@;SVE=Bokf”ring rader       #3###### @4@@@@@@@@@@@@@';
      Text006@1006 : TextConst 'ENU=A maximum of %1 posting number series can be used in each journal.;NOR=Opptil %1 bokf›ringsnummerserier kan brukes i hver kladd.;SVE=Maximalt kan %1 nr-serier f”r bokf”ring anv„ndas i varje journal.';
      Text007@1007 : TextConst 'ENU=<Month Text>;NOR=<Month Text>;SVE=<Month Text>';
      Text008@1008 : TextConst 'ENU=There are new postings made in the period you want to revalue item no. %1.\;NOR=Det finnes nye bokf›ringer i perioden der du vil revaluere varenr. %1.\;SVE=Det finns nya bokf”ringar gjorda i perioden, vill du omv„rdera artikelnr %1.\';
      Text009@1009 : TextConst 'ENU=You must calculate the inventory value again.;NOR=Du m† beregne lagerverdien p† nytt.;SVE=Du m†ste ber„kna lagerv„rdet igen.';
      Text010@1061 : TextConst '@@@="One or more reservation entries exist for the item with Item No. = 1000, Location Code = BLUE, Variant Code = NEW which may be disrupted if you post this negative adjustment. Do you want to continue?";ENU="One or more reservation entries exist for the item with %1 = %2, %3 = %4, %5 = %6 which may be disrupted if you post this negative adjustment. Do you want to continue?";NOR="n eller flere reservasjonsposter finnes for varen med %1 = %2, %3 = %4, %5 = %6, som kan avbrutt hvis du bokf›rer denne nedjusteringen. Vil du fortsette?";SVE="Det finns en eller flera reservationstransaktioner f”r artikeln med %1 = %2, %3 = %4, %5 = %6 som kan avbrytas om du bokf”r den h„r negativa justeringen. Vill du forts„tta?"';
      Text11012001@1100485001 : TextConst 'ENU=Quantity must be greater than 0 if you want to create Plant;NOR=Antallet m† v‘re st›rre enn 0 om du vil opprette maskin;SVE=Antalet m†ste vara st”rre „n 0 om du vill skapa Maskin';
      ItemJnlTemplate@1010 : Record 82;
      ItemJnlBatch@1011 : Record 233;
      ItemJnlLine@1012 : Record 83;
      ItemLedgEntry@1015 : Record 32;
      WhseEntry@1049 : Record 7312;
      ItemReg@1016 : Record 46;
      WhseReg@1025 : Record 7313;
      GLSetup@1017 : Record 98;
      InvtSetup@1050 : Record 313;
      AccountingPeriod@1018 : Record 50;
      NoSeries@1019 : TEMPORARY Record 308;
      Location@1043 : Record 14;
      TempSKUrec@1100485009 : TEMPORARY Record 5700;
      ItemJnlCheckLine@1021 : Codeunit 21;
      ItemJnlPostLine@1022 : Codeunit 22;
      NoSeriesMgt@1023 : Codeunit 396;
      NoSeriesMgt2@1024 : ARRAY [10] OF Codeunit 396;
      WMSMgmt@1046 : Codeunit 7302;
      WhseJnlPostLine@1048 : Codeunit 7301;
      InvtAdjmt@1045 : Codeunit 5895;
      Window@1027 : Dialog;
      ItemRegNo@1028 : Integer;
      WhseRegNo@1044 : Integer;
      StartLineNo@1029 : Integer;
      Day@1030 : Integer;
      Week@1031 : Integer;
      Month@1032 : Integer;
      MonthText@1033 : Text[30];
      NoOfRecords@1034 : Integer;
      LineCount@1035 : Integer;
      LastDocNo@1036 : Code[20];
      LastDocNo2@1037 : Code[20];
      LastPostedDocNo@1038 : Code[20];
      NoOfPostingNoSeries@1039 : Integer;
      PostingNoSeriesNo@1040 : Integer;
      WhseTransaction@1047 : Boolean;
      PhysInvtCount@1052 : Boolean;
      SuppressCommit@1013 : Boolean;
      ProjSetupRec@1210190000 : Record 315;
      JobJnlLine@1210190009 : Record 11072008;
      JobJnlLine2@11012010 : Record 11072008;
      ProjRec@1210190001 : Record 11072003;
      ProjectType@1210190002 : Record 11012009;
      ServJnlLine@1210190015 : Record 11012820;
      ServJnlLine2@1210190014 : Record 11012820;
      ServOrderRec@1210190012 : Record 11012823;
      ServiceType@1210190011 : Record 11012814;
      PlantTypeRec@1100485003 : Record 11012551;
      PlantNoRec@1100525004 : Record 11012552;
      DimValRec@11012011 : Record 349;
      SurchDimValRec@1210190008 : Record 349;
      SurchargeRec@11012013 : Record 11020208;
      GenJnlLine@11012014 : Record 81;
      SurchargePostingBuffer@11012016 : ARRAY [2] OF TEMPORARY Record 49;
      ComplWIPPostingBuffer@1100485007 : ARRAY [2] OF TEMPORARY Record 49;
      ItemRec@1100485000 : Record 27;
      PlantLedgerEntryRec@1100485002 : Record 11012572;
      InventSetupRec@1100485004 : Record 313;
      JobJnlPostLine@11012021 : Codeunit 11072003;
      ServJnlPostLine@1210190013 : Codeunit 11012802;
      PlantJnlPostLineCU@1100485005 : Codeunit 11012569;
      GenJnlPostLine@1000000003 : Codeunit 12;
      NSItemTrackingEntriesApply@1100528603 : Codeunit 11012352;
      DimMgt@1100525005 : Codeunit 408;
      SKURevaluationRun@1100485006 : Boolean;
      NotIncrJnlBatchNr@1100485008 : Boolean;
      USENEWPROCESSFORITEMCOST@1100525000 : Boolean;
      ValueEntry@1100525001 : Record 5802;
      ItemEntryNoBuff@1100525002 : TEMPORARY Record 83;
      TempNSTrackingSpecification@1100528601 : TEMPORARY Record 336;
      Item@1100528602 : Record 27;
      TotalValuePosted@1100525003 : Decimal;
      Text11012002@1100530000 : TextConst 'ENU=On create Fixed Asset from Plant ''%1-%2'' no FA-Depreciation Book is added, must be setup that this is done automaticaly.;NOR=N†r anleggsmidler opprettes fra maskinen ''%1-%2'' blir ingen avskrivningsbok for anleggsmidler lagt til. Det m† stilles inn for at dette skal skje automatisk .;SVE=N„r Maskinstillg†ngen skapas fr†n Maskinen ''%1-%2'' l„ggs ingen avskrivningsbok f”r Maskinstillg†ngar till. Det h„r m†ste st„llas in s† att det g”rs automatiskt.';
      Text11012003@1100530001 : TextConst 'ENU="On create Fixed Asset several Depreciation Books are added. Main Depreciation Book can not be determined, use ''Default Depreciation Books'' for Plant Type ''%1''. ";NOR=N†r anleggsmidler opprettes legges flere avskrivningsb›ker til. Prim‘ravskrivningsbok kan ikke fastsettes, bruk ''Standardavskrivningsb›ker'' for maskintype ''%1''.;SVE="N„r Maskinstillg†ngar skapas l„ggs flera avskrivningsb”cker till. Prim„r avskrivningsbok kan inte fastst„llas, anv„nd ''standardavskrivningsb”cker'' f”r Maskinstyp ''%1''. "';
      JobLedgEntryNo@1100528500 : Integer;
      ServLedgEntryNo@1100528501 : Integer;
      ICInventoryPostingSuccessful@1100528301 : Boolean;
      SupplyingCompany@1100409000 : Text[250];
      ReceivingCompany@1100409001 : Text[250];
      StdCostUpdated@1100525006 : Boolean;
      Text11012004@1100525007 : TextConst 'ENU=cannot exceed %1 characters;SVE=kan inte vara mer „n %1 tecken';

    LOCAL PROCEDURE Code@3();
    VAR
      lvStockSurchargeRec@1100485000 : Record 11020316;
      UpdateAnalysisView@1002 : Codeunit 410;
      UpdateItemAnalysisView@1006 : Codeunit 7150;
      PhysInvtCountMgt@1007 : Codeunit 7380;
      OldEntryType@1008 : 'Purchase,Sale,Positive Adjmt.,Negative Adjmt.,Transfer,Consumption,Output, ,Assembly Consumption,Assembly Output';
      RaiseError@1000 : Boolean;
      DimMgt@1100285001 : Codeunit 408;
      prevDimSetID@1100285000 : Integer;
    BEGIN
      OnBeforeCode(ItemJnlLine);

      USENEWPROCESSFORITEMCOST := TRUE; //**4PS.n Quick escape to old procedure

      WITH ItemJnlLine DO BEGIN
        LOCKTABLE;
        SETRANGE("Journal Template Name","Journal Template Name");
        SETRANGE("Journal Batch Name","Journal Batch Name");

        ItemJnlTemplate.GET("Journal Template Name");
        ItemJnlBatch.GET("Journal Template Name","Journal Batch Name");

        RaiseError := STRLEN(INCSTR(ItemJnlBatch.Name)) > MAXSTRLEN(ItemJnlBatch.Name); //**4PS.n
        OnBeforeRaiseExceedLengthError(ItemJnlBatch,RaiseError);

        //**4PS.sn
        IF RaiseError THEN
          IF NOT ItemJnlBatch."Do Not Increase Name" THEN
            IF NOT NotIncrJnlBatchNr THEN
              ItemJnlBatch.FIELDERROR(Name,STRSUBSTNO(Text11012004,MAXSTRLEN(ItemJnlBatch.Name)));
        //**4PS.en

        IF ItemJnlTemplate.Recurring THEN BEGIN
          SETRANGE("Posting Date",0D,WORKDATE);
          SETFILTER("Expiration Date",'%1 | %2..',0D,WORKDATE);
        END;

        IF NOT FIND('=><') THEN BEGIN
          "Line No." := 0;
          IF NOT SuppressCommit THEN
            COMMIT;
          EXIT;
        END;

        CheckItemAvailability(ItemJnlLine);

        IF ItemJnlTemplate.Recurring THEN
          Window.OPEN(
            Text001 +
            Text002 +
            Text003 +
            Text004)
        ELSE
          Window.OPEN(
            Text001 +
            Text002 +
            Text005);

        Window.UPDATE(1,"Journal Batch Name");

        //>>150121
        DimMgt.SetCompany("Receiving Company"); //**4PS.n
        DimMgt.SetDimensionValueChainsBool(TRUE);
        IF "Shortcut Dimension 1 Code" <> '' THEN BEGIN
          prevDimSetID := "Dimension Set ID";
          DimMgt.ValidateShortcutDimValues(1,"Shortcut Dimension 1 Code","Dimension Set ID");
          IF "Dimension Set ID" <> prevDimSetID THEN
            MODIFY(FALSE);
        END;
        IF "Shortcut Dimension 2 Code" <> '' THEN BEGIN
          prevDimSetID := "Dimension Set ID";
          DimMgt.ValidateShortcutDimValues(2,"Shortcut Dimension 2 Code","Dimension Set ID");
          IF "Dimension Set ID" <> prevDimSetID THEN
            MODIFY(FALSE);
        END;
        //<<150121
        //**4PS.sn
        SKURevaluationRun := FALSE;
        TempSKUrec.DELETEALL;
        //**4PS.en

        CheckLines(ItemJnlLine);

        // Find next register no.
        ItemLedgEntry.LOCKTABLE;
        IF ItemLedgEntry.FINDLAST THEN;
        IF WhseTransaction THEN BEGIN
          WhseEntry.LOCKTABLE;
          IF WhseEntry.FINDLAST THEN;
        END;

        ItemReg.LOCKTABLE;
        IF ItemReg.FINDLAST THEN
          ItemRegNo := ItemReg."No." + 1
        ELSE
          ItemRegNo := 1;

        WhseReg.LOCKTABLE;
        IF WhseReg.FINDLAST THEN
          WhseRegNo := WhseReg."No." + 1
        ELSE
          WhseRegNo := 1;

        GLSetup.GET;
        PhysInvtCount := FALSE;

        // Post lines
        LineCount := 0;
        OldEntryType := "Entry Type";
        PostLines(ItemJnlLine,PhysInvtCountMgt);

        // Copy register no. and current journal batch name to item journal
        IF NOT ItemReg.FINDLAST OR (ItemReg."No." <> ItemRegNo) THEN
          ItemRegNo := 0;
        IF NOT WhseReg.FINDLAST OR (WhseReg."No." <> WhseRegNo) THEN
          WhseRegNo := 0;

        PreparePostingForProjectAndService(ItemJnlLine);  //**4PS.n  C059363

        INIT;

        "Line No." := ItemRegNo;
        IF "Line No." = 0 THEN
          "Line No." := WhseRegNo;

        IF NOT USENEWPROCESSFORITEMCOST THEN BEGIN //**4PS.n
          InvtSetup.GET;
          IF InvtSetup."Automatic Cost Adjustment" <>
             InvtSetup."Automatic Cost Adjustment"::Never
          THEN BEGIN
            InvtAdjmt.SetProperties(TRUE,InvtSetup."Automatic Cost Posting");
            InvtAdjmt.MakeMultiLevelAdjmt;
          END;
        END; //**4PS.n


        // Update/delete lines
        OnBeforeUpdateDeleteLines(ItemJnlLine,ItemRegNo);
        IF "Line No." <> 0 THEN BEGIN
          IF ItemJnlTemplate.Recurring THEN BEGIN
            HandleRecurringLine(ItemJnlLine);
          END ELSE
            HandleNonRecurringLine(ItemJnlLine,OldEntryType);
          IF ItemJnlBatch."No. Series" <> '' THEN
            NoSeriesMgt.SaveNoSeries;
          IF NoSeries.FINDSET THEN
            REPEAT
              EVALUATE(PostingNoSeriesNo,NoSeries.Description);
              NoSeriesMgt2[PostingNoSeriesNo].SaveNoSeries;
            UNTIL NoSeries.NEXT = 0;
        END;

        IF PhysInvtCount THEN
          PhysInvtCountMgt.UpdateItemSKUListPhysInvtCount;
        //**4PS.sn
        IF SKURevaluationRun THEN BEGIN
          CalcSKUSurchargesNoStock;
          lvStockSurchargeRec.ImplementSKUSurchargeChanges(CURRENTDATETIME);
        END;
        //**4PS.en

        OnAfterPostJnlLines(ItemJnlBatch,ItemJnlLine,ItemRegNo,WhseRegNo);

        Window.CLOSE;
        IF NOT SuppressCommit THEN
          COMMIT;
        CLEAR(ItemJnlCheckLine);
        CLEAR(ItemJnlPostLine);
        CLEAR(WhseJnlPostLine);
        CLEAR(InvtAdjmt);
        CLEAR(JobJnlPostLine);  //**4PS.n
        CLEAR(GenJnlPostLine);  //**4PS.n
      END;
      UpdateAnalysisView.UpdateAll(0,TRUE);
      UpdateItemAnalysisView.UpdateAll(0,TRUE);
      IF NOT SuppressCommit THEN
        COMMIT;
    END;

    LOCAL PROCEDURE CheckLines@11(VAR ItemJnlLine@1000 : Record 83);
    BEGIN
      OnBeforeCheckLines(ItemJnlLine);

      WITH ItemJnlLine DO BEGIN
        LineCount := 0;
        StartLineNo := "Line No.";
        REPEAT
          LineCount := LineCount + 1;
          Window.UPDATE(2,LineCount);
          CheckRecurringLine(ItemJnlLine);

          //**4PS.sn
          IF "Receiving Company" <> '' THEN BEGIN
            ProjRec.CHANGECOMPANY("Receiving Company");
          END ELSE BEGIN
            ProjRec.CHANGECOMPANY(COMPANYNAME);
          END;
          IF ProjRec.GET("Job No.") THEN BEGIN
            IF ProjRec."Posting Element Mandatory" THEN
              TESTFIELD(Element);
          END;
          IF "Entry Type" IN ["Entry Type"::"Positive Adjmt.",  "Entry Type"::"Negative Adjmt."] THEN
            TESTFIELD("Job No.", '');
          //**4PS.en

          IF (("Value Entry Type" = "Value Entry Type"::"Direct Cost") AND ("Item Charge No." = '')) OR
             (("Invoiced Quantity" <> 0) AND (Amount <> 0))
             OR IsRevaluationOfAmountZero(ItemJnlLine) //**4PS.n C021411
          THEN BEGIN
            ItemJnlCheckLine.RunCheck(ItemJnlLine);

            IF (Quantity <> 0) AND
               ("Value Entry Type" = "Value Entry Type"::"Direct Cost") AND
               ("Item Charge No." = '')
            THEN
              CheckWMSBin(ItemJnlLine);

            IF ("Value Entry Type" = "Value Entry Type"::Revaluation) AND
               ("Inventory Value Per" = "Inventory Value Per"::" ") AND
               "Partial Revaluation"
            THEN
              CheckRemainingQty;

            OnAfterCheckJnlLine(ItemJnlLine,SuppressCommit);
          END;

          IF NEXT = 0 THEN
            FINDFIRST;
        UNTIL "Line No." = StartLineNo;
        NoOfRecords := LineCount;
      END;

      OnAfterCheckLines(ItemJnlLine);
    END;

    LOCAL PROCEDURE PostLines@12(VAR ItemJnlLine@1000 : Record 83;VAR PhysInvtCountMgt@1004 : Codeunit 7380);
    VAR
      TempTrackingSpecification@1003 : TEMPORARY Record 336;
      OriginalQuantity@1002 : Decimal;
      OriginalQuantityBase@1001 : Decimal;
      ICPostingSetupBySource@1100525000 : Record 11020565;
    BEGIN
      LastDocNo := '';
      LastDocNo2 := '';
      LastPostedDocNo := '';
      WITH ItemJnlLine DO BEGIN
        SETCURRENTKEY("Journal Template Name","Journal Batch Name","Line No.");
        FINDSET;

        //**4PS.sn
        IF "Receiving Company" <> '' THEN
          InventSetupRec.CHANGECOMPANY("Receiving Company")
        ELSE
          InventSetupRec.CHANGECOMPANY(COMPANYNAME);
        InventSetupRec.GET;
        ItemEntryNoBuff.RESET;   //** Temporary version of item journal line
        ItemEntryNoBuff.DELETEALL;
        //**4PS.en

        REPEAT
          IF NOT EmptyLine AND
             (ItemJnlBatch."No. Series" <> '') AND
             ("Document No." <> LastDocNo2)
             AND ("Posting No. Series" = '') //**4PS.n C020130
          THEN
            TESTFIELD("Document No.",NoSeriesMgt.GetNextNo(ItemJnlBatch."No. Series","Posting Date",FALSE));

          //**4PS.sn
          ItemEntryNoBuff.INIT;   //** Temporary version of item journal line
          ItemEntryNoBuff."Journal Template Name" := "Journal Template Name";
          ItemEntryNoBuff."Journal Batch Name" := "Journal Batch Name";
          ItemEntryNoBuff."Line No." := "Line No.";
          ItemLedgEntry.INIT;
          IF ItemLedgEntry.FINDLAST THEN;
          ItemEntryNoBuff."Last Item Ledger Entry No." := ItemLedgEntry."Entry No.";
          ItemEntryNoBuff.INSERT;
          //**4PS.en

          IF NOT EmptyLine THEN
            LastDocNo2 := "Document No.";
          MakeRecurringTexts(ItemJnlLine);
          ConstructPostingNumber(ItemJnlLine);

          //Fill temp table
          //**4PS.sn
          IF USENEWPROCESSFORITEMCOST THEN  BEGIN
            ItemEntryNoBuff."Document No." := "Document No.";
            ItemEntryNoBuff.MODIFY;
          END;
          //**4PS.en

          IF "Inventory Value Per" <> "Inventory Value Per"::" " THEN
            ItemJnlPostSumLine(ItemJnlLine)
          ELSE
            IF (("Value Entry Type" = "Value Entry Type"::"Direct Cost") AND ("Item Charge No." = '')) OR
               (("Invoiced Quantity" <> 0) AND (Amount <> 0))
            THEN BEGIN
              LineCount := LineCount + 1;
              Window.UPDATE(3,LineCount);
              Window.UPDATE(4,ROUND(LineCount / NoOfRecords * 10000,1));
              OriginalQuantity := Quantity;
              OriginalQuantityBase := "Quantity (Base)";

              //**4PS.sn
              IF (("Job No." <> '') OR ("Service Order No." <> '')) AND   //C038709
                 ("Receiving Company" <> '') AND
                 (InventSetupRec."Automatic Cost Posting") THEN
              BEGIN
                IF ICPostingSetupBySource.GET("Source Code", COMPANYNAME, "Receiving Company") THEN
                  IF ICPostingSetupBySource."Prod. Account Debit" <> '' THEN
                    ItemJnlPostLine.SetCOGSAccount(ICPostingSetupBySource."Prod. Account Debit");
              END;

              IF ("Receiving Company" <> '') AND ("Job No." = '') AND ("Service Order No." = '') THEN
                ItemJnlPostLine.SetCOGSAccount(GetAccount("Receiving Company",FALSE));

              IF ("Supplying Company" <> '') AND ("Job No." = '') AND ("Service Order No." = '') AND
                 ("IC Inventory Line Type" = "IC Inventory Line Type"::Transfer) THEN
                ItemJnlPostLine.SetCOGSAccount(GetAccount("Supplying Company", FALSE));

              IF ("Receiving Company" <> '') THEN BEGIN  //DP00387
                "Dimension Set ID" := 0;
                //DimMgt.SetCompany("Receiving Company");  //C053514
                DimMgt.ValidateShortcutDimValues(1,"Shortcut Dimension 1 Code","Dimension Set ID");
                DimMgt.ValidateShortcutDimValues(2,"Shortcut Dimension 2 Code","Dimension Set ID");
              END;
              //**4PS.en

              ItemJnlPostLine.SetSkipChangeCompany(TRUE);  //4PS.n
              IF NOT ItemJnlPostLine.RunWithCheck(ItemJnlLine) THEN
                ItemJnlPostLine.CheckItemTracking;
              ItemJnlPostLine.SetSkipChangeCompany(FALSE);  //4PS.n
              IF "Value Entry Type" <> "Value Entry Type"::Revaluation THEN BEGIN
                ItemJnlPostLine.CollectTrackingSpecification(TempTrackingSpecification);
                OnPostLinesBeforePostWhseJnlLine(ItemJnlLine,SuppressCommit);
                PostWhseJnlLine(ItemJnlLine,OriginalQuantity,OriginalQuantityBase,TempTrackingSpecification);
              END;
              //**4PS.sn
              IF NSItemTrackingApplicable(ItemJnlLine) THEN  //DP000121
                IF TempTrackingSpecification.FINDSET THEN
                  REPEAT
                    TempNSTrackingSpecification := TempTrackingSpecification;
                    TempNSTrackingSpecification.INSERT;
                  UNTIL TempTrackingSpecification.NEXT = 0;
              //**4PS.en
            END;

          IF IsPhysInvtCount(ItemJnlTemplate,"Phys Invt Counting Period Code","Phys Invt Counting Period Type") THEN BEGIN
            IF NOT PhysInvtCount THEN BEGIN
              PhysInvtCountMgt.InitTempItemSKUList;
              PhysInvtCount := TRUE;
            END;
            PhysInvtCountMgt.AddToTempItemSKUList("Item No.","Location Code","Variant Code","Phys Invt Counting Period Type");
          END;

          //**4PS.sn
          ItemJnlPostLine.SetCOGSAccount('');
          IF "To Plant Inventory" THEN
            CreatePlant;

          IF NOT USENEWPROCESSFORITEMCOST THEN
            PostSubadmin; //C025794.n

          IF ItemLedgEntry.FINDLAST THEN;
          IF ItemEntryNoBuff."Last Item Ledger Entry No." <> ItemLedgEntry."Entry No." THEN BEGIN
            ItemEntryNoBuff."Document Line No." := ItemEntryNoBuff."Last Item Ledger Entry No." + 1;
            ItemEntryNoBuff."Last Item Ledger Entry No." := ItemLedgEntry."Entry No.";
          END ELSE
            ItemEntryNoBuff."Last Item Ledger Entry No." := 0;

          ItemEntryNoBuff.MODIFY;
          //**4PS.en

        UNTIL NEXT = 0;
      END;

      OnAfterPostLines(ItemJnlLine,ItemRegNo);
    END;

    LOCAL PROCEDURE HandleRecurringLine@15(VAR ItemJnlLine@1000 : Record 83);
    VAR
      ItemJnlLine2@1002 : Record 83;
    BEGIN
      LineCount := 0;
      ItemJnlLine2.COPYFILTERS(ItemJnlLine);
      ItemJnlLine2.FINDSET;
      REPEAT
        LineCount := LineCount + 1;
        Window.UPDATE(5,LineCount);
        Window.UPDATE(6,ROUND(LineCount / NoOfRecords * 10000,1));
        IF ItemJnlLine2."Posting Date" <> 0D THEN
          ItemJnlLine2.VALIDATE("Posting Date",CALCDATE(ItemJnlLine2."Recurring Frequency",ItemJnlLine2."Posting Date"));
        IF (ItemJnlLine2."Recurring Method" = ItemJnlLine2."Recurring Method"::Variable) AND
           (ItemJnlLine2."Item No." <> '')
        THEN BEGIN
          ItemJnlLine2.Quantity := 0;
          ItemJnlLine2."Invoiced Quantity" := 0;
          ItemJnlLine2.Amount := 0;
          OnHandleRecurringLineOnBeforeModify(ItemJnlLine2);
        END;
        ItemJnlLine2.MODIFY;
      UNTIL ItemJnlLine2.NEXT = 0;
    END;

    LOCAL PROCEDURE HandleNonRecurringLine@17(VAR ItemJnlLine@1000 : Record 83;OldEntryType@1001 : 'Purchase,Sale,Positive Adjmt.,Negative Adjmt.,Transfer,Consumption,Output, ,Assembly Consumption,Assembly Output');
    VAR
      ItemJnlLine2@1003 : Record 83;
      ItemJnlLine3@1002 : Record 83;
      IncrBatchName@1004 : Boolean;
    BEGIN
      WITH ItemJnlLine DO BEGIN
        ItemJnlLine2.COPYFILTERS(ItemJnlLine);
        ItemJnlLine2.SETFILTER("Item No.",'<>%1','');
        IF ItemJnlLine2.FINDLAST THEN; // Remember the last line
        ItemJnlLine2."Entry Type" := OldEntryType;

        ItemJnlLine3.COPY(ItemJnlLine);

        //**4PS.sn
        IF ItemJnlLine3.FINDSET THEN
          REPEAT
            ItemJnlLine3.DeleteSurcharge;
          UNTIL ItemJnlLine3.NEXT = 0;
        //**4PS.en

        ItemJnlLine3.DELETEALL;
        ItemJnlLine3.RESET;
        ItemJnlLine3.SETRANGE("Journal Template Name","Journal Template Name");
        ItemJnlLine3.SETRANGE("Journal Batch Name","Journal Batch Name");
        IF ItemJnlTemplate."Increment Batch Name" THEN
          IF NOT ItemJnlLine3.FINDLAST THEN BEGIN
            IncrBatchName := INCSTR("Journal Batch Name") <> '';

            //**4PS.sn
            IF ItemJnlBatch."Do Not Increase Name" OR NotIncrJnlBatchNr THEN
              IncrBatchName := FALSE;
            //**4PS.en

            OnBeforeIncrBatchName(ItemJnlLine,IncrBatchName);
            IF IncrBatchName THEN BEGIN
              ItemJnlBatch.DELETE;
              ItemJnlBatch.Name := INCSTR("Journal Batch Name");
              IF ItemJnlBatch.INSERT THEN;
              "Journal Batch Name" := ItemJnlBatch.Name;
            END;
          END;

        OnHandleNonRecurringLineOnInsertNewLine(ItemJnlLine3);

        ItemJnlLine3.SETRANGE("Journal Batch Name","Journal Batch Name");
        IF (ItemJnlBatch."No. Series" = '') AND NOT ItemJnlLine3.FINDLAST AND
           NOT (ItemJnlLine2."Entry Type" IN [ItemJnlLine2."Entry Type"::Consumption,ItemJnlLine2."Entry Type"::Output])
        THEN BEGIN
          ItemJnlLine3.INIT;
          ItemJnlLine3."Journal Template Name" := "Journal Template Name";
          ItemJnlLine3."Journal Batch Name" := "Journal Batch Name";
          ItemJnlLine3."Line No." := 10000;
          ItemJnlLine3.INSERT;
          ItemJnlLine3.SetUpNewLine(ItemJnlLine2);
          ItemJnlLine3.MODIFY;
        END;
      END;
    END;

    LOCAL PROCEDURE ConstructPostingNumber@13(VAR ItemJnlLine@1000 : Record 83);
    BEGIN
      WITH ItemJnlLine DO
        IF "Posting No. Series" = '' THEN
          "Posting No. Series" := ItemJnlBatch."No. Series"
        ELSE
          IF NOT EmptyLine THEN
            IF "Document No." = LastDocNo THEN
              "Document No." := LastPostedDocNo
            ELSE BEGIN
              IF NOT NoSeries.GET("Posting No. Series") THEN BEGIN
                NoOfPostingNoSeries := NoOfPostingNoSeries + 1;
                IF NoOfPostingNoSeries > ARRAYLEN(NoSeriesMgt2) THEN
                  ERROR(
                    Text006,
                    ARRAYLEN(NoSeriesMgt2));
                NoSeries.Code := "Posting No. Series";
                NoSeries.Description := FORMAT(NoOfPostingNoSeries);
                NoSeries.INSERT;
              END;
              LastDocNo := "Document No.";
              EVALUATE(PostingNoSeriesNo,NoSeries.Description);
              "Document No." := NoSeriesMgt2[PostingNoSeriesNo].GetNextNo("Posting No. Series","Posting Date",FALSE);
              LastPostedDocNo := "Document No.";
            END;

      OnAfterConstructPostingNumber(ItemJnlLine);
    END;

    LOCAL PROCEDURE CheckRecurringLine@1(VAR ItemJnlLine2@1000 : Record 83);
    VAR
      NULDF@1001 : DateFormula;
    BEGIN
      WITH ItemJnlLine2 DO
        IF "Item No." <> '' THEN
          IF ItemJnlTemplate.Recurring THEN BEGIN
            TESTFIELD("Recurring Method");
            TESTFIELD("Recurring Frequency");
            IF "Recurring Method" = "Recurring Method"::Variable THEN
              TESTFIELD(Quantity);
          END ELSE BEGIN
            CLEAR(NULDF);
            TESTFIELD("Recurring Method",0);
            TESTFIELD("Recurring Frequency",NULDF);
          END;
    END;

    LOCAL PROCEDURE MakeRecurringTexts@2(VAR ItemJnlLine2@1000 : Record 83);
    BEGIN
      WITH ItemJnlLine2 DO
        IF ("Item No." <> '') AND ("Recurring Method" <> 0) THEN BEGIN // Not recurring
          Day := DATE2DMY("Posting Date",1);
          Week := DATE2DWY("Posting Date",2);
          Month := DATE2DMY("Posting Date",2);
          MonthText := FORMAT("Posting Date",0,Text007);
          AccountingPeriod.SETRANGE("Starting Date",0D,"Posting Date");
          IF NOT AccountingPeriod.FINDLAST THEN
            AccountingPeriod.Name := '';
          "Document No." :=
            DELCHR(
              PADSTR(
                STRSUBSTNO("Document No.",Day,Week,Month,MonthText,AccountingPeriod.Name),
                MAXSTRLEN("Document No.")),
              '>');
          Description :=
            DELCHR(
              PADSTR(
                STRSUBSTNO(Description,Day,Week,Month,MonthText,AccountingPeriod.Name),
                MAXSTRLEN(Description)),
              '>');
        END;
    END;

    LOCAL PROCEDURE ItemJnlPostSumLine@5800(ItemJnlLine4@1000 : Record 83);
    VAR
      Item@1002 : Record 27;
      ItemLedgEntry4@1003 : Record 32;
      ItemLedgEntry5@1005 : Record 32;
      Remainder@1006 : Decimal;
      RemAmountToDistribute@1007 : Decimal;
      RemQuantity@1008 : Decimal;
      DistributeCosts@1009 : Boolean;
      IncludeExpectedCost@1010 : Boolean;
      PostingDate@1012 : Date;
      IsLastEntry@1011 : Boolean;
    BEGIN
      DistributeCosts := TRUE;
      RemAmountToDistribute := ItemJnlLine.Amount;
      RemQuantity := ItemJnlLine.Quantity;
      IF ItemJnlLine.Amount <> 0 THEN BEGIN
        LineCount := LineCount + 1;
        Window.UPDATE(3,LineCount);
        Window.UPDATE(4,ROUND(LineCount / NoOfRecords * 10000,1));
        WITH ItemLedgEntry4 DO BEGIN
          Item.GET(ItemJnlLine4."Item No.");
          IncludeExpectedCost := (Item."Costing Method" = Item."Costing Method"::Standard) AND
            (ItemJnlLine4."Inventory Value Per" <> ItemJnlLine4."Inventory Value Per"::" ");
          RESET;
          SETCURRENTKEY("Item No.",Positive,"Location Code","Variant Code");
          SETRANGE("Item No.",ItemJnlLine."Item No.");
          SETRANGE(Positive,TRUE);
          PostingDate := ItemJnlLine."Posting Date";

          IF (ItemJnlLine4."Location Code" <> '') OR
             (ItemJnlLine4."Inventory Value Per" IN
              [ItemJnlLine."Inventory Value Per"::Location,
               ItemJnlLine4."Inventory Value Per"::"Location and Variant"])
          THEN
            SETRANGE("Location Code",ItemJnlLine."Location Code");
          IF (ItemJnlLine."Variant Code" <> '') OR
             (ItemJnlLine4."Inventory Value Per" IN
              [ItemJnlLine."Inventory Value Per"::Variant,
               ItemJnlLine4."Inventory Value Per"::"Location and Variant"])
          THEN
            SETRANGE("Variant Code",ItemJnlLine."Variant Code");
          IF FINDSET THEN
            REPEAT
              IF IncludeEntryInCalc(ItemLedgEntry4,PostingDate,IncludeExpectedCost) THEN BEGIN
                ItemLedgEntry5 := ItemLedgEntry4;

                ItemJnlLine4."Entry Type" := "Entry Type";
                ItemJnlLine4.Quantity :=
                  CalculateRemQuantity("Entry No.",ItemJnlLine."Posting Date");

                ItemJnlLine4."Quantity (Base)" := ItemJnlLine4.Quantity;
                ItemJnlLine4."Invoiced Quantity" := ItemJnlLine4.Quantity;
                ItemJnlLine4."Invoiced Qty. (Base)" := ItemJnlLine4.Quantity;
                ItemJnlLine4."Location Code" := "Location Code";
                ItemJnlLine4."Variant Code" := "Variant Code";
                ItemJnlLine4."Applies-to Entry" := "Entry No.";
                ItemJnlLine4."Source No." := "Source No.";
                ItemJnlLine4."Order Type" := "Order Type";
                ItemJnlLine4."Order No." := "Order No.";
                ItemJnlLine4."Order Line No." := "Order Line No.";

                IF ItemJnlLine4.Quantity <> 0 THEN BEGIN
                  ItemJnlLine4.Amount :=
                    ItemJnlLine."Inventory Value (Revalued)" * ItemJnlLine4.Quantity /
                    ItemJnlLine.Quantity -
                    ROUND(
                      CalculateRemInventoryValue(
                        "Entry No.",Quantity,ItemJnlLine4.Quantity,
                        IncludeExpectedCost AND NOT "Completely Invoiced",PostingDate),
                      GLSetup."Amount Rounding Precision") + Remainder;

                  RemQuantity := RemQuantity - ItemJnlLine4.Quantity;

                  IF RemQuantity = 0 THEN BEGIN
                    IF NEXT > 0 THEN
                      REPEAT
                        IF IncludeEntryInCalc(ItemLedgEntry4,PostingDate,IncludeExpectedCost) THEN BEGIN
                          RemQuantity := CalculateRemQuantity("Entry No.",ItemJnlLine."Posting Date");
                          IF RemQuantity > 0 THEN
                            ERROR(Text008 + Text009,ItemJnlLine4."Item No.");
                        END;
                      UNTIL NEXT = 0;

                    ItemJnlLine4.Amount := RemAmountToDistribute;
                    DistributeCosts := FALSE;
                  END ELSE BEGIN
                    REPEAT
                      IsLastEntry := NEXT = 0;
                    UNTIL IncludeEntryInCalc(ItemLedgEntry4,PostingDate,IncludeExpectedCost) OR IsLastEntry;
                    IF IsLastEntry OR (RemQuantity < 0) THEN
                      ERROR(Text008 + Text009,ItemJnlLine4."Item No.");
                    Remainder := ItemJnlLine4.Amount - ROUND(ItemJnlLine4.Amount,GLSetup."Amount Rounding Precision");
                    ItemJnlLine4.Amount := ROUND(ItemJnlLine4.Amount,GLSetup."Amount Rounding Precision");
                    RemAmountToDistribute := RemAmountToDistribute - ItemJnlLine4.Amount;
                  END;
                  ItemJnlLine4."Unit Cost" := ItemJnlLine4.Amount / ItemJnlLine4.Quantity;

                  IF ItemJnlLine4.Amount <> 0 THEN BEGIN
                    IF IncludeExpectedCost AND NOT ItemLedgEntry5."Completely Invoiced" THEN BEGIN
                      ItemJnlLine4."Applied Amount" := ROUND(
                          ItemJnlLine4.Amount * (ItemLedgEntry5.Quantity - ItemLedgEntry5."Invoiced Quantity") /
                          ItemLedgEntry5.Quantity,
                          GLSetup."Amount Rounding Precision");
                    END ELSE
                      ItemJnlLine4."Applied Amount" := 0;
                    OnBeforeItemJnlPostSumLine(ItemJnlLine4,ItemLedgEntry4);
                    ItemJnlPostLine.RunWithCheck(ItemJnlLine4);
                  END;
                END ELSE BEGIN
                  REPEAT
                    IsLastEntry := NEXT = 0;
                  UNTIL IncludeEntryInCalc(ItemLedgEntry4,PostingDate,IncludeExpectedCost) OR IsLastEntry;
                  IF IsLastEntry THEN
                    ERROR(Text008 + Text009,ItemJnlLine4."Item No.");
                END;
              END ELSE
                DistributeCosts := NEXT <> 0;
            UNTIL NOT DistributeCosts;
        END;

        IF ItemJnlLine."Update Standard Cost" THEN
          UpdateStdCost;
      END;

      //**4PS.sn C021411
      IF IsRevaluationOfAmountZero(ItemJnlLine) THEN
        IF ItemJnlLine."Update Standard Cost" THEN
          UpdateStdCost;
      //**4PS.en
      OnAfterItemJnlPostSumLine(ItemJnlLine);
    END;

    LOCAL PROCEDURE IncludeEntryInCalc@7(ItemLedgEntry@1000 : Record 32;PostingDate@1002 : Date;IncludeExpectedCost@1001 : Boolean) : Boolean;
    VAR
      lvSKU@1210190000 : Record 5700;
    BEGIN
      WITH ItemLedgEntry DO BEGIN
        //**4PS.sn
        IF lvSKU.GET("Location Code", "Item No.", "Variant Code") THEN BEGIN
          IF lvSKU.Consignment THEN
            EXIT(FALSE);
        END;
        //**4PS.en
        IF IncludeExpectedCost THEN
          EXIT("Posting Date" IN [0D..PostingDate]);
        EXIT("Completely Invoiced" AND ("Last Invoice Date" IN [0D..PostingDate]));
      END;
    END;

    LOCAL PROCEDURE UpdateStdCost@5();
    VAR
      SKU@1002 : Record 5700;
    BEGIN
      WITH ItemJnlLine DO BEGIN
        IF SKU.GET("Location Code","Item No.","Variant Code") THEN BEGIN
          //**4PS.sn
          IF ItemJnlLine."SKU Surcharge Revaluation" THEN BEGIN
            SKU.VALIDATE("Surcharge SKU", ROUND("SKU Surcharge (Revalued)"/ItemJnlLine."Invoiced Quantity",
                          GLSetup."Amount Rounding Precision"));
            SKU.MODIFY;

            TempSKUrec."Location Code" := SKU."Location Code";
            TempSKUrec."Item No." :=  SKU."Item No.";
            TempSKUrec."Variant Code" := SKU."Variant Code";
            IF TempSKUrec.INSERT THEN;

            IF NOT SKURevaluationRun THEN
              SKURevaluationRun := TRUE;
          END ELSE BEGIN
            IF SKU."Surcharge SKU" <> 0 THEN BEGIN
              SKU.VALIDATE("Standard Cost","Unit Cost (Revalued)" - SKU."Surcharge SKU");    //M25128
            END ELSE BEGIN
            //**4PS.en
              SKU.VALIDATE("Standard Cost","Unit Cost (Revalued)");
            END;
            SKU.MODIFY;
          END;  //**4PS.n
        END ELSE
          UpdateItemStdCost;
      END;

      StdCostUpdated := TRUE; //**4PS.n C021411
    END;

    LOCAL PROCEDURE UpdateItemStdCost@23();
    VAR
      Item@1000 : Record 27;
    BEGIN
      WITH ItemJnlLine DO BEGIN
        Item.GET("Item No.");
        Item.VALIDATE("Standard Cost","Unit Cost (Revalued)");
        SetItemSingleLevelCosts(Item,ItemJnlLine);
        SetItemRolledUpCosts(Item,ItemJnlLine);
        Item."Last Unit Cost Calc. Date" := "Posting Date";
        Item.MODIFY;
      END;
    END;

    LOCAL PROCEDURE CheckRemainingQty@4();
    VAR
      ItemLedgerEntry@1001 : Record 32;
      RemainingQty@1000 : Decimal;
    BEGIN
      RemainingQty := ItemLedgerEntry.CalculateRemQuantity(
          ItemJnlLine."Applies-to Entry",ItemJnlLine."Posting Date");

      IF RemainingQty <> ItemJnlLine.Quantity THEN
        ERROR(Text008 + Text009,ItemJnlLine."Item No.");
    END;

    LOCAL PROCEDURE PostWhseJnlLine@7302(ItemJnlLine@1000 : Record 83;OriginalQuantity@1001 : Decimal;OriginalQuantityBase@1006 : Decimal;VAR TempTrackingSpecification@1003 : TEMPORARY Record 336);
    VAR
      ItemJnlTemplateType@1007 : Option;
    BEGIN
      WITH ItemJnlLine DO BEGIN
        Quantity := OriginalQuantity;
        "Quantity (Base)" := OriginalQuantityBase;
        GetLocation("Location Code");
        ItemJnlTemplateType := ItemJnlTemplate.Type;
        OnPostWhseJnlLineOnBeforeCreateWhseJnlLines(ItemJnlLine,ItemJnlTemplateType);
        IF NOT ("Entry Type" IN ["Entry Type"::Consumption,"Entry Type"::Output]) THEN
          PostWhseJnlLines(ItemJnlLine,TempTrackingSpecification,ItemJnlTemplateType,FALSE);

        IF "Entry Type" = "Entry Type"::Transfer THEN BEGIN
          GetLocation("New Location Code");
          PostWhseJnlLines(ItemJnlLine,TempTrackingSpecification,ItemJnlTemplateType,TRUE);
        END;
      END;
    END;

    LOCAL PROCEDURE PostWhseJnlLines@40(ItemJnlLine@1000 : Record 83;VAR TempTrackingSpecification@1007 : TEMPORARY Record 336;ItemJnlTemplateType@1003 : Option;ToTransfer@1001 : Boolean);
    VAR
      WhseJnlLine@1005 : Record 7311;
      TempWhseJnlLine@1004 : TEMPORARY Record 7311;
      ItemTrackingMgt@1006 : Codeunit 6500;
      IsHandled@1002 : Boolean;
    BEGIN
      WITH ItemJnlLine DO
        IF Location."Bin Mandatory" THEN
          IF WMSMgmt.CreateWhseJnlLine(ItemJnlLine,ItemJnlTemplateType,WhseJnlLine,ToTransfer) THEN BEGIN
            ItemTrackingMgt.SplitWhseJnlLine(WhseJnlLine,TempWhseJnlLine,TempTrackingSpecification,ToTransfer);
            IF TempWhseJnlLine.FINDSET THEN
              REPEAT
                WMSMgmt.CheckWhseJnlLine(TempWhseJnlLine,1,0,ToTransfer);
                IsHandled := FALSE;
                OnBeforeWhseJnlPostLineRun(ItemJnlLine,TempWhseJnlLine,IsHandled);
                IF NOT IsHandled THEN
                  WhseJnlPostLine.RUN(TempWhseJnlLine);
              UNTIL TempWhseJnlLine.NEXT = 0;
            OnAfterPostWhseJnlLine(ItemJnlLine,SuppressCommit);
          END;
    END;

    LOCAL PROCEDURE CheckWMSBin@8(ItemJnlLine@1000 : Record 83);
    BEGIN
      WITH ItemJnlLine DO BEGIN
        GetLocation("Location Code");
        IF Location."Bin Mandatory" THEN
          WhseTransaction := TRUE;
        CASE "Entry Type" OF
          "Entry Type"::Purchase,"Entry Type"::Sale,
          "Entry Type"::"Positive Adjmt.","Entry Type"::"Negative Adjmt.":
            BEGIN
              IF Location."Directed Put-away and Pick" THEN
                WMSMgmt.CheckAdjmtBin(
                  Location,Quantity,
                  ("Entry Type" IN
                   ["Entry Type"::Purchase,
                    "Entry Type"::"Positive Adjmt."]));
            END;
          "Entry Type"::Transfer:
            BEGIN
              IF Location."Directed Put-away and Pick" THEN
                WMSMgmt.CheckAdjmtBin(Location,-Quantity,FALSE);
              GetLocation("New Location Code");
              IF Location."Directed Put-away and Pick" THEN
                WMSMgmt.CheckAdjmtBin(Location,Quantity,TRUE);
              IF Location."Bin Mandatory" THEN
                WhseTransaction := TRUE;
            END;
        END;
      END;
    END;

    LOCAL PROCEDURE GetLocation@7301(LocationCode@1000 : Code[10]);
    BEGIN
      IF LocationCode = '' THEN
        CLEAR(Location)
      ELSE
        IF Location.Code <> LocationCode THEN
          Location.GET(LocationCode);
    END;

    [External]
    PROCEDURE GetWhseRegNo@6() : Integer;
    BEGIN
      EXIT(WhseRegNo);
    END;

    [External]
    PROCEDURE GetItemRegNo@9() : Integer;
    BEGIN
      EXIT(ItemRegNo);
    END;

    LOCAL PROCEDURE IsPhysInvtCount@10(ItemJnlTemplate2@1000 : Record 82;PhysInvtCountingPeriodCode@1002 : Code[10];PhysInvtCountingPeriodType@1003 : ' ,Item,SKU') : Boolean;
    BEGIN
      EXIT(
        (ItemJnlTemplate2.Type = ItemJnlTemplate2.Type::"Phys. Inventory") AND
        (PhysInvtCountingPeriodType <> PhysInvtCountingPeriodType::" ") AND
        (PhysInvtCountingPeriodCode <> ''));
    END;

    LOCAL PROCEDURE CheckItemAvailability@14(VAR ItemJnlLine@1000 : Record 83);
    VAR
      Item@1003 : Record 27;
      TempSKU@1001 : TEMPORARY Record 5700;
      ItemJnlLine2@1005 : Record 83;
      QtyinItemJnlLine@1002 : Decimal;
      AvailableQty@1004 : Decimal;
      IsHandled@1006 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeCheckItemAvailabilityHandled(ItemJnlLine,IsHandled);
      IF IsHandled THEN
        EXIT;

      ItemJnlLine2.COPYFILTERS(ItemJnlLine);
      IF ItemJnlLine2.FINDSET THEN
        REPEAT
          IF NOT TempSKU.GET(ItemJnlLine2."Location Code",ItemJnlLine2."Item No.",ItemJnlLine2."Variant Code") THEN
            InsertTempSKU(TempSKU,ItemJnlLine2);
          OnBeforeCheckItemAvailability(ItemJnlLine2,TempSKU);
        UNTIL ItemJnlLine2.NEXT = 0;

      IF TempSKU.FINDSET THEN
        REPEAT
          QtyinItemJnlLine := CalcRequiredQty(TempSKU,ItemJnlLine2);
          IF QtyinItemJnlLine < 0 THEN BEGIN
            Item.GET(TempSKU."Item No.");
            Item.SETFILTER("Location Filter",TempSKU."Location Code");
            Item.SETFILTER("Variant Filter",TempSKU."Variant Code");
            Item.CALCFIELDS("Reserved Qty. on Inventory","Net Change");
            AvailableQty := Item."Net Change" - Item."Reserved Qty. on Inventory" + SelfReservedQty(TempSKU,ItemJnlLine2);

            IF (Item."Reserved Qty. on Inventory" > 0) AND (AvailableQty < ABS(QtyinItemJnlLine)) THEN
              IF NOT CONFIRM(
                   Text010,FALSE,TempSKU.FIELDCAPTION("Item No."),TempSKU."Item No.",TempSKU.FIELDCAPTION("Location Code"),
                   TempSKU."Location Code",TempSKU.FIELDCAPTION("Variant Code"),TempSKU."Variant Code")
              THEN
                ERROR('');
          END;
        UNTIL TempSKU.NEXT = 0 ;
    END;

    LOCAL PROCEDURE InsertTempSKU@18(VAR TempSKU@1000 : TEMPORARY Record 5700;ItemJnlLine@1001 : Record 83);
    BEGIN
      WITH TempSKU DO BEGIN
        INIT;
        "Location Code" := ItemJnlLine."Location Code";
        "Item No." := ItemJnlLine."Item No.";
        "Variant Code" := ItemJnlLine."Variant Code";
        INSERT;
      END;
    END;

    LOCAL PROCEDURE CalcRequiredQty@19(TempSKU@1000 : TEMPORARY Record 5700;VAR ItemJnlLine@1001 : Record 83) : Decimal;
    VAR
      SignFactor@1003 : Integer;
      QtyinItemJnlLine@1002 : Decimal;
    BEGIN
      QtyinItemJnlLine := 0;
      ItemJnlLine.SETRANGE("Item No.",TempSKU."Item No.");
      ItemJnlLine.SETRANGE("Location Code",TempSKU."Location Code");
      ItemJnlLine.SETRANGE("Variant Code",TempSKU."Variant Code");
      ItemJnlLine.FINDSET;
      REPEAT
        IF (ItemJnlLine."Entry Type" IN
            [ItemJnlLine."Entry Type"::Sale,
             ItemJnlLine."Entry Type"::"Negative Adjmt.",
             ItemJnlLine."Entry Type"::Consumption]) OR
           (ItemJnlLine."Entry Type" = ItemJnlLine."Entry Type"::Transfer)
        THEN
          SignFactor := -1
        ELSE
          SignFactor := 1;
        QtyinItemJnlLine += ItemJnlLine."Quantity (Base)" * SignFactor;
      UNTIL ItemJnlLine.NEXT = 0;
      EXIT(QtyinItemJnlLine);
    END;

    LOCAL PROCEDURE SelfReservedQty@20(SKU@1000 : Record 5700;ItemJnlLine@1003 : Record 83) : Decimal;
    VAR
      ReservationEntry@1002 : Record 337;
    BEGIN
      IF ItemJnlLine."Order Type" <> ItemJnlLine."Order Type"::Production THEN
        EXIT;

      WITH ReservationEntry DO BEGIN
        SETRANGE("Item No.",SKU."Item No.");
        SETRANGE("Location Code",SKU."Location Code");
        SETRANGE("Variant Code",SKU."Variant Code");
        SETRANGE("Source Type",DATABASE::"Prod. Order Component");
        SETRANGE("Source ID",ItemJnlLine."Order No.");
        IF ISEMPTY THEN
          EXIT;
        CALCSUMS("Quantity (Base)");
        EXIT(-"Quantity (Base)");
      END;
    END;

    LOCAL PROCEDURE SetItemSingleLevelCosts@24(VAR Item@1000 : Record 27;ItemJournalLine@1001 : Record 83);
    BEGIN
      WITH ItemJournalLine DO BEGIN
        Item."Single-Level Material Cost" := "Single-Level Material Cost";
        Item."Single-Level Capacity Cost" := "Single-Level Capacity Cost";
        Item."Single-Level Subcontrd. Cost" := "Single-Level Subcontrd. Cost";
        Item."Single-Level Cap. Ovhd Cost" := "Single-Level Cap. Ovhd Cost";
        Item."Single-Level Mfg. Ovhd Cost" := "Single-Level Mfg. Ovhd Cost";
      END;
    END;

    LOCAL PROCEDURE SetItemRolledUpCosts@25(VAR Item@1001 : Record 27;ItemJournalLine@1000 : Record 83);
    BEGIN
      WITH ItemJournalLine DO BEGIN
        Item."Rolled-up Material Cost" := "Rolled-up Material Cost";
        Item."Rolled-up Capacity Cost" := "Rolled-up Capacity Cost";
        Item."Rolled-up Subcontracted Cost" := "Rolled-up Subcontracted Cost";
        Item."Rolled-up Mfg. Ovhd Cost" := "Rolled-up Mfg. Ovhd Cost";
        Item."Rolled-up Cap. Overhead Cost" := "Rolled-up Cap. Overhead Cost";
      END;
    END;

    [External]
    PROCEDURE SetSuppressCommit@52(NewSuppressCommit@1000 : Boolean);
    BEGIN
      SuppressCommit := NewSuppressCommit;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCheckLines@31(VAR ItemJnlLine@1000 : Record 83);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCheckJnlLine@21(VAR ItemJournalLine@1000 : Record 83;CommitIsSuppressed@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterConstructPostingNumber@37(VAR ItemJournalLine@1000 : Record 83);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterItemJnlPostSumLine@26(VAR ItemJournalLine@1000 : Record 83);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPostLines@35(VAR ItemJournalLine@1000 : Record 83;VAR ItemRegNo@1001 : Integer);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPostJnlLines@32(VAR ItemJournalBatch@1000 : Record 233;VAR ItemJournalLine@1001 : Record 83;ItemRegNo@1002 : Integer;WhseRegNo@1003 : Integer);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPostWhseJnlLine@16(ItemJournalLine@1000 : Record 83;CommitIsSuppressed@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCode@34(VAR ItemJournalLine@1000 : Record 83);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCheckItemAvailability@33(VAR ItemJournalLine@1000 : Record 83;VAR StockkeepingUnit@1001 : Record 5700);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCheckItemAvailabilityHandled@36(VAR ItemJournalLine@1000 : Record 83;VAR IsHandled@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCheckLines@29(VAR ItemJnlLine@1000 : Record 83);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeRaiseExceedLengthError@27(VAR ItemJournalBatch@1000 : Record 233;VAR RaiseError@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeWhseJnlPostLineRun@43(ItemJournalLine@1000 : Record 83;VAR TempWarehouseJournalLine@1001 : TEMPORARY Record 7311;VAR IsHandled@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnPostLinesBeforePostWhseJnlLine@22(VAR ItemJournalLine@1000 : Record 83;CommitIsSuppressed@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeIncrBatchName@28(VAR ItemJournalLine@1000 : Record 83;VAR IncrBatchName@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeItemJnlPostSumLine@127(VAR ItemJournalLine@1000 : Record 83;ItemLedgerEntry@1001 : Record 32);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeUpdateDeleteLines@30(VAR ItemJournalLine@1000 : Record 83;ItemRegNo@1001 : Integer);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnHandleRecurringLineOnBeforeModify@39(VAR ItemJournalLine@1000 : Record 83);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnHandleNonRecurringLineOnInsertNewLine@41(VAR ItemJournalLine@1000 : Record 83);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnPostWhseJnlLineOnBeforeCreateWhseJnlLines@42(ItemJournalLine@1000 : Record 83;VAR ItemJnlTemplateType@1001 : Option);
    BEGIN
    END;

    PROCEDURE PostJobEntry@11012000();
    BEGIN
      //**4PS
      WITH ItemJnlLine DO BEGIN
        JobJnlLine.INIT;
        JobJnlLine."Posting Date" := "Posting Date";
        JobJnlLine."Document Date" := "Document Date";
        JobJnlLine."Source Code" := "Source Code";
        JobJnlLine."Reason Code" := "Reason Code";
        JobJnlLine."Job No." := "Job No.";
        JobJnlLine.Element := Element;
        JobJnlLine."Extension Contract" := "Extension Contract";
        JobJnlLine."Rental Unit" := "Rental Unit";
        JobJnlLine."Service Order No." := "Service Order No.";
        JobJnlLine."Service Contract No." := "Service Contract No.";
        JobJnlLine."Service Location No." := "Service Location No.";
        JobJnlLine."Location Code" := "Location Code";
        JobJnlLine.Description := Description;
        JobJnlLine."Description 2" := "Description 2";
        JobJnlLine."Posting Group" := "Inventory Posting Group";
        ProjRec.CHANGECOMPANY("Receiving Company");
        ProjRec.GET("Job No.");
        JobJnlLine."Shortcut Dimension 1 Code" := ProjRec."Global Dimension 1 Code";
        JobJnlLine."Shortcut Dimension 2 Code" := "Shortcut Dimension 2 Code";
        JobJnlLine."Dimension Set ID" := "Dimension Set ID";
        DimMgt.ValidateShortcutDimValues(1,JobJnlLine."Shortcut Dimension 1 Code",JobJnlLine."Dimension Set ID");
        //DimMgt.ValidateShortcutDimValues(2,JobJnlLine."Shortcut Dimension 2 Code",JobJnlLine."Dimension Set ID");  //DP00387 not necessary, cost object not changed
        JobJnlLine."Gen. Bus. Posting Group" := "Gen. Bus. Posting Group";
        JobJnlLine."Gen. Prod. Posting Group" := "Gen. Prod. Posting Group";
        JobJnlLine."Entry Type" := JobJnlLine."Entry Type"::Usage;
        JobJnlLine."Document No." := "Document No.";
        JobJnlLine.Type := JobJnlLine.Type::"G/L Account";
        JobJnlLine."No." := GetAccount("Receiving Company", TRUE);
        JobJnlLine."Item No." := "Item No.";
        JobJnlLine."External Document No." := "External Document No.";
        IF "Entry Type" = "Entry Type"::Purchase THEN BEGIN
          JobJnlLine.Quantity := -Quantity;
          JobJnlLine."Quantity (Base)" := - "Quantity (Base)";
        END ELSE BEGIN
          JobJnlLine.Quantity := Quantity;
          JobJnlLine."Quantity (Base)" := "Quantity (Base)";
        END;
        //C-026008.sn
        JobJnlLine."Unit of Measure Code" := "Unit of Measure Code";
        JobJnlLine."Qty. per Unit of Measure" := "Qty. per Unit of Measure";
        //C-026008.en
        //JobJnlLine."Post Job Entry Only" := TRUE; //Removed in NAV2009
        IF (NOT (InventSetupRec."Item Sales Price for Proj/Serv" AND ("Entry Type" = "Entry Type"::Sale))) THEN BEGIN
          JobJnlLine."Direct Unit Cost (LCY)" := "Unit Cost";
          JobJnlLine."Total Cost (LCY)" := ROUND(JobJnlLine.Quantity * "Unit Cost");
        END ELSE BEGIN
          JobJnlLine."Direct Unit Cost (LCY)" := "Unit Amount";
          JobJnlLine."Total Cost (LCY)" := Amount;
        END;
        JobJnlLine."Entry Type" := JobJnlLine."Entry Type"::Usage;
        JobJnlLine."Cost Component" := "Cost Component";
        JobJnlLine."Yard No." := "Yard No.";
        JobJnlLine."System No." := "System No.";
        JobJnlLine."Entity Type" := "Entity Type";
        JobJnlLine."Entity No." := "Entity No.";
        JobJnlLine."Cable Transit Pos." := "Cable Transit Pos.";
        JobJnlLine."FSC Type Code" := "FSC Type Code"; //C007769.n

        JobJnlPostLine.RunWithCheck(JobJnlLine);
        JobLedgEntryNo := JobJnlPostLine.GetJobLedgEntryNo; //DP00121
      END;
    END;

    PROCEDURE PostServiceEntry@1210190004();
    BEGIN
      //**4PS
      WITH ItemJnlLine DO BEGIN
        ServJnlLine.INIT;
        ServOrderRec.CHANGECOMPANY("Receiving Company");
        ServOrderRec.GET("Service Order No.");
        ServJnlLine."Posting Date" := "Posting Date";
        ServJnlLine."Source Code" := "Source Code";
        ServJnlLine."Reason Code" := "Reason Code";
        ServJnlLine."Service Order No." := "Service Order No.";
        ServJnlLine."Service Category" := ServOrderRec.GetServiceCategory;
        ServJnlLine."Service Contract No." := "Service Contract No.";
        ServJnlLine."Service Location No." := "Service Location No.";
        ServJnlLine."Project No." := "Job No.";
        ServJnlLine.Description := Description;
        ServJnlLine."Description 2" := "Description 2";
        ServJnlLine."Shortcut Dimension 1 Code" := ServOrderRec."Global Dimension 1 Code";
        ServJnlLine."Shortcut Dimension 2 Code" := "Shortcut Dimension 2 Code";
        ServJnlLine."Dimension Set ID" := "Dimension Set ID";
        DimMgt.ValidateShortcutDimValues(1,ServJnlLine."Shortcut Dimension 1 Code",ServJnlLine."Dimension Set ID");
        //DimMgt.ValidateShortcutDimValues(2,ServJnlLine."Shortcut Dimension 2 Code",ServJnlLine."Dimension Set ID");  //DP00387 n  not necessary, cost object not changed

        ServJnlLine."Document No." := "Document No.";
        ServJnlLine."G/L Account" := GetAccount("Receiving Company", TRUE);
        ServJnlLine."Item No." := "Item No.";
        IF "Entry Type" = "Entry Type"::Purchase THEN
          ServJnlLine.Quantity := -Quantity
        ELSE
          ServJnlLine.Quantity := Quantity;
        //C006483.sn
        ServJnlLine."Unit of Measure Code" := "Unit of Measure Code";
        ServJnlLine."Qty. per Unit of Measure" := "Qty. per Unit of Measure";
        //C006483.en
        ServJnlLine."Entry Type" := ServJnlLine."Entry Type"::Usage; //C022170.n
        IF (NOT (InventSetupRec."Item Sales Price for Proj/Serv" AND ("Entry Type" = "Entry Type"::Sale))) THEN BEGIN
          ServJnlLine."Unit Cost (LCY)" := "Unit Cost";
          ServJnlLine."Total Cost (LCY)" := ROUND(ServJnlLine.Quantity * "Unit Cost");
          ServJnlLine."Unit Cost" := "Unit Cost";
          ServJnlLine."Total Cost" := ROUND(ServJnlLine.Quantity * "Unit Cost");
        END ELSE BEGIN
          ServJnlLine."Unit Cost (LCY)" := "Unit Amount";
          ServJnlLine."Total Cost (LCY)" := Amount;
          ServJnlLine."Unit Cost" := "Unit Amount";
          ServJnlLine."Total Cost" := Amount;
        END;
        ServJnlLine."Additional Cost" := "Additional Cost (Service)";
        ServJnlLine."Cost Component" := "Cost Component";
        ServJnlLine."Location Code" := "Location Code";

        ServJnlPostLine.RunWithCheck(ServJnlLine);
        ServLedgEntryNo := ServJnlPostLine.GetServLedgEntryNo; //DP00121
      END;
    END;

    PROCEDURE PostPlantEntry@1100485001();
    VAR
      lvPlantTypeRec@1100485000 : Record 11012551;
      lvPlantNoRec@1100525000 : Record 11012552;
    BEGIN
      //**4PS
      WITH ItemJnlLine DO BEGIN
        lvPlantTypeRec.GET("Plant Type");
        IF "Plant No." <> '' THEN BEGIN
          IF NOT lvPlantNoRec.GET("Plant Type", "Plant No.") THEN
            lvPlantNoRec.INIT;
        END;
        PlantLedgerEntryRec.INIT;
        PlantLedgerEntryRec."Posting Date" := "Posting Date";
        PlantLedgerEntryRec."Document No." := "Document No.";
        PlantLedgerEntryRec."Document Date" := "Document Date";
        PlantLedgerEntryRec."Account No." := GetAccount("Receiving Company", TRUE);
        PlantLedgerEntryRec.Description := Description;
        PlantLedgerEntryRec."Description 2" := "Description 2";
        IF "Entry Type" = "Entry Type"::Purchase THEN
          PlantLedgerEntryRec.Quantity := -Quantity
        ELSE
          PlantLedgerEntryRec.Quantity := Quantity;
        PlantLedgerEntryRec."Direct Unit Cost" := "Unit Amount";     //? Amount instead of Cost
        PlantLedgerEntryRec."Unit Cost" := PlantLedgerEntryRec."Direct Unit Cost";
        PlantLedgerEntryRec."Total Cost" := ROUND(PlantLedgerEntryRec.Quantity * PlantLedgerEntryRec."Unit Cost");
        PlantLedgerEntryRec."Unit of Measure Code" := "Unit of Measure Code";
        IF ("Plant No." <> '') AND (lvPlantNoRec."Department Code" <> '') THEN
          PlantLedgerEntryRec."Department Code" := lvPlantNoRec."Department Code"
        ELSE
          PlantLedgerEntryRec."Department Code" := lvPlantTypeRec."Department Code";
        PlantLedgerEntryRec."Cost Object" := "Shortcut Dimension 2 Code";
        PlantLedgerEntryRec."Dimension Set ID" := "Dimension Set ID";
        DimMgt.ValidateShortcutDimValues(1,PlantLedgerEntryRec."Department Code",PlantLedgerEntryRec."Dimension Set ID");
        //DimMgt.ValidateShortcutDimValues(2,PlantLedgerEntryRec."Cost Object",PlantLedgerEntryRec."Dimension Set ID"); //DP00387 n not necessary, cost object not changed

        PlantLedgerEntryRec."Source Code" := "Source Code";
        PlantLedgerEntryRec."Reason Code" := "Reason Code";
        PlantLedgerEntryRec."Gen. Bus. Posting Group" := "Gen. Bus. Posting Group";
        PlantLedgerEntryRec."Plant Posting Group" := lvPlantTypeRec.PlantPostingGrp("Plant No.","Receiving Company",'');
        PlantLedgerEntryRec."Plant Type" := "Plant Type";
        PlantLedgerEntryRec."Plant No." := "Plant No.";
        PlantLedgerEntryRec."Cost Component" := "Cost Component Plant";

        PlantJnlPostLineCU.RUN(PlantLedgerEntryRec);
      END;
    END;

    PROCEDURE PostSurcharge@1210190001(Origin@1210190000 : 'Project,Service');
    VAR
      PostingMgt@1100525000 : Codeunit 11012360;
    BEGIN
      //**4PS
      PostingMgt.SetPostingFromItemJnlLine(ItemJnlLine,GenJnlLine);
      PostingMgt.BufferSurcharges(
        Origin,GenJnlLine,JobJnlLine,ServJnlLine,JobJnlPostLine,ServJnlPostLine,SurchargePostingBuffer,ComplWIPPostingBuffer);
    END;

    PROCEDURE PostSurchargeIC@1100525000(lvOrigin@1210190000 : 'Project,Service,Indirect,InterCompany');
    VAR
      ICRec@1100525000 : Record 11012057;
      lvPostingSetup@1100525002 : Record 11020565;
      lvType@1210190008 : Code[20];
      lvOrigDim1@1210190007 : Code[20];
      lvOrigDim2@1210190006 : Code[20];
      lvDim1@1210190005 : Code[20];
      lvDim2@1210190004 : Code[20];
      lvAccount@1210190003 : Code[20];
      lvDesc@1210190002 : Text[100];
      lvCostTotal@1210190001 : Decimal;
      lvcostComp@1100485001 : Code[20];
      lvTotSurchAmount@1100485002 : Decimal;
      lvSourceType@1100525001 : 'Fixed,EmplOrFixed';
      IsReturnLine@1100528300 : Boolean;
    BEGIN
      //Surcharges for supplying company should also be calculated at IC postings and an ic posting should be created.
      //Surcharge is setup in supplying company.

      ICRec.GET(COMPANYNAME, ItemJnlLine."Receiving Company");

      IF lvOrigin = lvOrigin::Project THEN BEGIN
        IF ItemJnlLine."Service Order No." <> '' THEN EXIT;
        ProjRec.CHANGECOMPANY(ItemJnlLine."Receiving Company");
        ProjRec.GET(ItemJnlLine."Job No.");
        lvType := ProjRec."Project Type";
        ProjectType.CHANGECOMPANY(ItemJnlLine."Receiving Company");
        ProjectType.GET(lvType);
        lvOrigDim1 := ProjRec."Global Dimension 1 Code";
      END;

      IF lvOrigin = lvOrigin::Service THEN BEGIN
        ServOrderRec.CHANGECOMPANY(ItemJnlLine."Receiving Company");
        ServOrderRec.GET(ItemJnlLine."Service Order No.");
        IF ItemJnlLine."Additional Cost (Service)" = TRUE THEN
          lvType := ServOrderRec."Service Type (Other)"
        ELSE
          lvType := ServOrderRec."Service Type";
        ServiceType.CHANGECOMPANY(ItemJnlLine."Receiving Company");
        ServiceType.GET(lvType);
        lvOrigDim1 := ServOrderRec."Global Dimension 1 Code";
      END;

      lvOrigDim2 := ItemJnlLine."Shortcut Dimension 2 Code";
      DimMgt.GetDimValueRec(2, lvOrigDim2, DimValRec, TRUE, '');

      IF (ItemJnlLine.Quantity < 0) THEN
        IsReturnLine := TRUE;

      IF SurchargeRec.GetSurchargesIC1(
        lvOrigin::InterCompany,lvType, ItemJnlLine."Job No.",
        TRUE, DimValRec."Cost Type", DimValRec.Code, '',
        lvOrigDim1, '', ItemJnlLine."Cost Component", ItemJnlLine."Posting Date",
        SurchargeRec, COMPANYNAME, lvSourceType::Fixed, IsReturnLine) THEN
      BEGIN
        REPEAT
          SurchargeRec.GetSurchargeDimVal(DimValRec, SurchDimValRec);
          SurchargeRec.TESTFIELD("Coverage Account");

          IF lvOrigin = lvOrigin::Project THEN BEGIN

            JobJnlLine2."Job No." := ItemJnlLine."Job No.";
            JobJnlLine2."Total Cost (LCY)" := ItemJnlLine.Amount;
            JobJnlLine2."Quantity (Base)" := ItemJnlLine.Quantity;
            JobJnlLine2.Quantity := ItemJnlLine.Quantity;
            JobJnlLine2."Source Code" := ItemJnlLine."Source Code";


            JobJnlLine2.InitSurchargeIC(JobJnlLine2, DimValRec, SurchDimValRec, SurchargeRec, lvOrigDim1, lvType,
                                        lvTotSurchAmount, ItemJnlLine."Receiving Company");

            lvDim1 := JobJnlLine2."Shortcut Dimension 1 Code";
            lvDim2 := JobJnlLine2."Shortcut Dimension 2 Code";
            lvcostComp := JobJnlLine2."Cost Component";
            lvDesc := JobJnlLine2.Description;
            lvCostTotal := JobJnlLine2."Total Cost (LCY)";
            lvAccount:= JobJnlLine2."No.";
          END;

          IF lvOrigin = lvOrigin::Service THEN BEGIN

            ServJnlLine2."Service Order No." := ItemJnlLine."Service Order No.";
            ServJnlLine2."Service Category" := ServOrderRec.GetServiceCategory; //Is this line required here?
            ServJnlLine2."Total Cost (LCY)" := ItemJnlLine.Amount;
            ServJnlLine2.Quantity := ItemJnlLine.Quantity;

            ServJnlLine2.InitSurchargeIC(ServJnlLine2, DimValRec, SurchDimValRec, SurchargeRec, lvOrigDim1,
                                         lvType, lvTotSurchAmount, ItemJnlLine."Receiving Company");

            lvDim1 := ServJnlLine2."Shortcut Dimension 1 Code";
            lvDim2 := ServJnlLine2."Shortcut Dimension 2 Code";
            lvcostComp := ServJnlLine2."Cost Component";
            lvDesc := ServJnlLine2.Description;
            lvCostTotal := ServJnlLine2."Total Cost (LCY)";
            lvAccount:= ServJnlLine2."G/L Account";
          END;

          CLEAR(SurchargePostingBuffer[1]);

          //Post Debit Line
          SurchargePostingBuffer[1]."Receiving Company" := ItemJnlLine."Receiving Company";  //* Field not in index
          //*21489.sn
          //* These fields are not used for surcharge. Now used for the receiving company (keyfields).
          SurchargePostingBuffer[1]."Gen. Prod. Posting Group" := COPYSTR(ItemJnlLine."Receiving Company",1,10);
          SurchargePostingBuffer[1]."VAT Prod. Posting Group" := COPYSTR(ItemJnlLine."Receiving Company",11,10);
          SurchargePostingBuffer[1]."VAT Bus. Posting Group" := COPYSTR(ItemJnlLine."Receiving Company",21,10);
          //*21489.en
          SurchargePostingBuffer[1]."G/L Account" := lvAccount;
          SurchargePostingBuffer[1]."Global Dimension 1 Code" := lvDim1;
          SurchargePostingBuffer[1]."Global Dimension 2 Code" := lvDim2;
          SurchargePostingBuffer[1]."Cost Component" := lvcostComp;
          SurchargePostingBuffer[1]."Job No." := ItemJnlLine."Job No.";
          IF (ItemJnlLine."Job No." <> '') AND (SurchargeRec."Element Surcharge" <> '') THEN
            SurchargePostingBuffer[1].Element := SurchargeRec."Element Surcharge"
          ELSE
            SurchargePostingBuffer[1].Element := ItemJnlLine.Element;
          SurchargePostingBuffer[1]."Bal. Account No." := SurchargePostingBuffer[1].Element; // because element not in key
          SurchargePostingBuffer[1]."Service Order No." := ItemJnlLine."Service Order No.";
          SurchargePostingBuffer[1]."Service Contract No." := ItemJnlLine."Service Contract No.";
          SurchargePostingBuffer[1].Description := lvDesc;
          SurchargePostingBuffer[1].Amount := lvCostTotal;
          IF lvOrigin = lvOrigin::Project THEN
            SurchargePostingBuffer[1]."Origin Type" := SurchargePostingBuffer[1]."Origin Type"::Project;
          IF lvOrigin = lvOrigin::Service THEN
            SurchargePostingBuffer[1]."Origin Type" := SurchargePostingBuffer[1]."Origin Type"::Service;
          SurchargePostingBuffer[1]."Tax Area Code" := ItemJnlLine."Document No.";  //ps
          //M24815 "Gen. Bus. Posting Group" Now used for Posting date
          SurchargePostingBuffer[1]."Gen. Bus. Posting Group" := STRSUBSTNO('%1', ItemJnlLine."Posting Date");
          UpdateSurchargeBuffer;

          IF (lvOrigin = lvOrigin::Project) AND ProjectType."Post Complementary Surcharge" THEN BEGIN
            IF lvPostingSetup.GET(ItemJnlLine."Source Code", COMPANYNAME, ItemJnlLine."Receiving Company") THEN
            BEGIN
              lvPostingSetup.TESTFIELD("Prod. Account Debit");
              lvPostingSetup.TESTFIELD("Prod. Account Credit");
              PostComplWIPSurchargeIC(lvPostingSetup."Prod. Account Debit",
                                      lvPostingSetup."Prod. Account Credit");
            END;
          END;
          IF (lvOrigin = lvOrigin::Service) AND ServiceType."Post Complementary Surcharge" THEN BEGIN
            IF lvPostingSetup.GET(ItemJnlLine."Source Code", COMPANYNAME, ItemJnlLine."Receiving Company") THEN
            BEGIN
              lvPostingSetup.TESTFIELD("Prod. Account Debit");
              lvPostingSetup.TESTFIELD("Prod. Account Credit");
              PostComplWIPSurchargeIC(lvPostingSetup."Prod. Account Debit",
                                      lvPostingSetup."Prod. Account Credit");
            END;
          END;

          //Post Credit Line
          SurchargePostingBuffer[1]."Receiving Company" := '' ;
          SurchargePostingBuffer[1]."G/L Account" := SurchargeRec."Coverage Account";
          SurchargePostingBuffer[1]."Global Dimension 1 Code" := '';
          CASE SurchargeRec."Source Type Department" OF
            SurchargeRec."Source Type Department"::Employee:;
              //Impossible
            SurchargeRec."Source Type Department"::Job:
              SurchargePostingBuffer[1]."Global Dimension 1 Code" := lvDim1;
            SurchargeRec."Source Type Department"::Fixed:
              SurchargePostingBuffer[1]."Global Dimension 1 Code" := SurchargeRec."Coverage Department"
          END;
          SurchargePostingBuffer[1]."Global Dimension 2 Code" := lvDim2;

          IF SurchargeRec."Compress Coverage Posting" THEN BEGIN
            SurchargePostingBuffer[1]."Cost Component" := '';
            SurchargePostingBuffer[1]."Job No." := '';
            SurchargePostingBuffer[1].Element := '';
            //SurchargePostingBuffer[1]."Bal. Account No." := SurchargePostingBuffer[1].Element; // because element not in key
            SurchargePostingBuffer[1]."Service Order No." := '';
            SurchargePostingBuffer[1]."Service Contract No." := '';
          END ELSE BEGIN
            SurchargePostingBuffer[1]."Buy-Back Item (Plant Order)" := TRUE;   //C006064 FIELD USED FOR OTHER PURPOSE
          END;

          SurchargePostingBuffer[1].Amount := lvCostTotal * -1;
          SurchargePostingBuffer[1]."Origin Type" := SurchargePostingBuffer[1]."Origin Type"::" ";
          UpdateSurchargeBuffer;

          SurchargePostingBuffer[1]."Buy-Back Item (Plant Order)" := FALSE;  //C006064 FIELD USED FOR OTHER PURPOSE

          SurchargePostingBuffer[1]."Job No." := ItemJnlLine."Job No.";
          SurchargePostingBuffer[1]."Service Order No." := ItemJnlLine."Service Order No.";
          SurchargePostingBuffer[1]."Service Contract No." := ItemJnlLine."Service Contract No.";
          SurchargePostingBuffer[1]."Cost Component" := lvcostComp;
          SurchargePostingBuffer[1]."Global Dimension 1 Code" := lvDim1;
          SurchargePostingBuffer[1]."Global Dimension 2 Code" := lvDim2;

        UNTIL SurchargeRec.NEXT = 0;
      END;
    END;

    PROCEDURE UpdateSurchargeBuffer@11012024();
    BEGIN
      //**4PS
      SurchargePostingBuffer[2] := SurchargePostingBuffer[1];
      IF SurchargePostingBuffer[2].FIND THEN BEGIN
        SurchargePostingBuffer[2].Amount :=
          SurchargePostingBuffer[2].Amount + SurchargePostingBuffer[1].Amount;
        SurchargePostingBuffer[2].MODIFY;
      END ELSE
        SurchargePostingBuffer[1].INSERT;
    END;

    PROCEDURE PostSurchargeBuffer@11012003();
    VAR
      ICRec@1100525000 : Record 11012057;
    BEGIN
      //**4PS
      WITH ItemJnlLine DO BEGIN
        IF SurchargePostingBuffer[1].FIND('-') THEN BEGIN
          REPEAT
            GenJnlLine.INIT;
            GenJnlLine."Journal Template Name" := "Journal Template Name";
            GenJnlLine."Journal Batch Name" := "Journal Batch Name";
            GenJnlLine."Source Code" := "Source Code";
            GenJnlLine."Reason Code":= "Reason Code";
            GenJnlLine."Account Type" := GenJnlLine."Account Type"::"G/L Account";
            GenJnlLine."Account No." := SurchargePostingBuffer[1]."G/L Account";
            //M24815 "Gen. Bus. Posting Group" Now used for Posting date
            EVALUATE(GenJnlLine."Posting Date", SurchargePostingBuffer[1]."Gen. Bus. Posting Group");
            //GenJnlLine."Posting Date" := "Posting Date"; //M24815 old
            GenJnlLine."Document No." := SurchargePostingBuffer[1]."Tax Area Code";
            GenJnlLine."System-Created Entry" := TRUE;
            GenJnlLine."Document Date" := "Document Date";
            GenJnlLine.Description := SurchargePostingBuffer[1].Description;
            GenJnlLine."Description 2" := SurchargePostingBuffer[1]."Description 2";
            //* Field SurchargePostingBuffer[1]."Bal. Account No." was never filled. Field now used for Element because
            //* element not in key (can't be added). So do not fill "Bal. Account No." here.
            //GenJnlLine."Bal. Account No." := SurchargePostingBuffer[1]."Bal. Account No.";
            GenJnlLine.Amount := SurchargePostingBuffer[1].Amount;
            GenJnlLine.VALIDATE(Amount);
            GenJnlLine."Shortcut Dimension 1 Code" := SurchargePostingBuffer[1]."Global Dimension 1 Code";
            GenJnlLine."Shortcut Dimension 2 Code" := SurchargePostingBuffer[1]."Global Dimension 2 Code";
            GenJnlLine."Dimension Set ID" := SurchargePostingBuffer[1]."Dimension Set ID";
            DimMgt.ValidateShortcutDimValues(1,GenJnlLine."Shortcut Dimension 1 Code",GenJnlLine."Dimension Set ID"); //DP00387 n
            DimMgt.ValidateShortcutDimValues(2,GenJnlLine."Shortcut Dimension 2 Code",GenJnlLine."Dimension Set ID"); //DP00387 n

            GenJnlLine."Cost Component" := SurchargePostingBuffer[1]."Cost Component";
            GenJnlLine."Job No." := SurchargePostingBuffer[1]."Job No.";
            GenJnlLine.Element := SurchargePostingBuffer[1].Element;
            GenJnlLine."Service Order No." :=  SurchargePostingBuffer[1]."Service Order No.";
            IF GenJnlLine."Service Order No." <> '' THEN BEGIN
              //*21489.sn
              IF SurchargePostingBuffer[1]."Receiving Company" <> '' THEN
                ServOrderRec.CHANGECOMPANY(SurchargePostingBuffer[1]."Receiving Company")
              ELSE
                ServOrderRec.CHANGECOMPANY(COMPANYNAME);
              //*21489.en
              IF NOT ServOrderRec.GET(GenJnlLine."Service Order No.") THEN ServOrderRec.INIT;
              GenJnlLine."Service Contract No." := ServOrderRec."Service Contract No.";
              GenJnlLine."Service Category" := ServOrderRec.GetServiceCategory;
            END;
            GenJnlLine."Origin Type" := SurchargePostingBuffer[1]."Origin Type";

            IF SurchargePostingBuffer[1]."Receiving Company" <> '' THEN BEGIN

              ICRec.GET(COMPANYNAME, ItemJnlLine."Receiving Company");
              CreateICEntrySurcharge(ICRec);
              GenJnlLine."Job No." := '';
              GenJnlLine.Element := '';
              GenJnlLine."Extension Contract" := '';
              GenJnlLine."Service Order No." := '';
              GenJnlLine."Service Contract No." := '';
              GenJnlLine."Service Location No." := '';
              GenJnlLine."Service Category" := '';
              GenJnlLine."Plant Type" := '';
              GenJnlLine."Plant No." := '';
              GenJnlLine."Cost Component Plant" := '';
              GenJnlLine."Origin Type" := GenJnlLine."Origin Type"::" ";
              GenJnlLine."Shortcut Dimension 1 Code" := "Shortcut Dimension 1 Code";
              //DP00387 sn
              GenJnlLine."Dimension Set ID" := 0;
              GenJnlLine.CreateDim(
                DimMgt.TypeToTableID1(GenJnlLine."Account Type"),GenJnlLine."Account No.",
                DimMgt.TypeToTableID1(GenJnlLine."Bal. Account Type"),GenJnlLine."Bal. Account No.",
                DATABASE::"Plant Number", DimMgt.MakeCombinedPlantNo("Plant Type","Plant No."),
                DATABASE::"Plant Type", GenJnlLine."Plant Type",
                DATABASE::Job,GenJnlLine."Job No.",
                DATABASE::"Service Order",GenJnlLine."Service Order No.",
                DATABASE::"Salesperson/Purchaser",GenJnlLine."Salespers./Purch. Code",
                DATABASE::Campaign,GenJnlLine."Campaign No.");
              DimMgt.ValidateShortcutDimValues(1,GenJnlLine."Shortcut Dimension 1 Code",GenJnlLine."Dimension Set ID");
              DimMgt.ValidateShortcutDimValues(2,GenJnlLine."Shortcut Dimension 2 Code",GenJnlLine."Dimension Set ID");
              //DP00387 en
              GenJnlLine."Account No." := ICRec.GetICAccountOfCurrentCompany;

              ICRec.AddICRelationDimsOfCurrentCompany(
                GenJnlLine."Dimension Set ID",GenJnlLine."Shortcut Dimension 1 Code",GenJnlLine."Shortcut Dimension 2 Code");
            END;

            //C006064 FIELD "Buy-Back Item (Plant Order)" USED FOR OTHER PURPOSE
            GenJnlLine."Skip WIP Check" := SurchargePostingBuffer[1]."Buy-Back Item (Plant Order)";
            GenJnlPostLine.SetOverDimErr(); //DP00387 n
            RunGenJnlPostLine(GenJnlLine);
            GenJnlPostLine.ReSetOverDimErr();  //DP00387 n
          UNTIL SurchargePostingBuffer[1].NEXT = 0;
        END;
        SurchargePostingBuffer[1].DELETEALL;
      END;
    END;

    PROCEDURE PostComplWIPBuffer@1100485012();
    BEGIN
      //**4PS
      WITH ItemJnlLine DO BEGIN
        IF ComplWIPPostingBuffer[1].FIND('-') THEN
          REPEAT
            GenJnlLine.INIT;
            GenJnlLine."Journal Template Name" := "Journal Template Name";
            GenJnlLine."Journal Batch Name" := "Journal Batch Name";
            GenJnlLine."Source Code" := "Source Code";
            GenJnlLine."Reason Code":= "Reason Code";
            GenJnlLine."Account Type" := GenJnlLine."Account Type"::"G/L Account";
            GenJnlLine."Account No." := ComplWIPPostingBuffer[1]."G/L Account";
            //M24815 "Gen. Bus. Posting Group" Now used for Posting date
            EVALUATE(GenJnlLine."Posting Date", ComplWIPPostingBuffer[1]."Gen. Bus. Posting Group");
            //GenJnlLine."Posting Date" := ComplWIPPostingBuffer[1]."Posting Date";
            //GenJnlLine."Document Type" := GenJnlLineDocType;
            GenJnlLine."Document No." := ComplWIPPostingBuffer[1]."Tax Area Code";
            GenJnlLine."System-Created Entry" := TRUE;
            GenJnlLine."Document Date" := "Document Date";
            GenJnlLine.Description := ComplWIPPostingBuffer[1].Description;
            GenJnlLine."Description 2" := ComplWIPPostingBuffer[1]."Description 2";
            IF ComplWIPPostingBuffer[1].Element = '' THEN //C024228
              GenJnlLine."Bal. Account No." := ComplWIPPostingBuffer[1]."Bal. Account No.";
            GenJnlLine.Amount := ComplWIPPostingBuffer[1].Amount;
            GenJnlLine.VALIDATE(Amount);
            GenJnlLine."Shortcut Dimension 1 Code" := ComplWIPPostingBuffer[1]."Global Dimension 1 Code";
            GenJnlLine."Shortcut Dimension 2 Code" := ComplWIPPostingBuffer[1]."Global Dimension 2 Code";
            GenJnlLine."Dimension Set ID" := ComplWIPPostingBuffer[1]."Dimension Set ID";
            DimMgt.ValidateShortcutDimValues(1,GenJnlLine."Shortcut Dimension 1 Code",GenJnlLine."Dimension Set ID"); //DP00387 n
            DimMgt.ValidateShortcutDimValues(2,GenJnlLine."Shortcut Dimension 2 Code",GenJnlLine."Dimension Set ID"); //DP00387 n

            GenJnlLine."Closed Project No." := ComplWIPPostingBuffer[1]."Job No.";
            GenJnlLine."Closed Service Order No." := ComplWIPPostingBuffer[1]."Service Order No.";
            GenJnlLine."Closed Service Contract No." := ComplWIPPostingBuffer[1]."Service Contract No.";
            GenJnlLine."Origin Type" := ComplWIPPostingBuffer[1]."Origin Type";
            RunGenJnlPostLine(GenJnlLine);
          UNTIL ComplWIPPostingBuffer[1].NEXT = 0;
        ComplWIPPostingBuffer[1].DELETEALL;
      END;
    END;

    LOCAL PROCEDURE RunGenJnlPostLine@11012023(VAR GenJnlLine@1000 : Record 81);
    BEGIN
      //**4PS
      GenJnlPostLine.RunWithCheck(GenJnlLine);
    END;

    PROCEDURE PostWIPtoGLDebit@1210190000();
    VAR
      IntercompanyRelation@1100529400 : Record 11012057;
    BEGIN
      //**4PS
      WITH ItemJnlLine DO BEGIN
        GenJnlLine.INIT;
        GenJnlLine."Journal Template Name" := "Journal Template Name";
        GenJnlLine."Journal Batch Name" := "Journal Batch Name";
        GenJnlLine."Source Code" := "Source Code";
        GenJnlLine."Reason Code":= "Reason Code";
        GenJnlLine."Account Type" := GenJnlLine."Account Type"::"G/L Account";
        GenJnlLine."System-Created Entry" := TRUE;
        GenJnlLine."Document Date" := "Document Date";
        GenJnlLine."Posting Date" := "Posting Date";
        GenJnlLine."Document No." := "Document No.";
        GenJnlLine.Description := Description;
        GenJnlLine."Description 2" := "Description 2";
        GenJnlLine."Job No." := "Job No.";
        GenJnlLine.Element := Element;
        GenJnlLine."Extension Contract" := "Extension Contract";
        GenJnlLine."Service Order No." := "Service Order No.";
        GenJnlLine."Service Contract No." := "Service Contract No.";
        GenJnlLine."Service Location No." := "Service Location No.";
        GenJnlLine."Plant Type" := "Plant Type";
        GenJnlLine."Plant No." := "Plant No.";
        GenJnlLine."Cost Component Plant" := "Cost Component Plant";
        GenJnlLine."Additional Cost (Service)" := "Additional Cost (Service)";
        IF "Entry Type" = "Entry Type"::Purchase THEN
          GenJnlLine.Quantity := -Quantity
        ELSE
          GenJnlLine.Quantity := Quantity;
        GenJnlLine.Amount := ItemJnlLine."Unit Cost" * GenJnlLine.Quantity;
        IF (InventSetupRec."Item Sales Price for Proj/Serv" AND ("Entry Type" = "Entry Type"::Sale)) THEN
          GenJnlLine.Amount := "Unit Amount" * GenJnlLine.Quantity;  //* Amount instead of Cost

        GenJnlLine."Item No." := "Item No.";
        GenJnlLine."Dimension Set ID" := "Dimension Set ID";
        GenJnlLine."Shortcut Dimension 1 Code" := "Shortcut Dimension 1 Code";
        GenJnlLine."Shortcut Dimension 2 Code" := "Shortcut Dimension 2 Code";
        IF GenJnlLine."Service Order No." <> '' THEN BEGIN
          GenJnlLine."Origin Type" := GenJnlLine."Origin Type"::Service;
          ServOrderRec.CHANGECOMPANY("Receiving Company");
          ServOrderRec.GET("Service Order No.");
          GenJnlLine."Shortcut Dimension 1 Code" := ServOrderRec."Global Dimension 1 Code";
          GenJnlLine."Service Category" := ServOrderRec.GetServiceCategory;
        END ELSE BEGIN
          IF GenJnlLine."Job No." <> '' THEN BEGIN
            GenJnlLine."Origin Type" := GenJnlLine."Origin Type"::Project;
            ProjRec.CHANGECOMPANY("Receiving Company");
            ProjRec.GET("Job No.");
            GenJnlLine."Shortcut Dimension 1 Code" := ProjRec."Global Dimension 1 Code";
          END ELSE BEGIN
            IF GenJnlLine."Plant Type" <> '' THEN BEGIN
              GenJnlLine."Origin Type" := GenJnlLine."Origin Type"::Plant;
              PlantTypeRec.CHANGECOMPANY("Receiving Company");
              PlantTypeRec.GET("Plant Type");
              IF GenJnlLine."Plant No." <> '' THEN BEGIN
                PlantNoRec.CHANGECOMPANY("Receiving Company");
                IF NOT PlantNoRec.GET(GenJnlLine."Plant Type", GenJnlLine."Plant No.") THEN
                 PlantNoRec.INIT;
              END;
              IF ("Plant No." <> '') AND (PlantNoRec."Department Code" <> '') THEN
                GenJnlLine."Shortcut Dimension 1 Code" := PlantNoRec."Department Code"
              ELSE
                GenJnlLine."Shortcut Dimension 1 Code" := PlantTypeRec."Department Code";
            END ELSE BEGIN
              IF ("Delivery Account No." <> '') AND ("Receiving Company" = '') THEN
                GenJnlLine."Shortcut Dimension 1 Code" := "Delivery Dimension 1 Code";
            END;
          END;
        END;
        DimMgt.SetCompany("Receiving Company");  //DP00387 n
        DimMgt.ValidateShortcutDimValues(1, GenJnlLine."Shortcut Dimension 1 Code",GenJnlLine."Dimension Set ID" );
        DimMgt.ValidateShortcutDimValues(2, GenJnlLine."Shortcut Dimension 2 Code",GenJnlLine."Dimension Set ID" );

        GenJnlLine."Cost Component" := "Cost Component";

        GenJnlLine.VALIDATE(Amount);
        GenJnlLine."Account No." := GetAccount("Receiving Company", TRUE);

        DimMgt.SetCompany(COMPANYNAME);  //DP00387 n
        IF "Receiving Company" <> '' THEN BEGIN
          CreateICEntry(TRUE);
          GenJnlLine."Account No." := GetAccount("Receiving Company", FALSE);
          GenJnlLine."Job No." := '';
          GenJnlLine.Element := '';
          GenJnlLine."Extension Contract" := '';
          GenJnlLine."Service Order No." := '';
          GenJnlLine."Service Contract No." := '';
          GenJnlLine."Service Location No." := '';
          GenJnlLine."Service Category" := '';
          GenJnlLine."Plant Type" := '';
          GenJnlLine."Plant No." := '';
          GenJnlLine."Cost Component Plant" := '';
          GenJnlLine."Origin Type" := GenJnlLine."Origin Type"::" ";
          GenJnlLine."Shortcut Dimension 1 Code" := "Shortcut Dimension 1 Code";
          //DP00387 sn
          GenJnlLine."Dimension Set ID" := 0;
          GenJnlLine.CreateDim(
            DimMgt.TypeToTableID1(GenJnlLine."Account Type"),GenJnlLine."Account No.",
            DimMgt.TypeToTableID1(GenJnlLine."Bal. Account Type"),GenJnlLine."Bal. Account No.",
            DATABASE::"Plant Number", DimMgt.MakeCombinedPlantNo("Plant Type","Plant No."),
            DATABASE::"Plant Type", GenJnlLine."Plant Type",
            DATABASE::Job,GenJnlLine."Job No.",
            DATABASE::"Service Order",GenJnlLine."Service Order No.",
            DATABASE::"Salesperson/Purchaser",GenJnlLine."Salespers./Purch. Code",
            DATABASE::Campaign,GenJnlLine."Campaign No.");
          GenJnlLine.ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");  //-->> will not work, Cost center from other company??
          GenJnlLine.ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");
          //DP00387 en

          IntercompanyRelation.GET(COMPANYNAME,"Receiving Company");
          IntercompanyRelation.AddICRelationDimsOfCurrentCompany(
            GenJnlLine."Dimension Set ID",GenJnlLine."Shortcut Dimension 1 Code",GenJnlLine."Shortcut Dimension 2 Code");

        END;

        IF GenJnlLine."Service Order No." <> '' THEN
          GenJnlLine."Origin Type" := GenJnlLine."Origin Type"::Service
        ELSE
          IF GenJnlLine."Job No." <> '' THEN
            GenJnlLine."Origin Type" := GenJnlLine."Origin Type"::Project;
      END;
    END;

    PROCEDURE PostWIPtoGLCredit@1100485005();
    VAR
      GenPostingSetup@1100485000 : Record 252;
      lvPostingSetup@1100525000 : Record 11020565;
      IntercompanyRelation@1100529400 : Record 11012057;
      DepartmentCode@1100527000 : Code[20];
    BEGIN
      //**4PS
      WITH ItemJnlLine DO BEGIN
        //* Project, Service and Plant fields empty at reverse posting. Otherwise sometimes (at IC) 2 project-, service-, or
        //* plant entries. Sometimes also a problem regarding a message that account should be WIP account.
        GenJnlLine."Job No." := '';
        GenJnlLine.Element := '';
        GenJnlLine."Extension Contract" := '';
        GenJnlLine."Service Order No." := '';
        GenJnlLine."Service Contract No." := '';
        GenJnlLine."Service Location No." := '';
        GenJnlLine."Service Category" := '';
        GenJnlLine."Plant Type" := '';
        GenJnlLine."Plant No." := '';
        GenJnlLine."Cost Component Plant" := '';
        GenJnlLine."Origin Type" := GenJnlLine."Origin Type"::" ";

        GenJnlLine."Shortcut Dimension 1 Code" := "Shortcut Dimension 1 Code";
        GenJnlLine."Shortcut Dimension 2 Code" := "Shortcut Dimension 2 Code";
        GenJnlLine."Dimension Set ID" := "Dimension Set ID";

        GenJnlLine.Amount := -GenJnlLine.Amount;
        GenJnlLine.VALIDATE(Amount);

        GenPostingSetup.GET("Gen. Bus. Posting Group","Gen. Prod. Posting Group");
        GenPostingSetup.TESTFIELD("Job Cost Adjmt. Account");
        GenJnlLine."Account No." := GenPostingSetup."Job Cost Adjmt. Account";

        IF "Receiving Company" <> '' THEN BEGIN
          IF "Service Order No." <> '' THEN BEGIN
            ServOrderRec.CHANGECOMPANY("Receiving Company");
            ServOrderRec.GET("Service Order No.");
            GenJnlLine."Shortcut Dimension 1 Code" := ServOrderRec."Global Dimension 1 Code";
            //**C038709.sn
            IF InventSetupRec."Automatic Cost Posting" THEN BEGIN
              IF lvPostingSetup.GET("Source Code", COMPANYNAME, ItemJnlLine."Receiving Company") THEN BEGIN
                lvPostingSetup.TESTFIELD("Prod. Account Credit");
                GenJnlLine."Account No." := lvPostingSetup."Prod. Account Credit";
              END;
            END;
            //**C038709.sn
          END ELSE BEGIN
            IF "Job No." <> '' THEN BEGIN
              ProjRec.CHANGECOMPANY("Receiving Company");
              ProjRec.GET("Job No.");
              GenJnlLine."Shortcut Dimension 1 Code" := ProjRec."Global Dimension 1 Code";
              IF InventSetupRec."Automatic Cost Posting" THEN BEGIN
                IF lvPostingSetup.GET("Source Code", COMPANYNAME, ItemJnlLine."Receiving Company") THEN BEGIN
                  lvPostingSetup.TESTFIELD("Prod. Account Credit");
                  GenJnlLine."Account No." := lvPostingSetup."Prod. Account Credit";
                END;
              END;
            END ELSE BEGIN
              IF "Plant Type" <> '' THEN BEGIN
                PlantTypeRec.CHANGECOMPANY("Receiving Company");
                PlantTypeRec.GET("Plant Type");
                IF GenJnlLine."Plant No." <> '' THEN BEGIN
                  PlantNoRec.CHANGECOMPANY("Receiving Company");
                  IF NOT PlantNoRec.GET(GenJnlLine."Plant Type", GenJnlLine."Plant No.") THEN
                    PlantNoRec.INIT;
                END;
                IF ("Plant No." <> '') AND (PlantNoRec."Department Code" <> '') THEN
                  GenJnlLine."Shortcut Dimension 1 Code" := PlantNoRec."Department Code"
                ELSE
                  GenJnlLine."Shortcut Dimension 1 Code" := PlantTypeRec."Department Code";
              END ELSE BEGIN
                IF ("Delivery Account No." <> '') AND ("Receiving Company" = '') THEN
                  GenJnlLine."Shortcut Dimension 1 Code" := "Delivery Dimension 1 Code";
              END;
            END;
          END;
          DimMgt.SetCompany("Receiving Company");  //DP00387 n
          DimMgt.ValidateShortcutDimValues(1,GenJnlLine."Shortcut Dimension 1 Code",GenJnlLine."Dimension Set ID");
          DimMgt.SetCompany("Receiving Company");  //DP00387 n

          CreateICEntry(FALSE);
          GenJnlLine."Shortcut Dimension 1 Code" := "Shortcut Dimension 1 Code";

          IF lvPostingSetup.GET("Source Code", COMPANYNAME, "Receiving Company") THEN
            IF lvPostingSetup.GetDepartmentCode(GenJnlLine."Account No.",'',"Location Code",DepartmentCode) THEN
              GenJnlLine."Shortcut Dimension 1 Code" := DepartmentCode;

          //DP00387 sn
          GenJnlLine."Dimension Set ID" := 0;
          GenJnlLine.ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");  //-->> will not work, Cost center from other company!!

          GenJnlLine.CreateDim(
            DimMgt.TypeToTableID1(GenJnlLine."Account Type"),GenJnlLine."Account No.",
            DimMgt.TypeToTableID1(GenJnlLine."Bal. Account Type"),GenJnlLine."Bal. Account No.",
            DATABASE::"Plant Number", DimMgt.MakeCombinedPlantNo("Plant Type","Plant No."),
            DATABASE::"Plant Type", GenJnlLine."Plant Type",
            DATABASE::Job,GenJnlLine."Job No.",
            DATABASE::"Service Order",GenJnlLine."Service Order No.",
            DATABASE::"Salesperson/Purchaser",GenJnlLine."Salespers./Purch. Code",
            DATABASE::Campaign,GenJnlLine."Campaign No.");

          DimMgt.ValidateShortcutDimValues(1, GenJnlLine."Shortcut Dimension 1 Code",GenJnlLine."Dimension Set ID" );     //DP00387 n
          DimMgt.ValidateShortcutDimValues(2, GenJnlLine."Shortcut Dimension 2 Code",GenJnlLine."Dimension Set ID" );     //DP00387 n

          //DP00387 en
          //GenJnlLine."Shortcut Dimension 1 Code" := '';

          IntercompanyRelation.GET(COMPANYNAME,"Receiving Company");
          IntercompanyRelation.AddICRelationDimsOfCurrentCompany(
            GenJnlLine."Dimension Set ID",GenJnlLine."Shortcut Dimension 1 Code",GenJnlLine."Shortcut Dimension 2 Code");
        END;

        IF "Supplying Company" <> '' THEN
          GenJnlLine."Account No." := GetAccount("Supplying Company", FALSE);

      END;
    END;

    PROCEDURE CreateICEntry@11012027(IModeCredit@1100485000 : Boolean);
    VAR
      ICEntryRec@1210190015 : Record 11012058;
      lvPostingSetup@1100525000 : Record 11020565;
      IntercompanyRelation@1100528400 : Record 11012057;
      ICCounter@1210190016 : Integer;
    BEGIN
      //**4PS
      IF (ItemJnlLine."IC Inventory Line Type" > ItemJnlLine."IC Inventory Line Type"::Order) THEN
        EXIT;

      ICEntryRec.LOCKTABLE;
      IF ICEntryRec.FINDLAST THEN
        ICCounter := ICEntryRec."Line No."
      ELSE
        ICCounter := 0;

      WITH GenJnlLine DO BEGIN   //*  Fill most fields from 'GenJnlLine' instead of 'ItemJnlLine'!
        ICEntryRec.INIT;
        ICEntryRec."Line No." := ICCounter + 1;
        ICEntryRec."Post in Company" := ItemJnlLine."Receiving Company";
        ICEntryRec."Supplying Company" := COMPANYNAME;
        ICEntryRec."Receiving Company" := ItemJnlLine."Receiving Company";
        ICEntryRec."Account Type" := ICEntryRec."Account Type"::"G/L Account";
        IF IModeCredit THEN
          ICEntryRec."Bal. Account No." := GetAccount(ICEntryRec."Receiving Company", IModeCredit)
        ELSE BEGIN
          IntercompanyRelation.GET(COMPANYNAME, ICEntryRec."Receiving Company");
          IntercompanyRelation.TESTFIELD("Receiving Company IC Account");
          ICEntryRec."Account No." := IntercompanyRelation."Receiving Company IC Account";
        END;
        ICEntryRec.Description := Description;
        ICEntryRec."Description 2" := "Description 2";
        ICEntryRec."Project No." := "Job No.";
        ICEntryRec.Element := Element;
        ICEntryRec."Extension Contract" := "Extension Contract";
        ICEntryRec."Service Order No." := "Service Order No.";
        ICEntryRec."Service Contract No." := "Service Contract No.";
        ICEntryRec."Service Location No." := "Service Location No.";
        ICEntryRec."Additional Cost (Service)" := ItemJnlLine."Additional Cost (Service)";
        ICEntryRec."Plant Type" := "Plant Type";
        ICEntryRec."Plant No." := "Plant No.";
        ICEntryRec."Cost Component Plant" := "Cost Component Plant";
        ICEntryRec."Item No." := "Item No.";
        ICEntryRec."Cost Object" := "Shortcut Dimension 2 Code";
        ICEntryRec."Cost Component" := "Cost Component";
        ICEntryRec."Global Dimension 1 Code" := "Shortcut Dimension 1 Code";
        ICEntryRec."Dimension Set ID" := GenJnlLine."Dimension Set ID"; //**DP00387 n
        IF Quantity = 0 THEN
          ICEntryRec.Quantity := 1
        ELSE
          ICEntryRec.Quantity := Quantity;
        IF ItemRec.GET("Item No.") THEN
          ICEntryRec."Unit of Measure Code" := ItemRec."Base Unit of Measure"
        ELSE
          ICEntryRec."Unit of Measure Code" := ItemJnlLine."Unit of Measure Code";
        ICEntryRec.Amount := ROUND(ICEntryRec.Quantity * ItemJnlLine."Unit Cost");

        IF (InventSetupRec."Item Sales Price for Proj/Serv" AND
           (ItemJnlLine."Entry Type" = ItemJnlLine."Entry Type"::Sale)) THEN
          ICEntryRec.Amount := ICEntryRec.Quantity * ItemJnlLine."Unit Amount";  //* Amount instead of Cost

        ICEntryRec.Price := ICEntryRec.Amount / ICEntryRec.Quantity;  //* Qty always unequal to zero, see fill field
        ICEntryRec."Document No." := "Document No.";
        ICEntryRec."Posting Date" := "Posting Date";
        ICEntryRec."Reason Code" := "Reason Code";

        ICEntryRec.TESTFIELD("Cost Object");

        ICEntryRec.CheckProjStatusReceivingComp();
        ICEntryRec.CheckProjElemBlockedRecComp();

        //Source Code := empty
        IF lvPostingSetup.GET("Source Code", COMPANYNAME, ItemJnlLine."Receiving Company") THEN
        BEGIN
          lvPostingSetup.TESTFIELD("Prod. Account Debit");
          lvPostingSetup.TESTFIELD("Prod. Account Credit");
          ICEntryRec."Use IC Vendor Posting Group" := TRUE;
        END;

        DimMgt.SetCompany(ItemJnlLine."Receiving Company");
        ICEntryRec."Dimension Set ID" := DimMgt.GetDeltaDimSetID(ItemJnlLine."Dimension Set ID",GenJnlLine."Dimension Set ID",0);
        DimMgt.SetCompany(COMPANYNAME);

        ICEntryRec.AddICRelationDims(IntercompanyRelation);
        ICEntryRec.INSERT(TRUE);
      END;
    END;

    PROCEDURE CreateICEntrySurcharge@1100525001(IntercompanyRelation@1100528400 : Record 11012057);
    VAR
      ICEntryRec@1210190015 : Record 11012058;
      lvPostingSetup@1100525000 : Record 11020565;
      ICCounter@1210190016 : Integer;
    BEGIN
      //**4PS
      ICEntryRec.LOCKTABLE;
      IF ICEntryRec.FINDLAST THEN
        ICCounter := ICEntryRec."Line No."
      ELSE
        ICCounter := 0;

      WITH GenJnlLine DO BEGIN   //*  Fill most fields from 'GenJnlLine' instead of 'ItemJnlLine'!
        ICEntryRec.INIT;
        ICEntryRec."Line No." := ICCounter + 1;
        ICEntryRec."Post in Company" := ItemJnlLine."Receiving Company";
        ICEntryRec."Supplying Company" := COMPANYNAME;
        ICEntryRec."Receiving Company" := ItemJnlLine."Receiving Company";
        ICEntryRec."Account Type" := ICEntryRec."Account Type"::"G/L Account";

      //ICEntryRec."Bal. Account No." := "Account No."; //C039398.o
        ICEntryRec."Bal. Account No." := GetAccount(ItemJnlLine."Receiving Company",TRUE); //C039398.n
        ICEntryRec."Account No." := IntercompanyRelation."Receiving Company IC Account";

        ICEntryRec.Description := Description;
        ICEntryRec."Description 2" := "Description 2";
        ICEntryRec."Project No." := "Job No.";
        ICEntryRec.Element := Element;
        ICEntryRec."Extension Contract" := "Extension Contract";
        ICEntryRec."Service Order No." := "Service Order No.";
        ICEntryRec."Service Contract No." := "Service Contract No.";
        ICEntryRec."Service Location No." := "Service Location No.";
        ICEntryRec."Additional Cost (Service)" := ItemJnlLine."Additional Cost (Service)";
        ICEntryRec."Plant Type" := "Plant Type";
        ICEntryRec."Plant No." := "Plant No.";
        ICEntryRec."Cost Component Plant" := "Cost Component Plant";
        ICEntryRec."Item No." := "Item No.";
        ICEntryRec."Cost Object" := "Shortcut Dimension 2 Code";
        ICEntryRec."Cost Component" := "Cost Component";
        ICEntryRec."Global Dimension 1 Code" := "Shortcut Dimension 1 Code";

        IF Quantity = 0 THEN
          ICEntryRec.Quantity := 1
        ELSE
          ICEntryRec.Quantity := Quantity;

        IF ItemRec.GET("Item No.") THEN
          ICEntryRec."Unit of Measure Code" := ItemRec."Base Unit of Measure"
        ELSE
          ICEntryRec."Unit of Measure Code" := ItemJnlLine."Unit of Measure Code";

        ICEntryRec.Amount := SurchargePostingBuffer[1].Amount;

        ICEntryRec.Price := ICEntryRec.Amount / ICEntryRec.Quantity;  //* Qty always unequal to zero, see fill field
        ICEntryRec."Document No." := "Document No.";
        ICEntryRec."Posting Date" := "Posting Date";

        ICEntryRec.TESTFIELD("Cost Object");

        ICEntryRec.CheckProjStatusReceivingComp();
        ICEntryRec.CheckProjElemBlockedRecComp(); //Call 6564

        ICEntryRec.Surcharge := TRUE;

        //Source Code := empty
        IF lvPostingSetup.GET("Source Code", COMPANYNAME, ItemJnlLine."Receiving Company") THEN
        BEGIN
          lvPostingSetup.TESTFIELD("Prod. Account Debit");
          lvPostingSetup.TESTFIELD("Prod. Account Credit");
          ICEntryRec."Use IC Vendor Posting Group" := TRUE;
        END;

        DimMgt.SetCompany(ItemJnlLine."Receiving Company");
        ICEntryRec."Dimension Set ID" := ItemJnlLine."Dimension Set ID";
        DimMgt.GetDeltaDimSetID(ItemJnlLine."Dimension Set ID", GenJnlLine."Dimension Set ID",0);
        DimMgt.SetCompany(COMPANYNAME);

        ICEntryRec.AddICRelationDims(IntercompanyRelation);
        ICEntryRec.INSERT(TRUE);
      END;
    END;

    PROCEDURE GetAccount@1210190002(lvCompName@1210190000 : Text[50];lvBalAcc@1210190003 : Boolean) : Code[20];
    VAR
      ICRec@1210190004 : Record 11012057;
      lvPlantPostingSetupRec@1100485000 : Record 11012570;
      lvPostingSetup@1100525000 : Record 11020565;
      lvVendorPostingGroup@1100525001 : Code[20];
      lvVendPostGrpLedAccProj@1100525002 : Record 11020252;
      lvVendPostGrpLedAccServ@1100525003 : Record 11012889;
    BEGIN
      //**4PS
      GLSetup.GET;
      WITH ItemJnlLine DO BEGIN
        IF lvBalAcc = FALSE THEN BEGIN
          ICRec.GET(COMPANYNAME, lvCompName);
          ICRec.TESTFIELD("Supplying Company IC Account");
          ICRec.TESTFIELD("Receiving Company IC Account");
          EXIT(ICRec.GetICAccountOfCurrentCompany);
        END ELSE BEGIN
          IF lvCompName = '' THEN
            lvCompName := COMPANYNAME;

          lvVendorPostingGroup := '';

          IF ("Receiving Company" <> COMPANYNAME) AND
             (lvCompName <> '') AND
             ("Receiving Company" <> '') THEN
          BEGIN
            ICRec.GET(COMPANYNAME, lvCompName);
            IF lvPostingSetup.GET("Source Code", COMPANYNAME, "Receiving Company") THEN
            BEGIN;
              IF (lvPostingSetup."Prod. Account Debit" <> '') AND
                 (lvPostingSetup."Prod. Account Credit" <> '') THEN
              BEGIN
                lvVendorPostingGroup := ICRec."Vendor Posting Group";
              END;
            END;
          END;

          IF "Service Order No." <> '' THEN BEGIN
            ServOrderRec.CHANGECOMPANY(lvCompName);
            ServiceType.CHANGECOMPANY(lvCompName);
            lvVendPostGrpLedAccServ.CHANGECOMPANY(lvCompName); //C039398
            ServOrderRec.GET("Service Order No.");
            IF ItemJnlLine."Additional Cost (Service)" = TRUE THEN BEGIN
              ServOrderRec.TESTFIELD("Service Type (Other)");
              ServiceType.GET(ServOrderRec."Service Type (Other)");
            END ELSE BEGIN
              ServOrderRec.TESTFIELD("Service Type");
              ServiceType.GET(ServOrderRec."Service Type");
            END;
            IF lvVendorPostingGroup <> '' THEN BEGIN
              IF lvVendPostGrpLedAccServ.GET(ServiceType.Code, lvVendorPostingGroup) THEN
                ServiceType."WIP Account Material" := lvVendPostGrpLedAccServ."WIP Account Material";
            END;
            ServiceType.TESTFIELD("WIP Account Material");
            EXIT(ServiceType."WIP Account Material");
          END;

          IF "Job No." <> '' THEN BEGIN
            ProjSetupRec.CHANGECOMPANY(lvCompName);
            ProjRec.CHANGECOMPANY(lvCompName);
            ProjectType.CHANGECOMPANY(lvCompName);
            lvVendPostGrpLedAccProj.CHANGECOMPANY(lvCompName);
            ProjSetupRec.GET;
            ProjRec.GET("Job No.");
            ProjRec.TESTFIELD("Project Type");
            ProjectType.GET(ProjRec."Project Type");
            lvVendPostGrpLedAccProj.INIT;
            IF lvVendorPostingGroup <> '' THEN BEGIN
              IF lvVendPostGrpLedAccProj.GET(ProjectType.Code, lvVendorPostingGroup) THEN
                ProjectType."WIP Account Material" := lvVendPostGrpLedAccProj."WIP Account Material";
            END;

            IF (ProjRec."Project Status" >= ProjRec."Project Status"::Finished) AND
               (ProjSetupRec."Provisions at Closure" = TRUE) THEN BEGIN
              ProjectType.TESTFIELD("Provision Account Material");
              EXIT(ProjectType."Provision Account Material");
            END ELSE BEGIN
              ProjectType.TESTFIELD("WIP Account Material");
              EXIT(ProjectType."WIP Account Material");
            END;
          END;

          IF "Plant Type" <> '' THEN BEGIN
            lvPlantPostingSetupRec.CHANGECOMPANY(lvCompName);
            PlantTypeRec.CHANGECOMPANY(lvCompName);
            PlantTypeRec.GET("Plant Type");
            lvPlantPostingSetupRec.GET(
              PlantTypeRec.PlantPostingGrp("Plant No.","Receiving Company",''),
              lvVendorPostingGroup,     //field for vendor posting group
              "Cost Component Plant");
              lvPlantPostingSetupRec.TESTFIELD("Plant Cost Account");
            EXIT(lvPlantPostingSetupRec."Plant Cost Account");
          END;

          IF "Delivery Account No." <> '' THEN
            EXIT("Delivery Account No.");

        END;
      END;

      EXIT('');
    END;

    PROCEDURE CreatePlant@1100485000();
    VAR
      Item@1100485010 : Record 27;
      PlantSetup@1100485009 : Record 11012550;
      ToPlantTypeRec@1100485008 : Record 11012551;
      PlantNoRec@1100485007 : Record 11012552;
      PlantLedgerEntry@1100485006 : Record 11012572;
      PlantCompPostingSetup@1100485003 : Record 11012570;
      LocalGLAcc@1100485004 : Record 15;
      FA@1100530001 : Record 5600;
      FADeprBook@1100485011 : Record 5612;
      FAPostingGr@1100485002 : Record 5606;
      GenPostingSetup@1100485013 : Record 252;
      PostPlantNoCU@1100485000 : Codeunit 11012550;
      PlantJnlPostLine@1100485012 : Codeunit 11012569;
      DeprBookCode@1100530000 : Code[10];
    BEGIN
      //**4PS
      WITH ItemJnlLine DO BEGIN
        //Check and Read
        IF NOT "To Plant Inventory" THEN
          EXIT;
        TESTFIELD("To Plant Type");
        TESTFIELD("To Plant Location");
        TESTFIELD("Receiving Company",'');
        TESTFIELD("Job No.",'');
        TESTFIELD("Service Order No.",'');
        TESTFIELD("Plant Type",'');
        TESTFIELD("Delivery Account No.",'');
        TESTFIELD("Entry Type", "Entry Type"::Sale);
        Item.GET("Item No.");
        IF Quantity <= 0 THEN
          ERROR(Text11012001);

        PlantSetup.GET;
        PlantSetup.TESTFIELD("Cost Component Acquisition");
        ToPlantTypeRec.GET("To Plant Type");
        ToPlantTypeRec.TESTFIELD("Posting Group Internal");
        ToPlantTypeRec.TESTFIELD(Bulk,TRUE);
        ToPlantTypeRec.TESTFIELD(External,FALSE);
        IF (NOT PlantSetup."Integration Fixed Assets") OR (NOT ToPlantTypeRec."Integration Fixed Assets") THEN
          PlantSetup.TESTFIELD("Transferred Item Account");

        //Insert New Plant No.
        PlantNoRec.INIT;
        PlantNoRec."Plant Type" := "To Plant Type";
        PlantNoRec."No." := '';
        PlantNoRec.INSERT(TRUE);
        PlantNoRec.Description := COPYSTR(Description, 1, MAXSTRLEN(PlantNoRec.Description));
        PlantNoRec.VALIDATE(Description);
        PlantNoRec."Receipt Date" := "Posting Date";
        PlantNoRec."Posting Date" := "Posting Date";
        PlantNoRec.Quantity := Quantity;
        PlantNoRec.External := FALSE;
        PlantNoRec."Ship To Location" := "To Plant Location";
        PlantNoRec."Purchase Price" := Item."Unit Cost";
        PlantNoRec.MODIFY(TRUE);

        //Post Plant No.
        PostPlantNoCU.RUN(PlantNoRec);

        IF PlantSetup."Integration Fixed Assets" AND ToPlantTypeRec."Integration Fixed Assets" THEN BEGIN
          //Create FA
          PlantNoRec.CreateFA(TRUE, '');
          PlantNoRec.FIND;

          //Post Plant Investment to GL

          //*FA.sn
          DeprBookCode := FA.GetFA_MainDeprBook(PlantNoRec."Fixed Asset",FALSE); //* FALSE, not the standard error
          IF DeprBookCode = '' THEN BEGIN
            FADeprBook.SETRANGE("FA No.", PlantNoRec."Fixed Asset");
            IF NOT FADeprBook.FINDFIRST THEN
              ERROR(Text11012002, PlantNoRec."Plant Type", PlantNoRec."No.")
            ELSE  // Then there must be more than one, but none is setup as Main Depr. Book
              ERROR(Text11012003, PlantNoRec."Plant Type");
          END;
          FADeprBook.GET(PlantNoRec."Fixed Asset", DeprBookCode);
          FADeprBook.TESTFIELD("FA Posting Group");
          FAPostingGr.GET(FADeprBook."FA Posting Group");
          //*FA.en
          FAPostingGr.TESTFIELD("Acquisition Cost Account");
          LocalGLAcc.GET(FAPostingGr."Acquisition Cost Account");
          LocalGLAcc.CheckGLAcc;
          GenPostingSetup.GET("Gen. Bus. Posting Group","Gen. Prod. Posting Group");
          GenPostingSetup.TESTFIELD("Purch. Account");

          GenJnlLine.INIT;
          GenJnlLine."Posting Date" := "Posting Date";
          GenJnlLine."Document Date" := "Document Date";
          GenJnlLine.Description := PlantNoRec.Description;
          GenJnlLine."Description 2" := "Description 2";
          GenJnlLine."Source Code" := "Source Code";
          GenJnlLine."Reason Code" := "Reason Code";
          GenJnlLine."Document No." := "Document No.";
          GenJnlLine."Account Type" := GenJnlLine."Account Type"::"Fixed Asset";
          GenJnlLine."Account No." := PlantNoRec."Fixed Asset";
          GenJnlLine."FA Posting Type" := GenJnlLine."FA Posting Type"::"Acquisition Cost";
          GenJnlLine."System-Created Entry" := TRUE;
          GenJnlLine.Quantity := Quantity;
          GenJnlLine.Amount := ROUND(PlantNoRec."Purchase Price" * Quantity);
          GenJnlLine."Bal. Account Type" := GenJnlLine."Bal. Account Type"::"G/L Account";
          GenJnlLine."Bal. Account No." := GenPostingSetup."Purch. Account";
          GenJnlLine."FA Posting Date" := "Posting Date";
          //GenJnlLine."Depreciation Book Code" := FASetup."Default Depr. Book";  //*FA.o
          GenJnlLine."Depreciation Book Code" := DeprBookCode;  //*FA.n
          GenJnlLine."FA Posting Type" := GenJnlLine."FA Posting Type"::"Acquisition Cost";
          //GenJnlLine."Posting Group" := lvPlantPostingGrp."FA Posting Group";  //*FA.o
          GenJnlLine."Posting Group" := FADeprBook."FA Posting Group";  //*FA.n
          ItemJnlPostLine.PostPlantToGL(GenJnlLine); //Note TempJnlLineDim is empty here
        END ELSE BEGIN
          //Post Plant Investment to GL
          PlantCompPostingSetup.GET(ToPlantTypeRec."Posting Group Internal",
                                    '',  //fill vendor posting group here?
                                    PlantSetup."Cost Component Acquisition");
          PlantCompPostingSetup.TESTFIELD("Plant Cost Account");

          GenJnlLine.INIT;
          GenJnlLine."Posting Date" := "Posting Date";
          GenJnlLine."Document Date" := "Document Date";
          GenJnlLine.Description := PlantNoRec.Description;
          GenJnlLine."Description 2" := "Description 2";
          GenJnlLine."Source Code" := "Source Code";
          GenJnlLine."Reason Code" := "Reason Code";
          GenJnlLine."Document No." := "Document No.";
          GenJnlLine."Account Type" := GenJnlLine."Account Type"::"G/L Account";
          GenJnlLine."Account No." := PlantCompPostingSetup."Plant Cost Account";
          GenJnlLine."System-Created Entry" := TRUE;
          GenJnlLine.Quantity := Quantity;
          GenJnlLine.Amount := ROUND(PlantNoRec."Purchase Price" * Quantity);
          GenJnlLine."Bal. Account Type" := GenJnlLine."Bal. Account Type"::"G/L Account";
          GenJnlLine."Bal. Account No." := PlantSetup."Transferred Item Account";
          ItemJnlPostLine.PostPlantToGL(GenJnlLine); //Note TempJnlLineDim is empty here

          //Post Plant Investment Plant Jnl
          PlantLedgerEntry.INIT;
          PlantLedgerEntry."Document No." := "Document No.";
          PlantLedgerEntry."Posting Date" := "Posting Date";
          PlantLedgerEntry."Document Date" := "Document Date";
          PlantLedgerEntry."Account No." := PlantSetup."Transferred Item Account";
          PlantLedgerEntry.Description := PlantNoRec.Description;
          PlantLedgerEntry."Description 2" := "Description 2";
          PlantLedgerEntry.Quantity := Quantity;
          PlantLedgerEntry."Direct Unit Cost" := PlantNoRec."Purchase Price";
          PlantLedgerEntry."Unit Cost" := PlantNoRec."Purchase Price";
          PlantLedgerEntry."Total Cost" := ROUND(PlantNoRec."Purchase Price" * Quantity);
          PlantLedgerEntry."Unit of Measure Code" := "Unit of Measure Code";
          PlantLedgerEntry."Department Code" := "Shortcut Dimension 1 Code";
          PlantLedgerEntry."Cost Object" := "Shortcut Dimension 2 Code";
          PlantLedgerEntry."Dimension Set ID" := "Dimension Set ID";
          PlantLedgerEntry."Source Code" := "Source Code";
          PlantLedgerEntry."Reason Code" := "Reason Code";
          PlantLedgerEntry."Plant Posting Group" := ToPlantTypeRec."Posting Group Internal";
          PlantLedgerEntry."Plant Type" := "To Plant Type";
          PlantLedgerEntry."Plant No." := PlantNoRec."No.";
          PlantLedgerEntry."Cost Component" := PlantSetup."Cost Component Acquisition";
          PlantJnlPostLine.RUN(PlantLedgerEntry);
        END;

      END;
    END;

    PROCEDURE SetNotIncrJnlBatchNr@1100485004();
    BEGIN
      //**4PS
      NotIncrJnlBatchNr := TRUE;
    END;

    PROCEDURE PostProjectInventory@1100485002();
    VAR
      lvProjInventEntryRec@1100485001 : Record 11012670;
      lvProjInventPostLineCU@1100485000 : Codeunit 11012670;
    BEGIN
      //**4PS
      IF (NOT ItemJnlLine."Project Stock") OR (ItemJnlLine."Job No." = '') THEN
        EXIT;
      IF NOT (ItemJnlLine."Entry Type" IN [ItemJnlLine."Entry Type"::Purchase, ItemJnlLine."Entry Type"::Sale]) THEN
        EXIT;

      IF NOT InventSetupRec.JobInventoryByStockRelease(ItemJnlLine."Job No.") THEN
        EXIT;

      InvtSetup.GET; //C035757.n

      IF ItemJnlLine."Receiving Company" <> '' THEN
        lvProjInventEntryRec.CHANGECOMPANY(ItemJnlLine."Receiving Company");

      WITH lvProjInventEntryRec DO BEGIN
        INIT;
        "Document No." := ItemJnlLine."Document No.";
        "Document Date" := ItemJnlLine."Document Date";
        "Posting Date" := ItemJnlLine."Posting Date";
        "Project No." := ItemJnlLine."Job No.";
        "Item No." := ItemJnlLine."Item No.";
        "Variant Code" := ItemJnlLine."Variant Code";
        Description := ItemJnlLine.Description;
        "Description 2" := ItemJnlLine."Description 2";
        //C035757.sn
        IF InventSetupRec."Use Ship-to-Loc. Proj. Invt." AND (ItemJnlLine."Ship To Location" <> '') THEN
          "Location Code" := ItemJnlLine."Ship To Location"
        ELSE
        //C035757.en
          "Location Code" := ItemJnlLine."Location Code";
        IF ItemJnlLine."Bin Code (Project Inventory)" <> '' THEN
          "Bin Code" := ItemJnlLine."Bin Code (Project Inventory)"
        ELSE
          "Bin Code" := ItemJnlLine."Bin Code";
        "Shelf No." := ItemJnlLine."Shelf No.";
        //C002112.sn
        IF ItemJnlLine.Deliver IN [ItemJnlLine.Deliver::Location, ItemJnlLine.Deliver::"Project Location"] THEN BEGIN
          IF ItemJnlLine.Deliver = ItemJnlLine.Deliver::"Project Location" THEN
            Deliver := Deliver::"Project Location"
          ELSE
            Deliver := Deliver::Location;
          "Ship To Location" := ItemJnlLine."Ship To Location";
        END;
        //C002112.en
        Quantity := ItemJnlLine.Quantity;
        IF ItemJnlLine."Entry Type" = ItemJnlLine."Entry Type"::Purchase THEN
          Quantity := -Quantity;
        "Unit of Measure Code" := ItemJnlLine."Unit of Measure Code";
        "Entry Type" := lvProjInventEntryRec."Entry Type"::StockReceipt;
        "Cost Object" := ItemJnlLine."Shortcut Dimension 2 Code";
        "Yard No." := ItemJnlLine."Yard No.";
        "System No." := ItemJnlLine."System No.";
        "Entity Type" := ItemJnlLine."Entity Type";
        "Entity No." := ItemJnlLine."Entity No.";
        "Gland Position" := ItemJnlLine."Cable Transit Pos.";
        IF ItemJnlLine."Project Stock" THEN
          Element := ItemJnlLine.Element;
        "Plot No." := ItemJnlLine."Plot No.";
        "Extension Contract" := ItemJnlLine."Extension Contract";
        lvProjInventPostLineCU.RunWithCheck(lvProjInventEntryRec);
      END;
    END;

    LOCAL PROCEDURE CalcSKUSurchargesNoStock@1100485006() lvHandleIt : Boolean;
    VAR
      lSKUSurcharges@1100485000 : Record 11020317;
      lStockSurcharges@1100485001 : Record 11020316;
      SKU@1100485002 : Record 5700;
      lvSurchargeCalculated@1100485003 : Decimal;
      lvSurchargeRevalued@1100485004 : Decimal;
    BEGIN
      //**4PS
      IF SKU.FINDSET(TRUE, FALSE) THEN
        REPEAT
          IF NOT TempSKUrec.GET(SKU."Location Code", SKU."Item No.", SKU."Variant Code") THEN BEGIN

            lSKUSurcharges.SETRANGE("Location Code", SKU."Location Code");
            lSKUSurcharges.SETRANGE("Item No.", SKU."Item No.");
            lSKUSurcharges.SETRANGE("Variant Code", SKU."Variant Code");
            lSKUSurcharges.SETRANGE("Deletion Posted", FALSE);

            lvHandleIt := FALSE;
            lvSurchargeCalculated := 0;
            lvSurchargeRevalued := 0;

            IF lSKUSurcharges.FINDSET(FALSE, FALSE) THEN
              REPEAT
                IF lStockSurcharges.GET(lSKUSurcharges."Surcharge Code") THEN BEGIN

                  IF NOT ((lSKUSurcharges."Time Posted" = 0DT) AND (lSKUSurcharges."To Delete")) THEN BEGIN

                    IF lSKUSurcharges."Time Posted" = 0DT THEN BEGIN
                      lStockSurcharges."Actual Amount" := 0;
                      lStockSurcharges."Actual Percentage" := 0;
                    END;

                    IF lSKUSurcharges."To Delete" THEN BEGIN
                      lStockSurcharges.Amount := 0;
                      lStockSurcharges.Percentage := 0;
                    END;

                    IF ((lStockSurcharges.Amount <> lStockSurcharges."Actual Amount") OR
                       (lStockSurcharges.Percentage <> lStockSurcharges."Actual Percentage")) AND
                       (NOT lvHandleIt) THEN
                      lvHandleIt := TRUE;

                    IF lStockSurcharges."Actual Amount" <> 0 THEN
                      lvSurchargeCalculated += lStockSurcharges."Actual Amount";

                    IF lStockSurcharges.Amount <> 0 THEN
                      lvSurchargeRevalued  += lStockSurcharges.Amount;

                    IF lStockSurcharges."Actual Percentage" <> 0 THEN
                      lvSurchargeCalculated += ROUND(SKU."Standard Cost" * (lStockSurcharges."Actual Percentage"/100));

                    IF lStockSurcharges.Percentage <> 0 THEN
                      lvSurchargeRevalued += ROUND(SKU."Standard Cost" * (lStockSurcharges.Percentage/100));
                  END;
                END;

              UNTIL lSKUSurcharges.NEXT = 0;

            IF lvHandleIt AND (lvSurchargeCalculated <> lvSurchargeRevalued) THEN BEGIN
              SKU."Surcharge SKU" := lvSurchargeRevalued;
              SKU.MODIFY;
            END;
          END;
        UNTIL SKU.NEXT = 0 ;
    END;

    PROCEDURE PostComplementaryWIPProj@1100485007(lvGenJnlLine@1100485012 : Record 81);
    VAR
      GenJnlLine1@1100485006 : Record 81;
    BEGIN
      //**4PS
      IF lvGenJnlLine.Amount = 0 THEN
        EXIT;

      //C040452.sn
      IF ItemJnlLine."Receiving Company" <> '' THEN
        EXIT;
      //C040452.en

      WITH lvGenJnlLine DO BEGIN
        GenJnlLine1.INIT;
        GenJnlLine1."Source Code" := "Source Code";
        GenJnlLine1."Reason Code" := "Reason Code";
        GenJnlLine1."Account Type" := GenJnlLine1."Account Type"::"G/L Account";
        GenJnlLine1."Posting Date" := "Posting Date";
        GenJnlLine1."Document No." := "Document No.";
        GenJnlLine1."System-Created Entry" := TRUE;
        GenJnlLine1."Document Date" := "Document Date";
        GenJnlLine1.Description := Description;
        GenJnlLine1."Description 2" := "Description 2";
        GenJnlLine1.Amount := -1 * Amount;
        GenJnlLine1.VALIDATE(Amount, GenJnlLine1.Amount);
        GenJnlLine1."Shortcut Dimension 1 Code" := "Shortcut Dimension 1 Code";
        GenJnlLine1."Shortcut Dimension 2 Code" := "Shortcut Dimension 2 Code";
        GenJnlLine1."Dimension Set ID" := "Dimension Set ID";
        GenJnlLine1."Closed Project No." := "Job No.";
        GenJnlLine1."Closed Service Order No." := "Service Order No.";
        GenJnlLine1."Closed Service Contract No." := "Service Contract No.";
        GenJnlLine1."Origin Type" := GenJnlLine1."Origin Type"::Project;

        DimMgt.GetDimValueRec(2,"Shortcut Dimension 2 Code",DimValRec,TRUE,'');

        GenJnlLine1."Account No." := ProjectType.GetComplWipAcc(DimValRec."Cost Type",'','');
        GenJnlLine1."Bal. Account No." := ProjectType.GetComplWIPCoverAcc(DimValRec."Cost Type",'','');

        GenJnlPostLine.RunWithCheck(GenJnlLine1);
      END;
    END;

    PROCEDURE PostComplementaryWIPServ@1100485003(lvGenJnlLine@1100485011 : Record 81);
    VAR
      GenJnlLine1@1100485006 : Record 81;
    BEGIN
      //**4PS
      IF lvGenJnlLine.Amount = 0 THEN
        EXIT;

      //C040452.sn
      IF ItemJnlLine."Receiving Company" <> '' THEN
        EXIT;
      //C040452.en

      WITH lvGenJnlLine DO BEGIN

        GenJnlLine1.INIT;
        GenJnlLine1."Source Code" := "Source Code";
        GenJnlLine1."Reason Code":= "Reason Code";
        GenJnlLine1."Account Type" := GenJnlLine1."Account Type"::"G/L Account";
        GenJnlLine1."Posting Date" := "Posting Date";
        GenJnlLine1."Document No." := "Document No.";
        GenJnlLine1."System-Created Entry" := TRUE;
        GenJnlLine1."Document Date" := "Document Date";
        GenJnlLine1.Description := Description;
        GenJnlLine1."Description 2" := "Description 2";
        GenJnlLine1.Amount := -1 * Amount;
        GenJnlLine1.VALIDATE(Amount, GenJnlLine1.Amount);
        GenJnlLine1."Shortcut Dimension 1 Code" := "Shortcut Dimension 1 Code";
        GenJnlLine1."Shortcut Dimension 2 Code" := "Shortcut Dimension 2 Code";
        GenJnlLine1."Dimension Set ID" := "Dimension Set ID";
        GenJnlLine1."Cost Component" := "Cost Component";
        GenJnlLine1."Closed Service Contract No." := "Service Contract No.";
        GenJnlLine1."Closed Service Order No." := "Service Order No.";
        GenJnlLine1."Origin Type" := GenJnlLine1."Origin Type"::Service;

        DimMgt.GetDimValueRec(2,"Shortcut Dimension 2 Code",DimValRec,TRUE,'');

        GenJnlLine1."Account No." := ServiceType.GetComplWipAcc(DimValRec."Cost Type",'','');
        GenJnlLine1."Bal. Account No." := ServiceType.GetComplWIPCoverAcc(DimValRec."Cost Type",'','',ServOrderRec);

        GenJnlPostLine.RunWithCheck(GenJnlLine1);
      END;
    END;

    PROCEDURE PostComplWIPSurchargeIC@1100525008(lvAccount@1100485001 : Code[20];lvBalanceAccount@1100525000 : Code[20]);
    BEGIN
      //**4PS
      //starting point is 'normal surcharge' record

      IF SurchargePostingBuffer[1].Amount = 0 THEN
        EXIT;

      CLEAR(ComplWIPPostingBuffer[1]);
      ComplWIPPostingBuffer[1]."G/L Account" := lvAccount;
      ComplWIPPostingBuffer[1]."Bal. Account No." := lvBalanceAccount;
      ComplWIPPostingBuffer[1].Amount := SurchargePostingBuffer[1].Amount;
      ComplWIPPostingBuffer[1].Description := SurchargePostingBuffer[1].Description;
      ComplWIPPostingBuffer[1]."Job No." :=  SurchargePostingBuffer[1]."Job No.";
      ComplWIPPostingBuffer[1]."Service Order No." := SurchargePostingBuffer[1]."Service Order No.";
      ComplWIPPostingBuffer[1]."Service Contract No." := SurchargePostingBuffer[1]."Service Contract No.";
      ComplWIPPostingBuffer[1]."Global Dimension 1 Code" := SurchargePostingBuffer[1]."Global Dimension 1 Code";
      ComplWIPPostingBuffer[1]."Global Dimension 2 Code" := SurchargePostingBuffer[1]."Global Dimension 2 Code";
      ComplWIPPostingBuffer[1]."Dimension Set ID" := SurchargePostingBuffer[1]."Dimension Set ID";
      //M24815 "Gen. Bus. Posting Group" Now used for Posting date
      ComplWIPPostingBuffer[1]."Gen. Bus. Posting Group" := SurchargePostingBuffer[1]."Gen. Bus. Posting Group";
      ComplWIPPostingBuffer[1]."Tax Area Code" := SurchargePostingBuffer[1]."Tax Area Code";

      UpdateComplementaryWIPBuffer;
    END;

    PROCEDURE UpdateComplementaryWIPBuffer@1100485009();
    BEGIN
      //**4PS
      ComplWIPPostingBuffer[2] := ComplWIPPostingBuffer[1];
      IF ComplWIPPostingBuffer[2].FIND THEN BEGIN
        ComplWIPPostingBuffer[2].Amount :=
          ComplWIPPostingBuffer[2].Amount + ComplWIPPostingBuffer[1].Amount;
        ComplWIPPostingBuffer[2].MODIFY;
      END ELSE
        ComplWIPPostingBuffer[1].INSERT;
    END;

    PROCEDURE SetICCompanies@1100528301(SupplyingCompanyIC@1100528300 : Text[250];ReceivingCompanyIC@1100528301 : Text[250]);
    BEGIN
      //**4PS
      SupplyingCompany := SupplyingCompanyIC;
      ReceivingCompany := ReceivingCompanyIC;
    END;

    PROCEDURE PostNSItemTracking@1100528601();
    BEGIN
      //**4PS DP00121
      TempNSTrackingSpecification.SETRANGE("Source Ref. No.", ItemJnlLine."Line No.");
      IF TempNSTrackingSpecification.FINDSET THEN
        REPEAT
          PostNSItemTrackingEntry;
        UNTIL TempNSTrackingSpecification.NEXT = 0;
      TempNSTrackingSpecification.DELETEALL;
    END;

    PROCEDURE PostNSItemTrackingEntry@1100528603();
    VAR
      NSTrackingSpecification@1100528601 : Record 11071901;
      NSItemTrackingEntry@1100528604 : Record 11071902;
      NSItemTrackingEntryRelation@1100528603 : Record 11071903;
      NSItemTrackingRelation@1100528500 : Record 11071905;
      ItemTrackingMgt@1100528600 : Codeunit 6500;
      EntryNo@1100528602 : Integer;
    BEGIN
      //**4PS DP00121
      WITH NSTrackingSpecification DO BEGIN
        LOCKTABLE;
        IF FINDLAST THEN
          EntryNo := "Entry No." + 1
        ELSE
          EntryNo := 1;

        INIT;
        TRANSFERFIELDS(TempNSTrackingSpecification);
        "Entry No." := EntryNo;
        "Quantity Handled (Base)" := TempNSTrackingSpecification."Qty. to Handle (Base)";
        "Qty. to Handle (Base)" := 0;
        INSERT;
      END;

      WITH NSItemTrackingEntry DO BEGIN
        LOCKTABLE;
        IF FINDLAST THEN
          EntryNo := "Entry No." + 1
        ELSE
          EntryNo := 1;

        INIT;
        "Entry No." := EntryNo;
        "Item No." := ItemJnlLine."Item No.";
        "Posting Date" := ItemJnlLine."Posting Date";
        "Entry Type" := ItemJnlLine."Entry Type";
        "Location Code" := ItemJnlLine."Location Code";
        "Document Type" := ItemJnlLine."Document Type";
        "Document No." := ItemJnlLine."Document No.";
        "Document Line No." := ItemJnlLine."Line No.";
        "Variant Code" := ItemJnlLine."Variant Code";
        Open := TRUE;
        "Serial No." := TempNSTrackingSpecification."Serial No.";
        "Lot No." := TempNSTrackingSpecification."Lot No.";
        "Warranty Date Vendor" := TempNSTrackingSpecification."Warranty Date";
        "Warranty Code Vendor" := TempNSTrackingSpecification."Warranty Code Vendor";
        "Warranty Start Date Vendor" := TempNSTrackingSpecification."Warranty Start Date Vendor";
        "Warranty Period Vendor" := TempNSTrackingSpecification."Warranty Period Vendor";
        "Warranty Code Customer" := TempNSTrackingSpecification."Warranty Code Customer";
        "Warranty Start Date Customer" := TempNSTrackingSpecification."Warranty Start Date Customer";
        "Warranty Period Customer" := TempNSTrackingSpecification."Warranty Period Customer";
        "Warranty Date Customer" := TempNSTrackingSpecification."Warranty Date Customer";
        "Good Customs":= TempNSTrackingSpecification."Good Customs";
        "Shipment with T1" := TempNSTrackingSpecification."Shipment with T1";
        "Customs Destination Code" := TempNSTrackingSpecification."Customs Destination Code";
        "Expiration Date" := TempNSTrackingSpecification."Expiration Date";
        Quantity := -TempNSTrackingSpecification."Quantity Handled (Base)";
        "Remaining Quantity" := Quantity;
        Positive := ("Remaining Quantity" > 0);
        "Item Tracking" := ItemTrackingMgt.ItemTrackingOption("Lot No.","Serial No.");
        "Source Type":= "Source Type"::" ";
        "Project No." := ItemJnlLine."Job No.";
        "Service Order No." := ItemJnlLine."Service Order No.";

        //apply before insert cause applying data is added to NSItemTrackingEntry
        Item.GET("Item No.");
        NSItemTrackingEntriesApply.ApplyNSItemTrackingEntry(NSItemTrackingEntry, Item."Item Tracking Code");
        INSERT;
      END;

      WITH NSItemTrackingEntryRelation DO BEGIN
        INIT;
        "Item Tracking Entry No." := EntryNo;
        "Serial No." := TempNSTrackingSpecification."Serial No.";
        "Lot No." := TempNSTrackingSpecification."Lot No.";
        TransferFieldsItemJnlLine(ItemJnlLine);
        INSERT;
      END;

      WITH NSItemTrackingRelation DO BEGIN
        "Item Tracking Entry No." := EntryNo;
        "Source RowId" := ItemJnlLine.RowID1;
        "Project Ledger Entry No." := JobLedgEntryNo;
        "Service Ledger Entry No." := ServLedgEntryNo;
        INSERT;
      END;
    END;

    LOCAL PROCEDURE PostICInventoryTransfer@1100528308();
    BEGIN
      //**4PS #C001722
      IF ItemJnlLine.IsInventoryICLine THEN BEGIN
        WriteICInventoryLine(ItemJnlLine);
        ICInventoryPostingSuccessful := TRUE;
      END;
    END;

    PROCEDURE WriteICInventoryLine@1100528303(ItemJnlLine@1100528300 : Record 83);
    VAR
      ICInventoryLine@1100528301 : Record 11020692;
    BEGIN
      //**4PS
      WITH ItemJnlLine DO BEGIN
        IF NOT IsInventoryICLine THEN
          EXIT;

        ICInventoryLine.INIT;
        ICInventoryLine.Type := ICInventoryLine.Type::Transfer;
        ICInventoryLine."Supplying Company" := COMPANYNAME;
        ICInventoryLine."Receiving Company" := "Receiving Company";
        ICInventoryLine."Line No." := 0;
        ICInventoryLine."Entry Type" := "Entry Type";
        ICInventoryLine."Document Date" := "Document Date";
        ICInventoryLine."Posting Date" := "Posting Date";
        ICInventoryLine."Item No." := "Item No.";
        ICInventoryLine.Quantity := Quantity;
        ICInventoryLine."Unit of Measure" := "Unit of Measure Code";
        ICInventoryLine."Unit Amount" := "Unit Amount";
        ICInventoryLine."Unit Cost" := "Unit Cost";
        ICInventoryLine."Location Code" := "Ship To Location";
        ICInventoryLine."Delivery Date" := "Delivery Date";
        ICInventoryLine."Project No." := "Job No.";
        ICInventoryLine."Service Order No." := "Service Order No.";
        ICInventoryLine.INSERT(TRUE);
      END;
    END;

    LOCAL PROCEDURE NSItemTrackingApplicable@1100525003(ItemJournalLine@1100525000 : Record 83) : Boolean;
    VAR
      Item@1100525001 : Record 27;
    BEGIN
      //**4PS
      IF (ItemJournalLine."Job No." = '') AND (ItemJournalLine."Service Order No." = '') THEN
        EXIT(FALSE);

      IF NOT Item.GET(ItemJournalLine."Item No.") THEN
        EXIT(FALSE);

      IF Item."Item Tracking Code" = '' THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE IsRevaluationOfAmountZero@1100525004(ItemJnlLine@1100525000 : Record 83) : Boolean;
    BEGIN
      //**4PS C021411
      WITH ItemJnlLine DO
        EXIT(
          (Amount = 0) AND
          ("Value Entry Type" = "Value Entry Type"::Revaluation) AND
          ("Unit Cost (Calculated)" <> "Unit Cost (Revalued)"));
    END;

    LOCAL PROCEDURE PostSubadmin@1100525005();
    BEGIN
      //**4PS C025794
      WITH ItemJnlLine DO BEGIN
        IF "Receiving Company" = '' THEN BEGIN
          IF NOT (("Entry Type" = "Entry Type"::Transfer) AND (Amount = 0)) THEN BEGIN
            JobLedgEntryNo := 0;
            ServLedgEntryNo := 0;
            IF "Job No." <> '' THEN BEGIN
              PostProjectInventory;
              PostJobEntry;
              PostSurcharge(0);
            END;
            IF "Service Order No." <> '' THEN BEGIN
              PostServiceEntry;
              PostSurcharge(1);
            END;
            IF "Plant Type" <> '' THEN
              PostPlantEntry();
          END;
          PostNSItemTracking;
        END;

        IF ("Receiving Company" <> '') THEN BEGIN
          IF "Job No." <> '' THEN BEGIN
            PostSurchargeIC(0);
            PostProjectInventory;
          END;
          IF "Service Order No." <> '' THEN
            PostSurchargeIC(1);
          WriteICInventoryLine(ItemJnlLine);
        END;
      END;
    END;

    LOCAL PROCEDURE PreparePostingForProjectAndService@1100528900(VAR ItemJournalLine@1100528900 : Record 83);
    VAR
      IntercompanyRelation@1100528903 : Record 11012057;
      lvDim1@1100528902 : Code[20];
      lvDim2@1100528901 : Code[20];
    BEGIN
      //**4PS  C059363
      WITH ItemJournalLine DO BEGIN
        //First Recalc item cost to make correct cost posting for project or service
        InvtSetup.GET;
        IF USENEWPROCESSFORITEMCOST THEN  BEGIN
          IF InvtSetup."Automatic Cost Adjustment" <>
             InvtSetup."Automatic Cost Adjustment"::Never
          THEN BEGIN
            InvtAdjmt.SetProperties(TRUE,InvtSetup."Automatic Cost Posting");
            InvtAdjmt.MakeMultiLevelAdjmt;
          END;
        END;

        //**Process should start after check on ItemRegNo which is signal for mainprocess
        //  if anything has been processed; Also used to check if processed lines can be deleted.
        //  Next process generates it's own registry.
        FINDSET;
        REPEAT
          IF ("Job No." <> '') OR ("Service Order No." <> '') OR ("Plant Type" <> '') OR ("Delivery Account No." <> '') THEN BEGIN
            IF NOT (("Entry Type" = "Entry Type"::Transfer) AND (Amount = 0)) THEN BEGIN
              IF USENEWPROCESSFORITEMCOST THEN  BEGIN
                //**First Recalc Item Journal Cost Field
                //  In Case of FIFO of LIFO Costprice the Item Jnl cost price must be recalculated.
                //  This can be done by reading the Value Entries
                IF ItemRec.GET("Item No.") THEN BEGIN
                  IF (ItemRec."Costing Method" = ItemRec."Costing Method"::FIFO) OR
                     (ItemRec."Costing Method" = ItemRec."Costing Method"::LIFO) OR
                     (ItemRec."Costing Method" = ItemRec."Costing Method"::Specific) OR  //C059363
                     (ItemRec."Costing Method" = ItemRec."Costing Method"::Average) THEN
                  BEGIN
                    IF (InventSetupRec."Item Sales Price for Proj/Serv" = FALSE) AND
                       ("Entry Type" = "Entry Type"::Sale)  AND
                       (Quantity <> 0) THEN
                    BEGIN
                      ItemEntryNoBuff.RESET;
                      IF ItemEntryNoBuff.GET("Journal Template Name", "Journal Batch Name", "Line No.") THEN BEGIN
                        IF (ItemEntryNoBuff."Last Item Ledger Entry No." <> 0) AND
                           (ItemEntryNoBuff."Document Line No." <= ItemEntryNoBuff."Last Item Ledger Entry No.") THEN
                        BEGIN
                          TotalValuePosted := 0;
                          ValueEntry.RESET;
                          ValueEntry.SETCURRENTKEY("Item No.", "Posting Date", "Item Ledger Entry Type", "Entry Type",
                                                   "Variance Type", "Item Charge No.", "Location Code", "Variant Code");
                          ValueEntry.SETRANGE("Item No.", "Item No.");
                          ValueEntry.SETRANGE("Posting Date", "Posting Date");
                          ValueEntry.SETRANGE("Item Ledger Entry Type", ValueEntry."Item Ledger Entry Type"::Sale);

                          //Determine Document No. from temp table
                          IF ItemEntryNoBuff."Document No." = '' THEN
                            ValueEntry.SETRANGE("Document No.", "Document No.")
                          ELSE
                            ValueEntry.SETRANGE("Document No.", ItemEntryNoBuff."Document No.");
                          ValueEntry.SETRANGE("Location Code", "Location Code");
                          ValueEntry.SETRANGE("Item Ledger Entry No.",ItemEntryNoBuff."Document Line No.",
                                                                     ItemEntryNoBuff."Last Item Ledger Entry No.");
                          IF ValueEntry.FINDSET THEN
                            REPEAT
                              TotalValuePosted := TotalValuePosted - ValueEntry."Cost Posted to G/L"; // Cost Posted .. is a negative amount
                            UNTIL ValueEntry.NEXT = 0;

                          IF ItemJnlLine.Amount <> TotalValuePosted THEN
                            ItemJnlLine.VALIDATE("Unit Cost", TotalValuePosted/ItemJnlLine.Quantity);
                        END;
                      END;

                    END;
                  END;
                END;

                PostSubadmin;
              END;

              //**Due to problem of registration GL-Entries in different processes, next process
              //  had to be executed separately in order not to disturb standard process of
              //  posting inventory to GL. (see also codeunit 22/5802)

              PostWIPtoGLDebit();
              lvDim1 := GenJnlLine."Shortcut Dimension 1 Code";
              lvDim2 := GenJnlLine."Shortcut Dimension 2 Code";
              GenJnlPostLine.RunWithCheck(GenJnlLine);

              PostWIPtoGLCredit();
              IF ("IC Inventory Line Type" > 0) AND (ReceivingCompany <> SupplyingCompany) THEN BEGIN
                IntercompanyRelation.GET(ReceivingCompany, SupplyingCompany);
                GenJnlLine."Account No." := IntercompanyRelation.GetICAccountOfCurrentCompany;
                GenJnlLine.TESTFIELD("Account No.");
              END;
              GenJnlPostLine.RunWithCheck(GenJnlLine);

              IF (NOT (InventSetupRec."Automatic Cost Posting" AND InvtSetup."Combined Cost and WIP Posting")) THEN BEGIN
                GenJnlLine."Job No." := "Job No.";
                GenJnlLine."Service Order No." := "Service Order No.";
                GenJnlLine.GetServiceCategory;
                GenJnlLine."Service Contract No." := "Service Contract No.";

                GenJnlLine."Shortcut Dimension 1 Code" := lvDim1;
                GenJnlLine."Shortcut Dimension 2 Code" := lvDim2;
                DimMgt.ValidateShortcutDimValues(1 ,lvDim1, GenJnlLine."Dimension Set ID");
                DimMgt.ValidateShortcutDimValues(2 ,lvDim2, GenJnlLine."Dimension Set ID");

                IF ("Job No." <> '') AND (ProjectType."Post Complementary Costs") THEN
                  PostComplementaryWIPProj(GenJnlLine);

                IF ("Service Order No." <> '') AND (ServiceType."Post Complementary Costs") THEN
                  PostComplementaryWIPServ(GenJnlLine);

              END;
            END;
          END ELSE
            PostICInventoryTransfer;
        UNTIL NEXT = 0;

        PostSurchargeBuffer;
        PostComplWIPBuffer;

        IF ("IC Inventory Line Type" >= "IC Inventory Line Type"::Delivery) THEN
          ItemRegNo := 1;
        IF StdCostUpdated AND (ItemRegNo = 0) THEN
          ItemRegNo := 1;
      END;
    END;

    BEGIN
    {
      4PS05 HBK 10-08-2009 Added Transfer Objectfields to JobJnlLine
      4PS06 JD 19 mei 2010 Call 19454  JobJnlLine."Quantity (Base)" must be filled
      4PS06 HBK 06-05-2010 PostProjectInventory changed: added transfer Element

      Itero 160316 Correction "Item.Costing Method"::Average
    }
    END.
  }
}

