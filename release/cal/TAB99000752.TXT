OBJECT Table 99000752 Shop Calendar Working Days
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Shop Calendar Code;
    OnInsert=BEGIN
               CheckRedundancy;
             END;

    OnRename=BEGIN
               CheckRedundancy;
             END;

    CaptionML=[DEU=Betriebskalenderarbeitstage;
               ENU=Shop Calendar Working Days;
               NLD=Productieagendawerkdagen;
               NOR=Prod.kalender - virkedager;
               SVE=Fabrikskalender arbetsdagar];
  }
  FIELDS
  {
    { 1   ;   ;Shop Calendar Code  ;Code10        ;TableRelation="Shop Calendar";
                                                   CaptionML=[DEU=Betriebskalendercode;
                                                              ENU=Shop Calendar Code;
                                                              NLD=Prod.-agenda;
                                                              NOR=Produksjonskalenderkode;
                                                              SVE=Fabrikskalenderkod];
                                                   NotBlank=Yes }
    { 2   ;   ;Day                 ;Option        ;CaptionML=[DEU=Tag;
                                                              ENU=Day;
                                                              NLD=Dag;
                                                              NOR=Dag;
                                                              SVE=Dag];
                                                   OptionCaptionML=[DEU=Montag,Dienstag,Mittwoch,Donnerstag,Freitag,Samstag,Sonntag;
                                                                    ENU=Monday,Tuesday,Wednesday,Thursday,Friday,Saturday,Sunday;
                                                                    NLD=Maandag,Dinsdag,Woensdag,Donderdag,Vrijdag,Zaterdag,Zondag;
                                                                    NOR=Mandag,Tirsdag,Onsdag,Torsdag,Fredag,L›rdag,S›ndag;
                                                                    SVE=M†ndag,Tisdag,Onsdag,Torsdag,Fredag,L”rdag,S”ndag];
                                                   OptionString=Monday,Tuesday,Wednesday,Thursday,Friday,Saturday,Sunday }
    { 3   ;   ;Work Shift Code     ;Code10        ;TableRelation="Work Shift";
                                                   CaptionML=[DEU=Schichtcode;
                                                              ENU=Work Shift Code;
                                                              NLD=Ploeg;
                                                              NOR=Arbeidsskiftkode;
                                                              SVE=Arbetsskiftskod];
                                                   NotBlank=Yes }
    { 4   ;   ;Starting Time       ;Time          ;OnValidate=BEGIN
                                                                IF ("Ending Time" = 0T) OR
                                                                   ("Ending Time" < "Starting Time")
                                                                THEN BEGIN
                                                                  ShopCalendar.SETRANGE("Shop Calendar Code","Shop Calendar Code");
                                                                  ShopCalendar.SETRANGE(Day,Day);
                                                                  ShopCalendar.SETRANGE("Starting Time","Starting Time",235959T);
                                                                  IF ShopCalendar.FINDFIRST THEN
                                                                    "Ending Time" := ShopCalendar."Starting Time"
                                                                  ELSE
                                                                    "Ending Time" := 235959T;
                                                                END;
                                                                CheckRedundancy;
                                                              END;

                                                   CaptionML=[DEU=Startzeit;
                                                              ENU=Starting Time;
                                                              NLD=Begintijd;
                                                              NOR=Starttidspunkt;
                                                              SVE=Starttid] }
    { 5   ;   ;Ending Time         ;Time          ;OnValidate=BEGIN
                                                                IF ("Ending Time" < "Starting Time") AND
                                                                   ("Ending Time" <> 000000T)
                                                                THEN
                                                                  ERROR(Text000,FIELDCAPTION("Ending Time"),FIELDCAPTION("Starting Time"));

                                                                CheckRedundancy;
                                                              END;

                                                   CaptionML=[DEU=Endzeit;
                                                              ENU=Ending Time;
                                                              NLD=Eindtijd;
                                                              NOR=Sluttidspunkt;
                                                              SVE=Sluttid] }
  }
  KEYS
  {
    {    ;Shop Calendar Code,Day,Starting Time,Ending Time,Work Shift Code;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=%1 muss gr”áer als %2 sein.;ENU=%1 must be higher than %2.;NLD=%1 moet groter dan %2 zijn.;NOR=%1 m† v‘re st›rre enn %2.;SVE=%1 m†ste vara h”gre „n %2.';
      Text001@1001 : TextConst 'DEU=Es gibt Redundanzen im Betriebskalender. Aktuelle Schichten %1 von: %2 bis %3. Kollidiert mit %4 von: %5 bis %6.;ENU=There is redundancy in the Shop Calendar. Actual work shift %1 from : %2 to %3. Conflicting work shift %4 from : %5 to %6.;NLD=Redundantie in de productieagenda. Huidige ploeg %1: van %2 tot %3. Conflict met ploeg %4: van %5 tot %6.;NOR=Det finnes en overfl›dighet i produksjonskalenderen. Faktisk arbeidsskift %1 fra: %2 til %3. Motstridende arb.skift %4 fra: %5 til %6.;SVE=Det finns redundans i verkstadskalender. Aktuellt arbetsskift %1 fr†n : %2 till %3. Arbetsskift i konflikt %4 fr†n : %5 till %6.';
      ShopCalendar@1002 : Record 99000752;

    LOCAL PROCEDURE CheckRedundancy@1();
    VAR
      ShopCalendar2@1000 : Record 99000752;
      TempShopCalendar@1001 : TEMPORARY Record 99000752;
    BEGIN
      ShopCalendar2.SETRANGE("Shop Calendar Code","Shop Calendar Code");
      ShopCalendar2.SETRANGE(Day,Day);
      IF ShopCalendar2.FIND('-') THEN
        REPEAT
          TempShopCalendar := ShopCalendar2;
          TempShopCalendar.INSERT;
        UNTIL ShopCalendar2.NEXT = 0;

      TempShopCalendar := xRec;
      IF TempShopCalendar.DELETE THEN ;

      TempShopCalendar.SETRANGE("Shop Calendar Code","Shop Calendar Code");
      TempShopCalendar.SETRANGE(Day,Day);
      TempShopCalendar.SETRANGE("Starting Time",0T,"Ending Time" - 1);
      TempShopCalendar.SETRANGE("Ending Time","Starting Time" + 1,235959T);

      IF TempShopCalendar.FINDFIRST THEN BEGIN
        IF (TempShopCalendar."Starting Time" = "Starting Time") AND
           (TempShopCalendar."Ending Time" = "Ending Time") AND
           (TempShopCalendar."Work Shift Code" = "Work Shift Code")
        THEN
          EXIT;

        ERROR(
          Text001,
          "Work Shift Code",
          "Starting Time",
          "Ending Time",
          TempShopCalendar."Work Shift Code",
          TempShopCalendar."Starting Time",
          TempShopCalendar."Ending Time");
      END;
    END;

    BEGIN
    END.
  }
}

