OBJECT Codeunit 5465 Graph Mgt - General Tools
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    Permissions=TableData 112=rimd,
                TableData 114=rimd,
                TableData 122=rimd;
    OnRun=BEGIN
            ApiSetup;
          END;

  }
  CODE
  {
    VAR
      CannotChangeIDErr@1002 : TextConst '@@@={Locked};ENU=Value of Id is immutable.;NOR=Value of Id is immutable.';
      CannotChangeLastDateTimeModifiedErr@1001 : TextConst '@@@={Locked};ENU=Value of LastDateTimeModified is immutable.;NOR=Value of LastDateTimeModified is immutable.';
      MissingFieldValueErr@1000 : TextConst '@@@={Locked};ENU=%1 must be specified.;NOR=%1 must be specified.';
      AggregateErrorTxt@1006 : TextConst '@@@={LOCKED};ENU=AL APIAggregate;NOR=AL APIAggregate';
      AggregateIsMissingMainRecordTxt@1004 : TextConst '@@@={LOCKED};ENU=Aggregate does not have main record.;NOR=Aggregate does not have main record.';

    PROCEDURE GetMandatoryStringPropertyFromJObject@51(VAR JsonObject@1000 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";PropertyName@1003 : Text;VAR PropertyValue@1004 : Text);
    VAR
      JSONManagement@1001 : Codeunit 5459;
      Found@1002 : Boolean;
    BEGIN
      Found := JSONManagement.GetStringPropertyValueFromJObjectByName(JsonObject,PropertyName,PropertyValue);
      IF NOT Found THEN
        ERROR(MissingFieldValueErr,PropertyName);
    END;

    [External]
    PROCEDURE UpdateIntegrationRecords@3(VAR SourceRecordRef@1000 : RecordRef;FieldNumber@1008 : Integer;OnlyRecordsWithoutID@1009 : Boolean);
    VAR
      IntegrationRecord@1007 : Record 5151;
      UpdatedIntegrationRecord@1006 : Record 5151;
      IntegrationManagement@1005 : Codeunit 5150;
      FilterFieldRef@1010 : FieldRef;
      IDFieldRef@1003 : FieldRef;
      NullGuid@1004 : GUID;
    BEGIN
      IF OnlyRecordsWithoutID THEN BEGIN
        FilterFieldRef := SourceRecordRef.FIELD(FieldNumber);
        FilterFieldRef.SETRANGE(NullGuid);
      END;

      IF SourceRecordRef.FINDSET THEN
        REPEAT
          IDFieldRef := SourceRecordRef.FIELD(FieldNumber);
          IF NOT IntegrationRecord.GET(IDFieldRef.VALUE) THEN BEGIN
            IntegrationManagement.InsertUpdateIntegrationRecord(SourceRecordRef,CURRENTDATETIME);
            IF ISNULLGUID(FORMAT(IDFieldRef.VALUE)) THEN BEGIN
              UpdatedIntegrationRecord.SETRANGE("Record ID",SourceRecordRef.RECORDID);
              UpdatedIntegrationRecord.FINDFIRST;
              IDFieldRef.VALUE := IntegrationManagement.GetIdWithoutBrackets(UpdatedIntegrationRecord."Integration ID");
            END;

            SourceRecordRef.MODIFY(FALSE);
          END;
        UNTIL SourceRecordRef.NEXT = 0;
    END;

    [External]
    PROCEDURE HandleUpdateReferencedIdFieldOnItem@19(VAR RecRef@1000 : RecordRef;NewId@1001 : GUID;VAR Handled@1002 : Boolean;DatabaseNumber@1006 : Integer;RecordFieldNumber@1007 : Integer);
    VAR
      IdFieldRef@1003 : FieldRef;
    BEGIN
      IF Handled THEN
        EXIT;

      IF RecRef.NUMBER <> DatabaseNumber THEN
        EXIT;

      IdFieldRef := RecRef.FIELD(RecordFieldNumber);
      IdFieldRef.VALUE(NewId);

      Handled := TRUE;
    END;

    [External]
    PROCEDURE HandleGetPredefinedIdValue@21(VAR Id@1000 : GUID;VAR RecRef@1001 : RecordRef;VAR Handled@1002 : Boolean;DatabaseNumber@1007 : Integer;RecordFieldNumber@1008 : Integer);
    VAR
      IntegrationRecord@1006 : Record 5151;
      IdFieldRef@1003 : FieldRef;
      FieldValue@1005 : GUID;
    BEGIN
      IF Handled THEN
        EXIT;

      IF RecRef.NUMBER <> DatabaseNumber THEN
        EXIT;

      IdFieldRef := RecRef.FIELD(RecordFieldNumber);
      FieldValue := FORMAT(IdFieldRef.VALUE);

      IF ISNULLGUID(FieldValue) THEN
        EXIT;

      EVALUATE(Id,FieldValue);
      IF IntegrationRecord.GET(Id) THEN BEGIN
        CLEAR(Id);
        EXIT;
      END;

      Handled := TRUE;
    END;

    PROCEDURE InsertOrUpdateODataType@17(NewKey@1001 : Code[50];NewDescription@1002 : Text[250];OdmDefinition@1004 : Text);
    VAR
      ODataEdmType@1000 : Record 2000000179;
      ODataOutStream@1003 : OutStream;
      RecordExist@1005 : Boolean;
    BEGIN
      IF NOT ODataEdmType.WRITEPERMISSION THEN
        EXIT;

      RecordExist := ODataEdmType.GET(NewKey);

      IF NOT RecordExist THEN BEGIN
        CLEAR(ODataEdmType);
        ODataEdmType.Key := NewKey;
      END;

      ODataEdmType.VALIDATE(Description,NewDescription);
      ODataEdmType."Edm Xml".CREATEOUTSTREAM(ODataOutStream,TEXTENCODING::UTF8);
      ODataOutStream.WRITETEXT(OdmDefinition);

      IF RecordExist THEN
        ODataEdmType.MODIFY(TRUE)
      ELSE
        ODataEdmType.INSERT(TRUE);
    END;

    [Internal]
    PROCEDURE ProcessNewRecordFromAPI@5(VAR InsertedRecordRef@1000 : RecordRef;VAR TempFieldSet@1003 : Record 2000000041;ModifiedDateTime@1002 : DateTime);
    VAR
      IntegrationManagement@1004 : Codeunit 5150;
      ConfigTemplateManagement@1005 : Codeunit 8612;
      UpdatedRecRef@1001 : RecordRef;
    BEGIN
      IF ConfigTemplateManagement.ApplyTemplate(InsertedRecordRef,TempFieldSet,UpdatedRecRef) THEN
        InsertedRecordRef := UpdatedRecRef.DUPLICATE;

      IntegrationManagement.InsertUpdateIntegrationRecord(InsertedRecordRef,ModifiedDateTime);
    END;

    [External]
    PROCEDURE ErrorIdImmutable@2();
    BEGIN
      ERROR(CannotChangeIDErr);
    END;

    [External]
    PROCEDURE ErrorLastDateTimeModifiedImmutable@4();
    BEGIN
      ERROR(CannotChangeLastDateTimeModifiedErr);
    END;

    [Integration]
    PROCEDURE ApiSetup@1();
    BEGIN
    END;

    [External]
    PROCEDURE IsApiEnabled@34() : Boolean;
    VAR
      ServerConfigSettingHandler@1000 : Codeunit 6723;
      Handled@1001 : Boolean;
      IsAPIEnabled@1002 : Boolean;
    BEGIN
      OnGetIsAPIEnabled(Handled,IsAPIEnabled);
      IF Handled THEN
        EXIT(IsAPIEnabled);

      EXIT(ServerConfigSettingHandler.GetApiServicesEnabled);
    END;

    [External]
    PROCEDURE IsApiSubscriptionEnabled@15() : Boolean;
    VAR
      ServerConfigSettingHandler@1000 : Codeunit 6723;
      Handled@1001 : Boolean;
      APISubscriptionsEnabled@1003 : Boolean;
    BEGIN
      IF NOT IsApiEnabled THEN
        EXIT(FALSE);

      OnGetAPISubscriptionsEnabled(Handled,APISubscriptionsEnabled);
      IF Handled THEN
        EXIT(APISubscriptionsEnabled);

      EXIT(ServerConfigSettingHandler.GetApiSubscriptionsEnabled);
    END;

    [External]
    PROCEDURE APISetupIfEnabled@11();
    BEGIN
      IF IsApiEnabled THEN
        ApiSetup;
    END;

    [EventSubscriber(Codeunit,5150,OnGetIntegrationActivated)]
    LOCAL PROCEDURE OnGetIntegrationActivated@12(VAR IsSyncEnabled@1000 : Boolean);
    VAR
      ApiWebService@1001 : Record 2000000193;
      ODataEdmType@1003 : Record 2000000179;
      ForceIsApiEnabledVerification@1002 : Boolean;
    BEGIN
      OnForceIsApiEnabledVerification(ForceIsApiEnabledVerification);

      IF NOT ForceIsApiEnabledVerification AND IsSyncEnabled THEN
        EXIT;

      IF ForceIsApiEnabledVerification THEN
        IF NOT IsApiEnabled THEN
          EXIT;

      IF NOT ApiWebService.READPERMISSION THEN
        EXIT;

      ApiWebService.SETRANGE("Object Type",ApiWebService."Object Type"::Page);
      ApiWebService.SETRANGE(Published,TRUE);
      IF ApiWebService.ISEMPTY THEN
        EXIT;
      IF NOT ODataEdmType.READPERMISSION THEN
        EXIT;

      IsSyncEnabled := NOT ODataEdmType.ISEMPTY;
    END;

    [External]
    PROCEDURE TranslateNAVCurrencyCodeToCurrencyCode@8(VAR CachedLCYCurrencyCode@1001 : Code[10];CurrencyCode@1000 : Code[10]) : Code[10];
    VAR
      GeneralLedgerSetup@1002 : Record 98;
    BEGIN
      IF CurrencyCode <> '' THEN
        EXIT(CurrencyCode);

      IF CachedLCYCurrencyCode = '' THEN BEGIN
        GeneralLedgerSetup.GET;
        CachedLCYCurrencyCode := GeneralLedgerSetup."LCY Code";
      END;

      EXIT(CachedLCYCurrencyCode);
    END;

    [External]
    PROCEDURE TranslateCurrencyCodeToNAVCurrencyCode@7(VAR CachedLCYCurrencyCode@1000 : Code[10];CurrentCurrencyCode@1001 : Code[10]) : Code[10];
    VAR
      GeneralLedgerSetup@1002 : Record 98;
    BEGIN
      IF CurrentCurrencyCode = '' THEN
        EXIT('');

      // Update LCY cache
      IF CachedLCYCurrencyCode = '' THEN BEGIN
        GeneralLedgerSetup.GET;
        CachedLCYCurrencyCode := GeneralLedgerSetup."LCY Code";
      END;

      IF CachedLCYCurrencyCode = CurrentCurrencyCode THEN
        EXIT('');

      EXIT(CurrentCurrencyCode);
    END;

    [EventSubscriber(Codeunit,2,OnCompanyInitialize,"",Skip,Skip)]
    LOCAL PROCEDURE InitDemoCompanyApisForSaaS@10();
    VAR
      CompanyInformation@1001 : Record 79;
      APIEntitiesSetup@1003 : Record 5466;
      PermissionManager@1000 : Codeunit 9002;
      GraphMgtGeneralTools@1002 : Codeunit 5465;
    BEGIN
      IF NOT PermissionManager.SoftwareAsAService THEN
        EXIT;

      IF NOT CompanyInformation.GET THEN
        EXIT;

      IF NOT CompanyInformation."Demo Company" THEN
        EXIT;

      APIEntitiesSetup.SafeGet;

      IF APIEntitiesSetup."Demo Company API Initialized" THEN
        EXIT;

      GraphMgtGeneralTools.ApiSetup;

      APIEntitiesSetup.SafeGet;
      APIEntitiesSetup.VALIDATE("Demo Company API Initialized",TRUE);
      APIEntitiesSetup.MODIFY(TRUE);
    END;

    [External]
    PROCEDURE TransferRelatedRecordIntegrationIDs@9(VAR OriginalRecordRef@1000 : RecordRef;VAR UpdatedRecordRef@1001 : RecordRef;VAR TempRelatedRecodIdsField@1002 : Record 2000000041);
    VAR
      OriginalFieldRef@1003 : FieldRef;
      UpdatedFieldRef@1004 : FieldRef;
    BEGIN
      // We cannot use GETTABLE to set values back e.g. RecRef.GETTABLE(Customer) since it will interupt Insert/Modify transaction.
      // The Insert and Modify triggers will not run. We can assign the fields via FieldRef.
      IF NOT TempRelatedRecodIdsField.FINDFIRST THEN
        EXIT;

      REPEAT
        OriginalFieldRef := OriginalRecordRef.FIELD(TempRelatedRecodIdsField."No.");
        UpdatedFieldRef := UpdatedRecordRef.FIELD(TempRelatedRecodIdsField."No.");
        OriginalFieldRef.VALUE := UpdatedFieldRef.VALUE;
      UNTIL TempRelatedRecodIdsField.NEXT = 0;
    END;

    [External]
    PROCEDURE CleanAggregateWithoutParent@53(MainRecordVariant@1000 : Variant);
    VAR
      MainRecordRef@1001 : RecordRef;
    BEGIN
      MainRecordRef.GETTABLE(MainRecordVariant);
      MainRecordRef.DELETE;
      SENDTRACETAG(
        '00001QW',AggregateErrorTxt,VERBOSITY::Error,AggregateIsMissingMainRecordTxt,DATACLASSIFICATION::SystemMetadata);
    END;

    [Integration]
    LOCAL PROCEDURE OnGetIsAPIEnabled@6(VAR Handled@1000 : Boolean;VAR IsAPIEnabled@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnGetAPISubscriptionsEnabled@14(VAR Handled@1000 : Boolean;VAR APISubscriptionsEnabled@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnForceIsApiEnabledVerification@13(VAR ForceIsApiEnabledVerification@1000 : Boolean);
    BEGIN
    END;

    [External]
    PROCEDURE StripBrackets@16(StringWithBrackets@1000 : Text) : Text;
    BEGIN
      IF STRPOS(StringWithBrackets,'{') = 1 THEN
        EXIT(COPYSTR(FORMAT(StringWithBrackets),2,36));
      EXIT(StringWithBrackets);
    END;

    BEGIN
    END.
  }
}

