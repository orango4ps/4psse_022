OBJECT Codeunit 1485 Rolecenter Selector Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      ActionContainerXmlElementLbl@1000 : TextConst '@@@={Locked};ENU=ActionContainers;NOR=ActionContainers';
      ActionContainerTypeAttrLbl@1001 : TextConst '@@@={Locked};ENU=ActionContainerType;NOR=ActionContainerType';
      ActivityButtonsLbl@1002 : TextConst '@@@={Locked};ENU=ActivityButtons;NOR=ActivityButtons';
      JsonNameElementLbl@1003 : TextConst '@@@={Locked};ENU=name;NOR=name';
      TooltipAttrLbl@1004 : TextConst '@@@={Locked};ENU=ToolTipML;NOR=ToolTipML';
      CaptionAttrLbl@1005 : TextConst '@@@={Locked};ENU=CaptionML;NOR=CaptionML';
      JsonRowElementLbl@1007 : TextConst '@@@={Locked};ENU=rows;NOR=rows';
      JsonTooltipLbl@1008 : TextConst '@@@={Locked};ENU=tooltip;NOR=tooltip';
      PromotedAttrLbl@1009 : TextConst '@@@={Locked};ENU=Promoted;NOR=Promoted';
      ActionCaptionTxt@1011 : TextConst 'ENU=Continue;NOR=Fortsett;SVE=Forts„tt';
      ActionDescriptionTxt@1010 : TextConst 'ENU=Choose a Role Center that suits your business role, and then choose the Continue button. You can change the Role Center any time.;NOR=Velg et rollesenter som passer din bedriftsrolle, og velg deretter Fortsett. Du kan endre rollesenteret n†r som helst.';
      DropdownLbl@1006 : TextConst 'ENU=My Role Center;NOR=Mitt rollesenter';
      JsonHeaderLbl@1012 : TextConst '@@@={Locked};ENU=HeaderLabel;NOR=HeaderLabel';
      JsonDropdownContentLbl@1013 : TextConst '@@@={Locked};ENU=DropContent;NOR=DropContent';
      JsonProfileNameLbl@1014 : TextConst '@@@={Locked};ENU=Name;NOR=Name';
      JsonProfileDescriptionLbl@1015 : TextConst '@@@={Locked};ENU=Description;NOR=Description';
      JsonRolecenterIdLbl@1016 : TextConst '@@@={Locked};ENU=RolecenterId;NOR=RolecenterId';
      JsonDefaultActionLbl@1017 : TextConst '@@@={Locked};ENU=DefaultActionLabel;NOR=DefaultActionLabel';
      JsonDisclaimerTextLbl@1018 : TextConst '@@@={Locked};ENU=DisclaimerText;NOR=DisclaimerText';
      JsonActionDescriptionLbl@1019 : TextConst '@@@={Locked};ENU=ActionDescription;NOR=ActionDescription';
      DisclaimerTxt@1020 : TextConst 'ENU=Note that this view merely shows you the breadth of functionality in the selected Role Center. What you will actually see depends on your company and personal UI settings.;NOR=Legg merke til at denne visningen bare viser omfanget av funksjonaliteten i det valgte rollesenteret. Det du faktisk ser, avhenger av firmaet og dine personlige innstillinger i brukergrensesnittet.';
      DefaultLangaugeCodeTxt@1021 : TextConst '@@@={Locked};ENU=ENU;NOR=ENU';
      LanguageCodeRegExPatternTxt@1023 : TextConst '@@@={Locked};ENU="[A-Z]{3}=";NOR="[A-Z]{3}="';
      LanguageCodePatternTxt@1024 : TextConst '@@@={Locked};ENU="%1=";NOR="%1="';
      RoleCenterSelectorAddinTxt@1022 : TextConst '@@@={Locked};ENU=Microsoft.Dynamics.Nav.Client.RoleCenterSelector;NOR=Microsoft.Dynamics.Nav.Client.RoleCenterSelector';
      RoleCenterSelectorPublickKeyTxt@1025 : TextConst '@@@={Locked};ENU=31bf3856ad364e35;NOR=31bf3856ad364e35';

    [Internal]
    PROCEDURE BuildJsonFromPageMetadata@14(RolecenterId@1000 : Integer) : Text;
    VAR
      TempBlob@1021 : Record 99008535;
      ObjectMetadata@1019 : Record 2000000071;
      JSONManagement@1017 : Codeunit 5459;
      XMLDOMManagement@1016 : Codeunit 6224;
      ReturnXmlDocument@1014 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      ReturnedXMLNodeList@1013 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeList";
      ActivityButtonsXmlNode@1011 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      BucketXmlNode@1010 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      FeatureXmlNode@1009 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      FeatureBucketsJArray@1008 : DotNet "'Newtonsoft.Json, Version=9.0.0.0, Culture=neutral, PublicKeyToken=30ad4fe6b2a6aeed'.Newtonsoft.Json.Linq.JArray";
      FeatureBucketJObject@1007 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
      FeatureJArray@1006 : DotNet "'Newtonsoft.Json, Version=9.0.0.0, Culture=neutral, PublicKeyToken=30ad4fe6b2a6aeed'.Newtonsoft.Json.Linq.JArray";
      FeatureJObject@1005 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
      Instream@1004 : InStream;
      Outstream@1003 : OutStream;
      Tooltip@1025 : Text;
      Caption@1026 : Text;
    BEGIN
      JSONManagement.InitializeEmptyCollection;
      JSONManagement.GetJsonArray(FeatureBucketsJArray);

      IF NOT ObjectMetadata.GET(ObjectMetadata."Object Type"::Page,RolecenterId) THEN
        EXIT(FeatureBucketsJArray.ToString);

      ObjectMetadata.CALCFIELDS(Metadata);
      TempBlob.Blob.CREATEOUTSTREAM(Outstream);
      ObjectMetadata.Metadata.CREATEINSTREAM(Instream);

      XMLDOMManagement.LoadXMLDocumentFromInStream(Instream,ReturnXmlDocument);
      ReturnedXMLNodeList := ReturnXmlDocument.GetElementsByTagName(ActionContainerXmlElementLbl);

      IF NOT GetActivityButtonsActionContainerXmlNode(ActivityButtonsXmlNode,ReturnedXMLNodeList) THEN
        EXIT(FeatureBucketsJArray.ToString);

      FOREACH BucketXmlNode IN ActivityButtonsXmlNode.ChildNodes DO BEGIN
        JSONManagement.InitializeEmptyObject;
        JSONManagement.GetJSONObject(FeatureBucketJObject);
        GetLanguageSpecificCaptionAndTooltip(BucketXmlNode,Caption,Tooltip);
        JSONManagement.AddJPropertyToJObject(FeatureBucketJObject,JsonNameElementLbl,Caption);
        JSONManagement.AddJPropertyToJObject(FeatureBucketJObject,JsonTooltipLbl,Tooltip);

        FeatureJArray := FeatureJArray.JArray;
        FOREACH FeatureXmlNode IN BucketXmlNode.ChildNodes DO
          IF IsNodePromoted(FeatureXmlNode) THEN BEGIN
            FeatureJObject := FeatureJObject.JObject;
            GetLanguageSpecificCaptionAndTooltip(FeatureXmlNode,Caption,Tooltip);
            JSONManagement.AddJPropertyToJObject(FeatureJObject,JsonNameElementLbl,Caption);
            JSONManagement.AddJPropertyToJObject(FeatureJObject,JsonTooltipLbl,Tooltip);
            JSONManagement.AddJObjectToJArray(FeatureJArray,FeatureJObject);
          END;

        IF FeatureJArray.Count > 0 THEN BEGIN
          JSONManagement.AddJArrayToJObject(FeatureBucketJObject,JsonRowElementLbl,FeatureJArray);
          JSONManagement.AddJObjectToJArray(FeatureBucketsJArray,FeatureBucketJObject);
        END;
      END;

      EXIT(FeatureBucketsJArray.ToString);
    END;

    [External]
    PROCEDURE BuildJsonFromPageActionTable@13(RolecenterId@1000 : Integer) : Text;
    VAR
      PageAction@1001 : Record 2000000143;
      BucketPageAction@1002 : Record 2000000143;
      FeaturePageAction@1012 : Record 2000000143;
      JSONManagement@1017 : Codeunit 5459;
      FeatureBucketsJArray@1008 : DotNet "'Newtonsoft.Json, Version=9.0.0.0, Culture=neutral, PublicKeyToken=30ad4fe6b2a6aeed'.Newtonsoft.Json.Linq.JArray";
      FeatureBucketJObject@1007 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
      FeatureJArray@1006 : DotNet "'Newtonsoft.Json, Version=9.0.0.0, Culture=neutral, PublicKeyToken=30ad4fe6b2a6aeed'.Newtonsoft.Json.Linq.JArray";
      FeatureJObject@1005 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
    BEGIN
      JSONManagement.InitializeEmptyCollection;
      JSONManagement.GetJsonArray(FeatureBucketsJArray);

      PageAction.SETRANGE("Page ID",RolecenterId);
      PageAction.SETRANGE("Action Type",PageAction."Action Type"::ActionContainer);
      PageAction.SETRANGE("Action Subtype",PageAction."Action Subtype"::ActivityButtons);
      IF NOT PageAction.FINDFIRST THEN
        EXIT(FeatureBucketsJArray.ToString);

      BucketPageAction.SETRANGE("Page ID",RolecenterId);
      BucketPageAction.SETRANGE("Parent Action ID",PageAction."Action ID");
      BucketPageAction.SETRANGE("Action Type",BucketPageAction."Action Type"::ActionGroup);
      BucketPageAction.SETRANGE(Indentation,1);
      IF BucketPageAction.FINDSET THEN
        REPEAT
          JSONManagement.InitializeEmptyObject;
          JSONManagement.GetJSONObject(FeatureBucketJObject);
          JSONManagement.AddJPropertyToJObject(FeatureBucketJObject,JsonNameElementLbl,BucketPageAction.Caption);
          JSONManagement.AddJPropertyToJObject(FeatureBucketJObject,JsonTooltipLbl,
            BucketPageAction.ToolTip1 + BucketPageAction.ToolTip2 + BucketPageAction.ToolTip3 + BucketPageAction.ToolTip4);

          FeatureJArray := FeatureJArray.JArray;
          FeaturePageAction.SETRANGE("Page ID",RolecenterId);
          FeaturePageAction.SETRANGE("Parent Action ID",BucketPageAction."Action ID");
          FeaturePageAction.SETRANGE("Action Type",FeaturePageAction."Action Type"::Action);
          FeaturePageAction.SETRANGE(Indentation,2);
          FeaturePageAction.SETRANGE(Promoted,TRUE);

          IF FeaturePageAction.FINDSET THEN
            REPEAT
              FeatureJObject := FeatureJObject.JObject;
              JSONManagement.AddJPropertyToJObject(FeatureJObject,JsonNameElementLbl,FeaturePageAction.Caption);
              JSONManagement.AddJPropertyToJObject(FeatureJObject,JsonTooltipLbl,
                FeaturePageAction.ToolTip1 + FeaturePageAction.ToolTip2 + FeaturePageAction.ToolTip3 + FeaturePageAction.ToolTip4);
              JSONManagement.AddJObjectToJArray(FeatureJArray,FeatureJObject);
            UNTIL FeaturePageAction.NEXT = 0;

          IF FeatureJArray.Count > 0 THEN BEGIN
            JSONManagement.AddJArrayToJObject(FeatureBucketJObject,JsonRowElementLbl,FeatureJArray);
            JSONManagement.AddJObjectToJArray(FeatureBucketsJArray,FeatureBucketJObject);
          END;
        UNTIL BucketPageAction.NEXT = 0;

      EXIT(FeatureBucketsJArray.ToString);
    END;

    [External]
    PROCEDURE BuildPageDataJsonForRolecenterSelector@3() : Text;
    VAR
      AllProfile@1000 : Record 2000000178;
      JSONManagement@1017 : Codeunit 5459;
      PageDataJObject@1007 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
      ProfileJArray@1006 : DotNet "'Newtonsoft.Json, Version=9.0.0.0, Culture=neutral, PublicKeyToken=30ad4fe6b2a6aeed'.Newtonsoft.Json.Linq.JArray";
      ProfileJObject@1005 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
    BEGIN
      JSONManagement.InitializeEmptyObject;
      JSONManagement.GetJSONObject(PageDataJObject);
      JSONManagement.AddJPropertyToJObject(PageDataJObject,JsonHeaderLbl,DropdownLbl);
      JSONManagement.AddJPropertyToJObject(PageDataJObject,JsonDefaultActionLbl,ActionCaptionTxt);
      JSONManagement.AddJPropertyToJObject(PageDataJObject,JsonDisclaimerTextLbl,DisclaimerTxt);
      JSONManagement.AddJPropertyToJObject(PageDataJObject,JsonActionDescriptionLbl,ActionDescriptionTxt);

      JSONManagement.InitializeEmptyCollection;
      JSONManagement.GetJsonArray(ProfileJArray);

      IF FindSaaSProfiles(AllProfile) THEN
        REPEAT
          JSONManagement.InitializeEmptyObject;
          JSONManagement.GetJSONObject(ProfileJObject);
          JSONManagement.AddJPropertyToJObject(ProfileJObject,JsonProfileNameLbl,FORMAT(AllProfile.RECORDID));
          JSONManagement.AddJPropertyToJObject(ProfileJObject,JsonProfileDescriptionLbl,AllProfile.Description);
          JSONManagement.AddJPropertyToJObject(ProfileJObject,JsonRolecenterIdLbl,AllProfile."Role Center ID");
          JSONManagement.AddJObjectToJArray(ProfileJArray,ProfileJObject);
        UNTIL AllProfile.NEXT = 0;

      JSONManagement.AddJArrayToJObject(PageDataJObject,JsonDropdownContentLbl,ProfileJArray);

      EXIT(PageDataJObject.ToString);
    END;

    LOCAL PROCEDURE FindSaaSProfiles@11(VAR AllProfile@1000 : Record 2000000178) : Boolean;
    VAR
      ConfPersonalizationMgt@1001 : Codeunit 9170;
    BEGIN
      ConfPersonalizationMgt.HideSandboxProfiles(AllProfile);
      EXIT(AllProfile.FINDSET);
    END;

    LOCAL PROCEDURE IsRolecenterSelectorInstalled@5() : Boolean;
    VAR
      Addin@1000 : Record 2000000069;
    BEGIN
      Addin.SETRANGE("Add-in Name",RoleCenterSelectorAddinTxt);
      Addin.SETRANGE("Public Key Token",RoleCenterSelectorPublickKeyTxt);
      Addin.SETRANGE(Category,Addin.Category::"JavaScript Control Add-in");

      IF Addin.FINDFIRST THEN
        IF Addin.Resource.HASVALUE THEN
          EXIT(TRUE);
    END;

    PROCEDURE IsRolecenterSelectorEnabled@1(UserName@1000 : Code[50]) : Boolean;
    BEGIN
      IF NOT IsRolecenterSelectorInstalled THEN
        EXIT(FALSE);

      EXIT(GetShowStateFromUserPreference(UserName));
    END;

    LOCAL PROCEDURE GetActivityButtonsActionContainerXmlNode@8(VAR ActivityButtonsXmlNode@1000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";ActionContainerXmlNodeList@1002 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeList") : Boolean;
    VAR
      XMLDOMManagement@1001 : Codeunit 6224;
    BEGIN
      IF ISNULL(ActionContainerXmlNodeList) THEN
        EXIT(FALSE);

      FOREACH ActivityButtonsXmlNode IN ActionContainerXmlNodeList DO
        IF XMLDOMManagement.GetAttributeValue(ActivityButtonsXmlNode,ActionContainerTypeAttrLbl) = ActivityButtonsLbl THEN
          EXIT(TRUE);

      EXIT(FALSE);
    END;

    LOCAL PROCEDURE IsNodePromoted@7(FeatureXmlNode@1000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode") : Boolean;
    VAR
      XMLDOMManagement@1003 : Codeunit 6224;
    BEGIN
      EXIT(XMLDOMManagement.GetAttributeValue(FeatureXmlNode,PromotedAttrLbl) = '1');
    END;

    LOCAL PROCEDURE GetLanguageSpecificCaptionAndTooltip@10(XmlNode@1000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";VAR Caption@1001 : Text;VAR Tooltip@1002 : Text);
    VAR
      XMLDOMManagement@1003 : Codeunit 6224;
    BEGIN
      Caption := '';
      Tooltip := '';

      Caption := GetLanguageSpecificText(GLOBALLANGUAGE,XMLDOMManagement.GetAttributeValue(XmlNode,CaptionAttrLbl));
      Tooltip := GetLanguageSpecificText(GLOBALLANGUAGE,XMLDOMManagement.GetAttributeValue(XmlNode,TooltipAttrLbl));
      IF Tooltip = '' THEN
        Tooltip := Caption;
    END;

    LOCAL PROCEDURE GetLanguageSpecificText@4(LanguageID@1000 : Integer;InputMLText@1001 : Text) ReturnText : Text;
    VAR
      Language@1002 : Record 8;
      RegEx@1008 : DotNet "'System, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.RegularExpressions.Regex";
      RegExMatch@1009 : DotNet "'System, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.RegularExpressions.Match";
      RegExMatchs@1010 : DotNet "'System, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.RegularExpressions.MatchCollection";
      Dictionary@1011 : DotNet "'mscorlib'.System.Collections.Generic.Dictionary`2";
      PrevMatch@1012 : DotNet "'System, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.RegularExpressions.Match";
      HtmlUtility@1004 : DotNet "'System.Web, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b03f5f7f11d50a3a'.System.Web.HttpUtility";
      LanguageCode@1003 : Text;
    BEGIN
      LanguageCode := DefaultLangaugeCodeTxt;

      Language.SETRANGE("Windows Language ID",LanguageID);
      IF Language.FINDFIRST THEN
        LanguageCode := Language.Code;

      Dictionary := Dictionary.Dictionary;
      RegExMatchs := RegEx.Matches(InputMLText,STRSUBSTNO(LanguageCodeRegExPatternTxt));

      FOREACH RegExMatch IN RegExMatchs DO BEGIN
        IF NOT ISNULL(PrevMatch) THEN
          Dictionary.Add(
            PrevMatch.Value,
            COPYSTR(InputMLText,PrevMatch.Index + PrevMatch.Length + 1,RegExMatch.Index - (PrevMatch.Index + PrevMatch.Length) - 1));
        PrevMatch := RegExMatch;
      END;

      IF NOT ISNULL(PrevMatch) THEN
        Dictionary.Add(
          PrevMatch.Value,
          COPYSTR(InputMLText,PrevMatch.Index + PrevMatch.Length + 1,STRLEN(InputMLText) - (PrevMatch.Index + PrevMatch.Length)));

      IF NOT Dictionary.TryGetValue(STRSUBSTNO(LanguageCodePatternTxt,LanguageCode),ReturnText) THEN
        IF LanguageCode <> DefaultLangaugeCodeTxt THEN
          IF NOT Dictionary.TryGetValue(STRSUBSTNO(LanguageCodePatternTxt,DefaultLangaugeCodeTxt),ReturnText) THEN
            EXIT;

      ReturnText := HtmlUtility.HtmlDecode(ReturnText);
      IF STRPOS(ReturnText,'"') <> 0 THEN
        ReturnText := COPYSTR(ReturnText,2,STRLEN(ReturnText) - 2);
    END;

    [External]
    PROCEDURE GetShowStateFromUserPreference@2(UserName@1000 : Code[50]) RoleCenterSelectorIsEnabled : Boolean;
    VAR
      UserPreference@1001 : Record 1306;
    BEGIN
      IF NOT UserPreference.GET(UserName,GetUserPreferenceCode) THEN
        EXIT(FALSE);

      UserPreference.CALCFIELDS("User Selection");

      IF NOT EVALUATE(RoleCenterSelectorIsEnabled,UserPreference.GetUserSelectionAsText) THEN
        EXIT(FALSE);
    END;

    [External]
    PROCEDURE SetShowStateFromUserPreference@6(UserName@1000 : Code[50];State@1001 : Boolean);
    VAR
      UserPreference@1002 : Record 1306;
    BEGIN
      IF UserPreference.GET(UserName,GetUserPreferenceCode) THEN BEGIN
        UserPreference.SetUserSelection(State);
        UserPreference.MODIFY;
        EXIT;
      END;

      UserPreference.INIT;
      UserPreference."User ID" := UserName;
      UserPreference."Instruction Code" := GetUserPreferenceCode;
      UserPreference.SetUserSelection(State);
      UserPreference.INSERT;
    END;

    [External]
    PROCEDURE GetUserPreferenceCode@9() : Code[50];
    BEGIN
      EXIT(UPPERCASE('RoleCenterOverviewShowState'));
    END;

    BEGIN
    END.
  }
}

