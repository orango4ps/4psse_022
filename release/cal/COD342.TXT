OBJECT Codeunit 342 Currency CaptionClass Mgmt
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    SingleInstance=Yes;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      GLSetup@1000 : Record 98;
      DefaultTxt@1002 : TextConst 'ENU=LCY;NOR=NOK;SVE=BVA';
      DefaultLongTxt@1001 : TextConst 'ENU=Local Currency;NOR=Lokal valuta;SVE=Lokal valuta';
      GLSetupRead@1003 : Boolean;

    [EventSubscriber(Codeunit,42,OnResolveCaptionClass,"",Skip,Skip)]
    LOCAL PROCEDURE ResolveCaptionClass@2(CaptionArea@1001 : Text;CaptionExpr@1002 : Text;Language@1000 : Integer;VAR Caption@1006 : Text;VAR Resolved@1003 : Boolean);
    BEGIN
      IF CaptionArea = '101' THEN BEGIN
        Caption := CurCaptionClassTranslate(CaptionExpr);
        Resolved := TRUE;
      END;
    END;

    LOCAL PROCEDURE CurCaptionClassTranslate@7(CaptionExpr@1001 : Text) : Text;
    VAR
      Currency@1002 : Record 4;
      CurrencyResult@1004 : Text[30];
      CommaPosition@1005 : Integer;
      CurCaptionType@1006 : Text[30];
      CurCaptionRef@1007 : Text;
    BEGIN
      // CurCaptionType
      // <DataType>   := [String]
      // <DataValue>  :=
      // '0' -> Currency Result := Local Currency Code
      // '1' -> Currency Result := Local Currency Description
      // '2' -> Currency Result := Additional Reporting Currency Code
      // '3' -> Currency Result := Additional Reporting Currency Description

      // CurCaptionRef
      // <DataType>   := [SubString]
      // <DataValue>  := [String]
      // This string is the actual string making up the Caption.
      // It will contain a '%1', and the Currency Result will substitute for it.

      CommaPosition := STRPOS(CaptionExpr,',');
      IF CommaPosition > 0 THEN BEGIN
        CurCaptionType := COPYSTR(CaptionExpr,1,CommaPosition - 1);
        CurCaptionRef := COPYSTR(CaptionExpr,CommaPosition + 1);
        IF NOT GLSetupRead THEN BEGIN
          IF NOT GLSetup.GET THEN
            EXIT(CurCaptionRef);
          GLSetupRead := TRUE;
        END;
        CASE CurCaptionType OF
          '0','1':
            BEGIN
              IF GLSetup."LCY Code" = '' THEN
                IF CurCaptionType = '0' THEN
                  CurrencyResult := DefaultTxt
                ELSE
                  CurrencyResult := DefaultLongTxt
              ELSE
                IF NOT Currency.GET(GLSetup."LCY Code") THEN
                  CurrencyResult := GLSetup."LCY Code"
                ELSE
                  IF CurCaptionType = '0' THEN
                    CurrencyResult := Currency.Code
                  ELSE
                    CurrencyResult := Currency.Description;
              EXIT(COPYSTR(STRSUBSTNO(CurCaptionRef,CurrencyResult),1,MAXSTRLEN(CurCaptionRef)));
            END;
          '2','3':
            BEGIN
              IF GLSetup."Additional Reporting Currency" = '' THEN
                EXIT(CurCaptionRef);
              IF NOT Currency.GET(GLSetup."Additional Reporting Currency") THEN
                CurrencyResult := GLSetup."Additional Reporting Currency"
              ELSE
                IF CurCaptionType = '2' THEN
                  CurrencyResult := Currency.Code
                ELSE
                  CurrencyResult := Currency.Description;
              EXIT(COPYSTR(STRSUBSTNO(CurCaptionRef,CurrencyResult),1,MAXSTRLEN(CurCaptionRef)));
            END;
          ELSE
            EXIT(CurCaptionRef);
        END;
      END;
      EXIT(CaptionExpr);
    END;

    BEGIN
    END.
  }
}

