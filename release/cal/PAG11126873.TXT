OBJECT Page 11126873 Transfer Estimate-Service RAW
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=öbertragen Kalkulation in Service;
               ENU=Transfer Estimate to Service;
               NLD=Overzetten calculatie naar service;
               NOR=Overfõr kalkyle til service;
               SVE=ôverfîr kalkyl till service];
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table11012151;
    PageType=NavigatePage;
    OnOpenPage=BEGIN
                 //**4PS Generated Code. Do Not Change.sn01
                 DPA_IsHardFilter := DPA_SetFilters(CurrPage.OBJECTID(FALSE));
                 //**4PS Generated Code.en01
                 InitSelection;  //db, 31-01-17
                 CheckTransferDestination;

                 EstRec.GET("No.");

                 WizardStep := 1;
                 ShowStep(TRUE);
               END;

    OnNewRecord=BEGIN
                  //**4PS Generated Code. Do Not Change.sn02
                  DPA_SetDefaultDepartment;
                  //**4PS Generated Code.en02
                END;

    OnInsertRecord=VAR
                     xRecRef@1100000999 : RecordRef;
                   BEGIN
                     //**4PS Generated Code. Do Not Change.sn05
                     xRecRef.GETTABLE(xRec);
                     DPA_SetDefaultDpmtFromNoSeries(xRecRef);
                     //**4PS Generated Code.en05
                   END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100528603;1 ;Action    ;
                      Name=Back;
                      CaptionML=[DEU=< &ZurÅck;
                                 ENU=< &Back;
                                 NLD=< &Vorige;
                                 NOR=< &Tilbake;
                                 SVE=< &Tillbaka];
                      Enabled=BackEnable;
                      InFooterBar=Yes;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 ShowStep(FALSE);
                                 WizardStep -= 1;
                                 ShowStep(TRUE);
                                 CurrPage.UPDATE(TRUE);
                               END;
                                }
      { 1100528602;1 ;Action    ;
                      Name=Next;
                      CaptionML=[DEU=&Weiter >;
                                 ENU=&Next >;
                                 NLD=Vo&lgende >;
                                 NOR=&Neste >;
                                 SVE=&NÑsta >];
                      Enabled=NextEnable;
                      InFooterBar=Yes;
                      Image=NextRecord;
                      OnAction=BEGIN
                                 //CheckStatus;
                                 ShowStep(FALSE);
                                 WizardStep += 1;
                                 ShowStep(TRUE);
                                 CurrPage.UPDATE(TRUE);
                               END;
                                }
      { 1100528601;1 ;Action    ;
                      Name=Finish;
                      CaptionML=[DEU=B&eenden;
                                 ENU=&Finish;
                                 NLD=Voltooie&n;
                                 NOR=&Avslutt;
                                 SVE=&Slutfîr];
                      Enabled=FinishEnable;
                      InFooterBar=Yes;
                      Image=Completed;
                      OnAction=BEGIN
                                 //CheckStatus;
                                 FinishWizard;
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1210190022;1;Group  ;
                Name=Step1;
                CaptionML=[DEU=öbertragen von;
                           ENU=Transfer from;
                           NLD=Overzetten van;
                           NOR=Overfõring fra;
                           SVE=ôverfîring frÜn];
                Visible=Step1Visible;
                GroupType=Group;
                InstructionalTextML=[NOR=Velg data som skal kopieres;
                                     SVE=VÑlj de data som ska kopieras] }

    { 1100528503;2;Field  ;
                CaptionML=[DEU=öbertragen von;
                           ENU=Transfer from;
                           NLD=Overzetten van];
                Style=Strong;
                StyleExpr=TRUE }

    { 1   ;2   ;Field     ;
                CaptionML=[DEU=Kalkulation;
                           ENU=Estimate;
                           NLD=Calculatie;
                           NOR=Kalkyle;
                           SVE=Kalkyl];
                SourceExpr="No.";
                Editable=FALSE }

    { 1100485005;2;Field  ;
                CaptionML=[DEU=Baubeschreibungsposten;
                           ENU=Tender Paragraph;
                           NLD=Bestekpost;
                           NOR=Tilbudsavsnitt;
                           SVE=Anbudsstycke];
                SourceExpr=ParagraphFilter;
                OnValidate=BEGIN
                             ValidatePart;
                           END;

                OnLookup=BEGIN
                           LookupPart;
                         END;
                          }

    { 1100525006;2;Field  ;
                CaptionML=[DEU=Status;
                           ENU=Status;
                           NLD=Status;
                           NOR=Status;
                           SVE=Status];
                SourceExpr=StatusFilter;
                OnValidate=BEGIN
                             IF NOT StatusRec.GET(StatusFilter) THEN
                               StatusTxt := ''
                             ELSE
                               StatusTxt := StatusRec.Description;
                           END;

                OnLookup=BEGIN
                           IF PAGE.RUNMODAL(PAGE::"Line Statuses",StatusRec) = ACTION::LookupOK THEN BEGIN
                              StatusFilter := StatusRec.Status;
                              StatusTxt := StatusRec.Description;
                           END;
                           CurrPage.UPDATE(TRUE);
                         END;
                          }

    { 1100525007;2;Field  ;
                CaptionML=[DEU=Status Beschreibung;
                           ENU=Status Description;
                           NLD=Status omschrijving;
                           NOR=Statusbeskrivelse;
                           SVE=Statusbeskrivning];
                SourceExpr=StatusTxt;
                Importance=Additional;
                Editable=FALSE }

    { 1100528600;1;Group  ;
                Name=Step2;
                CaptionML=[DEU=öbertragen in;
                           ENU=Transfer To;
                           NLD=Overzetten naar;
                           NOR=Overfõr til;
                           SVE=ôverfîr till];
                Visible=Step2Visible;
                GroupType=Group;
                InstructionalTextML=[NOR=Velg destinasjon for kopiert kalkyle;
                                     SVE=VÑlj destination fîr kopierad kalkyl] }

    { 1100528502;2;Field  ;
                CaptionML=[DEU=öbertragen in;
                           ENU=Transfer to;
                           NLD=Overzetten naar];
                Style=Strong;
                StyleExpr=TRUE }

    { 1100485011;2;Field  ;
                CaptionML=[DEU=Ziel;
                           ENU=Destination;
                           NLD=Doel;
                           NOR=Mottaker;
                           SVE=Destination];
                OptionCaptionML=[DEU=" ,,Serviceauftrag,Modifikation";
                                 ENU=" ,,Service Order,Modification";
                                 NLD=" ,,Serviceorder,Modificatie";
                                 SVE=" ,,Serviceorder,éndring"];
                SourceExpr=TransferDestination;
                Editable=TransferDestinationEditable;
                OnValidate=BEGIN
                             ServiceLocationNo := '';
                             CASE TransferDestination OF
                               TransferDestination::Modification:
                                 ERROR(Text000);
                               TransferDestination::ServOrder:
                                 BEGIN
                                   ServiceOrderNo := "Service Order No.";
                                   ServiceLocationNo := "Service Location No.";
                                 END;
                             END;
                             UpdateEditable;
                           END;
                            }

    { 1100525009;2;Field  ;
                CaptionML=[DEU=Serviceauftragsnr.;
                           ENU=Service Order No.;
                           NLD=Serviceordernr.;
                           NOR=Serviceordrenr.;
                           SVE=Serviceordernr];
                SourceExpr=ServiceOrderNo;
                TableRelation="Service Order";
                Editable=ServiceOrderNoEditable;
                OnValidate=BEGIN
                             EstRec.GET("No.");
                             IF EstRec."Service Order No." <> '' THEN  //C049450
                               EstRec.TESTFIELD("Service Order No.", ServiceOrderNo);  //C043087
                           END;
                            }

    { 1100528604;2;Field  ;
                CaptionML=[DEU=Servicestandort;
                           ENU=Service Location No.;
                           NLD=Servicelocatienr.;
                           NOR=Servicelokasjon Nr.;
                           SVE=Serviceplatsnr];
                SourceExpr=ServiceLocationNo;
                TableRelation="Service Location";
                Editable=ServiceLocationNoEditable;
                OnValidate=BEGIN
                             //C049450.sn
                             EstRec.GET("No.");
                             IF EstRec."Service Location No." <> '' THEN
                               EstRec.TESTFIELD("Service Location No.", ServiceLocationNo);  //C043087
                             IF ServiceLocation.GET(ServiceLocationNo) THEN
                               IF EstRec."Customer No." <> '' THEN
                                 ServiceLocation.TESTFIELD("Customer No.", EstRec."Customer No.");
                             //C049450.en
                           END;
                            }

    { 1100525015;2;Field  ;
                CaptionML=[DEU=Auftragsart;
                           ENU=Order Kind;
                           NLD=Ordersoort;
                           NOR=Type ordre;
                           SVE=Ordertyp];
                SourceExpr=OrderKind;
                Editable=OrderKindEditable;
                OnValidate=BEGIN
                             ValidateOrderIndication(OrderIndRec.Indication::Kind, OrderKind);
                           END;

                OnLookup=BEGIN
                           IF NOT (TransferDestination IN [TransferDestination::ServOrder, TransferDestination::Modification]) THEN EXIT;

                           OrderIndRec.Code := OrderKind;
                           OrderIndRec.SETRANGE(Indication, OrderIndRec.Indication::Kind);
                           IF PAGE.RUNMODAL(0, OrderIndRec) = ACTION::LookupOK THEN BEGIN
                             OrderKind := OrderIndRec.Code;
                           END;
                         END;
                          }

    { 1100485015;2;Field  ;
                CaptionML=[DEU=öbertragen Nullzeilen;
                           ENU=Transfer Zero Lines;
                           NLD=Overzetten nulregels;
                           NOR=Overfõr nullrader;
                           SVE=ôverfîr nollrader];
                SourceExpr=TransferZeroLines;
                Editable=TransferZeroLinesEditable }

    { 1100528500;2;Field  ;
                CaptionML=[DEU=öbertragen Budget nach Vorschlagsauftrag;
                           ENU=Transfer Budget to Advice Order;
                           NLD=Overzetten begroting naar adviesorder];
                SourceExpr=TransferBudgetToAdviceOrder }

    { 1100525004;2;Field  ;
                CaptionML=[DEU=Tarifcode validieren;
                           ENU=Validate Rate Code;
                           NLD=Valideren tariefcode];
                SourceExpr=ValidateRateCode }

    { 1100528501;2;Field  ;
                CaptionML=[DEU=Verrechnungsmethode;
                           ENU=Settlement Method;
                           NLD=Verrekenmethode];
                OptionCaptionML=[DEU=Vertrag,Regie;
                                 ENU=Fixed Price,Cost Plus;
                                 NLD=Aanneming,Regie];
                SourceExpr=SettlementMethod;
                Enabled=FALSE }

    { 1100525000;2;Field  ;
                CaptionML=[DEU=Vertragsbetrag aktualisieren;
                           ENU=Update Order Amount;
                           NLD=Bijwerken aanneemsom;
                           NOR=Oppdater Ordreantall];
                OptionCaptionML=[DEU=" ,,Angebotsgenerierung";
                                 ENU=" ,,TryOut Quote";
                                 NLD=" ,,Trajectofferte"];
                SourceExpr=UpdateOrderAmount;
                Editable=TargetFieldsEditable;
                OnValidate=BEGIN
                             GetOrderAmount;
                           END;
                            }

    { 1100525001;2;Field  ;
                CaptionML=[DEU=Angebotsgenerierung;
                           ENU=Try-out Quote;
                           NLD=Trajectofferte;
                           NOR=Tilbudsforespõrsel;
                           SVE=AnbudsfîrfrÜgan];
                SourceExpr=TryoutQuote;
                Editable=TargetFieldsEditable;
                OnValidate=BEGIN
                             GetOrderAmount;
                           END;

                OnLookup=VAR
                           SelectedQuote@1100409001 : Code[20];
                         BEGIN
                           //db, 22-01-15: C020309
                           SelectedQuote := ShowTryOut(2);
                           IF SelectedQuote <> '' THEN BEGIN
                             TryoutQuote := SelectedQuote;
                             GetOrderAmount;
                           END;
                         END;
                          }

    { 1100525002;2;Field  ;
                CaptionML=[DEU=Vertragspreis;
                           ENU=Order Amount;
                           NLD=Aanneemsom;
                           NOR=Ordrebelõp;
                           SVE=Orderbelopp];
                SourceExpr=OrderAmount;
                Editable=TargetFieldsEditable;
                OnValidate=BEGIN
                             ValidateOrderAmount;  //C049537
                           END;
                            }

    { 1100525003;2;Field  ;
                CaptionML=[DEU=Ratenschema;
                           ENU=Installment Scheme;
                           NLD=Termijnschema;
                           NOR=Betalingsplan;
                           SVE=Betalplan];
                SourceExpr=InstScheme;
                Editable=TargetFieldsEditable;
                OnValidate=BEGIN
                             IF InstScheme <> '' THEN
                               InstSchemeRec.GET(InstScheme);
                           END;

                OnLookup=BEGIN
                           InstSchemeRec.SETRANGE(Type, InstSchemeRec.Type::"Service Order");
                           IF PAGE.RUNMODAL(0, InstSchemeRec) = ACTION::LookupOK THEN BEGIN
                             InstScheme := InstSchemeRec.Code;
                           END;
                         END;
                          }

    { 1100525005;2;Field  ;
                CaptionML=[DEU=Kopieren Fortsetzungsgenerierung;
                           ENU=Copy Try-Out Successor;
                           NLD=Kopiâren vervolgtraject;
                           NOR=Kopier etterfõlger (prospekt);
                           SVE=Kopiera efterfîljare, anbud];
                SourceExpr=CopyTryOut }

  }
  CODE
  {
    VAR
      EstRec@1100525011 : Record 11012151;
      StatusRec@1100525007 : Record 11012158;
      ServiceLocation@1100528501 : Record 11012801;
      OrderIndRec@1100528601 : Record 11012837;
      InstSchemeRec@1100525012 : Record 11012016;
      OrderKind@1100525013 : Code[10];
      ParagraphFilter@1100485002 : Code[250];
      StatusFilter@1100525006 : Code[250];
      StatusTxt@1100525005 : Text[50];
      ElementTxt@1100485003 : Text[50];
      SettlementMethod@1100485000 : 'FixedPrice,CostPlus';
      CopyTryOut@1100485005 : Boolean;
      TransferDestination@1100485007 : ' ,,ServOrder,Modification';
      TransferZeroLines@1100485010 : Boolean;
      TransferBudgetToAdviceOrder@1100528500 : Boolean;
      DPA_IsHardFilter@1100000999 : Boolean;
      CopyFeatures@1100528600 : ' ,TechSpec,CheckList,Both';
      Step1Visible@1100528602 : Boolean INDATASET;
      Step2Visible@1100528603 : Boolean INDATASET;
      WizardStep@1100528604 : Integer;
      FinishEnable@1100528605 : Boolean INDATASET;
      BackEnable@1100528606 : Boolean INDATASET;
      NextEnable@1100528607 : Boolean INDATASET;
      TargetFieldsEditable@1100525000 : Boolean INDATASET;
      OrderKindEditable@1100528608 : Boolean INDATASET;
      TransferZeroLinesEditable@1100528609 : Boolean INDATASET;
      TransferDestinationEditable@1100525010 : Boolean INDATASET;
      ServiceOrderNoEditable@1100525009 : Boolean INDATASET;
      ServiceLocationNoEditable@1100528612 : Boolean INDATASET;
      UpdateOrderAmount@1100525008 : ' ,,TryOutQuote';
      OrderAmount@1100525003 : Decimal;
      InstScheme@1100525002 : Code[20];
      TryoutQuote@1100525001 : Code[20];
      ValidateRateCode@1100525004 : Boolean;
      Text000@1100528610 : TextConst 'DEU=FÅr die Verwendung dieser Funktion muss die Kalkulation konvertiert werden;ENU=Estimate must be converted to use this function;NLD=Calculatie dient geconverteerd te zijn voordat deze functie gebruikt kan worden';
      ServiceLocationNo@1100528611 : Code[20];
      ServiceOrderNo@1100525015 : Code[20];
      ServiceOrder@1100525014 : Record 11012823;

    PROCEDURE ValidatePart@1100528904();
    VAR
      EstimateParagraphRAW@1100528901 : Record 11125679;
    BEGIN
      //DP01585
      EstimateParagraphRAW.SETRANGE("Estimate No.", "No.");
      EstimateParagraphRAW.SETFILTER("Tender Paragraph No.", ParagraphFilter);
      EstimateParagraphRAW.SETRANGE(Title, TRUE);
      IF NOT EstimateParagraphRAW.FINDFIRST THEN EstimateParagraphRAW.INIT;
      ElementTxt := EstimateParagraphRAW.Text;
    END;

    PROCEDURE LookupPart@1100528902();
    VAR
      EstimateParagraphRAW@1100528900 : Record 11125679;
    BEGIN
      //DP01585
      EstimateParagraphRAW.SETRANGE("Estimate No.", "No.");
      EstimateParagraphRAW.SETFILTER("Tender Paragraph No.", ParagraphFilter);
      EstimateParagraphRAW.SETRANGE(Title, TRUE);
      IF EstimateParagraphRAW.FINDFIRST THEN;
      EstimateParagraphRAW.SETRANGE("Tender Paragraph No.");
      IF PAGE.RUNMODAL(PAGE::"Estimate Paragraph List RAW",EstimateParagraphRAW) = ACTION::LookupOK THEN BEGIN
        ParagraphFilter := EstimateParagraphRAW."Tender Paragraph No.";
        ElementTxt := EstimateParagraphRAW.Text;
      END;
      CurrPage.UPDATE(TRUE);
    END;

    PROCEDURE CheckTransferDestination@1100485000();
    VAR
      lvServOrderRec@1100525000 : Record 11012823;
    BEGIN
      IF TransferDestination = TransferDestination::ServOrder THEN BEGIN
        SettlementMethod := SettlementMethod::CostPlus;
        IF lvServOrderRec.GET(GetNumServiceOrder) THEN BEGIN
          OrderKind := lvServOrderRec."Order Kind";
          //C049537.sn
          OrderAmount := lvServOrderRec."Order Amount";
          InstScheme := lvServOrderRec."Installment Scheme";
          ValidateOrderAmount;
          //C049537.en
        END;
      END ELSE BEGIN
        UpdateOrderAmount := UpdateOrderAmount::" ";
        OrderAmount := 0;
        InstScheme := '';
      END;

      UpdateEditable;
    END;

    PROCEDURE ValidateOrderIndication@1100485003(IIndication@1100485000 : Option;ICode@1100485001 : Code[10]);
    BEGIN
      IF (ICode <> '') THEN
        OrderIndRec.GET(IIndication, ICode);
    END;

    LOCAL PROCEDURE ShowStep@2(Visible@1001 : Boolean);
    BEGIN
      CASE WizardStep OF
        1:
          BEGIN
            Step1Visible := Visible;
            IF Visible THEN BEGIN
              BackEnable := FALSE;
              NextEnable := TRUE;
              FinishEnable := FALSE;
            END;
          END;
        2:
          BEGIN
            Step2Visible := Visible;
            IF Visible THEN BEGIN
              BackEnable := TRUE;
              NextEnable := FALSE;
              FinishEnable := TRUE;
            END;
          END;
      END;
    END;

    PROCEDURE FinishWizard@1100525001();
    VAR
      TransferMgt@1100528900 : Codeunit 11125670;
    BEGIN
      IF TransferDestination = TransferDestination::" " THEN
        ERROR('');

      IF OrderAmount = 0 THEN
        SettlementMethod := SettlementMethod::CostPlus;

      CASE TransferDestination OF
        TransferDestination::ServOrder:
          BEGIN
            //DP01585.sn
            TransferMgt.SetTransferOptions(
              TransferZeroLines,
              FALSE,FALSE,FALSE,FALSE,FALSE,FALSE,
              TransferDestination,SettlementMethod,CopyTryOut,CopyFeatures::" ",FALSE);
            TransferMgt.SetEstimateOptions("No.",'',ParagraphFilter,'',StatusFilter);
            TransferMgt.SetServiceOptions(OrderKind,'',UpdateOrderAmount,OrderAmount,InstScheme,
              ServiceLocationNo,ServiceOrderNo,TransferBudgetToAdviceOrder);  //C030978
            TransferMgt.SetValidateRateCode(ValidateRateCode);
            TransferMgt.Start;
            //DP01585.en
          END;
        TransferDestination::Modification:
          BEGIN
            //DP01585.sn
            TransferMgt.SetTransferOptions(
              TransferZeroLines,
              FALSE,FALSE,FALSE,FALSE,FALSE,FALSE,
              TransferDestination,SettlementMethod,CopyTryOut,CopyFeatures::" ",FALSE);
            TransferMgt.SetEstimateOptions("No.",'',ParagraphFilter,'',StatusFilter);
            TransferMgt.SetServiceOptions(OrderKind,'',UpdateOrderAmount::" ",0,'','','',TransferBudgetToAdviceOrder);  //C030978
            TransferMgt.SetValidateRateCode(ValidateRateCode);
            TransferMgt.Start;
            //DP01585.en
          END;
      END;
    END;

    PROCEDURE UpdateEditable@1100525002();
    BEGIN
      //C043087
      EstRec.GET("No.");
      IF EstRec."Service Order No." <> '' THEN BEGIN
        TransferDestinationEditable := FALSE;
        ServiceOrderNoEditable := FALSE;
        ServiceLocationNoEditable := FALSE;
        OrderKindEditable := FALSE;
      END ELSE BEGIN
        TransferDestinationEditable := TRUE;
        ServiceOrderNoEditable := TransferDestination = TransferDestination::ServOrder;
        ServiceLocationNoEditable := TransferDestination = TransferDestination::ServOrder;
        OrderKindEditable := TransferDestination IN [TransferDestination::ServOrder, TransferDestination::Modification];
      END;
      TargetFieldsEditable := TransferDestination = TransferDestination::ServOrder;
      TransferZeroLinesEditable := TransferDestination IN [TransferDestination::ServOrder, TransferDestination::Modification];
    END;

    PROCEDURE GetOrderAmount@1100409000();
    VAR
      TryoutQuoteRec@1100409001 : Record 11012758;
    BEGIN
      IF UpdateOrderAmount = UpdateOrderAmount::TryOutQuote THEN BEGIN
        IF TryoutQuoteRec.GET(TryoutQuote) THEN BEGIN
          SettlementMethod := SettlementMethod::FixedPrice;  //C049537
          OrderAmount := TryoutQuoteRec."Offered Amount (LCY)";
          InstScheme := TryoutQuoteRec."Installment Scheme Service";
        END;
      END;
    END;

    LOCAL PROCEDURE InitSelection@1100528905();
    BEGIN
      //C035508
      //source
      ParagraphFilter := '';
      StatusFilter := '';
      //destination
      OrderKind := '';
      OrderAmount := 0;
      InstScheme := '';
      TryoutQuote := '';
      ServiceLocationNo := '';
      //C043087.sn
      EstRec.GET("No.");
      ServiceOrderNo := EstRec."Service Order No.";
      ServiceLocationNo := EstRec."Service Location No.";
      EstRec.GET("No.");
      IF ServiceOrder.GET(ServiceOrderNo) THEN
        OrderKind := ServiceOrder."Order Kind";
      //C043087.en
    END;

    LOCAL PROCEDURE ValidateOrderAmount@1100528500();
    BEGIN
      //C049537
      IF OrderAmount <> 0 THEN
        SettlementMethod := SettlementMethod::FixedPrice
      ELSE
        SettlementMethod := SettlementMethod::CostPlus;
      IF SettlementMethod = SettlementMethod::CostPlus THEN
        InstScheme := '';
    END;

    BEGIN
    {
      DP01585: copy from page 11020467
    }
    END.
  }
}

