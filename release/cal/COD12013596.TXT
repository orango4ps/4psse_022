OBJECT Codeunit 12013596 ExFlow User Management
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF512000;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      EXF001@1100285000 : TextConst 'ENU=User %1 has documents associated with it and cannot be deleted!;SVE=Anv„ndare %1 har dokument inlagda och kan inte bli bortplockad!';
      EXF002@1100285001 : TextConst 'ENU=Approval group %1 has template rules associated with it and cannot be deleted!;SVE=Attestgrupp %1 har attestregler tilldelade och kan d„rf”r ej bli borttagen! \Vill du se de tilldelade attestreglerna?';
      EXF004@1100285006 : TextConst 'ENU=Do you want unblock user %1?;SVE=Vill du ”ppna anv„ndare %1?';
      EXF005@1100285005 : TextConst 'ENU=Approved because user was blocked.;SVE=Godk„nd eftersom anv„ndaren sp„rrades.';
      EXF006@1100285003 : TextConst 'ENU=Added because of history replacement.;SVE=Tillagt f”r historikers„ttning.';
      EXF007@1100285004 : TextConst 'ENU=Updating approval records   #1###########;SVE=Uppdaterar attestantposter #1###########';
      EXF008@1100285002 : TextConst 'ENU=Updating posted approval records   #1###########;SVE=Uppdaterar bokf”rda attestantposter #1###########';
      EXF009@1100285008 : TextConst 'ENU=User %1 have been assigned to template rules and must be blocked before it can be deleted!;SVE=Anv„ndare %1 anv„nds av attestregler och m†ste vara blockerad innan den kan plockas bort!';
      EXF010@1100285010 : TextConst 'ENU=No user groups for user %1 have been found.;SVE=Inga attestgrupper f”r anv„ndare %1 har hittats.';
      EXF011@1100285007 : TextConst 'ENU=Warning! There are no user group lines, is that correct?;SVE=Varning! D„r finns inga attestgrupprader, „r det korrekt?';
      EXF012@1100285011 : TextConst 'ENU=Starting and Ending date must either BOTH be empty or filled in!;SVE=B†de start och slutdatum m†ste antingen b†da vara blanka eller ifyllda!';
      EXF013@1100285012 : TextConst 'ENU=Starting Date cannot be after Ending Date!;SVE=Startdatum kan inte vara f”re slutdatum!';
      EXF014@1100285013 : TextConst 'ENU=You must have access to all companies in NAV in order to delete an ExFlow user.;SVE=Du m†ste ha beh”righet till samtliga bolag i NAV f”r att kunna radera en ExFlow-anv„ndare.';

    PROCEDURE DeleteUser@1100285000(VAR ExUserCompany@1100285001 : Record 12013641);
    VAR
      ExUserGroupLine@1100285000 : Record 12013607;
      ExUserGroup@1100285004 : Record 12013606;
      ExUserComp2@1100285008 : Record 12013641;
      ExUser@1100285007 : Record 12013640;
      Company@1100285002 : Record 2000000006;
      ExUserFound@1100285009 : Boolean;
    BEGIN
      ExUserFound := FALSE;

      Company.RESET;
      Company.SETFILTER(Name,'<>%1',COMPANYNAME);
      IF Company.FINDSET THEN
        REPEAT
          ExUserComp2.RESET;
          IF ExUserComp2.CHANGECOMPANY(Company.Name) THEN BEGIN
            ExUserComp2.SETRANGE("User ID", ExUserCompany."User ID");
            IF ExUserComp2.FINDFIRST THEN
              ExUserFound := TRUE;
          END ELSE
            ERROR(EXF014);
        UNTIL (Company.NEXT = 0) OR (ExUserFound);

      IF ExUserGroup.GET(ExUserCompany."User ID") THEN BEGIN
          IF ExDocExist(ExUserGroup) THEN
            ERROR(STRSUBSTNO(EXF001,ExUserGroup.Code));
          IF TemplateLineExist(ExUserGroup) THEN BEGIN
            ERROR(STRSUBSTNO(EXF009,ExUserCompany."User ID"));
          END;
          //Delete ExUserGroup + Line.
          ExUserGroupLine.RESET;
          ExUserGroupLine.SETRANGE(Code, ExUserGroup.Code);
          ExUserGroupLine.DELETEALL;

          ExUserGroupLine.RESET;
          ExUserGroupLine.SETRANGE("User ID", ExUserGroup.Code);
          ExUserGroupLine.DELETEALL;

          ExUserGroup.DELETE;
      END;

      IF NOT ExUserFound THEN BEGIN
        ExUser.GET(ExUserCompany."User ID");
        ExUser.DELETE;
      END;
    END;

    PROCEDURE ExDocExist@1100285003(ExUserGroup@1100285001 : Record 12013606) : Boolean;
    VAR
      ExDocLineApp@1100285000 : Record 12013610;
      EXPostedDocLineApp@1100285002 : Record 12013628;
    BEGIN
      //Approver + Approved By
      ExDocLineApp.RESET;
      ExDocLineApp.SETRANGE(Approver, ExUserGroup.Code);
      IF ExDocLineApp.FINDFIRST THEN
        EXIT(TRUE);

      ExDocLineApp.RESET;
      ExDocLineApp.SETRANGE("Approved By", ExUserGroup.Code);
      IF ExDocLineApp.FINDFIRST THEN
        EXIT(TRUE);

      //Posted Approver + Approved by
      EXPostedDocLineApp.RESET;
      EXPostedDocLineApp.SETRANGE(Approver, ExUserGroup.Code);
      IF EXPostedDocLineApp.FINDFIRST THEN
        EXIT(TRUE);

      EXPostedDocLineApp.RESET;
      EXPostedDocLineApp.SETRANGE("Approved By", ExUserGroup.Code);
      IF EXPostedDocLineApp.FINDFIRST THEN
        EXIT(TRUE);
    END;

    PROCEDURE TemplateLineExist@1100285001(ExUserGroup@1100285000 : Record 12013606) : Boolean;
    VAR
      TemplateRuleLine@1100285001 : Record 12013603;
    BEGIN
      TemplateRuleLine.RESET;
      TemplateRuleLine.SETCURRENTKEY("Approver Group");
      TemplateRuleLine.SETRANGE("Approver Group", ExUserGroup.Code);
      EXIT(TemplateRuleLine.FINDFIRST);
    END;

    PROCEDURE DisplayTemplates@1100285013(ExUserGroup@1100285003 : Record 12013606);
    VAR
      TemplateRuleLine@1100285002 : Record 12013603;
      TemplateRule@1100285001 : Record 12013645;
      TempTemplateRule@1100285000 : TEMPORARY Record 12013645;
    BEGIN
      IF NOT CONFIRM(EXF002,TRUE) THEN
        EXIT;

      TemplateRuleLine.RESET;
      TemplateRuleLine.SETCURRENTKEY("Approver Group");
      TemplateRuleLine.SETRANGE("Approver Group", ExUserGroup.Code);
      IF TemplateRuleLine.FINDFIRST THEN BEGIN
        REPEAT
          IF NOT TempTemplateRule.GET(TemplateRuleLine.Code) THEN BEGIN
            TemplateRule.GET(TemplateRuleLine.Code);

            TempTemplateRule.INIT;
            TempTemplateRule.TRANSFERFIELDS(TemplateRule);
            TempTemplateRule.INSERT;
          END;
        UNTIL TemplateRuleLine.NEXT = 0;

        PAGE.RUN(PAGE::"Template Rule List",TempTemplateRule);
      END;
    END;

    PROCEDURE BlockUser@1100285004(ExUserComp@1100285002 : Record 12013641);
    VAR
      UserComp@1100285001 : Record 12013641;
      BlockUser@1100285000 : Page 12013627;
      NewUser@1100285003 : Code[50];
      ReplaceHistory@1100285004 : Boolean;
      ReplaceTempCodeRules@1100285007 : Boolean;
    BEGIN
      IF ExUserComp.Blocked THEN BEGIN
        IF NOT CONFIRM(STRSUBSTNO(EXF004,ExUserComp."User ID"),TRUE) THEN
          EXIT;

        ExUserComp.Blocked := FALSE;
        ExUserComp.MODIFY;
        UnblockUser(ExUserComp."User ID");
      END ELSE BEGIN
        CLEAR(BlockUser);
        UserComp.RESET;
        UserComp.SETRANGE("User ID", ExUserComp."User ID");

        BlockUser.LOOKUPMODE(TRUE);
        BlockUser.SETTABLEVIEW(UserComp);
        IF BlockUser.RUNMODAL = ACTION::LookupOK THEN BEGIN
          NewUser := BlockUser.GetNewUser;
          ReplaceHistory := BlockUser.GetReplaceHistory;
          ReplaceTempCodeRules := BlockUser.GetReplaceTempCodeRules;

          ReplaceBlockedUser(ExUserComp."User ID", NewUser, ReplaceHistory, ReplaceTempCodeRules);
        END;
      END;
    END;

    PROCEDURE UnblockUser@1100285007(UserID@1100285000 : Code[50]);
    VAR
      UserGroup@1100285003 : Record 12013606;
    BEGIN
      IF NOT UserGroup.GET(UserID) THEN
        EXIT;

      UserGroup.Blocked := FALSE;
      UserGroup."Exclude from web list" := FALSE;
      UserGroup.MODIFY;
    END;

    PROCEDURE ReplaceBlockedUser@1100285002(OldUser@1100285004 : Code[50];NewUser@1100285003 : Code[50];ReplaceHistory@1100285011 : Boolean;ReplaceTempCodeRules@1100285017 : Boolean);
    VAR
      UserComp@1100285002 : Record 12013641;
      NewUserGroup@1100285001 : Record 12013606;
      OldUserGroup@1100285005 : Record 12013606;
      ExDocument@1100285008 : Record 12013608;
      ExDocument2@1100285009 : Record 12013608;
    BEGIN
      IF NOT OldUserGroup.GET(OldUser) THEN
        ERROR(EXF010, OldUser);

      IF NOT NewUserGroup.GET(NewUser) THEN
        ERROR(EXF010, NewUser);

      UserComp.GET(OldUser);
      UserComp.Blocked := TRUE;
      UserComp."Replaced with User" := NewUser;
      UserComp.MODIFY;

      OldUserGroup.Blocked := TRUE;
      OldUserGroup."Exclude from web list" := TRUE;
      OldUserGroup.MODIFY;

      IF ReplaceTempCodeRules THEN BEGIN
        ReplaceTemplateRules(OldUser,NewUser);
        ReplaceRequisitionRules(OldUser,NewUser);
        ReplaceCodingRules(OldUser,NewUser);
        ReplaceDimOwners(OldUser,NewUser);
        ReplaceFirstApproverInEXReference(OldUser,NewUser);
      END;

      ReplaceExDoc(ReplaceHistory,OldUserGroup,NewUserGroup);
      ReplacePostedExDoc(ReplaceHistory,OldUserGroup,NewUserGroup);

      // Add new receiving person
      ExDocument.RESET;
      ExDocument.SETCURRENTKEY(Initiator);
      ExDocument.SETFILTER("Document Type", '%1|%2|%3', ExDocument."Document Type"::Quote,
                                                        ExDocument."Document Type"::Order,
                                                        ExDocument."Document Type"::Requisition);
      ExDocument.SETRANGE(Initiator, OldUser);
      IF ExDocument.FINDSET(TRUE) THEN
        REPEAT
          ExDocument2.GET(ExDocument.ID);
          ExDocument2.Initiator := NewUser;
          ExDocument2.MODIFY;
        UNTIL ExDocument.NEXT = 0;

      ExDocument.RESET;
      ExDocument.SETCURRENTKEY("Assigned Receipt Group");
      ExDocument.SETFILTER("Document Type", '%1|%2|%3', ExDocument."Document Type"::Quote,
                                                        ExDocument."Document Type"::Order,
                                                        ExDocument."Document Type"::Requisition);
      ExDocument.SETRANGE("Assigned Receipt Group", OldUser);
      IF ExDocument.FINDSET(TRUE) THEN
        REPEAT
          ExDocument2.GET(ExDocument.ID);
          ExDocument2."Assigned Receipt Group" := NewUser;
          ExDocument2.MODIFY;
        UNTIL ExDocument.NEXT = 0;

      ReplacerEscalator(OldUser,NewUser);
    END;

    PROCEDURE ReplaceTemplateRules@1100285010(OldUser@1100285001 : Code[50];NewUser@1100285000 : Code[50]);
    VAR
      TemplateRule@1100285003 : Record 12013645;
      TemplateRule2@1100285006 : Record 12013645;
      TemplateRuleLine@1100285002 : Record 12013603;
      TemplateRuleLine2@1100285004 : Record 12013603;
      TempTemplateRuleLine@1100285008 : TEMPORARY Record 12013603;
    BEGIN
      IF OldUser = NewUser THEN
        EXIT;

      TemplateRuleLine.RESET;
      TemplateRuleLine.SETCURRENTKEY("Approver Group");
      TemplateRuleLine.SETRANGE("Approver Group", OldUser);
      IF TemplateRuleLine.FINDSET(TRUE) THEN
        REPEAT
          TemplateRuleLine2.GET(TemplateRuleLine.Code,TemplateRuleLine."Line No.");
          TemplateRuleLine2.VALIDATE("Approver Group", NewUser);
          TemplateRuleLine2.MODIFY;
        UNTIL TemplateRuleLine.NEXT = 0;

      TemplateRule.RESET;
      TemplateRule.SETRANGE("First Approver Filter", OldUser);
      IF TemplateRule.FINDSET(TRUE) THEN
        REPEAT
          TemplateRule2.GET(TemplateRule.Code);
          TemplateRule2."First Approver Filter" := NewUser;
          TemplateRule2.MODIFY;
        UNTIL TemplateRule.NEXT = 0;

      TemplateRule.RESET;
      TemplateRule.SETFILTER("First Approver Filter", '%1', '*' + OldUser + '*');
      IF TemplateRule.FINDSET(TRUE) THEN
        REPEAT
          TempTemplateRuleLine.RESET;
          TempTemplateRuleLine.DELETEALL;

          ParseIntoTempTable(TemplateRule.Code,TemplateRule."First Approver Filter",TempTemplateRuleLine);

          TempTemplateRuleLine.SETRANGE("Approver Group", OldUser);
          IF TempTemplateRuleLine.FINDFIRST THEN BEGIN
            TempTemplateRuleLine.MODIFYALL("Approver Group", NewUser);
            TemplateRule2.GET(TemplateRule.Code);

            ParseFromTempTable(TempTemplateRuleLine,TemplateRule2."First Approver Filter");
            TemplateRule2.MODIFY;
          END;
        UNTIL TemplateRule.NEXT = 0;
    END;

    PROCEDURE ReplaceCodingRules@1100285008(OldUser@1100285002 : Code[50];NewUser@1100285001 : Code[50]);
    VAR
      CodingRuleAppGroup@1100285000 : Record 12013664;
      CodingRuleAppGroup2@1100285004 : Record 12013664;
    BEGIN
      IF OldUser = NewUser THEN
        EXIT;

      CodingRuleAppGroup.RESET;
      CodingRuleAppGroup.SETRANGE("User Group Code", OldUser);
      IF CodingRuleAppGroup.FINDSET(TRUE,TRUE) THEN BEGIN
        REPEAT
          CodingRuleAppGroup2.INIT;
          CodingRuleAppGroup2.TRANSFERFIELDS(CodingRuleAppGroup);
          CodingRuleAppGroup2."User Group Code" := NewUser;
          CodingRuleAppGroup2.INSERT;
        UNTIL CodingRuleAppGroup.NEXT = 0;
        CodingRuleAppGroup.DELETEALL;
      END;
    END;

    PROCEDURE ReplaceRequisitionRules@1100285011(OldUser@1100285001 : Code[50];NewUser@1100285000 : Code[50]);
    VAR
      ReqRule@1100285002 : Record 12013621;
      ReqRule2@1100285004 : Record 12013621;
      TempTemplateRuleLine@1100285003 : TEMPORARY Record 12013603;
    BEGIN
      IF OldUser = NewUser THEN
        EXIT;

      ReqRule.RESET;
      ReqRule.SETRANGE("Created by Filter", OldUser);
      IF ReqRule.FINDSET(TRUE) THEN
        REPEAT
          ReqRule2.GET(ReqRule.Code);
          ReqRule2."Created by Filter" := NewUser;
          ReqRule2.MODIFY;
        UNTIL ReqRule.NEXT = 0;

      ReqRule.RESET;
      ReqRule.SETFILTER("Created by Filter", '%1', '*' + OldUser + '*');
      IF ReqRule.FINDSET(TRUE) THEN
        REPEAT
          TempTemplateRuleLine.RESET;
          TempTemplateRuleLine.DELETEALL;

          ParseIntoTempTable(ReqRule.Code,ReqRule."Created by Filter",TempTemplateRuleLine);

          TempTemplateRuleLine.SETRANGE("Approver Group", OldUser);
          IF TempTemplateRuleLine.FINDFIRST THEN BEGIN
            TempTemplateRuleLine.MODIFYALL("Approver Group", NewUser);
            ReqRule2.GET(ReqRule.Code);

            ParseFromTempTable(TempTemplateRuleLine,ReqRule2."Created by Filter");
            ReqRule2.MODIFY;
          END;
        UNTIL ReqRule.NEXT = 0;
    END;

    LOCAL PROCEDURE ReplaceExDoc@1100285014(ReplaceHistory@1100285001 : Boolean;VAR OldUserGroup@1100285000 : Record 12013606;VAR UserGroup@1100285007 : Record 12013606);
    VAR
      ExDocApp@1100285003 : Record 12013610;
      NewExDocApp@1100285002 : Record 12013610;
      TempDocLine@1100285006 : TEMPORARY Record 12013609;
      NewApproverOrder@1100285005 : Integer;
      Window@1100285004 : Dialog;
    BEGIN
      IF GUIALLOWED THEN
        Window.OPEN(EXF007);

      ExDocApp.RESET;
      ExDocApp.SETCURRENTKEY(Approver,"Days to Due Date");
      ExDocApp.SETRANGE(Approver, OldUserGroup.Code);
      IF NOT ReplaceHistory THEN
        ExDocApp.SETFILTER(Flowstatus, '<>%1&<>%2', ExDocApp.Flowstatus::Approved,ExDocApp.Flowstatus::Rejected);

      IF ExDocApp.FINDSET(TRUE,FALSE) THEN BEGIN
        REPEAT
          IF GUIALLOWED THEN
            Window.UPDATE(1,ExDocApp.ID);

          NewExDocApp.RESET;
          NewExDocApp.SETCURRENTKEY("Document Type","Document No.","Line No.","Approver Order");
          NewExDocApp.SETRANGE("Document Type", ExDocApp."Document Type");
          NewExDocApp.SETRANGE("Document No.", ExDocApp."Document No.");
          NewExDocApp.SETRANGE("Line No.", ExDocApp."Line No.");
          NewExDocApp.SETFILTER("Approver Order", '>%1', ExDocApp."Approver Order");
          IF NewExDocApp.FINDFIRST THEN
            NewApproverOrder := ExDocApp."Approver Order" + ROUND((NewExDocApp."Approver Order" - ExDocApp."Approver Order") / 2,1)
          ELSE
            NewApproverOrder := ExDocApp."Approver Order" + 10000;

          NewExDocApp.INIT;
          NewExDocApp.TRANSFERFIELDS(ExDocApp);
          NewExDocApp."Approver Order" := NewApproverOrder;
          NewExDocApp.Approver := UserGroup.Code;
          NewExDocApp.Comment := '';
          NewExDocApp."Approval Date" := CREATEDATETIME(0D,0T);
          NewExDocApp."Approved By" := '';
          NewExDocApp.Flowstatus := NewExDocApp.Flowstatus::"Not processed";
          NewExDocApp.ID := 0;

          IF (ExDocApp.Flowstatus IN [ExDocApp.Flowstatus::Approved, ExDocApp.Flowstatus::Rejected]) THEN BEGIN
            NewExDocApp.Comment := EXF006;
            NewExDocApp."Approval Date" := ExDocApp."Approval Date";
            NewExDocApp."Approved By" := ExDocApp."Approved By";
            NewExDocApp.Flowstatus := ExDocApp.Flowstatus;
            NewExDocApp.INSERT;
          END ELSE
            NewExDocApp.INSERT;

          IF NOT (ExDocApp.Flowstatus IN [ExDocApp.Flowstatus::Approved, ExDocApp.Flowstatus::Rejected]) THEN BEGIN
            ExDocApp."Approval Date" := CREATEDATETIME(TODAY,TIME);
            ExDocApp."Approved By" := ExDocApp.Approver;
            ExDocApp.Comment := EXF005;
            ExDocApp.Flowstatus := ExDocApp.Flowstatus::Approved;
            ExDocApp.MODIFY;
          END;

          IF NOT TempDocLine.GET(ExDocApp."Document Line ID") THEN BEGIN
            IF NOT (ExDocApp."Document Type" IN [ExDocApp."Document Type"::"6",
                                                 ExDocApp."Document Type"::"7"]) THEN BEGIN
              TempDocLine.ID := ExDocApp."Document Line ID";
              TempDocLine.INSERT;
            END;
          END;
        UNTIL ExDocApp.NEXT = 0;

        IF TempDocLine.FINDFIRST THEN
          REPEAT
            ExDocApp.RESET;
            ExDocApp.SETRANGE("Document Line ID", TempDocLine.ID);
            ExDocApp.SETRANGE(Flowstatus, ExDocApp.Flowstatus::Rejected);
            IF NOT ExDocApp.FINDFIRST THEN BEGIN
              ExDocApp.SETFILTER(Flowstatus, '<=%1', ExDocApp.Flowstatus::Current);
              IF ExDocApp.FINDFIRST THEN BEGIN
                ExDocApp.ApprovalStatus(ExDocApp,'Update');
                ExDocApp.MODIFY;
              END;
            END;
          UNTIL TempDocLine.NEXT = 0;
      END;

      IF GUIALLOWED THEN
        Window.CLOSE;
    END;

    LOCAL PROCEDURE ReplacePostedExDoc@1100285018(ReplaceHistory@1100285001 : Boolean;VAR OldUserGroup@1100285000 : Record 12013606;VAR UserGroup@1100285007 : Record 12013606);
    VAR
      PostedExDocApp@1100285003 : Record 12013628;
      PostedNewExDocApp@1100285002 : Record 12013628;
      NewApproverOrder@1100285005 : Integer;
      Window@1100285004 : Dialog;
    BEGIN
      IF NOT ReplaceHistory THEN
        EXIT;

      IF GUIALLOWED THEN
        Window.OPEN(EXF008);

      PostedExDocApp.RESET;
      PostedExDocApp.SETCURRENTKEY(Approver,"Days to Due Date");
      PostedExDocApp.SETRANGE(Approver, OldUserGroup.Code);
      IF PostedExDocApp.FINDSET(TRUE,FALSE) THEN BEGIN
        REPEAT
          IF GUIALLOWED THEN
            Window.UPDATE(1,PostedExDocApp.ID);

          PostedNewExDocApp.RESET;
          PostedNewExDocApp.SETCURRENTKEY("Document Line ID","Approver Order");
          PostedNewExDocApp.SETRANGE("Document Line ID", PostedExDocApp."Document Line ID");
          PostedNewExDocApp.SETFILTER("Approver Order", '>%1', PostedExDocApp."Approver Order");
          IF PostedNewExDocApp.FINDFIRST THEN
            NewApproverOrder := PostedExDocApp."Approver Order" + ROUND((PostedNewExDocApp."Approver Order" - PostedExDocApp."Approver Order") / 2,1)
          ELSE
            NewApproverOrder := PostedExDocApp."Approver Order" + 10000;

          PostedNewExDocApp.INIT;
          PostedNewExDocApp.TRANSFERFIELDS(PostedExDocApp);
          PostedNewExDocApp.ID := 0;

          PostedNewExDocApp."Approver Order" := NewApproverOrder;
          PostedNewExDocApp.Approver := UserGroup.Code;
          PostedNewExDocApp.Comment := EXF006;
          PostedNewExDocApp."Approval Date" := PostedExDocApp."Approval Date";
          PostedNewExDocApp."Approved By" := PostedExDocApp."Approved By";
          PostedNewExDocApp.Flowstatus := PostedExDocApp.Flowstatus;
          PostedNewExDocApp.INSERT;

          IF PostedExDocApp."Approval Date" = 0DT THEN BEGIN
            PostedExDocApp."Approval Date" := CREATEDATETIME(TODAY,TIME);
            PostedExDocApp."Approved By" := PostedExDocApp.Approver;
            PostedExDocApp.Comment := EXF005;
            PostedExDocApp.Flowstatus := PostedExDocApp.Flowstatus::Approved;
            PostedExDocApp.MODIFY;
          END;
        UNTIL PostedExDocApp.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE ReplacerEscalator@1100285017(OldUser@1100285001 : Code[50];NewUser@1100285000 : Code[50]);
    VAR
      EXUserGroup@1100285002 : Record 12013606;
      ExFlowSetup@1100285003 : Record 12013601;
    BEGIN
      EXUserGroup.RESET;
      EXUserGroup.SETRANGE(Manager, OldUser);
      EXUserGroup.MODIFYALL(Manager, NewUser);

      ExFlowSetup.GET;
      IF ExFlowSetup."Senior Manager" = OldUser THEN
        ExFlowSetup."Senior Manager" := NewUser;

      IF ExFlowSetup."System Approver" = OldUser THEN
        ExFlowSetup."System Approver" := NewUser;

      ExFlowSetup.MODIFY;
    END;

    PROCEDURE ParseIntoTempTable@1100285012(Code@1100285006 : Code[20];FilterValue@1100285008 : Text[1024];VAR TempTemplateRuleLine@1100285001 : TEMPORARY Record 12013603);
    VAR
      TempPos@1100285002 : Integer;
      TempCode@1100285003 : Code[20];
      InStr@1100285000 : Text[1024];
      NoMoreString@1100285005 : Boolean;
      TempLineNo@1100285007 : Integer;
    BEGIN
      InStr := FilterValue;
      IF InStr = '' THEN
        EXIT;

      NoMoreString := FALSE;

      REPEAT
        TempPos := STRPOS(InStr,'|');
        IF TempPos <> 0 THEN BEGIN
          TempCode := COPYSTR(InStr,1,TempPos - 1);

          TempTemplateRuleLine.Code := Code;
          TempTemplateRuleLine."Line No." := TempLineNo;
          TempLineNo := TempLineNo + 10000;
          TempTemplateRuleLine."Approver Group" := TempCode;
          TempTemplateRuleLine.INSERT;

          InStr := DELSTR(InStr,1,TempPos);
        END ELSE BEGIN
          IF InStr <> '' THEN BEGIN
            TempCode := InStr;

            TempTemplateRuleLine.Code := Code;
            TempTemplateRuleLine."Line No." := TempLineNo;
            TempLineNo := TempLineNo + 10000;
            TempTemplateRuleLine."Approver Group" := TempCode;
            TempTemplateRuleLine.INSERT;
          END;

          NoMoreString := TRUE;
        END;
      UNTIL NoMoreString;
    END;

    PROCEDURE ParseFromTempTable@1100285009(VAR TempTemplateRuleLine@1100285004 : TEMPORARY Record 12013603;VAR OutStr@1100285001 : Code[1024]);
    BEGIN
      OutStr := '';
      TempTemplateRuleLine.RESET;
      IF TempTemplateRuleLine.FINDFIRST THEN BEGIN
        REPEAT
          OutStr := OutStr + TempTemplateRuleLine."Approver Group" + '|';
        UNTIL TempTemplateRuleLine.NEXT = 0;

        OutStr := COPYSTR(OutStr,1,STRLEN(OutStr)-1);
      END;
    END;

    PROCEDURE CheckUserGroupUponPageExit@1100285015(UserGroup@1100285000 : Record 12013606;VAR MessageType@1100285003 : Integer;VAR TextMessage@1100285002 : Text[250]);
    VAR
      UserGroupLine@1100285001 : Record 12013607;
    BEGIN
      UserGroupLine.RESET;
      UserGroupLine.SETRANGE(Code, UserGroup.Code);
      IF NOT UserGroupLine.FINDSET THEN BEGIN
        IF NOT UserGroup."Auto Approve" AND (UserGroup."Replacement type" = 0) THEN BEGIN
          MessageType := 1;
          TextMessage := EXF011;
        END;
      END ELSE
        REPEAT
          IF (UserGroupLine."Starting Date" <> 0D) AND (UserGroupLine."Ending Date" = 0D) THEN BEGIN
            MessageType := 2;
            TextMessage := EXF012;
          END ELSE IF (UserGroupLine."Starting Date" = 0D) AND (UserGroupLine."Ending Date" <> 0D) THEN BEGIN
            MessageType := 2;
            TextMessage := EXF012;
          END ELSE IF (UserGroupLine."Starting Date" <> 0D) AND (UserGroupLine."Ending Date" <> 0D) THEN
            IF UserGroupLine."Starting Date" > UserGroupLine."Ending Date" THEN BEGIN
              MessageType := 2;
              TextMessage := EXF013;
            END;
        UNTIL UserGroupLine.NEXT = 0;
    END;

    PROCEDURE UserCleanUp@1100285005();
    VAR
      Company@1100285000 : Record 2000000006;
      ExUserComp@1100285001 : Record 12013641;
      ExUser@1100285002 : Record 12013640;
      ExUser2@1100285004 : Record 12013640;
      UserFound@1100285003 : Boolean;
    BEGIN
      Company.FINDSET;
      REPEAT
        ExUserComp.CHANGECOMPANY(Company.Name);
        IF ExUserComp.FINDSET THEN
          REPEAT
            IF NOT ExUser.GET(ExUserComp."User ID") THEN BEGIN
              ExUser."User ID" := ExUserComp."User ID";
              ExUser."User Name" := ExUserComp."User Name";
              ExUser.INSERT;
            END ELSE BEGIN
              ExUser."User Name" := ExUserComp."User Name";
              ExUser.MODIFY;
            END;
          UNTIL ExUserComp.NEXT = 0;
      UNTIL Company.NEXT = 0;

      ExUser.RESET;
      IF ExUser.FINDSET THEN
        REPEAT
          UserFound := FALSE;
          Company.FINDSET;
          REPEAT
            ExUserComp.CHANGECOMPANY(Company.Name);
            ExUserComp.SETRANGE("User ID", ExUser."User ID");
            IF ExUserComp.FINDFIRST THEN
              UserFound := TRUE;
          UNTIL (Company.NEXT = 0) OR UserFound;

          IF NOT UserFound THEN BEGIN
            ExUser2.GET(ExUser."User ID");
            ExUser2.DELETE;
          END;
        UNTIL ExUser.NEXT = 0;
    END;

    LOCAL PROCEDURE ReplaceDimOwners@1100285016(OldUser@1100285002 : Text[50];NewUser@1100285001 : Text[50]);
    VAR
      DimOwners@1100285000 : Record 12013655;
    BEGIN
      IF OldUser = NewUser THEN
        EXIT;

      IF NewUser = '' THEN
        EXIT;

      DimOwners.RESET;
      DimOwners.SETRANGE("Approver Group",OldUser);
      DimOwners.MODIFYALL("Approver Group",NewUser);
    END;

    LOCAL PROCEDURE ReplaceFirstApproverInEXReference@1100285006(OldUser@1100285001 : Text[50];NewUser@1100285000 : Text[50]);
    VAR
      EXReference@1100285002 : Record 12013632;
    BEGIN
      IF OldUser = NewUser THEN
        EXIT;

      IF NewUser = '' THEN
        EXIT;

      EXReference.RESET;
      EXReference.SETRANGE("First Approver", OldUser);
      EXReference.MODIFYALL("First Approver", NewUser);
    END;

    LOCAL PROCEDURE GetNewLineNo@1100285022(GroupCode@1100285000 : Code[50]) NewLineNo : Integer;
    VAR
      EXUserGroupLine@1100285001 : Record 12013607;
    BEGIN
      EXUserGroupLine.SETRANGE(Code, GroupCode);

      IF EXUserGroupLine.FINDLAST THEN
        NewLineNo := EXUserGroupLine."Line No." + 10000;
    END;

    BEGIN
    END.
  }
}

