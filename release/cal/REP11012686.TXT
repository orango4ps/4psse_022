OBJECT Report 11012686 Export Hours DBS
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Exportieren Stunden DBS;
               ENU=Export Hours DBS;
               NLD=Exporteren uren DBS;
               NOR=Eksporter timer DBS;
               SVE=Exportera timmar DBS];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   HumanResourcesSetup.GET;
                 END;

    OnPreReport=VAR
                  TempBlob@1100528403 : Record 99008535;
                  ThreeTierAutomationMgt@1100528402 : Codeunit 419;
                  OStream@1100528401 : OutStream;
                  ExportHoursDBS@1100528400 : XMLport 11012081;
                  InStream@1100529500 : InStream;
                  ToFile@1100529501 : Text;
                  ClientTypeManagement@1100529502 : Codeunit 4030;
                BEGIN
                  TempBlob.Blob.CREATEOUTSTREAM(OStream);
                  ExportHoursDBS.SETDESTINATION(OStream);
                  ExportHoursDBS.SetGlobalVars(
                    DocumentFilterCde,
                    EmployerCde,
                    NotExportedLinesOnly,
                    gvStartDate,
                    gvEndDate,
                    AllCompanies);
                  ExportHoursDBS.EXPORT;

                  IF ClientTypeManagement.IsCommonWebClientType THEN BEGIN
                    TempBlob.Blob.CREATEINSTREAM(InStream);
                    ToFile := STRSUBSTNO('dbs_hours_%1_%2.exp', gvStartDate, gvEndDate);
                    DOWNLOADFROMSTREAM(InStream, Text000, '', '', ToFile);
                  END ELSE
                    ThreeTierAutomationMgt.BLOBExport4PS(TempBlob, ExportHoursDBS.FILENAME, FALSE, FALSE);
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100528407;;Container;
                  ContainerType=ContentArea }

      { 1100528406;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100528405;2;Field  ;
                  Name=DocumentFilterCde;
                  CaptionML=[DEU=Belegartenfilter;
                             ENU=Document Filter;
                             NLD=Documentfilter;
                             NOR=Dokumentfilter;
                             SVE=Dokumentfilter];
                  SourceExpr=DocumentFilterCde;
                  Editable=DocumentFilterCdeEditable;
                  OnValidate=VAR
                               lDocNo@1100528400 : Code[20];
                               HoursExportRec@1100528401 : Record 11012041;
                             BEGIN
                               lDocNo := '';
                               HoursExportRec.SETCURRENTKEY("Document No.");
                               HoursExportRec.SETFILTER("Document No.",DocumentFilterCde);
                               IF HoursExportRec.FINDSET THEN
                                 REPEAT
                                   IF HoursExportRec."Document No." <> lDocNo THEN
                                     MESSAGE(Text016, HoursExportRec."Document No.");
                                   lDocNo := HoursExportRec."Document No.";
                                 UNTIL HoursExportRec.NEXT = 0;
                               HoursExportRec.SETRANGE("Document No.");
                             END;
                              }

      { 1100528404;2;Field  ;
                  Name=StartDate;
                  CaptionML=[DEU=Von Datum;
                             ENU=From Date;
                             NLD=Vanaf boekingsdatum;
                             NOR=Fra dato;
                             SVE=Fr†n datum];
                  SourceExpr=gvStartDate;
                  Editable=StartDateEditable }

      { 1100528403;2;Field  ;
                  Name=EndDate;
                  CaptionML=[DEU=Bis Datum;
                             ENU=To Date;
                             NLD=Tot en met boekingsdatum;
                             NOR=Til dato;
                             SVE=Till datum];
                  SourceExpr=gvEndDate;
                  Editable=EndDateEditable }

      { 1100528401;2;Field  ;
                  Name=Employer;
                  CaptionML=[DEU=Fr Arbeitgeber;
                             ENU=For Employer;
                             NLD=Voor werkgever;
                             NOR=For arbeidsgiver;
                             SVE=F”r arbetsgivare];
                  SourceExpr=EmployerCde;
                  OnLookup=VAR
                             EmployerPage@1100528401 : Page 11012064;
                             EmployerRec@1100528400 : Record 11012013;
                           BEGIN
                             CLEAR(EmployerPage);
                             EmployerPage.LOOKUPMODE(TRUE);
                             IF EmployerPage.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               EmployerPage.GETRECORD(EmployerRec);
                               EmployerCde := EmployerRec.Code;
                             END;
                           END;
                            }

      { 1100528402;2;Field  ;
                  Name=NotExportedLinesOnly;
                  CaptionML=[DEU=Ausschliesslich nicht-exportierte Zeilen;
                             ENU=Not Exported Lines only;
                             NLD=Alleen niet ge¼xporteerde regels;
                             NOR=Kun ikke-eksporterte rader;
                             SVE=Endast icke-exporterade rader];
                  SourceExpr=NotExportedLinesOnly }

      { 1100528400;2;Field  ;
                  Name=AllCompanies;
                  CaptionML=[DEU=Alle Mandanten;
                             ENU=All Companies;
                             NLD=Alle bedrijven;
                             NOR=Alle selskaper;
                             SVE=Alla f”retag];
                  SourceExpr=AllCompanies;
                  Editable=AllCompaniesEditable }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      HumanResourcesSetup@1100528410 : Record 5218;
      DocumentFilterCde@1100528413 : Code[100];
      EmployerCde@1100528412 : Code[20];
      NotExportedLinesOnly@1100528409 : Boolean;
      gvStartDate@1100528407 : Date;
      gvEndDate@1100528406 : Date;
      AllCompanies@1100528405 : Boolean;
      DocumentFilterCdeEditable@1100528404 : Boolean INDATASET;
      StartDateEditable@1100528403 : Boolean INDATASET;
      EndDateEditable@1100528402 : Boolean INDATASET;
      AllCompaniesEditable@1100528401 : Boolean INDATASET;
      Text016@1100528400 : TextConst 'DEU=Achtung, Belegnummer %1 ist bereits frher exportiert worden.;ENU=Document Number %1 has been exported before.;NLD=Let op, documentnummer %1 is eerder ge‰xporteerd.;NOR=Dokumentnr %1 har blitt eksportert tidligere.;SVE=Dokumentnr %1 har exporterats tidigare.';
      Text000@1100529500 : TextConst 'DEU=Export Datei...;ENU=Export File...;NLD=Exporteer bestand...';

    PROCEDURE SetSelection@10(DocumentNo@11012000 : Code[20]);
    BEGIN
      DocumentFilterCde := DocumentNo;
      DocumentFilterCdeEditable := TRUE;
      StartDateEditable := FALSE;
      EndDateEditable := FALSE;
      AllCompaniesEditable := FALSE;
      SetDocumentOrDate;
    END;

    PROCEDURE SetDocumentOrDate@1100525001();
    VAR
      lStrPos@1100525006 : Integer;
      lYearStr@1100525005 : Text[4];
      lPeriodStr@1100525003 : Text[2];
      lYear@1100525002 : Integer;
      lPeriod@1100525000 : Integer;
    BEGIN
      lStrPos := STRPOS(DocumentFilterCde, '-');
      IF STRLEN(DocumentFilterCde) > lStrPos THEN BEGIN
        lYearStr := COPYSTR(DocumentFilterCde, lStrPos - 4,4);
        lPeriodStr := COPYSTR(DocumentFilterCde, lStrPos + 1, 2);
        IF EVALUATE(lYear,lYearStr) AND EVALUATE(lPeriod,lPeriodStr) THEN BEGIN
          CASE HumanResourcesSetup."Hour Document No." OF
            HumanResourcesSetup."Hour Document No."::Week:
              BEGIN
                gvStartDate := DWY2DATE(1, lPeriod, lYear);
                gvEndDate := DWY2DATE(7, lPeriod, lYear);
              END;
            HumanResourcesSetup."Hour Document No."::"4-Weeks":
              BEGIN
                lPeriod := (lPeriod * 4) - 3;    //* 4wk-periodno -> first week of period
                gvStartDate := DWY2DATE(1, lPeriod, lYear);
                gvEndDate := gvStartDate + 27;
              END;
            HumanResourcesSetup."Hour Document No."::Month:
              BEGIN
                gvStartDate := DMY2DATE(1, lPeriod, lYear);
                gvEndDate := CALCDATE('<1M-1D>' ,gvStartDate);
              END;
          END;

          IF HumanResourcesSetup."Selection Posting Date" THEN BEGIN
            DocumentFilterCde := '' ;
            DocumentFilterCdeEditable := FALSE;
            StartDateEditable := TRUE;
            EndDateEditable := TRUE;
          END;
        END;
      END;
    END;

    BEGIN
    {
      This report is only used to run XMLport Export Hours DBS to prevent selection of the
      output file by the user.
    }
    END.
  }
  RDLDATA
  {
  }
}

