OBJECT Report 11012195 Create Resp Pers Perm (PPT)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Projektportaal Berechtigungen verantw. Personen Projekt erstellen;
               ENU=Create Resp Pers Perm (PPT);
               NLD=Projectportaal permissies projectverantwoordelijken aanmaken];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 1100525000;;DataItem;                  ;
               DataItemTable=Table11012040;
               DataItemTableView=SORTING(Project No.,Line No.);
               OnPreDataItem=BEGIN
                               RecCount := 0;
                             END;

               OnAfterGetRecord=BEGIN
                                  ProcessProjectResponsiblePerson("Project Responsible Person");
                                END;

               OnPostDataItem=BEGIN
                                MESSAGE(Text000, RecCount);
                              END;

               ReqFilterFields=Project No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      RecCount@1100525000 : Integer;
      Text000@1100525001 : TextConst 'DEU=%1 Zugangs- und/oder FilterdatensÑtze hinzugefÅgt.;ENU=%1 Access and/or Filter Records added.;NLD=%1 toegang- en/of filterrecords toegevoegd.';
      SubsiteType@1100528300 : Option;

    PROCEDURE ProcessProjectResponsiblePerson@1100528301(ProjectResponsiblePerson@1100528300 : Record 11012040);
    VAR
      ProjectPortalSite@1100528303 : Record 11229810;
      SiteDocTypePPT@1100528302 : Record 11229811;
    BEGIN
      InsertRecord(ProjectResponsiblePerson, 1, '', '');

      ProjectPortalSite.SETRANGE(Type, SubsiteType);
      ProjectPortalSite.SETRANGE("High Security Subsite", TRUE);
      IF (ProjectPortalSite.FINDSET) THEN BEGIN
        REPEAT
          SiteDocTypePPT.SETRANGE("Subsite Type", SubsiteType);
          SiteDocTypePPT.SETRANGE("Project Portal Subsite", ProjectPortalSite.Code);
          IF (SiteDocTypePPT.FINDSET) THEN BEGIN
            InsertRecord(ProjectResponsiblePerson, 1, ProjectPortalSite.Code, ''); //1=Access
            REPEAT
              InsertRecord(ProjectResponsiblePerson, 1, ProjectPortalSite.Code, SiteDocTypePPT."Document Type");
            UNTIL(SiteDocTypePPT.NEXT = 0);
          END;
        UNTIL (ProjectPortalSite.NEXT = 0);
      END;

      ProjectPortalSite.SETRANGE("High Security Subsite", FALSE);
      IF (ProjectPortalSite.FINDSET) THEN BEGIN
        REPEAT
          InsertRecord(ProjectResponsiblePerson, 1, ProjectPortalSite.Code, ''); //1=Access
        UNTIL (ProjectPortalSite.NEXT = 0);
      END;

      ProjectPortalSite.SETRANGE("High Security Subsite");
      IF (ProjectPortalSite.FINDSET) THEN BEGIN
        REPEAT
          SiteDocTypePPT.SETRANGE("Subsite Type", SubsiteType);
          SiteDocTypePPT.SETRANGE("Project Portal Subsite", ProjectPortalSite.Code);
          IF (SiteDocTypePPT.FINDSET) THEN BEGIN
            REPEAT
              InsertRecord(ProjectResponsiblePerson, 2, ProjectPortalSite.Code, SiteDocTypePPT."Document Type"); //2=Filter
            UNTIL (SiteDocTypePPT.NEXT = 0);
          END;
        UNTIL (ProjectPortalSite.NEXT = 0);
      END;
    END;

    PROCEDURE SetSubsiteType@1100528304(iSubsiteType@1100528300 : Option);
    BEGIN
      SubsiteType := iSubsiteType;
    END;

    LOCAL PROCEDURE InsertRecord@1100528300(ProjectResponsiblePerson@1100528306 : Record 11012040;PermissionType@1100528300 : Option;SiteCode@1100528304 : Code[20];DocumentType@1100528305 : Code[20]);
    VAR
      RespPersPermPPT@1100528301 : Record 11229812;
      Job@1100528302 : Record 11072003;
      DefaultRespPersPermPPT@1100528303 : Record 11229814;
    BEGIN
      Job.GET(ProjectResponsiblePerson."Project No.");

        IF (NOT DefaultRespPersPermPPT.GET(SubsiteType, PermissionType, ProjectResponsiblePerson.Responsibility,
           SiteCode, DocumentType))
        THEN BEGIN
          DefaultRespPersPermPPT.INIT;
          DefaultRespPersPermPPT."Show in View" := TRUE;
        END;

        RespPersPermPPT.INIT;
        RespPersPermPPT."Subsite Type" := SubsiteType;
        RespPersPermPPT.Type := PermissionType;
        RespPersPermPPT."Project No." := ProjectResponsiblePerson."Project No.";
        RespPersPermPPT."Line No." := ProjectResponsiblePerson."Line No.";
        RespPersPermPPT."Project Portal Subsite" := SiteCode;
        RespPersPermPPT."Document Type" := DocumentType;
        RespPersPermPPT."Permission Level" := DefaultRespPersPermPPT."Permission Level";
        RespPersPermPPT."Show in View" := DefaultRespPersPermPPT."Show in View";
        RespPersPermPPT."Private Permission Level" := DefaultRespPersPermPPT."Private Permission Level";

        IF (RespPersPermPPT.INSERT) THEN //not TRUE
          RecCount += 1;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

