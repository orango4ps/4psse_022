OBJECT Table 11020204 Discipline
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00,4PSSE;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ExchangeFSA('I');
               "Last Updated Date/Time" := CURRENTDATETIME;
             END;

    OnModify=BEGIN
               ExchangeFSA('M');
               "Last Updated Date/Time" := CURRENTDATETIME;
               UpdateResourceGroup(1, xRec, Rec);  // 1=Modify
             END;

    OnDelete=VAR
               TradeSpeciality@1100485000 : Record 11020285;
               TradeExpertise@1100485001 : Record 11020286;
             BEGIN
               IF Code <> '' THEN BEGIN
                 TradeSpeciality.SETRANGE(Discipline,Code);
                 IF TradeSpeciality.FIND('-') THEN
                   ERROR(Text000,TradeSpeciality.TABLECAPTION);

                 TradeExpertise.SETRANGE(Discipline,Code);
                 IF TradeExpertise.FIND('-') THEN
                   ERROR(Text000,TradeExpertise.TABLECAPTION);
               END;

               ExchangeFSA('D');
               UpdateResourceGroup(2, xRec, Rec);  // 2=Delete
             END;

    OnRename=BEGIN
               ExchangeFSA('R');
               UpdateResourceGroup(3, xRec, Rec);  // 3=Rename
             END;

    CaptionML=[ENU=Discipline;
               NOR=Disiplin;
               SVE=Disciplin];
    LookupPageID=Page11020211;
  }
  FIELDS
  {
    { 10  ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 20  ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 11012990;;Export to FSA      ;Boolean       ;CaptionML=[ENU=Export to Field Service;
                                                              NOR=Eksporter til Feltservice;
                                                              SVE=Exportera till f„ltservice];
                                                   Description=Indication new/changed. (Field Service Admin.), provisionally this is only Field Vision. }
    { 11012991;;Date/Time Export to FSA;DateTime  ;CaptionML=[ENU=Date/Time Export to Field Service;
                                                              NOR=Dato/tid, Eksporter til Feltservice;
                                                              SVE=Datum/tid, export till f„ltservice] }
    { 11128000;;Last Updated Date/Time;DateTime    }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100485000 : TextConst 'ENU=%1 present, cannot delete.;NOR=%1 finnes, kan ikke fjerne.;SVE=%1 finns, kan inte ta bort.';

    PROCEDURE ExchangeFSA@1100485000(IMode@1100485003 : Code[1]);
    VAR
      lvProjSetupRec@1100485001 : Record 315;
      lvFieldVisionExchangeMgtCU@1100485000 : Codeunit 11012091;
    BEGIN
      //**4PS.n
      IF IMode = 'I' THEN BEGIN
        "Export to FSA" := FALSE;
        "Date/Time Export to FSA" := 0DT;
      END;
      lvProjSetupRec.GET;
      IF lvProjSetupRec."Field Service Active" THEN BEGIN
        IF IMode <> 'D' THEN BEGIN
          "Export to FSA" := TRUE;
          IF IMode = 'R' THEN
            lvFieldVisionExchangeMgtCU.StoreDeletedDiscipline(xRec);
        END ELSE BEGIN
          lvFieldVisionExchangeMgtCU.StoreDeletedDiscipline(Rec);
        END;
      END;
    END;

    PROCEDURE GetResourceGroupNo@1100529013() : Code[20];
    BEGIN
      EXIT(GetResourceGroupNoWithCompany(COMPANYNAME));
    END;

    PROCEDURE GetResourceGroupNoWithCompany@1100528600(ICompanyName@1100528601 : Text[30]) : Code[20];
    VAR
      ResourceGroup@1100528600 : Record 11229384;
    BEGIN
      IF Code <> '' THEN BEGIN
        ResourceGroup.SETCURRENTKEY(Type, Company, "Source No.");
        ResourceGroup.SETRANGE(Type, ResourceGroup.Type::Discipline);
        ResourceGroup.SETRANGE(Company, ICompanyName);
        ResourceGroup.SETRANGE("Source No.", Code);
        IF ResourceGroup.FINDFIRST THEN
          EXIT(ResourceGroup."No.");
      END;
      EXIT('');
    END;

    PROCEDURE ShowResourceGroup@1100529012();
    VAR
      ResourceGroup@1100525001 : Record 11229384;
    BEGIN
      ResourceGroup."No." := GetResourceGroupNo();
      IF ResourceGroup."No." <> '' THEN
        ResourceGroup.GET(ResourceGroup."No.");
      ResourceGroup.SETRANGE(Company, COMPANYNAME);
      ResourceGroup.SETRANGE(Type, ResourceGroup.Type::Discipline);
      PAGE.RUNMODAL(PAGE::"Resource Group List (WOP)", ResourceGroup);
    END;

    PROCEDURE UpdateResourceGroup@1100529011(UpdateMode@1100525002 : ',Modify,Delete,Rename';xDiscipline@1100525003 : Record 11020204;Discipline@1100525004 : Record 11020204);
    VAR
      WorkOrderPlanningSetup@1100525000 : Record 11229277;
      ResourceMgt@1100525001 : Codeunit 11229277;
    BEGIN
      IF NOT WorkOrderPlanningSetup.ISEMPTY THEN
        ResourceMgt.UpdateResourceGroupDiscipline(UpdateMode, xDiscipline, Discipline);
    END;

    BEGIN
    {
      140416 4PSHM ITERO.MH added field "Last Updated Date/Time" and code onInsert() and onModify()
    }
    END.
  }
}

