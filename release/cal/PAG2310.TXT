OBJECT Page 2310 BC O365 Sales Invoice
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Draft Invoice;
               NOR=Lag fakturakladd;
               SVE=Fakturautkast];
    SourceTable=Table36;
    DataCaptionExpr='';
    SourceTableView=WHERE(Document Type=CONST(Invoice));
    PageType=Document;
    RefreshOnActivate=Yes;
    PromotedActionCategoriesML=[ENU=New,Process,Report,Manage;
                                NOR=Ny,Prosess,Rapport,Administrer;
                                SVE=Ny,Process,Rapport,Hantera];
    OnInit=BEGIN
             IsDevice := ClientTypeManagement.GetCurrentClientType IN [CLIENTTYPE::Tablet,CLIENTTYPE::Phone];
             FieldsVisible := TRUE;
           END;

    OnOpenPage=VAR
                 O365SalesInitialSetup@6115 : Record 2110;
                 NoFilter@1001 : Text;
               BEGIN
                 IsUsingVAT := O365SalesInitialSetup.IsUsingVAT;
                 NoFilter := GETFILTER("No.");
                 IF STRPOS(UPPERCASE(NoFilter),'TESTINVOICE') <> 0 THEN BEGIN
                   TestInvoiceVisible := TRUE;
                   SETFILTER("No.",'');
                 END;
               END;

    OnNextRecord=BEGIN
                   IF NOT IsNew THEN
                     ForceExit := TRUE;

                   O365SalesInvoiceMgmt.OnQueryCloseForSalesHeader(Rec,ForceExit,CustomerName);
                   IsNew := FALSE;

                   EXIT(NEXT(Steps));
                 END;

    OnNewRecord=BEGIN
                  CustomerName := '';
                  CustomerEmail := '';
                  WorkDescription := '';
                  "Document Type" := "Document Type"::Invoice;
                  IsNew := TRUE;

                  SetDefaultPaymentServices;
                END;

    OnDeleteRecord=VAR
                     CustInvoiceDisc@1000 : Record 19;
                   BEGIN
                     ForceExit := TRUE;
                     IF NOT FIND THEN BEGIN
                       CurrPage.CLOSE;
                       EXIT(FALSE);
                     END;

                     IF CustInvoiceDisc.GET("Invoice Disc. Code","Currency Code",0) THEN
                       CustInvoiceDisc.DELETE;
                   END;

    OnQueryClosePage=BEGIN
                       IF NOT IsNew THEN
                         ForceExit := TRUE;

                       EXIT(O365SalesInvoiceMgmt.OnQueryCloseForSalesHeader(Rec,ForceExit,CustomerName));
                     END;

    OnAfterGetCurrRecord=BEGIN
                           // Tasks shared with estimates (PAG2341)
                           O365SalesInvoiceMgmt.OnAfterGetSalesHeaderRecord(Rec,CurrencyFormat,TaxAreaDescription,NoOfAttachmentsValueTxt,WorkDescription);
                           CurrPageEditable := CurrPage.EDITABLE;

                           O365SalesInvoiceMgmt.UpdateCustomerFields(Rec,CustomerName,CustomerEmail,IsCompanyContact);
                           O365SalesInvoiceMgmt.UpdateAddress(Rec,FullAddress,CountryRegionCode);

                           O365SalesInvoiceMgmt.CalcInvoiceDiscountAmount(Rec,SubTotalAmount,DiscountTxt,InvoiceDiscountAmount,InvDiscAmountVisible);

                           IF ClientTypeManagement.GetCurrentClientType = CLIENTTYPE::Phone THEN
                             FieldsVisible := CustomerName <> '';

                           IsCustomerBlocked := O365SalesInvoiceMgmt.IsCustomerBlocked("Sell-to Customer No.");
                           IF IsCustomerBlocked THEN
                             O365SalesInvoiceMgmt.SendCustomerHasBeenBlockedNotification("Sell-to Customer Name");

                           // Invoice specific tasks
                           IF IsNew AND TestInvoiceVisible THEN
                             VALIDATE(IsTest,TRUE);
                           TestInvoiceVisible := IsTest;

                           CurrPage.CAPTION := GetInvTypeCaption;

                           IF TestInvoiceVisible THEN
                             NextInvoiceNo := "No."
                           ELSE
                             IF SalesReceivablesSetup.GET THEN
                               IF SalesReceivablesSetup."Posted Invoice Nos." <> '' THEN
                                 NextInvoiceNo := NoSeriesManagement.ClearStateAndGetNextNo(SalesReceivablesSetup."Posted Invoice Nos.");

                           O365SalesInvoiceMgmt.GetPaymentInstructionsName("Payment Instructions Id",PaymentInstructionsName);
                         END;

    ActionList=ACTIONS
    {
      { 22      ;    ;ActionContainer;
                      CaptionML=[ENU=Invoice;
                                 NOR=Faktura;
                                 SVE=Faktura];
                      ActionContainerType=ActionItems }
      { 3       ;1   ;Action    ;
                      Name=ViewPdf;
                      CaptionML=[ENU=Preview;
                                 NOR=ForhÜndsvisning;
                                 SVE=Fîrhandsgranska];
                      ToolTipML=[ENU=Show a preview of the invoice before you send it.;
                                 NOR=Vis en forhÜndsvisning av fakturaen fõr du sender den.;
                                 SVE=Visa en fîrhandsgranskning av fakturan innan du skickar den.];
                      ApplicationArea=#Basic,#Suite,#Invoicing;
                      Promoted=Yes;
                      Visible=NOT IsCustomerBlocked;
                      Enabled=CustomerName <> '';
                      PromotedIsBig=Yes;
                      Image=Document;
                      PromotedCategory=Category4;
                      PromotedOnly=Yes;
                      OnAction=VAR
                                 ReportSelections@1001 : Record 77;
                                 DocumentPath@1000 : Text[250];
                               BEGIN
                                 SETRECFILTER;
                                 LOCKTABLE;
                                 FIND;
                                 ReportSelections.GetPdfReport(DocumentPath,ReportSelections.Usage::"S.Invoice Draft",Rec,"Sell-to Customer No.");
                                 DOWNLOAD(DocumentPath,'','','',DocumentPath);
                                 FIND;
                               END;
                                }
      { 43      ;1   ;Action    ;
                      Name=SendTest;
                      CaptionML=[ENU=Send test invoice;
                                 NOR=Send testfaktura;
                                 SVE=Skicka testfaktura];
                      ToolTipML=[ENU=Send the test invoice.;
                                 NOR=Send testfakturaen.;
                                 SVE=Skicka testfakturan.];
                      ApplicationArea=#Basic,#Suite,#Invoicing;
                      Promoted=Yes;
                      Visible=TestInvoiceVisible AND NOT IsCustomerBlocked;
                      Enabled=CustomerName <> '';
                      PromotedIsBig=Yes;
                      Image=Email;
                      PromotedCategory=Category4;
                      PromotedOnly=Yes;
                      OnAction=VAR
                                 O365SendResendInvoice@1001 : Codeunit 2104;
                               BEGIN
                                 IF O365SendResendInvoice.SendTestInvoiceFromBC(Rec) THEN BEGIN
                                   ForceExit := TRUE;
                                   CurrPage.CLOSE;
                                 END;
                               END;
                                }
      { 23      ;1   ;Action    ;
                      Name=Post;
                      ShortCutKey=Ctrl+Right;
                      CaptionML=[ENU=Send;
                                 NOR=Send;
                                 SVE=Skicka];
                      ToolTipML=[ENU=Finalize and send the invoice.;
                                 NOR=Fullfõr og send fakturaen.;
                                 SVE=Slutfîr och skicka fakturan.];
                      ApplicationArea=#Basic,#Suite,#Invoicing;
                      Promoted=Yes;
                      Visible=NOT TestInvoiceVisible AND NOT IsCustomerBlocked;
                      Enabled=CustomerName <> '';
                      PromotedIsBig=Yes;
                      Image=Email;
                      PromotedCategory=Category4;
                      PromotedOnly=Yes;
                      OnAction=VAR
                                 O365SendResendInvoice@1001 : Codeunit 2104;
                               BEGIN
                                 IF O365SendResendInvoice.SendSalesInvoiceOrQuoteFromBC(Rec) THEN BEGIN
                                   ForceExit := TRUE;
                                   CurrPage.CLOSE;
                                 END;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 25  ;1   ;Group     ;
                Name=SellToWeb;
                CaptionML=[ENU=Sell to;
                           NOR=Selg til;
                           SVE=Kund];
                Visible=NOT IsDevice;
                GroupType=Group }

    { 9   ;2   ;Field     ;
                CaptionML=[ENU=Customer Name;
                           NOR=Kundens navn;
                           SVE=Kundens namn];
                ToolTipML=[ENU=Specifies the customer's name.;
                           NOR=Angir kundens navn.;
                           SVE=Anger kundens namn.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Sell-to Customer Name";
                Importance=Promoted;
                LookupPageID=BC O365 Contact Lookup;
                OnValidate=BEGIN
                             O365SalesInvoiceMgmt.ValidateCustomerName(Rec,CustomerName,CustomerEmail);
                             O365SalesInvoiceMgmt.UpdateAddress(Rec,FullAddress,CountryRegionCode);

                             CurrPage.UPDATE(TRUE);
                           END;

                ShowMandatory=TRUE;
                QuickEntry=FALSE }

    { 18  ;2   ;Field     ;
                ExtendedDatatype=E-Mail;
                CaptionML=[ENU=Email Address;
                           NOR=E-postadresse;
                           SVE=E-postadress];
                ToolTipML=[ENU=Specifies the customer's email address.;
                           NOR=Angir kundens e-postadresse.;
                           SVE=Anger kundens e-postadress.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=CustomerEmail;
                Editable=CurrPageEditable AND (CustomerName <> '');
                OnValidate=BEGIN
                             O365SalesInvoiceMgmt.ValidateCustomerEmail(Rec,CustomerEmail);
                           END;
                            }

    { 24  ;2   ;Group     ;
                GroupType=Group }

    { 16  ;3   ;Field     ;
                Name=ViewContactCard;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=ViewContactDetailsLbl;
                Enabled=CustomerName <> '';
                Editable=FALSE;
                OnDrillDown=BEGIN
                              O365SalesInvoiceMgmt.EditCustomerCardFromSalesHeader(Rec,FullAddress,CountryRegionCode);
                            END;

                ShowCaption=No }

    { 11  ;2   ;Group     ;
                GroupType=Group }

    { 15  ;3   ;Field     ;
                CaptionML=[ENU=Address;
                           NOR=Adresse;
                           SVE=Adress];
                ToolTipML=[ENU=Specifies the address where the customer is located.;
                           NOR=Angir adressen der kunden befinner seg.;
                           SVE=Anger kundens adress.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Sell-to Address";
                Editable=CustomerName <> '';
                OnValidate=BEGIN
                             O365SalesInvoiceMgmt.ValidateCustomerAddress("Sell-to Address","Sell-to Customer No.");
                             O365SalesInvoiceMgmt.UpdateAddress(Rec,FullAddress,CountryRegionCode);
                           END;
                            }

    { 12  ;3   ;Field     ;
                CaptionML=[ENU=Address 2;
                           NOR=Adresse 2;
                           SVE=Adress 2];
                ToolTipML=[ENU=Specifies additional address information.;
                           NOR=Angir ekstra adresseinformasjon.;
                           SVE=Anger ytterligare adressuppgifter.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Sell-to Address 2";
                Editable=CustomerName <> '';
                OnValidate=BEGIN
                             O365SalesInvoiceMgmt.ValidateCustomerAddress2("Sell-to Address 2","Sell-to Customer No.");
                             O365SalesInvoiceMgmt.UpdateAddress(Rec,FullAddress,CountryRegionCode);
                           END;
                            }

    { 2   ;3   ;Field     ;
                Lookup=No;
                CaptionML=[ENU=City;
                           NOR=Sted;
                           SVE=Ort];
                ToolTipML=[ENU=Specifies the address city.;
                           NOR=Angir poststedet for adressen.;
                           SVE=Anger adressens ort.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Sell-to City";
                Editable=CustomerName <> '';
                OnValidate=BEGIN
                             O365SalesInvoiceMgmt.ValidateCustomerCity("Sell-to City","Sell-to Customer No.");
                             O365SalesInvoiceMgmt.UpdateAddress(Rec,FullAddress,CountryRegionCode);
                           END;
                            }

    { 5   ;3   ;Field     ;
                Lookup=No;
                CaptionML=[ENU=Post Code;
                           NOR=Postnummer;
                           SVE=Postnummer];
                ToolTipML=[ENU=Specifies the postal code.;
                           NOR=Angir postnummeret.;
                           SVE=Anger postnumret.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Sell-to Post Code";
                Editable=CustomerName <> '';
                OnValidate=BEGIN
                             O365SalesInvoiceMgmt.ValidateCustomerPostCode("Sell-to Post Code","Sell-to Customer No.");
                             O365SalesInvoiceMgmt.UpdateAddress(Rec,FullAddress,CountryRegionCode);
                           END;
                            }

    { 8   ;3   ;Field     ;
                Lookup=No;
                CaptionML=[ENU=County;
                           NOR=Fylke/delstat;
                           SVE=Delstat];
                ToolTipML=[ENU=Specifies the address county.;
                           NOR=Angir fylket for adressen.;
                           SVE=Anger adressens delstat.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Sell-to County";
                Editable=CustomerName <> '';
                OnValidate=BEGIN
                             O365SalesInvoiceMgmt.ValidateCustomerCounty("Sell-to County","Sell-to Customer No.");
                             O365SalesInvoiceMgmt.UpdateAddress(Rec,FullAddress,CountryRegionCode);
                           END;
                            }

    { 33  ;3   ;Field     ;
                Lookup=Yes;
                CaptionML=[ENU=Country/Region Code;
                           NOR=Lands-/omrÜdekode;
                           SVE=Lands-/regionkod];
                ToolTipML=[ENU=Specifies the address country/region.;
                           NOR=Angir landet/omrÜdet for adressen.;
                           SVE=Anger adressens land/region.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=CountryRegionCode;
                TableRelation=Country/Region;
                Editable=CurrPageEditable AND (CustomerName <> '');
                LookupPageID=BC O365 Country/Region List;
                OnValidate=BEGIN
                             CountryRegionCode := O365SalesInvoiceMgmt.FindCountryCodeFromInput(CountryRegionCode);
                             "Sell-to Country/Region Code" := CountryRegionCode;

                             O365SalesInvoiceMgmt.ValidateCustomerCountryRegion("Sell-to Country/Region Code","Sell-to Customer No.");
                             O365SalesInvoiceMgmt.UpdateAddress(Rec,FullAddress,CountryRegionCode);
                           END;
                            }

    { 45  ;1   ;Group     ;
                Name=SellToDevice;
                CaptionML=[ENU=Sell to;
                           NOR=Selg til;
                           SVE=Kund];
                Visible=IsDevice AND NOT FieldsVisible;
                GroupType=Group }

    { 38  ;2   ;Field     ;
                Name=EnterNewSellToCustomerName;
                Lookup=No;
                CaptionML=[ENU=Add new customer;
                           NOR=Legg til ny kunde;
                           SVE=LÑgg till ny kund];
                ToolTipML=[ENU=Specifies the customer's name.;
                           NOR=Angir kundens navn.;
                           SVE=Anger kundens namn.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Sell-to Customer Name";
                Importance=Promoted;
                OnValidate=VAR
                             Customer@1000 : Record 18;
                           BEGIN
                             O365SalesInvoiceMgmt.CreateCustomer(Rec,Customer,CustomerName,CustomerEmail);
                             O365SalesInvoiceMgmt.UpdateAddress(Rec,FullAddress,CountryRegionCode);

                             CurrPage.UPDATE(TRUE);
                           END;

                QuickEntry=FALSE }

    { 46  ;2   ;Group     ;
                CaptionML=[ENU=Or;
                           NOR=Eller;
                           SVE=Eller];
                GroupType=Group }

    { 4   ;3   ;Field     ;
                Name=PickExistingCustomer;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=PickExistingCustomerLbl;
                Editable=FALSE;
                LookupPageID=BC O365 Contact Lookup;
                OnDrillDown=BEGIN
                              IF O365SalesInvoiceMgmt.LookupContactFromSalesHeader(Rec) THEN
                                CurrPage.UPDATE(TRUE);
                            END;

                ShowCaption=No;
                QuickEntry=FALSE }

    { 69  ;1   ;Group     ;
                Name=SellToDeviceWithCustomerName;
                CaptionML=[ENU=Sell to;
                           NOR=Selg til;
                           SVE=Kund];
                Visible=IsDevice AND FieldsVisible;
                GroupType=Group }

    { 42  ;2   ;Field     ;
                Name=PhoneSellToCustomerName;
                CaptionML=[ENU=Customer Name;
                           NOR=Kundenavn;
                           SVE=Kundens namn];
                ToolTipML=[ENU=Specifies the customer's name.;
                           NOR=Angir kundens navn.;
                           SVE=Anger kundens namn.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Sell-to Customer Name";
                Importance=Promoted;
                Visible=IsDevice;
                LookupPageID=BC O365 Contact Lookup;
                OnValidate=BEGIN
                             O365SalesInvoiceMgmt.ValidateCustomerName(Rec,CustomerName,CustomerEmail);
                             O365SalesInvoiceMgmt.UpdateAddress(Rec,FullAddress,CountryRegionCode);

                             CurrPage.UPDATE(TRUE);
                           END;

                OnLookup=BEGIN
                           IF O365SalesInvoiceMgmt.LookupContactFromSalesHeader(Rec) THEN
                             CurrPage.UPDATE(TRUE);
                         END;

                ShowMandatory=TRUE;
                QuickEntry=FALSE }

    { 41  ;2   ;Field     ;
                Name=PhoneCustomerEmail;
                ExtendedDatatype=E-Mail;
                CaptionML=[ENU=Email Address;
                           NOR=E-postadresse;
                           SVE=E-postadress];
                ToolTipML=[ENU=Specifies the customer's email address.;
                           NOR=Angir kundens e-postadresse.;
                           SVE=Anger kundens e-postadress.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=CustomerEmail;
                Editable=CurrPageEditable AND (CustomerName <> '');
                OnValidate=BEGIN
                             O365SalesInvoiceMgmt.ValidateCustomerEmail(Rec,CustomerEmail);
                           END;
                            }

    { 68  ;2   ;Field     ;
                CaptionML=[ENU=Address;
                           NOR=Adresse;
                           SVE=Adress];
                ToolTipML=[ENU=Specifies the address of the customer.;
                           NOR=Angir adressen til kunden.;
                           SVE=Anger kundens adress.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=FullAddress;
                Editable=FALSE;
                OnAssistEdit=VAR
                               TempStandardAddress@1000 : TEMPORARY Record 730;
                             BEGIN
                               IF NOT CurrPageEditable THEN
                                 EXIT;
                               IF CustomerName = '' THEN
                                 EXIT;

                               CurrPage.SAVERECORD;
                               COMMIT;
                               TempStandardAddress.CopyFromSalesHeaderSellTo(Rec);
                               IF PAGE.RUNMODAL(PAGE::"O365 Address",TempStandardAddress) = ACTION::LookupOK THEN BEGIN
                                 FIND;
                                 O365SalesInvoiceMgmt.UpdateAddress(Rec,FullAddress,CountryRegionCode);
                                 // UpdateAddress changes Bill-To address without MODIFYing: make sure the next line is either MODIFY or CurrPage.UPDATE(TRUE);
                                 CurrPage.UPDATE(TRUE);
                               END;
                             END;
                              }

    { 20  ;1   ;Group     ;
                CaptionML=[ENU=Invoice Details;
                           NOR=Fakturadetaljer;
                           SVE=Fakturadetaljer];
                Visible=FieldsVisible;
                GroupType=Group }

    { 44  ;2   ;Field     ;
                CaptionML=[ENU=Expected Invoice No.;
                           NOR=Forventet fakturanr.;
                           SVE=FîrvÑntat fakturanummer];
                ToolTipML=[ENU=Specifies the number that your next sent invoice will get.;
                           NOR=Angir nummeret som din neste sendte faktura fÜr.;
                           SVE=Anger numret som nÑsta skickade faktura fÜr.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=NextInvoiceNo;
                Editable=FALSE }

    { 48  ;2   ;Field     ;
                CaptionML=[ENU=Payment Terms;
                           NOR=Betalingsbetingelser;
                           SVE=Betalningsvillkor];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Payment Terms Code";
                Enabled=CustomerName <> '';
                Editable=FALSE;
                OnAssistEdit=VAR
                               TempO365PaymentTerms@1000 : TEMPORARY Record 2153;
                             BEGIN
                               TempO365PaymentTerms.RefreshRecords;
                               IF TempO365PaymentTerms.GET("Payment Terms Code") THEN;
                               IF PAGE.RUNMODAL(PAGE::"O365 Payment Terms List",TempO365PaymentTerms) = ACTION::LookupOK THEN
                                 VALIDATE("Payment Terms Code",TempO365PaymentTerms.Code);
                             END;
                              }

    { 89  ;2   ;Field     ;
                CaptionML=[ENU=Payment Instructions;
                           NOR=Betalingsinstruksjoner;
                           SVE=Betalningsinstruktioner];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=PaymentInstructionsName;
                Enabled=CustomerName <> '';
                Editable=FALSE;
                OnAssistEdit=VAR
                               O365PaymentInstructions@1000 : Record 2155;
                             BEGIN
                               IF O365PaymentInstructions.GET("Payment Instructions Id") THEN;

                               IF PAGE.RUNMODAL(PAGE::"BC O365 Payment Instr. List",O365PaymentInstructions) = ACTION::LookupOK THEN BEGIN
                                 VALIDATE("Payment Instructions Id",O365PaymentInstructions.Id);
                                 SENDTRACETAG('00001SB',PaymentInstrCategoryLbl,
                                   VERBOSITY::Normal,PaymentInstrChangedTelemetryTxt,DATACLASSIFICATION::SystemMetadata);
                                 O365SalesInvoiceMgmt.GetPaymentInstructionsName("Payment Instructions Id",PaymentInstructionsName);
                               END;
                             END;
                              }

    { 26  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies when the sales invoice must be paid.;
                           NOR=Angir nÜr salgsfakturaen mÜ betales.;
                           SVE=Anger nÑr fîrsÑljningsfakturan mÜste vara betald.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Due Date";
                Editable=CustomerName <> '';
                OnValidate=BEGIN
                             IF "Due Date" < "Document Date" THEN
                               VALIDATE("Due Date","Document Date");
                           END;
                            }

    { 6   ;2   ;Field     ;
                CaptionML=[ENU=Invoice Date;
                           NOR=Fakturadato;
                           SVE=Fakturadatum];
                ToolTipML=[ENU=Specifies when the sales invoice was created.;
                           NOR=Angir nÜr salgsfakturaen ble opprettet.;
                           SVE=Anger nÑr fîrsÑljningsfakturan skapades.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Document Date";
                Editable=CustomerName <> '';
                OnValidate=VAR
                             NotificationLifecycleMgt@1001 : Codeunit 1511;
                             PastNotification@1000 : Notification;
                           BEGIN
                             VALIDATE("Posting Date","Document Date");

                             IF "Document Date" < WORKDATE THEN BEGIN
                               PastNotification.ID := DocumentDatePastWorkdateNotificationGuidTok;
                               PastNotification.MESSAGE(DocumentDatePastMsg);
                               PastNotification.SCOPE(NOTIFICATIONSCOPE::LocalScope);
                               NotificationLifecycleMgt.SendNotification(PastNotification,RECORDID);
                             END;
                           END;
                            }

    { 19  ;2   ;Field     ;
                CaptionML=[ENU=Customer is tax liable;
                           NOR=Kunde er skyldig mva;
                           SVE=Kunden Ñr momspliktig];
                ToolTipML=[ENU=Specifies if the sales invoice contains sales tax.;
                           NOR=Angir om salgsfakturaen inneholder salgsmva.;
                           SVE=Anger om fîrsÑljningsfakturan innehÜller moms.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Tax Liable";
                Visible=NOT IsUsingVAT;
                Editable=CustomerName <> '';
                OnValidate=BEGIN
                             CurrPage.UPDATE;
                           END;
                            }

    { 17  ;2   ;Field     ;
                Name=TaxAreaDescription;
                CaptionML=[ENU=Customer tax rate;
                           NOR=Mva-sats for kunde;
                           SVE=Kundens momssats];
                ToolTipML=[ENU=Specifies the customer's tax area.;
                           NOR=Angir kundens mva-omrÜde.;
                           SVE=Anger kundens skatteomrÜde.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                NotBlank=Yes;
                SourceExpr=TaxAreaDescription;
                Visible=NOT IsUsingVAT;
                Enabled=CustomerName <> '';
                Editable=FALSE;
                OnAssistEdit=VAR
                               TaxArea@1000 : Record 318;
                             BEGIN
                               IF TaxArea.GET("Tax Area Code") THEN;
                               IF PAGE.RUNMODAL(PAGE::"O365 Tax Area List",TaxArea) = ACTION::LookupOK THEN BEGIN
                                 VALIDATE("Tax Area Code",TaxArea.Code);
                                 TaxAreaDescription := TaxArea.GetDescriptionInCurrentLanguage;
                                 CurrPage.UPDATE;
                               END;
                             END;

                QuickEntry=FALSE }

    { 37  ;2   ;Field     ;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="VAT Registration No.";
                Visible=IsUsingVAT;
                Editable=(IsUsingVAT AND IsCompanyContact) }

    { 14  ;1   ;Part      ;
                Name=Lines;
                CaptionML=[ENU=Line Items;
                           NOR=Linjevarer;
                           SVE=Radartiklar];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SubPageLink=Document Type=FIELD(Document Type),
                            Document No.=FIELD(No.);
                PagePartID=Page2311;
                Visible=FieldsVisible;
                Enabled=CustomerName <> '';
                Editable=CustomerName <> '';
                PartType=Page }

    { 40  ;1   ;Group     ;
                CaptionML=[ENU=Totals;
                           NOR=Totaler;
                           SVE=Summor];
                Visible=FieldsVisible AND NOT InvDiscAmountVisible;
                GroupType=Group }

    { 39  ;2   ;Group     ;
                GroupType=Group }

    { 35  ;3   ;Field     ;
                Name=Amount;
                Lookup=No;
                DrillDown=No;
                CaptionML=[ENU=Net Total;
                           NOR=Nettosum;
                           SVE=Nettosumma];
                ToolTipML=[ENU=Specifies the total amount on the sales invoice excluding VAT.;
                           NOR=Angir det totale belõpet pÜ salgsfakturaen, ekskl. mva.;
                           SVE=Anger fîrsÑljningsfakturans totalbelopp, exklusive moms.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=Amount;
                AutoFormatType=10;
                AutoFormatExpr=CurrencyFormat }

    { 34  ;3   ;Field     ;
                Name=AmountInclVAT;
                Lookup=No;
                DrillDown=No;
                CaptionML=[ENU=Total Including VAT;
                           NOR=Sum inkludert mva.;
                           SVE=Summa inklusive moms];
                ToolTipML=[ENU=Specifies the total amount on the sales invoice including VAT.;
                           NOR=Angir det totale belõpet pÜ salgsfakturaen, inkl. mva.;
                           SVE=Anger fîrsÑljningsfakturans totalbelopp, inklusive moms.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Amount Including VAT";
                AutoFormatType=10;
                AutoFormatExpr=CurrencyFormat;
                Importance=Promoted;
                Style=Strong;
                StyleExpr=TRUE }

    { 36  ;3   ;Field     ;
                Name=DiscountLink;
                DrillDown=Yes;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=DiscountTxt;
                Importance=Promoted;
                Enabled=CustomerName <> '';
                Editable=FALSE;
                OnDrillDown=BEGIN
                              IF PAGE.RUNMODAL(PAGE::"O365 Sales Invoice Discount",Rec) = ACTION::LookupOK THEN
                                CurrPage.UPDATE;
                            END;

                ShowCaption=No }

    { 30  ;1   ;Group     ;
                CaptionML=[ENU=Totals;
                           NOR=Totaler;
                           SVE=Summor];
                Visible=FieldsVisible AND InvDiscAmountVisible;
                GroupType=Group }

    { 32  ;2   ;Group     ;
                GroupType=Group }

    { 31  ;3   ;Field     ;
                CaptionML=[ENU=Subtotal;
                           NOR=Delsum;
                           SVE=Delsumma];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=SubTotalAmount;
                AutoFormatType=10;
                AutoFormatExpr=CurrencyFormat;
                Importance=Promoted;
                Editable=FALSE }

    { 29  ;3   ;Field     ;
                Name=InvoiceDiscount;
                CaptionML=[ENU=Invoice Discount;
                           NOR=Fakturarabatt;
                           SVE=Fakturarabatt];
                ToolTipML=[ENU=Specifies the invoice discount amount. To edit the invoice discount, click on the amount.;
                           NOR=Angir fakturarabattbelõpet. Hvis du vil redigere fakturarabatten, klikker du belõpet.;
                           SVE=Anger fakturarabattens belopp. Klicka pÜ beloppet om vill redigera fakturarabatten.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=-InvoiceDiscountAmount;
                AutoFormatType=10;
                AutoFormatExpr=CurrencyFormat;
                CaptionClass=GetInvDiscountCaption;
                Importance=Promoted;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              IF PAGE.RUNMODAL(PAGE::"O365 Sales Invoice Discount",Rec) = ACTION::LookupOK THEN
                                CurrPage.UPDATE;
                            END;
                             }

    { 28  ;3   ;Field     ;
                Name=Amount2;
                Lookup=No;
                DrillDown=No;
                CaptionML=[ENU=Net Total;
                           NOR=Nettosum;
                           SVE=Nettosumma];
                ToolTipML=[ENU=Specifies the total amount on the sales invoice excluding VAT.;
                           NOR=Angir det totale belõpet pÜ salgsfakturaen, ekskl. mva.;
                           SVE=Anger fîrsÑljningsfakturans totalbelopp, exklusive moms.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=Amount;
                AutoFormatType=10;
                AutoFormatExpr=CurrencyFormat }

    { 21  ;3   ;Field     ;
                Name=AmountInclVAT2;
                Lookup=No;
                DrillDown=No;
                CaptionML=[ENU=Total Including VAT;
                           NOR=Sum inkludert mva.;
                           SVE=Summa inklusive moms];
                ToolTipML=[ENU=Specifies the total amount on the sales invoice including VAT.;
                           NOR=Angir det totale belõpet pÜ salgsfakturaen, inkl. mva.;
                           SVE=Anger fîrsÑljningsfakturans totalbelopp, inklusive moms.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Amount Including VAT";
                AutoFormatType=10;
                AutoFormatExpr=CurrencyFormat;
                Importance=Promoted;
                Style=Strong;
                StyleExpr=TRUE }

    { 7   ;1   ;Group     ;
                CaptionML=[ENU=Note and attachments;
                           NOR=Merknad og vedlegg;
                           SVE=Noteringar och bilagor];
                Visible=FieldsVisible;
                GroupType=Group }

    { 10  ;2   ;Group     ;
                GroupType=Group }

    { 27  ;3   ;Field     ;
                CaptionML=[ENU=Note for customer;
                           NOR=Notat til kunde;
                           SVE=AnmÑrkning fîr kund];
                ToolTipML=[ENU=Specifies the products or service being offered;
                           NOR=Angir produktene eller tjenestene som tilbys;
                           SVE=Anger erbjudna produkter eller tjÑnster];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=WorkDescription;
                Editable=CurrPageEditable AND (CustomerName <> '');
                MultiLine=Yes;
                OnValidate=BEGIN
                             SetWorkDescription(WorkDescription);
                           END;
                            }

    { 13  ;3   ;Field     ;
                Name=NoOfAttachments;
                DrillDown=Yes;
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=NoOfAttachmentsValueTxt;
                Importance=Promoted;
                Enabled=CustomerName <> '';
                Editable=FALSE;
                OnDrillDown=BEGIN
                              O365SalesInvoiceMgmt.UpdateNoOfAttachmentsLabel(O365SalesAttachmentMgt.EditAttachments(Rec),NoOfAttachmentsValueTxt);
                              CurrPage.UPDATE(FALSE);
                            END;

                ShowCaption=No }

    { 990 ;    ;Container ;
                ContainerType=FactBoxArea }

    { 999 ;1   ;Part      ;
                Name=CustomerStatisticsFactBox;
                CaptionML=[ENU=Customer statistics;
                           NOR=Kundestatistikk;
                           SVE=Kundstatistik];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SubPageLink=No.=FIELD(Sell-to Customer No.);
                PagePartID=Page2306;
                PartType=Page }

  }
  CODE
  {
    VAR
      SalesReceivablesSetup@1027 : Record 311;
      O365SalesAttachmentMgt@1006 : Codeunit 2112;
      ClientTypeManagement@1048 : Codeunit 4030;
      O365SalesInvoiceMgmt@1023 : Codeunit 2310;
      NoSeriesManagement@1028 : Codeunit 396;
      CustomerName@1000 : Text[100];
      CustomerEmail@1004 : Text[80];
      WorkDescription@1002 : Text;
      FullAddress@1016 : Text;
      CurrPageEditable@1003 : Boolean;
      IsUsingVAT@6116 : Boolean;
      ForceExit@1012 : Boolean;
      NoOfAttachmentsValueTxt@1011 : Text;
      CurrencyFormat@1013 : Text;
      IsCompanyContact@1090 : Boolean;
      InvDiscAmountVisible@1024 : Boolean;
      InvoiceDiscountAmount@1001 : Decimal;
      SubTotalAmount@1017 : Decimal;
      DiscountTxt@1005 : Text;
      TaxAreaDescription@1018 : Text[50];
      DocumentDatePastMsg@1008 : TextConst 'ENU=Invoice date is in the past.;NOR=Fakturadato er i fortiden.;SVE=Fakturadatum har infallit.';
      ViewContactDetailsLbl@1007 : TextConst 'ENU=Open customer details;NOR=èpne kundedetaljer;SVE=ôppna kunddetaljer';
      TestInvoiceVisible@1019 : Boolean;
      IsNew@1021 : Boolean;
      IsDevice@1049 : Boolean;
      CountryRegionCode@1022 : Code[10];
      PaymentInstructionsName@1095 : Text[20];
      NextInvoiceNo@1026 : Code[20];
      PickExistingCustomerLbl@1014 : TextConst 'ENU=Choose existing customer;NOR=Velg eksisterende kunde;SVE=VÑlj befintlig kund';
      TestInvTypeTxt@1025 : TextConst 'ENU=Test Invoice;NOR=Testfaktura;SVE=Testfaktura';
      DraftInvTypeTxt@1029 : TextConst 'ENU=Draft Invoice;NOR=Lag fakturakladd;SVE=Fakturautkast';
      IsCustomerBlocked@1031 : Boolean;
      PaymentInstrCategoryLbl@1020 : TextConst '@@@={LOCKED};ENU=AL Payment Instructions;NOR=AL Payment Instructions;SVE=AL Payment Instructions';
      PaymentInstrChangedTelemetryTxt@1030 : TextConst '@@@={LOCKED};ENU=Payment instructions were changed for an invoice.;NOR=Payment instructions were changed for an invoice.;SVE=Payment instructions were changed for an invoice.';
      DocumentDatePastWorkdateNotificationGuidTok@1010 : TextConst '@@@={Locked};ENU=cfa9edd9-03d7-4bbb-ba07-a90660c28772;NOR=cfa9edd9-03d7-4bbb-ba07-a90660c28772;SVE=cfa9edd9-03d7-4bbb-ba07-a90660c28772';
      FieldsVisible@1009 : Boolean;

    [External]
    PROCEDURE SuppressExitPrompt@5();
    BEGIN
      ForceExit := TRUE;
    END;

    LOCAL PROCEDURE GetInvDiscountCaption@8() : Text;
    BEGIN
      EXIT(O365SalesInvoiceMgmt.GetInvoiceDiscountCaption(ROUND("Invoice Discount Value",0.1)));
    END;

    LOCAL PROCEDURE GetInvTypeCaption@1() : Text;
    BEGIN
      IF IsTest THEN
        EXIT(TestInvTypeTxt);
      EXIT(DraftInvTypeTxt);
    END;

    BEGIN
    END.
  }
}

