OBJECT Codeunit 454 Job Queue - Send Notification
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03;
  }
  PROPERTIES
  {
    TableNo=472;
    OnRun=VAR
            RecordLink@1000 : Record 2000000068;
            PermissionManager@1001 : Codeunit 9002;
            IsHandled@1002 : Boolean;
          BEGIN
            IsHandled := FALSE;
            OnBeforeRun(Rec,RecordLink,IsHandled);
            IF IsHandled THEN
              EXIT;

            IF PermissionManager.SoftwareAsAService THEN
              EXIT;

            RecordLink."Link ID" := 0;
            RecordLink."Record ID" := RECORDID;
            IF Status = Status::Error THEN
              RecordLink.Description := COPYSTR(GetErrorMessage,1,MAXSTRLEN(RecordLink.Description))
            ELSE
              RecordLink.Description := Description;
            SetURL(Rec,RecordLink);
            RecordLink.Type := RecordLink.Type::Note;
            RecordLink.Created := CURRENTDATETIME;
            RecordLink."User ID" := USERID;
            RecordLink.Company := COMPANYNAME;
            RecordLink.Notify := TRUE;
            RecordLink."To User ID" := "User ID";
            SetText(Rec,RecordLink);
            RecordLink.INSERT;

            OnAfterRun(Rec,RecordLink);
          END;

  }
  CODE
  {
    VAR
      ErrorWhenProcessingTxt@1002 : TextConst 'ENU=Error when processing ''%1''.;NOR=Feil under behandlingen av %1.;SVE=Fel vid behandling av ''%1''.';
      ErrorMessageLabelTxt@1000 : TextConst 'ENU=Error message:;NOR=Feilmelding:;SVE=Felmeddelande:';
      JobQueueFinishedTxt@1001 : TextConst '@@@="%1 = job description, e.g. ''Post Sales Order 1234''";ENU=''%1'' finished successfully.;NOR=%1 er fullf›rt.;SVE=%1 slutf”rdes.';

    [External]
    PROCEDURE SetJobQueueEntryStatusToOnHold@1(ModifyOnlyWhenReadOnlyNotification@1001 : Notification);
    VAR
      JobQueueEntry@1000 : Record 472;
    BEGIN
      IF NOT ModifyOnlyWhenReadOnlyNotification.HASDATA(JobQueueEntry.FIELDNAME(ID)) THEN
        EXIT;

      IF JobQueueEntry.GET(ModifyOnlyWhenReadOnlyNotification.GETDATA(JobQueueEntry.FIELDNAME(ID))) THEN
        JobQueueEntry.SetStatus(JobQueueEntry.Status::"On Hold");
    END;

    LOCAL PROCEDURE SetURL@3(VAR JobQueueEntry@1002 : Record 472;VAR RecordLink@1000 : Record 2000000068);
    VAR
      Link@1001 : Text;
    BEGIN
      WITH JobQueueEntry DO BEGIN
        // Generates a URL such as dynamicsnav://host:port/instance/company/runpage?page=672&$filter=
        // The intent is to open up the Job Queue Entries page and show the list of "my errors".
        // TODO: Leverage parameters ",JobQueueEntry,TRUE)" to take into account the filters, which will add the
        // following to the Url: &$filter=JobQueueEntry.Status IS 2 AND JobQueueEntry."User ID" IS <UserID>.
        // This will also require setting the filters on the record, such as:
        // SETFILTER(Status,'=2');
        // SETFILTER("User ID",'=%1',"User ID");
        Link := GETURL(DEFAULTCLIENTTYPE,COMPANYNAME,OBJECTTYPE::Page,PAGE::"Job Queue Entries") +
          STRSUBSTNO('&$filter=''%1''.''%2''%20IS%20''2''%20AND%20''%1''.''%3''%20IS%20''%4''&mode=View',
            HtmlEncode(TABLENAME),HtmlEncode(FIELDNAME(Status)),HtmlEncode(FIELDNAME("User ID")),HtmlEncode("User ID"));

        RecordLink.URL1 := COPYSTR(Link,1,MAXSTRLEN(RecordLink.URL1));
        IF STRLEN(Link) > MAXSTRLEN(RecordLink.URL1) THEN
          RecordLink.URL2 := COPYSTR(Link,MAXSTRLEN(RecordLink.URL1) + 1,MAXSTRLEN(RecordLink.URL2));
      END;
    END;

    LOCAL PROCEDURE SetText@4(VAR JobQueueEntry@1001 : Record 472;VAR RecordLink@1000 : Record 2000000068);
    VAR
      TypeHelper@1002 : Codeunit 10;
      s@1007 : Text;
      lf@1006 : Text;
      c1@1005 : Byte;
    BEGIN
      WITH JobQueueEntry DO BEGIN
        c1 := 13;
        lf[1] := c1;

        IF Status = Status::Error THEN
          s := STRSUBSTNO(ErrorWhenProcessingTxt,Description) + lf + ErrorMessageLabelTxt + ' ' + GetErrorMessage
        ELSE
          s := STRSUBSTNO(JobQueueFinishedTxt,Description);

        TypeHelper.WriteRecordLinkNote(RecordLink,s);
      END;
    END;

    LOCAL PROCEDURE HtmlEncode@20(InText@1000 : Text[1024]) : Text[1024];
    VAR
      SystemWebHttpUtility@1001 : DotNet "'System.Web, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b03f5f7f11d50a3a'.System.Web.HttpUtility";
    BEGIN
      SystemWebHttpUtility := SystemWebHttpUtility.HttpUtility;
      EXIT(SystemWebHttpUtility.HtmlEncode(InText));
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterRun@2(JobQueueEntry@1000 : Record 472;RecordLink@1001 : Record 2000000068);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeRun@5(VAR JobQueueEntry@1000 : Record 472;VAR RecordLink@1001 : Record 2000000068;VAR IsHandled@1002 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

