OBJECT Table 11072019 Damping Ratio Project
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Damping Ratio Project;
               NOR=Dempingsgrad, prosjekt;
               SVE=D„mpningsgrad, projekt];
    LookupPageID=Page11020293;
    DrillDownPageID=Page11020293;
  }
  FIELDS
  {
    { 10  ;   ;Until Production    ;Integer       ;CaptionML=[ENU=Until Production %;
                                                              NOR=Til produksjon %;
                                                              SVE=Till produktion %] }
    { 20  ;   ;Ratio               ;Decimal       ;OnValidate=BEGIN
                                                                IF Ratio <> 0 THEN
                                                                  TESTFIELD(Formula, '');
                                                              END;

                                                   CaptionML=[ENU=Ratio;
                                                              NOR=Ratio;
                                                              SVE=F”rh†llande] }
    { 30  ;   ;Damping Ratio Code  ;Code10        ;TableRelation="Damping Ratio";
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Damping Ratio Code;
                                                   NotBlank=Yes }
    { 40  ;   ;Formula             ;Text250       ;OnValidate=BEGIN
                                                                IF Formula <> '' THEN
                                                                  TESTFIELD(Ratio, 0);
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Formula;
                                                              SVE=Formel] }
  }
  KEYS
  {
    {    ;Damping Ratio Code,Until Production     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100529601 : TextConst 'ENU=No matching closing bracket found for a opening bracket.;SVE=Det gick inte att hitta n†gon avslutande parentes f”r en inledande parentes.';
      Text002@1100529600 : TextConst 'ENU=''%1'' is not a valid parameter.';

    PROCEDURE EvaluateFormula@1100485001(Formula@1100485000 : Text[250];ProgressPercentage@1100529600 : Decimal) : Decimal;
    VAR
      Math@1100485002 : Codeunit 11020204;
      LocDec@1100529604 : Decimal;
      OpeningBracketPos@1100529601 : Integer;
      ClosingBracketPos@1100529602 : Integer;
      RemainderFormula@1100485003 : Text;
      VariableTxt@1100485006 : Text;
      NewFormula@1100485007 : Text;
    BEGIN
      RemainderFormula := Formula;

      // find parameters
      OpeningBracketPos := STRPOS(RemainderFormula, '[');
      IF OpeningBracketPos = 0 THEN
        NewFormula := RemainderFormula;
      WHILE OpeningBracketPos > 0 DO BEGIN
        NewFormula := NewFormula + COPYSTR(RemainderFormula, 1, OpeningBracketPos - 1);
        ClosingBracketPos := STRPOS(RemainderFormula, ']');
        IF (ClosingBracketPos = 0) OR (ClosingBracketPos < OpeningBracketPos) THEN
          //No matching closing bracket found for an opening bracket
          ERROR(Text001);
        VariableTxt := COPYSTR(RemainderFormula, OpeningBracketPos + 1, ClosingBracketPos - 1 - OpeningBracketPos);
        IF UPPERCASE(VariableTxt) <> 'P' THEN
          ERROR(Text002, VariableTxt);
        NewFormula := NewFormula + STRSUBSTNO('%1', ProgressPercentage);

        //find next opening bracket
        IF ClosingBracketPos = STRLEN(RemainderFormula) THEN BEGIN
          RemainderFormula := '';
          OpeningBracketPos := 0;
        END ELSE BEGIN
          RemainderFormula := COPYSTR(RemainderFormula, ClosingBracketPos + 1);
          OpeningBracketPos := STRPOS(RemainderFormula, '[');
        END;
      END;
      NewFormula := NewFormula + RemainderFormula;

      //Evaluate formula
      LocDec := Math.EvaluateFormula(NewFormula);
      IF LocDec <= 0 THEN
        EXIT(0);
      EXIT(LocDec * 100);
    END;

    BEGIN
    END.
  }
}

