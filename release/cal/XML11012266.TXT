OBJECT XMLport 11012266 Export Hours LosaKraan
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Export Hours Payroll;
               SVE=Exportera timmar Payroll];
    Direction=Export;
    OnInitXMLport=BEGIN
                    HumanResourcesSetup.GET;
                    HumanResourcesSetup.TESTFIELD("Exchange Salary Application");
                    HumanResourcesSetup.TESTFIELD("Salary Application",HumanResourcesSetup."Salary Application"::"LOSA-Kraan");
                  END;

    OnPreXMLport=BEGIN
                   IF gvDocument = '' THEN
                     ERROR(Text002);

                   IF gvEmployer = '' THEN
                     ERROR(Text007);

                   PathText := HumanResourcesSetup."Pathname Files to Salary";
                   currXMLport.FILENAME(PathText + 'LSU' + gvEmployer + Text003);

                   IF FileMgt.ClientFileExists(currXMLport.FILENAME) THEN
                     IF NOT FileMgt.ExistingFileMustBeReplaced(currXMLport.FILENAME, TRUE) THEN
                       ERROR('');

                   FillExportBuffer;
                 END;

    OnPostXMLport=BEGIN
                    BufferHourExport.DELETEALL;
                    COMMIT;
                  END;

    Format=Fixed Text;
  }
  ELEMENTS
  {
    { [{D4F04B97-74C5-44DA-BB5B-2FC485C37E94}];  ;Root                ;Element ;Text     }

    { [{B62276AB-C6F4-41B6-A721-183DDC8BC71A}];1 ;BufferHourExport    ;Element ;Table   ;
                                                  SourceTable=Table11012042;
                                                  SourceTableView=SORTING(Field10);
                                                  Export::OnPreXMLItem=BEGIN
                                                                         IF NOT "Buffer Hour Export".FINDFIRST THEN
                                                                           MESSAGE(Text004);
                                                                       END;

                                                  Export::OnAfterGetRecord=VAR
                                                                             RecRef@1100528400 : RecordRef;
                                                                             WorkNoTotal@1100529200 : Code[13];
                                                                             TotalHours@1100529201 : Decimal;
                                                                           BEGIN
                                                                             RecRef.GETTABLE("Buffer Hour Export");
                                                                             XMLportStatusDialogMgt.ShowExportStatus(RecRef);

                                                                             WITH "Buffer Hour Export" DO BEGIN
                                                                               TotalHours := (Monday + Tuesday + Wednesday + Thursday + Friday + Saturday + Sunday) * 100;
                                                                               IF TotalHours = 0 THEN
                                                                                 currXMLport.SKIP;
                                                                               WorkNoTotal := FillWithZero("Project No.",13);

                                                                               RecordType := 'LSU';                                                  //  1
                                                                               CompanyNo := COPYSTR(gvEmployer,1,3);                                 //  2
                                                                               ProcesGroup := COPYSTR(gvEmployer,4,2);                               //  3
                                                                               IF Employee.GET("Employee No.") THEN                                  //  4
                                                                                 SalaryNo := FillWithZero(Employee."Salary No.",6);
                                                                               SubNo := FillWithZero('',1);                                          //  5
                                                                               Year := FillWithZero(FORMAT(DATE2DMY("Posting Date",3)),4);           //  6
                                                                               Month := FillWithZero(FORMAT(DATE2DMY("Posting Date",2)),2);          //  7
                                                                               Day := FillWithZero(FORMAT(DATE2DMY("Posting Date",1)),2);            //  8
                                                                               FixedTime := '07:30';                                                 //  9
                                                                               WeekNo := FillWithZero(FORMAT(Week),2);                               // 10
                                                                               Hours := FillWithZero(FORMAT(ROUND(TotalHours,1)),5);                 // 11
                                                                               IF TotalHours > 0 THEN
                                                                                 HourSign := '+'                                                     // 12
                                                                               ELSE
                                                                                 HourSign := '-';
                                                                               WageComponent := FillWithZero("Wage Component",3);                    // 13
                                                                               WorkNo := COPYSTR(WorkNoTotal,1,5);                                   // 14
                                                                               ControlNo := COPYSTR(WorkNoTotal,6,8);                                // 15
                                                                               AccountNo := FillWithZero("G/L Account No.",6);                       // 16
                                                                               Department := '';                                                     // 17 Specs not clear.
                                                                               CostObject := FillWithZero("Cost Object",4);                          // 18
                                                                               FACode := '';                                                         // 19
                                                                               SupplyingCompany := '';                                               // 20 Specs not clear.
                                                                               RateCode := FillWithZero('',5);                                       // 21
                                                                               RateCodeSign := '';                                                   // 22
                                                                               Rate := FillWithZero('',5);                                           // 23
                                                                               RateSign := '';                                                       // 24
                                                                               HourRate := FillWithZero('',5);                                       // 25
                                                                               HourRateSign := '';                                                   // 26
                                                                               Reserved := '';                                                       // 27
                                                                               //ClosingRecord                                                       // 28 Default on every line except last line.
                                                                             END;
                                                                           END;
                                                                            }

    { [{BC4202F9-BA2D-4579-8C18-2E6ADC281AC8}];2 ;RecordType          ;Element ;Text    ;
                                                  Width=3 }

    { [{95BA8605-0F1C-4A4E-8B62-AF3184C6664C}];2 ;CompanyNo           ;Element ;Text    ;
                                                  Width=3 }

    { [{A395E61A-0487-4EED-8898-62353D84FF87}];2 ;ProcesGroup         ;Element ;Text    ;
                                                  Width=2 }

    { [{6E896DAA-2E12-4EF4-A3D5-DAA0CFC32CFD}];2 ;SalaryNo            ;Element ;Text    ;
                                                  DataType=Code;
                                                  Width=6 }

    { [{E3268D69-B16E-403C-80C3-0E59528260B5}];2 ;SubNo               ;Element ;Text    ;
                                                  Width=1 }

    { [{91AA0E08-E32C-49FD-9436-50A95C51854D}];2 ;Year                ;Element ;Text    ;
                                                  Width=4 }

    { [{C01FD4CB-6021-48A1-B6F7-372DCB7E2157}];2 ;Month               ;Element ;Text    ;
                                                  Width=2 }

    { [{E2BB8E60-5EBE-4939-B45E-5D375CD5AF7A}];2 ;Day                 ;Element ;Text    ;
                                                  DataType=Integer;
                                                  Width=2 }

    { [{1EBF2838-8F42-4DC1-9AEC-36A545BC11C7}];2 ;FixedTime           ;Element ;Text    ;
                                                  DataType=Integer;
                                                  Width=5 }

    { [{B06C028D-7A84-4D8A-AAC9-73185C3755A7}];2 ;WeekNo              ;Element ;Text    ;
                                                  Width=2 }

    { [{E83DE4E1-385C-4F42-AED3-2A27CF3EBFAD}];2 ;Hours               ;Element ;Text    ;
                                                  DataType=Code;
                                                  Width=5 }

    { [{07A5B67E-6D69-4004-B66D-B9012E2521A5}];2 ;HourSign            ;Element ;Text    ;
                                                  DataType=Code;
                                                  Width=1 }

    { [{46127DA6-3BC1-4C14-91A2-61789C26D231}];2 ;WageComponent       ;Element ;Text    ;
                                                  DataType=Code;
                                                  Width=3 }

    { [{A45DBC60-2A25-4C6C-AEB3-66A8663952D7}];2 ;WorkNo              ;Element ;Text    ;
                                                  DataType=Code;
                                                  Width=5 }

    { [{BF1A4C24-D737-414D-8C6C-F9E3CDF4E85B}];2 ;ControlNo           ;Element ;Text    ;
                                                  DataType=Code;
                                                  Width=8 }

    { [{938947AF-A1B3-4876-8A11-6EEF6F64D0DC}];2 ;AccountNo           ;Element ;Text    ;
                                                  Width=6 }

    { [{7500FC37-D964-4DB2-BCBC-61BB16E5B006}];2 ;Department          ;Element ;Text    ;
                                                  Width=4 }

    { [{6B34EAB2-91F7-4B5C-B627-E74D8ACCC220}];2 ;CostObject          ;Element ;Text    ;
                                                  DataType=Code;
                                                  Width=4 }

    { [{CA652ACC-AB9B-4CD6-8541-58F78C21BEF0}];2 ;FACode              ;Element ;Text    ;
                                                  DataType=Decimal;
                                                  Width=8 }

    { [{B3AED9B0-2222-4509-B21C-CA82AE96AC03}];2 ;SupplyingCompany    ;Element ;Text    ;
                                                  DataType=Date;
                                                  Width=3 }

    { [{0E234300-A1CE-4C93-99EF-69DA802B7EFA}];2 ;RateCode            ;Element ;Text    ;
                                                  DataType=Decimal;
                                                  Width=5 }

    { [{38B28B42-F7C1-4190-8AA9-BC6B338F3CC9}];2 ;RateCodeSign        ;Element ;Text    ;
                                                  DataType=Decimal;
                                                  Width=1 }

    { [{13717F17-FE89-4282-9B74-07A7920EADC1}];2 ;Rate                ;Element ;Text    ;
                                                  DataType=Decimal;
                                                  Width=5 }

    { [{5ED490AA-36DE-4520-8DCA-8822ECF460A6}];2 ;RateSign            ;Element ;Text    ;
                                                  DataType=Decimal;
                                                  Width=1 }

    { [{15D5ADF5-9ACB-408F-81C7-4FC7AFA1728E}];2 ;HourRate            ;Element ;Text    ;
                                                  DataType=Decimal;
                                                  Width=5 }

    { [{C820E8AB-AFD7-46E2-9415-78985A4A21D6}];2 ;HourRateSign        ;Element ;Text    ;
                                                  DataType=Decimal;
                                                  Width=1 }

    { [{8E24DD67-3D6D-434C-97D9-488465D34907}];2 ;Reserved            ;Element ;Text    ;
                                                  Width=155 }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      Employee@1100528416 : Record 5200;
      ExportedHour@1100528415 : Record 11012041;
      HumanResourcesSetup@1100528414 : Record 5218;
      PostedHourLine@1100528413 : Record 11012085;
      BufferHourExport@1100528411 : Record 11012042;
      CostCode@1100528409 : Record 11012083;
      XMLportStatusDialogMgt@1100528423 : Codeunit 11130006;
      FileMgt@1100528424 : Codeunit 419;
      PathText@1100528408 : Text[50];
      gvDocument@1100528407 : Text[250];
      gvEmployer@1100528406 : Text[250];
      LineNo@1100528404 : Integer;
      LastDocNo@1100528402 : Code[20];
      ExportDateTime@1100528400 : DateTime;
      Text002@1100528419 : TextConst 'ENU=Fill out Document Number.;SVE=Fyll i dokumentnummer.';
      Text003@1100528418 : TextConst 'ENU=.ASC';
      Text004@1100528417 : TextConst 'ENU=No hours present for this Document Number;SVE=Det finns inga timmar f”r det h„r dokumentnumret';
      Text007@1100529200 : TextConst 'ENU=Fill out Employer Code.';

    PROCEDURE SetSelection@10(DocumentNo@11012000 : Code[20]);
    BEGIN
      gvDocument := DocumentNo;
    END;

    PROCEDURE SetGlobalVars@1100528401(Document2@1100528400 : Text[250];Employer2@1100528401 : Text[250]);
    BEGIN
      gvDocument := Document2;
      gvEmployer := Employer2;
    END;

    PROCEDURE UpdateStatus@1(lvYear@11012001 : Integer;lvWeek@11012002 : Integer;lvEmpl@11012003 : Code[20];lvLine@11012004 : Integer);
    BEGIN
    END;

    PROCEDURE FillExportBuffer@1100485001();
    BEGIN
      BufferHourExport.LOCKTABLE;
      BufferHourExport.DELETEALL;
      LineNo := 0;
      ExportDateTime := CURRENTDATETIME;
      ExportedHour.RESET;

      ReadHourLines;
      COMMIT;
    END;

    PROCEDURE ReadHourLines@1100485002();
    BEGIN
      LastDocNo := '';

      WITH PostedHourLine DO BEGIN
        SETCURRENTKEY("Document No." ,"Employee No.");
        IF gvDocument <> '' THEN
          SETFILTER("Document No.",gvDocument);
        IF gvEmployer <> '' THEN
          SETFILTER(Employer,gvEmployer);
        SETFILTER("Cost Type", '%1|%2', "Cost Type"::Labor, "Cost Type"::Sundry);
        SETFILTER(External, '%1', FALSE);
        SETRANGE("Component Type","Component Type"::Hours);

        IF FINDSET(FALSE,FALSE) THEN BEGIN
          REPEAT
            FillLogBook("Document No.");
            IF Monday <> 0 THEN
              FillBufferHourExport(PostedHourLine,DWY2DATE(1,PostedHourLine.Week,PostedHourLine.Year));
            IF Tuesday <> 0 THEN
              FillBufferHourExport(PostedHourLine,DWY2DATE(2,PostedHourLine.Week,PostedHourLine.Year));
            IF Wednesday <> 0 THEN
              FillBufferHourExport(PostedHourLine,DWY2DATE(3,PostedHourLine.Week,PostedHourLine.Year));
            IF Thursday <> 0 THEN
              FillBufferHourExport(PostedHourLine,DWY2DATE(4,PostedHourLine.Week,PostedHourLine.Year));
            IF Friday <> 0 THEN
              FillBufferHourExport(PostedHourLine,DWY2DATE(5,PostedHourLine.Week,PostedHourLine.Year));
            IF Saturday <> 0 THEN
              FillBufferHourExport(PostedHourLine,DWY2DATE(6,PostedHourLine.Week,PostedHourLine.Year));
            IF Sunday <> 0 THEN
              FillBufferHourExport(PostedHourLine,DWY2DATE(7,PostedHourLine.Week,PostedHourLine.Year));
            Status := Status::Export;
            Exported := TRUE;
            "Exported on" := ExportDateTime;
            MODIFY;
          UNTIL NEXT = 0;
        END;
      END;
    END;

    PROCEDURE FillLogBook@33(CurrDocNo@1100485001 : Code[20]);
    VAR
      lvLineNo@1100485002 : Integer;
    BEGIN
      IF CurrDocNo = LastDocNo THEN EXIT;

      IF ExportedHour.FIND('+') THEN
        lvLineNo := ExportedHour."Line No." + 1
      ELSE
        lvLineNo := 1;

      ExportedHour.INIT;
      ExportedHour."Line No." := lvLineNo;
      ExportedHour."Document No." := CurrDocNo;
      ExportedHour."User ID" := USERID;
      ExportedHour.Date := TODAY;
      ExportedHour.Time := TIME;
      ExportedHour.INSERT;

      LastDocNo := ExportedHour."Document No.";
    END;

    PROCEDURE FillBufferHourExport@1100529230(PostedHourLine@1100529201 : Record 11012085;DateOfDay@1100529202 : Date);
    BEGIN
      WITH BufferHourExport DO BEGIN
        SETRANGE("Document No.",PostedHourLine."Document No.");
        SETRANGE(Employer,PostedHourLine.Employer);
        SETRANGE("Employee No.",PostedHourLine."Employee No.");
        SETRANGE("Wage Component",PostedHourLine."Wage Component");
        SETRANGE("Posting Date",DateOfDay);
        SETRANGE("Cost Object",PostedHourLine."Cost Object");
        CASE PostedHourLine.Type OF
          PostedHourLine.Type::Project,PostedHourLine.Type::Service:
            SETRANGE("Project No.",DetermineProjectNo);
          PostedHourLine.Type::Indirect:
            SETRANGE("G/L Account No.",DetermineAccountNo);
        END;
        IF NOT FINDFIRST THEN
          InsertBufferHourExport(PostedHourLine,DateOfDay);

        UpdateBufferHourExport(PostedHourLine,DateOfDay);
      END;
    END;

    PROCEDURE InsertBufferHourExport@1100529232(PostedHourLine@1100529200 : Record 11012085;DateOfDay@1100529202 : Date);
    BEGIN
      CLEAR(BufferHourExport);
      WITH BufferHourExport DO BEGIN
        LineNo += 1;
        "Line No." := LineNo;
        "Document No." := PostedHourLine."Document No.";
        Employer := PostedHourLine.Employer;
        "Employee No." := PostedHourLine."Employee No.";
        "Posting Date" := DateOfDay;
        "Wage Component" := PostedHourLine."Wage Component";
        "Cost Object" := PostedHourLine."Cost Object";
        CASE PostedHourLine.Type OF
          PostedHourLine.Type::Project,PostedHourLine.Type::Service:
            "Project No." := DetermineProjectNo;
          PostedHourLine.Type::Indirect:
            "G/L Account No." := DetermineAccountNo;
        END;
        Week := PostedHourLine.Week;
        INSERT;
      END;
    END;

    PROCEDURE UpdateBufferHourExport@1100529211(PostedHourLine@1100529200 : Record 11012085;DateOfDay@1100529202 : Date);
    BEGIN
      CLEAR(BufferHourExport);
      WITH BufferHourExport DO BEGIN
        SETRANGE("Document No.",PostedHourLine."Document No.");
        SETRANGE(Employer,PostedHourLine.Employer);
        SETRANGE("Employee No.",PostedHourLine."Employee No.");
        SETRANGE("Posting Date",DateOfDay);
        SETRANGE("Wage Component",PostedHourLine."Wage Component");
        SETRANGE("Cost Object",PostedHourLine."Cost Object");
        CASE PostedHourLine.Type OF
          PostedHourLine.Type::Project,PostedHourLine.Type::Service:
            SETRANGE("Project No.",DetermineProjectNo);
          PostedHourLine.Type::Indirect:
            SETRANGE("G/L Account No.",DetermineAccountNo);
        END;
        IF FINDSET(TRUE,FALSE) THEN BEGIN
          CASE DateOfDay OF
            DWY2DATE(1,PostedHourLine.Week,PostedHourLine.Year): Monday := Monday + PostedHourLine.Monday;
            DWY2DATE(2,PostedHourLine.Week,PostedHourLine.Year): Tuesday := Tuesday + PostedHourLine.Tuesday;
            DWY2DATE(3,PostedHourLine.Week,PostedHourLine.Year): Wednesday := Wednesday + PostedHourLine.Wednesday;
            DWY2DATE(4,PostedHourLine.Week,PostedHourLine.Year): Thursday := Thursday + PostedHourLine.Thursday;
            DWY2DATE(5,PostedHourLine.Week,PostedHourLine.Year): Friday := Friday + PostedHourLine.Friday;
            DWY2DATE(6,PostedHourLine.Week,PostedHourLine.Year): Saturday := Saturday + PostedHourLine.Saturday;
            DWY2DATE(7,PostedHourLine.Week,PostedHourLine.Year): Sunday := Sunday + PostedHourLine.Sunday;
          END;
          MODIFY;
        END;
      END;
    END;

    PROCEDURE FillWithZero@1100529210(SourceTxt@1100529200 : Text[30];FixedLength@1100529201 : Integer) : Text[30];
    VAR
      NewString@1100529202 : Text[30];
    BEGIN
      NewString := SourceTxt;
      WHILE STRLEN(NewString) < FixedLength DO BEGIN
        NewString := '0' + NewString;
      END;

      EXIT(NewString);
    END;

    PROCEDURE DetermineProjectNo@1100529201() : Code[20];
    BEGIN
      IF PostedHourLine."Project No." <> '' THEN
        EXIT(PostedHourLine."Project No.")
      ELSE
        IF PostedHourLine."Service Order No." <> '' THEN
          EXIT('S~'+PostedHourLine."Service Order No.");
    END;

    PROCEDURE DetermineAccountNo@1100529202() : Code[20];
    VAR
      CostCode@1100529200 : Record 11012083;
    BEGIN
      IF PostedHourLine."Cost Code" <> '' THEN BEGIN
        IF CostCode.GET(PostedHourLine."Cost Code") THEN
          EXIT(CostCode."Account No.");
      END;
    END;

    BEGIN
    END.
  }
}

