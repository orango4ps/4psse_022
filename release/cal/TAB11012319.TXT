OBJECT Table 11012319 Item Relation
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               "Last Date Modified" := TODAY;
               IF FPSLicenseManagement.LicenseAndReadPermissionForTable(CompRelationRec) THEN
                 CentrManagementCU.EditItemRelation(Rec,1,xRec);
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;
               IF FPSLicenseManagement.LicenseAndReadPermissionForTable(CompRelationRec) THEN
                 CentrManagementCU.EditItemRelation(Rec,2,xRec);
             END;

    OnDelete=BEGIN
               IF FPSLicenseManagement.LicenseAndReadPermissionForTable(CompRelationRec) THEN
                 CentrManagementCU.EditItemRelation(Rec,4,xRec);
               StoreDeletedData('D', COMPANYNAME);
             END;

    OnRename=BEGIN
               "Last Date Modified" := TODAY;
               StoreDeletedData('R', COMPANYNAME);
             END;

    CaptionML=[ENU=Item Relation;
               NOR=Artikkelrelasjon;
               SVE=Artikelrelation];
    LookupPageID=Page11012318;
    DrillDownPageID=Page11012318;
  }
  FIELDS
  {
    { 10  ;   ;Navision Item       ;Code20        ;TableRelation=Item.No.;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Description Navision-item");

                                                                IF FPSLicenseManagement.LicenseAndReadPermissionForTable(CompRelationRec) AND ("Navision Item" <> xRec."Navision Item") THEN BEGIN
                                                                  ItemRelRec := xRec;
                                                                  IF ItemRelRec.FIND THEN
                                                                    CentrManagementCU.EditItemRelation(Rec,3,xRec);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Navision Item;
                                                              NOR=Navision-artikkel;
                                                              SVE=Navision-artikel];
                                                   NotBlank=Yes }
    { 20  ;   ;Description Navision-item;Text100  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Item.Description WHERE (No.=FIELD(Navision Item)));
                                                   CaptionML=[ENU=Description Navision-item;
                                                              NOR=Beskrivelse, Navision-artikkel;
                                                              SVE=Beskrivning, Navision-artikel];
                                                   Editable=No }
    { 30  ;   ;Basic Item          ;Code20        ;TableRelation="Basic Item"."Product Code" WHERE (Manufacturer=FIELD(Manufacturer));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Product Class",Series,"Item Brand");
                                                                IF CurrFieldNo <> 0 THEN
                                                                  UpdateGTINCode;
                                                              END;

                                                   CaptionML=[ENU=Basic Item;
                                                              NOR=Fabrikantartikkel;
                                                              SVE=Basartikel] }
    { 40  ;   ;Manufacturer        ;Code15        ;TableRelation="ETIM Vendor - Central Mgt";
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Product Class",Series,"Item Brand");
                                                              END;

                                                   CaptionML=[ENU=Manufacturer;
                                                              NOR=Produsent;
                                                              SVE=Tillverkare] }
    { 50  ;   ;Trade Item          ;Code20        ;TableRelation="Trade Item"."Item Code" WHERE (Vendor=FIELD(Vendor));
                                                   OnValidate=VAR
                                                                NavisionItemNo@1100528400 : Code[20];
                                                              BEGIN
                                                                IF CurrFieldNo <> 0 THEN
                                                                  UpdateGTINCode;
                                                                IF "Trade Item" <> '' THEN
                                                                  IF GUIALLOWED THEN
                                                                    IF TradeItemIsRelatedToDifferentNavisionItem(NavisionItemNo) THEN
                                                                      IF NOT CONFIRM(
                                                                        Text011, FALSE, FIELDCAPTION("Trade Item"), "Trade Item",
                                                                        FIELDCAPTION("Navision Item"), NavisionItemNo)
                                                                      THEN
                                                                        ERROR('');
                                                              END;

                                                   CaptionML=[ENU=Trade Item;
                                                              NOR=Katalogartikkel;
                                                              SVE=Anskaffningsartikel] }
    { 55  ;   ;Vendor              ;Code15        ;TableRelation="ETIM Vendor - Central Mgt";
                                                   CaptionML=[ENU=Vendor;
                                                              NOR=Leverand›r;
                                                              SVE=Leverant”r] }
    { 60  ;   ;Product Class       ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Basic Item"."Product Class" WHERE (Product Code=FIELD(Basic Item),
                                                                                                          Manufacturer=FIELD(Manufacturer)));
                                                   CaptionML=[ENU=Product Class;
                                                              NOR=Produktklasse;
                                                              SVE=Produktklass];
                                                   Editable=No }
    { 70  ;   ;Series              ;Text40        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Item."Item Series" WHERE (No.=FIELD(Navision Item)));
                                                   CaptionML=[ENU=Series;
                                                              NOR=Serier;
                                                              SVE=Serier];
                                                   Editable=No }
    { 75  ;   ;Item Type           ;Text40        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Item."Item Type" WHERE (No.=FIELD(Navision Item)));
                                                   CaptionML=[ENU=Item Type;
                                                              NOR=Artikkeltype;
                                                              SVE=Artikeltyp];
                                                   Editable=No }
    { 90  ;   ;Item Brand          ;Text40        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Item."Item Brand" WHERE (No.=FIELD(Navision Item)));
                                                   CaptionML=[ENU=Item Brand;
                                                              NOR=Artikkelens Varemerke;
                                                              SVE=Artikelns varum„rke];
                                                   Editable=No }
    { 100 ;   ;Source Type         ;Option        ;CaptionML=[ENU=Source Type;
                                                              NOR=Kildetype;
                                                              SVE=Ursprungstyp];
                                                   OptionCaptionML=[ENU=Basic Item,Trade Item;
                                                                    NOR=Fabrikantartikkel,Katalogartikkel;
                                                                    SVE=Basartikel,Anskaffningsartikel];
                                                   OptionString=Basic Item,Trade Item }
    { 110 ;   ;Cost Object (Basic Item);Code20    ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Basic Item"."Cost Object" WHERE (Product Code=FIELD(Basic Item),
                                                                                                        Manufacturer=FIELD(Manufacturer)));
                                                   CaptionML=[ENU=Cost Object (Basic Item);
                                                              NOR=Kostnadsobjekt (fabrikantartikkel);
                                                              SVE=Kostnadsobjekt (basartikel)];
                                                   Editable=No }
    { 111 ;   ;Cost Object (Trade Item);Code20    ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Trade Item"."Cost Object" WHERE (Item Code=FIELD(Trade Item),
                                                                                                        Vendor=FIELD(Vendor)));
                                                   CaptionML=[ENU=Cost Object (Trade Item);
                                                              NOR=Kostnadsobjekt (katalogartikkel);
                                                              SVE=Kostnadsobjekt (skaffningsartikel)];
                                                   Editable=No }
    { 120 ;   ;GTIN Code           ;Code20        ;OnValidate=VAR
                                                                lvTradeItemRec@1210190000 : Record 11012317;
                                                              BEGIN
                                                                IF ("GTIN Code" <> '') AND ("Source Type" = "Source Type"::"Basic Item") THEN BEGIN
                                                                  lvTradeItemRec.RESET;
                                                                  lvTradeItemRec.SETCURRENTKEY("GTIN Code (Item)");
                                                                  lvTradeItemRec.SETRANGE("GTIN Code (Item)", "GTIN Code");
                                                                  IF NOT lvTradeItemRec.FINDFIRST THEN BEGIN
                                                                    ERROR(Text000);
                                                                  END ELSE BEGIN
                                                                    lvTradeItemRec.SETRANGE("Product Code", "Basic Item");
                                                                    lvTradeItemRec.SETRANGE(Manufacturer, Manufacturer);
                                                                    IF NOT lvTradeItemRec.FINDFIRST THEN
                                                                      MESSAGE(Text000);
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=GTIN Code;
                                                              NOR=GTIN-kode;
                                                              SVE=GTIN] }
    { 130 ;   ;Generated Link      ;Boolean       ;CaptionML=[ENU=Generated Link;
                                                              NOR=Generert Link;
                                                              SVE=Genererad l„nk] }
    { 140 ;   ;Expired (Basic Item);Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Basic Item".Expired WHERE (Product Code=FIELD(Basic Item),
                                                                                                  Manufacturer=FIELD(Manufacturer)));
                                                   CaptionML=[ENU=Expired (Basic Item);
                                                              NOR=Utl›pt (fabrikantartikkel)];
                                                   Editable=No }
    { 141 ;   ;Expired (Trade Item);Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Trade Item".Expired WHERE (Item Code=FIELD(Trade Item),
                                                                                                  Vendor=FIELD(Vendor)));
                                                   CaptionML=[ENU=Expired (Trade Item);
                                                              NOR=Utl›pt (katalogartikkel)];
                                                   Editable=No }
    { 150 ;   ;Last Date Modified  ;Date          ;DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Last Date Modified;
                                                              SVE=Uppdateringsdatum];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Navision Item                           ;Clustered=Yes }
    {    ;Manufacturer,Basic Item                  }
    {    ;Vendor,Trade Item                        }
    {    ;GTIN Code                                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1210190000 : TextConst 'ENU=No Trade Item present for GTIN Code;NOR=Det finnes ingen katalogartikkel for GTIN-koden;SVE=Det finns ingen skaffningsartikel f”r GTIN-koden';
      ItemRelRec@1100485000 : Record 11012319;
      CompRelationRec@1210190002 : Record 11012059;
      CentrManagementCU@1210190003 : Codeunit 11012010;
      Text001@1210190008 : TextConst 'ENU=Item        #1################# @2@@@@@@@@@@@@@;NOR=Artikkel        #1################# @2@@@@@@@@@@@@@;SVE=Artikel     #1################# @2@@@@@@@@@@@@@';
      Text010@1100525000 : TextConst 'ENU=Update Item Relation;NOR=Oppdater artikkelrelasjon;SVE=Uppdatera artikelrelation';
      Text011@1100528400 : TextConst 'ENU=%1 %2 is already related to %3 %4. Do you want to continue?';
      FPSLicenseManagement@1100527400 : Codeunit 11229289;

    PROCEDURE UpdateItemRelation@1210190000(VAR gvMainRec@1210190005 : Record 11012319;SkipStatus@1100485003 : Boolean);
    VAR
      lvMainRec@1100485000 : Record 11012319;
      lvItemRec@1100485006 : Record 27;
      lvBasicItemRec@1100485004 : Record 11012316;
      lvTradeItemRec@1100485005 : Record 11012317;
      lvType@1210190000 : 'BasicItem,TradeItem';
      Status@1210190003 : Dialog;
      currec@1210190002 : Integer;
      maxrec@1210190001 : Integer;
      LogRec@1100525001 : Record 11012335;
      StartDateLog@1100525000 : DateTime;
    BEGIN
      WITH gvMainRec DO BEGIN
        IF NOT SkipStatus THEN BEGIN
          MARKEDONLY(TRUE);
          IF NOT FINDFIRST THEN
            MARKEDONLY(FALSE);
          maxrec := COUNT;
          IF maxrec = 0 THEN
            EXIT;
          FINDSET(TRUE, TRUE);
          Status.OPEN(Text001);
          StartDateLog := CURRENTDATETIME;  //db, 08-03-13: C006329
        END ELSE BEGIN
          //if SkipStatus=TRUE then only 1 Item Relation is processed (import Trade Items).
          maxrec := 1;
        END;

        currec := 0;
        REPEAT
          currec := currec + 1;
          IF NOT SkipStatus THEN BEGIN
            Status.UPDATE(1, "Navision Item");
            Status.UPDATE(2,ROUND(currec / maxrec * 10000,1));
          END;

          IF lvItemRec.GET("Navision Item") THEN BEGIN
            CASE "Source Type" OF
              "Source Type"::"Trade Item":
                BEGIN
                  IF lvTradeItemRec.GET(Vendor, "Trade Item") THEN BEGIN
                    lvMainRec := gvMainRec;
                    lvMainRec.Manufacturer := lvTradeItemRec.Manufacturer;
                    lvMainRec."Basic Item" := lvTradeItemRec."Product Code";
                    lvMainRec."GTIN Code" := lvTradeItemRec."GTIN Code (Item)";
                    lvMainRec.MODIFY(TRUE);
                    IF NOT lvBasicItemRec.GET(lvTradeItemRec.Manufacturer, lvTradeItemRec."Product Code") THEN
                      lvBasicItemRec.INIT;
                    lvType := lvType::TradeItem;
                    IF lvItemRec.Blocked = FALSE THEN
                      lvItemRec.ProcessItemRelation(
                        lvType, lvItemRec."No.", lvItemRec."No. Series", lvBasicItemRec, lvTradeItemRec);  //db, 19-12-11: M31760
                  END;
                END;
              "Source Type"::"Basic Item":
                BEGIN
                  IF lvBasicItemRec.GET(Manufacturer, "Basic Item") THEN BEGIN
                    IF NOT lvTradeItemRec.GET(Manufacturer, "Basic Item") THEN
                       lvTradeItemRec.INIT;
                    lvType := lvType::BasicItem;
                    IF lvItemRec.Blocked = FALSE THEN
                      lvItemRec.ProcessItemRelation(
                        lvType, lvItemRec."No.", lvItemRec."No. Series", lvBasicItemRec, lvTradeItemRec);  //db, 19-12-11: M31760
                  END;
                END;
            END;
          END;
        UNTIL CheckEndUntil(gvMainRec,SkipStatus);

        IF NOT SkipStatus THEN BEGIN
          CLEARMARKS;
          Status.CLOSE;
          LogRec.UpdateLogbook(Text010, maxrec, 0, 0, StartDateLog);  //db, 08-03-13: C006329
        END;
      END;
    END;

    LOCAL PROCEDURE CheckEndUntil@1100485004(VAR MainRec@1100485001 : Record 11012319;SkipStatus@1100485000 : Boolean) : Boolean;
    BEGIN
      IF SkipStatus THEN
        EXIT(TRUE);

      EXIT(MainRec.NEXT = 0);
    END;

    PROCEDURE UpdateGTINCode@1100485000();
    VAR
      lvBasicItemRec@1100485000 : Record 11012316;
      lvTradeItemRec@1100485001 : Record 11012317;
    BEGIN
      IF "Source Type" = "Source Type"::"Basic Item" THEN BEGIN
        IF lvBasicItemRec.GET(Manufacturer, "Basic Item") THEN
          "GTIN Code" := lvBasicItemRec."GTIN Code";
      END;

      IF "Source Type" = "Source Type"::"Trade Item" THEN BEGIN
        IF lvTradeItemRec.GET(Vendor, "Trade Item") THEN
          "GTIN Code" := lvTradeItemRec."GTIN Code (Item)";
      END;
    END;

    LOCAL PROCEDURE TradeItemIsRelatedToDifferentNavisionItem@1100528400(VAR NavisionItemNo@1100528401 : Code[20]) : Boolean;
    VAR
      ItemRelation@1100528400 : Record 11012319;
    BEGIN
      ItemRelation.SETCURRENTKEY(Vendor, "Trade Item");
      ItemRelation.SETRANGE(Vendor, Vendor);
      ItemRelation.SETRANGE("Trade Item", "Trade Item");
      ItemRelation.SETFILTER("Navision Item", '<>%1', "Navision Item");
      IF ItemRelation.FINDFIRST THEN BEGIN
        NavisionItemNo := ItemRelation."Navision Item";
        EXIT(TRUE);
      END;
      EXIT(FALSE);
    END;

    LOCAL PROCEDURE StoreDeletedData@1100528703(Mode@1100528001 : Code[1];Company@1100528000 : Text[50]);
    VAR
      InstallationManagement@1100528700 : Codeunit 11012268;
      DeletedDataMgtApps@1100528701 : Codeunit 11012440;
    BEGIN
      IF NOT InstallationManagement.LicensePermissionForCodeUnit(CODEUNIT::"Deleted Data Mgt (Apps)") THEN
        EXIT;

      IF (Mode = 'D') THEN
        DeletedDataMgtApps.StoreDeletedItemRelation(Rec, Company);

      IF (Mode = 'R') THEN
        IF (xRec."Navision Item" = '') THEN
          DeletedDataMgtApps.StoreDeletedItemRelation(Rec, Company)
        ELSE
          DeletedDataMgtApps.StoreDeletedItemRelation(xRec, Company);
    END;

    BEGIN
    END.
  }
}

