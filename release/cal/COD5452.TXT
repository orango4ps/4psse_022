OBJECT Codeunit 5452 Graph Sync. Runner
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            RunFullSync;
          END;

  }
  CODE
  {
    VAR
      GraphDataSetup@1000 : Codeunit 5455;
      ALGraphSyncSynchronouslyCategoryTxt@1001 : TextConst '@@@={Locked};ENU=AL Graph Sync Synchronously;NOR=AL Graph Sync Synchronously';

    [External]
    PROCEDURE IsGraphSyncEnabled@4() : Boolean;
    VAR
      MarketingSetup@1000 : Record 5079;
      AuxSyncEnabled@1001 : Boolean;
    BEGIN
      IF CURRENTEXECUTIONMODE = EXECUTIONMODE::Debug THEN
        EXIT(FALSE);

      IF NOT MarketingSetup.GET THEN
        EXIT(FALSE);

      IF MarketingSetup."Sync with Microsoft Graph" THEN
        EXIT(TRUE);

      OnCheckAuxiliarySyncEnabled(AuxSyncEnabled);
      EXIT(AuxSyncEnabled);
    END;

    [External]
    PROCEDURE RunDeltaSync@10();
    BEGIN
      OnRunGraphDeltaSync;
    END;

    [External]
    PROCEDURE RunDeltaSyncForEntity@3(TableID@1002 : Integer);
    VAR
      IntegrationTableMapping@1000 : Record 5335;
      IntegrationMappingCode@1001 : Code[20];
    BEGIN
      IntegrationMappingCode := GraphDataSetup.GetMappingCodeForTable(TableID);
      GraphDataSetup.GetIntegrationTableMapping(IntegrationTableMapping,IntegrationMappingCode);
      IF NOT IntegrationTableMapping.IsFullSyncAllowed THEN
        EXIT;

      IntegrationTableMapping.SetFullSyncStartAndCommit;
      RunIntegrationTableSynch(IntegrationTableMapping);

      IntegrationTableMapping.GET(IntegrationTableMapping.Name);
      IntegrationTableMapping.SetFullSyncEndAndCommit;

      OnAfterRunDeltaSyncForEntity(TableID);
    END;

    [External]
    PROCEDURE RunFullSync@11();
    BEGIN
      OnRunGraphFullSync;
    END;

    [External]
    PROCEDURE RunFullSyncForEntity@2(TableID@1002 : Integer);
    VAR
      IntegrationTableMapping@1000 : Record 5335;
      IntegrationMappingCode@1001 : Code[20];
    BEGIN
      IntegrationMappingCode := GraphDataSetup.GetMappingCodeForTable(TableID);
      GraphDataSetup.GetIntegrationTableMapping(IntegrationTableMapping,IntegrationMappingCode);
      IF NOT IntegrationTableMapping.IsFullSyncAllowed THEN
        EXIT;
      IntegrationTableMapping.SetFullSyncStartAndCommit;

      IntegrationTableMapping."Graph Delta Token" := '';
      IntegrationTableMapping.MODIFY(TRUE);

      CreateIntegrationRecordsForUncoupledRecords(IntegrationTableMapping."Table ID");
      RunIntegrationTableSynch(IntegrationTableMapping);

      IntegrationTableMapping.GET(IntegrationTableMapping.Name);
      IntegrationTableMapping.SetFullSyncEndAndCommit;

      OnAfterRunFullSyncForEntity(TableID);
    END;

    [External]
    PROCEDURE RunIntegrationTableSynch@1(IntegrationTableMapping@1000 : Record 5335);
    VAR
      IntegrationManagement@1001 : Codeunit 5150;
      InsertEnabled@1005 : Boolean;
      ModifyEnabled@1004 : Boolean;
      DeleteEnabled@1003 : Boolean;
      RenameEnabled@1002 : Boolean;
    BEGIN
      IntegrationManagement.GetDatabaseTableTriggerSetup(
        IntegrationTableMapping."Table ID",InsertEnabled,ModifyEnabled,DeleteEnabled,RenameEnabled);
      CODEUNIT.RUN(IntegrationTableMapping."Synch. Codeunit ID",IntegrationTableMapping);
    END;

    LOCAL PROCEDURE CreateIntegrationRecordsForUncoupledRecords@7(TableId@1000 : Integer);
    VAR
      IntegrationRecord@1002 : Record 5151;
      NavRecordRef@1001 : RecordRef;
    BEGIN
      NavRecordRef.OPEN(TableId);

      IF NavRecordRef.FINDSET THEN
        REPEAT
          CLEAR(IntegrationRecord);
          IntegrationRecord.SETRANGE("Record ID",NavRecordRef.RECORDID);
          IF IntegrationRecord.ISEMPTY THEN BEGIN
            CLEAR(IntegrationRecord);
            IntegrationRecord."Record ID" := NavRecordRef.RECORDID;
            IntegrationRecord."Table ID" := NavRecordRef.NUMBER;
            IntegrationRecord.INSERT(TRUE);
          END;
        UNTIL NavRecordRef.NEXT = 0;
    END;

    [Integration]
    LOCAL PROCEDURE OnCheckAuxiliarySyncEnabled@8(VAR AuxSyncEnabled@1000 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnRunGraphDeltaSync@6();
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnRunGraphFullSync@5();
    BEGIN
    END;

    [External]
    PROCEDURE SyncFromGraphSynchronously@9(CodeunitId@1005 : Integer;TimeoutInSeconds@1006 : Integer);
    VAR
      SessionId@1004 : Integer;
      StartDateTime@1001 : DateTime;
      TimePassed@1000 : Duration;
      TimeoutReached@1002 : Boolean;
    BEGIN
      // Start session will use CPU time of main thread while main thread is SLEEPing
      // Taskscheduler cannot be used since it requires a COMMIT to start
      SessionId := 0;

      IF NOT STARTSESSION(SessionId,CodeunitId,COMPANYNAME) THEN BEGIN
        OnSyncSynchronouslyCannotStartSession('Codeunit: ' + FORMAT(CodeunitId));
        EXIT;
      END;

      StartDateTime := CURRENTDATETIME;

      REPEAT
        SLEEP(300);
        IF NOT ISSESSIONACTIVE(SessionId) THEN
          EXIT;

        TimePassed := CURRENTDATETIME - StartDateTime;
        TimeoutReached := TimePassed > TimeoutInSeconds * 1000;
      UNTIL TimeoutReached;

      OnSyncSynchronouslyTimeout('Codeunit: ' + FORMAT(CodeunitId));
    END;

    [External]
    PROCEDURE GetDefaultSyncSynchronouslyTimeoutInSeconds@12() : Integer;
    BEGIN
      // User is waiting for the sync to complete
      // This value should not be too great
      EXIT(30);
    END;

    [Integration]
    LOCAL PROCEDURE OnSyncSynchronouslyCannotStartSession@17(AdditionalDetails@1000 : Text);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnSyncSynchronouslyTimeout@18(AdditionalDetails@1000 : Text);
    BEGIN
    END;

    [EventSubscriber(Codeunit,5452,OnSyncSynchronouslyCannotStartSession)]
    LOCAL PROCEDURE HandleOnSyncSynchronouslyCannotStartSession@22(AdditionalDetails@1000 : Text);
    BEGIN
      SENDTRACETAG(
        '00001KX',ALGraphSyncSynchronouslyCategoryTxt,VERBOSITY::Error,
        'Could not start the session. ' + AdditionalDetails,DATACLASSIFICATION::SystemMetadata);
    END;

    [EventSubscriber(Codeunit,5452,OnSyncSynchronouslyTimeout)]
    LOCAL PROCEDURE HandleOnSyncSynchronouslySessionTimeout@23(AdditionalDetails@1000 : Text);
    BEGIN
      SENDTRACETAG(
        '00001KY',ALGraphSyncSynchronouslyCategoryTxt,VERBOSITY::Error,
        'Timeout on the Forced Graph Sync. ' + AdditionalDetails,DATACLASSIFICATION::SystemMetadata);
    END;

    [Integration]
    [External]
    PROCEDURE OnAfterRunDeltaSyncForEntity@13(TableId@1000 : Integer);
    BEGIN
    END;

    [Integration]
    [External]
    PROCEDURE OnAfterRunFullSyncForEntity@14(TableId@1000 : Integer);
    BEGIN
    END;

    BEGIN
    END.
  }
}

