OBJECT Page 11126866 Copy Base Activity
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Grundaktivit„t kopieren;
               ENU=Copy Base Activity;
               NLD=Standaard hoofdcode kopi‰ren];
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table11125682;
    PageType=StandardDialog;
    OnOpenPage=BEGIN
                 PartToSetEditable;
               END;

    OnQueryClosePage=BEGIN
                       CloseActionOK := CloseAction IN [ACTION::OK,ACTION::LookupOK];
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1210190000;1;Group  ;
                CaptionML=[DEU=Kopieren von;
                           ENU=Copy from;
                           NLD=Kopi‰ren van;
                           NOR=Kopier fra;
                           SVE=Kopiera fr†n];
                GroupType=Group }

    { 22  ;2   ;Field     ;
                CaptionML=[DEU=Teilgruppe;
                           ENU=Part Group;
                           NLD=Onderdeelgroep;
                           NOR=Del gruppe;
                           SVE=Delgrupp];
                SourceExpr=PartGrpFrom;
                OnValidate=BEGIN
                             IF NOT GrpRec.GET(PartGrpFrom, GrpRec.Type::Part) THEN
                               ERROR(Text003)
                             ELSE
                               PartGrpFromTxt := GrpRec.Description;
                           END;

                OnLookup=BEGIN
                           GrpRec.SETRANGE(Type, GrpRec.Type::Part);
                           IF PAGE.RUNMODAL(PAGE::"Estimate Groups", GrpRec) = ACTION::LookupOK THEN BEGIN
                             PartGrpFrom := GrpRec."No.";
                             PartGrpFromTxt := GrpRec.Description;
                           END;
                         END;
                          }

    { 23  ;2   ;Field     ;
                CaptionML=[DEU=Beschreibung Teilgruppe;
                           ENU=Part Group Description;
                           NLD=Onderdeelgroepomschrijving;
                           NOR=Delgruppe, Beskrivelse;
                           SVE=Delgrupp, beskrivning];
                SourceExpr=PartGrpFromTxt;
                Editable=FALSE }

    { 1210190001;2;Field  ;
                Name=PartFrom;
                CaptionML=[DEU=Teil;
                           ENU=Part;
                           NLD=Onderdeel;
                           NOR=Del;
                           SVE=Del];
                SourceExpr=PartFrom;
                OnValidate=BEGIN
                             PartFromOnAfterValidate;
                             PartToSetEditable;
                           END;

                OnLookup=BEGIN
                           PartRec.RESET;
                           PartRec.SETRANGE("Part Group", PartGrpFrom);
                           IF PAGE.RUNMODAL(0, PartRec) = ACTION::LookupOK THEN BEGIN
                             PartFrom := PartRec.Activity;
                             PartFromTxt := PartRec.Description;
                             PartTo := PartFrom;
                             PartToTxt := PartFromTxt;
                           END;
                           PartToSetEditable;
                         END;
                          }

    { 30  ;2   ;Field     ;
                CaptionML=[DEU=Teilbeschreibung;
                           ENU=Part Description;
                           NLD=Onderdeelomschrijving;
                           NOR=Delbeskrivelse;
                           SVE=Delbeskrivning];
                SourceExpr=PartFromTxt;
                Editable=FALSE }

    { 1210190002;1;Group  ;
                CaptionML=[DEU=Kopieren in;
                           ENU=Copy to;
                           NLD=Kopi‰ren naar;
                           NOR=Kopier til;
                           SVE=Kopiera till];
                GroupType=Group }

    { 26  ;2   ;Field     ;
                CaptionML=[DEU=Teilgruppe;
                           ENU=Part Group;
                           NLD=Onderdeelgroep;
                           NOR=Del gruppe;
                           SVE=Delgrupp];
                SourceExpr=PartGrpTo;
                OnValidate=BEGIN
                             IF NOT GrpRec.GET(PartGrpTo, GrpRec.Type::Part) THEN
                               ERROR(Text003)
                             ELSE
                               PartGrpToTxt := GrpRec.Description;
                           END;

                OnLookup=BEGIN
                           GrpRec.SETRANGE(Type, GrpRec.Type::Part);
                           IF PAGE.RUNMODAL(PAGE::"Estimate Groups", GrpRec) = ACTION::LookupOK THEN BEGIN
                              PartGrpTo := GrpRec."No.";
                              PartGrpToTxt := GrpRec.Description;
                           END;
                         END;
                          }

    { 27  ;2   ;Field     ;
                CaptionML=[DEU=Beschreibung Teilgruppe;
                           ENU=Part Group Description;
                           NLD=Onderdeelgroepomschrijving;
                           NOR=Delgruppe, Beskrivelse;
                           SVE=Delgrupp, beskrivning];
                SourceExpr=PartGrpToTxt;
                Editable=FALSE }

    { 1210190003;2;Field  ;
                Name=PartTo;
                CaptionML=[DEU=Teil;
                           ENU=Part;
                           NLD=Onderdeel;
                           NOR=Del;
                           SVE=Del];
                SourceExpr=PartTo;
                Editable=PartToEditable;
                OnLookup=BEGIN
                           PartRec.RESET;
                           PartRec.SETRANGE("Part Group", PartGrpTo);
                           PAGE.RUNMODAL(0, PartRec);
                         END;
                          }

    { 33  ;2   ;Field     ;
                Name=PartToTxt;
                CaptionML=[DEU=Teilbeschreibung;
                           ENU=Part Description;
                           NLD=Onderdeelomschrijving;
                           NOR=Delbeskrivelse;
                           SVE=Delbeskrivning];
                SourceExpr=PartToTxt;
                Editable=PartToTxtEditable }

  }
  CODE
  {
    VAR
      Text000@1100485011 : TextConst 'DEU=Besch„ftigt mit #1################# @2@@@@@@@@@@@@@;ENU=Busy with            #1################# @2@@@@@@@@@@@@@;NLD=Bezig met           #1################# @2@@@@@@@@@@@@@;NOR=Opptatt med            #1################# @2@@@@@@@@@@@@@;SVE=Upptagen med  #1################# @2@@@@@@@@@@@@@';
      Text003@11012044 : TextConst 'DEU=Teilgruppe nicht vorhanden;ENU=Part Group not present;NLD=Onderdeelgroep niet aanwezig;NOR=Delgruppen finnes ikke;SVE=Delgruppen finns inte';
      Text005@11012046 : TextConst 'DEU=Teil nicht vorhanden;ENU=Part does not exist;NLD=Onderdeel niet aanwezig;NOR=Delen finnes ikke;SVE=Delen finns inte';
      PartGrpFrom@11012009 : Code[20];
      PartGrpTo@11012010 : Code[20];
      PartFrom@11012011 : Code[20];
      PartTo@11012012 : Code[20];
      PartGrpFromTxt@11012023 : Text[50];
      PartGrpToTxt@11012024 : Text[50];
      PartFromTxt@11012025 : Text[100];
      PartToTxt@11012026 : Text[100];
      GrpRec@11012032 : Record 11012157;
      PartRec@1100485000 : Record 11125682;
      PartRec2@1100485001 : Record 11125682;
      Status@1100485010 : Dialog;
      NoOfRecords@1100485009 : Integer;
      LineCount@1100485008 : Integer;
      PartToTxtEditable@1100525000 : Boolean INDATASET;
      PartToEditable@1100525001 : Boolean INDATASET;
      CloseActionOK@1100528400 : Boolean;
      TextRec@1100525002 : Record 11125684;
      TextRec2@1100525003 : Record 11125684;

    PROCEDURE FillDefaults@1(lPartGrpFrom@11012002 : Code[20];lPartFrom@11012003 : Code[20]);
    BEGIN
      PartGrpFrom := lPartGrpFrom;
      PartFrom  := lPartFrom;

      PartGrpTo := PartGrpFrom;
      PartTo := PartFrom;

      IF GrpRec.GET(PartGrpFrom, GrpRec.Type::Part) THEN BEGIN
        PartGrpFromTxt := GrpRec.Description;
        PartGrpToTxt := GrpRec.Description;
      END;

      IF PartRec.GET(PartGrpFrom, PartFrom) THEN BEGIN
        PartFromTxt := PartRec.Description;
        PartToTxt := PartRec.Description;
      END;

      PartToSetEditable;
    END;

    PROCEDURE StartUpdate@2();
    BEGIN
      Status.OPEN(Text000);

      PartRec.SETRANGE("Part Group", PartGrpFrom);
      IF PartFrom <> '' THEN
        PartRec.SETRANGE(Activity, PartFrom);
      IF PartRec.FIND('-') THEN BEGIN
        LineCount := 0;
        NoOfRecords := PartRec.COUNT;
        REPEAT
          PartRec2.COPY(PartRec);
          PartRec2."Part Group" := PartGrpTo;
          IF PartFrom <> '' THEN BEGIN
            PartRec2.Activity := PartTo;
            PartRec2.Description := PartToTxt;
          END;
          PartRec2.INSERT(TRUE);
          //
          TextRec.SETRANGE("Table No.", DATABASE::"Constr. Spec. Activity RAW");
          TextRec.SETRANGE("Part Group", PartRec."Part Group");
          TextRec.SETRANGE(Activity, PartRec.Activity);
          TextRec.SETFILTER("Estimate No.", '%1', '');
          TextRec.SETFILTER("Tender Paragraph No.", '%1', '');
          TextRec.SETRANGE("Tender Paragraph Line", 0);
          TextRec.SETRANGE("Estimate Line No.", 0);
          IF TextRec.FINDSET THEN BEGIN
            REPEAT
              TextRec2.COPY(TextRec);
              TextRec2."Part Group" := PartRec2."Part Group";
              TextRec2.Activity := PartRec2.Activity;
              TextRec2.INSERT;
            UNTIL TextRec.NEXT = 0;
          END;
          CopyDetails(PartRec."Part Group", PartRec.Activity, PartRec2."Part Group", PartRec2.Activity);
          //
          LineCount := LineCount + 1;
          Status.UPDATE(1, PartRec2."Part Group" + ' - ' + PartRec2.Activity);
          Status.UPDATE(2,ROUND(LineCount / NoOfRecords * 10000,1));
        UNTIL PartRec.NEXT = 0;
      END;

      Status.CLOSE;
    END;

    PROCEDURE CopyDetails@1100528000(OldPartGrp@1100485000 : Code[20];OldPart@1100485001 : Code[20];NewPartGrp@1100485002 : Code[20];NewPart@1100485003 : Code[20]);
    VAR
      PartLineRec@1100528005 : Record 11125683;
      PartLineRec2@1100528004 : Record 11125683;
    BEGIN
      PartLineRec.SETRANGE("Part Group", OldPartGrp);
      PartLineRec.SETRANGE(Activity, OldPart);
      IF PartLineRec.FIND('-') THEN BEGIN
        REPEAT
          PartLineRec2.COPY(PartLineRec);
          PartLineRec2."Part Group" := NewPartGrp;
          PartLineRec2.Activity := NewPart;
          PartLineRec2.INSERT(TRUE);
        UNTIL PartLineRec.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE PartFromOnAfterValidate@19035741();
    BEGIN
      IF NOT PartRec.GET(PartGrpFrom, PartFrom) THEN
        IF PartFrom = '' THEN
          PartRec.INIT
        ELSE
          ERROR(Text005);

      PartFromTxt := PartRec.Description;
      PartTo := PartFrom;
      PartToTxt := PartFromTxt;
    END;

    LOCAL PROCEDURE PartToSetEditable@19044010();
    BEGIN
      PartToEditable := (PartFrom <> '');
      PartToTxtEditable := (PartFrom <> '');
    END;

    LOCAL PROCEDURE StdDialogConfirm@1100528400() : Boolean;
    BEGIN
      CurrPage.RUNMODAL;
      EXIT(CloseActionOK);
    END;

    LOCAL PROCEDURE StdDialogDoAction@1100528401();
    BEGIN
      StartUpdate;
    END;

    PROCEDURE StdDialogRunModal@1100528404();
    BEGIN
      IF StdDialogConfirm THEN
        StdDialogDoAction;
    END;

    BEGIN
    END.
  }
}

