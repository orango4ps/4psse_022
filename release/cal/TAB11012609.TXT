OBJECT Table 11012609 UAR Hour Accounting
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               FillEmplRelated();
               FillWeekRelatedFields();
             END;

    OnDelete=VAR
               lvHourAccLineRec@1210190000 : Record 11012610;
             BEGIN
               IF Status <> Status::Open THEN BEGIN
                 Status := Status::Open;
                 MODIFY;
               END;

               lvHourAccLineRec.SETRANGE(Year,Year);
               lvHourAccLineRec.SETRANGE(Week,Week);
               lvHourAccLineRec.SETRANGE("Employee No.","Employee No.");
               lvHourAccLineRec.DELETEALL(TRUE);
             END;

    OnRename=VAR
               lvHourAccLineRec@1210190000 : Record 11012610;
               lvHourAccLineRec2@1210190001 : Record 11012610;
               lvHourAccPauseRec@1210190002 : Record 11012611;
               lvHourAccPauseRec2@1210190003 : Record 11012611;
             BEGIN
               TESTFIELD(Status, Status::Open);

               lvHourAccLineRec.SETRANGE(Year, xRec.Year);
               lvHourAccLineRec.SETRANGE(Week, xRec.Week);
               lvHourAccLineRec.SETRANGE("Employee No.", xRec."Employee No.");
               IF lvHourAccLineRec.FIND('-') THEN BEGIN
                 REPEAT
                   lvHourAccLineRec2.TRANSFERFIELDS(lvHourAccLineRec);
                   lvHourAccLineRec2.Year := Year;
                   lvHourAccLineRec2.Week := Week;
                   lvHourAccLineRec2."Employee No." := "Employee No.";
                   lvHourAccLineRec2.INSERT;
                   lvHourAccLineRec.DELETE;
                 UNTIL lvHourAccLineRec.NEXT = 0;
               END;

               lvHourAccPauseRec.SETRANGE(Year, xRec.Year);
               lvHourAccPauseRec.SETRANGE(Week, xRec.Week);
               lvHourAccPauseRec.SETRANGE("Employee No.", xRec."Employee No.");
               IF lvHourAccPauseRec.FIND('-') THEN BEGIN
                 REPEAT
                   lvHourAccPauseRec2.TRANSFERFIELDS(lvHourAccPauseRec);
                   lvHourAccPauseRec2.Year := Year;
                   lvHourAccPauseRec2.Week := Week;
                   lvHourAccPauseRec2."Employee No." := "Employee No.";
                   lvHourAccPauseRec2.INSERT;
                   lvHourAccPauseRec.DELETE;
                 UNTIL lvHourAccPauseRec.NEXT = 0;
               END;

               FillEmplRelated();
               FillWeekRelatedFields();
             END;

    CaptionML=[ENU=Hour Accounting (UAR);
               NOR=Timerapportering (UAR);
               SVE=Tidrapport (UAR)];
    LookupPageID=Page11012644;
  }
  FIELDS
  {
    { 10  ;   ;Year                ;Integer       ;OnValidate=BEGIN
                                                                IF (Year <> xRec.Year) AND (Week <> 0) THEN
                                                                  Week := 0;

                                                                IF ("Employee No." <> '') THEN
                                                                  VALIDATE("Employee No.");

                                                                FillWeekRelatedFields();
                                                              END;

                                                   CaptionML=[ENU=Year;
                                                              NOR=èr;
                                                              SVE=èr];
                                                   MinValue=2000;
                                                   MaxValue=2099;
                                                   NotBlank=Yes }
    { 20  ;   ;Week                ;Integer       ;TableRelation="UAR Week Table".Week WHERE (Year=FIELD(Year));
                                                   OnValidate=BEGIN
                                                                IF (Week = 0) THEN
                                                                  EXIT;

                                                                IF ("Employee No." <> '') THEN
                                                                  VALIDATE("Employee No.");

                                                                FillWeekRelatedFields();
                                                              END;

                                                   CaptionML=[ENU=Week;
                                                              NOR=Uke;
                                                              SVE=Vecka];
                                                   MinValue=1;
                                                   MaxValue=53;
                                                   NotBlank=Yes }
    { 30  ;   ;Employee No.        ;Code20        ;TableRelation=Employee;
                                                   OnValidate=VAR
                                                                lvStartDate@1210190000 : Date;
                                                                lvEndDate@1210190001 : Date;
                                                              BEGIN
                                                                IF ("Employee No." = '') THEN
                                                                  EXIT;

                                                                IF (Year <> 0) AND (Week <> 0) THEN BEGIN
                                                                  EmplRec.GET("Employee No.");
                                                                  WeekRec.GET(Year, Week);
                                                                  lvStartDate := WeekRec."Starting Date";
                                                                  lvEndDate := WeekRec."Starting Date" + 6;
                                                                  IF (lvEndDate < EmplRec."Employment Date") THEN
                                                                    ERROR(Text000, EmplRec."Employment Date");
                                                                  IF (lvStartDate > EmplRec."Termination Date") AND (EmplRec."Termination Date" <> 0D) THEN
                                                                    ERROR(Text001, EmplRec."Termination Date");
                                                                  IF (EmplRec.Status <> EmplRec.Status::Active) AND (EmplRec."Inactive Date" <> 0D) THEN BEGIN
                                                                    IF (lvStartDate > EmplRec."Inactive Date") AND
                                                                       ((("Employee No." <> xRec."Employee No.") AND (CurrFieldNo = FIELDNO("Employee No."))) OR
                                                                        ((Year <> xRec.Year) AND (CurrFieldNo = FIELDNO(Year))) OR
                                                                        ((Week <> xRec.Week) AND (CurrFieldNo = FIELDNO(Week))) )
                                                                    THEN
                                                                      MESSAGE(Text002, EmplRec."Inactive Date");
                                                                  END;
                                                                END;

                                                                FillEmplRelated();
                                                                CALCFIELDS("Company No. Paymaster", "Administration No. Paymaster");
                                                              END;

                                                   CaptionML=[ENU=Employee No.;
                                                              NOR=Ansattnr;
                                                              SVE=AnstÑllningsnr];
                                                   NotBlank=Yes }
    { 40  ;   ;Document No.        ;Code20        ;CaptionML=[ENU=Document No.;
                                                              NOR=Bilagsnr.;
                                                              SVE=Dokumentnr];
                                                   Editable=No }
    { 50  ;   ;Posting Date        ;Date          ;CaptionML=[ENU=Posting Date;
                                                              NOR=Bokfõringsdato;
                                                              SVE=Bokfîringsdatum];
                                                   Editable=No }
    { 60  ;   ;Company No. Paymaster;Code10       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Company No. Paymaster" WHERE (No.=FIELD(Employee No.)));
                                                   CaptionML=[ENU=Company No.;
                                                              NOR=Selskapsnr.;
                                                              SVE=Fîretagsnummer];
                                                   Editable=No }
    { 70  ;   ;Administration No. Paymaster;Code10;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Administration No. Paymaster" WHERE (No.=FIELD(Employee No.)));
                                                   CaptionML=[ENU=Administration No.;
                                                              NOR=Administrasjonsnr.;
                                                              SVE=Administrationsnr];
                                                   Editable=No }
    { 80  ;   ;External            ;Boolean       ;FieldClass=Normal;
                                                   CaptionML=[ENU=External;
                                                              NOR=Eksternt;
                                                              SVE=Externt];
                                                   Editable=No }
    { 90  ;   ;Employer            ;Code20        ;FieldClass=Normal;
                                                   CaptionML=[ENU=Employer;
                                                              NOR=Arbeidsgiver;
                                                              SVE=Arbetsgivare];
                                                   Editable=No }
    { 100 ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=[ENU=Open,,Released;
                                                                    NOR=èpen,,Frigitt;
                                                                    SVE=ôppen,,SlÑppt];
                                                   OptionString=Open,,Released }
    { 110 ;   ;Released By         ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1100529601 : Codeunit 418;
                                                              UserCode@1100529600 : Code[50];
                                                            BEGIN
                                                              UserCode := "Released By";
                                                              UserMgt.LookupUserID(UserCode);
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Released By;
                                                              NOR=Frigitt av;
                                                              SVE=SlÑppt av];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Year,Week,Employee No.                  ;Clustered=Yes }
    {    ;Year,Week,External,Employer,Employee No. }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      HrsSetupRec@1210190000 : Record 11012600;
      Text000@1210190003 : TextConst 'ENU=Employee employed from %1;NOR=Ansatt ansatt fra %1;SVE=AnstÑlld har varit anstÑlld frÜn %1';
      Text001@1210190001 : TextConst 'ENU=Employee not employed since %1;NOR=Ansatt ikke ansatt siden %1;SVE=AnstÑlld har inte varit anstÑlld sedan %1';
      Text002@1210190002 : TextConst 'ENU=Employee inactive since %1;NOR=Ansatt inaktiv siden %1;SVE=AnstÑlld har varit inaktiv sedan %1';
      EmplRec@1210190007 : Record 5200;
      WeekRec@1210190004 : Record 11012601;
      DayNoFirstDay@1210190005 : Integer;
      DayNoPostDate@1210190006 : Integer;

    PROCEDURE FillEmplRelated@1210190002();
    BEGIN
      IF NOT EmplRec.GET("Employee No.") THEN
        EmplRec.INIT;

      External := EmplRec.External;
      Employer := EmplRec.Employer;
    END;

    PROCEDURE FillWeekRelatedFields@1210190000();
    BEGIN
      IF (Year = 0) OR (Week = 0) THEN BEGIN
        "Document No." := '';
        "Posting Date" := 0D;
        EXIT;
      END;

      HrsSetupRec.GET;
      IF Week < 10 THEN
        "Document No." := STRSUBSTNO('UAR%1-0%2', Year, Week)
      ELSE
        "Document No." := STRSUBSTNO('UAR%1-%2', Year, Week);

      WeekRec.GET(Year, Week);
      DayNoFirstDay := DATE2DWY(WeekRec."Starting Date", 1);
      DayNoPostDate := HrsSetupRec."Posting Date on" + 1;

      IF (DayNoPostDate < DayNoFirstDay) THEN
        "Posting Date" := WeekRec."Starting Date" + (7 - (DayNoFirstDay - DayNoPostDate))
      ELSE
        "Posting Date" := WeekRec."Starting Date" + (DayNoPostDate - DayNoFirstDay);
    END;

    BEGIN
    END.
  }
}

