OBJECT Page 11229952 Plant Number List with FA-Disp
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[DEU=WerkzeugnummerÅbersicht mit Anlagen-Entsorgung;
               ENU=Plant Numbers with FA-Disposal;
               NLD=Materieelnummers met VA-BGS];
    SourceTable=Table11012552;
    PageType=List;
    UsageCategory=Tasks;
    CardPageID=Plant Number;
    OnInit=BEGIN
             PlantSetup.GET;
           END;

    OnOpenPage=BEGIN
                 //**4PS Generated Code. Do Not Change.sn01
                 DPA_IsHardFilter := DPA_SetFilters(CurrPage.OBJECTID(FALSE));
                 //**4PS Generated Code.en01
               END;

    OnAfterGetRecord=BEGIN
                       CALCFIELDS(Bulk, "Quantity in Stock");
                       QuantityInStockHideValue := ("Stock No." = '0');

                       //**FA-Disposal.sn
                       FA_QuantityOrig := FA_GetQuantity("Fixed Asset", TRUE);
                       FA_QuantityCurr := GetQuantityFA;
                       FA_Disposal := FA_GetIsDisposal;
                       FA_QuantityDisposal := FA_QuantityPartlyDisposals(FA_NoOfDisposals);
                       IF FA_Disposal THEN BEGIN
                         FA_NoOfDisposals += 1;
                         FA_QuantityDisposal += FA_GetQuantity("Fixed Asset",FALSE);
                       END;
                       FA_LastFAPostingDate := FA_GetLastFAPostingDate;
                       FA_NoOfDisposalsHideValue := (FA_NoOfDisposals = 0);
                       //**FA-Disposal.en

                       StockNoStyleExpr := GetStockNoStyleExpr;

                       QuantityStockAndFADiffers := FALSE;
                       IF ("Fixed Asset" <> '') AND (NOT QuantityInStockHideValue) AND ("Quantity in Stock" <> FA_QuantityCurr) THEN
                         QuantityStockAndFADiffers := TRUE;
                     END;

    OnNewRecord=BEGIN
                  //**4PS Generated Code. Do Not Change.sn02
                  DPA_SetDefaultDepartment;
                  //**4PS Generated Code.en02
                END;

    ActionList=ACTIONS
    {
      { 1100409024;  ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 1100409022;1 ;Action    ;
                      ShortCutKey=Ctrl+F7;
                      CaptionML=[DEU=&Posten;
                                 ENU=Ledger Entries;
                                 NLD=Posten;
                                 NOR=Reskontrotransaksjoner];
                      RunObject=Page 11012585;
                      RunPageView=SORTING(Plant Type,Plant No.,Posting Date);
                      RunPageLink=Plant Type=FIELD(Plant Type),
                                  Plant No.=FIELD(No.);
                      Promoted=Yes;
                      Image=GLRegisters;
                      PromotedCategory=Process }
      { 1100409006;1 ;Action    ;
                      CaptionML=[DEU=Lagerbestand;
                                 ENU=Inventory;
                                 NLD=Voorraad;
                                 NOR=Lager;
                                 SVE=Lager];
                      RunObject=Page 11012559;
                      RunPageView=SORTING(Removed,Plant Type,Plant No.,Rate Code,Location);
                      RunPageLink=Removed=CONST(No),
                                  Plant Type=FIELD(Plant Type),
                                  Plant No.=FIELD(Stock No.);
                      Promoted=Yes;
                      Image=InventoryJournal;
                      PromotedCategory=Process }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                SourceExpr="Plant Type" }

    { 4   ;2   ;Field     ;
                SourceExpr="No.";
                StyleExpr=StockNoStyleExpr }

    { 1100529000;2;Field  ;
                SourceExpr="Stock No.";
                StyleExpr=StockNoStyleExpr }

    { 6   ;2   ;Field     ;
                SourceExpr=Description }

    { 8   ;2   ;Field     ;
                SourceExpr=Bulk }

    { 1100485000;2;Field  ;
                SourceExpr=External }

    { 1100525003;2;Field  ;
                SourceExpr="Blocked (Plant Type)";
                Visible=FALSE }

    { 1100485002;2;Field  ;
                SourceExpr=Vendor }

    { 1100525004;2;Field  ;
                SourceExpr="Vendor Name";
                Visible=FALSE }

    { 1210190004;2;Field  ;
                SourceExpr="Purchase Order No." }

    { 1210190002;2;Field  ;
                SourceExpr="Department Code";
                Visible=FALSE }

    { 1100525006;2;Field  ;
                SourceExpr="Receipt Date" }

    { 20  ;2   ;Field     ;
                SourceExpr=Posted }

    { 22  ;2   ;Field     ;
                SourceExpr="Posting Date" }

    { 18  ;2   ;Field     ;
                SourceExpr="Quantity in Stock";
                HideValue=QuantityInStockHideValue;
                Style=Attention;
                StyleExpr=QuantityStockAndFADiffers }

    { 12  ;2   ;Field     ;
                SourceExpr="Unit of Measure" }

    { 1210190000;2;Field  ;
                SourceExpr="Fixed Asset" }

    { 1100525007;2;Field  ;
                CaptionML=[DEU=AfA-Buch;
                           ENU=Depreciation Book;
                           NLD=Afschr.boek;
                           NOR=AvskrivningstablÜ;
                           SVE=Avskrivningsregel];
                SourceExpr=FA_GetDepreciationBook() }

    { 10  ;2   ;Field     ;
                CaptionML=[DEU=Anzahl Werkzeug (Urspr.);
                           ENU=Quantity Plant (Orig.);
                           NLD=Aantal mat. (Oorspr.)];
                SourceExpr=Quantity }

    { 1100525010;2;Field  ;
                CaptionML=[DEU=Menge Anlage (Urspr.);
                           ENU=Qty. FA (Orig.);
                           NLD=Aantal VA (Oorspr.)];
                DecimalPlaces=0:5;
                BlankZero=Yes;
                SourceExpr=FA_QuantityOrig }

    { 1100525001;2;Field  ;
                CaptionML=[DEU=Anzahl Anlage (Aktuell);
                           ENU=Quantity FA (Curr.);
                           NLD=Aantal VA (Huidig)];
                DecimalPlaces=0:5;
                BlankZero=Yes;
                SourceExpr=FA_QuantityCurr;
                Style=Attention;
                StyleExpr=QuantityStockAndFADiffers }

    { 1100525008;2;Field  ;
                CaptionML=[DEU=Verkauf;
                           ENU=Disposal;
                           NLD=BGS;
                           NOR=Salg;
                           SVE=Avyttring];
                SourceExpr=FA_Disposal }

    { 1100525009;2;Field  ;
                CaptionML=[DEU=Anz. Exits;
                           ENU=No. Of Exits;
                           NLD=Aant. Exits];
                SourceExpr='[' + FORMAT(FA_NoOfDisposals) + ']';
                HideValue=FA_NoOfDisposalsHideValue }

    { 1100525011;2;Field  ;
                CaptionML=[DEU=Anzahl Verkauf;
                           ENU=Quantity Disposal;
                           NLD=Aantal BGS];
                DecimalPlaces=0:5;
                BlankZero=Yes;
                SourceExpr=FA_QuantityDisposal }

    { 1100528500;2;Field  ;
                CaptionML=[DEU=Letztes Anl.-Buch.-Datum;
                           ENU=Last FA Posting Date;
                           NLD=Laatste VA- boekingsdatum;
                           NOR=Siste aktivabokf.dato;
                           SVE=Senaste anl. bokf.datum];
                SourceExpr=FA_LastFAPostingDate }

  }
  CODE
  {
    VAR
      PlantSetup@1100529001 : Record 11012550;
      DPA_IsHardFilter@1100000999 : Boolean;
      QuantityInStockHideValue@19059762 : Boolean INDATASET;
      QuantityStockAndFADiffers@1100529002 : Boolean INDATASET;
      FA_Disposal@1100525002 : Boolean;
      FA_NoOfDisposalsHideValue@1100525005 : Boolean INDATASET;
      FA_NoOfDisposals@1100525003 : Integer;
      FA_QuantityOrig@1100525000 : Decimal;
      FA_QuantityCurr@1100525001 : Decimal;
      FA_QuantityDisposal@1100525004 : Decimal;
      FA_LastFAPostingDate@1100528500 : Date;
      StockNoStyleExpr@1100529000 : Text INDATASET;

    PROCEDURE GetQuantityFA@1100485000() : Decimal;
    VAR
      FA@1100485000 : Record 5600;
    BEGIN
      IF ("Fixed Asset" = '') THEN
        EXIT(0);

      FA."No." := "Fixed Asset";
      EXIT(FA.GetQuantity('', FALSE));
    END;

    LOCAL PROCEDURE FA_GetDepreciationBook@1100525001() : Code[10];
    VAR
      FA@1100525000 : Record 5600;
    BEGIN
      //**FA-Disposal.n
      IF ("Fixed Asset" = '') THEN
        EXIT('');
      EXIT(FA.GetFA_MainDeprBook("Fixed Asset",FALSE));
    END;

    LOCAL PROCEDURE FA_GetIsDisposal@1100525002() : Boolean;
    VAR
      FADepreciationBook@1100525002 : Record 5612;
      FALedgerEntry@1100525000 : Record 5601;
      DepreciationBookCode@1100525001 : Code[10];
    BEGIN
      //**FA-Disposal.n    (Parts copied from function 'GetQuantity' in FA table)
      IF ("Fixed Asset" = '') THEN
        EXIT(FALSE);
      DepreciationBookCode := FA_GetDepreciationBook;
      IF DepreciationBookCode = '' THEN
        EXIT(FALSE);
      IF FADepreciationBook.GET("Fixed Asset", DepreciationBookCode) THEN BEGIN
        IF FADepreciationBook."Disposal Date" > 0D THEN
          EXIT(TRUE);
      END;
      FALedgerEntry.SETCURRENTKEY("FA No.","Depreciation Book Code","FA Posting Category","FA Posting Type");
      FALedgerEntry.SETRANGE("FA No.", "Fixed Asset");
      FALedgerEntry.SETRANGE("Depreciation Book Code", DepreciationBookCode);
      FALedgerEntry.SETRANGE("FA Posting Category", FALedgerEntry."FA Posting Category"::Disposal);
      IF NOT FALedgerEntry.ISEMPTY THEN
        EXIT(TRUE);
      EXIT(FALSE);
    END;

    LOCAL PROCEDURE FA_GetQuantity@1210190000(FixetAssetNo@1100525001 : Code[20];iBlnAcquisitionOnly@1210190002 : Boolean) : Decimal;
    VAR
      FALedgerEntry@1210190004 : Record 5601;
      DepreciationBookCode@1100525000 : Code[10];
      QuantityFA@1210190005 : Decimal;
    BEGIN
      //**FA-Disposal.n    (Parts copied from function 'GetQuantity' in FA table)
      IF (FixetAssetNo = '') THEN
        EXIT(0);
      DepreciationBookCode := FA_GetDepreciationBook;
      IF DepreciationBookCode = '' THEN
        EXIT(0);
      FALedgerEntry.SETCURRENTKEY("FA No.","Depreciation Book Code","FA Posting Category","FA Posting Type");
      FALedgerEntry.SETRANGE("FA No.", FixetAssetNo);
      FALedgerEntry.SETRANGE("Depreciation Book Code", DepreciationBookCode);
      FALedgerEntry.SETRANGE("FA Posting Type", FALedgerEntry."FA Posting Type"::"Acquisition Cost");
      FALedgerEntry.SETFILTER(Quantity, '<>%1', 0.0);
      IF (iBlnAcquisitionOnly) THEN
        FALedgerEntry.SETRANGE("Reclassification Entry", FALSE);
      IF FALedgerEntry.FINDSET THEN
        REPEAT
          QuantityFA += FALedgerEntry.Quantity;
        UNTIL (FALedgerEntry.NEXT = 0);
      EXIT(QuantityFA);
    END;

    LOCAL PROCEDURE FA_QuantityPartlyDisposals@1100525000(VAR NoOfPartlyDisposals@1100525002 : Integer) : Decimal;
    VAR
      FA2@1100525000 : Record 5600;
      QuantityFA@1100525001 : Decimal;
    BEGIN
      //**FA-Disposal.n
      CALCFIELDS(Bulk);
      NoOfPartlyDisposals := 0;
      IF ("Fixed Asset" = '') OR (NOT Bulk) THEN
        EXIT(0);
      FA2.SETFILTER("No.", '<>%1', "Fixed Asset");
      FA2.SETRANGE("Original Asset", "Fixed Asset");
      IF FA2.FINDSET THEN BEGIN
        REPEAT
           QuantityFA += FA_GetQuantity(FA2."No.", FALSE);
           NoOfPartlyDisposals += 1;
        UNTIL FA2.NEXT = 0;
      END;
      EXIT(QuantityFA);
    END;

    LOCAL PROCEDURE FA_GetLastFAPostingDate@1100528500() : Date;
    VAR
      FALedgerEntry@1100525000 : Record 5601;
      DepreciationBookCode@1100525001 : Code[10];
    BEGIN
      //**FA-Disposal.n
      IF ("Fixed Asset" = '') THEN
        EXIT(0D);
      DepreciationBookCode := FA_GetDepreciationBook;
      IF DepreciationBookCode = '' THEN
        EXIT(0D);
      CALCFIELDS(Bulk);
      FALedgerEntry.SETCURRENTKEY("FA No.", "Depreciation Book Code", "FA Posting Date");
      FALedgerEntry.SETRANGE("FA No.", "Fixed Asset");
      FALedgerEntry.SETRANGE("Depreciation Book Code", DepreciationBookCode);
      IF Bulk AND (NOT FA_Disposal) AND (FA_NoOfDisposals > 0) THEN
        FALedgerEntry.SETRANGE("Reclassification Entry", FALSE);
      IF FALedgerEntry.FINDLAST THEN
        EXIT(FALedgerEntry."FA Posting Date");
      EXIT(0D);
    END;

    LOCAL PROCEDURE GetStockNoStyleExpr@1100529000() : Text;
    BEGIN
      IF "Stock No." = '' THEN
        EXIT('Attention');
      IF NOT Bulk THEN BEGIN
        IF "Stock No." <> "No." THEN
          EXIT('Attention');
      END ELSE BEGIN
        IF NOT External THEN BEGIN
          IF "Stock No." <> '0' THEN
            EXIT('Attention');
        END ELSE BEGIN
          IF ("Stock No." <> '0') AND ("Stock No." <> "No.") THEN
            EXIT('Attention');
          IF (PlantSetup."Ext Bulk Plant with No. in Inv" AND ("Stock No." <> "No.")) OR
             ((NOT PlantSetup."Ext Bulk Plant with No. in Inv") AND ("Stock No." <> '0'))
          THEN
            EXIT('StandardAccent');
        END;
      END;
      EXIT('');
    END;

    BEGIN
    {
      Copy of Plant Number List. Delteted all what is not needed (mostly PageActions).
      Added extra fields for Fixed Asset, See 'FA-Disposal'.
    }
    END.
  }
}

