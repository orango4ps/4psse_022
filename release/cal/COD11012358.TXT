OBJECT Codeunit 11012358 Budget Rate Management
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text001@1100528600 : TextConst 'DEU="Tabelle %1 ; %2";ENU="Table %1 ; %2";NLD="Tabel %1 ; %2";NOR="Tabel %1 ; %2";SVE="Tabell %1 ; %2"';

    PROCEDURE GetBudgetRate@1100528600(IBudgetRateType@1100528600 : 'Project,Service';IProjectNo@1100528601 : Code[20];IServiceOrderNo@1100528602 : Code[20];IServiceContractNo@1100528603 : Code[20];IDepartmentCode@1100528609 : Code[20];IBudgetRateCode@1100528604 : Code[10];IPostingDate@1100528610 : Date;VAR OBudgetRateFoundAt@1100528605 : Text[250]) : Decimal;
    BEGIN
      EXIT(
        GetBudgetRateByCompany(
          IBudgetRateType, IProjectNo, IServiceOrderNo, IServiceContractNo, IDepartmentCode, IBudgetRateCode,
          IPostingDate, '', OBudgetRateFoundAt));
    END;

    PROCEDURE GetBudgetRateByCompany@1100525000(IBudgetRateType@1100528600 : 'Project,Service';IProjectNo@1100528601 : Code[20];IServiceOrderNo@1100528602 : Code[20];IServiceContractNo@1100528603 : Code[20];IDepartmentCode@1100528609 : Code[20];IBudgetRateCode@1100528604 : Code[10];IPostingDate@1100528610 : Date;ICompanyName@1100525001 : Text[1024];VAR OBudgetRateFoundAt@1100528605 : Text[250]) : Decimal;
    VAR
      BudgetRateReadOrderLine@1100528606 : Record 11071894;
      BudgetRate@1100528607 : Record 11012000;
      ProjectBudgetRate@1100528612 : Record 11020231;
      DepartmentBudgetRate@1100528608 : Record 11071895;
      ServiceSetup@1100525000 : Record 11012800;
      RecRef@1100528611 : RecordRef;
    BEGIN
      IF ICompanyName <> '' THEN
        BudgetRateReadOrderLine.CHANGECOMPANY(ICompanyName);
      BudgetRateReadOrderLine.SETCURRENTKEY("Budget Rate Type", "Read Order");
      BudgetRateReadOrderLine.SETRANGE("Budget Rate Type", IBudgetRateType);
      IF IBudgetRateType = IBudgetRateType::Service THEN
        ServiceSetup.GET;
      BudgetRateReadOrderLine.SETFILTER("Read Order", '1..');
      IF NOT BudgetRateReadOrderLine.FINDSET THEN
        EXIT;
      REPEAT
        CASE BudgetRateReadOrderLine."Table No." OF
          DATABASE::"Budget Rate":
            BEGIN
              IF ICompanyName <> '' THEN
                BudgetRate.CHANGECOMPANY(ICompanyName);
              BudgetRate.SETRANGE(Code, IBudgetRateCode);
              BudgetRate.SETRANGE("Starting Date", 0D, IPostingDate);
              BudgetRate.SETFILTER("Ending Date", '%1|>=%2', 0D, IPostingDate);
              IF BudgetRate.FINDLAST THEN BEGIN
                RecRef.GETTABLE(BudgetRate);
                OBudgetRateFoundAt := FillBudgetRateFoundAt(RecRef);
                EXIT(BudgetRate.Rate);
              END;
            END;
          DATABASE::"Project Budget Rate":
            IF IBudgetRateType = IBudgetRateType::Project THEN BEGIN
              IF ICompanyName <> '' THEN
                ProjectBudgetRate.CHANGECOMPANY(ICompanyName);
              ProjectBudgetRate.SETRANGE("Project No.", IProjectNo);
              ProjectBudgetRate.SETRANGE("Rate Code", IBudgetRateCode);
              ProjectBudgetRate.SETRANGE("Starting Date", 0D, IPostingDate);
              ProjectBudgetRate.SETFILTER("Ending Date", '%1|>=%2', 0D, IPostingDate);
              IF ProjectBudgetRate.FINDLAST THEN BEGIN
                RecRef.GETTABLE(ProjectBudgetRate);
                OBudgetRateFoundAt := FillBudgetRateFoundAt(RecRef);
                EXIT(ProjectBudgetRate."Rate (LCY)")
              END;
            END;
          DATABASE::"Department Budget Rate":
            BEGIN
              IF ICompanyName <> '' THEN
                DepartmentBudgetRate.CHANGECOMPANY(ICompanyName);
              DepartmentBudgetRate.SETRANGE("Department Code", IDepartmentCode);
              DepartmentBudgetRate.SETRANGE("Rate Code", IBudgetRateCode);
              DepartmentBudgetRate.SETRANGE("Starting Date", 0D, IPostingDate);
              DepartmentBudgetRate.SETFILTER("Ending Date", '%1|>=%2', 0D, IPostingDate);
              IF DepartmentBudgetRate.FINDLAST THEN BEGIN
                RecRef.GETTABLE(DepartmentBudgetRate);
                OBudgetRateFoundAt := FillBudgetRateFoundAt(RecRef);
                EXIT(DepartmentBudgetRate.Rate);
              END;
            END;
        END;
      UNTIL BudgetRateReadOrderLine.NEXT = 0;
    END;

    LOCAL PROCEDURE FillBudgetRateFoundAt@1100528602(IRecRef@1100528600 : RecordRef) : Text[250];
    VAR
      AllObjWithCaption@1100528601 : Record 2000000058;
    BEGIN
      AllObjWithCaption.SETRANGE("Object Type", AllObjWithCaption."Object Type"::Table);
      AllObjWithCaption.SETRANGE("Object ID", IRecRef.NUMBER);
      IF AllObjWithCaption.FINDFIRST THEN;

      IRecRef.RESET;
      IRecRef.SETRECFILTER;

      EXIT(STRSUBSTNO(Text001, AllObjWithCaption."Object Caption", IRecRef.GETFILTERS));
    END;

    PROCEDURE GetBudgetRateForServicePrognosis@1100529600(DepartmentCode@1100528609 : Code[20];BudgetRateCode@1100528604 : Code[10];StartDate@1100528610 : Date;EndDate@1100529600 : Date) : Decimal;
    VAR
      BudgetRateReadOrderLine@1100528606 : Record 11071894;
      BudgetRate@1100528607 : Record 11012000;
      DepartmentBudgetRate@1100528608 : Record 11071895;
    BEGIN
      BudgetRateReadOrderLine.SETCURRENTKEY("Budget Rate Type", "Read Order");
      BudgetRateReadOrderLine.SETRANGE("Budget Rate Type", BudgetRateReadOrderLine."Budget Rate Type"::Service);
      BudgetRateReadOrderLine.SETFILTER("Read Order", '1..');
      IF BudgetRateReadOrderLine.ISEMPTY THEN
        EXIT;

      BudgetRateReadOrderLine.FINDSET;
      REPEAT
        CASE BudgetRateReadOrderLine."Table No." OF
          DATABASE::"Budget Rate":
            BEGIN
              BudgetRate.SETRANGE(Code, BudgetRateCode);
              BudgetRate.SETFILTER("Starting Date", '%1|<=%2', 0D, EndDate);
              BudgetRate.SETFILTER("Ending Date", '%1|>=%2', 0D, StartDate);
              IF BudgetRate.FINDLAST THEN
                EXIT(BudgetRate.Rate);
            END;
          DATABASE::"Department Budget Rate":
            BEGIN
              DepartmentBudgetRate.SETRANGE("Department Code", DepartmentCode);
              DepartmentBudgetRate.SETRANGE("Rate Code", BudgetRateCode);
              DepartmentBudgetRate.SETFILTER("Starting Date", '%1|<=%2', 0D, EndDate);
              DepartmentBudgetRate.SETFILTER("Ending Date", '%1|>=%2', 0D, StartDate);
              IF DepartmentBudgetRate.FINDLAST THEN
                EXIT(DepartmentBudgetRate.Rate);
            END;
        END;
      UNTIL BudgetRateReadOrderLine.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

