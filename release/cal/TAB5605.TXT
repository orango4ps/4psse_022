OBJECT Table 5605 FA Journal Setup
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW113.00,4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               DeprBook.LOCKTABLE;
               DeprBook.GET("Depreciation Book Code");
             END;

    CaptionML=[ENU=FA Journal Setup;
               NOR=Aktivakladdoppsett;
               SVE=Anl. journalinst„llningar];
  }
  FIELDS
  {
    { 1   ;   ;Depreciation Book Code;Code10      ;TableRelation="Depreciation Book";
                                                   CaptionML=[ENU=Depreciation Book Code;
                                                              NOR=Avskrivningstabl†kode;
                                                              SVE=Avskrivningsregelkod];
                                                   NotBlank=Yes }
    { 2   ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   OnValidate=VAR
                                                                UserMgt@1000 : Codeunit 418;
                                                              BEGIN
                                                                UserMgt.ValidateUserID("User ID");
                                                              END;

                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserMgt.LookupUserID("User ID");
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   DataClassification=EndUserIdentifiableInformation;
                                                   CaptionML=[ENU=User ID;
                                                              NOR=Bruker-ID;
                                                              SVE=Anv„ndar-ID] }
    { 3   ;   ;FA Jnl. Template Name;Code10       ;TableRelation="FA Journal Template";
                                                   OnValidate=BEGIN
                                                                "FA Jnl. Batch Name" := '';
                                                              END;

                                                   CaptionML=[ENU=FA Jnl. Template Name;
                                                              NOR=Aktivakladdemalnavn];
                                                   Editable=Yes }
    { 4   ;   ;FA Jnl. Batch Name  ;Code10        ;TableRelation="FA Journal Batch".Name WHERE (Journal Template Name=FIELD(FA Jnl. Template Name));
                                                   CaptionML=[ENU=FA Jnl. Batch Name;
                                                              NOR=Aktivakladdenavn] }
    { 5   ;   ;Gen. Jnl. Template Name;Code10     ;TableRelation="Gen. Journal Template";
                                                   OnValidate=BEGIN
                                                                "Gen. Jnl. Batch Name" := '';
                                                              END;

                                                   CaptionML=[ENU=Gen. Jnl. Template Name;
                                                              NOR=Finanskladdemalnavn;
                                                              SVE=Redov. jnlmallnamn] }
    { 6   ;   ;Gen. Jnl. Batch Name;Code10        ;TableRelation="Gen. Journal Batch".Name WHERE (Journal Template Name=FIELD(Gen. Jnl. Template Name));
                                                   CaptionML=[ENU=Gen. Jnl. Batch Name;
                                                              NOR=Finanskladdenavn;
                                                              SVE=Redovisning journalnamn] }
    { 7   ;   ;Insurance Jnl. Template Name;Code10;TableRelation="Insurance Journal Template";
                                                   OnValidate=BEGIN
                                                                "Insurance Jnl. Batch Name" := '';
                                                              END;

                                                   CaptionML=[ENU=Insurance Jnl. Template Name;
                                                              NOR=Forsikringskladdemalnavn] }
    { 8   ;   ;Insurance Jnl. Batch Name;Code10   ;TableRelation="Insurance Journal Batch".Name WHERE (Journal Template Name=FIELD(Insurance Jnl. Template Name));
                                                   CaptionML=[ENU=Insurance Jnl. Batch Name;
                                                              NOR=Forsikringskladdenavn] }
    { 11012550;;Reclass Jnl. Template Name;Code10 ;TableRelation="FA Reclass. Journal Template".Name;
                                                   OnValidate=BEGIN
                                                                "FA Jnl. Batch Name" := '';  //**4PS.n
                                                              END;

                                                   CaptionML=ENU=Reclass Jnl. Template Name;
                                                   Description=4PS FA;
                                                   Editable=Yes }
    { 11012560;;Reclass Jnl. Batch Name;Code10    ;TableRelation="FA Reclass. Journal Batch".Name WHERE (Journal Template Name=FIELD(Reclass Jnl. Template Name));
                                                   CaptionML=ENU=Reclass Jnl. Batch Name;
                                                   Description=4PS FA }
    { 11012570;;Plant Salvage Val. Jnl. Templ.;Code10;
                                                   TableRelation="FA Journal Template";
                                                   OnValidate=BEGIN
                                                                //**4PS
                                                                "Plant Salvage Val. Jnl. Batch" := '';
                                                              END;

                                                   CaptionML=ENU=Plant Salvage Val. Jnl. Template;
                                                   Editable=Yes }
    { 11012575;;Plant Salvage Val. Jnl. Batch;Code10;
                                                   TableRelation="FA Journal Batch".Name WHERE (Journal Template Name=FIELD(Plant Salvage Val. Jnl. Templ.));
                                                   CaptionML=ENU=Plant Salvage Val. Jnl. Batch }
  }
  KEYS
  {
    {    ;Depreciation Book Code,User ID          ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You must specify %1.;NOR=Du m† angi %1.;SVE=Du m†ste specificera %1.';
      DeprBook@1001 : Record 5611;

    [External]
    PROCEDURE GenJnlName@1(VAR DeprBook@1000 : Record 5611;VAR GenJnlLine@1001 : Record 81;VAR NextLineNo@1002 : Integer);
    VAR
      FAJnlSetup@1003 : Record 5605;
      GenJnlTemplate@1004 : Record 80;
      GenJnlBatch@1005 : Record 232;
      FAGetJnl@1006 : Codeunit 5639;
      TemplateName@1007 : Code[10];
      BatchName@1008 : Code[10];
    BEGIN
      IF NOT FAJnlSetup.GET(DeprBook.Code,USERID) THEN
        FAJnlSetup.GET(DeprBook.Code,'');
      FAJnlSetup.TESTFIELD("Gen. Jnl. Template Name");
      FAJnlSetup.TESTFIELD("Gen. Jnl. Batch Name");
      TemplateName := FAJnlSetup."Gen. Jnl. Template Name";
      BatchName := FAJnlSetup."Gen. Jnl. Batch Name";
      GenJnlTemplate.GET(TemplateName);
      GenJnlBatch.GET(TemplateName,BatchName);
      FAGetJnl.SetGenJnlRange(GenJnlLine,TemplateName,BatchName);
      NextLineNo := GenJnlLine."Line No.";
    END;

    [External]
    PROCEDURE FAJnlName@2(VAR DeprBook@1000 : Record 5611;VAR FAJnlLine@1001 : Record 5621;VAR NextLineNo@1002 : Integer);
    VAR
      FAJnlSetup@1003 : Record 5605;
      FAJnlTemplate@1004 : Record 5619;
      FAJnlBatch@1005 : Record 5620;
      FAGetJnl@1006 : Codeunit 5639;
      TemplateName@1007 : Code[10];
      BatchName@1008 : Code[10];
    BEGIN
      IF NOT FAJnlSetup.GET(DeprBook.Code,USERID) THEN
        FAJnlSetup.GET(DeprBook.Code,'');
      FAJnlSetup.TESTFIELD("FA Jnl. Template Name");
      FAJnlSetup.TESTFIELD("FA Jnl. Batch Name");
      TemplateName := FAJnlSetup."FA Jnl. Template Name";
      BatchName := FAJnlSetup."FA Jnl. Batch Name";
      FAJnlTemplate.GET(TemplateName);
      FAJnlBatch.GET(TemplateName,BatchName);
      FAGetJnl.SetFAJnlRange(FAJnlLine,TemplateName,BatchName);
      NextLineNo := FAJnlLine."Line No.";
    END;

    [External]
    PROCEDURE InsuranceJnlName@3(VAR DeprBook@1000 : Record 5611;VAR InsuranceJnlLine@1001 : Record 11072092;VAR NextLineNo@1002 : Integer);
    VAR
      FAJnlSetup@1003 : Record 5605;
      InsuranceJnlTempl@1005 : Record 11072090;
      InsuranceJnlBatch@1006 : Record 11072091;
      FAGetJnl@1004 : Codeunit 5639;
      TemplateName@1007 : Code[10];
      BatchName@1008 : Code[10];
    BEGIN
      IF NOT FAJnlSetup.GET(DeprBook.Code,USERID) THEN
        FAJnlSetup.GET(DeprBook.Code,'');
      FAJnlSetup.TESTFIELD("Insurance Jnl. Template Name");
      FAJnlSetup.TESTFIELD("Insurance Jnl. Batch Name");
      TemplateName := FAJnlSetup."Insurance Jnl. Template Name";
      BatchName := FAJnlSetup."Insurance Jnl. Batch Name";
      InsuranceJnlTempl.GET(TemplateName);
      InsuranceJnlBatch.GET(TemplateName,BatchName);
      FAGetJnl.SetInsuranceJnlRange(InsuranceJnlLine,TemplateName,BatchName);
      NextLineNo := InsuranceJnlLine."Line No.";
    END;

    [External]
    PROCEDURE SetGenJnlTrailCodes@6(VAR GenJnlLine@1000 : Record 81);
    VAR
      GenJnlTemplate@1001 : Record 80;
      GenJnlBatch@1002 : Record 232;
    BEGIN
      WITH GenJnlLine DO BEGIN
        GenJnlTemplate.GET("Journal Template Name");
        GenJnlBatch.GET("Journal Template Name","Journal Batch Name");
        "Source Code" := GenJnlTemplate."Source Code";
        "Reason Code" := GenJnlBatch."Reason Code";
        GenJnlBatch.TESTFIELD("Bal. Account Type","Bal. Account Type"::"G/L Account");
        GenJnlBatch.TESTFIELD("Bal. Account No.",'');
      END;
    END;

    [External]
    PROCEDURE GetFAJnlDocumentNo@4(VAR FAJnlLine@1000 : Record 5621;PostingDate@1001 : Date;CreateError@1002 : Boolean) : Code[20];
    VAR
      FAJnlBatch@1003 : Record 5620;
      NoSeriesMgt@1004 : Codeunit 396;
      DocumentNo@1005 : Code[20];
    BEGIN
      WITH FAJnlLine DO BEGIN
        FAJnlBatch.GET("Journal Template Name","Journal Batch Name");
        IF (FAJnlBatch."No. Series" <> '') AND NOT FIND('=><') THEN
          DocumentNo := NoSeriesMgt.GetNextNo(FAJnlBatch."No. Series",PostingDate,FALSE);
        IF (DocumentNo = '') AND CreateError THEN
          ERROR(Text000,FIELDCAPTION("Document No."));
      END;
      EXIT(DocumentNo);
    END;

    [External]
    PROCEDURE GetGenJnlDocumentNo@8(VAR GenJnlLine@1000 : Record 81;PostingDate@1001 : Date;CreateError@1002 : Boolean) : Code[20];
    VAR
      GenJnlBatch@1003 : Record 232;
      NoSeriesMgt@1004 : Codeunit 396;
      DocumentNo@1005 : Code[20];
    BEGIN
      WITH GenJnlLine DO BEGIN
        GenJnlBatch.GET("Journal Template Name","Journal Batch Name");
        IF (GenJnlBatch."No. Series" <> '') AND NOT FIND('=><') THEN
          DocumentNo := NoSeriesMgt.GetNextNo(GenJnlBatch."No. Series",PostingDate,FALSE);
        IF (DocumentNo = '') AND CreateError THEN
          ERROR(Text000,FIELDCAPTION("Document No."));
      END;
      EXIT(DocumentNo);
    END;

    [External]
    PROCEDURE GetInsuranceJnlDocumentNo@11(VAR InsuranceJnlLine@1000 : Record 11072092;PostingDate@1001 : Date) : Code[20];
    VAR
      InsuranceJnlBatch@1002 : Record 11072091;
      NoSeriesMgt@1003 : Codeunit 396;
      DocumentNo@1004 : Code[20];
    BEGIN
      WITH InsuranceJnlLine DO BEGIN
        InsuranceJnlBatch.GET("Journal Template Name","Journal Batch Name");
        IF (InsuranceJnlBatch."No. Series" <> '') AND NOT FIND('=><') THEN
          DocumentNo := NoSeriesMgt.GetNextNo(InsuranceJnlBatch."No. Series",PostingDate,FALSE);
        IF DocumentNo = '' THEN
          ERROR(Text000,FIELDCAPTION("Document No."));
      END;
      EXIT(DocumentNo);
    END;

    [External]
    PROCEDURE GetFANoSeries@5(VAR FAJnlLine@1000 : Record 5621) : Code[20];
    VAR
      FAJnlBatch@1001 : Record 5620;
    BEGIN
      FAJnlBatch.GET(FAJnlLine."Journal Template Name",FAJnlLine."Journal Batch Name");
      IF FAJnlBatch."No. Series" <> FAJnlBatch."Posting No. Series" THEN
        EXIT(FAJnlBatch."Posting No. Series");
      EXIT('');
    END;

    [External]
    PROCEDURE GetGenNoSeries@7(VAR GenJnlLine@1000 : Record 81) : Code[20];
    VAR
      GenJnlBatch@1001 : Record 232;
    BEGIN
      GenJnlBatch.GET(GenJnlLine."Journal Template Name",GenJnlLine."Journal Batch Name");
      IF GenJnlBatch."No. Series" <> GenJnlBatch."Posting No. Series" THEN
        EXIT(GenJnlBatch."Posting No. Series");
      EXIT('');
    END;

    [External]
    PROCEDURE GetInsuranceNoSeries@9(VAR InsuranceJnlLine@1000 : Record 11072092) : Code[20];
    VAR
      InsuranceJnlBatch@1001 : Record 11072091;
    BEGIN
      InsuranceJnlBatch.GET(InsuranceJnlLine."Journal Template Name",InsuranceJnlLine."Journal Batch Name");
      IF InsuranceJnlBatch."No. Series" <> InsuranceJnlBatch."Posting No. Series" THEN
        EXIT(InsuranceJnlBatch."Posting No. Series");
      EXIT('');
    END;

    [External]
    PROCEDURE SetFAJnlTrailCodes@10(VAR FAJnlLine@1000 : Record 5621);
    VAR
      FAJnlTemplate@1001 : Record 5619;
      FAJnlBatch@1002 : Record 5620;
    BEGIN
      WITH FAJnlLine DO BEGIN
        FAJnlTemplate.GET("Journal Template Name");
        FAJnlBatch.GET("Journal Template Name","Journal Batch Name");
        "Source Code" := FAJnlTemplate."Source Code";
        "Reason Code" := FAJnlBatch."Reason Code";
      END;
    END;

    [External]
    PROCEDURE SetInsuranceJnlTrailCodes@12(VAR InsuranceJnlLine@1000 : Record 11072092);
    VAR
      InsuranceJnlTempl@1001 : Record 11072090;
      InsuranceJnlBatch@1002 : Record 11072091;
    BEGIN
      WITH InsuranceJnlLine DO BEGIN
        InsuranceJnlTempl.GET("Journal Template Name");
        InsuranceJnlBatch.GET("Journal Template Name","Journal Batch Name");
        "Source Code" := InsuranceJnlTempl."Source Code";
        "Reason Code" := InsuranceJnlBatch."Reason Code";
      END;
    END;

    [External]
    PROCEDURE IncFAJnlBatchName@13(VAR FAJnlBatch@1000 : Record 5620);
    VAR
      FAJnlSetup@1001 : Record 5605;
    BEGIN
      IF FAJnlSetup.FIND('-') THEN
        REPEAT
          IF (FAJnlSetup."FA Jnl. Template Name" = FAJnlBatch."Journal Template Name") AND
             (FAJnlSetup."FA Jnl. Batch Name" = FAJnlBatch.Name)
          THEN BEGIN
            FAJnlSetup."FA Jnl. Batch Name" := INCSTR(FAJnlSetup."FA Jnl. Batch Name");
            FAJnlSetup.MODIFY;
          END;
        UNTIL FAJnlSetup.NEXT = 0;
    END;

    [External]
    PROCEDURE IncGenJnlBatchName@15(VAR GenJnlBatch@1000 : Record 232);
    VAR
      FAJnlSetup@1001 : Record 5605;
    BEGIN
      IF FAJnlSetup.FIND('-') THEN
        REPEAT
          IF (FAJnlSetup."Gen. Jnl. Template Name" = GenJnlBatch."Journal Template Name") AND
             (FAJnlSetup."Gen. Jnl. Batch Name" = GenJnlBatch.Name)
          THEN BEGIN
            FAJnlSetup."Gen. Jnl. Batch Name" := INCSTR(FAJnlSetup."Gen. Jnl. Batch Name");
            FAJnlSetup.MODIFY;
          END;
        UNTIL FAJnlSetup.NEXT = 0;
    END;

    [External]
    PROCEDURE IncInsuranceJnlBatchName@14(VAR InsuranceJnlBatch@1000 : Record 11072091);
    VAR
      FAJnlSetup@1001 : Record 5605;
    BEGIN
      IF FAJnlSetup.FIND('-') THEN
        REPEAT
          IF (FAJnlSetup."Insurance Jnl. Template Name" = InsuranceJnlBatch."Journal Template Name") AND
             (FAJnlSetup."Insurance Jnl. Batch Name" = InsuranceJnlBatch.Name)
          THEN BEGIN
            FAJnlSetup."Insurance Jnl. Batch Name" := INCSTR(FAJnlSetup."Insurance Jnl. Batch Name");
            FAJnlSetup.MODIFY;
          END;
        UNTIL FAJnlSetup.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

