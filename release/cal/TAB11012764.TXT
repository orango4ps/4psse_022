OBJECT Table 11012764 Try-out Quote Estimate Version
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               SubVersRec@1210190000 : Record 11012765;
             BEGIN
               SubVersRec.RESET;
               SubVersRec.SETRANGE("Try-out Quote","Try-out Quote");
               SubVersRec.SETRANGE("Version Line","Version Line");
               SubVersRec.SETRANGE("Estimate No.","Estimate No.");

               SubVersRec.DELETEALL;
             END;

    CaptionML=[ENU=Try-out Quote Estimate Version;
               NOR=Kalkyle av tilbudsforesp›rsel Í versjon;
               SVE=Kalkylversion av anbudsf”rfr†gan];
    LookupPageID=Page11012773;
  }
  FIELDS
  {
    { 5   ;   ;Try-out Quote       ;Code20        ;TableRelation="Try-out Quote";
                                                   CaptionML=[ENU=Try-out Quote;
                                                              NOR=Tilbudsforesp›rsel;
                                                              SVE=Anbudsf”rfr†gan] }
    { 10  ;   ;Version Line        ;Integer       ;CaptionML=[ENU=Version Line;
                                                              NOR=Versjonsrad;
                                                              SVE=Versionsrad] }
    { 20  ;   ;Estimate No.        ;Code20        ;TableRelation=Estimate.No.;
                                                   OnValidate=BEGIN
                                                                TryoutSuccRec.SETRANGE("Try-out Quote", "Try-out Quote");
                                                                TryoutSuccRec.SETRANGE("Estimate No.", "Estimate No.");
                                                                IF NOT TryoutSuccRec.FINDFIRST THEN
                                                                  ERROR(Text000);

                                                                CalcSumSheetAmount;
                                                                CheckSubEstimates;
                                                              END;

                                                   OnLookup=BEGIN
                                                              TryoutSuccRec.SETRANGE("Try-out Quote", "Try-out Quote");
                                                              TryoutSuccRec.SETFILTER("Estimate No.", '<>%1', '');
                                                              IF TryoutSuccRec.FINDSET THEN BEGIN
                                                                REPEAT
                                                                  EstRec.MARKEDONLY := TRUE;
                                                                  EstRec.GET(TryoutSuccRec."Estimate No.");
                                                                  EstRec.MARK := TRUE;
                                                                UNTIL TryoutSuccRec.NEXT = 0;
                                                              END ELSE BEGIN
                                                                EstRec.SETRANGE("No.", '');
                                                              END;

                                                              IF PAGE.RUNMODAL(PAGE::"Estimate List", EstRec) = ACTION::LookupOK THEN
                                                                VALIDATE("Estimate No.", EstRec."No.");
                                                            END;

                                                   CaptionML=[ENU=Estimate No.;
                                                              NOR=Kalkylenr;
                                                              SVE=Kalkylnr];
                                                   NotBlank=Yes }
    { 30  ;   ;Description         ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Estimate.Description WHERE (No.=FIELD(Estimate No.)));
                                                   CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning];
                                                   Editable=No }
    { 43  ;   ;Summary Sheet       ;Code10        ;TableRelation="Summary Sheet".Summary WHERE (Estimate No.=FIELD(Estimate No.));
                                                   OnValidate=BEGIN
                                                                CalcSumSheetAmount;
                                                              END;

                                                   CaptionML=[ENU=Summary Sheet;
                                                              NOR=Sammendragsark;
                                                              SVE=Summeringsark] }
    { 45  ;   ;Summary Sheet Amount;Decimal       ;CaptionML=[ENU=Summary Sheet Amount;
                                                              NOR=Sammendragsark, bel›p;
                                                              SVE=Summeringsark, belopp];
                                                   Editable=No }
    { 50  ;   ;Offered amount      ;Decimal       ;FieldClass=Normal;
                                                   CaptionML=[ENU=Offered Amount;
                                                              NOR=Tilbudt bel›p;
                                                              SVE=Anbudsbelopp];
                                                   Editable=Yes }
  }
  KEYS
  {
    {    ;Try-out Quote,Version Line,Estimate No. ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      EstRec@1210190004 : Record 11012151;
      TryoutSuccRec@1210190003 : Record 11012761;
      Text000@1210190005 : TextConst 'ENU=Estimate not defined as Try-out Successor;NOR=Kalkyle er ikke definert som etterf›lger (prospekt);SVE=Kalkyl „r inte definierat som anbudsefterf”ljare';

    PROCEDURE CalcSumSheetAmount@1210190000();
    VAR
      lvSumRec@1210190001 : Record 11012172;
      lvSumLineRec@1210190000 : Record 11012171;
    BEGIN
      "Summary Sheet Amount" := 0;
      IF lvSumRec.GET("Estimate No.", "Summary Sheet") THEN
        lvSumRec.TESTFIELD("Sub-Estimate No.", '');

      lvSumLineRec.SETRANGE("Estimate No.", "Estimate No.");
      lvSumLineRec.SETRANGE(Summary, "Summary Sheet");
      lvSumLineRec.SETRANGE("Contract Amount", TRUE);
      IF lvSumLineRec.FINDFIRST THEN BEGIN
        lvSumLineRec.CALCFIELDS("Subtotal Amount");
        "Summary Sheet Amount" := lvSumLineRec."Subtotal Amount";
      END;
    END;

    PROCEDURE CheckSubEstimates@1210190001();
    VAR
      SubEstRec@1210190000 : Record 11072074;
      SubVersRec@1210190001 : Record 11012765;
      SummarySheetRec@1210190002 : Record 11012172;
    BEGIN
      SubEstRec.SETRANGE("Estimate No.", "Estimate No.");
      IF SubEstRec.FINDSET THEN BEGIN
        REPEAT
          IF NOT SubVersRec.GET("Try-out Quote", "Version Line", "Estimate No.", SubEstRec."Sub-Estimate No.") THEN
          BEGIN
            SubVersRec.INIT;
            SubVersRec."Try-out Quote" := "Try-out Quote";
            SubVersRec."Version Line" := "Version Line";
            SubVersRec."Estimate No." := "Estimate No.";
            SubVersRec."Sub-Estimate No." := SubEstRec."Sub-Estimate No.";

            SummarySheetRec.SETRANGE("Estimate No.","Estimate No.");
            SummarySheetRec.SETRANGE("Sub-Estimate No.",SubEstRec."Sub-Estimate No.");
            IF SummarySheetRec.FINDFIRST THEN
              IF SummarySheetRec.NEXT = 0 THEN
                SubVersRec."Summary Sheet" := SummarySheetRec.Summary;

            SubVersRec.INSERT;
          END;
        UNTIL SubEstRec.NEXT = 0;
      END;
    END;

    BEGIN
    {
      DP01090: flowfield Calculated Amount expired; use function GetCostAmount or GetSalesAmount (table Estimate)
    }
    END.
  }
}

