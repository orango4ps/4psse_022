OBJECT Table 11012577 Plant Hours Costs
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               VALIDATE(Week,Week);
               TESTFIELD("Employee No.");
             END;

    OnDelete=BEGIN
               PlantHourLineRec.RESET;
               PlantHourLineRec.SETRANGE(Year,Year);
               PlantHourLineRec.SETRANGE(Week,Week);
               PlantHourLineRec.SETRANGE("Employee Company","Employee Company");
               PlantHourLineRec.SETRANGE("Employee No.","Employee No.");
               PlantHourLineRec.DELETEALL;
             END;

    OnRename=BEGIN
               PlantHourLineRec.RESET;
               PlantHourLineRec.SETRANGE(Year,xRec.Year);
               PlantHourLineRec.SETRANGE(Week,xRec.Week);
               PlantHourLineRec.SETRANGE("Employee Company",xRec."Employee Company");
               PlantHourLineRec.SETRANGE("Employee No.",xRec."Employee No.");
               IF PlantHourLineRec.FIND('-') THEN
                 REPEAT
                   PlantHourLineRec.TESTFIELD(Status,PlantHourLineRec.Status::Open);
                   IF PlantHourLineRec.Origin <> PlantHourLineRec.Origin::Plant THEN
                     PlantHourLineRec.FIELDERROR(Origin);
                 UNTIL PlantHourLineRec.NEXT = 0;
               VALIDATE(Week,Week);
               TESTFIELD("Employee No.");

               PlantHourLineRec.RESET;
               PlantHourLineRec.SETRANGE(Year,xRec.Year);
               PlantHourLineRec.SETRANGE(Week,xRec.Week);
               PlantHourLineRec.SETRANGE("Employee Company",xRec."Employee Company");
               PlantHourLineRec.SETRANGE("Employee No.",xRec."Employee No.");
               IF PlantHourLineRec.FIND('-') THEN
                 REPEAT
                   PlantHourLineRec2.TRANSFERFIELDS(PlantHourLineRec);
                   PlantHourLineRec2.Year := Year;
                   PlantHourLineRec2.Week := Week;
                   PlantHourLineRec2."Employee No." := "Employee No.";
                   PlantHourLineRec2.INSERT;
                   PlantHourLineRec.DELETE;
                 UNTIL PlantHourLineRec.NEXT = 0;
             END;

    CaptionML=[ENU=Plant Hours Costs;
               NOR=Maskintimer koster;
               SVE=Maskintimmar kostnader];
    LookupPageID=Page11012603;
  }
  FIELDS
  {
    { 20  ;   ;Year                ;Integer       ;OnValidate=BEGIN
                                                                Date2.RESET;
                                                                Date2.SETRANGE("Period Type",Date2."Period Type"::Year);
                                                                Date2.SETRANGE("Period No.",Year);
                                                                IF NOT Date2.FIND('-') THEN
                                                                  FIELDERROR(Year);

                                                                StartDateYear := Date2."Period Start";
                                                                YearEndDate := Date2."Period End";
                                                              END;

                                                   CaptionML=[ENU=Year;
                                                              NOR=èr;
                                                              SVE=èr];
                                                   MinValue=1 }
    { 30  ;   ;Week                ;Integer       ;OnValidate=BEGIN
                                                                IF Year = 0 THEN
                                                                  Year := DATE2DMY(TODAY, 3);
                                                                VALIDATE(Year,Year);
                                                                Date.RESET;
                                                                Date.SETRANGE("Period Type",Date."Period Type"::Week);
                                                                Date.SETRANGE("Period No.",Week);
                                                                Date.SETRANGE("Period Start",CALCDATE('<-3D>',StartDateYear),YearEndDate);
                                                                Date.SETRANGE("Period End",StartDateYear,CALCDATE('<+3D>',YearEndDate));
                                                                IF NOT Date.FIND('-') THEN
                                                                  FIELDERROR(Week);
                                                              END;

                                                   OnLookup=VAR
                                                              DateSelectionManagement@1100409000 : Codeunit 11130005;
                                                            BEGIN
                                                              DateSelectionManagement.GetWeekYear(Week, Year);
                                                              VALIDATE(Week);
                                                            END;

                                                   CaptionML=[ENU=Week;
                                                              NOR=Uke;
                                                              SVE=Vecka];
                                                   MinValue=1;
                                                   MaxValue=53 }
    { 35  ;   ;Employee Company    ;Text30        ;TableRelation=Company;
                                                   OnValidate=BEGIN
                                                                IF "Employee Company" = COMPANYNAME THEN
                                                                  "Employee Company" := '';
                                                              END;

                                                   CaptionML=[ENU=Employee Company;
                                                              NOR=Ansattes selskap;
                                                              SVE=Den anstÑlldes fîretag];
                                                   Description=Only allowed for hours from 'Hour Accounting', so field not editable;
                                                   Editable=No }
    { 40  ;   ;Employee No.        ;Code20        ;TableRelation=IF (Employee Company=CONST()) Employee;
                                                   OnValidate=VAR
                                                                lvEmpRec@1100525000 : Record 5200;
                                                              BEGIN
                                                                IF "Employee No." <> '' THEN BEGIN
                                                                  IF "Employee Company" <> '' THEN
                                                                    lvEmpRec.CHANGECOMPANY("Employee Company");
                                                                  lvEmpRec.GET("Employee No.");
                                                                END;

                                                                CALCFIELDS("Employee Name");
                                                              END;

                                                   CaptionML=[ENU=Employee No.;
                                                              NOR=Ansattnr;
                                                              SVE=AnstÑllningsnr];
                                                   NotBlank=Yes }
    { 50  ;   ;Employee Name       ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Full Name" WHERE (No.=FIELD(Employee No.)));
                                                   CaptionML=[ENU=Employee Name;
                                                              NOR=Navn pÜ ansatt;
                                                              SVE=Namn pÜ anstÑlld];
                                                   Editable=No }
    { 60  ;   ;Open                ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Plant Hours Costs Line" WHERE (Year=FIELD(Year),
                                                                                                     Week=FIELD(Week),
                                                                                                     Employee Company=FIELD(Employee Company),
                                                                                                     Employee No.=FIELD(Employee No.),
                                                                                                     Status=CONST(Open)));
                                                   CaptionML=[ENU=Open;
                                                              NOR=èpne;
                                                              SVE=ôppen];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Year,Week,Employee Company,Employee No. ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PlantHourLineRec@11012000 : Record 11012578;
      PlantHourLineRec2@11012001 : Record 11012578;
      Date@11012004 : Record 2000000007;
      Date2@11012005 : Record 2000000007;
      StartDateYear@11012006 : Date;
      YearEndDate@11012007 : Date;

    PROCEDURE GetEmployeeName@1100525000() : Text[100];
    VAR
      lvEmplRec@1100525000 : Record 5200;
    BEGIN
      IF ("Employee No." <> '') AND ("Employee Company" <> '') THEN BEGIN
        lvEmplRec.CHANGECOMPANY("Employee Company");
        IF lvEmplRec.GET("Employee No.") THEN
          EXIT(lvEmplRec."Full Name")
      END ELSE BEGIN
        CALCFIELDS("Employee Name");
        EXIT("Employee Name");
      END;
      EXIT('');
    END;

    BEGIN
    {
      24-10-2002 Cor Hofman: Copied from table "Plant Hour Revenues"
    }
    END.
  }
}

