OBJECT Table 11071875 Project Collective-List
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               JobSetup@1100528600 : Record 315;
               NoSeriesManagementCU@1100528601 : Codeunit 396;
             BEGIN
               IF "No." = '' THEN BEGIN
                 JobSetup.GET;
                 JobSetup.TESTFIELD("Collective List Nos.");
                 NoSeriesManagementCU.InitSeries(JobSetup."Collective List Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;

               "Created By" := USERID;
               "Creation Date" := TODAY;

               IF "Commission No." <> '' THEN BEGIN
                 CombinedProjPrincComm  :=  "Project No." + "Customer No." + "Commission No.";
                 CombinedProjPrincCommSettl  :=  "Project No." +  "Customer No."  + "Commission No." + "Settl.Sheet No.";
               END ELSE BEGIN
                 CombinedProjPrincComm  :=  '';
                 CombinedProjPrincCommSettl := '';
               END;

               //**4PS Generated Code. Do Not Change.sn05
               DPA_OnInsert;
               //**4PS Generated Code.en05
             END;

    OnModify=BEGIN
               IF "Commission No." <> '' THEN BEGIN
                 CombinedProjPrincComm  :=  "Project No." + "Customer No." + "Commission No.";
                 CombinedProjPrincCommSettl  :=  "Project No." +  "Customer No."  + "Commission No." + "Settl.Sheet No.";
               END ELSE BEGIN
                 CombinedProjPrincComm  :=  '';
                 CombinedProjPrincCommSettl := '';
               END;
             END;

    OnDelete=VAR
               SettlementSheet@1100528600 : Record 11012021;
               ProjectCostPlusEntry@1100528601 : Record 11012019;
             BEGIN
               TESTFIELD(Status, Status::Open);

               SettlementSheet.SETCURRENTKEY("Collective List No.");
               SettlementSheet.SETRANGE("Collective List No.", "No.");
               SettlementSheet.MODIFYALL("Collective List No.", '', TRUE);

               ProjectCostPlusEntry.SETCURRENTKEY("Collective List No.");
               ProjectCostPlusEntry.SETRANGE("Collective List No.", "No.");
               ProjectCostPlusEntry.MODIFYALL("Collective List No.", '', TRUE);
             END;

    CaptionML=[ENU=Project Collective-List;
               NOR=Samlingsfaktura for prosjekt;
               SVE=Projektsamlingsfaktura];
    LookupPageID=Page11126483;
    DrillDownPageID=Page11126483;
  }
  FIELDS
  {
    { 10  ;   ;No.                 ;Code20        ;OnValidate=VAR
                                                                JobSetup@1100528200 : Record 315;
                                                                NoSeriesManagementCU@1100528600 : Codeunit 396;
                                                              BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  JobSetup.GET;
                                                                  NoSeriesManagementCU.TestManual(JobSetup."Collective List Nos.");
                                                                  "No. Series" := '';
                                                                END;

                                                                CheckCollectiveListInUse;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              NOR=Nr.;
                                                              SVE=Nr] }
    { 20  ;   ;Customer No.        ;Code20        ;TableRelation=Customer;
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Status, Status::Open);
                                                                CheckCollectiveListInUse;
                                                              END;

                                                   CaptionML=[ENU=Customer No.;
                                                              NOR=Kundenr.;
                                                              SVE=Kundnr];
                                                   NotBlank=Yes;
                                                   Description=Principal }
    { 22  ;   ;Customer Name       ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Customer.Name WHERE (No.=FIELD(Customer No.)));
                                                   CaptionML=[ENU=Customer Name;
                                                              NOR=Kundnavn;
                                                              SVE=Kundnamn];
                                                   Editable=No }
    { 25  ;   ;Bill-to Customer No.;Code20        ;TableRelation=Customer;
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Status, Status::Open);
                                                                CheckCollectiveListInUse;
                                                                IF "Bill-to Customer No." <> xRec."Bill-to Customer No." THEN
                                                                  VALIDATE("Alternative Bill-to Address", '');
                                                              END;

                                                   CaptionML=[ENU=Bill-to Customer No.;
                                                              NOR=Faktureringskundenr;
                                                              SVE=Faktureringskundnr] }
    { 27  ;   ;Bill-to Customer Name;Text100      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Customer.Name WHERE (No.=FIELD(Bill-to Customer No.)));
                                                   CaptionML=[ENU=Bill-to Customer Name;
                                                              NOR="Faktura til-kunde, navn ";
                                                              SVE=Namn pÜ faktureringskund];
                                                   Editable=No }
    { 28  ;   ;Alternative Bill-to Address;Code10 ;TableRelation="Ship-to Address".Code WHERE (Customer No.=FIELD(Bill-to Customer No.),
                                                                                               Billing Address=CONST(Yes));
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Status, Status::Open);
                                                                CheckCollectiveListInUse;
                                                              END;

                                                   CaptionML=[ENU=Alternative Bill-to Address;
                                                              NOR=Alternativ Faktureringsadresse;
                                                              SVE=Alternativ faktureringsadress] }
    { 30  ;   ;Supervisor          ;Code20        ;OnValidate=VAR
                                                                JobSetup@1100528200 : Record 315;
                                                                ContJobRespRec@1100528201 : Record 5067;
                                                              BEGIN
                                                                TESTFIELD(Status, Status::Open);
                                                                IF Supervisor = '' THEN
                                                                  EXIT;

                                                                JobSetup.GET;
                                                                ContJobRespRec.SETRANGE("Contact No.", Supervisor);
                                                                ContJobRespRec.SETRANGE("Job Responsibility Code", JobSetup."Function Group Supervisor");
                                                                IF NOT ContJobRespRec.FIND('-') THEN
                                                                  ERROR(Text001, Supervisor);

                                                                CheckCollectiveListInUse;
                                                              END;

                                                   CaptionML=[ENU=Supervisor;
                                                              NOR=Ekstern arbeidsleder;
                                                              SVE=Extern arbetsledare] }
    { 31  ;   ;Supervisor Name     ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Supervisor)));
                                                   CaptionML=[ENU=Supervisor Name;
                                                              NOR=Arbeidslederens navn;
                                                              SVE=Arbetsledarens namn];
                                                   Editable=No }
    { 40  ;   ;Project No.         ;Code20        ;TableRelation=Job.No.;
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Status, Status::Open);
                                                                CheckCollectiveListInUse;
                                                              END;

                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr] }
    { 45  ;   ;Commission No.      ;Code20        ;TableRelation=Commission.No.;
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Status, Status::Open);
                                                                CheckCollectiveListInUse;
                                                              END;

                                                   CaptionML=[ENU=Commission No.;
                                                              NOR=Oppdragsnr.;
                                                              SVE=Uppdragsnr] }
    { 46  ;   ;Settl.Sheet No.     ;Code10        ;TableRelation="Settlement Sheet".No.;
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Status, Status::Open);
                                                                CheckCollectiveListInUse;
                                                              END;

                                                   CaptionML=[ENU=Settl.Sheet No.;
                                                              NOR=Avregningsarknr;
                                                              SVE=AvrÑkningsarknr] }
    { 50  ;   ;Status              ;Option        ;OnValidate=BEGIN
                                                                CheckStatus;
                                                              END;

                                                   CaptionML=[ENU=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=[ENU=Open,,,Released,,,Processed;
                                                                    NOR=èpen,,,Frigitt,,,Behandlet;
                                                                    SVE=÷ppen,,,Sl‰ppt,,,Behandlad];
                                                   OptionString=Open,,,Released,,,Processed }
    { 60  ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                TESTFIELD(Status, Status::Open);
                                                                CalcEndingDate;
                                                              END;

                                                   CaptionML=[ENU=Starting Date;
                                                              NOR=Startdato;
                                                              SVE=Startdatum] }
    { 70  ;   ;Invoice Period      ;DateFormula   ;OnValidate=BEGIN
                                                                TESTFIELD(Status, Status::Open);
                                                                CalcEndingDate;
                                                              END;

                                                   CaptionML=[ENU=Invoice Period;
                                                              NOR=Fakturaperiode;
                                                              SVE=Fakturaperiod] }
    { 75  ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                TESTFIELD(Status, Status::Open);
                                                              END;

                                                   CaptionML=[ENU=Ending Date;
                                                              NOR=Sluttdato;
                                                              SVE=Slutdatum] }
    { 80  ;   ;Minimal Invoice Amount;Decimal     ;OnValidate=BEGIN
                                                                TESTFIELD(Status, Status::Open);
                                                              END;

                                                   CaptionML=[ENU=Minimal Invoice Amount;
                                                              NOR=Minimalt fakturabelõp;
                                                              SVE=Min. fakturabelopp] }
    { 90  ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              NOR=Nr-serie;
                                                              SVE=Nr-serie];
                                                   Editable=No }
    { 100 ;   ;Invoice Price       ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Project Cost Plus Entry"."Sales Amount (LCY)" WHERE (Collective List No.=FIELD(No.),
                                                                                                                         Chargeable=CONST(Yes),
                                                                                                                         Invoiced=CONST(No)));
                                                   CaptionML=[ENU=Invoice Price;
                                                              NOR=Fakturapris;
                                                              SVE=Fakturapris];
                                                   Editable=No }
    { 105 ;   ;Invoiced Price      ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Project Cost Plus Entry"."Sales Amount (LCY)" WHERE (Collective List No.=FIELD(No.),
                                                                                                                         Invoiced=CONST(Yes)));
                                                   CaptionML=[ENU=Invoiced Price;
                                                              NOR=Fakturert pris;
                                                              SVE=Fakturerat pris];
                                                   Editable=No }
    { 110 ;   ;Collect Invoices By ;Option        ;OnValidate=BEGIN
                                                                TESTFIELD(Status, Status::Open);
                                                              END;

                                                   CaptionML=[ENU=Collect Invoices By;
                                                              NOR=Samle fakturaer etter;
                                                              SVE=Samla in faktura efter];
                                                   OptionCaptionML=[ENU=Collective-List,Main Project,Project,Commission,Settlement Sheet,Cost Plus Entry;
                                                                    NOR=Samlingsfaktura,Hovedprosjekt,Prosjekt,Kommisjon,Oppgjõrsskjema,Selvkosttransaksjon;
                                                                    SVE=Samlingsfaktura,Huvudprojekt,Projekt,Uppdrag,AvrÑkningsark,SjÑlvkostnader];
                                                   OptionString=Collective-List,Main Project,Project,Commission,Settlement Sheet,Cost Plus Entry }
    { 120 ;   ;System-Created Entry;Boolean       ;CaptionML=[ENU=System-Created Entry;
                                                              NOR=Opprettet automatisk;
                                                              SVE=Systemkreerad trans.] }
    { 130 ;   ;Created By          ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Created By;
                                                              NOR=Created By;
                                                              SVE=Skapad av] }
    { 140 ;   ;Creation Date       ;Date          ;CaptionML=[ENU=Creation Date;
                                                              NOR=Opprettet den;
                                                              SVE=UpplÑggningsdatum] }
    { 170 ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   OnValidate=BEGIN
                                                                CheckCollectiveListInUse;
                                                              END;

                                                   CaptionML=[ENU=Currency Code;
                                                              NOR=Valutakode;
                                                              SVE=Valutakod] }
    { 200 ;   ;CombinedProjPrincComm;Code60       ;CaptionML=ENU=Combined Project Principal Commission }
    { 201 ;   ;CombinedProjPrincCommSettl;Code70  ;CaptionML=ENU=Combined Project Principal Commission Settlement }
    { 210 ;   ;Global Dimension 1 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                //CheckProjectAuthorization(FIELDCAPTION("Global Dimension 1 Code"));
                                                                ValidateShortcutDimCode(1,"Global Dimension 1 Code");
                                                                IF NOT SkipModify THEN
                                                                  MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Global Dimension 1 Code;
                                                              NOR=Global dimensjon 1 kode;
                                                              SVE=Global dimension 1 kod];
                                                   CaptionClass='1,1,1' }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Bill-to Customer No.                     }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528600 : TextConst 'ENU=There are Settlement Sheets linked to this Collective List, changes not allowed;NOR=Det finnes avregningslister koblet til denne samlingsfakturaen, endringer ikke tillatt';
      Text001@1100528601 : TextConst 'ENU=There are Project Cost Plus Entries linked to this Collective List, changes not allowed;NOR=Det er prosjektselvkostposter koblet til denne samlingsfakturaen, endringer ikke tillatt';
      Text004@1100528200 : TextConst 'ENU=There are no valid Settlement Sheets present within the filter of the Collective-List.;NOR="Det er finnes ingen gyldige avregningslister innad i filteret til samlingsfakturaen "';
      DPA_Management@1100525000 : Codeunit 11012783;
      SkipModify@1100528400 : Boolean;

    PROCEDURE AssistEdit@2(OldProjectCollectiveList@1000 : Record 11071875) : Boolean;
    VAR
      JobSetup@1100528600 : Record 315;
      ProjectCollectiveList@1100526200 : Record 11071875;
      NoSeriesManagementCU@1100528601 : Codeunit 396;
    BEGIN
      JobSetup.GET;
      JobSetup.TESTFIELD("Collective List Nos.");
      ProjectCollectiveList := Rec;
      IF NoSeriesManagementCU.SelectSeries(JobSetup."Collective List Nos.", OldProjectCollectiveList."No. Series","No. Series") THEN
      BEGIN
        JobSetup.GET;
        NoSeriesManagementCU.SetSeries("No.");
        Rec := ProjectCollectiveList;
        EXIT(TRUE);
      END;
    END;

    PROCEDURE AssignSettlementSheets@1100528600();
    VAR
      SettlementSheet@1100528600 : Record 11012021;
      SettlementSheetFRM@1100528601 : Page 11012047;
    BEGIN
      SettlementSheet.SETCURRENTKEY(Principal);
      SettlementSheet.SETRANGE("Bill-to Customer No.", "Bill-to Customer No.");
      SettlementSheet.SETRANGE("Alternative Bill-to Address", "Alternative Bill-to Address");
      SettlementSheet.SETRANGE(Principal, "Customer No.");
      SettlementSheet.SETRANGE("Collective List No.", '');
      SettlementSheet.SETRANGE("Currency Code", "Currency Code");
      SettlementSheet.SETRANGE(Status, SettlementSheet.Status::Invoice);
      SettlementSheet.SETRANGE("Global Dimension 1 Code", "Global Dimension 1 Code");

      IF Supervisor <> '' THEN
        SettlementSheet.SETRANGE(Supervisor, Supervisor);
      IF "Project No." <> '' THEN
        SettlementSheet.SETRANGE("Project No.", "Project No.");
      IF "Commission No." <> '' THEN
        SettlementSheet.SETRANGE("Commission No.", "Commission No.");
      IF "Settl.Sheet No." <> '' THEN
        SettlementSheet.SETRANGE("No.", "Settl.Sheet No." );

      IF NOT SettlementSheet.FINDFIRST THEN
        ERROR(Text004);

      SettlementSheetFRM.SetRecordSet(SettlementSheet);
      SettlementSheetFRM.LOOKUPMODE(TRUE);
      SettlementSheetFRM.SETTABLEVIEW(SettlementSheet);
      IF SettlementSheetFRM.RUNMODAL = ACTION::LookupOK THEN BEGIN
        SettlementSheetFRM.GetRecordSet(SettlementSheet);
        IF SettlementSheet.ISEMPTY THEN BEGIN
          SettlementSheet.RESET;
          SettlementSheetFRM.GetRecordSet(SettlementSheet);
          SettlementSheet.SETRECFILTER;
        END;

        IF SettlementSheet.FINDSET THEN
          REPEAT
            SettlementSheet.VALIDATE("Collective List No.", "No.");
            SettlementSheet.MODIFY(TRUE);
          UNTIL SettlementSheet.NEXT = 0;

      END;
    END;

    PROCEDURE CalcEndingDate@1100528601();
    BEGIN
      IF "Starting Date" = 0D THEN
        EXIT;
      IF FORMAT("Invoice Period") = '' THEN
        EXIT;

      "Ending Date" := CALCDATE("Invoice Period", "Starting Date");
    END;

    PROCEDURE CheckCollectiveListInUse@1100528602();
    VAR
      SettlementSheet@1100528600 : Record 11012021;
      ProjectCostPlusEntry@1100528601 : Record 11012019;
    BEGIN
      IF "No." = '' THEN EXIT;

      SettlementSheet.SETCURRENTKEY("Collective List No.");
      SettlementSheet.SETRANGE("Collective List No.", "No.");
      IF NOT SettlementSheet.ISEMPTY THEN
        ERROR(Text000);

      ProjectCostPlusEntry.SETCURRENTKEY("Collective List No.");
      ProjectCostPlusEntry.SETRANGE("Collective List No.", "No.");
      IF NOT ProjectCostPlusEntry.ISEMPTY THEN
        ERROR(Text001);
    END;

    PROCEDURE CheckStatus@1100528605();
    VAR
      SettlementSheet@1100528600 : Record 11012021;
    BEGIN
      IF (xRec.Status <> Status) AND (Status = Status::Released) THEN BEGIN
        MarkSettlementSheets(SettlementSheet);
        IF SettlementSheet.FINDSET THEN
          REPEAT
            SettlementSheet.TESTFIELD(Status,SettlementSheet.Status::Invoice);
          UNTIL SettlementSheet.NEXT = 0;
      END;
    END;

    PROCEDURE MarkSettlementSheets@1100528606(VAR OSettlementSheet@1100528603 : Record 11012021);
    VAR
      ProjectCostPlusEntry@1100528601 : Record 11012019;
      SettlementSheet@1100528602 : Record 11012021;
    BEGIN
      OSettlementSheet.RESET;

      ProjectCostPlusEntry.SETCURRENTKEY("Collective List No.");
      ProjectCostPlusEntry.SETRANGE("Collective List No.", "No.");
      IF ProjectCostPlusEntry.FINDSET THEN
        REPEAT
          OSettlementSheet."Project No."  := ProjectCostPlusEntry."Project No.";
          OSettlementSheet.Principal  := ProjectCostPlusEntry.Principal;
          OSettlementSheet."Commission No."  := ProjectCostPlusEntry."Commission No.";
          OSettlementSheet."No." := ProjectCostPlusEntry."Settl.Sheet No.";
          OSettlementSheet.MARK(TRUE);
        UNTIL ProjectCostPlusEntry.NEXT = 0;

      SettlementSheet.SETCURRENTKEY("Collective List No.");
      SettlementSheet.SETRANGE("Collective List No.", "No.");
      IF SettlementSheet.FINDSET THEN
        REPEAT
          OSettlementSheet."Project No."  := SettlementSheet."Project No.";
          OSettlementSheet.Principal  := SettlementSheet.Principal;
          OSettlementSheet."Commission No."  := SettlementSheet."Commission No.";
          OSettlementSheet."No." := SettlementSheet."No.";
          OSettlementSheet.MARK(TRUE);
        UNTIL SettlementSheet.NEXT = 0;

      OSettlementSheet.MARKEDONLY(TRUE);
    END;

    PROCEDURE GetBilltoAddress@1100528607(VAR OBilltoName@1100528600 : Text[100];VAR OBilltoName2@1100528601 : Text[50];VAR OBilltoAddress@1100528602 : Text[100];VAR OBilltoAddress2@1100528603 : Text[50];VAR OBilltoPostCode@1100528604 : Text[20];VAR OBilltoCity@1100528605 : Text[30]);
    VAR
      Customer@1100528606 : Record 18;
      ShiptoAddress@1100528607 : Record 222;
    BEGIN
      IF "Alternative Bill-to Address" = '' THEN BEGIN
        IF NOT Customer.GET("Bill-to Customer No.") THEN
          Customer.INIT;
        OBilltoName := Customer.Name;
        OBilltoName2 := Customer."Name 2";
        OBilltoAddress := Customer.Address;
        OBilltoAddress2 := Customer."Address 2";
        OBilltoPostCode := Customer."Post Code";
        OBilltoCity := Customer.City;
      END ELSE BEGIN
        IF NOT ShiptoAddress.GET("Bill-to Customer No.", "Alternative Bill-to Address") THEN
          ShiptoAddress.INIT;
        OBilltoName := ShiptoAddress.Name;
        OBilltoName2 := ShiptoAddress."Name 2";
        OBilltoAddress := ShiptoAddress.Address;
        OBilltoAddress2 := ShiptoAddress."Address 2";
        OBilltoPostCode := ShiptoAddress."Post Code";
        OBilltoCity := ShiptoAddress.City;
      END;
    END;

    PROCEDURE GetTotalBasicSalesAmount@1100525000() TotalCost : Decimal;
    VAR
      ProjectCostPlusEntry@1100525000 : Record 11012019;
    BEGIN
      ProjectCostPlusEntry.SETCURRENTKEY("Collective List No.");
      ProjectCostPlusEntry.SETRANGE("Collective List No.", "No.");
      IF ProjectCostPlusEntry.FINDSET THEN
        REPEAT
          TotalCost += ProjectCostPlusEntry."Basic Price (LCY)" * ProjectCostPlusEntry.Quantity;
        UNTIL ProjectCostPlusEntry.NEXT = 0;
    END;

    PROCEDURE GetTotalRevenue@1100525003() TotalRevenue : Decimal;
    VAR
      ProjectCostPlusEntry@1100525000 : Record 11012019;
    BEGIN
      ProjectCostPlusEntry.SETCURRENTKEY("Collective List No.");
      ProjectCostPlusEntry.SETRANGE("Collective List No.", "No.");
      IF ProjectCostPlusEntry.FINDSET THEN
        REPEAT
          ProjectCostPlusEntry.CALCFIELDS("Sales Amount (LCY)", "Invoiced Price");
          TotalRevenue += ProjectCostPlusEntry."Sales Amount (LCY)" + ProjectCostPlusEntry."Invoiced Price";
        UNTIL ProjectCostPlusEntry.NEXT = 0;
    END;

    PROCEDURE GetSurcharge@1210190000(VAR OSurcharge@1210190001 : Decimal;VAR OSurchargePerc@1210190002 : Decimal);
    VAR
      TotalBasicAmount@1210190000 : Decimal;
    BEGIN
      TotalBasicAmount := GetTotalBasicSalesAmount;
      OSurcharge := "Invoiced Price" + "Invoice Price" - TotalBasicAmount;
      IF TotalBasicAmount <> 0 THEN
        OSurchargePerc := ROUND(OSurcharge / TotalBasicAmount * 100)
      ELSE
        OSurchargePerc := 0;
    END;

    PROCEDURE GetTotalInvoicePrice@1100528608() : Decimal;
    BEGIN
      CALCFIELDS("Invoice Price");
      EXIT("Invoice Price");
    END;

    PROCEDURE ValidateShortcutDimCode@29(FieldNumber@1000 : Integer;ShortcutDimCode@1001 : Code[20]);
    VAR
      DimMgt@1100525000 : Codeunit 408;
    BEGIN
      DimMgt.ValidateDimValueCode(FieldNumber,ShortcutDimCode);
      IF NOT SkipModify THEN
        MODIFY(TRUE);
    END;

    PROCEDURE DPA_SetDefaultDpmtFromNoSeries@1100000995(xRecRef@1100525002 : RecordRef);
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN
        EXIT;

      xRecRef.SETTABLE(xRec);
      DPA_Management.SetDepartmentCodeNoSeries(
        TABLENAME,
        "Global Dimension 1 Code",
        "No. Series",
        xRec."No. Series" );
    END;

    LOCAL PROCEDURE DPA_OnInsert@1100000991();
    VAR
      Dimension1Code@1100528400 : Code[20];
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN
        EXIT;

      Dimension1Code :=
        DPA_Management.GetNoSeriesDepartmentCode(
          "No. Series",
          xRec."No. Series",
          DATABASE::Job);
      IF Dimension1Code <> '' THEN
        "Global Dimension 1 Code" := Dimension1Code;

      IF "Global Dimension 1 Code" <> '' THEN BEGIN
        SkipModify := TRUE;
        VALIDATE("Global Dimension 1 Code");
        SkipModify := FALSE;
      END;
    END;

    PROCEDURE DPA_GetFieldNoDepartmentFilter@1100000993() : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      EXIT(0);
    END;

    PROCEDURE DPA_GetFiltersFltrGrp@1100000997(FilterGroupNo@1100525000 : Integer) FilterText : Text[250];
    BEGIN
      //**4PS Generated Code - Do Not Change
      FILTERGROUP(FilterGroupNo);
      FilterText := COPYSTR(GETFILTERS, 1, MAXSTRLEN(FilterText));
      FILTERGROUP(0);
    END;

    PROCEDURE DPA_SetFilters@1100000998(Context@1100525000 : Text[30]) : Boolean;
    VAR
      RecRef@1100525003 : RecordRef;
      i@1100525004 : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN
        EXIT;

      RecRef.GETTABLE(Rec);
      DPA_Management.SetDPAFilterFromContext(
        RecRef,
        Context,
        DPA_GetFieldNoDepartmentCode,
        DPA_GetFieldNoDepartmentFilter );

      FOR i := 10 DOWNTO 0 DO BEGIN
        RecRef.FILTERGROUP(i);
        FILTERGROUP(i);
        SETVIEW(RecRef.GETVIEW(FALSE));
      END;

      EXIT(DPA_Management.IsHardFilter);
    END;

    PROCEDURE DPA_IsActive@1100000999() : Boolean;
    VAR
      DepartmentAuthorizObject@1100526000 : Record 11012466;
      FPSLicenseManagement@1100527400 : Codeunit 11229289;
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT FPSLicenseManagement.LicenseAndReadPermissionForTable(DepartmentAuthorizObject) THEN
        EXIT(FALSE);

      IF NOT DPA_Management.AuthorizationActive THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    PROCEDURE DPA_FindRecWithDPAFilterAppld@1100000989();
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN BEGIN
        FIND;
        EXIT;
      END;

      IF NOT DPA_Management.HasTableHardFilter(11071875) THEN BEGIN
        FIND;
        EXIT;
      END;

      DPA_SetFilters('Table 11071875');
      IF NOT FIND THEN
        DPA_Management.RaiseDPAErrorForTable(TABLECAPTION);
    END;

    PROCEDURE DPA_ChkDepartmentCodeAllowed@1100000990(IsHardFilter@1100526000 : Boolean);
    VAR
      DimensionManagement@1100526001 : Codeunit 408;
    BEGIN
      //**4PS Generated Code - Do Not Change
      DimensionManagement.CheckDepartmAuthWithFilterType(
        DATABASE::Job, "Global Dimension 1 Code", IsHardFilter);
    END;

    PROCEDURE DPA_GetFieldNoDepartmentCode@1100000994() : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      EXIT(FIELDNO("Global Dimension 1 Code"));
    END;

    PROCEDURE DPA_SetDefaultDepartment@1100000996();
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN
        EXIT;

      DPA_Management.SetDepartmentCodeUser(TABLENAME, "Global Dimension 1 Code");
    END;

    BEGIN
    END.
  }
}

