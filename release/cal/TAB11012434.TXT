OBJECT Table 11012434 Project Planning Base Activity
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Code,Description;
    OnInsert=VAR
               RecRef@1100528400 : RecordRef;
             BEGIN
               RecRef.GETTABLE(Rec);
               "Record ID" := RecRef.RECORDID;
             END;

    OnDelete=VAR
               BaseActResourceRec@1210190000 : Record 11125715;
               BaseActPlanningConstraint@1210190001 : Record 11125717;
             BEGIN
               BaseActResourceRec.SETRANGE("Activity Code", Code);
               BaseActResourceRec.DELETEALL(TRUE);

               BaseActPlanningConstraint.RESET;
               BaseActPlanningConstraint.SETCURRENTKEY("From Activity No.","To Activity No.");
               BaseActPlanningConstraint.SETRANGE("From Activity No.", Code);
               BaseActPlanningConstraint.DELETEALL(TRUE);

               BaseActPlanningConstraint.RESET;
               BaseActPlanningConstraint.SETCURRENTKEY("To Activity No.","From Activity No.");
               BaseActPlanningConstraint.SETRANGE("To Activity No.", Code);
               BaseActPlanningConstraint.DELETEALL(TRUE);

               DeleteCommentLines;
             END;

    OnRename=BEGIN
               ERROR(Text001);
             END;

    CaptionML=[ENU=Project Planning Base Activity;
               NOR=Prosjektplanlegging, basisaktivtet;
               SVE=Projektplaneringsbas, aktivitet];
    LookupPageID=Page11124753;
    DrillDownPageID=Page11124753;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;OnValidate=BEGIN
                                                                FormatManagement.CreateProjPlanningBaseActivity(Code, Chapter, Paragraph, Level);
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 10  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 20  ;   ;Type                ;Option        ;OnValidate=VAR
                                                                ProjPlanBActResourceRec@1210190000 : Record 11125715;
                                                              BEGIN
                                                                IF (Type <> Type::Activity) AND (Code <> '') THEN BEGIN
                                                                  ProjPlanBActResourceRec.SETRANGE("Activity Code", Code);
                                                                  IF NOT ProjPlanBActResourceRec.ISEMPTY THEN BEGIN
                                                                      IF CONFIRM(STRSUBSTNO(Text002, ProjPlanBActResourceRec.TABLECAPTION, TRUE)) THEN
                                                                        ProjPlanBActResourceRec.DELETEALL
                                                                      ELSE
                                                                        ERROR('')
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=[ENU=Activity,Total,Milestone;
                                                                    NOR=Aktivitet,Total,Milep‘l;
                                                                    SVE=Aktivitet,Totalt,Milstolpe];
                                                   OptionString=Activity,Summary,Milestone }
    { 250 ;   ;Production Quantity ;Decimal       ;CaptionML=[ENU=Production Quantity;
                                                              NOR=Produksjon antall;
                                                              SVE=Produktion antal] }
    { 260 ;   ;Production Unit     ;Code10        ;CaptionML=[ENU=Production Unit;
                                                              NOR=Produksjonsenhet;
                                                              SVE=Produktionsenhet] }
    { 270 ;   ;Production Rate per day;Decimal    ;CaptionML=[ENU=Production Rate per day;
                                                              NOR=Produksjonsrate per dag;
                                                              SVE=Produktionstakt per dag] }
    { 400 ;   ;Level               ;Integer       ;CaptionML=[ENU=Level;
                                                              NOR=Niv†;
                                                              SVE=Niv†];
                                                   Editable=No }
    { 410 ;   ;Chapter             ;Code20        ;CaptionML=[ENU=Chapter;
                                                              NOR=Kapittel;
                                                              SVE=Kapitel];
                                                   Editable=No }
    { 420 ;   ;Paragraph           ;Code20        ;CaptionML=[ENU=Paragraph;
                                                              NOR=Avsnitt;
                                                              SVE=Stycke];
                                                   Editable=No }
    { 430 ;   ;Record ID           ;RecordID      ;CaptionML=[ENU=Record ID;
                                                              NOR=Post-ID;
                                                              SVE=Post-ID] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1210190000 : TextConst 'ENU=Rename not allowed.;NOR=Ikke tillatt † bytte navn.;SVE=Det „r inte till†tet att byta namn.';
      Text002@1210190001 : TextConst 'ENU=Delete related recordsin table %3?;NOR=Fjerne relaterte poster i tabell %3?;SVE=Ta bort relaterad poster i tabell %3?';
      FormatManagement@1100528400 : Codeunit 11012022;

    PROCEDURE EditCommentLines@1100529800();
    VAR
      CommentLineJobText@1100529800 : Record 11020634;
      RecRef@1100529801 : RecordRef;
      RecID@1100529802 : RecordID;
    BEGIN
      RecRef.GETTABLE(Rec);
      RecID := RecRef.RECORDID();
      CommentLineJobText.EditCommentLines(CommentLineJobText."Table Name"::"Project Planning Base Activity", RecID);
    END;

    PROCEDURE CommentLinesExist@1210190011() : Boolean;
    VAR
      RecRef@1210190000 : RecordRef;
      CommentLineJobText@1210190001 : Record 11020634;
    BEGIN
      RecRef.GETTABLE(Rec);
      CommentLineJobText.SETRANGE("Table Name", CommentLineJobText."Table Name"::"Project Planning Base Activity");
      CommentLineJobText.SETFILTER("Record ID", FORMAT(RecRef.RECORDID));
      IF NOT CommentLineJobText.ISEMPTY THEN
        EXIT(TRUE);
    END;

    LOCAL PROCEDURE DeleteCommentLines@1100528415();
    VAR
      CommentLineJobText@1100529801 : Record 11020634;
      RecRef@1100529800 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      CommentLineJobText.RESET;
      CommentLineJobText.SETRANGE("Table Name", CommentLineJobText."Table Name"::"Project Planning Base Activity");
      CommentLineJobText.SETFILTER("Record ID", FORMAT(RecRef.RECORDID));
      CommentLineJobText.DELETEALL(TRUE);
    END;

    PROCEDURE ActivityCodeIsFormatted@1100528407() : Boolean;
    VAR
      JobsSetup@1100528400 : Record 315;
    BEGIN
      JobsSetup.GET;
      IF JobsSetup."Planning Activity Format" <> '' THEN
        EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

