OBJECT Table 5941 Service Item Component
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    OnModify=BEGIN
               IF Active AND (Type <> xRec.Type) THEN BEGIN
                 ServItemComponent.RESET;
                 ServItemComponent.SETCURRENTKEY(Active,"Parent Service Item No.","Line No.");
                 ServItemComponent.SETRANGE(Active,FALSE);
                 ServItemComponent.SETRANGE("Parent Service Item No.","Parent Service Item No.");
                 IF ServItemComponent.FINDLAST THEN
                   NextNo := ServItemComponent."Line No." + 1
                 ELSE
                   NextNo := 1;
                 ServItemComponent := xRec;
                 ServItemComponent.Active := FALSE;
                 ServItemComponent."Line No." := NextNo;
                 ServItemComponent."From Line No." := "Line No.";
                 ServItemComponent."Last Date Modified" := TODAY;
                 ServItemComponent.INSERT;
               END;
             END;

    OnDelete=VAR
               ConfirmManagement@1000 : Codeunit 27;
             BEGIN
               IF Active THEN BEGIN
                 ServItemComponent.RESET;
                 ServItemComponent.SETCURRENTKEY(Active,"Parent Service Item No.","From Line No.");
                 ServItemComponent.SETRANGE(Active,FALSE);
                 ServItemComponent.SETRANGE("Parent Service Item No.","Parent Service Item No.");
                 ServItemComponent.SETRANGE("From Line No.","Line No.");
                 IF ServItemComponent.FINDFIRST THEN
                   IF NOT ConfirmManagement.ConfirmProcess(STRSUBSTNO(Text002,"No."),TRUE) THEN
                     ERROR('');
                 ServItemComponent.DELETEALL;
               END;
             END;

    CaptionML=[ENU=Service Item Component;
               NOR=Servicevarekomponent];
    LookupPageID=Page5986;
    DrillDownPageID=Page5986;
  }
  FIELDS
  {
    { 1   ;   ;Parent Service Item No.;Code20     ;TableRelation="Service Item";
                                                   CaptionML=[ENU=Parent Service Item No.;
                                                              NOR=Nr. p† overordnet servicevare] }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 4   ;   ;Active              ;Boolean       ;CaptionML=[ENU=Active;
                                                              NOR=Aktiv;
                                                              SVE=Aktiv] }
    { 5   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF Type <> xRec.Type THEN
                                                                  VALIDATE("No.",'');
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              NOR=Type;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=[ENU=Service Item,Item;
                                                                    NOR=Servicevare,Vare];
                                                   OptionString=Service Item,Item }
    { 6   ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST(Service Item)) "Service Item"
                                                                 ELSE IF (Type=CONST(Item)) Item;
                                                   OnValidate=BEGIN
                                                                IF "No." <> '' THEN BEGIN
                                                                  IF Type = Type::"Service Item" THEN BEGIN
                                                                    IF "No." = "Parent Service Item No." THEN
                                                                      ERROR(
                                                                        Text000,
                                                                        Type,"No.",TABLECAPTION,ServItem.TABLECAPTION,"Parent Service Item No.");
                                                                    ServItem.GET("No.");
                                                                    "Serial No." := ServItem."Serial No.";
                                                                    "Variant Code" := ServItem."Variant Code";
                                                                    Description := ServItem.Description;
                                                                    "Description 2" := ServItem."Description 2";
                                                                  END ELSE BEGIN
                                                                    Item.GET("No.");
                                                                    "Serial No." := '';
                                                                    Description := Item.Description;
                                                                    "Description 2" := Item."Description 2";
                                                                  END;
                                                                  "Date Installed" := WORKDATE;
                                                                END ELSE BEGIN
                                                                  "No." := '';
                                                                  "Date Installed" := 0D;
                                                                  "Serial No." := '';
                                                                  Description := '';
                                                                  "Description 2" := '';
                                                                END;

                                                                VALIDATE("Serial No.");
                                                              END;

                                                   OnLookup=BEGIN
                                                              IF Type = Type::Item THEN BEGIN
                                                                Item."No." := xRec."No.";
                                                                IF PAGE.RUNMODAL(0,Item) = ACTION::LookupOK THEN
                                                                  VALIDATE("No.",Item."No.");
                                                              END ELSE BEGIN
                                                                ServItem.GET("Parent Service Item No.");
                                                                ServItem2.RESET;
                                                                ServItem2.SETCURRENTKEY("Customer No.","Ship-to Code");
                                                                ServItem2.SETRANGE("Customer No.",ServItem."Customer No.");
                                                                ServItem2.SETRANGE("Ship-to Code",ServItem."Ship-to Code");
                                                                ServItem2."No." := "No.";
                                                                IF PAGE.RUNMODAL(0,ServItem2) = ACTION::LookupOK THEN
                                                                  VALIDATE("No.",ServItem2."No.");
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=No.;
                                                              NOR=Nr.;
                                                              SVE=Nr] }
    { 7   ;   ;Date Installed      ;Date          ;CaptionML=[ENU=Date Installed;
                                                              NOR=Installert den] }
    { 8   ;   ;Variant Code        ;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Variant".Code WHERE (Item No.=FIELD(No.));
                                                   OnValidate=BEGIN
                                                                IF Type = Type::"Service Item" THEN
                                                                  IF "No." <> '' THEN BEGIN
                                                                    ServItem.GET("No.");
                                                                    TESTFIELD("Variant Code",ServItem."Variant Code");
                                                                  END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              CASE Type OF
                                                                Type::Item:
                                                                  BEGIN
                                                                    ItemVariant.RESET;
                                                                    ItemVariant.SETRANGE("Item No.","No.");
                                                                    IF PAGE.RUNMODAL(PAGE::"Item Variants",ItemVariant) = ACTION::LookupOK THEN
                                                                      "Variant Code" := ItemVariant.Code;
                                                                  END;
                                                                Type::"Service Item":
                                                                  BEGIN
                                                                    IF "No." <> '' THEN BEGIN
                                                                      ServItem.GET("No.");
                                                                      ItemVariant.RESET;
                                                                      ItemVariant.SETRANGE("Item No.",ServItem."Item No.");
                                                                    END ELSE
                                                                      ItemVariant.SETRANGE("Item No.",'');
                                                                    IF PAGE.RUNMODAL(PAGE::"Item Variants",ItemVariant) = ACTION::LookupOK THEN
                                                                      VALIDATE("Variant Code",ItemVariant.Code);
                                                                  END;
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Variant Code;
                                                              NOR=Variantkode;
                                                              SVE=Variantkod] }
    { 11  ;   ;Serial No.          ;Code50        ;OnValidate=BEGIN
                                                                IF (Type = Type::"Service Item") AND
                                                                   ("No." <> '')
                                                                THEN BEGIN
                                                                  ServItem.GET("No.");
                                                                  IF "Serial No." <> ServItem."Serial No." THEN
                                                                    ERROR(Text001,FIELDCAPTION("Serial No."),Type,"No.");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Serial No.;
                                                              NOR=Serienr.;
                                                              SVE=Tillverkningsnr] }
    { 12  ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 13  ;   ;Description 2       ;Text50        ;CaptionML=[ENU=Description 2;
                                                              NOR=Beskrivelse 2;
                                                              SVE=Beskrivning 2] }
    { 15  ;   ;Service Order No.   ;Code20        ;OnLookup=BEGIN
                                                              ServOrderMgt.ServHeaderLookup(1,"Service Order No.");
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Service Order No.;
                                                              NOR=Serviceordrenr.;
                                                              SVE=Serviceordernr] }
    { 16  ;   ;From Line No.       ;Integer       ;CaptionML=[ENU=From Line No.;
                                                              NOR=Fra linjenr.] }
    { 17  ;   ;Last Date Modified  ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              NOR=Endret den;
                                                              SVE=Uppdateringsdatum] }
  }
  KEYS
  {
    {    ;Active,Parent Service Item No.,Line No. ;Clustered=Yes }
    {    ;Active,Parent Service Item No.,From Line No. }
    {    ;Type,No.,Active                          }
    {    ;Active,Parent Service Item No.,Type,No.  }
    {    ;Parent Service Item No.,Line No.         }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 %2 can not be a component in %3 for %4 %5;NOR=%1 %2 kan ikke v‘re en komponent i %3 for %4 %5';
      Text001@1001 : TextConst 'ENU=%1 must be the same as in %2 %3.;NOR=%1 m† v‘re den samme som i %2 %3.';
      ServItemComponent@1002 : Record 5941;
      ServItem@1003 : Record 5940;
      ServItem2@1004 : Record 5940;
      Item@1005 : Record 27;
      ItemVariant@1006 : Record 5401;
      ServOrderMgt@1007 : Codeunit 5900;
      NextNo@1008 : Integer;
      Text002@1009 : TextConst 'ENU=Component %1 has replacements\Do you want to delete this Component?;NOR=Det finnes erstatninger for komponenten %1.\Vil du slette denne komponenten?';

    [External]
    PROCEDURE AssistEditSerialNo@1();
    VAR
      ItemLedgEntry@1000 : Record 32;
    BEGIN
      TESTFIELD(Type,Type::Item);
      CLEAR(ItemLedgEntry);
      ItemLedgEntry.SETCURRENTKEY("Item No.",Open);
      ItemLedgEntry.SETRANGE("Item No.","No.");
      ItemLedgEntry.SETRANGE(Open,TRUE);
      IF "Variant Code" <> '' THEN
        ItemLedgEntry.SETRANGE("Variant Code","Variant Code");
      ItemLedgEntry.SETFILTER("Serial No.",'<>%1','');
      IF PAGE.RUNMODAL(0,ItemLedgEntry) = ACTION::LookupOK THEN
        "Serial No." := ItemLedgEntry."Serial No.";
    END;

    [External]
    PROCEDURE SplitLineNo@2(xServiceItemComponent@1000 : Record 5941;BelowxRec@1002 : Boolean) ResultLineNo : Integer;
    VAR
      ServiceItemComponent@1001 : Record 5941;
    BEGIN
      IF "Line No." <> 0 THEN
        ResultLineNo := "Line No."
      ELSE BEGIN
        ServiceItemComponent.SETCURRENTKEY("Parent Service Item No.","Line No.");
        TESTFIELD("Parent Service Item No.");
        ServiceItemComponent.SETRANGE("Parent Service Item No.","Parent Service Item No.");
        IF BelowxRec THEN BEGIN
          IF ServiceItemComponent.FINDLAST THEN;
          ResultLineNo := ServiceItemComponent."Line No." + 10000;
        END ELSE
          IF "Parent Service Item No." = xServiceItemComponent."Parent Service Item No." THEN BEGIN
            ServiceItemComponent.SETFILTER("Line No.",'<%1',xServiceItemComponent."Line No.");
            IF ServiceItemComponent.FINDLAST THEN;
            ResultLineNo := ROUND((xServiceItemComponent."Line No." + ServiceItemComponent."Line No.") / 2,1);
          END ELSE BEGIN
            IF ServiceItemComponent.FINDLAST THEN;
            ResultLineNo := ServiceItemComponent."Line No." + 10000;
          END;
      END;
    END;

    BEGIN
    END.
  }
}

