OBJECT Table 11229348 Project Capacity Need
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CASE Level OF
                 Level::Project:
                   BEGIN
                     Discipline := '';
                     Element := '';
                   END;
                 Level::Discipline:
                   Element := '';
               END;

               UpdateEstimatedHoursMain("Estimated Hours");
               UpdateTotalLine("Budget Hours","Estimated Hours");
             END;

    OnModify=BEGIN
               UpdateEstimatedHoursMain("Estimated Hours" - PreviousEstimatedHours);
               UpdateTotalLine("Budget Hours" - PreviousBudgetHours,"Estimated Hours" - PreviousEstimatedHours);
               UpdateNeedSubLines;
             END;

    OnDelete=VAR
               ProjectCapacityNeedDetail@1100528500 : Record 11229349;
               ProjectScheduleEmployee@1100528501 : Record 11229412;
               ProjectCapacityNeed2@1100528502 : Record 11229348;
             BEGIN
               ProjectCapacityNeedDetail.SETRANGE("Project No.","Project No.");
               ProjectCapacityNeedDetail.SETRANGE(Element,Element);
               ProjectCapacityNeedDetail.SETRANGE(Discipline,Discipline);
               ProjectCapacityNeedDetail.SETRANGE("Line Type","Line Type");
               ProjectCapacityNeedDetail.SETRANGE("Detail Line No.","Detail Line No.");
               ProjectCapacityNeedDetail.DELETEALL(TRUE);

               IF "Line Type" = "Line Type"::Main THEN BEGIN
                 ProjectScheduleEmployee.SETRANGE("Project No.","Project No.");
                 CASE Level OF
                   Level::Project:
                     ProjectScheduleEmployee.DELETEALL(TRUE);
                   Level::Discipline:
                     BEGIN
                       ProjectScheduleEmployee.SETRANGE(Discipline,Discipline);
                       ProjectScheduleEmployee.DELETEALL(TRUE);
                     END;
                   Level::"Element - Discipline":
                     BEGIN
                       IF Discipline <> '' THEN BEGIN
                         ProjectCapacityNeed2.SETRANGE("Project No.","Project No.");
                         ProjectCapacityNeed2.SETFILTER(Element,'<>%1',Element);
                         ProjectCapacityNeed2.SETRANGE(Discipline,Discipline);
                         IF ProjectCapacityNeed2.ISEMPTY THEN BEGIN
                           //Discipline not used for other Elements
                           ProjectScheduleEmployee.SETRANGE(Discipline,Discipline);
                           ProjectScheduleEmployee.DELETEALL(TRUE);
                         END;
                       END;
                     END;
                 END;
               END;

               IF NOT SubLinesDeleteMode THEN BEGIN
                 UpdateEstimatedHoursMain(-PreviousEstimatedHours);
                 UpdateTotalLine(-PreviousBudgetHours,-PreviousEstimatedHours);
                 DeleteNeedSubLines;
               END;
             END;

    OnRename=VAR
               ProjectCapacityNeedDetail@1100528500 : Record 11229349;
               ProjectCapacityNeedDetail2@1100528501 : Record 11229349;
             BEGIN
               ProjectCapacityNeedDetail.SETRANGE("Project No.",xRec."Project No.");
               ProjectCapacityNeedDetail.SETRANGE(Element,xRec.Element);
               ProjectCapacityNeedDetail.SETRANGE(Discipline,xRec.Discipline);
               ProjectCapacityNeedDetail.SETRANGE("Line Type",xRec."Line Type");
               ProjectCapacityNeedDetail.SETRANGE("Detail Line No.",xRec."Detail Line No.");
               IF ProjectCapacityNeedDetail.FINDSET(TRUE) THEN
                 REPEAT
                   ProjectCapacityNeedDetail2 := ProjectCapacityNeedDetail;
                   ProjectCapacityNeedDetail2.RENAME("Project No.",Element,Discipline,"Line Type","Detail Line No.",ProjectCapacityNeedDetail."Need Date");
                 UNTIL ProjectCapacityNeedDetail.NEXT = 0;

               RenameNeedSubLines;
             END;

    CaptionML=[ENU=Project Capacity Need;
               SVE=Projektkapacitetbehov];
  }
  FIELDS
  {
    { 5   ;   ;Level               ;Option        ;CaptionML=[ENU=Level;
                                                              SVE=Niv†];
                                                   OptionCaptionML=ENU=Project,Discipline,,Element - Discipline,,,Total;
                                                   OptionString=Project,Discipline,,Element - Discipline,,,Total;
                                                   Editable=No }
    { 10  ;   ;Project No.         ;Code20        ;TableRelation=Job.No.;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projekt- nr];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 20  ;   ;Element             ;Code20        ;TableRelation="Project Element".Element WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=[ENU=Element;
                                                              NOR=Element;
                                                              SVE=Element];
                                                   Editable=No }
    { 30  ;   ;Element Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Element".Description WHERE (Project No.=FIELD(Project No.),
                                                                                                           Element=FIELD(Element)));
                                                   CaptionML=[ENU=Element Description;
                                                              NOR=Elementbeskrivelse;
                                                              SVE=Elementbeskrivning];
                                                   Editable=No }
    { 35  ;   ;Line Type           ;Option        ;CaptionML=[ENU=Line Type;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=ENU=Main,Sub;
                                                   OptionString=Main,Sub;
                                                   Editable=No }
    { 36  ;   ;Detail Line No.     ;Integer       ;CaptionML=ENU=Detail Line No.;
                                                   Editable=No }
    { 40  ;   ;Discipline          ;Code10        ;FieldClass=Normal;
                                                   TableRelation=Discipline;
                                                   CaptionML=[ENU=Discipline;
                                                              NOR=Disiplin;
                                                              SVE=Disciplin];
                                                   Editable=No }
    { 50  ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                IF "Starting Date" <> 0D THEN
                                                                  IF ("Starting Date" > "Ending Date") AND ("Ending Date" <> 0D) THEN
                                                                    FIELDERROR("Starting Date",STRSUBSTNO(Text001,FIELDCAPTION("Ending Date")));
                                                              END;

                                                   CaptionML=[ENU=Starting Date;
                                                              NOR=Startdato;
                                                              SVE=Startdatum] }
    { 60  ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                IF "Ending Date" <> 0D THEN
                                                                  IF ("Starting Date" > "Ending Date") AND ("Ending Date" <> 0D) THEN
                                                                    FIELDERROR("Ending Date",STRSUBSTNO(Text002,FIELDCAPTION("Starting Date")));
                                                              END;

                                                   CaptionML=[ENU=Ending Date;
                                                              NOR=Sluttdato;
                                                              SVE=Slutdatum] }
    { 70  ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              SVE=Beskrivning] }
    { 80  ;   ;Project Status      ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Job."Project Status" WHERE (No.=FIELD(Project No.)));
                                                   CaptionML=[ENU=Project Status;
                                                              SVE=Projektstatus];
                                                   OptionCaptionML=[ENU=Estimation,Preparation,Production,Technical Finished,Administrative Finished,Finished,Archive,Archived;
                                                                    SVE=Kalkyl,F”rberedelse,Produktion,Tekniskt f„rdig,Administrativt f„rdig,F„rdig,Arkiv,Arkiverad];
                                                   OptionString=Estimation,Preparation,Production,Technical Finished,Administrative Finished,Finished,Archive,Archived;
                                                   Editable=No }
    { 90  ;   ;Reference Date      ;Date          ;CaptionML=[ENU=Reference Date;
                                                              SVE=Referensdatum] }
    { 110 ;   ;Budget Hours        ;Decimal       ;CaptionML=[ENU=Budget Hours;
                                                              NOR=Reelle timer;
                                                              SVE=Verkliga timmar];
                                                   DecimalPlaces=0:2;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 120 ;   ;Requested Hours     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Project Capacity Need Detail"."Requested Hours" WHERE (Project No.=FIELD(Project No.),
                                                                                                                           Element=FIELD(Element),
                                                                                                                           Discipline=FIELD(Discipline),
                                                                                                                           Line Type=FIELD(Line Type),
                                                                                                                           Detail Line No.=FIELD(Detail Line No.)));
                                                   CaptionML=[ENU=Requested Hours;
                                                              NOR=nskede timer;
                                                              SVE=Beg„rda timmar];
                                                   DecimalPlaces=0:2;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 130 ;   ;Estimated Hours     ;Decimal       ;CaptionML=[ENU=Estimated Hours;
                                                              SVE=Kalkylerade timmar];
                                                   DecimalPlaces=0:2;
                                                   BlankZero=Yes }
    { 140 ;   ;Project Manager     ;Code20        ;TableRelation=Employee;
                                                   CaptionML=[ENU=Project Manager;
                                                              SVE=Platschef];
                                                   Description=only relevant for overall capacity }
  }
  KEYS
  {
    {    ;Level,Project No.,Element,Discipline,Line Type,Detail Line No.;
                                                   Clustered=Yes }
    {    ;Level,Project No.,Discipline             }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ConfirmDeleteOfCapNeed@1100528500 : TextConst 'ENU=There are lines present of a different level. Do you want to delete these ?';
      WithoutDiscipline@1100528502 : TextConst 'ENU=Without Discipline';
      SubLinesDeleteMode@1100528503 : Boolean;
      Text001@1100528505 : TextConst 'ENU=must occur before %1;SVE=m†ste intr„ffa f”re %1';
      Text002@1100528504 : TextConst 'ENU=Must occur after %1';

    PROCEDURE GetStyle@13(Quantity@1100528500 : Decimal) : Text;
    BEGIN
      IF Quantity >= 0 THEN
        EXIT('Standard')
      ELSE
        EXIT('Unfavorable');
    END;

    PROCEDURE GetStyleLineType@1100528521() : Text;
    VAR
      ProjectCapacityNeed@1100528500 : Record 11229348;
    BEGIN
      IF "Line Type" = "Line Type"::Main THEN BEGIN
        ProjectCapacityNeed.SETRANGE(Level,Level);
        ProjectCapacityNeed.SETRANGE("Project No.","Project No.");
        ProjectCapacityNeed.SETRANGE(Element,Element);
        ProjectCapacityNeed.SETRANGE(Discipline,Discipline);
        ProjectCapacityNeed.SETRANGE("Line Type","Line Type"::Sub);
        IF NOT ProjectCapacityNeed.ISEMPTY THEN
          EXIT('Strong');
      END;

      EXIT('Standard');
    END;

    PROCEDURE GetStyleActualHours@1100528528(DisciplineUnknown@1100528500 : Boolean) : Text;
    BEGIN
      IF DisciplineUnknown THEN
        EXIT('Subordinate')
      ELSE
        EXIT('Standard');
    END;

    PROCEDURE GetNewDetailLineNo@1100528500() : Integer;
    VAR
      ProjectCapacityNeed@1100528500 : Record 11229348;
    BEGIN
      IF "Line Type" = "Line Type"::Main THEN
        EXIT(0);

      ProjectCapacityNeed.SETRANGE(Level,Level);
      ProjectCapacityNeed.SETRANGE("Project No.","Project No.");
      ProjectCapacityNeed.SETRANGE(Element,Element);
      ProjectCapacityNeed.SETRANGE(Discipline,Discipline);
      ProjectCapacityNeed.SETRANGE("Line Type","Line Type");
      IF ProjectCapacityNeed.FINDLAST THEN
        EXIT(ProjectCapacityNeed."Detail Line No." + 10000);

      EXIT(10000);
    END;

    PROCEDURE CreateProjectCapacityNeed@1100525001(CapacityLevel@1100528502 : Option;ProjectNo@1100528500 : Code[20]);
    VAR
      Project@1100528503 : Record 11072003;
      ProjectElement@1100525001 : Record 11012010;
      DummyProjectElement@1100528504 : Record 11012010;
      ProjectCapacityNeed@1100525000 : Record 11229348;
      Discipline@1100528501 : Record 11020204;
      DummyDiscipline@1100528505 : Record 11020204;
      BudgetLine@1100528506 : Record 11012001;
      BudgetLineWithoutDisciplinePresent@1100528508 : Boolean;
    BEGIN
      IF ProjectNo = '' THEN
        EXIT;

      ProjectCapacityNeed.SETFILTER(Level,'<>%1&<>%2',CapacityLevel,Level::Total);
      ProjectCapacityNeed.SETRANGE("Project No.",ProjectNo);
      IF NOT ProjectCapacityNeed.ISEMPTY THEN BEGIN
        IF NOT CONFIRM(ConfirmDeleteOfCapNeed) THEN
          ERROR('');
        ProjectCapacityNeed.SETRANGE(Level);
        ProjectCapacityNeed.DELETEALL(TRUE);
      END;

      Project.GET(ProjectNo);

      CASE CapacityLevel OF
        Level::Project:
          InsertMainNeed(CapacityLevel,Project,DummyProjectElement,DummyDiscipline);
        Level::Discipline:
          BEGIN
            BudgetLine.RESET;
            BudgetLine.SETRANGE("Project No.",ProjectNo);
            BudgetLine.SETFILTER(Hours,'<>0');
            IF BudgetLine.FINDSET THEN BEGIN
              REPEAT
                IF BudgetLine.Discipline = '' THEN
                  BudgetLineWithoutDisciplinePresent := TRUE
                ELSE BEGIN
                  Discipline.Code := BudgetLine.Discipline;
                  Discipline.MARK(TRUE);
                END;
              UNTIL BudgetLine.NEXT = 0;
              IF BudgetLineWithoutDisciplinePresent THEN
                InsertMainNeed(CapacityLevel,Project,DummyProjectElement,DummyDiscipline);
              Discipline.MARKEDONLY(TRUE);
            END;
            IF Discipline.FINDSET THEN
              REPEAT
                InsertMainNeed(CapacityLevel,Project,DummyProjectElement,Discipline);
              UNTIL Discipline.NEXT = 0;
          END;
        Level::"Element - Discipline":
          BEGIN
            BudgetLine.RESET;
            BudgetLine.SETRANGE("Project No.",ProjectNo);
            BudgetLine.SETFILTER(Hours,'<>0');
            BudgetLine.SETCURRENTKEY("Project No.",Element,Discipline);
            IF BudgetLine.FINDSET THEN BEGIN
              REPEAT
                IF BudgetLine.Discipline <> '' THEN
                  Discipline.GET(BudgetLine.Discipline)
                ELSE
                  Discipline := DummyDiscipline;
                IF BudgetLine.Element <> '' THEN
                  ProjectElement.GET(BudgetLine."Project No.",BudgetLine.Element)
                ELSE
                  ProjectElement := DummyProjectElement;
                InsertMainNeed(CapacityLevel,Project,ProjectElement,Discipline);
              UNTIL BudgetLine.NEXT = 0;
            END ELSE BEGIN
              ProjectElement.SETRANGE("Project No.",ProjectNo);
              IF ProjectElement.FINDSET THEN
                REPEAT
                  IF Discipline.FINDSET THEN
                    REPEAT
                      InsertMainNeed(CapacityLevel,Project,ProjectElement,Discipline);
                    UNTIL Discipline.NEXT = 0;
                UNTIL ProjectElement.NEXT = 0;
            END;
          END;
      END;

      //Total Line
      InsertMainNeed(Level::Total,Project,DummyProjectElement,DummyDiscipline);
    END;

    LOCAL PROCEDURE InsertMainNeed@1100528511(CapacityLevel@1100528505 : Option;Project@1100528501 : Record 11072003;ProjectElement@1100528502 : Record 11012010;Discipline@1100528503 : Record 11020204);
    VAR
      ProjectCapacityNeed@1100528500 : Record 11229348;
    BEGIN
      IF ProjectCapacityNeed.GET(CapacityLevel,Project."No.",ProjectElement.Element,Discipline.Code,ProjectCapacityNeed."Line Type"::Main,0) THEN
        EXIT;

      ProjectCapacityNeed.INIT;
      ProjectCapacityNeed.Level := CapacityLevel;
      ProjectCapacityNeed."Project No." := Project."No.";
      ProjectCapacityNeed.Element := ProjectElement.Element;
      ProjectCapacityNeed.Discipline := Discipline.Code;
      ProjectCapacityNeed."Line Type" := ProjectCapacityNeed."Line Type"::Main;
      ProjectCapacityNeed."Detail Line No." := 0;

      IF CapacityLevel  = Level::"Element - Discipline" THEN BEGIN
        ProjectCapacityNeed."Starting Date" := ProjectElement."Starting Date";
        ProjectCapacityNeed."Ending Date" := ProjectElement."Ending Date";
      END;
      IF ProjectCapacityNeed."Starting Date" = 0D THEN
        ProjectCapacityNeed."Starting Date" := Project."Starting Date";
      IF ProjectCapacityNeed."Ending Date" = 0D THEN
        ProjectCapacityNeed."Ending Date" := Project."Ending Date";
      IF ProjectCapacityNeed."Ending Date" < ProjectCapacityNeed."Starting Date" THEN
        ProjectCapacityNeed."Ending Date" := 0D;

      CASE CapacityLevel OF
        Level::Project:
          ProjectCapacityNeed.Description := Project.Description;
        Level::Discipline:
          ProjectCapacityNeed.Description := Discipline.Description;
        Level::"Element - Discipline":
          ProjectCapacityNeed.Description := ProjectElement.Description;
        Level::Total:
          ProjectCapacityNeed.Description := FORMAT(Level::Total);
      END;

      ProjectCapacityNeed."Budget Hours" := ProjectCapacityNeed.ReadCurrentBudgetHours;
      ProjectCapacityNeed."Reference Date" := TODAY;

      ProjectCapacityNeed.INSERT;
    END;

    LOCAL PROCEDURE UpdateEstimatedHoursMain@1100528504(EstimatedHoursToAdd@1100528501 : Decimal);
    VAR
      ProjectCapacityNeed@1100528500 : Record 11229348;
      PresentDetailEstimatedHours@1100528502 : Decimal;
    BEGIN
      IF "Line Type" <> "Line Type"::Sub THEN
        EXIT;

      IF EstimatedHoursToAdd = 0 THEN
        EXIT;

      ProjectCapacityNeed := Rec;
      ProjectCapacityNeed.SETRECFILTER;
      ProjectCapacityNeed.SETRANGE("Detail Line No.");
      ProjectCapacityNeed.CALCSUMS("Estimated Hours");
      PresentDetailEstimatedHours := ProjectCapacityNeed."Estimated Hours";

      ProjectCapacityNeed.GET(Level,"Project No.",Element,Discipline,"Line Type"::Main,0);
      ProjectCapacityNeed."Estimated Hours" := PresentDetailEstimatedHours + EstimatedHoursToAdd;
      ProjectCapacityNeed.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE UpdateTotalLine@1100528508(BudgetHoursToAdd@1100528502 : Decimal;EstimatedHoursToAdd@1100528501 : Decimal);
    VAR
      ProjectCapacityNeed@1100528500 : Record 11229348;
    BEGIN
      IF "Line Type" <> "Line Type"::Main THEN
        EXIT;

      IF Level = Level::Total THEN
        EXIT;

      IF (BudgetHoursToAdd = 0) AND (EstimatedHoursToAdd = 0) THEN
        EXIT;

      IF NOT ProjectCapacityNeed.GET(ProjectCapacityNeed.Level::Total,"Project No.",'','',ProjectCapacityNeed."Line Type"::Main,0) THEN
        EXIT;

      ProjectCapacityNeed."Budget Hours" += BudgetHoursToAdd;
      ProjectCapacityNeed."Estimated Hours" += EstimatedHoursToAdd;
      ProjectCapacityNeed.MODIFY;
    END;

    PROCEDURE DistributeLinear@1100525002(VAR ProjectCapacityNeed@1100525014 : Record 11229348);
    VAR
      ProjectCapacityNeedDetail@1100528501 : Record 11229349;
    BEGIN
      ProjectCapacityNeed.SETFILTER(Level,'<>%1',ProjectCapacityNeed.Level::Total);
      IF ProjectCapacityNeed.FINDSET THEN
        REPEAT
          ProjectCapacityNeedDetail.RESET;
          ProjectCapacityNeedDetail.SETRANGE("Project No.",ProjectCapacityNeed."Project No.");
          ProjectCapacityNeedDetail.SETRANGE(Element,ProjectCapacityNeed.Element);
          ProjectCapacityNeedDetail.SETRANGE(Discipline,ProjectCapacityNeed.Discipline);
          ProjectCapacityNeedDetail.SETRANGE("Line Type",ProjectCapacityNeed."Line Type");
          ProjectCapacityNeedDetail.SETRANGE("Detail Line No.",ProjectCapacityNeed."Detail Line No.");
          ProjectCapacityNeedDetail.SETRANGE(Comment,'');
          ProjectCapacityNeedDetail.DELETEALL(TRUE);
          ProjectCapacityNeedDetail.SETRANGE(Comment);
          ProjectCapacityNeedDetail.MODIFYALL("Requested Hours",0);

          IF ProjectCapacityNeed."Estimated Hours" <> 0 THEN
            UpdateCapacityNeedDetail(ProjectCapacityNeed,ProjectCapacityNeed."Starting Date",ProjectCapacityNeed."Ending Date",ProjectCapacityNeed."Estimated Hours");

        UNTIL ProjectCapacityNeed.NEXT = 0;
    END;

    PROCEDURE UpdateCapacityNeedDetail@1100528523(ProjectCapacityNeed@1100528500 : Record 11229348;StartingDate@1100528501 : Date;EndingDate@1100528502 : Date;EstimatedHours@1100528503 : Decimal);
    VAR
      ProjectCapacityNeedDetail@1100528510 : Record 11229349;
      Date@1100528509 : Record 2000000007;
      TotalNumberOfDays@1100528508 : Integer;
      NumberOfDaysInPeriod@1100528507 : Integer;
      NumberOfDaysRemaining@1100528506 : Integer;
      EstimatedHoursToDistributeInPeriod@1100528505 : Decimal;
      TotalEstimatedHoursDistributed@1100528504 : Decimal;
      RoundingFactor@1100528511 : Decimal;
    BEGIN
      IF (StartingDate = 0D) OR (EndingDate = 0D) THEN
        EXIT;

      TotalNumberOfDays := DetermineNumberOfDays(StartingDate,EndingDate);

      IF TotalNumberOfDays = 0 THEN
        EXIT;

      NumberOfDaysRemaining := TotalNumberOfDays;
      Date.SETRANGE("Period Type", Date."Period Type"::Date);
      Date.SETRANGE("Period Start",StartingDate,EndingDate);
      IF Date.FINDSET THEN BEGIN
        TotalEstimatedHoursDistributed := 0;
        REPEAT
          NumberOfDaysInPeriod := DetermineNumberOfDays(Date."Period Start",Date."Period End");

          IF NumberOfDaysInPeriod > 0 THEN BEGIN
            IF NumberOfDaysInPeriod = NumberOfDaysRemaining THEN
              RoundingFactor := 0.01
            ELSE
              RoundingFactor := 1;

            EstimatedHoursToDistributeInPeriod := ROUND((EstimatedHours - TotalEstimatedHoursDistributed) * (NumberOfDaysInPeriod/NumberOfDaysRemaining),RoundingFactor);
            ProjectCapacityNeedDetail.UpdateProjectCapacityNeedDetail(ProjectCapacityNeed,Date."Period Start",EstimatedHoursToDistributeInPeriod);

            NumberOfDaysRemaining := NumberOfDaysRemaining - NumberOfDaysInPeriod;
            TotalEstimatedHoursDistributed := TotalEstimatedHoursDistributed + EstimatedHoursToDistributeInPeriod;
          END;
        UNTIL Date.NEXT = 0;
      END;
    END;

    PROCEDURE CopyBudgetToEstimatedHours@1100528517(VAR ProjectCapacityNeed@1100528500 : Record 11229348);
    VAR
      BudgetHours@1100528501 : Decimal;
    BEGIN
      ProjectCapacityNeed.SETFILTER(Level,'<>%1',ProjectCapacityNeed.Level::Total);
      IF ProjectCapacityNeed.FINDSET(TRUE) THEN
        REPEAT
          BudgetHours := ROUND(ProjectCapacityNeed."Budget Hours",1);
          IF (BudgetHours <> 0) AND (ProjectCapacityNeed."Estimated Hours" <> BudgetHours) THEN BEGIN
            ProjectCapacityNeed.VALIDATE("Estimated Hours",BudgetHours);
            ProjectCapacityNeed.MODIFY(TRUE);
          END;
        UNTIL ProjectCapacityNeed.NEXT = 0;
    END;

    PROCEDURE InsertNeedSubLine@1100528506();
    VAR
      ProjectCapacityNeed@1100528500 : Record 11229348;
    BEGIN
      IF Level = Level::Total THEN
        FIELDERROR(Level);

      ProjectCapacityNeed.Level := Level;
      ProjectCapacityNeed."Project No." := "Project No.";
      ProjectCapacityNeed.Element := Element;
      ProjectCapacityNeed.Discipline := Discipline;
      ProjectCapacityNeed."Line Type" := "Line Type"::Sub;
      ProjectCapacityNeed."Detail Line No." := ProjectCapacityNeed.GetNewDetailLineNo;
      ProjectCapacityNeed."Starting Date" := "Starting Date";
      ProjectCapacityNeed."Ending Date" := "Ending Date";
      ProjectCapacityNeed.INSERT(TRUE);
    END;

    LOCAL PROCEDURE UpdateNeedSubLines@1100528502();
    VAR
      ProjectCapacityNeed@1100528500 : Record 11229348;
    BEGIN
      IF "Line Type" = "Line Type"::Sub THEN
        EXIT;

      IF ("Starting Date" = xRec."Starting Date") AND
         ("Ending Date" = xRec."Ending Date") THEN
        EXIT;

      ProjectCapacityNeed.SETRANGE(Level,Level);
      ProjectCapacityNeed.SETRANGE("Project No.","Project No.");
      ProjectCapacityNeed.SETRANGE(Element,Element);
      ProjectCapacityNeed.SETRANGE(Discipline,Discipline);
      ProjectCapacityNeed.SETRANGE("Line Type","Line Type"::Sub);
      ProjectCapacityNeed.SETRANGE("Starting Date",xRec."Starting Date");
      ProjectCapacityNeed.SETRANGE("Ending Date",xRec."Ending Date");
      IF ProjectCapacityNeed.FINDSET(TRUE) THEN
        REPEAT
          ProjectCapacityNeed."Starting Date" := "Starting Date";
          ProjectCapacityNeed."Ending Date" := "Ending Date";
          ProjectCapacityNeed.MODIFY;
        UNTIL ProjectCapacityNeed.NEXT = 0;
    END;

    LOCAL PROCEDURE DeleteNeedSubLines@1100528503();
    VAR
      ProjectCapacityNeed@1100528500 : Record 11229348;
    BEGIN
      IF "Line Type" = "Line Type"::Sub THEN
        EXIT;

      ProjectCapacityNeed.SETRANGE(Level,Level);
      ProjectCapacityNeed.SETRANGE("Project No.","Project No.");
      ProjectCapacityNeed.SETRANGE(Element,Element);
      ProjectCapacityNeed.SETRANGE(Discipline,Discipline);
      ProjectCapacityNeed.SETRANGE("Line Type","Line Type"::Sub);
      IF ProjectCapacityNeed.FINDSET THEN
        REPEAT
          ProjectCapacityNeed.SetSubLinesDeleteMode;
          ProjectCapacityNeed.DELETE(TRUE);
        UNTIL ProjectCapacityNeed.NEXT = 0;
    END;

    LOCAL PROCEDURE RenameNeedSubLines@1100528516();
    VAR
      ProjectCapacityNeed@1100528500 : Record 11229348;
      ProjectCapacityNeed2@1100528501 : Record 11229348;
    BEGIN
      IF "Line Type" = "Line Type"::Sub THEN
        EXIT;

      ProjectCapacityNeed.SETRANGE(Level,xRec.Level);
      ProjectCapacityNeed.SETRANGE("Project No.",xRec."Project No.");
      ProjectCapacityNeed.SETRANGE(Element,xRec.Element);
      ProjectCapacityNeed.SETRANGE(Discipline,xRec.Discipline);
      ProjectCapacityNeed.SETRANGE("Line Type","Line Type"::Sub);
      IF ProjectCapacityNeed.FINDSET(TRUE) THEN
        REPEAT
          ProjectCapacityNeed2 := ProjectCapacityNeed;
          ProjectCapacityNeed2.RENAME(Level,"Project No.",Element,Discipline,"Line Type"::Sub,ProjectCapacityNeed."Detail Line No.");
        UNTIL ProjectCapacityNeed.NEXT = 0;
    END;

    PROCEDURE ReadCurrentBudgetHours@1100528509() CurrentBudgetHours : Decimal;
    VAR
      BudgetLine@1100528500 : Record 11012001;
      ProjectCapacityNeed@1100528501 : Record 11229348;
    BEGIN
      IF "Line Type" = "Line Type"::Sub THEN
        EXIT(0);

      IF Level = Level::Total THEN BEGIN
        ProjectCapacityNeed.SETRANGE("Project No.","Project No.");
        ProjectCapacityNeed.SETRANGE("Line Type","Line Type"::Main);
        ProjectCapacityNeed.SETFILTER(Level,'<>%1',Level::Total);
        IF ProjectCapacityNeed.FINDSET THEN
          REPEAT
            CurrentBudgetHours += ProjectCapacityNeed.ReadCurrentBudgetHours;
          UNTIL ProjectCapacityNeed.NEXT = 0;
      END ELSE BEGIN
        BudgetLine.SETRANGE("Project No.","Project No.");
        IF Level = Level::"Element - Discipline" THEN
          BudgetLine.SETRANGE(Element,Element);
        BudgetLine.SETFILTER("Extension Contract Status",'%1|>=%1&<%3',
          BudgetLine."Extension Contract Status"::"Not Applicable",BudgetLine."Extension Contract Status"::Order,BudgetLine."Extension Contract Status"::Expired);
        IF (Level IN [Level::Discipline,Level::"Element - Discipline"]) THEN
          BudgetLine.SETRANGE(Discipline,Discipline);
        BudgetLine.CALCSUMS(Hours);
        CurrentBudgetHours := BudgetLine.Hours;
      END;
    END;

    PROCEDURE ReadBudgetHours@1100528527() BudgetHours : Decimal;
    BEGIN
      IF "Line Type" = "Line Type"::Sub THEN
        EXIT(0);

      BudgetHours := "Budget Hours";
    END;

    PROCEDURE ReadChangedBudgetHours@1100528529() : Decimal;
    BEGIN
      EXIT(ReadCurrentBudgetHours - ReadBudgetHours);
    END;

    PROCEDURE ReadActualHours@1100528507(VAR DisciplineUnknown@1100528504 : Boolean) ActualHours : Decimal;
    VAR
      JobLedgerEntry@1100528500 : Record 11072005;
      ProjectCapacityNeed@1100528501 : Record 11229348;
      MultipleDisciplinesPerElement@1100528502 : Boolean;
      FirstDisciplinePerElement@1100528503 : Boolean;
    BEGIN
      IF "Line Type" = "Line Type"::Sub THEN
        EXIT(0);

      JobLedgerEntry.SETRANGE("Job No.","Project No.");
      JobLedgerEntry.SETRANGE("Cost Type",JobLedgerEntry."Cost Type"::Labor);
      JobLedgerEntry.SETRANGE("Entry Type",JobLedgerEntry."Entry Type"::Usage);
      IF "Reference Date" <> 0D THEN
        JobLedgerEntry.SETFILTER("Posting Date",'<%1',"Reference Date");

      CASE Level OF
        Level::Project:;
        Level::Discipline:
          EXIT(0);
        Level::"Element - Discipline":
          BEGIN
            IF Discipline <> '' THEN BEGIN
              ProjectCapacityNeed.SETCURRENTKEY(Level,"Project No.",Element,Discipline);
              ProjectCapacityNeed.SETRANGE(Level,Level);
              ProjectCapacityNeed.SETRANGE("Project No.","Project No.");
              ProjectCapacityNeed.SETRANGE(Element,Element);
              ProjectCapacityNeed.SETFILTER(Discipline,'<>%1',Discipline);
              MultipleDisciplinesPerElement := NOT ProjectCapacityNeed.ISEMPTY;
              IF MultipleDisciplinesPerElement THEN BEGIN
                DisciplineUnknown := TRUE;
                ProjectCapacityNeed.SETRANGE(Discipline);
                ProjectCapacityNeed.FINDFIRST;
                FirstDisciplinePerElement := (Discipline = ProjectCapacityNeed.Discipline);
                IF NOT FirstDisciplinePerElement THEN
                  EXIT(0);
              END;
            END;
            JobLedgerEntry.SETRANGE(Element,Element);
          END;
      END;

      JobLedgerEntry.CALCSUMS(Quantity);
      ActualHours := JobLedgerEntry.Quantity;
    END;

    PROCEDURE ReadRequestedHours@1100528510(LimitToRefDate@1100528500 : Boolean;LimitFromRefDate@1100528502 : Boolean) RequestedHours : Decimal;
    VAR
      ProjectCapacityNeedDetail@1100528501 : Record 11229349;
    BEGIN
      ProjectCapacityNeedDetail.SETRANGE("Project No.","Project No.");
      CASE Level OF
        Level::Discipline:
          ProjectCapacityNeedDetail.SETRANGE(Discipline,Discipline);
        Level::"Element - Discipline":
          BEGIN
            ProjectCapacityNeedDetail.SETRANGE(Element,Element);
            ProjectCapacityNeedDetail.SETRANGE(Discipline,Discipline);
          END;
      END;
      ProjectCapacityNeedDetail.SETRANGE("Line Type","Line Type");
      ProjectCapacityNeedDetail.SETRANGE("Detail Line No.","Detail Line No.");

      IF LimitToRefDate AND ("Reference Date" <> 0D) THEN
        ProjectCapacityNeedDetail.SETFILTER("Need Date",'<%1',"Reference Date");

      IF LimitFromRefDate AND ("Reference Date" <> 0D) THEN
        ProjectCapacityNeedDetail.SETFILTER("Need Date",'%1..',"Reference Date");

      ProjectCapacityNeedDetail.CALCSUMS("Requested Hours");
      RequestedHours := ProjectCapacityNeedDetail."Requested Hours";
    END;

    PROCEDURE ReadAvailableHours@1100528512() AvailableHours : Decimal;
    BEGIN
      AvailableHours := "Estimated Hours" - ReadRequestedHours(FALSE,FALSE);
    END;

    PROCEDURE ReadScheduledHours@1100528520() ScheduledHours : Decimal;
    VAR
      ProjectScheduleEmployee@1100528500 : Record 11229412;
    BEGIN
      IF "Line Type" = "Line Type"::Sub THEN
        EXIT(0);

      ProjectScheduleEmployee.SETRANGE("Project No.","Project No.");
      CASE Level OF
        Level::Discipline:
          ProjectScheduleEmployee.SETRANGE(Discipline,Discipline);
        Level::"Element - Discipline":
          BEGIN
            IF IsDisciplineUsedForMultipleElements THEN
              EXIT(0);
            ProjectScheduleEmployee.SETRANGE(Discipline,Discipline);
          END;
      END;
      ProjectScheduleEmployee.CALCSUMS("Scheduled Hours");
      ScheduledHours := ProjectScheduleEmployee."Scheduled Hours";
    END;

    PROCEDURE ReadHoursToSchedule@1100528522() HoursToSchedule : Decimal;
    BEGIN
      IF "Line Type" = "Line Type"::Sub THEN
        EXIT(0);

      IF Level = Level::"Element - Discipline" THEN
        IF IsDisciplineUsedForMultipleElements THEN
          EXIT(0);

      HoursToSchedule := ReadRequestedHours(FALSE,FALSE) - ReadScheduledHours;
    END;

    PROCEDURE ReadPlannedHoursWorkOrder@1100528513() PlannedWorkOrderHours : Decimal;
    VAR
      WorkOrder@1100528501 : Record 11229279;
    BEGIN
      IF "Line Type" = "Line Type"::Sub THEN
        EXIT(0);

      CASE Level OF
        Level::Project:
          BEGIN
            WorkOrder.SETRANGE("Source Type",WorkOrder."Source Type"::Project);
            WorkOrder.SETRANGE("Source No.","Project No.");
            WorkOrder.SETFILTER("Resource No.",'<>%1','');
            WorkOrder.SETFILTER("Starting Date/Time",'<>%1',0DT);
            WorkOrder.CALCSUMS("Duration Time");
            PlannedWorkOrderHours := WorkOrder."Duration Time";
          END;
      END;
    END;

    PROCEDURE ReadHoursToPlanWorkorder@1100528514() HoursToPlan : Decimal;
    BEGIN
      IF "Line Type" = "Line Type"::Sub THEN
        EXIT(0);

      HoursToPlan := ReadRequestedHours(FALSE,FALSE) - ReadPlannedHoursWorkOrder;
    END;

    LOCAL PROCEDURE PreviousBudgetHours@1100528505() : Decimal;
    VAR
      ProjectCapacityNeed@1100528500 : Record 11229348;
    BEGIN
      ProjectCapacityNeed := Rec;
      ProjectCapacityNeed.FIND;
      EXIT(ProjectCapacityNeed."Budget Hours");
    END;

    LOCAL PROCEDURE PreviousEstimatedHours@1100528515() : Decimal;
    VAR
      ProjectCapacityNeed@1100528500 : Record 11229348;
    BEGIN
      ProjectCapacityNeed := Rec;
      ProjectCapacityNeed.FIND;
      EXIT(ProjectCapacityNeed."Estimated Hours");
    END;

    PROCEDURE StartWeek@1100528519() : Integer;
    BEGIN
      IF "Starting Date" = 0D THEN
        EXIT(0);

      EXIT(DATE2DWY("Starting Date",2));
    END;

    PROCEDURE EndWeek@1100528518() : Integer;
    BEGIN
      IF "Ending Date" = 0D THEN
        EXIT(0);

      EXIT(DATE2DWY("Ending Date",2));
    END;

    LOCAL PROCEDURE DetermineNumberOfDays@1100528802(StartingDate@1100528502 : Date;EndingDate@1100528503 : Date) NoOfDays : Integer;
    VAR
      CalendarManagement@1100528500 : Codeunit 7600;
      BaseCalender@1100528501 : Code[10];
      TargetDate@1100528801 : Date;
      DummyDesc@1100528800 : Text[30];
    BEGIN
      NoOfDays := 0;

      IF (StartingDate = 0D) OR (EndingDate = 0D) THEN
        EXIT;

      BaseCalender := GetBaseCalender;
      TargetDate := StartingDate;

      REPEAT
        IF NOT CalendarManagement.CheckDateStatus(BaseCalender,TargetDate,DummyDesc) THEN
          NoOfDays += 1;
        TargetDate := CALCDATE('<+1D>', TargetDate);
      UNTIL TargetDate > EndingDate;
    END;

    LOCAL PROCEDURE GetBaseCalender@1100528807() : Code[10];
    VAR
      JobsSetup@1100528500 : Record 315;
      CompanyInformation@1100528501 : Record 79;
    BEGIN
      JobsSetup.GET;
      IF JobsSetup."Proj. Elem. Plan Base Calendar" <> '' THEN
        EXIT(JobsSetup."Proj. Elem. Plan Base Calendar");

      CompanyInformation.GET;
      EXIT(CompanyInformation."Base Calendar Code");
    END;

    PROCEDURE GetTempDisciplines@1100528524(VAR TempDiscipline@1100528500 : TEMPORARY Record 11020204;ProjectNo@1100528504 : Code[20]);
    VAR
      Project@1100528503 : Record 11072003;
      ProjectCapacityNeed@1100528501 : Record 11229348;
    BEGIN
      IF ProjectNo = '' THEN
        EXIT;

      Project.GET(ProjectNo);
      CASE Project."Capacity Need Level" OF
        Project."Capacity Need Level"::Project:
          TempDiscipline.INSERT;
        Project."Capacity Need Level"::Discipline,
        Project."Capacity Need Level"::"Element - Discipline":
          BEGIN
            ProjectCapacityNeed.SETRANGE("Project No.",ProjectNo);
            ProjectCapacityNeed.SETRANGE(Level,Project."Capacity Need Level");
            IF ProjectCapacityNeed.FINDSET THEN
              REPEAT
                IF ProjectCapacityNeed."Estimated Hours" <> 0 THEN BEGIN
                  TempDiscipline.Code := ProjectCapacityNeed.Discipline;
                  IF TempDiscipline.INSERT THEN;
                END;
              UNTIL ProjectCapacityNeed.NEXT = 0;
          END;
      END;
    END;

    PROCEDURE GetTempProjectDisciplines@1100528530(VAR TempProjectDiscipline@1100528500 : TEMPORARY Record 11229448;ProjectNo@1100528504 : Code[20]);
    VAR
      Project@1100528503 : Record 11072003;
      Discipline@1100528502 : Record 11020204;
      ProjectCapacityNeed@1100528501 : Record 11229348;
    BEGIN
      IF ProjectNo <> '' THEN
        ProjectCapacityNeed.SETRANGE("Project No.",ProjectNo);
      ProjectCapacityNeed.SETFILTER("Estimated Hours",'<>0');
      ProjectCapacityNeed.SETFILTER(Level,'<>%1',ProjectCapacityNeed.Level::Total);
      IF ProjectCapacityNeed.FINDSET THEN
        REPEAT
          IF IsProjectCapNeedDetailPresent(ProjectCapacityNeed) THEN BEGIN
            TempProjectDiscipline."Project No." := ProjectCapacityNeed."Project No.";
            TempProjectDiscipline.Discipline := ProjectCapacityNeed.Discipline;
            IF TempProjectDiscipline.INSERT THEN BEGIN
              IF ProjectCapacityNeed.Level = ProjectCapacityNeed.Level::Project THEN
                TempProjectDiscipline.Description := WithoutDiscipline
              ELSE
                IF Discipline.GET(TempProjectDiscipline.Discipline) THEN
                  TempProjectDiscipline.Description := Discipline.Description;
              IF Project.GET(TempProjectDiscipline."Project No.") THEN BEGIN
                TempProjectDiscipline."Project Descripton" := Project.Description;
                TempProjectDiscipline."Project Manager" := Project."Project Manager";
              END;
              TempProjectDiscipline.MODIFY;
            END;
          END;
        UNTIL ProjectCapacityNeed.NEXT = 0;
    END;

    PROCEDURE SetSubLinesDeleteMode@1100528525();
    BEGIN
      SubLinesDeleteMode := TRUE;
    END;

    PROCEDURE MoveBudgetChangeToBudget@1100528526(VAR ProjectCapacityNeed@1100528501 : Record 11229348);
    VAR
      CurrentBudgetHours@1100528500 : Decimal;
    BEGIN
      ProjectCapacityNeed.SETFILTER(Level,'<>%1',ProjectCapacityNeed.Level::Total);
      IF ProjectCapacityNeed.FINDSET(TRUE) THEN
        REPEAT
          CurrentBudgetHours := ProjectCapacityNeed.ReadCurrentBudgetHours;
          IF CurrentBudgetHours <> ProjectCapacityNeed."Budget Hours" THEN BEGIN
            ProjectCapacityNeed."Budget Hours" := CurrentBudgetHours;
            ProjectCapacityNeed.MODIFY(TRUE);
          END;
        UNTIL ProjectCapacityNeed.NEXT = 0;
    END;

    LOCAL PROCEDURE IsDisciplineUsedForMultipleElements@1100528501() : Boolean;
    VAR
      ProjectCapacityNeed@1100528501 : Record 11229348;
    BEGIN
      ProjectCapacityNeed := Rec;
      ProjectCapacityNeed.SETRECFILTER;
      ProjectCapacityNeed.SETFILTER(Element,'<>%1',Element);
      EXIT(NOT ProjectCapacityNeed.ISEMPTY);
    END;

    LOCAL PROCEDURE IsProjectCapNeedDetailPresent@1100528531(ProjectCapacityNeed@1100528500 : Record 11229348) : Boolean;
    VAR
      ProjectCapacityNeedDetail@1100528502 : Record 11229349;
    BEGIN
      ProjectCapacityNeedDetail.SETRANGE("Project No.",ProjectCapacityNeed."Project No.");
      ProjectCapacityNeedDetail.SETRANGE(Discipline,ProjectCapacityNeed.Discipline);
      ProjectCapacityNeedDetail.SETFILTER("Need Date",'%1..',TODAY);
      EXIT(NOT ProjectCapacityNeedDetail.ISEMPTY);
    END;

    BEGIN
    END.
  }
}

