OBJECT Report 11012059 Project Resource Graph
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Projektressource Grafik;
               ENU=Project Resource Graph;
               NLD=Projectresourcegrafiek;
               NOR=Diagram over prosjektressurser];
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  UserIDLength@1100527701 : Integer;
                  UserIDPosition@1100527700 : Integer;
                BEGIN
                  UserIDLength := STRLEN(USERID);
                  IF UserIDLength > 20 THEN BEGIN
                    UserIDPosition := UserIDLength - 19;
                    ShortendUserID := COPYSTR(USERID,UserIDPosition);
                  END ELSE BEGIN
                    ShortendUserID := USERID;
                  END;


                  IF gvScaleUnit = gvScaleUnit::Week THEN
                    "Increment-by No." := 7
                  ELSE
                    IF gvScaleUnit = gvScaleUnit::Month THEN
                      "Increment-by No." := 30
                    ELSE
                      "Increment-by No." := 1;
                END;

  }
  DATASET
  {
    { 5115;    ;DataItem;                    ;
               DataItemTable=Table11012001;
               OnAfterGetRecord=BEGIN

                                  "Budget Line".SETFILTER(Hours, '<>0');

                                  IF ProjectElementRec.GET("Budget Line"."Project No.","Budget Line".Element) THEN
                                  BEGIN
                                    IF (ProjectElementRec."Starting Date" >= gvStartDate) OR
                                       (ProjectElementRec."Ending Date" <= gvEndDate) THEN
                                    BEGIN
                                      IF ProjectElementRec."Ending Date" < gvEndDate THEN
                                        edat := ProjectElementRec."Ending Date"
                                      ELSE
                                        edat := gvEndDate;
                                      IF ProjectElementRec."Starting Date" < gvStartDate THEN
                                        sdat := gvStartDate
                                      ELSE
                                        sdat := ProjectElementRec."Starting Date";
                                      MaxPer := (edat - sdat) DIV "Increment-by No.";
                                      CurPer := (sdat - gvStartDate) DIV "Increment-by No.";
                                      IF MaxPer < 1 THEN MaxPer := 1;
                                      Counter := 1;
                                      REPEAT
                                        TempBudget.SETRANGE("Project No.", ShortendUserID);
                                        TempBudget.SETRANGE("Line No.", CurPer);
                                        IF NOT TempBudget.FIND('-') THEN
                                        BEGIN
                                          TempBudget.INIT();
                                          TempBudget."Project No." := ShortendUserID;
                                          TempBudget."Line No." := CurPer;
                                          TempBudget.Description := FORMAT(Text000) + STRSUBSTNO('%1', CurPer);
                                          TempBudget.Hours := ROUND(("Budget Line".Hours / MaxPer), 0.01);
                                          TempBudget.INSERT();
                                        END ELSE BEGIN
                                          TempBudget.Hours := TempBudget.Hours + ROUND(("Budget Line".Hours / MaxPer), 0.01);
                                          TempBudget.MODIFY();
                                        END;
                                        CurPer := CurPer + 1;
                                        Counter := Counter + 1;
                                      UNTIL Counter > MaxPer
                                    END;
                                  END;
                                END;

               ReqFilterFields=Project No.,Element }

    { 5444;    ;DataItem;                    ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number);
               OnPreDataItem=BEGIN
                               ExcelAutomation.InitExcel;
                               ExcelAutomation.InitWorkbook(Text001);

                               TempBudget.RESET;
                               TempBudget."Project No." := ShortendUserID;
                               SETRANGE(Number,1,TempBudget.COUNT);

                               ExcelAutomation.WriteCell(1, 1, Text003);
                               ExcelAutomation.WriteCell(1, 3, Text005);
                               ExcelAutomation.WriteCell(2, 3, Text007);

                               Counter := 4;
                             END;

               OnAfterGetRecord=BEGIN
                                  IF Number = 1 THEN BEGIN
                                    TempBudget.FIND('-');
                                  END ELSE BEGIN
                                    TempBudget.NEXT;
                                  END;

                                  ExcelAutomation.WriteCell(1, Counter, TempBudget.Description);
                                  ExcelAutomation.WriteCell(2, Counter, FORMAT(TempBudget.Hours));

                                  Counter := Counter + 1;
                                END;

               OnPostDataItem=BEGIN
                                ExcelAutomation.AutoFit(1, 2);
                                ExcelAutomation.AddChart(
                                  Text011,
                                  1, 1, 2, Counter,
                                  ExcelEnumeration.XlChartType_xl3DColumn,
                                  7,
                                  ExcelEnumeration.XlRowCol_xlColumns,
                                  1,
                                  1,
                                  TRUE,
                                  Text012);

                                ExcelAutomation.GiveUserControl;
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100528703;;Container;
                  ContainerType=ContentArea }

      { 1100528702;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100528704;2;Field  ;
                  CaptionML=[DEU=Startdatum;
                             ENU=Starting Date;
                             NLD=Begindatum;
                             NOR=Startdato;
                             SVE=Startdatum];
                  SourceExpr=gvStartDate;
                  OnValidate=BEGIN
                               IF gvStartDate = 0D THEN
                                 ERROR(Text013);
                             END;
                              }

      { 1100528701;2;Field  ;
                  CaptionML=[DEU=Enddatum;
                             ENU=Ending Date;
                             NLD=Einddatum;
                             NOR=Sluttdato;
                             SVE=Slutdatum];
                  SourceExpr=gvEndDate;
                  OnValidate=BEGIN
                               IF gvEndDate = 0D THEN
                                 ERROR(Text014);
                             END;
                              }

      { 1100528700;2;Field  ;
                  CaptionML=[DEU=Massstabeinheid in;
                             ENU=Scale Unit in;
                             NLD=Schaaleenheid in;
                             NOR=Skaler enhet i;
                             SVE=Skalanpassa enhet i];
                  OptionCaptionML=[DEU=Tag,Woche,Monat;
                                   ENU=Day,Week,Month;
                                   NLD=Dag,Week,Maand;
                                   NOR=Dato,Uke,M†ned;
                                   SVE=Datum,Vecka,M†nad];
                  SourceExpr=gvScaleUnit }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@11012017 : TextConst 'DEU=Periode;ENU="Period ";NLD="Periode ";NOR=Periode;SVE=Period';
      Text001@11012018 : TextConst 'DEU=šbersicht Stunden Budget;ENU=Budget Hours Overview;NLD=Overzicht uren werkbegroting;NOR=Oversikt over budsjettimer;SVE=™versikt ”ver budgettimmar';
      Text003@11012020 : TextConst 'DEU=šbersicht Stunden Budget in Periode:;ENU=Budget Hour List for Period:;NLD=Overzicht uren werkbegroting in Periode:;NOR=Liste over budsjettimer for periode:;SVE=Lista ”ver budgettimmar f”r period:';
      Text005@11012022 : TextConst 'DEU=Periode;ENU=Period;NLD=Periode;NOR=Periode;SVE=Period';
      Text007@11012024 : TextConst 'DEU=Anzahl Arbeitsstunden;ENU=Hour Quantity;NLD=Aantal uren;NOR=Timeantall;SVE=Timantal';
      Text011@11012028 : TextConst 'DEU=Diagramm;ENU=Graph;NLD=Grafiek;NOR=Graf;SVE=Diagram';
      Text012@11012029 : TextConst 'DEU=Projektressource;ENU=Project Resource;NLD=Projectresource;NOR=Prosjektressurs;SVE=Projektresurs';
      Text013@11012030 : TextConst 'DEU=Startdatum nicht eingegeben;ENU=Start Date not Entered;NLD=Begindatum niet ingevuld;NOR=Startdato er ikke spesifisert;SVE=Startdatum har inte angetts';
      Text014@11012031 : TextConst 'DEU=Enddatum nicht eingegeben;ENU=End Date not Entered;NLD=Einddatum niet ingevuld;NOR=Ingen sluttdato er angitt;SVE=Inget slutdatum har angetts';
      ExcelEnumeration@1100528703 : Codeunit 11012375;
      ExcelAutomation@1100528700 : Codeunit 11012402;
      gvStartDate@11012006 : Date;
      gvEndDate@11012007 : Date;
      gvScaleUnit@11012008 : 'Day,Week,Month';
      ProjectElementRec@11012009 : Record 11012010;
      TempBudget@11012010 : TEMPORARY Record 11012001;
      Counter@11012011 : Integer;
      CurPer@11012012 : Integer;
      MaxPer@11012013 : Integer;
      sdat@11012014 : Date;
      edat@11012015 : Date;
      "Increment-by No."@11012016 : Integer;
      ShortendUserID@1100527700 : Code[20];

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

