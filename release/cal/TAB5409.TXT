OBJECT Table 5409 Prod. Order Routing Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.05;
  }
  PROPERTIES
  {
    Permissions=TableData 5410=rimd;
    OnInsert=BEGIN
               TESTFIELD("Routing No.");
               IF Status = Status::Finished THEN
                 ERROR(Text006,Status,TABLECAPTION);

               IF "Next Operation No." = '' THEN
                 SetNextOperations(Rec);

               UpdateComponentsBin(0); // from trigger = insert
             END;

    OnModify=BEGIN
               IF Status = Status::Finished THEN
                 ERROR(Text006,Status,TABLECAPTION);

               UpdateComponentsBin(1); // from trigger = modify
             END;

    OnDelete=VAR
               CapLedgEntry@1000 : Record 5832;
             BEGIN
               IF Status = Status::Finished THEN
                 ERROR(Text006,Status,TABLECAPTION);

               IF Status = Status::Released THEN BEGIN
                 CapLedgEntry.SETRANGE("Order Type",CapLedgEntry."Order Type"::Production);
                 CapLedgEntry.SETRANGE("Order No.","Prod. Order No.");
                 CapLedgEntry.SETRANGE("Routing Reference No.","Routing Reference No.");
                 CapLedgEntry.SETRANGE("Routing No.","Routing No.");
                 CapLedgEntry.SETRANGE("Operation No.","Operation No.");
                 IF NOT CapLedgEntry.ISEMPTY THEN
                   ERROR(
                     Text000,
                     Status,TABLECAPTION,"Operation No.",CapLedgEntry.TABLECAPTION);
               END;

               IF SubcontractPurchOrderExist THEN
                 ERROR(
                   Text000,
                   Status,TABLECAPTION,"Operation No.",PurchLine.TABLECAPTION);

               DeleteRelations;

               UpdateComponentsBin(2); // from trigger = delete
             END;

    OnRename=BEGIN
               ERROR(Text001,TABLECAPTION);
             END;

    CaptionML=[ENU=Prod. Order Routing Line;
               NOR=Prod.ordrerutelinje];
    LookupPageID=Page99000817;
    DrillDownPageID=Page99000817;
  }
  FIELDS
  {
    { 1   ;   ;Routing No.         ;Code20        ;TableRelation="Routing Header";
                                                   CaptionML=[ENU=Routing No.;
                                                              NOR=Rutenr.;
                                                              SVE=Operationsfîljdsnr] }
    { 3   ;   ;Routing Reference No.;Integer      ;CaptionML=[ENU=Routing Reference No.;
                                                              NOR=Rutereferansenr.;
                                                              SVE=Oper.fîljd referensnr];
                                                   Editable=No }
    { 4   ;   ;Operation No.       ;Code10        ;OnValidate=BEGIN
                                                                SetRecalcStatus;

                                                                GetLine;
                                                                "Starting Time" := ProdOrderLine."Starting Time";
                                                                "Ending Time" := ProdOrderLine."Ending Time";
                                                                "Starting Date" := ProdOrderLine."Starting Date";
                                                                "Ending Date" := ProdOrderLine."Ending Date";
                                                              END;

                                                   CaptionML=[ENU=Operation No.;
                                                              NOR=Operasjonsnr.;
                                                              SVE=Operationsnr];
                                                   NotBlank=Yes }
    { 5   ;   ;Next Operation No.  ;Code30        ;OnValidate=VAR
                                                                IsHandled@1000 : Boolean;
                                                              BEGIN
                                                                GetLine;
                                                                OnBeforeTerminationProcessesErr(IsHandled);
                                                                IF NOT IsHandled THEN
                                                                  IF (xRec."Next Operation No." = '') AND ("Next Operation No." <> '') AND NoTerminationProcessesExist THEN
                                                                    ERROR(NoTerminationProcessesErr);

                                                                SetRecalcStatus;
                                                              END;

                                                   CaptionML=[ENU=Next Operation No.;
                                                              NOR=Neste operasjonsnr.;
                                                              SVE=NÑsta operationsnr] }
    { 6   ;   ;Previous Operation No.;Code30      ;OnValidate=BEGIN
                                                                SetRecalcStatus;
                                                              END;

                                                   CaptionML=[ENU=Previous Operation No.;
                                                              NOR=Forrige operasjonsnr.;
                                                              SVE=FîregÜende operationsnr] }
    { 7   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                SetRecalcStatus;

                                                                "No." := '';
                                                                "Work Center No." := '';
                                                                "Work Center Group Code" := '';

                                                                ModifyCapNeedEntries;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              NOR=Type;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=[ENU=Work Center,Machine Center;
                                                                    NOR=Arbeidssenter,Maskinsenter;
                                                                    SVE=Produktionsgrupp,Maskingrupp];
                                                   OptionString=Work Center,Machine Center }
    { 8   ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST(Work Center)) "Work Center"
                                                                 ELSE IF (Type=CONST(Machine Center)) "Machine Center";
                                                   OnValidate=BEGIN
                                                                IF ("No." <> xRec."No.") AND (xRec."No." <> '') THEN
                                                                  IF SubcontractPurchOrderExist THEN
                                                                    ERROR(
                                                                      Text007,
                                                                      FIELDCAPTION("No."),PurchLine.TABLECAPTION,Status,TABLECAPTION,"Operation No.");

                                                                SetRecalcStatus;

                                                                IF "No." = '' THEN
                                                                  EXIT;

                                                                CASE Type OF
                                                                  Type::"Work Center":
                                                                    BEGIN
                                                                      WorkCenter.GET("No.");
                                                                      WorkCenter.TESTFIELD(Blocked,FALSE);
                                                                      WorkCenterTransferFields;
                                                                    END;
                                                                  Type::"Machine Center":
                                                                    BEGIN
                                                                      MachineCenter.GET("No.");
                                                                      MachineCenter.TESTFIELD(Blocked,FALSE);
                                                                      MachineCtrTransferFields;
                                                                    END;
                                                                END;
                                                                ModifyCapNeedEntries;

                                                                GetLine;
                                                                IF (ProdOrderLine."Routing Type" = ProdOrderLine."Routing Type"::Serial) OR (xRec."No." <> '') THEN
                                                                  CalcStartingEndingDates(Direction::Forward);

                                                                OnAfterValidateNo(Rec,xRec,ProdOrderLine);
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              NOR=Nr.;
                                                              SVE=Nr] }
    { 9   ;   ;Work Center No.     ;Code20        ;TableRelation="Work Center";
                                                   CaptionML=[ENU=Work Center No.;
                                                              NOR=Arbeidssenternr.;
                                                              SVE=Produktionsgruppsnr];
                                                   Editable=No }
    { 10  ;   ;Work Center Group Code;Code10      ;TableRelation="Work Center Group";
                                                   CaptionML=[ENU=Work Center Group Code;
                                                              NOR=Arbeidssentergruppekode;
                                                              SVE=Produktionsavd.kod];
                                                   Editable=No }
    { 11  ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 12  ;   ;Setup Time          ;Decimal       ;OnValidate=BEGIN
                                                                CalcStartingEndingDates(Direction::Forward);
                                                              END;

                                                   CaptionML=[ENU=Setup Time;
                                                              NOR=Oppstillingstid;
                                                              SVE=OmstÑllningstid];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 13  ;   ;Run Time            ;Decimal       ;OnValidate=BEGIN
                                                                CalcStartingEndingDates(Direction::Forward);
                                                              END;

                                                   CaptionML=[ENU=Run Time;
                                                              NOR=Operasjonstid;
                                                              SVE=Bearbetningstid];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 14  ;   ;Wait Time           ;Decimal       ;OnValidate=BEGIN
                                                                CalcStartingEndingDates(Direction::Forward);
                                                              END;

                                                   CaptionML=[ENU=Wait Time;
                                                              NOR=Ventetid;
                                                              SVE=VÑntetid];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 15  ;   ;Move Time           ;Decimal       ;OnValidate=BEGIN
                                                                CalcStartingEndingDates(Direction::Forward);
                                                              END;

                                                   CaptionML=[ENU=Move Time;
                                                              NOR=Transporttid;
                                                              SVE=Transporttid];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 16  ;   ;Fixed Scrap Quantity;Decimal       ;OnValidate=BEGIN
                                                                SetRecalcStatus;
                                                              END;

                                                   CaptionML=[ENU=Fixed Scrap Quantity;
                                                              NOR=Fast vrakantall;
                                                              SVE=Fast kassationsantal];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 17  ;   ;Lot Size            ;Decimal       ;CaptionML=[ENU=Lot Size;
                                                              NOR=Partistõrrelse;
                                                              SVE=Partistorlek];
                                                   DecimalPlaces=0:5 }
    { 18  ;   ;Scrap Factor %      ;Decimal       ;OnValidate=BEGIN
                                                                SetRecalcStatus;
                                                              END;

                                                   CaptionML=[ENU=Scrap Factor %;
                                                              NOR=Vrakfaktor-%;
                                                              SVE=Kassationsfaktor %];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 19  ;   ;Setup Time Unit of Meas. Code;Code10;
                                                   TableRelation="Capacity Unit of Measure";
                                                   OnValidate=BEGIN
                                                                CalcStartingEndingDates(Direction::Forward);
                                                              END;

                                                   CaptionML=[ENU=Setup Time Unit of Meas. Code;
                                                              NOR=Oppstillingstid - enhetskode;
                                                              SVE=OmstÑllningstid enhetskod] }
    { 20  ;   ;Run Time Unit of Meas. Code;Code10 ;TableRelation="Capacity Unit of Measure";
                                                   OnValidate=BEGIN
                                                                CalcStartingEndingDates(Direction::Forward);
                                                              END;

                                                   CaptionML=[ENU=Run Time Unit of Meas. Code;
                                                              NOR=Operasjonstid - enhetskode;
                                                              SVE=Bearbetningstid enhetskod] }
    { 21  ;   ;Wait Time Unit of Meas. Code;Code10;TableRelation="Capacity Unit of Measure";
                                                   OnValidate=BEGIN
                                                                CalcStartingEndingDates(Direction::Forward);
                                                              END;

                                                   CaptionML=[ENU=Wait Time Unit of Meas. Code;
                                                              NOR=Ventetid - enhetskode;
                                                              SVE=VÑntetid enhetskod] }
    { 22  ;   ;Move Time Unit of Meas. Code;Code10;TableRelation="Capacity Unit of Measure";
                                                   OnValidate=BEGIN
                                                                CalcStartingEndingDates(Direction::Forward);
                                                              END;

                                                   CaptionML=[ENU=Move Time Unit of Meas. Code;
                                                              NOR=Transporttid - enhetskode;
                                                              SVE=Transporttid enhetskod] }
    { 27  ;   ;Minimum Process Time;Decimal       ;CaptionML=[ENU=Minimum Process Time;
                                                              NOR=Minimum behandlingstid;
                                                              SVE=Min. operationstid];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 28  ;   ;Maximum Process Time;Decimal       ;CaptionML=[ENU=Maximum Process Time;
                                                              NOR=Maksimal behandlingstid;
                                                              SVE=Max. operationstid];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 30  ;   ;Concurrent Capacities;Decimal      ;InitValue=1;
                                                   OnValidate=BEGIN
                                                                CalcStartingEndingDates(Direction::Forward);
                                                              END;

                                                   CaptionML=[ENU=Concurrent Capacities;
                                                              NOR=Samtidige kapasiteter;
                                                              SVE=Samtidiga kapaciteter];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 31  ;   ;Send-Ahead Quantity ;Decimal       ;OnValidate=BEGIN
                                                                CalcStartingEndingDates(Direction::Forward);
                                                              END;

                                                   CaptionML=[ENU=Send-Ahead Quantity;
                                                              NOR=Overlappantall;
                                                              SVE=ôverlappningsantal];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 34  ;   ;Routing Link Code   ;Code10        ;TableRelation="Routing Link";
                                                   CaptionML=[ENU=Routing Link Code;
                                                              NOR=Rutekoblingskode;
                                                              SVE=OperationsfîljdslÑnkkod];
                                                   Editable=No }
    { 35  ;   ;Standard Task Code  ;Code10        ;TableRelation="Standard Task";
                                                   OnValidate=VAR
                                                                StandardTask@1000 : Record 99000778;
                                                                StdTaskTool@1001 : Record 99000781;
                                                                StdTaskPersonnel@1002 : Record 99000782;
                                                                StdTaskQltyMeasure@1003 : Record 99000784;
                                                                StdTaskComment@1004 : Record 99000783;
                                                              BEGIN
                                                                IF "Standard Task Code" = '' THEN
                                                                  EXIT;

                                                                StandardTask.GET("Standard Task Code");
                                                                Description := StandardTask.Description;

                                                                DeleteRelations;

                                                                StdTaskTool.SETRANGE("Standard Task Code","Standard Task Code");
                                                                IF StdTaskTool.FIND('-') THEN
                                                                  REPEAT
                                                                    ProdOrderRoutTool.Status := Status;
                                                                    ProdOrderRoutTool."Prod. Order No." := "Prod. Order No.";
                                                                    ProdOrderRoutTool."Routing Reference No." := "Routing Reference No.";
                                                                    ProdOrderRoutTool."Routing No." := "Routing No.";
                                                                    ProdOrderRoutTool."Operation No." := "Operation No.";
                                                                    ProdOrderRoutTool."Line No." := StdTaskTool."Line No.";
                                                                    ProdOrderRoutTool."No." := StdTaskTool."No.";
                                                                    ProdOrderRoutTool.Description := StdTaskTool.Description;
                                                                    ProdOrderRoutTool.INSERT;
                                                                    OnAfterTransferFromStdTaskTool(ProdOrderRoutTool,StdTaskTool);
                                                                  UNTIL StdTaskTool.NEXT = 0;

                                                                StdTaskPersonnel.SETRANGE("Standard Task Code","Standard Task Code");
                                                                IF StdTaskPersonnel.FIND('-') THEN
                                                                  REPEAT
                                                                    ProdOrderRtngPersonnel.Status := Status;
                                                                    ProdOrderRtngPersonnel."Prod. Order No." := "Prod. Order No.";
                                                                    ProdOrderRtngPersonnel."Routing Reference No." := "Routing Reference No.";
                                                                    ProdOrderRtngPersonnel."Routing No." := "Routing No.";
                                                                    ProdOrderRtngPersonnel."Operation No." := "Operation No.";
                                                                    ProdOrderRtngPersonnel."Line No." := StdTaskPersonnel."Line No.";
                                                                    ProdOrderRtngPersonnel."No." := StdTaskPersonnel."No.";
                                                                    ProdOrderRtngPersonnel.Description := StdTaskPersonnel.Description;
                                                                    ProdOrderRtngPersonnel.INSERT;
                                                                    OnAfterTransferFromStdTaskPersonnel(ProdOrderRtngPersonnel,StdTaskPersonnel);
                                                                  UNTIL StdTaskPersonnel.NEXT = 0;

                                                                StdTaskQltyMeasure.SETRANGE("Standard Task Code","Standard Task Code");
                                                                IF StdTaskQltyMeasure.FIND('-') THEN
                                                                  REPEAT
                                                                    ProdOrderRtngQltyMeas.Status := Status;
                                                                    ProdOrderRtngQltyMeas."Prod. Order No." := "Prod. Order No.";
                                                                    ProdOrderRtngQltyMeas."Routing Reference No." := "Routing Reference No.";
                                                                    ProdOrderRtngQltyMeas."Routing No." := "Routing No.";
                                                                    ProdOrderRtngQltyMeas."Operation No." := "Operation No.";
                                                                    ProdOrderRtngQltyMeas."Line No." := StdTaskQltyMeasure."Line No.";
                                                                    ProdOrderRtngQltyMeas."Qlty Measure Code" := StdTaskQltyMeasure."Qlty Measure Code";
                                                                    ProdOrderRtngQltyMeas.Description := StdTaskQltyMeasure.Description;
                                                                    ProdOrderRtngQltyMeas."Min. Value" := StdTaskQltyMeasure."Min. Value";
                                                                    ProdOrderRtngQltyMeas."Max. Value" := StdTaskQltyMeasure."Max. Value";
                                                                    ProdOrderRtngQltyMeas."Mean Tolerance" := StdTaskQltyMeasure."Mean Tolerance";
                                                                    ProdOrderRtngQltyMeas.INSERT;
                                                                    OnAfterTransferFromStdTaskQltyMeasure(ProdOrderRtngQltyMeas,StdTaskQltyMeasure);
                                                                  UNTIL StdTaskQltyMeasure.NEXT = 0;

                                                                StdTaskComment.SETRANGE("Standard Task Code","Standard Task Code");
                                                                IF StdTaskComment.FIND('-') THEN
                                                                  REPEAT
                                                                    ProdOrderRtngComment.Status := Status;
                                                                    ProdOrderRtngComment."Prod. Order No." := "Prod. Order No.";
                                                                    ProdOrderRtngComment."Routing Reference No." := "Routing Reference No.";
                                                                    ProdOrderRtngComment."Routing No." := "Routing No.";
                                                                    ProdOrderRtngComment."Operation No." := "Operation No.";
                                                                    ProdOrderRtngComment."Line No." := StdTaskComment."Line No.";
                                                                    ProdOrderRtngComment.Comment := StdTaskComment.Text;
                                                                    ProdOrderRtngComment.INSERT;
                                                                    OnAfterTransferFromStdTaskComment(ProdOrderRtngComment,StdTaskComment);
                                                                  UNTIL StdTaskComment.NEXT = 0;
                                                              END;

                                                   CaptionML=[ENU=Standard Task Code;
                                                              NOR=Standardoppgavekode;
                                                              SVE=Standarduppgiftskod] }
    { 40  ;   ;Unit Cost per       ;Decimal       ;OnValidate=BEGIN
                                                                GLSetup.GET;
                                                                "Direct Unit Cost" :=
                                                                  ROUND(
                                                                    ("Unit Cost per" - "Overhead Rate") /
                                                                    (1 + "Indirect Cost %" / 100),
                                                                    GLSetup."Unit-Amount Rounding Precision");

                                                                CalcStartingEndingDates(Direction::Forward);
                                                              END;

                                                   CaptionML=[ENU=Unit Cost per;
                                                              NOR=Enhetskost per;
                                                              SVE=Styckkostnad per];
                                                   MinValue=0;
                                                   AutoFormatType=2 }
    { 41  ;   ;Recalculate         ;Boolean       ;CaptionML=[ENU=Recalculate;
                                                              NOR=Beregn pÜ nytt;
                                                              SVE=OmberÑkna] }
    { 50  ;   ;Sequence No. (Forward);Integer     ;CaptionML=[ENU=Sequence No. (Forward);
                                                              NOR=Rekkefõlgenr. (fremover);
                                                              SVE=Sekvensnr (framÜt)];
                                                   Editable=No }
    { 51  ;   ;Sequence No. (Backward);Integer    ;CaptionML=[ENU=Sequence No. (Backward);
                                                              NOR=Rekkefõlgenr. (bakover);
                                                              SVE=Sekvensnr (bakÜt)];
                                                   Editable=No }
    { 52  ;   ;Fixed Scrap Qty. (Accum.);Decimal  ;CaptionML=[ENU=Fixed Scrap Qty. (Accum.);
                                                              NOR=Fast vrakantall (akkum.);
                                                              SVE=Fast kassationsantal (ack.)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 53  ;   ;Scrap Factor % (Accumulated);Decimal;
                                                   CaptionML=[ENU=Scrap Factor % (Accumulated);
                                                              NOR=Vrakfaktor-% (akkumulert);
                                                              SVE=Kassationsfaktor % (ack.)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 55  ;   ;Sequence No. (Actual);Integer      ;CaptionML=[ENU=Sequence No. (Actual);
                                                              NOR=Rekkefõlgenr. (faktisk);
                                                              SVE=Sekvensnr (faktisk)];
                                                   Editable=No }
    { 56  ;   ;Direct Unit Cost    ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Indirect Cost %");
                                                              END;

                                                   CaptionML=[ENU=Direct Unit Cost;
                                                              NOR=Direkte enhetskost;
                                                              SVE=Inkîpspris];
                                                   DecimalPlaces=2:5 }
    { 57  ;   ;Indirect Cost %     ;Decimal       ;OnValidate=BEGIN
                                                                GLSetup.GET;
                                                                "Unit Cost per" :=
                                                                  ROUND(
                                                                    "Direct Unit Cost" * (1 + "Indirect Cost %" / 100) + "Overhead Rate",
                                                                    GLSetup."Unit-Amount Rounding Precision");
                                                              END;

                                                   CaptionML=[ENU=Indirect Cost %;
                                                              NOR=Indirekte kost-%;
                                                              SVE=Indirekt kostnad %];
                                                   DecimalPlaces=0:5 }
    { 58  ;   ;Overhead Rate       ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Indirect Cost %");
                                                              END;

                                                   CaptionML=[ENU=Overhead Rate;
                                                              NOR=Sats for indirekte kostnader;
                                                              SVE=Omkostnad];
                                                   DecimalPlaces=0:5 }
    { 70  ;   ;Starting Time       ;Time          ;OnValidate=BEGIN
                                                                CalcStartingEndingDates(Direction::Forward);
                                                              END;

                                                   CaptionML=[ENU=Starting Time;
                                                              NOR=Starttidspunkt;
                                                              SVE=Starttid] }
    { 71  ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                VALIDATE("Starting Time");
                                                              END;

                                                   CaptionML=[ENU=Starting Date;
                                                              NOR=Startdato;
                                                              SVE=Startdatum] }
    { 72  ;   ;Ending Time         ;Time          ;OnValidate=BEGIN
                                                                CalcStartingEndingDates(Direction::Backward);
                                                              END;

                                                   CaptionML=[ENU=Ending Time;
                                                              NOR=Sluttidspunkt;
                                                              SVE=Sluttid] }
    { 73  ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                VALIDATE("Ending Time");
                                                              END;

                                                   CaptionML=[ENU=Ending Date;
                                                              NOR=Sluttdato;
                                                              SVE=Slutdatum] }
    { 74  ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=[ENU=Simulated,Planned,Firm Planned,Released,Finished;
                                                                    NOR=Simulert,Planlagt,Fast planlagt,Frigitt,Ferdig;
                                                                    SVE=Simulerad,Planerad,Fast planerad,SlÑppt,FÑrdig];
                                                   OptionString=Simulated,Planned,Firm Planned,Released,Finished }
    { 75  ;   ;Prod. Order No.     ;Code20        ;TableRelation="Production Order".No. WHERE (Status=FIELD(Status));
                                                   CaptionML=[ENU=Prod. Order No.;
                                                              NOR=Prod.ordrenr.;
                                                              SVE=Prod.ordernr];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 76  ;   ;Unit Cost Calculation;Option       ;CaptionML=[ENU=Unit Cost Calculation;
                                                              NOR=Beregning av enhetskost;
                                                              SVE=Styckkost. berÑkningstyp];
                                                   OptionCaptionML=[ENU=Time,Units;
                                                                    NOR=Tid,Enheter;
                                                                    SVE=Tid,Antal];
                                                   OptionString=Time,Units }
    { 77  ;   ;Input Quantity      ;Decimal       ;CaptionML=[ENU=Input Quantity;
                                                              NOR=Tilgangsantall;
                                                              SVE=Inflîde antal];
                                                   DecimalPlaces=0:5 }
    { 78  ;   ;Critical Path       ;Boolean       ;CaptionML=[ENU=Critical Path;
                                                              NOR=Kritisk bane;
                                                              SVE=Kritisk linje];
                                                   Editable=No }
    { 79  ;   ;Routing Status      ;Option        ;OnValidate=VAR
                                                                ProdOrderCapacityNeed@1000 : Record 5410;
                                                              BEGIN
                                                                IF (xRec."Routing Status" = xRec."Routing Status"::Finished) AND (xRec."Routing Status" <> "Routing Status") THEN
                                                                  ERROR(Text008,FIELDCAPTION("Routing Status"),xRec."Routing Status","Routing Status");

                                                                IF ("Routing Status" = "Routing Status"::Finished) AND (xRec."Routing Status" <> "Routing Status") THEN BEGIN
                                                                  IF NOT CONFIRM(Text009,FALSE,FIELDCAPTION("Routing Status"),"Routing Status") THEN
                                                                    ERROR('');

                                                                  ProdOrderCapacityNeed.SETCURRENTKEY(
                                                                    Status,"Prod. Order No.","Requested Only","Routing No.","Routing Reference No.","Operation No.","Line No.");
                                                                  ProdOrderCapacityNeed.SETRANGE(Status,Status);
                                                                  ProdOrderCapacityNeed.SETRANGE("Prod. Order No.","Prod. Order No.");
                                                                  ProdOrderCapacityNeed.SETRANGE("Requested Only",FALSE);
                                                                  ProdOrderCapacityNeed.SETRANGE("Routing No.","Routing No.");
                                                                  ProdOrderCapacityNeed.SETRANGE("Routing Reference No.","Routing Reference No.");
                                                                  ProdOrderCapacityNeed.SETRANGE("Operation No.","Operation No.");
                                                                  ProdOrderCapacityNeed.MODIFYALL("Allocated Time",0);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Routing Status;
                                                              NOR=Rutestatus];
                                                   OptionCaptionML=[ENU=" ,Planned,In Progress,Finished";
                                                                    NOR=" ,Planlagt,I arbeid,Ferdig"];
                                                   OptionString=[ ,Planned,In Progress,Finished] }
    { 81  ;   ;Flushing Method     ;Option        ;InitValue=Manual;
                                                   CaptionML=[ENU=Flushing Method;
                                                              NOR=Trekkmetode;
                                                              SVE=Bokfîringsmetod];
                                                   OptionCaptionML=[ENU=Manual,Forward,Backward;
                                                                    NOR=Manuell,Forlengs,Baklengs;
                                                                    SVE=Manuellt,FramÜt,BakÜt];
                                                   OptionString=Manual,Forward,Backward }
    { 90  ;   ;Expected Operation Cost Amt.;Decimal;
                                                   CaptionML=[ENU=Expected Operation Cost Amt.;
                                                              NOR=Forventet operasjonskostbelõp;
                                                              SVE=FîrvÑntad operationskostnad];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 91  ;   ;Expected Capacity Need;Decimal     ;FieldClass=Normal;
                                                   CaptionML=[ENU=Expected Capacity Need;
                                                              NOR=Forventet kapasitetsbehov;
                                                              SVE=FîrvÑntat kap.behov];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 96  ;   ;Expected Capacity Ovhd. Cost;Decimal;
                                                   CaptionML=[ENU=Expected Capacity Ovhd. Cost;
                                                              NOR=Forventet indir. kap.kostn.;
                                                              SVE=FîrvÑntad kap.overh.kost.];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 98  ;   ;Starting Date-Time  ;DateTime      ;OnValidate=BEGIN
                                                                "Starting Date" := DT2DATE("Starting Date-Time");
                                                                "Starting Time" := DT2TIME("Starting Date-Time");
                                                                VALIDATE("Starting Time");
                                                              END;

                                                   CaptionML=[ENU=Starting Date-Time;
                                                              NOR=Startdato-tidsp.;
                                                              SVE=Startdatum tid] }
    { 99  ;   ;Ending Date-Time    ;DateTime      ;OnValidate=BEGIN
                                                                "Ending Date" := DT2DATE("Ending Date-Time");
                                                                "Ending Time" := DT2TIME("Ending Date-Time");
                                                                VALIDATE("Ending Time");
                                                              END;

                                                   CaptionML=[ENU=Ending Date-Time;
                                                              NOR=Sluttdato-tidsp.;
                                                              SVE=Slutdatum tid] }
    { 100 ;   ;Schedule Manually   ;Boolean       ;CaptionML=[ENU=Schedule Manually;
                                                              NOR=Tidsplanlegg manuelt] }
    { 101 ;   ;Location Code       ;Code10        ;AccessByPermission=TableData 14=R;
                                                   CaptionML=[ENU=Location Code;
                                                              NOR=Lokasjonskode;
                                                              SVE=LagerstÑllekod];
                                                   Editable=No }
    { 7301;   ;Open Shop Floor Bin Code;Code20    ;AccessByPermission=TableData 5771=R;
                                                   CaptionML=[ENU=Open Shop Floor Bin Code;
                                                              NOR=èpen prod.hyllekode;
                                                              SVE=ôppen prod.lagerplats, kod];
                                                   Editable=No }
    { 7302;   ;To-Production Bin Code;Code20      ;AccessByPermission=TableData 5771=R;
                                                   CaptionML=[ENU=To-Production Bin Code;
                                                              NOR=Til-Hyllekode for produksjon;
                                                              SVE=Till prod.-lagerplats - kod];
                                                   Editable=No }
    { 7303;   ;From-Production Bin Code;Code20    ;AccessByPermission=TableData 5771=R;
                                                   CaptionML=[ENU=From-Production Bin Code;
                                                              NOR=Fra-Hyllekode for produksjon;
                                                              SVE=FrÜn prod.lagerplats - kod];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Status,Prod. Order No.,Routing Reference No.,Routing No.,Operation No.;
                                                   Clustered=Yes }
    {    ;Prod. Order No.,Routing Reference No.,Status,Routing No.,Operation No. }
    {    ;Status,Prod. Order No.,Routing Reference No.,Routing No.,Sequence No. (Forward) }
    {    ;Status,Prod. Order No.,Routing Reference No.,Routing No.,Sequence No. (Backward) }
    {    ;Status,Prod. Order No.,Routing Reference No.,Routing No.,Sequence No. (Actual);
                                                   SumIndexFields=Expected Operation Cost Amt. }
    {    ;Work Center No.                         ;SumIndexFields=Expected Operation Cost Amt.;
                                                   MaintainSIFTIndex=No }
    {    ;Type,No.,Starting Date                  ;SumIndexFields=Expected Operation Cost Amt.;
                                                   MaintainSIFTIndex=No }
    {    ;Status,Work Center No.                  ;SumIndexFields=Expected Operation Cost Amt. }
    {    ;Prod. Order No.,Status,Flushing Method   }
    {    ;Starting Date,Starting Time,Routing Status }
    {    ;Ending Date,Ending Time,Routing Status   }
    {    ;Type,No.,Status                         ;SumIndexFields=Expected Operation Cost Amt. }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst '@@@="%1 = Document status; %2 = Table Caption; %3 = Field Value; %4 = Table Caption";ENU=You cannot delete %1 %2 %3 because there is at least one %4 associated with it.;NOR=Du kan ikke slette %1 %2 %3 fordi det finnes minst Çn %4 tilknyttet.';
      Text001@1001 : TextConst 'ENU=You cannot rename a %1.;NOR=Du kan ikke gi nytt navn til en %1.;SVE=Du kan inte byta namn pÜ %1.';
      Text002@1002 : TextConst 'ENU=This routing line cannot be moved because of critical work centers in previous operations;NOR=Denne rutelinjen kan ikke flyttes pÜ grunn av kritiske arbeidssentre i tidligere operasjoner;SVE=Operationsfîljdsraden kan inte flyttas p.g.a. kritiska prod.grupper i fîregÜende operationer';
      Text003@1003 : TextConst 'ENU=This routing line cannot be moved because of critical work centers in next operations;NOR=Denne rutelinjen kan ikke flyttes pÜ grunn av kritiske arbeidssentre i kommende operasjoner;SVE=Operationsfîljdsraden kan inte flyttas p.g.a. kritiska prod.grupper i pÜfîljande operationer';
      WorkCenter@1006 : Record 99000754;
      MachineCenter@1007 : Record 99000758;
      ProdOrderLine@1008 : Record 5406;
      ProdOrderRtngLine@1009 : Record 5409;
      ProdOrderRoutTool@1010 : Record 5411;
      ProdOrderRtngPersonnel@1011 : Record 5412;
      ProdOrderRtngQltyMeas@1012 : Record 5413;
      ProdOrderRtngComment@1013 : Record 5415;
      GLSetup@1005 : Record 98;
      ProdOrderCapNeed@1014 : Record 5410;
      PurchLine@1021 : Record 39;
      TempErrorMessage@1026 : TEMPORARY Record 700;
      CalcProdOrder@1015 : Codeunit 99000773;
      ProdOrderRouteMgt@1016 : Codeunit 99000772;
      Text004@1019 : TextConst 'ENU=Some routing lines are referring to the operation just deleted. The references are\in the fields %1 and %2.\\This may have to be corrected as a routing line referring to a non-existent\operation will lead to serious errors in capacity planning.\\Do you want to see a list of the lines in question?\(Access the columns Next Operation No. and Previous Operation No.);NOR=Noen av rutelinjene viser til operasjonen som nettopp ble slettet. Henvisningene finnes\i feltene %1 og %2.\\Det kan vëre at dette mÜ korrigeres fordi en rutelinje som viser til en ikke-eksisterende\operasjon kan medfõre alvorlige feil i kapasitetsplanleggingen.\\Vil du se en oversikt over de aktuelle linjene?\(Se i kolonnene Neste operasjonsnr. og Forrige operasjonsnr.)';
      Text005@1004 : TextConst 'ENU=Routing Lines referring to deleted Operation No. %1;NOR=Rutelinjene viser til slettet operasjonsnr. %1';
      Text006@1020 : TextConst 'ENU=A %1 %2 can not be inserted, modified, or deleted.;NOR=Det kan ikke settes inn, endres eller slettes noen %1 %2.';
      Direction@1018 : 'Forward,Backward';
      Text007@1022 : TextConst 'ENU=You cannot change %1, because there is at least one %2 associated with %3 %4 %5.;NOR=Du kan ikke endre %1 fordi det er minst Çn %2 tilknyttet %3 %4 %5.';
      Text008@1023 : TextConst 'ENU=You cannot change the %1 from %2 to %3.;NOR=Du kan ikke endre %1 fra %2 til %3.';
      Text009@1024 : TextConst 'ENU=If you change the %1 to %2, then all related allocated capacity will be deleted, and you will not be able to change the %1 of the operation again.\\Are you sure that you want to continue?;NOR=Hvis du endrer %1 til %2, slettes all relatert tildelt kapasitet, og du kan ikke endre %1 for operasjonen pÜ nytt.\\Er du sikker pÜ at du vil fortsette?';
      SkipUpdateOfCompBinCodes@1017 : Boolean;
      TimeShiftedOnParentLineMsg@1025 : TextConst 'ENU=The production starting date-time of the end item has been moved forward because a subassembly is taking longer than planned.;NOR=Dato og klokkeslett for produksjonsstart for sluttvaren har blitt flyttet fremover fordi et halvfabrikat tar lengre tid enn planlagt.';
      NoTerminationProcessesErr@1028 : TextConst 'ENU=On the last operation, the Next Operation No. field must be empty.;NOR=Feltet Neste operasjonsnr. mÜ vëre tomt i den siste operasjonen.';

    [External]
    PROCEDURE Caption@12() : Text[100];
    VAR
      ProdOrder@1000 : Record 5405;
    BEGIN
      IF GETFILTERS = '' THEN
        EXIT('');

      IF NOT ProdOrder.GET(Status,"Prod. Order No.") THEN
        EXIT('');

      EXIT(
        STRSUBSTNO('%1 %2 %3',
          "Prod. Order No.",ProdOrder.Description,"Routing No."));
    END;

    LOCAL PROCEDURE GetLine@15();
    BEGIN
      ProdOrderLine.SETRANGE(Status,Status);
      ProdOrderLine.SETRANGE("Prod. Order No.","Prod. Order No.");
      ProdOrderLine.SETRANGE("Routing No.","Routing No.");
      ProdOrderLine.SETRANGE("Routing Reference No.","Routing Reference No.");
      ProdOrderLine.FIND('-');
    END;

    PROCEDURE CopyFromPlanningRoutingLine@30(PlanningRoutingLine@1000 : Record 99000830);
    BEGIN
      "Operation No." := PlanningRoutingLine."Operation No.";
      "Next Operation No." := PlanningRoutingLine."Next Operation No.";
      "Previous Operation No." := PlanningRoutingLine."Previous Operation No.";
      Type := PlanningRoutingLine.Type;
      "No." := PlanningRoutingLine."No.";
      "Work Center No." := PlanningRoutingLine."Work Center No.";
      "Work Center Group Code" := PlanningRoutingLine."Work Center Group Code";
      Description := PlanningRoutingLine.Description;
      "Setup Time" := PlanningRoutingLine."Setup Time";
      "Run Time" := PlanningRoutingLine."Run Time";
      "Wait Time" := PlanningRoutingLine."Wait Time";
      "Move Time" := PlanningRoutingLine."Move Time";
      "Fixed Scrap Quantity" := PlanningRoutingLine."Fixed Scrap Quantity";
      "Lot Size" := PlanningRoutingLine."Lot Size";
      "Scrap Factor %" := PlanningRoutingLine."Scrap Factor %";
      "Setup Time Unit of Meas. Code" := PlanningRoutingLine."Setup Time Unit of Meas. Code";
      "Run Time Unit of Meas. Code" := PlanningRoutingLine."Run Time Unit of Meas. Code";
      "Wait Time Unit of Meas. Code" := PlanningRoutingLine."Wait Time Unit of Meas. Code";
      "Move Time Unit of Meas. Code" := PlanningRoutingLine."Move Time Unit of Meas. Code";
      "Minimum Process Time" := PlanningRoutingLine."Minimum Process Time";
      "Maximum Process Time" := PlanningRoutingLine."Maximum Process Time";
      "Concurrent Capacities" := PlanningRoutingLine."Concurrent Capacities";
      "Send-Ahead Quantity" := PlanningRoutingLine."Send-Ahead Quantity";
      "Routing Link Code" := PlanningRoutingLine."Routing Link Code";
      "Standard Task Code" := PlanningRoutingLine."Standard Task Code";
      "Unit Cost per" := PlanningRoutingLine."Unit Cost per";
      Recalculate := PlanningRoutingLine.Recalculate;
      "Sequence No. (Forward)" := PlanningRoutingLine."Sequence No.(Forward)";
      "Sequence No. (Backward)" := PlanningRoutingLine."Sequence No.(Backward)";
      "Fixed Scrap Qty. (Accum.)" := PlanningRoutingLine."Fixed Scrap Qty. (Accum.)";
      "Scrap Factor % (Accumulated)" := PlanningRoutingLine."Scrap Factor % (Accumulated)";
      "Sequence No. (Actual)" := PlanningRoutingLine."Sequence No. (Actual)";
      "Starting Time" := PlanningRoutingLine."Starting Time";
      "Starting Date" := PlanningRoutingLine."Starting Date";
      "Ending Time" := PlanningRoutingLine."Ending Time";
      "Ending Date" := PlanningRoutingLine."Ending Date";
      "Unit Cost Calculation" := PlanningRoutingLine."Unit Cost Calculation";
      "Input Quantity" := PlanningRoutingLine."Input Quantity";
      "Critical Path" := PlanningRoutingLine."Critical Path";
      "Direct Unit Cost" := PlanningRoutingLine."Direct Unit Cost";
      "Indirect Cost %" := PlanningRoutingLine."Indirect Cost %";
      "Overhead Rate" := PlanningRoutingLine."Overhead Rate";
      "Expected Operation Cost Amt." := PlanningRoutingLine."Expected Operation Cost Amt.";
      "Expected Capacity Ovhd. Cost" := PlanningRoutingLine."Expected Capacity Ovhd. Cost";
      "Expected Capacity Need" := PlanningRoutingLine."Expected Capacity Need";
    END;

    PROCEDURE CopyFromRoutingLine@31(RoutingLine@1000 : Record 99000764);
    BEGIN
      "Operation No." := RoutingLine."Operation No.";
      "Next Operation No." := RoutingLine."Next Operation No.";
      "Previous Operation No." := RoutingLine."Previous Operation No.";
      Type := RoutingLine.Type;
      "No." := RoutingLine."No.";
      "Work Center No." := RoutingLine."Work Center No.";
      "Work Center Group Code" := RoutingLine."Work Center Group Code";
      Description := RoutingLine.Description;
      "Setup Time" := RoutingLine."Setup Time";
      "Run Time" := RoutingLine."Run Time";
      "Wait Time" := RoutingLine."Wait Time";
      "Move Time" := RoutingLine."Move Time";
      "Fixed Scrap Quantity" := RoutingLine."Fixed Scrap Quantity";
      "Lot Size" := RoutingLine."Lot Size";
      "Scrap Factor %" := RoutingLine."Scrap Factor %";
      "Minimum Process Time" := RoutingLine."Minimum Process Time";
      "Maximum Process Time" := RoutingLine."Maximum Process Time";
      "Concurrent Capacities" := RoutingLine."Concurrent Capacities";
      IF "Concurrent Capacities" = 0 THEN
        "Concurrent Capacities" := 1;
      "Send-Ahead Quantity" := RoutingLine."Send-Ahead Quantity";
      "Setup Time Unit of Meas. Code" := RoutingLine."Setup Time Unit of Meas. Code";
      "Run Time Unit of Meas. Code" := RoutingLine."Run Time Unit of Meas. Code";
      "Wait Time Unit of Meas. Code" := RoutingLine."Wait Time Unit of Meas. Code";
      "Move Time Unit of Meas. Code" := RoutingLine."Move Time Unit of Meas. Code";
      "Routing Link Code" := RoutingLine."Routing Link Code";
      "Standard Task Code" := RoutingLine."Standard Task Code";
      "Sequence No. (Forward)" := RoutingLine."Sequence No. (Forward)";
      "Sequence No. (Backward)" := RoutingLine."Sequence No. (Backward)";
      "Fixed Scrap Qty. (Accum.)" := RoutingLine."Fixed Scrap Qty. (Accum.)";
      "Scrap Factor % (Accumulated)" := RoutingLine."Scrap Factor % (Accumulated)";
      "Unit Cost per" := RoutingLine."Unit Cost per";
      FillDefaultLocationAndBins;
    END;

    LOCAL PROCEDURE DeleteRelations@3();
    BEGIN
      OnBeforeDeleteRelations(Rec);

      ProdOrderRoutTool.SETRANGE(Status,Status);
      ProdOrderRoutTool.SETRANGE("Prod. Order No.","Prod. Order No.");
      ProdOrderRoutTool.SETRANGE("Routing Reference No.","Routing Reference No.");
      ProdOrderRoutTool.SETRANGE("Routing No.","Routing No.");
      ProdOrderRoutTool.SETRANGE("Operation No.","Operation No.");
      ProdOrderRoutTool.DELETEALL;

      ProdOrderRtngPersonnel.SETRANGE(Status,Status);
      ProdOrderRtngPersonnel.SETRANGE("Prod. Order No.","Prod. Order No.");
      ProdOrderRtngPersonnel.SETRANGE("Routing Reference No.","Routing Reference No.");
      ProdOrderRtngPersonnel.SETRANGE("Routing No.","Routing No.");
      ProdOrderRtngPersonnel.SETRANGE("Operation No.","Operation No.");
      ProdOrderRtngPersonnel.DELETEALL;

      ProdOrderRtngQltyMeas.SETRANGE(Status,Status);
      ProdOrderRtngQltyMeas.SETRANGE("Prod. Order No.","Prod. Order No.");
      ProdOrderRtngQltyMeas.SETRANGE("Routing Reference No.","Routing Reference No.");
      ProdOrderRtngQltyMeas.SETRANGE("Routing No.","Routing No.");
      ProdOrderRtngQltyMeas.SETRANGE("Operation No.","Operation No.");
      ProdOrderRtngQltyMeas.DELETEALL;

      ProdOrderRtngComment.SETRANGE(Status,Status);
      ProdOrderRtngComment.SETRANGE("Prod. Order No.","Prod. Order No.");
      ProdOrderRtngComment.SETRANGE("Routing Reference No.","Routing Reference No.");
      ProdOrderRtngComment.SETRANGE("Routing No.","Routing No.");
      ProdOrderRtngComment.SETRANGE("Operation No.","Operation No.");
      ProdOrderRtngComment.DELETEALL;

      ProdOrderCapNeed.SETRANGE(Status,Status);
      ProdOrderCapNeed.SETRANGE("Prod. Order No.","Prod. Order No.");
      ProdOrderCapNeed.SETRANGE("Routing No.","Routing No.");
      ProdOrderCapNeed.SETRANGE("Routing Reference No.","Routing Reference No.");
      ProdOrderCapNeed.SETRANGE("Operation No.","Operation No.");
      ProdOrderCapNeed.DELETEALL;

      OnAfterDeleteRelations(Rec,SkipUpdateOfCompBinCodes);
    END;

    LOCAL PROCEDURE WorkCenterTransferFields@2();
    BEGIN
      "Work Center No." := WorkCenter."No.";
      "Work Center Group Code" := WorkCenter."Work Center Group Code";
      "Setup Time Unit of Meas. Code" := WorkCenter."Unit of Measure Code";
      "Run Time Unit of Meas. Code" := WorkCenter."Unit of Measure Code";
      "Wait Time Unit of Meas. Code" := WorkCenter."Unit of Measure Code";
      "Move Time Unit of Meas. Code" := WorkCenter."Unit of Measure Code";
      Description := WorkCenter.Name;
      "Flushing Method" := WorkCenter."Flushing Method";
      "Unit Cost per" := WorkCenter."Unit Cost";
      "Direct Unit Cost" := WorkCenter."Direct Unit Cost";
      "Indirect Cost %" := WorkCenter."Indirect Cost %";
      "Overhead Rate" := WorkCenter."Overhead Rate";
      "Unit Cost Calculation" := WorkCenter."Unit Cost Calculation";
      FillDefaultLocationAndBins;
      OnAfterWorkCenterTransferFields(Rec,WorkCenter);
    END;

    LOCAL PROCEDURE MachineCtrTransferFields@1();
    BEGIN
      WorkCenter.GET(MachineCenter."Work Center No.");
      WorkCenterTransferFields;

      Description := MachineCenter.Name;
      "Setup Time" := MachineCenter."Setup Time";
      "Wait Time" := MachineCenter."Wait Time";
      "Move Time" := MachineCenter."Move Time";
      "Fixed Scrap Quantity" := MachineCenter."Fixed Scrap Quantity";
      "Scrap Factor %" := MachineCenter."Scrap %";
      "Minimum Process Time" := MachineCenter."Minimum Process Time";
      "Maximum Process Time" := MachineCenter."Maximum Process Time";
      "Concurrent Capacities" := MachineCenter."Concurrent Capacities";
      IF "Concurrent Capacities" = 0 THEN
        "Concurrent Capacities" := 1;
      "Send-Ahead Quantity" := MachineCenter."Send-Ahead Quantity";
      "Setup Time Unit of Meas. Code" := MachineCenter."Setup Time Unit of Meas. Code";
      "Wait Time Unit of Meas. Code" := MachineCenter."Wait Time Unit of Meas. Code";
      "Move Time Unit of Meas. Code" := MachineCenter."Move Time Unit of Meas. Code";
      "Flushing Method" := MachineCenter."Flushing Method";
      "Unit Cost per" := MachineCenter."Unit Cost";
      "Direct Unit Cost" := MachineCenter."Direct Unit Cost";
      "Indirect Cost %" := MachineCenter."Indirect Cost %";
      "Overhead Rate" := MachineCenter."Overhead Rate";
      FillDefaultLocationAndBins;
      OnAfterMachineCtrTransferFields(Rec,WorkCenter,MachineCenter);
    END;

    [External]
    PROCEDURE FillDefaultLocationAndBins@8();
    BEGIN
      OnBeforeFillDefaultLocationAndBins(Rec);

      GetLine;
      "Location Code" := ProdOrderLine."Location Code";
      CASE Type OF
        Type::"Work Center":
          BEGIN
            IF WorkCenter."No." <> "No." THEN
              WorkCenter.GET("No.");
            IF WorkCenter."Location Code" = "Location Code" THEN BEGIN
              "Open Shop Floor Bin Code" := WorkCenter."Open Shop Floor Bin Code";
              "To-Production Bin Code" := WorkCenter."To-Production Bin Code";
              "From-Production Bin Code" := WorkCenter."From-Production Bin Code";
            END;
          END;
        Type::"Machine Center":
          BEGIN
            IF MachineCenter."No." <> "No." THEN
              MachineCenter.GET("No.");
            IF MachineCenter."Location Code" = "Location Code" THEN BEGIN
              "Open Shop Floor Bin Code" := MachineCenter."Open Shop Floor Bin Code";
              "To-Production Bin Code" := MachineCenter."To-Production Bin Code";
              "From-Production Bin Code" := MachineCenter."From-Production Bin Code";
            END;
            IF WorkCenter."No." <> MachineCenter."Work Center No." THEN
              WorkCenter.GET(MachineCenter."Work Center No.");
            IF WorkCenter."Location Code" = "Location Code" THEN BEGIN
              IF "Open Shop Floor Bin Code" = '' THEN
                "Open Shop Floor Bin Code" := WorkCenter."Open Shop Floor Bin Code";
              IF "To-Production Bin Code" = '' THEN
                "To-Production Bin Code" := WorkCenter."To-Production Bin Code";
              IF "From-Production Bin Code" = '' THEN
                "From-Production Bin Code" := WorkCenter."From-Production Bin Code";
            END;
          END;
      END;

      OnAfterFillDefaultLocationAndBins(Rec);
    END;

    [External]
    PROCEDURE CalcStartingEndingDates@17(Direction1@1000 : 'Forward,Backward');
    VAR
      ReservationCheckDateConfl@1001 : Codeunit 99000815;
    BEGIN
      OnBeforeCalcStartingEndingDates(Rec,Direction1);

      IF "Routing Status" = "Routing Status"::Finished THEN
        FIELDERROR("Routing Status");

      MODIFY(TRUE);

      ProdOrderRtngLine.GET(Status,"Prod. Order No.","Routing Reference No.","Routing No.","Operation No.");

      ProdOrderRouteMgt.CalcSequenceFromActual(ProdOrderRtngLine,Direction1);
      ProdOrderRtngLine.GET(Status,"Prod. Order No.","Routing Reference No.","Routing No.","Operation No.");
      ProdOrderRtngLine.SETCURRENTKEY(
        Status,"Prod. Order No.","Routing Reference No.","Routing No.","Sequence No. (Actual)");
      CalcProdOrder.CalculateRoutingFromActual(ProdOrderRtngLine,Direction1,FALSE);

      CalculateRoutingBack;
      CalculateRoutingForward;

      GET(Status,"Prod. Order No.","Routing Reference No.","Routing No.","Operation No.");
      GetLine;

      IF Direction1 = Direction1::Forward THEN
        ShiftTimeForwardOnParentProdOrderLines(ProdOrderLine);

      ReservationCheckDateConfl.ProdOrderLineCheck(ProdOrderLine,TRUE);

      OnAfterCalcStartingEndingDates(Rec,xRec,ProdOrderLine,CurrFieldNo);
    END;

    [External]
    PROCEDURE SetRecalcStatus@5();
    BEGIN
      Recalculate := TRUE;

      OnAfterSetRecalcStatus(Rec,ProdOrderLine);
    END;

    [External]
    PROCEDURE RunTimePer@7() : Decimal;
    BEGIN
      IF "Lot Size" = 0 THEN
        "Lot Size" := 1;

      EXIT("Run Time" / "Lot Size");
    END;

    LOCAL PROCEDURE CalculateRoutingBack@10();
    VAR
      ProdOrderLine@1000 : Record 5406;
      ProdOrderRtngLine@1001 : Record 5409;
    BEGIN
      IF "Previous Operation No." <> '' THEN BEGIN
        ProdOrderRtngLine.SETRANGE(Status,Status);
        ProdOrderRtngLine.SETRANGE("Prod. Order No.","Prod. Order No.");
        ProdOrderRtngLine.SETRANGE("Routing Reference No.","Routing Reference No.");
        ProdOrderRtngLine.SETRANGE("Routing No.","Routing No.");
        ProdOrderRtngLine.SETFILTER("Operation No.","Previous Operation No.");
        ProdOrderRtngLine.SETFILTER("Routing Status",'<>%1',ProdOrderRtngLine."Routing Status"::Finished);

        IF ProdOrderRtngLine.FIND('-') THEN
          REPEAT
            ProdOrderRtngLine.SETCURRENTKEY(Status,"Prod. Order No.","Routing Reference No.",
              "Routing No.","Sequence No. (Actual)");
            WorkCenter.GET(ProdOrderRtngLine."Work Center No.");
            CASE WorkCenter."Simulation Type" OF
              WorkCenter."Simulation Type"::Moves:
                BEGIN
                  ProdOrderRouteMgt.CalcSequenceFromActual(ProdOrderRtngLine,Direction::Backward);
                  CalcProdOrder.CalculateRoutingFromActual(ProdOrderRtngLine,Direction::Backward,TRUE);
                END;
              WorkCenter."Simulation Type"::"Moves When Necessary":
                IF (ProdOrderRtngLine."Ending Date" > "Starting Date") OR
                   ((ProdOrderRtngLine."Ending Date" = "Starting Date") AND
                    (ProdOrderRtngLine."Ending Time" > "Starting Time"))
                THEN BEGIN
                  ProdOrderRouteMgt.CalcSequenceFromActual(ProdOrderRtngLine,Direction::Backward);
                  CalcProdOrder.CalculateRoutingFromActual(ProdOrderRtngLine,Direction::Backward,TRUE);
                END;
              WorkCenter."Simulation Type"::Critical:
                BEGIN
                  IF (ProdOrderRtngLine."Ending Date" > "Starting Date") OR
                     ((ProdOrderRtngLine."Ending Date" = "Starting Date") AND
                      (ProdOrderRtngLine."Ending Time" > "Starting Time"))
                  THEN
                    ERROR(Text002);
                END;
            END;
            ProdOrderRtngLine.SETCURRENTKEY(Status,"Prod. Order No.","Routing Reference No.",
              "Routing No.","Operation No.");
          UNTIL ProdOrderRtngLine.NEXT = 0;
      END;

      ProdOrderLine.SETRANGE(Status,Status);
      ProdOrderLine.SETRANGE("Prod. Order No.","Prod. Order No.");
      ProdOrderLine.SETRANGE("Routing Reference No.","Routing Reference No.");
      ProdOrderLine.SETRANGE("Routing No.","Routing No.");
      IF ProdOrderLine.FIND('-') THEN
        REPEAT
          CalcProdOrder.CalculateProdOrderDates(ProdOrderLine,TRUE);
          AdjustComponents(ProdOrderLine);
        UNTIL ProdOrderLine.NEXT = 0;
    END;

    LOCAL PROCEDURE CalculateRoutingForward@9();
    VAR
      ProdOrderLine@1000 : Record 5406;
      ProdOrderRtngLine@1001 : Record 5409;
    BEGIN
      IF "Next Operation No." <> '' THEN BEGIN
        ProdOrderRtngLine.SETRANGE(Status,Status);
        ProdOrderRtngLine.SETRANGE("Prod. Order No.","Prod. Order No.");
        ProdOrderRtngLine.SETRANGE("Routing Reference No.","Routing Reference No.");
        ProdOrderRtngLine.SETRANGE("Routing No.","Routing No.");
        ProdOrderRtngLine.SETFILTER("Operation No.","Next Operation No.");
        ProdOrderRtngLine.SETFILTER("Routing Status",'<>%1',ProdOrderRtngLine."Routing Status"::Finished);

        IF ProdOrderRtngLine.FIND('-') THEN
          REPEAT
            ProdOrderRtngLine.SETCURRENTKEY(Status,"Prod. Order No.","Routing Reference No.",
              "Routing No.","Sequence No. (Actual)");
            WorkCenter.GET(ProdOrderRtngLine."Work Center No.");
            CASE WorkCenter."Simulation Type" OF
              WorkCenter."Simulation Type"::Moves:
                BEGIN
                  ProdOrderRouteMgt.CalcSequenceFromActual(ProdOrderRtngLine,Direction::Forward);
                  CalcProdOrder.CalculateRoutingFromActual(ProdOrderRtngLine,Direction::Forward,TRUE);
                END;
              WorkCenter."Simulation Type"::"Moves When Necessary":
                IF (ProdOrderRtngLine."Starting Date" < "Ending Date") OR
                   ((ProdOrderRtngLine."Starting Date" = "Ending Date") AND
                    (ProdOrderRtngLine."Starting Time" < "Ending Time"))
                THEN BEGIN
                  ProdOrderRouteMgt.CalcSequenceFromActual(ProdOrderRtngLine,Direction::Forward);
                  CalcProdOrder.CalculateRoutingFromActual(ProdOrderRtngLine,Direction::Forward,TRUE);
                END;
              WorkCenter."Simulation Type"::Critical:
                BEGIN
                  IF (ProdOrderRtngLine."Starting Date" < "Ending Date") OR
                     ((ProdOrderRtngLine."Starting Date" = "Ending Date") AND
                      (ProdOrderRtngLine."Starting Time" < "Ending Time"))
                  THEN
                    ERROR(Text003);
                END;
            END;
            ProdOrderRtngLine.SETCURRENTKEY(Status,"Prod. Order No.","Routing Reference No.",
              "Routing No.","Operation No.");
          UNTIL ProdOrderRtngLine.NEXT = 0;
      END;

      ProdOrderLine.SETRANGE(Status,Status);
      ProdOrderLine.SETRANGE("Prod. Order No.","Prod. Order No.");
      ProdOrderLine.SETRANGE("Routing Reference No.","Routing Reference No.");
      ProdOrderLine.SETRANGE("Routing No.","Routing No.");
      IF ProdOrderLine.FIND('-') THEN
        REPEAT
          CalcProdOrder.CalculateProdOrderDates(ProdOrderLine,TRUE);
          AdjustComponents(ProdOrderLine);
        UNTIL ProdOrderLine.NEXT = 0;
      CalcProdOrder.CalculateComponents;
    END;

    LOCAL PROCEDURE ModifyCapNeedEntries@13();
    BEGIN
      ProdOrderCapNeed.SETRANGE(Status,Status);
      ProdOrderCapNeed.SETRANGE("Prod. Order No.","Prod. Order No.");
      ProdOrderCapNeed.SETRANGE("Routing Reference No.","Routing Reference No.");
      ProdOrderCapNeed.SETRANGE("Routing No.","Routing No.");
      ProdOrderCapNeed.SETRANGE("Operation No.","Operation No.");
      ProdOrderCapNeed.SETRANGE("Requested Only",FALSE);
      IF ProdOrderCapNeed.FIND('-') THEN
        REPEAT
          ProdOrderCapNeed."No." := "No.";
          ProdOrderCapNeed."Work Center No." := "Work Center No.";
          ProdOrderCapNeed."Work Center Group Code" := "Work Center Group Code";
          ProdOrderCapNeed.MODIFY;
        UNTIL ProdOrderCapNeed.NEXT = 0;
    END;

    LOCAL PROCEDURE AdjustComponents@14(VAR ProdOrderLine@1000 : Record 5406);
    VAR
      ProdOrderComp@1001 : Record 5407;
    BEGIN
      ProdOrderComp.SETRANGE(Status,Status);
      ProdOrderComp.SETRANGE("Prod. Order No.","Prod. Order No.");
      ProdOrderComp.SETRANGE("Prod. Order Line No.",ProdOrderLine."Line No.");

      IF ProdOrderComp.FIND('-') THEN
        REPEAT
          ProdOrderComp.VALIDATE("Routing Link Code");
          ProdOrderComp.MODIFY;
        UNTIL ProdOrderComp.NEXT = 0;
    END;

    [External]
    PROCEDURE UpdateDatetime@11();
    BEGIN
      IF ("Starting Date" <> 0D) AND ("Starting Time" <> 0T) THEN
        "Starting Date-Time" := CREATEDATETIME("Starting Date","Starting Time")
      ELSE
        "Starting Date-Time" := 0DT;

      IF ("Ending Date" <> 0D) AND ("Ending Time" <> 0T) THEN
        "Ending Date-Time" := CREATEDATETIME("Ending Date","Ending Time")
      ELSE
        "Ending Date-Time" := 0DT;

      OnAfterUpdateDateTime(Rec,xRec,CurrFieldNo);
    END;

    [External]
    PROCEDURE CheckPreviousAndNext@18();
    VAR
      ProdOrderRtngLine@1001 : Record 5409;
      TempDeletedProdOrderRtngLine@1002 : TEMPORARY Record 5409;
      TempRemainingProdOrderRtngLine@1003 : TEMPORARY Record 5409;
      ProdOrderRoutingForm@1006 : Page 99000817;
      ErrorOnNext@1004 : Boolean;
      ErrorOnPrevious@1005 : Boolean;
    BEGIN
      IF IsSerial THEN
        SetPreviousAndNext
      ELSE BEGIN
        TempDeletedProdOrderRtngLine := Rec;
        TempDeletedProdOrderRtngLine.INSERT;

        ProdOrderRtngLine.SETRANGE(Status,Status);
        ProdOrderRtngLine.SETRANGE("Prod. Order No.","Prod. Order No.");
        ProdOrderRtngLine.SETRANGE("Routing Reference No.","Routing Reference No.");
        ProdOrderRtngLine.SETRANGE("Routing No.","Routing No.");
        ProdOrderRtngLine.SETFILTER("Operation No.",'<>%1',"Operation No.");
        ProdOrderRtngLine.SETFILTER("Routing Status",'<>%1',ProdOrderRtngLine."Routing Status"::Finished);

        IF ProdOrderRtngLine.FIND('-') THEN
          REPEAT
            IF ProdOrderRtngLine."Next Operation No." <> '' THEN BEGIN
              TempDeletedProdOrderRtngLine.SETFILTER("Operation No.",ProdOrderRtngLine."Next Operation No.");
              ErrorOnNext := TempDeletedProdOrderRtngLine.FINDFIRST;
            END ELSE
              ErrorOnNext := FALSE;

            IF ProdOrderRtngLine."Previous Operation No." <> '' THEN BEGIN
              TempDeletedProdOrderRtngLine.SETFILTER("Operation No.",ProdOrderRtngLine."Previous Operation No.");
              ErrorOnPrevious := TempDeletedProdOrderRtngLine.FINDFIRST;
            END ELSE
              ErrorOnPrevious := FALSE;

            IF ErrorOnNext OR ErrorOnPrevious THEN BEGIN
              TempRemainingProdOrderRtngLine := ProdOrderRtngLine;
              TempRemainingProdOrderRtngLine.INSERT;
            END
          UNTIL ProdOrderRtngLine.NEXT = 0;

        IF TempRemainingProdOrderRtngLine.FIND('-') THEN BEGIN
          COMMIT;
          IF NOT CONFIRM(
               STRSUBSTNO(Text004,FIELDCAPTION("Next Operation No."),FIELDCAPTION("Previous Operation No.")),
               TRUE)
          THEN
            EXIT;
          ProdOrderRoutingForm.Initialize(STRSUBSTNO(Text005,"Operation No."));
          REPEAT
            TempRemainingProdOrderRtngLine.MARK(TRUE);
          UNTIL TempRemainingProdOrderRtngLine.NEXT = 0;
          TempRemainingProdOrderRtngLine.MARKEDONLY(TRUE);
          ProdOrderRoutingForm.SETTABLEVIEW(TempRemainingProdOrderRtngLine);
          ProdOrderRoutingForm.RUNMODAL;
        END;
      END;
    END;

    LOCAL PROCEDURE SetPreviousAndNext@34();
    VAR
      ProdOrderRtngLine@1001 : Record 5409;
    BEGIN
      IF ProdOrderRtngLine.GET(Status,"Prod. Order No.","Routing Reference No.","Routing No.","Previous Operation No.") THEN BEGIN
        ProdOrderRtngLine."Next Operation No." := "Next Operation No.";
        ProdOrderRtngLine.MODIFY;
      END;
      IF ProdOrderRtngLine.GET(Status,"Prod. Order No.","Routing Reference No.","Routing No.","Next Operation No.") THEN BEGIN
        ProdOrderRtngLine."Previous Operation No." := "Previous Operation No.";
        ProdOrderRtngLine.MODIFY;
      END;
    END;

    [External]
    PROCEDURE SetNextOperations@4(VAR RtngLine@1001 : Record 5409);
    VAR
      RtngLine2@1003 : Record 5409;
    BEGIN
      RtngLine2.SETRANGE(Status,RtngLine.Status);
      RtngLine2.SETRANGE("Prod. Order No.",RtngLine."Prod. Order No.");
      RtngLine2.SETRANGE("Routing Reference No.",RtngLine."Routing Reference No.");
      RtngLine2.SETRANGE("Routing No.",RtngLine."Routing No.");
      RtngLine2.SETFILTER("Operation No.",'>%1',RtngLine."Operation No.");

      IF RtngLine2.FINDFIRST THEN
        RtngLine."Next Operation No." := RtngLine2."Operation No."
      ELSE BEGIN
        RtngLine2.SETFILTER("Operation No.",'');
        RtngLine2.SETRANGE("Next Operation No.",'');
        IF RtngLine2.FINDFIRST THEN BEGIN
          RtngLine2."Next Operation No." := RtngLine."Operation No.";
          RtngLine2.MODIFY;
        END;
      END;
    END;

    LOCAL PROCEDURE SubcontractPurchOrderExist@6() : Boolean;
    BEGIN
      IF Status <> Status::Released THEN
        EXIT(FALSE);

      ProdOrderLine.RESET;
      ProdOrderLine.SETRANGE(Status,Status);
      ProdOrderLine.SETRANGE("Prod. Order No.","Prod. Order No.");
      ProdOrderLine.SETRANGE("Routing Reference No.","Routing Reference No.");
      ProdOrderLine.SETRANGE("Routing No.","Routing No.");
      IF ProdOrderLine.FIND('-') THEN
        REPEAT
          PurchLine.SETCURRENTKEY(
            "Document Type",Type,"Prod. Order No.","Prod. Order Line No.","Routing No.","Operation No.");
          PurchLine.SETRANGE("Document Type",PurchLine."Document Type"::Order);
          PurchLine.SETRANGE(Type,PurchLine.Type::Item);
          PurchLine.SETRANGE("Prod. Order No.","Prod. Order No.");
          PurchLine.SETRANGE("Prod. Order Line No.",ProdOrderLine."Line No.");
          PurchLine.SETRANGE("Operation No.","Operation No.");
          IF NOT PurchLine.ISEMPTY THEN
            EXIT(TRUE);
        UNTIL ProdOrderLine.NEXT = 0;

      EXIT(FALSE);
    END;

    [External]
    PROCEDURE UpdateComponentsBin@16(FromTrigger@1000 : 'Insert,Modify,Delete');
    VAR
      TempProdOrderRtngLine@1001 : TEMPORARY Record 5409;
    BEGIN
      IF SkipUpdateOfCompBinCodes THEN
        EXIT;

      IF NOT UpdateOfComponentsBinRequired(FromTrigger) THEN
        EXIT;

      PopulateNewRoutingLineSet(TempProdOrderRtngLine,FromTrigger);
      ProdOrderRouteMgt.UpdateComponentsBin(TempProdOrderRtngLine,FALSE);
    END;

    LOCAL PROCEDURE UpdateOfComponentsBinRequired@19(FromTrigger@1000 : 'Insert,Modify,Delete') : Boolean;
    BEGIN
      IF ("No." = '') AND (xRec."No." = "No.") THEN // bin codes are and were empty
        EXIT(FALSE);

      CASE FromTrigger OF
        FromTrigger::Insert,FromTrigger::Delete:
          EXIT(("Previous Operation No." = '') OR ("Routing Link Code" <> ''));
        FromTrigger::Modify:
          EXIT(
            ((xRec."Previous Operation No." = '') AND ("Previous Operation No." <> '')) OR
            ((xRec."Previous Operation No." <> '') AND ("Previous Operation No." = '')) OR
            (xRec."Routing Link Code" <> "Routing Link Code") OR
            ((("Previous Operation No." = '') OR ("Routing Link Code" <> '')) AND
             ((xRec."To-Production Bin Code" <> "To-Production Bin Code") OR
              (xRec."Open Shop Floor Bin Code" <> "Open Shop Floor Bin Code"))));
      END;
    END;

    LOCAL PROCEDURE PopulateNewRoutingLineSet@20(VAR ProdOrderRtngLineTmp@1000 : Record 5409;FromTrigger@1001 : 'Insert,Modify,Delete');
    VAR
      ProdOrderRtngLine2@1002 : Record 5409;
    BEGIN
      // copy existing routings for this prod. order to temporary table
      ProdOrderRtngLineTmp.DELETEALL;
      ProdOrderRtngLine2.SETCURRENTKEY(Status,"Prod. Order No.","Routing Reference No.","Routing No.","Operation No.");
      ProdOrderRtngLine2.SETRANGE(Status,Status);
      ProdOrderRtngLine2.SETRANGE("Prod. Order No.","Prod. Order No.");
      ProdOrderRtngLine2.SETRANGE("Routing Reference No.","Routing Reference No.");
      ProdOrderRtngLine2.SETRANGE("Routing No.","Routing No.");
      IF ProdOrderRtngLine2.FINDSET(FALSE) THEN
        REPEAT
          ProdOrderRtngLineTmp := ProdOrderRtngLine2;
          ProdOrderRtngLineTmp.INSERT;
        UNTIL ProdOrderRtngLine2.NEXT = 0;

      // update the recordset with the current change
      ProdOrderRtngLineTmp := Rec;
      CASE FromTrigger OF
        FromTrigger::Insert:
          ProdOrderRtngLineTmp.INSERT;
        FromTrigger::Modify:
          ProdOrderRtngLineTmp.MODIFY;
        FromTrigger::Delete:
          ProdOrderRtngLineTmp.DELETE;
      END;
    END;

    [External]
    PROCEDURE SetSkipUpdateOfCompBinCodes@21(Setting@1000 : Boolean);
    BEGIN
      SkipUpdateOfCompBinCodes := Setting;
    END;

    LOCAL PROCEDURE ShiftTimeForwardOnParentProdOrderLines@29(VAR ChildProdOrderLine@1000 : Record 5406);
    VAR
      ParentProdOrderLine@1001 : Record 5406;
      ProdOrderComponent@1003 : Record 5407;
    BEGIN
      ParentProdOrderLine.SETRANGE(Status,ChildProdOrderLine.Status);
      ParentProdOrderLine.SETRANGE("Prod. Order No.",ChildProdOrderLine."Prod. Order No.");
      ParentProdOrderLine.SETRANGE("Planning Level Code",ChildProdOrderLine."Planning Level Code" - 1);
      ParentProdOrderLine.SETFILTER("Starting Date-Time",'<%1',ChildProdOrderLine."Ending Date-Time");
      IF ParentProdOrderLine.FINDSET THEN
        REPEAT
          ProdOrderComponent.SETRANGE(Status,ParentProdOrderLine.Status);
          ProdOrderComponent.SETRANGE("Prod. Order No.",ParentProdOrderLine."Prod. Order No.");
          ProdOrderComponent.SETRANGE("Prod. Order Line No.",ParentProdOrderLine."Line No.");
          ProdOrderComponent.SETRANGE("Supplied-by Line No.",ChildProdOrderLine."Line No.");
          IF NOT ProdOrderComponent.ISEMPTY THEN BEGIN
            IF GUIALLOWED THEN
              ShowMessage(TimeShiftedOnParentLineMsg);
            ParentProdOrderLine.VALIDATE("Starting Date-Time",ChildProdOrderLine."Ending Date-Time");
            IF ParentProdOrderLine."Ending Date-Time" < ParentProdOrderLine."Starting Date-Time" THEN
              ParentProdOrderLine."Ending Date-Time" := ParentProdOrderLine."Starting Date-Time";
            ParentProdOrderLine.MODIFY(TRUE);
            ShiftTimeForwardOnParentProdOrderLines(ParentProdOrderLine);
          END;
        UNTIL ParentProdOrderLine.NEXT = 0;
    END;

    LOCAL PROCEDURE ShowMessage@28(MessageText@1000 : Text);
    BEGIN
      TempErrorMessage.SetContext(Rec);
      IF TempErrorMessage.FindRecord(RECORDID,0,TempErrorMessage."Message Type"::Warning,MessageText) = 0 THEN BEGIN
        TempErrorMessage.LogMessage(Rec,0,TempErrorMessage."Message Type"::Warning,MessageText);
        MESSAGE(MessageText);
      END;
    END;

    LOCAL PROCEDURE NoTerminationProcessesExist@32() : Boolean;
    VAR
      ProdOrderRoutingLine@1000 : Record 5409;
    BEGIN
      ProdOrderRoutingLine.SETRANGE(Status,Status);
      ProdOrderRoutingLine.SETRANGE("Prod. Order No.","Prod. Order No.");
      ProdOrderRoutingLine.SETRANGE("Routing Reference No.","Routing Reference No.");
      ProdOrderRoutingLine.SETRANGE("Routing No.","Routing No.");
      ProdOrderRoutingLine.SETRANGE("Next Operation No.",'');
      ProdOrderRoutingLine.SETFILTER("Operation No.",'<>%1',"Operation No.");
      EXIT(ProdOrderRoutingLine.ISEMPTY);
    END;

    PROCEDURE IsSerial@33() : Boolean;
    BEGIN
      GetLine;
      EXIT(ProdOrderLine."Routing Type" = ProdOrderLine."Routing Type"::Serial)
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCalcStartingEndingDates@39(VAR ProdOrderRoutingLine@1000 : Record 5409;VAR xProdOrderRoutingLine@1001 : Record 5409;VAR ProdOrderLine@1002 : Record 5406;CallingFieldNo@1003 : Integer);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterDeleteRelations@44(VAR ProdOrderRoutingLine@1000 : Record 5409;SkipUpdateOfCompBinCodes@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterWorkCenterTransferFields@22(VAR ProdOrderRoutingLine@1000 : Record 5409;WorkCenter@1001 : Record 99000754);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterMachineCtrTransferFields@23(VAR ProdOrderRoutingLine@1000 : Record 5409;WorkCenter@1001 : Record 99000754;MachineCenter@1002 : Record 99000758);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSetRecalcStatus@36(VAR ProdOrderRoutingLine@1000 : Record 5409;VAR ProdOrderLine@1001 : Record 5406);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterTransferFromStdTaskTool@24(VAR ProdOrderRoutingTool@1000 : Record 5411;StandardTaskTool@1001 : Record 99000781);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterTransferFromStdTaskPersonnel@25(VAR ProdOrderRoutingPersonnel@1000 : Record 5412;StandardTaskPersonnel@1001 : Record 99000782);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterTransferFromStdTaskQltyMeasure@26(VAR ProdOrderRtngQltyMeas@1000 : Record 5413;StandardTaskQualityMeasure@1001 : Record 99000784);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterTransferFromStdTaskComment@27(VAR ProdOrderRtngCommentLine@1000 : Record 5415;StandardTaskDescription@1001 : Record 99000783);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterUpdateDateTime@38(VAR ProdOrderRoutingLine@1000 : Record 5409;VAR xProdOrderRoutingLine@1001 : Record 5409;CallingFieldNo@1002 : Integer);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterValidateNo@41(VAR ProdOrderRoutingLine@1000 : Record 5409;VAR xProdOrderRoutingLine@1001 : Record 5409;VAR ProdOrderLine@1002 : Record 5406);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCalcStartingEndingDates@35(VAR ProdOrderRoutingLine@1000 : Record 5409;Direction@1001 : 'Forward,Backward');
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeDeleteRelations@37(VAR ProdOrderRoutingLine@1000 : Record 5409);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeFillDefaultLocationAndBins@42(VAR ProdOrderRoutingLine@1000 : Record 5409);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeTerminationProcessesErr@40(VAR IsHandled@1000 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterFillDefaultLocationAndBins@43(VAR ProdOrderRoutingLine@1000 : Record 5409);
    BEGIN
    END;

    BEGIN
    END.
  }
}

