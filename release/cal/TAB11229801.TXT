OBJECT Table 11229801 Document Update Member (PPT)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ValidateFields;
             END;

    OnModify=BEGIN
               ValidateFields;
             END;

    OnDelete=VAR
               DocumentUpdatePPT@1100525000 : Record 11229800;
             BEGIN
               DocumentUpdatePPT.GET("Project No.", "Seq. No.");
               DocumentUpdatePPT.TESTFIELD("E-Mail Send On", 0DT);
             END;

    OnRename=BEGIN
               ValidateFields;
             END;

    CaptionML=ENU=Document Update Member;
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation=Job WHERE (Project Portal Project=CONST(Yes));
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr];
                                                   NotBlank=Yes }
    { 20  ;   ;Seq. No.            ;Integer       ;CaptionML=[ENU=Seq. No.;
                                                              NOR=Sekv. nr;
                                                              SVE=Sekv. nr];
                                                   Editable=No }
    { 30  ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Typ];
                                                   OptionCaptionML=ENU=" ,Responsible Person,Authority";
                                                   OptionString=[ ,Responsible Person,Authority] }
    { 40  ;   ;Employee No.        ;Code20        ;OnValidate=BEGIN
                                                                ValidateFields;
                                                                CALCFIELDS("Name Employee");
                                                              END;

                                                   OnLookup=VAR
                                                              ProjectResponsiblePerson@1100528300 : Record 11012040;
                                                            BEGIN
                                                              IF (Type <> Type::"Responsible Person") THEN
                                                                EXIT;

                                                              ProjectResponsiblePerson.SETRANGE("Project No.", "Project No.");

                                                              IF (PAGE.RUNMODAL(0, ProjectResponsiblePerson) = ACTION::LookupOK) THEN BEGIN
                                                                "Employee No." := ProjectResponsiblePerson."Employee No.";
                                                                VALIDATE("Employee No.");
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Employee No.;
                                                              NOR=Ansattnr;
                                                              SVE=Anst„llningsnr] }
    { 41  ;   ;Name Employee       ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Full Name" WHERE (No.=FIELD(Employee No.)));
                                                   CaptionML=[ENU=Name Employee;
                                                              NOR=Navn p† ansatt.;
                                                              SVE=Namn p† anst„lld.];
                                                   Editable=No }
    { 50  ;   ;Contact No.         ;Code20        ;OnValidate=BEGIN
                                                                CALCFIELDS("Contact Name");
                                                              END;

                                                   OnLookup=VAR
                                                              ProjectAuthority@1100528300 : Record 11020216;
                                                            BEGIN
                                                              IF (Type <> Type::Authority) THEN
                                                                EXIT;

                                                              ProjectAuthority.SETRANGE("Project No.", "Project No.");

                                                              IF (PAGE.RUNMODAL(0, ProjectAuthority) = ACTION::LookupOK) THEN BEGIN
                                                                "Contact No." := ProjectAuthority."Contact No.";
                                                                VALIDATE("Contact No.");
                                                                "Contact Person No." := ProjectAuthority."Contact Person No.";
                                                                VALIDATE("Contact Person No.");
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Contact No.;
                                                              NOR=Kontaktnr;
                                                              SVE=Kontaktnr] }
    { 60  ;   ;Contact Person No.  ;Code20        ;OnValidate=BEGIN
                                                                ValidateFields;
                                                                CALCFIELDS("Contact Person Name");
                                                              END;

                                                   OnLookup=VAR
                                                              ProjectAuthority@1100528300 : Record 11020216;
                                                            BEGIN
                                                              IF (Type <> Type::Authority) OR ("Contact No." = '') THEN
                                                                EXIT;

                                                              ProjectAuthority.SETRANGE("Project No.", "Project No.");
                                                              ProjectAuthority.SETRANGE("Contact No.", "Contact No.");

                                                              IF (PAGE.RUNMODAL(0, ProjectAuthority) = ACTION::LookupOK) THEN BEGIN
                                                                "Contact No." := ProjectAuthority."Contact No.";
                                                                VALIDATE("Contact No.");
                                                                "Contact Person No." := ProjectAuthority."Contact Person No.";
                                                                VALIDATE("Contact Person No.");
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Contact Person No.;
                                                              NOR=Kontaktperson;
                                                              SVE=Kontaktperson] }
    { 61  ;   ;Contact Name        ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=[ENU=Contact Name;
                                                              NOR=Kontaktnavn;
                                                              SVE=Kontaktnamn];
                                                   Editable=No }
    { 62  ;   ;Contact Person Name ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Contact Person No.)));
                                                   CaptionML=[ENU=Contact Person Name;
                                                              NOR=Kontaktpersonens navn;
                                                              SVE=Kontaktpersonens namn];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Project No.,Seq. No.,Type,Employee No.,Contact No.,Contact Person No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE ValidateFields@1100525000();
    VAR
      DocumentUpdatePPT@1100525000 : Record 11229800;
      ProjectResponsiblePerson@1100525001 : Record 11012040;
      ProjectAuthority@1100525002 : Record 11020216;
      Employee@1100528300 : Record 5200;
      Contact@1100528301 : Record 5050;
    BEGIN
      TESTFIELD("Project No.");
      DocumentUpdatePPT.GET("Project No.", "Seq. No.");
      DocumentUpdatePPT.TESTFIELD("E-Mail Send On", 0DT);

      CASE Type OF
        Type::"Responsible Person": BEGIN
          "Contact No." := '';
          "Contact Person No." := '';
          TESTFIELD("Employee No.");
          ProjectResponsiblePerson.SETRANGE("Project No.", "Project No.");
          ProjectResponsiblePerson.SETRANGE("Employee No.", "Employee No.");
          ProjectResponsiblePerson.FINDFIRST;

          Employee.GET("Employee No.");
          Employee.TESTFIELD("Company E-Mail");
        END;
        Type::Authority: BEGIN
          "Employee No." := '';
          TESTFIELD("Contact No.");
          ProjectAuthority.SETRANGE("Project No.", "Project No.");
          ProjectAuthority.SETRANGE("Contact No.", "Contact No.");
          IF ("Contact Person No." <> '') THEN
            ProjectAuthority.SETRANGE("Contact Person No.", "Contact Person No.");
          ProjectAuthority.FINDFIRST;

          IF ("Contact Person No." <> '') THEN
            Contact.GET("Contact Person No.")
          ELSE
            Contact.GET("Contact No.");
          Contact.TESTFIELD("E-Mail 2");
        END;
        ELSE
          TESTFIELD(Type);
      END;
    END;

    BEGIN
    END.
  }
}

