OBJECT Codeunit 12013610 ExFlow Release Purch. Doc.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF512000;
  }
  PROPERTIES
  {
    TableNo=38;
    OnRun=VAR
            AppSetUp@1100285011 : Record 12013601;
            Object@1100285017 : Record 2000000001;
            PurchHeader@1100285008 : Record 38;
            DocHead@1100285007 : Record 12013608;
            DocLine@1100285006 : Record 12013609;
            DocLineApp@1100285005 : Record 12013610;
            PurchLine@1100285014 : Record 39;
            AppGroup@1100285012 : Record 12013606;
            ExDocHead2@1100285003 : Record 12013612;
            ExFlow@1100285010 : Codeunit 12013601;
            ExFlowtoInvoice@1100285015 : Codeunit 12013592;
            CreateQuote@1100285004 : Codeunit 12013614;
            Folder@1100285002 : Text[170];
            SubFolder@1100285001 : Text[30];
            FileName@1100285000 : Text[50];
            First@1100285016 : Boolean;
            ReportNoToPrint@1100285018 : Integer;
          BEGIN
            IF NOT ("Document Type" IN ["Document Type"::Order,"Document Type"::Quote]) THEN
              EXIT;

            IF "Document Type" = "Document Type"::Quote THEN BEGIN
              IF NOT Approve THEN
                EXIT;

              IF Status = Status::Open THEN
                EXIT;

              PurchLine.RESET;
              PurchLine.SETRANGE("Document Type", "Document Type");
              PurchLine.SETRANGE("Document No.", "No.");
              IF PurchLine.FINDFIRST THEN
                IF DocHead.RetrieveRecord(DocHead,"Document Type","No.") THEN
                  ExFlow.AmountToDocHead(PurchLine,FALSE,Rec);

              DocHead.RetrieveRecord(DocHead, "Document Type", "No.");
              IF DocHead.Status >= DocHead.Status::Approved THEN BEGIN
                CreateQuote.PrintPQDoc("No.");

                EXIT;
              END;

              DocHead.Status := DocHead.Status::Active;
              DocHead."Changed By" := USERID;
              DocHead."Change Date" := CREATEDATETIME(TODAY,TIME);
              DocHead.MODIFY;

              ExFlow.CreateWebVendor(DocHead);

              DocLineApp.RESET;
              DocLineApp.SETCURRENTKEY("Document Type","Document No.","Line No.","Approver Order");
              DocLineApp.SETRANGE("Document Type", "Document Type");
              DocLineApp.SETRANGE("Document No.", "No.");
              IF DocLineApp.FIND('-') THEN
                DocLineApp.ApprovalStatus(DocLineApp,'Update');

              CreateQuote.PrintPQDoc("No.");
            END
            ELSE BEGIN
              AppSetUp.GET;
              IF NOT AppSetUp."Purchase Order" THEN
                IF NOT Approve THEN
                  EXIT;

              Object.RESET;
              Object.SETRANGE(Type, Object.Type::Report);
              Object.SETRANGE(ID, AppSetUp."Order Report");
              IF NOT Object.FINDFIRST THEN
                ERROR(EXF51);

              ReportNoToPrint := AppSetUp."Order Report";
              Folder := AppSetUp."Path to Used Invoices";
              SubFolder := ExFlow.DayMap(WORKDATE);
              FileName := 'Order-' + "No." + '.pdf';

              IF NOT DocHead.RetrieveRecord(DocHead, "Document Type", "No.") THEN BEGIN
                PurchLine.RESET;
                PurchLine.SETRANGE("Document Type", "Document Type");
                PurchLine.SETRANGE("Document No.", "No.");
                IF PurchLine.FINDSET THEN BEGIN
                  REPEAT
                    ExFlow.CreateDocumentLine(PurchLine,TRUE,TRUE);
                  UNTIL PurchLine.NEXT = 0;

                  IF DocHead.RetrieveRecord(DocHead,"Document Type","No.") THEN
                    ExFlow.AmountToDocHead(PurchLine,FALSE,Rec);
                END;
              END
              ELSE BEGIN
                PurchLine.RESET;
                PurchLine.SETRANGE("Document Type", "Document Type");
                PurchLine.SETRANGE("Document No.", "No.");
                IF PurchLine.FINDFIRST THEN
                  IF DocHead.RetrieveRecord(DocHead,"Document Type","No.") THEN
                    ExFlow.AmountToDocHead(PurchLine,FALSE,Rec);

                PurchHeader.GET("Document Type", "No.");
                IF DocHead.Status >= DocHead.Status::Approved THEN BEGIN
                  QuoteToOrder.PrintPODoc(PurchHeader);
                  EXIT;
                END;
              END;

              DocLineApp.RESET;
              DocLineApp.SETCURRENTKEY("Document No.");
              DocLineApp.SETRANGE("Document Type", "Document Type");
              DocLineApp.SETRANGE("Document No.", "No.");
              DocLineApp.SETFILTER(Flowstatus,'>=%1',DocLineApp.Flowstatus::Approved);
              IF NOT DocLineApp.FIND('-') THEN
                DocLineApp.DELETEALL(FALSE);

              PurchHeader.GET("Document Type", "No.");
              PurchHeader."New Line" := TRUE;
              PurchHeader.MODIFY(FALSE);

              DocLine.RESET;
              DocLine.SETCURRENTKEY("Document No.");
              DocLine.SETRANGE("Document Type", "Document Type");
              DocLine.SETRANGE("Document No.", "No.");
              DocLine.SETRANGE("Connected Line ID",0);
              DocLine.SETFILTER(Flowstatus, '<%1', DocLine.Flowstatus::Approved);
              IF DocLine.FINDSET THEN
                REPEAT
                  PurchLine.GET(DocLine."Document Type", DocLine."Document No.", DocLine."Line No.");
                  ExFlow.CreateApproversFromPurch(DocLine,PurchLine,PurchLine."Template Rule",FALSE,'','',FALSE);

                  DocLineApp.RESET;
                  DocLineApp.SETCURRENTKEY("Document Line ID");
                  DocLineApp.SETRANGE("Document Line ID", DocLine.ID);
                  IF NOT DocLineApp.FINDFIRST THEN
                    ERROR(EXF02, DocLine."Line No.");
                UNTIL DocLine.NEXT = 0;

              PurchHeader.GET("Document Type", "No.");
              PurchHeader."New Line" := FALSE;
              PurchHeader.MODIFY(FALSE);

              DocHead.RetrieveRecord(DocHead, PurchHeader."Document Type", PurchHeader."No.");
              DocHead."Image Name" := SubFolder + FileName;
              DocHead."Changed By" := USERID;
              DocHead."Change Date" := CREATEDATETIME(TODAY,TIME);
              DocHead.MODIFY(FALSE);

              ExFlow.CreateWebVendor(DocHead);

              ExDocHead2.RESET;
              ExDocHead2.SETCURRENTKEY("Document No.");
              ExDocHead2.SETRANGE("Document Type", DocHead."Document Type");
              ExDocHead2.SETRANGE("Document No.", DocHead."Document No.");
              IF NOT ExDocHead2.FINDFIRST THEN BEGIN
                ExDocHead2.INIT;
                ExDocHead2."Entry No." := 0;
                ExDocHead2."Document Type" := DocHead."Document Type";
                ExDocHead2."Document No." := DocHead."Document No.";
                ExDocHead2."Image Name" := DocHead."Image Name";
                ExDocHead2.INSERT;
              END
              ELSE BEGIN
                ExDocHead2."Image Name" := DocHead."Image Name";
                ExDocHead2.MODIFY;
              END;

              PurchLine.RESET;
              PurchLine.SETRANGE("Document Type", PurchHeader."Document Type");
              PurchLine.SETRANGE("Document No.", PurchHeader."No.");
              IF PurchLine.FINDSET THEN
                REPEAT
                  First := TRUE;

                  DocLineApp.RESET;
                  DocLineApp.SETCURRENTKEY("Document No.");
                  DocLineApp.SETRANGE("Document Type", "Document Type");
                  DocLineApp.SETRANGE("Document No.", "No.");
                  DocLineApp.SETRANGE("Line No.", PurchLine."Line No.");
                  DocLineApp.SETFILTER(Flowstatus, '%1|%2', DocLineApp.Flowstatus::"Not processed",DocLineApp.Flowstatus::Current);
                  IF DocLineApp.FINDSET(TRUE,FALSE) THEN BEGIN
                    REPEAT
                      AppGroup.GET(DocLineApp.Approver);
                      IF AppGroup."Auto Approve" THEN
                        ExFlowtoInvoice.AutoApproveGrp(DocLineApp,AppGroup.Code, AppGroup."Auto Approve Comment")
                      ELSE
                        IF AppSetUp."Auto Approve PO from Blanket" AND (PurchLine."Blanket Order No." <> '') THEN
                          AutoApproveBlanket(DocLineApp,EXF003)
                        ELSE
                          IF First THEN
                            BEGIN
                              First := FALSE;
                              DocLineApp.VALIDATE(Flowstatus,DocLineApp.Flowstatus::Current);
                            END;

                      DocLineApp.MODIFY(FALSE);
                    UNTIL DocLineApp.NEXT = 0;
                  END;

                  DocLineApp.SETRANGE(Flowstatus);
                  IF DocLineApp.FIND('-') THEN
                    DocLineApp.ApprovalStatus(DocLineApp,'Update');
                UNTIL PurchLine.NEXT = 0;

              DocLine.RESET;
              DocLine.SETCURRENTKEY("Document No.");
              DocLine.SETRANGE("Document Type", "Document Type");
              DocLine.SETRANGE("Document No.", "No.");
              DocLine.SETRANGE(Flowstatus, DocLine.Flowstatus::Active);
              IF DocLine.FINDFIRST THEN BEGIN
                Status := Status::Released;

                IF AppSetUp."Unapproved Order Report" <> 0 THEN BEGIN
                  Object.RESET;
                  Object.SETRANGE(Type, Object.Type::Report);
                  Object.SETRANGE(ID, AppSetUp."Unapproved Order Report");
                  IF Object.FINDFIRST THEN
                    ReportNoToPrint := AppSetUp."Unapproved Order Report";
                END;
              END
              ELSE
                Status := Status::Released;

              UpdateApprovedAmount(Rec);

              MODIFY;

              QuoteToOrder.PrintPODoc(PurchHeader);
            END;
          END;

  }
  CODE
  {
    VAR
      EXF02@1100285000 : TextConst 'ENU=Line %1 has no Approvers.;SVE=Rad %1 saknar attestanter!';
      EXF003@1100285002 : TextConst 'ENU=Automatically approved by system (blanket order).;SVE=Automatiskt godk„nd av systemet (avropsorder)';
      EXF51@1100285003 : TextConst 'ENU=Order Report ID missing in Approval Setup.;SVE=Orderrapport-ID saknas i Attestinst„llningar!';
      QuoteToOrder@1100285001 : Codeunit 12013607;

    PROCEDURE Reopen@1100285000(PurchHeader@1100285000 : Record 38);
    VAR
      AppSetUp@1100285001 : Record 12013601;
      DocHead@1100285003 : Record 12013608;
    BEGIN
      IF NOT (PurchHeader."Document Type" IN [PurchHeader."Document Type"::Order,PurchHeader."Document Type"::Quote]) THEN
        EXIT;

      AppSetUp.GET;
      IF (PurchHeader."Document Type" = PurchHeader."Document Type"::Quote) OR
         ((PurchHeader."Document Type" = PurchHeader."Document Type"::Order) AND AppSetUp."Purchase Order") THEN BEGIN
        DocHead.RetrieveRecord(DocHead, PurchHeader."Document Type", PurchHeader."No.");
        DocHead.Status := DocHead.Status::Inactive;
        DocHead.MODIFY(FALSE);
      END;
    END;

    PROCEDURE AutoApproveBlanket@1100285005(VAR DocLineApp@1100285002 : Record 12013610;AutoApprText@1100285001 : Text[100]);
    BEGIN
      DocLineApp.Flowstatus := DocLineApp.Flowstatus::Approved;
      DocLineApp."Approval Date" := CREATEDATETIME(WORKDATE,TIME);
      DocLineApp."Approved By" := USERID;
      DocLineApp."Auto Approved" := TRUE;
      DocLineApp.Comment := AutoApprText;
    END;

    PROCEDURE UpdateApprovedAmount@1100285002(PurchHeader@1100285000 : Record 38);
    VAR
      PurchLine@1100285001 : Record 39;
      PurchBlanketOrderLine@1100285002 : Record 39;
      QuantityOnOrders@1100285003 : Decimal;
    BEGIN
      IF PurchHeader.Status <> PurchHeader.Status::Released THEN
        EXIT;

      PurchLine.SETRANGE("Document Type", PurchHeader."Document Type");
      PurchLine.SETRANGE("Document No.", PurchHeader."No.");
      IF PurchLine.FINDSET(TRUE,FALSE) THEN
        REPEAT
          IF PurchLine."Blanket Order No." <> '' THEN BEGIN
            IF PurchBlanketOrderLine.GET(PurchBlanketOrderLine."Document Type"::"Blanket Order",PurchLine."Blanket Order No.",
                                         PurchLine."Blanket Order Line No.") THEN BEGIN
              QuantityOnOrders := CalculateOutstandingBlanketQty(PurchBlanketOrderLine);

              IF ((QuantityOnOrders + PurchBlanketOrderLine."Qty. Received (Base)") >
                  ABS(PurchBlanketOrderLine."Quantity (Base)")) OR
                 (PurchBlanketOrderLine."Quantity (Base)" * PurchBlanketOrderLine."Outstanding Qty. (Base)" < 0)
              THEN
                PurchLine."PO Approved Amount" := 0
              ELSE
                PurchLine."PO Approved Amount" := PurchLine.Quantity * PurchBlanketOrderLine."Direct Unit Cost" *
                                                  (1- (PurchBlanketOrderLine."Line Discount %" / 100));
            END;
          END
          ELSE
            PurchLine."PO Approved Amount" := PurchLine."Line Amount";

          PurchLine.MODIFY;
        UNTIL PurchLine.NEXT = 0;
    END;

    PROCEDURE CalculateOutstandingBlanketQty@1100285003(PurchBlanketOrderLine@1100285001 : Record 39) QuantityOnOrders : Decimal;
    VAR
      PurchLine@1100285000 : Record 39;
    BEGIN
      PurchLine.SETCURRENTKEY("Document Type","Blanket Order No.","Blanket Order Line No.");
      PurchLine.SETRANGE("Blanket Order No.",PurchBlanketOrderLine."Document No.");
      PurchLine.SETRANGE("Blanket Order Line No.",PurchBlanketOrderLine."Line No.");
      QuantityOnOrders := 0;
      IF PurchLine.FINDSET THEN
        REPEAT
          IF PurchLine."Document Type" IN
              [PurchLine."Document Type"::"Return Order",
               PurchLine."Document Type"::"Credit Memo"] THEN
            QuantityOnOrders := QuantityOnOrders - PurchLine."Outstanding Qty. (Base)"
          ELSE
            QuantityOnOrders := QuantityOnOrders + PurchLine."Outstanding Qty. (Base)";
        UNTIL PurchLine.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

