OBJECT Table 11229847 Indexing Rate (Rental Unit)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               IndexGroup@1100529000 : Record 11229846;
             BEGIN
               IndexGroup.GET("Index Group");
               IndexGroup.TESTFIELD(Type, IndexGroup.Type::Index);
               TESTFIELD(Year);
               TESTFIELD("Index Date");
               CheckDateInYear();
               CheckIndexRates(FIELDNO("Previous Index"));
             END;

    OnModify=BEGIN
               CheckIndexRates(FIELDNO("Previous Index"));
             END;

    OnRename=BEGIN
               TESTFIELD("Index Date");
               CheckDateInYear();
             END;

    CaptionML=ENU=Indexing Rate (Rental Unit);
  }
  FIELDS
  {
    { 10  ;   ;Index Group         ;Code10        ;TableRelation="Index Group (Rental Unit)";
                                                   CaptionML=ENU=Index Group;
                                                   NotBlank=Yes }
    { 20  ;   ;Year                ;Integer       ;OnValidate=BEGIN
                                                                IF Year <> xRec.Year THEN BEGIN
                                                                  IF "Index Date" <> 0D  THEN BEGIN
                                                                    IF DATE2DMY("Index Date", 3) <> Year THEN
                                                                      "Index Date" := 0D;
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Year;
                                                              NOR=èr;
                                                              SVE=èr];
                                                   NotBlank=Yes;
                                                   BlankZero=Yes }
    { 30  ;   ;Index Date          ;Date          ;OnValidate=BEGIN
                                                                IF "Index Date" <> 0D THEN BEGIN
                                                                  TESTFIELD(Year);
                                                                  CheckDateInYear();
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Index Date;
                                                              NOR=Indeksdato;
                                                              SVE=Indexdatum];
                                                   NotBlank=Yes }
    { 40  ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 50  ;   ;Based on Index      ;Boolean       ;OnValidate=BEGIN
                                                                IF NOT "Based on Index" THEN BEGIN
                                                                  TESTFIELD(Index, 0);
                                                                  TESTFIELD("Previous Index", 0);
                                                                END ELSE BEGIN
                                                                  IF ("Based on Index" <> xRec."Based on Index") OR ("Previous Index" = 0) THEN BEGIN
                                                                    FillDefaultPrevIndexRate();
                                                                    CalculatePercentage();
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Based on Index }
    { 60  ;   ;Previous Index      ;Decimal       ;OnValidate=BEGIN
                                                                CheckIndexRates(FIELDNO("Previous Index"));
                                                              END;

                                                   CaptionML=ENU=Previous Index;
                                                   DecimalPlaces=2:5;
                                                   BlankZero=Yes }
    { 70  ;   ;Index               ;Decimal       ;OnValidate=BEGIN
                                                                CheckIndexRates(FIELDNO(Index));
                                                              END;

                                                   CaptionML=[ENU=Index;
                                                              NOR=Indeks;
                                                              SVE=Index];
                                                   DecimalPlaces=2:5;
                                                   BlankZero=Yes }
    { 80  ;   ;Percentage          ;Decimal       ;OnValidate=BEGIN
                                                                IF Percentage <> xRec.Percentage THEN BEGIN
                                                                  IF "Based on Index" THEN
                                                                    ERROR(Text002, FIELDCAPTION(Percentage), FIELDCAPTION("Based on Index"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Percentage;
                                                              NOR=Prosent;
                                                              SVE=Procent];
                                                   DecimalPlaces=2:5 }
  }
  KEYS
  {
    {    ;Index Group,Year,Index Date             ;Clustered=Yes }
    {    ;Index Group,Index Date                   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100529000 : TextConst 'ENU=%1 (%2) must be in %3';
      Text002@1100529001 : TextConst 'ENU=Modify %1 not allowed if ''%2''';
      Text003@1100529002 : TextConst 'ENU=%1 may not be empty ''%2''';

    PROCEDURE CheckDateInYear@1100529001();
    BEGIN
      IF (DATE2DMY("Index Date", 3) <> Year) THEN
        ERROR(Text001, FIELDCAPTION("Index Date"), "Index Date", Year);
    END;

    PROCEDURE CheckIndexRates@1100529006(FieldNumber@1100529000 : Integer);
    BEGIN
      IF (FieldNumber = 0) OR (FieldNumber = FIELDNO(Index)) THEN BEGIN
        IF Index <> 0 THEN
          TESTFIELD("Based on Index");
        IF "Based on Index" THEN BEGIN
          IF Index = 0 THEN
            ERROR(Text003, FIELDCAPTION(Index), FIELDCAPTION("Based on Index"));
          CalculatePercentage();
        END;
      END;
      IF (FieldNumber = 0) OR (FieldNumber = FIELDNO("Previous Index")) THEN BEGIN
        IF "Previous Index" <> 0 THEN
          TESTFIELD("Based on Index");
        IF "Based on Index" THEN BEGIN
          IF "Previous Index" = 0 THEN
            ERROR(Text003, FIELDCAPTION("Previous Index"), FIELDCAPTION("Based on Index"));
          CalculatePercentage();
        END;
      END;
    END;

    PROCEDURE FillDefaultPrevIndexRate@1100529004();
    VAR
      IndexingRate@1100529000 : Record 11229847;
    BEGIN
      IndexingRate.SETRANGE("Index Group", "Index Group");
      IndexingRate.SETRANGE(Year, Year - 1);
      IndexingRate.SETRANGE("Based on Index", TRUE);
      IF IndexingRate.FINDLAST THEN
        "Previous Index" := IndexingRate.Index;
    END;

    PROCEDURE CalculatePercentage@1100529005();
    BEGIN
      IF "Based on Index" THEN BEGIN
        IF ("Previous Index" = 0) OR (Index = 0) THEN
          Percentage := 0
        ELSE
          Percentage := ROUND( ((Index - "Previous Index") / "Previous Index" * 100), 0.00001);
      END;
    END;

    BEGIN
    END.
  }
}

