OBJECT Table 8613 Config. Package Table
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               InitPackageFields;
             END;

    OnDelete=BEGIN
               DeletePackageDataForPackage("Package Code","Table ID");
               DeleteRelatedTables("Package Code","Table ID");
             END;

    OnRename=BEGIN
               ERROR(Text004);
             END;

    ReplicateData=No;
    CaptionML=[ENU=Config. Package Table;
               NOR=Konfig.pakketabell];
  }
  FIELDS
  {
    { 1   ;   ;Package Code        ;Code20        ;TableRelation="Config. Package";
                                                   CaptionML=[ENU=Package Code;
                                                              NOR=Pakkekode;
                                                              SVE=Paketkod] }
    { 2   ;   ;Table ID            ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   OnValidate=BEGIN
                                                                IF ConfigMgt.IsSystemTable("Table ID") THEN
                                                                  ERROR(Text001,"Table ID");

                                                                IF "Table ID" <> xRec."Table ID" THEN
                                                                  "Page ID" := ConfigMgt.FindPage("Table ID");
                                                              END;

                                                   OnLookup=BEGIN
                                                              ConfigValidateMgt.LookupTable("Table ID");
                                                              IF "Table ID" <> 0 THEN
                                                                VALIDATE("Table ID");
                                                            END;

                                                   CaptionML=[ENU=Table ID;
                                                              NOR=Tabell-ID;
                                                              SVE=Tabell-ID];
                                                   NotBlank=Yes }
    { 3   ;   ;Table Name          ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Name" WHERE (Object Type=CONST(Table),
                                                                                                             Object ID=FIELD(Table ID)));
                                                   CaptionML=[ENU=Table Name;
                                                              NOR=Tabellnavn;
                                                              SVE=Tabellnamn];
                                                   Editable=No }
    { 4   ;   ;No. of Package Records;Integer     ;FieldClass=FlowField;
                                                   CalcFormula=Count("Config. Package Record" WHERE (Package Code=FIELD(Package Code),
                                                                                                     Table ID=FIELD(Table ID)));
                                                   CaptionML=[ENU=No. of Package Records;
                                                              NOR=Antall pakkeposter];
                                                   Editable=No }
    { 5   ;   ;No. of Package Errors;Integer      ;FieldClass=FlowField;
                                                   CalcFormula=Count("Config. Package Error" WHERE (Package Code=FIELD(Package Code),
                                                                                                    Table ID=FIELD(Table ID)));
                                                   CaptionML=[ENU=No. of Package Errors;
                                                              NOR=Antall pakkefeil];
                                                   Editable=No }
    { 7   ;   ;Imported Date and Time;DateTime    ;CaptionML=[ENU=Imported Date and Time;
                                                              NOR=Importert dato og klokkeslett];
                                                   Editable=No }
    { 8   ;   ;Exported Date and Time;DateTime    ;CaptionML=[ENU=Exported Date and Time;
                                                              NOR=Eksportert dato og klokkeslett];
                                                   Editable=No }
    { 9   ;   ;Comments            ;Text250       ;CaptionML=[ENU=Comments;
                                                              NOR=Merknader;
                                                              SVE=Kommentarer] }
    { 10  ;   ;Created Date and Time;DateTime     ;CaptionML=[ENU=Created Date and Time;
                                                              NOR=Opprettet dato og klokkeslett] }
    { 11  ;   ;Company Filter (Source Table);Text30;
                                                   FieldClass=FlowFilter;
                                                   TableRelation=Company;
                                                   CaptionML=[ENU=Company Filter (Source Table);
                                                              NOR=Selskapsfilter (kildetabell)] }
    { 12  ;   ;Table Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table ID)));
                                                   CaptionML=[ENU=Table Caption;
                                                              NOR=Tabelloverskrift;
                                                              SVE=Tabellrubrik];
                                                   Editable=No }
    { 13  ;   ;Data Template       ;Code10        ;TableRelation="Config. Template Header";
                                                   CaptionML=[ENU=Data Template;
                                                              NOR=Datamal] }
    { 14  ;   ;Package Processing Order;Integer   ;CaptionML=[ENU=Package Processing Order;
                                                              NOR=Pakkebehandlingsordre];
                                                   Editable=No }
    { 15  ;   ;Page ID             ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Page));
                                                   OnLookup=BEGIN
                                                              ConfigValidateMgt.LookupPage("Page ID");
                                                              VALIDATE("Page ID");
                                                            END;

                                                   CaptionML=[ENU=Page ID;
                                                              NOR=Side-ID;
                                                              SVE=Sid-ID] }
    { 16  ;   ;Processing Order    ;Integer       ;CaptionML=[ENU=Processing Order;
                                                              NOR=Behandler ordre] }
    { 17  ;   ;No. of Fields Included;Integer     ;FieldClass=FlowField;
                                                   CalcFormula=Count("Config. Package Field" WHERE (Package Code=FIELD(Package Code),
                                                                                                    Table ID=FIELD(Table ID),
                                                                                                    Include Field=CONST(Yes)));
                                                   CaptionML=[ENU=No. of Fields Included;
                                                              NOR=Antall felt inkludert];
                                                   Editable=No }
    { 18  ;   ;No. of Fields Available;Integer    ;FieldClass=FlowField;
                                                   CalcFormula=Count("Config. Package Field" WHERE (Package Code=FIELD(Package Code),
                                                                                                    Table ID=FIELD(Table ID)));
                                                   CaptionML=[ENU=No. of Fields Available;
                                                              NOR=Antall tilgjengelige felt];
                                                   Editable=No }
    { 19  ;   ;No. of Fields to Validate;Integer  ;FieldClass=FlowField;
                                                   CalcFormula=Count("Config. Package Field" WHERE (Package Code=FIELD(Package Code),
                                                                                                    Table ID=FIELD(Table ID),
                                                                                                    Validate Field=CONST(Yes)));
                                                   CaptionML=[ENU=No. of Fields to Validate;
                                                              NOR=Antall felt som skal valideres];
                                                   Editable=No }
    { 20  ;   ;Package Caption     ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Config. Package"."Package Name" WHERE (Code=FIELD(Package Code)));
                                                   CaptionML=[ENU=Package Caption;
                                                              NOR=Pakkeoverskrift];
                                                   Editable=No }
    { 21  ;   ;Imported by User ID ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserManagement@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserManagement.LookupUserID("Imported by User ID");
                                                            END;

                                                   TestTableRelation=No;
                                                   DataClassification=EndUserIdentifiableInformation;
                                                   CaptionML=[ENU=Imported by User ID;
                                                              NOR=Importert av bruker-ID];
                                                   Editable=No }
    { 22  ;   ;Created by User ID  ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserManagement@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserManagement.LookupUserID("Created by User ID");
                                                            END;

                                                   TestTableRelation=No;
                                                   DataClassification=EndUserIdentifiableInformation;
                                                   CaptionML=[ENU=Created by User ID;
                                                              NOR=Opprettet av bruker-ID;
                                                              SVE=Skapades av anv„ndar-ID];
                                                   Editable=No }
    { 24  ;   ;Dimensions as Columns;Boolean      ;OnValidate=BEGIN
                                                                IF "Dimensions as Columns" THEN BEGIN
                                                                  InitDimensionFields;
                                                                  UpdateDimensionsPackageData;
                                                                END ELSE
                                                                  DeleteDimensionFields;
                                                              END;

                                                   CaptionML=[ENU=Dimensions as Columns;
                                                              NOR=Dimensjoner som kolonner] }
    { 25  ;   ;Filtered            ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Config. Package Filter" WHERE (Package Code=FIELD(Package Code),
                                                                                                     Table ID=FIELD(Table ID)));
                                                   CaptionML=[ENU=Filtered;
                                                              NOR=Filtrert];
                                                   Editable=No }
    { 26  ;   ;Skip Table Triggers ;Boolean       ;CaptionML=[ENU=Skip Table Triggers;
                                                              NOR=Hopp over tabellutl›sere] }
    { 27  ;   ;Delete Recs Before Processing;Boolean;
                                                   CaptionML=[ENU=Delete Recs Before Processing;
                                                              NOR=Slett poster f›r behandling] }
    { 28  ;   ;Processing Report ID;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Report));
                                                   CaptionML=[ENU=Processing Report ID;
                                                              NOR=Behandlingsrapport-ID] }
    { 29  ;   ;Parent Table ID     ;Integer       ;TableRelation="Config. Package Table"."Table ID" WHERE (Package Code=FIELD(Package Code));
                                                   OnValidate=VAR
                                                                Levels@1003 : Integer;
                                                              BEGIN
                                                                IF "Table ID" = "Parent Table ID" THEN
                                                                  ERROR(CannotBeItsOwnParentErr);

                                                                Levels := CheckchildrenTables(Rec);
                                                                Levels += CheckParentTables(Rec);

                                                                // There should be no more than 2 level of parents, children or both for any table.
                                                                IF Levels > 2 THEN
                                                                  ERROR(CannotAddParentErr);
                                                              END;

                                                   CaptionML=[ENU=Parent Table ID;
                                                              NOR=ID for overordnet tabell] }
    { 30  ;   ;Validated           ;Boolean       ;CaptionML=[ENU=Validated;
                                                              NOR=Validert] }
    { 31  ;   ;Delayed Insert      ;Boolean       ;CaptionML=[ENU=Delayed Insert;
                                                              NOR=Forsinket innsetting] }
  }
  KEYS
  {
    {    ;Package Code,Table ID                   ;Clustered=Yes }
    {    ;Package Processing Order,Processing Order }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=You cannot use system table %1 in the package.;NOR=Du kan ikke bruke systemtabellen %1 i pakken.';
      Text002@1005 : TextConst 'ENU=You cannot use the Dimensions as Columns function for table %1.;NOR=Du kan ikke bruke dimensjonene som kolonnefunksjon for tabellen %1.';
      Text003@1006 : TextConst 'ENU=The Default Dimension and Dimension Value tables must be included in the package %1 to enable this option. The missing tables will be added to the package. Do you want to continue?;NOR=Tabellene Standarddimensjon og Dimensjonsverdi m† inkluderes i pakken %1 for † aktivere dette alternativet. De manglende tabellene vil bli lagt til i pakken. Vil du fortsette?';
      Text004@1008 : TextConst 'ENU=You cannot rename the configuration package table.;NOR=Du kan ikke endre navn p† konfigurasjonspakketabellen.';
      Text005@1009 : TextConst 'ENU=The setup of Dimensions as Columns was canceled.;NOR=Oppsettet for dimensjoner som kolonner ble avbrutt.';
      Text010@1003 : TextConst 'ENU=Define the drill-down page in the %1 field.;NOR=Definer drilldown-siden i feltet %1.';
      ConfigValidateMgt@1000 : Codeunit 8617;
      ConfigMgt@1002 : Codeunit 8616;
      ConfigPackageMgt@1004 : Codeunit 8611;
      HideValidationDialog@1010 : Boolean;
      i@1007 : Integer;
      CannotAddParentErr@1033 : TextConst 'ENU=Cannot add a parent table. This table is already included in a three-level hierarchy, which is the maximum.;NOR=Kan ikke legge til en overordnet tabell. Denne tabellen er allerede inkludert i et hierarki med tre niv†er, som er maksimum.';
      CannotBeItsOwnParentErr@1034 : TextConst 'ENU=Cannot add the parent table. A table cannot be its own parent or child.;NOR=Kan ikke legge til den overordnede tabellen. En tabell kan ikke v‘re sin egen overordnede eller underordnede.';
      CircularDependencyErr@1035 : TextConst 'ENU=Cannot add the parent table. The table is already the child of the selected tab.;NOR=Kan ikke legge til den overordnede tabellen. Tabellen er allerede underordnet den valgte kategorien.';
      ParentTableNotFoundErr@1036 : TextConst '@@@=%1 - Table number;ENU=Cannot find table %1.;NOR=Finner ikke tabellen %1.';

    PROCEDURE DeleteRelatedTables@20(PackageCode@1000 : Code[20];TableID@1001 : Integer);
    VAR
      ConfigLine@1005 : Record 8622;
      ConfigPackageField@1004 : Record 8616;
      ConfigFieldMapping@1006 : Record 8628;
      ConfigPackageFilter@1003 : Record 8626;
      ConfigTableProcessingRule@1002 : Record 8631;
    BEGIN
      ConfigFieldMapping.SETRANGE("Package Code",PackageCode);
      IF TableID <> 0 THEN
        ConfigFieldMapping.SETRANGE("Table ID","Table ID");
      ConfigFieldMapping.DELETEALL;

      ConfigPackageField.SETRANGE("Package Code",PackageCode);
      IF TableID <> 0 THEN
        ConfigPackageField.SETRANGE("Table ID","Table ID");
      ConfigPackageField.DELETEALL;

      ConfigPackageFilter.SETRANGE("Package Code",PackageCode);
      IF TableID <> 0 THEN
        ConfigPackageFilter.SETRANGE("Table ID","Table ID");
      ConfigPackageFilter.DELETEALL;

      ConfigTableProcessingRule.SETRANGE("Package Code",PackageCode);
      IF TableID <> 0 THEN
        ConfigTableProcessingRule.SETRANGE("Table ID","Table ID");
      ConfigTableProcessingRule.DELETEALL;

      ConfigLine.SETRANGE("Package Code",PackageCode);
      IF TableID <> 0 THEN
        ConfigLine.SETRANGE("Table ID","Table ID");
      ConfigLine.SETRANGE("Dimensions as Columns",TRUE);
      ConfigLine.MODIFYALL("Dimensions as Columns",FALSE);
      ConfigLine.SETRANGE("Dimensions as Columns");
      ConfigLine.MODIFYALL("Package Code",'');
    END;

    LOCAL PROCEDURE InitPackageFields@1() : Boolean;
    VAR
      ConfigPackageField@1001 : Record 8616;
      ConfigLine@1004 : Record 8622;
      Field@1000 : Record 2000000041;
      ProcessingOrder@1002 : Integer;
      FieldsAdded@1003 : Boolean;
    BEGIN
      FieldsAdded := FALSE;
      ConfigPackageMgt.SetFieldFilter(Field,"Table ID",0);
      IF Field.FINDSET THEN
        REPEAT
          IF NOT ConfigPackageField.GET("Package Code","Table ID",Field."No.") AND
             NOT ConfigPackageMgt.IsDimSetIDField("Table ID",Field."No.")
          THEN BEGIN
            ConfigPackageMgt.InsertPackageField(
              ConfigPackageField,"Package Code","Table ID",Field."No.",Field.FieldName,Field."Field Caption",
              TRUE,TRUE,FALSE,FALSE);
            ConfigPackageField.SETRANGE("Package Code","Package Code");
            ConfigPackageField.SETRANGE("Table ID","Table ID");
            ConfigPackageField.SETRANGE("Field ID",Field."No.");
            ConfigPackageMgt.SelectAllPackageFields(ConfigPackageField,TRUE);
            FieldsAdded := TRUE;
          END;
        UNTIL Field.NEXT = 0;

      IF FieldsAdded THEN BEGIN
        ProcessingOrder := 0;
        SetProcessingOrderPrimaryKey("Package Code","Table ID",ProcessingOrder);
        ConfigPackageField.RESET;
        ConfigPackageField.SETRANGE("Package Code","Package Code");
        ConfigPackageField.SETRANGE("Table ID","Table ID");
        ConfigPackageField.SETRANGE("Primary Key",FALSE);
        IF "Table ID" <> DATABASE::"Config. Line" THEN
          SetProcessingOrderFields(ConfigPackageField,ProcessingOrder)
        ELSE BEGIN
          ConfigPackageField.SETRANGE("Field ID",ConfigLine.FIELDNO("Line Type"),ConfigLine.FIELDNO("Table ID"));
          SetProcessingOrderFields(ConfigPackageField,ProcessingOrder);
          // package code must be processed just after table ID!
          ConfigPackageField.SETRANGE("Field ID",ConfigLine.FIELDNO("Package Code"));
          SetProcessingOrderFields(ConfigPackageField,ProcessingOrder);
          ConfigPackageField.SETRANGE("Field ID",ConfigLine.FIELDNO(Name),ConfigLine.FIELDNO("Package Code") - 1);
          SetProcessingOrderFields(ConfigPackageField,ProcessingOrder);
          ConfigPackageField.SETFILTER("Field ID",'%1..',ConfigLine.FIELDNO("Package Code") + 1);
          SetProcessingOrderFields(ConfigPackageField,ProcessingOrder);
        END;
      END;

      EXIT(FieldsAdded);
    END;

    LOCAL PROCEDURE SetProcessingOrderPrimaryKey@17(PackageCode@1006 : Code[20];TableID@1007 : Integer;VAR ProcessingOrder@1000 : Integer);
    VAR
      ConfigPackageField@1001 : Record 8616;
      RecRef@1004 : RecordRef;
      KeyRef@1003 : KeyRef;
      FieldRef@1002 : FieldRef;
      KeyFieldCount@1005 : Integer;
    BEGIN
      RecRef.OPEN(TableID);
      KeyRef := RecRef.KEYINDEX(1);
      FOR KeyFieldCount := 1 TO KeyRef.FIELDCOUNT DO BEGIN
        FieldRef := KeyRef.FIELDINDEX(KeyFieldCount);
        ConfigPackageField.GET(PackageCode,TableID,FieldRef.NUMBER);
        ProcessingOrder += 1;
        ConfigPackageField."Processing Order" := ProcessingOrder;
        ConfigPackageField.MODIFY;
      END;
    END;

    LOCAL PROCEDURE SetProcessingOrderFields@9(VAR ConfigPackageField@1000 : Record 8616;VAR ProcessingOrder@1002 : Integer);
    BEGIN
      IF ConfigPackageField.FINDSET THEN
        REPEAT
          ProcessingOrder += 1;
          ConfigPackageField."Processing Order" := ProcessingOrder;
          ConfigPackageField.MODIFY;
        UNTIL ConfigPackageField.NEXT = 0;
    END;

    [External]
    PROCEDURE InitDimensionFields@3();
    VAR
      Dimension@1002 : Record 348;
      ConfigPackageField@1000 : Record 8616;
      ConfigPackageTable@1001 : Record 8613;
      Confirmed@1004 : Boolean;
    BEGIN
      IF NOT (ConfigMgt.IsDimSetIDTable("Table ID") OR ConfigMgt.IsDefaultDimTable("Table ID")) THEN
        ERROR(Text002,"Table ID");

      IF ConfigMgt.IsDefaultDimTable("Table ID") THEN BEGIN
        Confirmed :=
          (ConfigPackageTable.GET("Package Code",DATABASE::"Dimension Value") AND
           ConfigPackageTable.GET("Package Code",DATABASE::"Default Dimension")) OR
          (HideValidationDialog OR NOT GUIALLOWED);
        IF NOT Confirmed THEN
          Confirmed := CONFIRM(Text003,TRUE,"Package Code");
        IF Confirmed THEN BEGIN
          ConfigPackageMgt.InsertPackageTable(ConfigPackageTable,"Package Code",DATABASE::"Dimension Value");
          ConfigPackageMgt.InsertPackageTable(ConfigPackageTable,"Package Code",DATABASE::"Default Dimension");
        END ELSE
          ERROR(Text005);
      END;

      i := 0;
      IF Dimension.FINDSET THEN
        REPEAT
          i := i + 1;
          ConfigPackageMgt.InsertPackageField(
            ConfigPackageField,"Package Code","Table ID",ConfigMgt.DimensionFieldID + i,
            Dimension.Code,Dimension."Code Caption",TRUE,FALSE,FALSE,TRUE);
        UNTIL Dimension.NEXT = 0;
    END;

    [External]
    PROCEDURE DeletePackageData@8();
    BEGIN
      DeletePackageDataForPackage("Package Code","Table ID");
    END;

    [External]
    PROCEDURE DeletePackageDataForPackage@21(PackageCode@1000 : Code[20];TableId@1002 : Integer);
    VAR
      ConfigPackageRecord@1001 : Record 8614;
      ConfigPackageData@1004 : Record 8615;
      ConfigPackageError@1003 : Record 8617;
    BEGIN
      ConfigPackageRecord.SETRANGE("Package Code",PackageCode);
      IF TableId <> 0 THEN
        ConfigPackageRecord.SETRANGE("Table ID",TableId);
      ConfigPackageRecord.DELETEALL;

      ConfigPackageData.SETRANGE("Package Code",PackageCode);
      IF TableId <> 0 THEN
        ConfigPackageData.SETRANGE("Table ID",TableId);
      ConfigPackageData.DELETEALL;

      ConfigPackageError.SETRANGE("Package Code",PackageCode);
      IF TableId <> 0 THEN
        ConfigPackageError.SETRANGE("Table ID",TableId);
      ConfigPackageError.DELETEALL;
    END;

    LOCAL PROCEDURE DeleteDimensionFields@7();
    VAR
      ConfigPackageField@1000 : Record 8616;
      ConfigPackageData@1002 : Record 8615;
    BEGIN
      ConfigPackageData.SETRANGE("Package Code","Package Code");
      ConfigPackageData.SETRANGE("Table ID","Table ID");
      ConfigPackageData.SETRANGE("Field ID",ConfigMgt.DimensionFieldID,ConfigMgt.DimensionFieldID + 999);
      ConfigPackageData.DELETEALL;

      ConfigPackageField.SETRANGE("Package Code","Package Code");
      ConfigPackageField.SETRANGE("Table ID","Table ID");
      ConfigPackageField.SETRANGE(Dimension,TRUE);
      ConfigPackageField.DELETEALL;
    END;

    [External]
    PROCEDURE DimensionFieldsCount@2() : Integer;
    VAR
      ConfigPackageField@1000 : Record 8616;
    BEGIN
      ConfigPackageField.SETRANGE("Package Code","Package Code");
      ConfigPackageField.SETRANGE("Table ID","Table ID");
      ConfigPackageField.SETRANGE(Dimension,TRUE);
      EXIT(ConfigPackageField.COUNT);
    END;

    [External]
    PROCEDURE DimensionPackageDataExist@6() : Boolean;
    VAR
      ConfigPackageData@1000 : Record 8615;
    BEGIN
      ConfigPackageData.SETRANGE("Package Code","Package Code");
      ConfigPackageData.SETRANGE("Table ID","Table ID");
      ConfigPackageData.SETRANGE("Field ID",ConfigMgt.DimensionFieldID,ConfigMgt.DimensionFieldID + 999);
      EXIT(NOT ConfigPackageData.ISEMPTY);
    END;

    [External]
    PROCEDURE ShowPackageRecords@5(Show@1004 : 'Records,Errors,All';ShowDim@1003 : Boolean);
    VAR
      ConfigPackageField@1000 : Record 8616;
      ConfigPackageRecord@1001 : Record 8614;
      ConfigPackageRecords@1002 : Page 8626;
      MatrixColumnCaptions@1005 : ARRAY [1000] OF Text[100];
    BEGIN
      ConfigPackageField.SETRANGE("Package Code","Package Code");
      ConfigPackageField.SETRANGE("Table ID","Table ID");
      ConfigPackageField.SETRANGE("Include Field",TRUE);
      IF NOT ShowDim THEN
        ConfigPackageField.SETRANGE(Dimension,FALSE);
      i := 1;
      CLEAR(MatrixColumnCaptions);
      IF ConfigPackageField.FINDSET THEN
        REPEAT
          MatrixColumnCaptions[i] := ConfigPackageField."Field Name";
          i := i + 1;
        UNTIL ConfigPackageField.NEXT = 0;

      CALCFIELDS("Table Caption");
      CLEAR(ConfigPackageRecords);
      ConfigPackageRecord.SETRANGE("Package Code","Package Code");
      ConfigPackageRecord.SETRANGE("Table ID","Table ID");
      CASE Show OF
        Show::Records:
          ConfigPackageRecord.SETRANGE(Invalid,FALSE);
        Show::Errors:
          ConfigPackageRecord.SETRANGE(Invalid,TRUE);
      END;
      ConfigPackageRecords.SETTABLEVIEW(ConfigPackageRecord);
      ConfigPackageRecords.LOOKUPMODE(TRUE);
      ConfigPackageRecords.Load(MatrixColumnCaptions,"Table Caption","Package Code","Table ID",ShowDim);
      ConfigPackageRecords.RUNMODAL;
    END;

    [External]
    PROCEDURE ShowDatabaseRecords@4();
    VAR
      ConfigLine@1000 : Record 8622;
    BEGIN
      IF "Page ID" <> 0 THEN
        PAGE.RUN("Page ID")
      ELSE BEGIN
        ConfigLine.SETRANGE("Package Code","Package Code");
        ConfigLine.SETRANGE("Table ID","Table ID");
        IF ConfigLine.FINDFIRST AND (ConfigLine."Page ID" > 0) THEN
          PAGE.RUN(ConfigLine."Page ID")
        ELSE
          ERROR(Text010,FIELDCAPTION("Page ID"));
      END;
    END;

    [External]
    PROCEDURE ShowPackageFields@11();
    BEGIN
      ShowFilteredPackageFields('');
    END;

    [External]
    PROCEDURE ShowFilteredPackageFields@19(FilterValue@1002 : Text);
    VAR
      ConfigPackageField@1000 : Record 8616;
      ConfigPackageFields@1001 : Page 8624;
    BEGIN
      IF InitPackageFields THEN
        COMMIT;

      IF "Dimensions as Columns" THEN
        IF NOT DimensionPackageDataExist THEN BEGIN
          IF DimensionFieldsCount > 0 THEN
            DeleteDimensionFields;
          InitDimensionFields;
          COMMIT;
        END;

      ConfigPackageField.FILTERGROUP(2);
      ConfigPackageField.SETRANGE("Package Code","Package Code");
      ConfigPackageField.SETRANGE("Table ID","Table ID");
      IF FilterValue <> '' THEN
        ConfigPackageField.SETFILTER("Field ID",FilterValue);
      ConfigPackageField.FILTERGROUP(0);
      ConfigPackageFields.SETTABLEVIEW(ConfigPackageField);
      ConfigPackageFields.RUNMODAL;
      CLEAR(ConfigPackageFields);
    END;

    [External]
    PROCEDURE ShowPackageCard@14(PackageCode@1002 : Code[20]);
    VAR
      ConfigPackage@1000 : Record 8623;
      ConfigPackageCard@1001 : Page 8614;
    BEGIN
      ConfigPackage.FILTERGROUP(2);
      ConfigPackage.SETRANGE(Code,PackageCode);
      ConfigPackage.FILTERGROUP(0);
      ConfigPackageCard.SETTABLEVIEW(ConfigPackage);
      ConfigPackageCard.RUNMODAL;
      CLEAR(ConfigPackageCard);
    END;

    [External]
    PROCEDURE SetFieldStyle@12(FieldNumber@1000 : Integer) : Text;
    BEGIN
      CASE FieldNumber OF
        FIELDNO("No. of Package Records"):
          BEGIN
            CALCFIELDS("No. of Package Records");
            IF "No. of Package Records" > 0 THEN
              EXIT('Strong');
          END;
        FIELDNO("No. of Package Errors"):
          BEGIN
            CALCFIELDS("No. of Package Errors");
            IF "No. of Package Errors" > 0 THEN
              EXIT('Unfavorable');
          END;
      END;

      EXIT('');
    END;

    [External]
    PROCEDURE ShowFilters@10();
    VAR
      ConfigPackageFilter@1001 : Record 8626;
      ConfigPackageFilters@1000 : Page 8623;
    BEGIN
      ConfigPackageFilter.FILTERGROUP(2);
      ConfigPackageFilter.SETRANGE("Package Code","Package Code");
      ConfigPackageFilter.SETRANGE("Table ID","Table ID");
      ConfigPackageFilter.SETRANGE("Processing Rule No.",0);
      ConfigPackageFilter.FILTERGROUP(0);
      ConfigPackageFilters.SETTABLEVIEW(ConfigPackageFilter);
      ConfigPackageFilters.RUNMODAL;
      CLEAR(ConfigPackageFilters);
    END;

    [External]
    PROCEDURE ShowProcessingRules@18();
    VAR
      ConfigTableProcessingRule@1001 : Record 8631;
      ConfigTableProcessingRules@1000 : Page 8640;
    BEGIN
      ConfigTableProcessingRule.FILTERGROUP(2);
      ConfigTableProcessingRule.SETRANGE("Package Code","Package Code");
      ConfigTableProcessingRule.SETRANGE("Table ID","Table ID");
      ConfigTableProcessingRule.FILTERGROUP(0);
      ConfigTableProcessingRules.SETTABLEVIEW(ConfigTableProcessingRule);
      ConfigTableProcessingRules.RUNMODAL;
      CLEAR(ConfigTableProcessingRules);
    END;

    LOCAL PROCEDURE UpdateDimensionsPackageData@13();
    VAR
      ConfigPackageField@1000 : Record 8616;
      ConfigPackageRecord@1001 : Record 8614;
      ConfigPackageData@1002 : Record 8615;
    BEGIN
      ConfigPackageRecord.SETRANGE("Package Code","Package Code");
      ConfigPackageRecord.SETRANGE("Table ID","Table ID");
      IF ConfigPackageRecord.FINDSET THEN
        REPEAT
          ConfigPackageField.SETRANGE("Package Code","Package Code");
          ConfigPackageField.SETRANGE("Table ID","Table ID");
          ConfigPackageField.SETRANGE(Dimension,TRUE);
          IF ConfigPackageField.FINDSET THEN
            REPEAT
              ConfigPackageMgt.InsertPackageData(
                ConfigPackageData,"Package Code","Table ID",ConfigPackageRecord."No.",
                ConfigPackageField."Field ID",'',ConfigPackageRecord.Invalid);
            UNTIL ConfigPackageField.NEXT = 0;
        UNTIL ConfigPackageRecord.NEXT = 0;
    END;

    [External]
    PROCEDURE SetHideValidationDialog@16(NewHideValidationDialog@1000 : Boolean);
    BEGIN
      HideValidationDialog := NewHideValidationDialog;
    END;

    [External]
    PROCEDURE GetNoOfDatabaseRecords@15() : Integer;
    VAR
      ConfigXMLExchange@1002 : Codeunit 8614;
      RecRef@1000 : RecordRef;
    BEGIN
      IF "Table ID" = 0 THEN
        EXIT(0);

      IF NOT ConfigXMLExchange.TableObjectExists("Table ID") THEN
        EXIT(0);

      RecRef.OPEN("Table ID",FALSE,"Company Filter (Source Table)");
      EXIT(RecRef.COUNT);
    END;

    LOCAL PROCEDURE CheckParentTables@31(ConfigPackageTable@1050 : Record 8613) : Integer;
    VAR
      ParentsFound@1001 : Integer;
    BEGIN
      IF ConfigPackageTable."Parent Table ID" <> 0 THEN
        REPEAT
          IF NOT ConfigPackageTable.GET(ConfigPackageTable."Package Code",ConfigPackageTable."Parent Table ID") THEN
            ERROR(ParentTableNotFoundErr,ConfigPackageTable."Parent Table ID");
          // Check there is no circular dependency
          IF ConfigPackageTable."Parent Table ID" = "Table ID" THEN
            ERROR(CircularDependencyErr);
          ParentsFound += 1;
          IF ParentsFound > 2 THEN
            EXIT(ParentsFound);
        UNTIL ConfigPackageTable."Parent Table ID" = 0;
      EXIT(ParentsFound);
    END;

    LOCAL PROCEDURE CheckchildrenTables@32(ConfigPackageTable@1060 : Record 8613) : Integer;
    VAR
      childrenFound@1001 : Integer;
      TempCounter@1002 : Integer;
    BEGIN
      ConfigPackageTable.SETRANGE("Package Code",ConfigPackageTable."Package Code");
      ConfigPackageTable.SETRANGE("Parent Table ID",ConfigPackageTable."Table ID");
      childrenFound := 0;
      IF ConfigPackageTable.FINDSET THEN BEGIN
        REPEAT
          // Check there is no circular dependency
          IF ConfigPackageTable."Table ID" = "Parent Table ID" THEN
            ERROR(CircularDependencyErr);
          TempCounter := CheckchildrenTables(ConfigPackageTable);
          IF childrenFound < TempCounter THEN
            childrenFound := TempCounter;
          IF childrenFound > 2 THEN
            EXIT(childrenFound)
        UNTIL ConfigPackageTable.NEXT = 0;
        childrenFound += 1;
      END;
      EXIT(childrenFound);
    END;

    BEGIN
    END.
  }
}

