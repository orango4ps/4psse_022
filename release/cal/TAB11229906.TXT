OBJECT Table 11229906 Invoice Proposal Batch
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               InvoiceProposalBatch@1100528600 : Record 11229906;
             BEGIN
               IF ID = 0 THEN BEGIN
                 InvoiceProposalBatch.LOCKTABLE;
                 IF NOT InvoiceProposalBatch.FINDLAST THEN
                   ID := 1
                 ELSE
                   ID := InvoiceProposalBatch.ID +1;
               END;
               "Created on" := TODAY;
               "Created by" := USERID;
             END;

    OnDelete=VAR
               InvoiceProposalBatchFilter@1100528600 : Record 11229909;
               InvoiceProposalBatchSeq@1100528601 : Record 11229907;
             BEGIN
               InvoiceProposalBatchFilter.SETRANGE("Invoice Proposal Batch ID", ID);
               InvoiceProposalBatchFilter.DELETEALL(TRUE);

               InvoiceProposalBatchSeq.SETRANGE("Invoice Proposal Batch ID", ID);
               InvoiceProposalBatchSeq.DELETEALL(TRUE);
             END;

    CaptionML=ENU=Proposal Invoice Batch;
    LookupPageID=Page11126571;
    DrillDownPageID=Page11126571;
  }
  FIELDS
  {
    { 10  ;   ;ID                  ;Integer       ;CaptionML=[ENU=ID;
                                                              NOR=ID;
                                                              SVE=ID];
                                                   Editable=No }
    { 20  ;   ;Invoice Proposal Type;Option       ;OnValidate=VAR
                                                                InvoiceProposalBatchFilter@1100528600 : Record 11229909;
                                                              BEGIN
                                                                IF xRec."Invoice Proposal Type" <> "Invoice Proposal Type" THEN BEGIN
                                                                  InvoiceProposalBatchFilter.SETRANGE("Invoice Proposal Batch ID", ID);
                                                                  IF NOT InvoiceProposalBatchFilter.ISEMPTY THEN
                                                                    ERROR(Text000);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Invoice Proposal Type;
                                                   OptionCaptionML=[ENU=Service Contract;
                                                                    NOR=Servicekontrakt;
                                                                    SVE=Servicekontrakt];
                                                   OptionString=Service Contract }
    { 30  ;   ;Batch Seq. No. Preface;Code10      ;CaptionML=ENU=Batch Seq. No. Preface }
    { 40  ;   ;Mode                ;Option        ;CaptionML=ENU=Mode;
                                                   OptionCaptionML=ENU=Manual,Automatic,Both;
                                                   OptionString=Manual,Automatic,Both }
    { 50  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 60  ;   ;Created on          ;Date          ;CaptionML=[ENU=Created on;
                                                              NOR=Opprettet den;
                                                              SVE=Skapades];
                                                   Editable=No }
    { 70  ;   ;Created by          ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserCode@1210190000 : Code[50];
                                                              UserMgt@1210190001 : Codeunit 418;
                                                            BEGIN
                                                              UserCode := "Created by";
                                                              UserMgt.LookupUserID(UserCode);
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Created by;
                                                              NOR=Opprettet av;
                                                              SVE=Skapades av];
                                                   Editable=No }
    { 80  ;   ;Transaction Day     ;Integer       ;InitValue=1;
                                                   CaptionML=ENU=Transaction Day;
                                                   MinValue=1;
                                                   MaxValue=31 }
    { 90  ;   ;Last Batch Seq. No. ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Max("Invoice Proposal Batch Seq."."Batch Seq. No." WHERE (Invoice Proposal Batch ID=FIELD(ID)));
                                                   CaptionML=ENU=Last Batch Seq. No.;
                                                   Editable=No }
    { 100 ;   ;No. of Days in Advance;Integer     ;CaptionML=ENU=No. of Days in Advance }
    { 110 ;   ;Active              ;Boolean       ;CaptionML=[ENU=Active;
                                                              NOR=Aktiv;
                                                              SVE=Aktiv] }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528600 : TextConst 'ENU=Cannot change field %1, there are filter lines present';

    PROCEDURE ApplyFilterSC@1210190000(IBatchSeqNo@1100528602 : Code[20];VAR OServiceContract@1210190000 : Record 11012812);
    VAR
      InvoiceProposalBatchFilter@1210190001 : Record 11229909;
      Customer@1100528600 : Record 18;
      TableRef@1210190002 : RecordRef;
      FieldRef@1210190003 : FieldRef;
    BEGIN
      TESTFIELD("Invoice Proposal Type", "Invoice Proposal Type"::"Service Contract");

      OServiceContract.RESET;

      TableRef.GETTABLE(OServiceContract);
      InvoiceProposalBatchFilter.SETRANGE("Invoice Proposal Batch ID", ID);
      InvoiceProposalBatchFilter.SETRANGE("Batch Seq. No.", IBatchSeqNo);
      InvoiceProposalBatchFilter.SETRANGE("Table Type", InvoiceProposalBatchFilter."Table Type"::"Service Contract");
      InvoiceProposalBatchFilter.SETFILTER("Value Filter", '<>%1', '');
      IF InvoiceProposalBatchFilter.FINDSET THEN BEGIN
        REPEAT
          FieldRef := TableRef.FIELD(InvoiceProposalBatchFilter."Field No.");
          FieldRef.SETFILTER(InvoiceProposalBatchFilter."Value Filter");
        UNTIL InvoiceProposalBatchFilter.NEXT = 0;
      END;
      TableRef.SETTABLE(OServiceContract);

      TableRef.GETTABLE(Customer);
      InvoiceProposalBatchFilter.SETRANGE("Table Type", InvoiceProposalBatchFilter."Table Type"::Customer);
      InvoiceProposalBatchFilter.SETFILTER("Value Filter", '<>%1', '');
      IF InvoiceProposalBatchFilter.FINDSET THEN BEGIN
        REPEAT
          FieldRef := TableRef.FIELD(InvoiceProposalBatchFilter."Field No.");
          FieldRef.SETFILTER(InvoiceProposalBatchFilter."Value Filter");
        UNTIL InvoiceProposalBatchFilter.NEXT = 0;
        TableRef.SETTABLE(Customer);
        IF Customer.FINDSET THEN
          REPEAT
            OServiceContract.SETFILTER("Bill-to Customer No. (Contr.)", Customer."No.");
            IF OServiceContract.FINDSET THEN
              REPEAT
                OServiceContract.MARK(TRUE);
              UNTIL OServiceContract.NEXT = 0;
          UNTIL Customer.NEXT = 0;
        OServiceContract.SETRANGE("Bill-to Customer No. (Contr.)");
        OServiceContract.MARKEDONLY(TRUE);
      END;
    END;

    PROCEDURE ShowFilterResultSC@1100528600();
    VAR
      ServiceContract@1100528600 : Record 11012812;
    BEGIN
      ApplyFilterSC('', ServiceContract);
      PAGE.RUNMODAL(0, ServiceContract);
    END;

    BEGIN
    END.
  }
}

