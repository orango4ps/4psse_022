OBJECT Report 93 Create Fiscal Year
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=GeschÑftsjahr erîffnen;
               ENU=Create Fiscal Year;
               NLD=Boekjaar maken;
               NOR=Opprett regnskapsÜr];
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  ConfirmManagement@1000 : Codeunit 27;
                BEGIN
                  AccountingPeriod."Starting Date" := FiscalYearStartDate;
                  AccountingPeriod.TESTFIELD("Starting Date");

                  AccountingPeriod.SETRANGE(Closed,FALSE);
                  IF AccountingPeriod.FIND('-') THEN BEGIN
                    FirstPeriodStartDate := AccountingPeriod."Starting Date";
                    FirstPeriodLocked := AccountingPeriod."Date Locked";
                    IF (NOT HideDialog) AND (FiscalYearStartDate < FirstPeriodStartDate) AND FirstPeriodLocked THEN
                      IF NOT ConfirmManagement.ConfirmProcess(CreateAndCloseQst,FALSE) THEN
                        EXIT;
                  END ELSE
                    IF NOT HideDialog THEN
                      IF NOT ConfirmManagement.ConfirmProcess(CreateQst,FALSE) THEN
                        EXIT;

                  AccountingPeriod.SETRANGE(Closed);
                  FiscalYearStartDate2 := FiscalYearStartDate;

                  FOR i := 1 TO NoOfPeriods + 1 DO BEGIN
                    IF (FiscalYearStartDate <= FirstPeriodStartDate) AND (i = NoOfPeriods + 1) THEN
                      EXIT;

                    AccountingPeriod.INIT;
                    AccountingPeriod."Starting Date" := FiscalYearStartDate;
                    AccountingPeriod.VALIDATE("Starting Date");
                    IF (i = 1) OR (i = NoOfPeriods + 1) THEN BEGIN
                      AccountingPeriod."New Fiscal Year" := TRUE;
                      InvtSetup.GET;
                      AccountingPeriod."Average Cost Calc. Type" := InvtSetup."Average Cost Calc. Type";
                      AccountingPeriod."Average Cost Period" := InvtSetup."Average Cost Period";
                    END;
                    IF (FirstPeriodStartDate = 0D) AND (i = 1) THEN
                      AccountingPeriod."Date Locked" := TRUE;
                    IF (AccountingPeriod."Starting Date" < FirstPeriodStartDate) AND FirstPeriodLocked THEN BEGIN
                      AccountingPeriod.Closed := TRUE;
                      AccountingPeriod."Date Locked" := TRUE;
                    END;
                    IF NOT AccountingPeriod.FIND('=') THEN
                      AccountingPeriod.INSERT;
                    FiscalYearStartDate := CALCDATE(PeriodLength,FiscalYearStartDate);
                  END;

                  AccountingPeriod.GET(FiscalYearStartDate2);
                  AccountingPeriod.UpdateAvgItems;
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF NoOfPeriods = 0 THEN BEGIN
                     NoOfPeriods := 12;
                     EVALUATE(PeriodLength,'<1M>');
                   END;
                   IF AccountingPeriod.FIND('+') THEN
                     FiscalYearStartDate := AccountingPeriod."Starting Date";
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1   ;2   ;Field     ;
                  Name=StartingDate;
                  CaptionML=[DEU=Startdatum;
                             ENU=Starting Date;
                             NLD=Begindatum;
                             NOR=Startdato;
                             SVE=Startdatum];
                  ToolTipML=[DEU=Gibt das Datum an, ab dem fÅr den Bericht oder die Stapelverarbeitung Informationen verarbeitet werden.;
                             ENU=Specifies the date from which the report or batch job processes information.;
                             NLD=Hiermee wordt de datum opgegeven vanaf wanneer het rapport of de batchverwerking informatie verwerkt.;
                             NOR=Angir datoen som rapporten eller kjõrselen behandler informasjon fra.;
                             SVE=Anger datumet frÜn vilket rapporten eller batch-jobbet bearbetar information.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=FiscalYearStartDate }

      { 2   ;2   ;Field     ;
                  Name=NoOfPeriods;
                  CaptionML=[DEU=Anzahl Perioden;
                             ENU=No. of Periods;
                             NLD=Aantal perioden;
                             NOR=Antall perioder;
                             SVE=Antal perioder];
                  ToolTipML=[DEU=Gibt an, wie viele Buchhaltungsperioden einbezogen werden sollen.;
                             ENU=Specifies how many accounting periods to include.;
                             NLD=Hiermee wordt opgegeven hoeveel boekingsperioden worden opgenomen.;
                             NOR=Angir hvor mange regnskapsperioder som skal inkluderes.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=NoOfPeriods }

      { 3   ;2   ;Field     ;
                  Name=PeriodLength;
                  CaptionML=[DEU=PeriodenlÑnge;
                             ENU=Period Length;
                             NLD=Periodelengte;
                             NOR=Periodelengde;
                             SVE=Period];
                  ToolTipML=[DEU=Gibt die Periode an, fÅr die Daten im Bericht angezeigt werden. Geben Sie z. B. "1M" fÅr einen Monat, "30D" fÅr 30 Tage, "3Q" fÅr drei Quartale oder "5Y" fÅr fÅnf Jahre ein.;
                             ENU=Specifies the period for which data is shown in the report. For example, enter "1M" for one month, "30D" for thirty days, "3Q" for three quarters, or "5Y" for five years.;
                             NLD=Hiermee wordt de periode opgegeven waarvoor gegevens worden weergegeven in het rapport, bijvoorbeeld '1M' voor ÇÇn maand, '30D' voor dertig dagen '3Q' voor drie kwartaal of '5Y' voor vijf jaar.;
                             NOR=Angir perioden som rapporten skal vise data for. Angi for eksempel 1M for Çn mÜned, 30D for 30 dager, 3K for tre kvartaler eller 5è for fem Ür.;
                             SVE=Anger perioden som data visas fîr i rapporten. Ange till exempel "1M" fîr en mÜnad, "30D" fîr 30ˇdagar, "3K" fîr tre kvartal eller "5è" fîr fem Ür.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=PeriodLength }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      CreateAndCloseQst@1000 : TextConst 'DEU=Das neue GeschÑftsjahr beginnt vor einem vorhandenen GeschÑftsjahr, das neue Jahr wird daher automatisch geschlossen.\\Mîchten Sie das GeschÑftsjahr erstellen und schlie·en?;ENU=The new fiscal year begins before an existing fiscal year, so the new year will be closed automatically.\\Do you want to create and close the fiscal year?;NLD=Het nieuwe boekjaar begint v¢¢r een bestaand boekjaar en wordt daarom automatisch afgesloten.\\Wilt u het boekjaar maken en sluiten?;NOR=Det nye regnskapsÜret begynner fõr et eksisterende regnskapsÜr, slik at det nye regnskapsÜret vil bli lukket automatisk.\\Vil du opprette og lukke regnskapsÜret?';
      CreateQst@1002 : TextConst 'DEU=Nach dem Erstellen des neuen GeschÑftsjahres kînnen Sie sein Anfangsdatum nicht Ñndern.\\Mîchten Sie das GeschÑftsjahr erstellen?;ENU=After you create the new fiscal year, you cannot change its starting date.\\Do you want to create the fiscal year?;NLD=Nadat u het nieuwe boekjaar hebt gemaakt, kunt u de begindatum ervan niet meer veranderen.\\Wilt u het boekjaar maken?;NOR=NÜr du har opprettet det nye regnskapsÜret, kan du ikke endre startdatoen.\\Vil du opprette regnskapsÜret?';
      AccountingPeriod@1005 : Record 50;
      InvtSetup@1013 : Record 313;
      NoOfPeriods@1006 : Integer;
      PeriodLength@1007 : DateFormula;
      FiscalYearStartDate@1008 : Date;
      FiscalYearStartDate2@1015 : Date;
      FirstPeriodStartDate@1009 : Date;
      FirstPeriodLocked@1011 : Boolean;
      i@1012 : Integer;
      HideDialog@1014 : Boolean;

    [External]
    PROCEDURE InitializeRequest@1(NewNoOfPeriods@1000 : Integer;NewPeriodLength@1001 : DateFormula;StartingDate@1002 : Date);
    BEGIN
      NoOfPeriods := NewNoOfPeriods;
      PeriodLength := NewPeriodLength;
      IF AccountingPeriod.FINDLAST THEN
        FiscalYearStartDate := AccountingPeriod."Starting Date"
      ELSE
        FiscalYearStartDate := StartingDate;
    END;

    [External]
    PROCEDURE HideConfirmationDialog@2(NewHideDialog@1000 : Boolean);
    BEGIN
      HideDialog := NewHideDialog;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

