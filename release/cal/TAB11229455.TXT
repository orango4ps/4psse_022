OBJECT Table 11229455 Mail Template Table Field
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               AddOrUpdateFieldCaptionElementName;
             END;

    OnModify=BEGIN
               AddOrUpdateFieldCaptionElementName;
             END;

    OnDelete=VAR
               MailTemplateTableField@1100528300 : Record 11229455;
             BEGIN
               MailTemplateTableField.SETRANGE("Mail Template No.", "Mail Template No.");
               MailTemplateTableField.SETRANGE(Level, Level);
               MailTemplateTableField.SETRANGE("Table No.", "Table No.");
               MailTemplateTableField.SETRANGE("Table Seq. No.", "Table Seq. No.");
               MailTemplateTableField.SETRANGE("Seq. No.", "Seq. No." + 1);
               MailTemplateTableField.SETRANGE("Field No.", "Field No.");
               MailTemplateTableField.SETRANGE(Type, Type::FieldCaption);
               MailTemplateTableField.DELETEALL;
             END;

    CaptionML=ENU=Mail Template Table Field;
    LookupPageID=Page11129766;
  }
  FIELDS
  {
    { 10  ;   ;Mail Template No.   ;Integer       ;TableRelation="Mail Template".No.;
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Mail Template No.;
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 11  ;   ;Mail Template Description;Text100  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Mail Template".Name WHERE (No.=FIELD(Mail Template No.)));
                                                   CaptionML=ENU=Mail Template Description;
                                                   Editable=No }
    { 20  ;   ;Level               ;Integer       ;TableRelation="Mail Template Table".Level WHERE (Mail Template No.=FIELD(Mail Template No.));
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Level;
                                                              SVE=Niv†];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 30  ;   ;Table No.           ;Integer       ;TableRelation="Mail Template Table"."Table No." WHERE (Mail Template No.=FIELD(Mail Template No.),
                                                                                                          Level=FIELD(Level));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Table Name", "Table Caption");
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Table No.;
                                                              SVE=Tabellnr];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 31  ;   ;Table Name          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Name" WHERE (Object Type=CONST(Table),
                                                                                                             Object ID=FIELD(Table No.)));
                                                   CaptionML=[ENU=Table Name;
                                                              SVE=Tabellnamn];
                                                   Editable=No }
    { 32  ;   ;Table Caption       ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table No.)));
                                                   CaptionML=[ENU=Table Caption;
                                                              SVE=Tabellrubrik];
                                                   Editable=No }
    { 40  ;   ;Table Seq. No.      ;Integer       ;DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Table Seq. No.;
                                                   Editable=No }
    { 50  ;   ;Seq. No.            ;Integer       ;DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Seq. No.;
                                                              SVE=Sekv. nr];
                                                   Editable=No }
    { 100 ;   ;Element Name        ;Text100       ;OnValidate=BEGIN
                                                                "Element Name" := DELCHR("Element Name", '<=>', ' -.,()%&');
                                                              END;

                                                   CaptionML=[ENU=Element Name;
                                                              SVE=Elementnamn] }
    { 120 ;   ;Field No.           ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table No.),
                                                                                  Class=FILTER(<>FlowFilter),
                                                                                  Enabled=CONST(Yes));
                                                   OnValidate=VAR
                                                                MailTemplateTable@1100528300 : Record 11229453;
                                                              BEGIN
                                                                IF ("Field No." <> 0) THEN BEGIN
                                                                  CALCFIELDS("Field Caption", "Field Name");
                                                                  MailTemplateTable.SETRANGE("Mail Template No.", "Mail Template No.");
                                                                  MailTemplateTable.SETRANGE(Level, Level);
                                                                  MailTemplateTable.SETRANGE("Table No.", "Table No.");
                                                                  MailTemplateTable.SETRANGE("Seq. No.", "Table Seq. No.");
                                                                  MailTemplateTable.FINDFIRST;
                                                                  VALIDATE("Element Name", STRSUBSTNO('%1_%2', MailTemplateTable."Element Prefix", "Field Caption"));
                                                                  "Function Name" := '';
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              FieldTableManagement@1100528300 : Codeunit 11012372;
                                                            BEGIN
                                                              IF (Type = Type::Value) THEN
                                                                VALIDATE("Field No.", FieldTableManagement.LookupFieldNo("Table No.", "Field No."));
                                                            END;

                                                   CaptionML=[ENU=Field No.;
                                                              SVE=F„ltnr.] }
    { 121 ;   ;Field Name          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field.FieldName WHERE (TableNo=FIELD(Table No.),
                                                                                             No.=FIELD(Field No.)));
                                                   CaptionML=[ENU=Field Name;
                                                              SVE=F„ltnamn];
                                                   Editable=No }
    { 122 ;   ;Field Caption       ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Table No.),
                                                                                                   No.=FIELD(Field No.)));
                                                   CaptionML=[ENU=Field Caption;
                                                              SVE=F„ltrubrik];
                                                   Editable=No }
    { 130 ;   ;Type                ;Option        ;DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Type;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=ENU=Value,Table Caption,Field Caption;
                                                   OptionString=Value,TableCaption,FieldCaption;
                                                   Editable=No }
    { 140 ;   ;Function Name       ;Text50        ;TableRelation="Mail Template Function".Name;
                                                   OnValidate=VAR
                                                                MailTemplate@1100528300 : Record 11229450;
                                                                MailTemplateTable@1100528301 : Record 11229453;
                                                              BEGIN
                                                                IF ("Function Name" <> '') THEN BEGIN
                                                                  "Field No." := 0;
                                                                  "Function Name" := DELCHR("Function Name", '<=>', ' -.,()%&');
                                                                  MailTemplateManagement.GetMailTemplate(MailTemplate, "Mail Template No.", '');

                                                                  MailTemplateTable.SETRANGE("Mail Template No.", "Mail Template No.");
                                                                  MailTemplateTable.SETRANGE(Level, Level);
                                                                  MailTemplateTable.SETRANGE("Table No.", "Table No.");
                                                                  MailTemplateTable.SETRANGE("Seq. No.", "Table Seq. No.");
                                                                  MailTemplateTable.FINDFIRST;
                                                                  VALIDATE("Element Name", STRSUBSTNO('%1_%2', MailTemplateTable."Element Prefix", "Function Name"));
                                                                END;
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Function Name;
                                                              SVE=Namn funktion] }
  }
  KEYS
  {
    {    ;Mail Template No.,Level,Table No.,Table Seq. No.,Seq. No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      MustBeTxt@1100528300 : TextConst 'ENU=must be %1 or %2.';
      MailTemplateManagement@1100528301 : Codeunit 11012622;

    PROCEDURE AddOrUpdateFieldCaptionElementName@1100528301();
    VAR
      MailTemplateTable@1100528301 : Record 11229453;
      MailTemplateTableField@1100528300 : Record 11229455;
    BEGIN
      IF ("Field No." = 0) THEN
        EXIT;
      CALCFIELDS("Field Caption", "Field Name");
      MailTemplateTable.GET("Mail Template No.", Level, "Table No.", "Table Seq. No.");

      IF (NOT MailTemplateTableField.GET("Mail Template No.", Level, "Table No.", "Table Seq. No.", "Seq. No." + 1)) THEN BEGIN
        MailTemplateTableField.INIT;
        MailTemplateTableField."Mail Template No." := "Mail Template No.";
        MailTemplateTableField.Level := Level;
        MailTemplateTableField."Table No." := "Table No.";
        MailTemplateTableField."Table Seq. No." := "Table Seq. No.";
        MailTemplateTableField."Seq. No." := "Seq. No." + 1;
        MailTemplateTableField.INSERT;
      END;
      MailTemplateTableField.Type := MailTemplateTableField.Type::FieldCaption;
      MailTemplateTableField."Field No." := "Field No.";
      MailTemplateTableField.VALIDATE("Element Name", STRSUBSTNO('%1_%2_%3', MailTemplateTable."Element Prefix", "Field Caption", MailTemplateTableField.Type));
      MailTemplateTableField.MODIFY;
    END;

    PROCEDURE GetLastSeqNo@1100528302() : Integer;
    VAR
      MailTemplateTableField@1100528300 : Record 11229455;
    BEGIN
      MailTemplateTableField.SETRANGE("Mail Template No.", "Mail Template No.");
      MailTemplateTableField.SETRANGE(Level, Level);
      MailTemplateTableField.SETRANGE("Table No.", "Table No.");
      MailTemplateTableField.SETRANGE("Table Seq. No.", "Table Seq. No.");
      IF (MailTemplateTableField.FINDLAST) THEN
        EXIT(MailTemplateTableField."Seq. No.");
      EXIT(0);
    END;

    LOCAL PROCEDURE "---Locals---"@1100528300();
    BEGIN
    END;

    BEGIN
    END.
  }
}

