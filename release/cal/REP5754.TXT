OBJECT Report 5754 Create Pick
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Kommissionierung erstellen;
               ENU=Create Pick;
               NLD=Pick maken;
               NOR=Opprett plukk;
               SVE=Skapa plockning];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 5444;    ;DataItem;                    ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number)
                                 WHERE(Number=CONST(1));
               OnPreDataItem=BEGIN
                               CLEAR(CreatePick);
                               CreatePick.SetValues(
                                 AssignedID,0,SortPick,1,MaxNoOfSourceDoc,MaxNoOfLines,PerZone,
                                 DoNotFillQtytoHandle,BreakbulkFilter,PerBin);
                             END;

               OnAfterGetRecord=BEGIN
                                  PickWkshLine.SETFILTER("Qty. to Handle (Base)",'>%1',0);
                                  PickWkshLineFilter.COPYFILTERS(PickWkshLine);

                                  IF PickWkshLine.FIND('-') THEN BEGIN
                                    IF PickWkshLine."Location Code" = '' THEN
                                      Location.INIT
                                    ELSE
                                      Location.GET(PickWkshLine."Location Code");
                                    REPEAT
                                      PickWkshLine.CheckBin(PickWkshLine."Location Code",PickWkshLine."To Bin Code",TRUE);
                                      TempNo := TempNo + 1;

                                      IF PerWhseDoc THEN BEGIN
                                        PickWkshLine.SETRANGE("Whse. Document Type",PickWkshLine."Whse. Document Type");
                                        PickWkshLine.SETRANGE("Whse. Document No.",PickWkshLine."Whse. Document No.");
                                      END;
                                      IF PerDestination THEN BEGIN
                                        PickWkshLine.SETRANGE("Destination Type",PickWkshLine."Destination Type");
                                        PickWkshLine.SETRANGE("Destination No.",PickWkshLine."Destination No.");
                                        SetPickFilters;
                                        PickWkshLineFilter.COPYFILTER("Destination Type",PickWkshLine."Destination Type");
                                        PickWkshLineFilter.COPYFILTER("Destination No.",PickWkshLine."Destination No.");
                                      END ELSE BEGIN
                                        PickWkshLineFilter.COPYFILTER("Destination Type",PickWkshLine."Destination Type");
                                        PickWkshLineFilter.COPYFILTER("Destination No.",PickWkshLine."Destination No.");
                                        SetPickFilters;
                                      END;
                                      PickWkshLineFilter.COPYFILTER("Whse. Document Type",PickWkshLine."Whse. Document Type");
                                      PickWkshLineFilter.COPYFILTER("Whse. Document No.",PickWkshLine."Whse. Document No.");
                                    UNTIL NOT PickWkshLine.FIND('-');
                                    CheckPickActivity;
                                  END ELSE
                                    ERROR(Text000);
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF LocationCode <> '' THEN BEGIN
                     Location.GET(LocationCode);
                     IF Location."Use ADCS" THEN
                       DoNotFillQtytoHandle := TRUE;
                   END;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 13  ;2   ;Group     ;
                  CaptionML=[DEU=Kommissionierung erstellen;
                             ENU=Create Pick;
                             NLD=Pick maken;
                             NOR=Opprett plukk;
                             SVE=Skapa plockning] }

      { 21  ;3   ;Field     ;
                  CaptionML=[DEU=Nach Logistikbeleg;
                             ENU=Per Whse. Document;
                             NLD=Per magazijndocument;
                             NOR=Per lagerdokument];
                  ToolTipML=[DEU=Gibt an, ob getrennte Kommissionierungsbelege fÅr Vorschlagszeilen mit demselben Herkunftsbeleg erstellt werden sollen.;
                             ENU=Specifies if you want to create separate pick documents for worksheet lines with the same warehouse source document.;
                             NLD=Hiermee wordt opgegeven of afzonderlijke pickdocumenten moeten worden gemaakt voor werkbladregels met hetzelfde magazijnbrondocument.;
                             NOR=Angir om du vil opprette egne plukkdokumenter for forslagslinjene med samme lagerkildedokumentet.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=PerWhseDoc }

      { 3   ;3   ;Field     ;
                  CaptionML=[DEU=Nach Deb./Kred./Lagerort;
                             ENU=Per Cust./Vend./Loc.;
                             NLD=Per klant/leverancier/vestiging;
                             NOR=Per kund/levrd/lok];
                  ToolTipML=[DEU=Gibt an, ob getrennte Kommissionierungsbelege fÅr jeden Debitor (VerkaufsauftrÑge), Kreditor (Einkaufsreklamationen) und Lagerort (UmlagerungsauftrÑge) erstellt werden sollen.;
                             ENU=Specifies if you want to create separate pick documents for each customer (sale orders), vendor (purchase return orders), and location (transfer orders).;
                             NLD=Hiermee wordt opgegeven of er afzonderlijke pickdocumenten worden gemaakt voor elke klant (verkooporders), leverancier (inkoopretourorders) en locatie (transferorders).;
                             NOR=Angir om du vil opprette egne plukkdokumenter for hver kunde (ordrer), leverandõr (bestillingsreturer) og lokasjon (overfõringsordrer).];
                  ApplicationArea=#Warehouse;
                  SourceExpr=PerDestination }

      { 6   ;3   ;Field     ;
                  CaptionML=[DEU=Nach Artikel;
                             ENU=Per Item;
                             NLD=Per artikel;
                             NOR=Per vare];
                  ToolTipML=[DEU=Gibt an, ob getrennte Kommissionierungsbelege fÅr jeden Artikel im Kommissioniervorschlag erstellt werden sollen.;
                             ENU=Specifies if you want to create separate pick documents for each item in the pick worksheet.;
                             NLD=Hiermee wordt opgegeven of er afzonderlijke pickdocumenten worden gemaakt voor elk artikel in het pickvoorstel.;
                             NOR=Angir om du vil opprette egne plukkdokumenter for hver vare i plukkforslaget.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=PerItem }

      { 23  ;3   ;Field     ;
                  CaptionML=[DEU=Nach Zone;
                             ENU=Per From Zone;
                             NLD=Per Van zone;
                             NOR=Per fra-sone];
                  ToolTipML=[DEU=Gibt an, ob getrennte Kommissionierungsbelege fÅr jede Zone erstellt werden sollen, aus der Sie Artikel entnehmen.;
                             ENU=Specifies if you want to create separate pick documents for each zone that you will take the items from.;
                             NLD=Hiermee wordt opgegeven of afzonderlijke pickdocumenten moeten worden gemaakt voor elke zone waaruit u artikelen opneemt.;
                             NOR=Angir om du vil opprette egne plukkdokumenter for hver sone du vil hente varene fra.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=PerZone }

      { 8   ;3   ;Field     ;
                  CaptionML=[DEU=Nach Lagerplatznr.;
                             ENU=Per Bin;
                             NLD=Per opslaglocatie;
                             NOR=Per hylle];
                  ToolTipML=[DEU=Gibt an, ob getrennte Kommissionierungsbelege fÅr jeden Lagerplatz erstellt werden sollen, aus dem Sie Artikel entnehmen.;
                             ENU=Specifies if you want to create separate pick documents for each bin that you will take the items from.;
                             NLD=Hiermee wordt opgegeven of er afzonderlijke pickdocumenten worden gemaakt voor elke opslaglocatie waaruit u de artikelen opneemt.;
                             NOR=Angir om du vil opprette egne plukkdokumenter for hver hylle du vil hente varene fra.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=PerBin }

      { 10  ;3   ;Field     ;
                  CaptionML=[DEU=Nach FÑlligkeitsdatum;
                             ENU=Per Due Date;
                             NLD=Per vervaldatum;
                             NOR=Per forfallsdato];
                  ToolTipML=[DEU=Gibt an, ob getrennte Kommissionierungsbelege fÅr Herkunftsbelege mit demselben FÑlligkeitsdatum erstellt werden sollen.;
                             ENU=Specifies if you want to create separate pick documents for source documents that have the same due date.;
                             NLD=Hiermee wordt opgegeven of er afzonderlijke pickdocumenten worden gemaakt voor brondocumenten met dezelfde vervaldatum.;
                             NOR=Angir om du vil opprette egne plukkdokumenter for kildedokumenter som har samme forfallsdato.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=PerDate }

      { 1   ;2   ;Field     ;
                  CaptionML=[DEU=Max. Anzahl Kommissionierzeilen;
                             ENU=Max. No. of Pick Lines;
                             NLD=Max. aantal pickregels;
                             NOR=Maks ant. plukklinjer];
                  ToolTipML=[DEU=Gibt an, ob Kommissionierungsbelege erstellt werden sollen, die nicht mehr als die angegebene Anzahl von Zeilen in den einzelnen Dokumenten haben.;
                             ENU=Specifies if you want to create pick documents that have no more than the specified number of lines in each document.;
                             NLD=Hiermee wordt opgegeven of er pickdocumenten worden gemaakt die niet meer dan het opgegeven aantal regels in elk document bevatten.;
                             NOR=Angir om du vil opprette plukkdokumenter som har ikke mer enn det angitte antallet linjer i hvert dokument.];
                  ApplicationArea=#Warehouse;
                  BlankZero=Yes;
                  SourceExpr=MaxNoOfLines;
                  MultiLine=Yes }

      { 12  ;2   ;Field     ;
                  CaptionML=[DEU=Max. Anzahl Komm.-Herkunftsbelege;
                             ENU=Max. No. of Pick Source Docs.;
                             NLD=Max. aantal pickbrondocumenten;
                             NOR=Maks. ant. plukkildedok.];
                  ToolTipML=[DEU=Gibt an, ob Kommissionierungsbelege erstellt werden sollen, die jeweils nicht mehr als die angegebene Anzahl von Herkunftsbelegen abdecken.;
                             ENU=Specifies if you want to create pick documents that each cover no more than the specified number of source documents.;
                             NLD=Hiermee wordt opgegeven of er pickdocumenten worden gemaakt die maximaal op het opgegeven aantal brondocumenten betrekking hebben.;
                             NOR=Angir om du vil opprette plukkdokumenter som hver for seg ikke omfatter mer enn det angitte antallet kildedokumenter.];
                  ApplicationArea=#Warehouse;
                  BlankZero=Yes;
                  SourceExpr=MaxNoOfSourceDoc;
                  MultiLine=Yes }

      { 16  ;2   ;Field     ;
                  CaptionML=[DEU=Zugewiesene Benutzer-ID;
                             ENU=Assigned User ID;
                             NLD=Toegewezen gebruikers-id;
                             NOR=Tilordnet bruker-ID;
                             SVE=Tilldelat anvÑndar-ID];
                  ToolTipML=[DEU=Gibt die ID des zugewiesenen Benutzers fÅr die Kommissionieranweisung an.;
                             ENU=Specifies the ID of the assigned user to perform the pick instruction.;
                             NLD=Hiermee wordt de id opgegeven van de toegewezen gebruiker die de pickinstructie moet uitvoeren.;
                             NOR=Angir ID-en for den tilordnede brukeren som skal utfõre plukkinstruksjonen.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=AssignedID;
                  TableRelation="Warehouse Employee";
                  OnValidate=VAR
                               WhseEmployee@1000 : Record 7301;
                             BEGIN
                               IF AssignedID <> '' THEN
                                 WhseEmployee.GET(AssignedID,LocationCode);
                             END;

                  OnLookup=VAR
                             WhseEmployee@1000 : Record 7301;
                             LookupWhseEmployee@1001 : Page 7348;
                           BEGIN
                             WhseEmployee.SETCURRENTKEY("Location Code");
                             WhseEmployee.SETRANGE("Location Code",LocationCode);
                             LookupWhseEmployee.LOOKUPMODE(TRUE);
                             LookupWhseEmployee.SETTABLEVIEW(WhseEmployee);
                             IF LookupWhseEmployee.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               LookupWhseEmployee.GETRECORD(WhseEmployee);
                               AssignedID := WhseEmployee."User ID";
                             END;
                           END;
                            }

      { 18  ;2   ;Field     ;
                  CaptionML=[DEU=Sortiermethode fÅr Kommissionierzeilen;
                             ENU=Sorting Method for Pick Lines;
                             NLD=Sorteringsmethode voor pickregels;
                             NOR=Sorteringsmetode for plukklinjer];
                  ToolTipML=[DEU=Gibt an, dass eine Auswahl aus den verfÅgbaren Optionen getroffen werden soll, um Zeilen im erstellten Kommissionierungsbeleg zu sortieren.;
                             ENU=Specifies that you want to select from the available options to sort lines in the created pick document.;
                             NLD=Hiermee wordt opgegeven dat u een selectie wilt maken uit de beschikbare opties om regels in het gemaakte pickdocument te sorteren.;
                             NOR=Angir at du vil velge fra de tilgjengelige alternativene for Ü sortere linjene i det opprettede plukkdokumentet.];
                  OptionCaptionML=[DEU=" ,Artikel,Beleg,Regalnummer oder Lagerplatz,FÑlligkeitsdatum,Ziel,LagerplatzprioritÑt,Aktionsart";
                                   ENU=" ,Item,Document,Shelf/Bin No.,Due Date,Destination,Bin Ranking,Action Type";
                                   NLD=" ,Artikel,Document,Schap-/opslaglocatienr.,Vervaldatum,Bestemming,Opslaglocatievolgorde,Actiesoort";
                                   NOR=" ,Vare,Dokument,Hyllenr.,Forfallsdato,MÜl,Hyllerangering,Handlingstype"];
                  ApplicationArea=#Warehouse;
                  SourceExpr=SortPick;
                  MultiLine=Yes }

      { 26  ;2   ;Field     ;
                  CaptionML=[DEU=Gebindeanbruchsfilter verw.;
                             ENU=Set Breakbulk Filter;
                             NLD=Breakbulkfilter plaatsen;
                             NOR=Angi anbrekksfilter];
                  ToolTipML=[DEU=Gibt an, ob die Gebindeanbruch-Kommissionierzwischenzeilen angezeigt werden sollen, wenn eine grî·ere Ma·einheit in eine kleinere Ma·einheit konvertiert wird und vollstÑndig entnommen wird.;
                             ENU=Specifies if you do not want to view the intermediate breakbulk pick lines, when a larger unit of measure is converted to a smaller unit of measure and completely picked.;
                             NLD=Hiermee wordt opgegeven of de tussenliggende breakbulkpickregels worden verborgen wanneer een grotere eenheid wordt omgezet in een kleinere eenheid en volledig wordt gepickt.;
                             NOR=Angir om du ikke vil vise de midlertidige anbrekksplukklinjene nÜr en stõrre mÜleenhet konverteres til en mindre enhet og plukkes ferdig.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=BreakbulkFilter }

      { 24  ;2   ;Field     ;
                  CaptionML=[DEU=Bewegungsmenge nicht ausfÅllen;
                             ENU=Do Not Fill Qty. to Handle;
                             NLD=Te verwerken aantal niet opvullen;
                             NOR=Ikke fyll ut antall som skal hÜndteres];
                  ToolTipML=[DEU=Gibt an, ob Sie das Feld "Zu bearbeitende Menge" in den erstellten Kommissionierzeilen leer lassen mîchten.;
                             ENU=Specifies if you want to leave the Quantity to Handle field in the created pick lines empty.;
                             NLD=Hiermee wordt opgegeven of u het veld Werk. verwerkt aantal op de gemaakte pickregels leeg wilt laten.;
                             NOR=Angir om du vil la antallet i feltet Antall som skal hÜndteres i de opprettede plukklinjene, vëre tomt.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=DoNotFillQtytoHandle }

      { 14  ;2   ;Field     ;
                  CaptionML=[DEU=Kommissionierschein drucken;
                             ENU=Print Pick;
                             NLD=Pick afdrukken;
                             NOR=Skriv ut plukk];
                  ToolTipML=[DEU=Gibt an, dass die Kommissionierungsbelege gedruckt werden sollen, wenn diese erstellt werden.;
                             ENU=Specifies that you want to print the pick documents when they are created.;
                             NLD=Hiermee wordt opgegeven dat de pickdocumenten worden afgedrukt wanneer deze worden gemaakt.;
                             NOR=Angir at du vil skrive ut plukkdokumentene nÜr de opprettes.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=PrintPick }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Es gibt nichts zu bewegen.;ENU=There is nothing to handle.;NLD=Niets te verwerken.;NOR=Det finnes ingenting Ü hÜndtere.;SVE=Det finns inget att hantera.';
      Text001@1001 : TextConst 'DEU=Kommissionierungsnr. %1 wurde erstellt.;ENU=Pick activity no. %1 has been created.;NLD=Pickactiviteitsnr. %1 is gemaakt.;NOR=Plukkaktivitet nr. %1 er opprettet.';
      Text002@1002 : TextConst 'DEU=Kommissionierungsnummern %1 bis %2 wurden erstellt.;ENU=Pick activities no. %1 to %2 have been created.;NLD=Pickactiviteitnrs. %1 tot %2 zijn gemaakt.;NOR=Plukkaktiviteter nr. %1 til %2 er opprettet.';
      Location@1006 : Record 14;
      PickWkshLine@1004 : Record 7326;
      PickWkshLineFilter@1005 : Record 7326;
      Cust@1011 : Record 18;
      CreatePick@1038 : Codeunit 7312;
      LocationCode@1009 : Code[10];
      AssignedID@1017 : Code[50];
      FirstPickNo@1018 : Code[20];
      FirstSetPickNo@1010 : Code[20];
      LastPickNo@1019 : Code[20];
      MaxNoOfLines@1020 : Integer;
      MaxNoOfSourceDoc@1021 : Integer;
      TempNo@1024 : Integer;
      SortPick@1029 : ' ,Item,Document,Shelf No.,Due Date,Destination,Bin Ranking,Action Type';
      PerDestination@1030 : Boolean;
      PerItem@1031 : Boolean;
      PerZone@1037 : Boolean;
      PerBin@1032 : Boolean;
      PerWhseDoc@1027 : Boolean;
      PerDate@1033 : Boolean;
      PrintPick@1034 : Boolean;
      DoNotFillQtytoHandle@1003 : Boolean;
      Text003@1008 : TextConst 'DEU="Sie kînnen eine Kommissionierung nur fÅr die verfÅgbare Menge in %1 %2 = %3,%4 = %5,%6 = %7,%8 = %9 erstellen.";ENU="You can create a Pick only for the available quantity in %1 %2 = %3,%4 = %5,%6 = %7,%8 = %9.";NLD="U kunt alleen een pick maken voor het beschikbare aantal in %1 %2 = %3,%4 = %5,%6 = %7,%8 = %9.";NOR="Du kan bare opprette et plukk for det disponible antallet i %1 %2 = %3,%4 = %5,%6 = %7,%8 = %9."';
      BreakbulkFilter@1007 : Boolean;
      NothingToHandleErr@1012 : TextConst 'DEU=Es gibt nichts zu bewegen. %1.;ENU=There is nothing to handle. %1.;NLD=Niets te verwerken. %1.;NOR=Det finnes ingenting Ü hÜndtere. %1.';

    LOCAL PROCEDURE CreateTempLine@1();
    VAR
      PickWhseActivHeader@1001 : Record 5766;
      TempWhseItemTrkgLine@1002 : TEMPORARY Record 6550;
      ItemTrackingMgt@1003 : Codeunit 6500;
      PickQty@1000 : Decimal;
      PickQtyBase@1005 : Decimal;
      TempMaxNoOfSourceDoc@1004 : Integer;
      OldFirstSetPickNo@1006 : Code[20];
      TotalQtyPickedBase@1007 : Decimal;
    BEGIN
      PickWkshLine.LOCKTABLE;
      REPEAT
        IF Location."Bin Mandatory" AND
           (NOT Location."Always Create Pick Line")
        THEN
          IF PickWkshLine.CalcAvailableQtyBase < PickWkshLine."Qty. to Handle" THEN
            ERROR(
              Text003,
              PickWkshLine.TABLECAPTION,PickWkshLine.FIELDCAPTION("Worksheet Template Name"),
              PickWkshLine."Worksheet Template Name",PickWkshLine.FIELDCAPTION(Name),
              PickWkshLine.Name,PickWkshLine.FIELDCAPTION("Location Code"),
              PickWkshLine."Location Code",PickWkshLine.FIELDCAPTION("Line No."),
              PickWkshLine."Line No.");

        PickWkshLine.TESTFIELD("Qty. per Unit of Measure");
        CreatePick.SetWhseWkshLine(PickWkshLine,TempNo);
        CASE PickWkshLine."Whse. Document Type" OF
          PickWkshLine."Whse. Document Type"::Shipment:
            CreatePick.SetTempWhseItemTrkgLine(
              PickWkshLine."Whse. Document No.",DATABASE::"Warehouse Shipment Line",'',0,
              PickWkshLine."Whse. Document Line No.",PickWkshLine."Location Code");
          PickWkshLine."Whse. Document Type"::Assembly:
            CreatePick.SetTempWhseItemTrkgLine(
              PickWkshLine."Whse. Document No.",DATABASE::"Assembly Line",'',0,
              PickWkshLine."Whse. Document Line No.",PickWkshLine."Location Code");
          PickWkshLine."Whse. Document Type"::"Internal Pick":
            CreatePick.SetTempWhseItemTrkgLine(
              PickWkshLine."Whse. Document No.",DATABASE::"Whse. Internal Pick Line",'',0,
              PickWkshLine."Whse. Document Line No.",PickWkshLine."Location Code");
          PickWkshLine."Whse. Document Type"::Production:
            CreatePick.SetTempWhseItemTrkgLine(
              PickWkshLine."Source No.",PickWkshLine."Source Type",'',PickWkshLine."Source Line No.",
              PickWkshLine."Source Subline No.",PickWkshLine."Location Code");
          ELSE // Movement Worksheet Line
            CreatePick.SetTempWhseItemTrkgLine(
              PickWkshLine.Name,DATABASE::"Prod. Order Component",PickWkshLine."Worksheet Template Name",
              0,PickWkshLine."Line No.",PickWkshLine."Location Code");
        END;

        PickQty := PickWkshLine."Qty. to Handle";
        PickQtyBase := PickWkshLine."Qty. to Handle (Base)";
        OnAfterSetQuantityToPick(PickWkshLine,PickQty,PickQtyBase);
        IF (PickQty > 0) AND
           (PickWkshLine."Destination Type" = PickWkshLine."Destination Type"::Customer)
        THEN BEGIN
          PickWkshLine.TESTFIELD("Destination No.");
          Cust.GET(PickWkshLine."Destination No.");
          Cust.CheckBlockedCustOnDocs(Cust,PickWkshLine."Source Document",FALSE,FALSE);
        END;

        CreatePick.SetCalledFromWksh(TRUE);

        WITH PickWkshLine DO
          CreatePick.CreateTempLine("Location Code","Item No.","Variant Code",
            "Unit of Measure Code",'',"To Bin Code","Qty. per Unit of Measure",PickQty,PickQtyBase);

        TotalQtyPickedBase := CreatePick.GetActualQtyPickedBase;

        // Update/delete lines
        PickWkshLine."Qty. to Handle (Base)" := PickWkshLine.CalcBaseQty(PickWkshLine."Qty. to Handle");
        IF PickWkshLine."Qty. (Base)" =
           PickWkshLine."Qty. Handled (Base)" + TotalQtyPickedBase
        THEN
          PickWkshLine.DELETE(TRUE)
        ELSE BEGIN
          PickWkshLine."Qty. Handled" := PickWkshLine."Qty. Handled" + PickWkshLine.CalcQty(TotalQtyPickedBase);
          PickWkshLine."Qty. Handled (Base)" := PickWkshLine.CalcBaseQty(PickWkshLine."Qty. Handled");
          PickWkshLine."Qty. Outstanding" := PickWkshLine.Quantity - PickWkshLine."Qty. Handled";
          PickWkshLine."Qty. Outstanding (Base)" := PickWkshLine.CalcBaseQty(PickWkshLine."Qty. Outstanding");
          PickWkshLine."Qty. to Handle" := 0;
          PickWkshLine."Qty. to Handle (Base)" := 0;
          PickWkshLine.MODIFY;
        END;
      UNTIL PickWkshLine.NEXT = 0;

      OldFirstSetPickNo := FirstSetPickNo;
      CreatePick.CreateWhseDocument(FirstSetPickNo,LastPickNo,FALSE);
      IF FirstSetPickNo = OldFirstSetPickNo THEN
        EXIT;

      IF FirstPickNo = '' THEN
        FirstPickNo := FirstSetPickNo;
      CreatePick.ReturnTempItemTrkgLines(TempWhseItemTrkgLine);
      ItemTrackingMgt.UpdateWhseItemTrkgLines(TempWhseItemTrkgLine);
      COMMIT;

      TempMaxNoOfSourceDoc := MaxNoOfSourceDoc;
      PickWhseActivHeader.SETRANGE(Type,PickWhseActivHeader.Type::Pick);
      PickWhseActivHeader.SETRANGE("No.",FirstSetPickNo,LastPickNo);
      PickWhseActivHeader.FIND('-');
      REPEAT
        IF SortPick > 0 THEN
          PickWhseActivHeader.SortWhseDoc;
        COMMIT;
        IF PrintPick THEN BEGIN
          REPORT.RUN(REPORT::"Picking List",FALSE,FALSE,PickWhseActivHeader);
          TempMaxNoOfSourceDoc -= 1;
        END;
      UNTIL ((PickWhseActivHeader.NEXT = 0) OR (TempMaxNoOfSourceDoc = 0));
    END;

    [External]
    PROCEDURE SetWkshPickLine@4(VAR PickWkshLine2@1000 : Record 7326);
    BEGIN
      PickWkshLine.COPYFILTERS(PickWkshLine2);
      LocationCode := PickWkshLine2."Location Code";
    END;

    [External]
    PROCEDURE GetResultMessage@2() ReturnValue : Boolean;
    BEGIN
      IF FirstPickNo <> '' THEN
        IF FirstPickNo = LastPickNo THEN
          MESSAGE(Text001,FirstPickNo)
        ELSE
          MESSAGE(Text002,FirstPickNo,LastPickNo);
      ReturnValue := FirstPickNo <> '';
      OnAfterGetResultMessage(ReturnValue);
      EXIT(ReturnValue);
    END;

    [External]
    PROCEDURE InitializeReport@3(AssignedID2@1013 : Code[50];MaxNoOfLines2@1010 : Integer;MaxNoOfSourceDoc2@1009 : Integer;SortPick2@1007 : ' ,Item,Document,Shelf/Bin No.,Due Date,Ship-To,Bin Ranking,Action Type';PerDestination2@1006 : Boolean;PerItem2@1005 : Boolean;PerZone2@1004 : Boolean;PerBin2@1003 : Boolean;PerWhseDoc2@1002 : Boolean;PerDate2@1001 : Boolean;PrintPick2@1000 : Boolean;DoNotFillQtytoHandle2@1008 : Boolean;BreakbulkFilter2@1011 : Boolean);
    BEGIN
      AssignedID := AssignedID2;
      MaxNoOfLines := MaxNoOfLines2;
      MaxNoOfSourceDoc := MaxNoOfSourceDoc2;
      SortPick := SortPick2;
      PerDestination := PerDestination2;
      PerItem := PerItem2;
      PerZone := PerZone2;
      PerBin := PerBin2;
      PerWhseDoc := PerWhseDoc2;
      PerDate := PerDate2;
      PrintPick := PrintPick2;
      DoNotFillQtytoHandle := DoNotFillQtytoHandle2;
      BreakbulkFilter := BreakbulkFilter2;
    END;

    LOCAL PROCEDURE CheckPickActivity@5();
    VAR
      IsHandled@1000 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeCheckPickActivity(IsHandled);
      IF IsHandled THEN
        EXIT;

      IF FirstPickNo = '' THEN
        ERROR(NothingToHandleErr,CreatePick.GetCannotBeHandledReason);
    END;

    LOCAL PROCEDURE SetPickFilters@6();
    BEGIN
      IF PerItem THEN BEGIN
        PickWkshLine.SETRANGE("Item No.",PickWkshLine."Item No.");
        IF PerBin THEN
          SetPerBinFilters
        ELSE BEGIN
          IF NOT Location."Bin Mandatory" THEN
            PickWkshLineFilter.COPYFILTER("Shelf No.",PickWkshLine."Shelf No.");
          SetPerDateFilters;
        END;
        PickWkshLineFilter.COPYFILTER("Item No.",PickWkshLine."Item No.");
      END ELSE BEGIN
        PickWkshLineFilter.COPYFILTER("Item No.",PickWkshLine."Item No.");
        IF PerBin THEN
          SetPerBinFilters
        ELSE BEGIN
          IF NOT Location."Bin Mandatory" THEN
            PickWkshLineFilter.COPYFILTER("Shelf No.",PickWkshLine."Shelf No.");
          SetPerDateFilters;
        END;
      END;
    END;

    LOCAL PROCEDURE SetPerBinFilters@7();
    BEGIN
      IF NOT Location."Bin Mandatory" THEN
        PickWkshLine.SETRANGE("Shelf No.",PickWkshLine."Shelf No.");
      IF PerDate THEN BEGIN
        PickWkshLine.SETRANGE("Due Date",PickWkshLine."Due Date");
        CreateTempLine;
        PickWkshLineFilter.COPYFILTER("Due Date",PickWkshLine."Due Date");
      END ELSE BEGIN
        PickWkshLineFilter.COPYFILTER("Due Date",PickWkshLine."Due Date");
        CreateTempLine;
      END;
      IF NOT Location."Bin Mandatory" THEN
        PickWkshLineFilter.COPYFILTER("Shelf No.",PickWkshLine."Shelf No.");
    END;

    LOCAL PROCEDURE SetPerDateFilters@10();
    BEGIN
      IF PerDate THEN BEGIN
        PickWkshLine.SETRANGE("Due Date",PickWkshLine."Due Date");
        CreateTempLine;
        PickWkshLineFilter.COPYFILTER("Due Date",PickWkshLine."Due Date");
      END ELSE BEGIN
        PickWkshLineFilter.COPYFILTER("Due Date",PickWkshLine."Due Date");
        CreateTempLine;
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterGetResultMessage@11(VAR ReturnValue@1000 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSetQuantityToPick@8(VAR WhseWorksheetLine@1000 : Record 7326;VAR PickQty@1001 : Decimal;VAR PickQtyBase@1002 : Decimal);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCheckPickActivity@12(VAR IsHandled@1000 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

