OBJECT Codeunit 11020207 Consolidated Contact Data
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      ConsContactData@1100485000 : Record 11020290;
      FPSLicenseManagement@1100527400 : Codeunit 11229289;

    PROCEDURE ShowTryOut@1100485000(ContactRec@1100485004 : Record 5050);
    VAR
      TryOutRec@1100485002 : Record 11012750;
      TryOutLineRec@1100485001 : Record 11012755;
      ConsTryOut@1100485000 : TEMPORARY Record 11020291;
    BEGIN
      ConsContactData.SETRANGE(Table,ConsContactData.Table::"Try-Out");
      IF ConsContactData.FIND('-') THEN BEGIN
        REPEAT
          TryOutLineRec.CHANGECOMPANY(ConsContactData.Company);
          IF FPSLicenseManagement.LicenseAndReadPermissionForTable(TryOutLineRec) THEN BEGIN //kk, 13-05-11: M27123
            TryOutLineRec.SETCURRENTKEY("Contact No."); //call 28569
            TryOutLineRec.SETRANGE("Contact No.",ContactRec."No.");
            IF TryOutLineRec.FIND('-') THEN BEGIN
              TryOutRec.CHANGECOMPANY(ConsContactData.Company);
              REPEAT
                IF TryOutRec.GET(TryOutLineRec."Try-out No.") THEN BEGIN
                  IF NOT ConsTryOut.GET(ConsContactData.Company,TryOutRec.Code) THEN BEGIN
                    ConsTryOut.INIT;
                    ConsTryOut.Company := ConsContactData.Company;
                    ConsTryOut.TRANSFERFIELDS(TryOutRec);
                    ConsTryOut.INSERT;
                  END;
                END;
              UNTIL TryOutLineRec.NEXT = 0;
            END;
          END; //kk, 13-05-11: M27123
        UNTIL ConsContactData.NEXT = 0;
      END;
      PAGE.RUN(0,ConsTryOut);
    END;

    PROCEDURE ShowTodo@1100525002(Contact@1100525000 : Record 5050);
    VAR
      ConsolidatedTodos@1100525001 : TEMPORARY Record 11229458;
    BEGIN
      SetConsTodoTemp(ConsolidatedTodos, Contact."No.");
      PAGE.RUN(0, ConsolidatedTodos);
    END;

    PROCEDURE ShowInteractEntries@1100485001(ContactRec@1100485000 : Record 5050);
    VAR
      ConsLogEntries@1100485003 : TEMPORARY Record 11020292;
      InterActLogEntry@1100485002 : Record 11072027;
      AttachmentRec@1100485001 : Record 11072030;
    BEGIN
      ConsContactData.SETRANGE(Table,ConsContactData.Table::"Interaction Log Entries");
      IF ConsContactData.FIND('-') THEN BEGIN
        REPEAT
          InterActLogEntry.CHANGECOMPANY(ConsContactData.Company);
          IF FPSLicenseManagement.LicenseAndReadPermissionForTable(InterActLogEntry) THEN BEGIN //kk, 13-05-11: M27123
            IF ContactRec.Type = ContactRec.Type::Company THEN BEGIN
              InterActLogEntry.SETCURRENTKEY("Contact Company No."); //call 28569
              InterActLogEntry.SETRANGE("Contact Company No.",ContactRec."No.")
            END ELSE BEGIN
              InterActLogEntry.SETCURRENTKEY("Contact No."); //call 28569
              InterActLogEntry.SETRANGE("Contact No.",ContactRec."No.");
            END;
            IF InterActLogEntry.FIND('-') THEN BEGIN
              REPEAT
                ConsLogEntries.INIT;
                ConsLogEntries.Company := ConsContactData.Company;
                ConsLogEntries.TRANSFERFIELDS(InterActLogEntry);

                InterActLogEntry.CALCFIELDS("Contact Name","Contact Company Name","Project Description","House Model");
                ConsLogEntries."Contact Name" := InterActLogEntry."Contact Name";
                ConsLogEntries."Contact Company Name" := InterActLogEntry."Contact Company Name";
                ConsLogEntries."Project Description" := InterActLogEntry."Project Description";
                ConsLogEntries."House Model" := InterActLogEntry."House Model";

                IF InterActLogEntry."Attachment No." <> 0 THEN BEGIN
                  AttachmentRec.CHANGECOMPANY(ConsContactData.Company);
                  IF AttachmentRec.GET(InterActLogEntry."Attachment No.") THEN BEGIN
                    AttachmentRec.CALCFIELDS("Attachment File");
                    ConsLogEntries."Attachment File" := AttachmentRec."Attachment File";
                    ConsLogEntries."File Extension" := AttachmentRec."File Extension";
                  END;
                END;
                ConsLogEntries.INSERT;
              UNTIL InterActLogEntry.NEXT = 0;
            END;
          END; //kk, 13-05-11: M27123
        UNTIL ConsContactData.NEXT = 0;
      END;
      PAGE.RUN(0,ConsLogEntries);
    END;

    PROCEDURE ShowActivities@1100485002(ContactRec@1100485000 : Record 5050);
    VAR
      ToDoRec@1100485001 : Record 5080;
      ConsActRec@1100485002 : TEMPORARY Record 11020293;
    BEGIN
      ConsContactData.SETRANGE(Table,ConsContactData.Table::"Try-Out");
      IF ConsContactData.FINDSET THEN BEGIN
        REPEAT
          ToDoRec.CHANGECOMPANY(ConsContactData.Company);
          IF FPSLicenseManagement.LicenseAndReadPermissionForTable(ToDoRec) THEN BEGIN //kk, 13-05-11: M27123
            IF ContactRec.Type = ContactRec.Type::Company THEN BEGIN
              ToDoRec.SETCURRENTKEY("Contact Company No."); //call 28569
              ToDoRec.SETRANGE("Contact Company No.",ContactRec."No.")
            END ELSE
              ToDoRec.SETRANGE("Contact No.",ContactRec."No.");
            IF ToDoRec.FINDSET THEN BEGIN
              REPEAT
                ConsActRec.INIT;
                ConsActRec.Company := ConsContactData.Company;
                ConsActRec.TRANSFERFIELDS(ToDoRec);
                ConsActRec."No." := ToDoRec."No.";

                ToDoRec.CALCFIELDS("Description Try-out","Contact Name","Team Name","Salesperson Name","Contact Company Name",
                                   "Opportunity Description","Description Try-out Quote","Name Employee","Name Sales Person",
                                   "Description Campaign");

                ConsActRec."Contact Name" := ToDoRec."Contact Name";
                ConsActRec."Team Name" := ToDoRec."Team Name";
                ConsActRec."Salesperson Name" := ToDoRec."Salesperson Name";
                ConsActRec."Contact Company Name" := ToDoRec."Contact Company Name";
                ConsActRec."Opportunity Description" := ToDoRec."Opportunity Description";
                ConsActRec."Description Try-out" := ToDoRec."Description Try-out";
                ConsActRec."Description Try-out Quote" := ToDoRec."Description Try-out Quote";
                ConsActRec."Name Employee" := ToDoRec."Name Employee";
                ConsActRec."Naam Sales Person" := ToDoRec."Name Sales Person";
                ConsActRec."Description Campaign" := ToDoRec."Description Campaign";

                ConsActRec.INSERT;
              UNTIL ToDoRec.NEXT = 0;
            END;
          END; //kk, 13-05-11: M27123
        UNTIL ConsContactData.NEXT = 0;
      END;
      PAGE.RUN(0,ConsActRec);
    END;

    PROCEDURE ShowCustomerandVendor@1100485003(ContactRec@1100485004 : Record 5050);
    VAR
      ConsVendCustRec@1100485001 : TEMPORARY Record 11020294;
      ContBussRelRec@1100485000 : Record 5054;
      CustomerRec@1100485002 : Record 18;
      VendorRec@1100485003 : Record 23;
      EmployeeRec@1100485005 : Record 5200;
    BEGIN
      ConsContactData.SETRANGE(Table,ConsContactData.Table::"Contact Relations");
      IF ConsContactData.FIND('-') THEN BEGIN
        REPEAT
          ContBussRelRec.CHANGECOMPANY(ConsContactData.Company);
          IF FPSLicenseManagement.LicenseAndReadPermissionForTable(ContBussRelRec) THEN BEGIN //kk, 13-05-11: M27123
            ContBussRelRec.SETRANGE("Contact No.",ContactRec."No.");
            IF ContBussRelRec.FINDSET THEN BEGIN
              REPEAT
                ConsVendCustRec.INIT;
                ConsVendCustRec.Company := ConsContactData.Company;
                ConsVendCustRec.TRANSFERFIELDS(ContBussRelRec);
                CASE ConsVendCustRec."Link to Table" OF
                  ConsVendCustRec."Link to Table"::Customer:
                    BEGIN
                      CustomerRec.CHANGECOMPANY(ConsContactData.Company);
                      IF CustomerRec.GET(ConsVendCustRec."No.") THEN BEGIN
                        ConsVendCustRec.Name := CustomerRec.Name;
                        ConsVendCustRec.Address := CustomerRec.Address;
                        ConsVendCustRec."Address 2" := CustomerRec."Address 2";
                        ConsVendCustRec."Post Code" := CustomerRec."Post Code";
                        ConsVendCustRec.City := CustomerRec.City;
                        ConsVendCustRec.Phone := CustomerRec."Phone No.";
                        ConsVendCustRec.Fax := CustomerRec."Fax No.";
                        CustomerRec.CALCFIELDS("Balance (LCY)");
                        ConsVendCustRec."Balance Due" := CustomerRec."Balance (LCY)";
                      END;
                    END;
                  ConsVendCustRec."Link to Table"::Vendor:
                    BEGIN
                      VendorRec.CHANGECOMPANY(ConsContactData.Company);
                      IF VendorRec.GET(ConsVendCustRec."No.") THEN BEGIN
                        ConsVendCustRec.Name :=  VendorRec.Name;
                        ConsVendCustRec.Address :=  VendorRec.Address;
                        ConsVendCustRec."Address 2" :=  VendorRec."Address 2";
                        ConsVendCustRec."Post Code" :=  VendorRec."Post Code";
                        ConsVendCustRec.City :=  VendorRec.City;
                        ConsVendCustRec.Phone :=  VendorRec."Phone No.";
                        ConsVendCustRec.Fax :=  VendorRec."Fax No.";
                        VendorRec.CALCFIELDS("Balance (LCY)");
                        ConsVendCustRec."Balance Due" := VendorRec."Balance (LCY)";
                      END;
                    END;
                  ConsVendCustRec."Link to Table"::Employee:
                    BEGIN
                      EmployeeRec.CHANGECOMPANY(ConsContactData.Company);
                      IF EmployeeRec.GET(ConsVendCustRec."No.") THEN BEGIN
                        ConsVendCustRec.Name :=  EmployeeRec.FullName;
                        ConsVendCustRec.Address :=  EmployeeRec.Address;
                        ConsVendCustRec."Address 2" :=  EmployeeRec."Address 2";
                        ConsVendCustRec."Post Code" :=  EmployeeRec."Post Code";
                        ConsVendCustRec.City :=  EmployeeRec.City;
                        ConsVendCustRec.Phone :=  EmployeeRec."Phone No.";
                        ConsVendCustRec.Fax :=  EmployeeRec."Fax No.";
                      END;
                    END;
                END;
                ContBussRelRec.CALCFIELDS("Business Relation Description");
                ConsVendCustRec."Business Relation Description" := ContBussRelRec."Business Relation Description";
                ConsVendCustRec.INSERT;
              UNTIL ContBussRelRec.NEXT = 0;
            END;
          END; //kk, 13-05-11: M27123
        UNTIL ConsContactData.NEXT = 0;
      END;
      PAGE.RUN(0,ConsVendCustRec);
    END;

    PROCEDURE ShowEstimates@1100485004(ContactRec@1100485000 : Record 5050);
    VAR
      EstimateRec@1100485001 : Record 11012151;
      ConsEstRec@1100485002 : TEMPORARY Record 11020298;
    BEGIN
      ConsContactData.SETRANGE(Table,ConsContactData.Table::Estimates);
      IF ConsContactData.FIND('-') THEN BEGIN
        REPEAT
          EstimateRec.CHANGECOMPANY(ConsContactData.Company);
          IF FPSLicenseManagement.LicenseAndReadPermissionForTable(EstimateRec) THEN BEGIN //kk, 13-05-11: M27123
            EstimateRec.SETRANGE("Contact No.",ContactRec."No.");
            IF EstimateRec.FIND('-') THEN BEGIN
              REPEAT
                ConsEstRec.INIT;
                ConsEstRec.Company := ConsContactData.Company;
                ConsEstRec.TRANSFERFIELDS(EstimateRec);
                EstimateRec.CALCFIELDS("Hours (Cost)","Labor Cost Amount","Material Cost Amount","Subcontracting Cost Amount","Plant Cost Amount","Consultancy Firm Name",
                                       "Contact Name","Purchaser Name","Name Estimator","Estimate Model Total",
                                       "Contact Person Name");
                ConsEstRec.Hours := EstimateRec."Hours (Cost)";
                ConsEstRec.Labor := EstimateRec."Labor Cost Amount";
                ConsEstRec.Material := EstimateRec."Material Cost Amount";
                ConsEstRec.Subcontracting := EstimateRec."Subcontracting Cost Amount";
                ConsEstRec.Plant := EstimateRec."Plant Cost Amount";
                ConsEstRec.Total := EstimateRec."Labor Cost Amount" + EstimateRec."Material Cost Amount" + EstimateRec."Subcontracting Cost Amount" + EstimateRec."Plant Cost Amount";
                ConsEstRec."Consultancy Firm Name" := EstimateRec."Consultancy Firm Name";
                ConsEstRec."Contact Name" := EstimateRec."Contact Name";
                ConsEstRec."Purchaser Name" := EstimateRec."Purchaser Name";
                ConsEstRec."Name Estimator" := EstimateRec."Name Estimator";
                ConsEstRec."Estimate Model Total" := EstimateRec."Estimate Model Total";
                ConsEstRec."Contact Person Name" := EstimateRec."Contact Person Name";
                ConsEstRec.INSERT;
               UNTIL EstimateRec.NEXT = 0;
            END;
          END; //kk, 13-05-11: M27123
        UNTIL ConsContactData.NEXT = 0;
      END;
      PAGE.RUN(0,ConsEstRec);
    END;

    PROCEDURE ShowProjects@1100485005(ContactRec@1100485000 : Record 5050);
    VAR
      ProjectRec@1100485001 : Record 11072003;
      ConsProjRec@1100485002 : TEMPORARY Record 11020299;
      ContBussRelRec@1100485003 : Record 5054;
      ProjPrincipalRec@1100485004 : Record 11012005;
    BEGIN
      ConsContactData.SETRANGE(Table,ConsContactData.Table::Projects);
      IF ConsContactData.FIND('-') THEN BEGIN
        REPEAT
          ContBussRelRec.CHANGECOMPANY(ConsContactData.Company);
          IF FPSLicenseManagement.LicenseAndReadPermissionForTable(ContBussRelRec) THEN BEGIN //kk, 13-05-11: M27123
            ContBussRelRec.SETRANGE("Contact No.",ContactRec."No.");
            ContBussRelRec.SETRANGE("Link to Table",ContBussRelRec."Link to Table"::Customer);
            IF ContBussRelRec.FINDSET THEN BEGIN
              REPEAT
                ProjPrincipalRec.CHANGECOMPANY(ConsContactData.Company);
                ProjPrincipalRec.SETCURRENTKEY(Principal); //call 28569
                ProjPrincipalRec.SETRANGE(Principal,ContBussRelRec."No.");
                IF ProjPrincipalRec.FINDSET THEN BEGIN
                  REPEAT
                    ProjectRec.CHANGECOMPANY(ConsContactData.Company);
                    IF ProjectRec.GET(ProjPrincipalRec."Project No.") THEN BEGIN
                      IF NOT ConsProjRec.GET(ConsContactData.Company,ProjPrincipalRec."Project No.") THEN BEGIN
                        ConsProjRec.INIT;
                        ConsProjRec.Company := ConsContactData.Company;
                        ConsProjRec.TRANSFERFIELDS(ProjectRec);
                        ProjectRec.CALCFIELDS("Contract Amount","Total Budget","Hours Budget","Labor Budget",
                                              "Material Budget","Subcontracting Budget","Plant Budget","Sundry Budget",
                                              Revenues,Prospect,"Principal Name","Name Project Manager","Name Project Engineer",
                                              "Name Estimator","Name Buyer Contact","Name Purchaser","Actual Costs","Actual Hours");
                        ConsProjRec."Contract Amount" := ProjectRec."Contract Amount";
                        ConsProjRec."Total Budget" := ProjectRec."Total Budget";
                        ConsProjRec."Hours Budget" := ProjectRec."Hours Budget";
                        ConsProjRec."Labor Budget" := ProjectRec."Labor Budget";
                        ConsProjRec."Material Budget" := ProjectRec."Material Budget";
                        ConsProjRec."Subcontracting Budget" := ProjectRec."Subcontracting Budget";
                        ConsProjRec."Plant Budget" := ProjectRec."Plant Budget";
                        ConsProjRec."Sundry Budget" := ProjectRec."Sundry Budget";
                        ConsProjRec.Revenues := ProjectRec.Revenues;
                        ConsProjRec.Prospect := ProjectRec.Prospect;
                        ConsProjRec."Principal Name" := ProjectRec."Principal Name";
                        ConsProjRec."Name Project Manager" := ProjectRec."Name Project Manager";
                        ConsProjRec."Name Project Engineer" := ProjectRec."Name Project Engineer";
                        ConsProjRec."Name Estimator" := ProjectRec."Name Estimator";
                        ConsProjRec."Name Buyer Contact" := ProjectRec."Name Buyer Contact";
                        ConsProjRec."Name Purchaser" := ProjectRec."Name Purchaser";
                        ConsProjRec."Actual Costs" := ProjectRec."Actual Costs";
                        ConsProjRec."Actual Hours" := ProjectRec."Actual Hours";
                        ConsProjRec.INSERT;
                      END;
                    END;
                  UNTIL ProjPrincipalRec.NEXT = 0;
                END;
              UNTIL ContBussRelRec.NEXT = 0;
            END;
          END; //kk, 13-05-11: M27123
        UNTIL ConsContactData.NEXT = 0;
      END;
      PAGE.RUN(0,ConsProjRec);
    END;

    PROCEDURE ShowTryOutQuotes@1100485006(ContactRec@1100485004 : Record 5050);
    VAR
      TryOutQuoteRec@1100485002 : Record 11012758;
      TryOutQuoteLineRec@1210190000 : Record 11125864;
      ConsTryOutQuote@1100485000 : TEMPORARY Record 11072022;
    BEGIN
      ConsContactData.SETRANGE(Table,ConsContactData.Table::"Try-Out Quote");
      IF ConsContactData.FINDSET(FALSE, FALSE) THEN BEGIN
        REPEAT
          TryOutQuoteRec.CHANGECOMPANY(ConsContactData.Company);
          IF FPSLicenseManagement.LicenseAndReadPermissionForTable(TryOutQuoteRec) THEN BEGIN //kk, 13-05-11: M27123
            TryOutQuoteRec.SETCURRENTKEY("Contact No."); //call 28569
            TryOutQuoteRec.SETRANGE("Contact No.", ContactRec."No.");
            IF TryOutQuoteRec.FINDSET(FALSE, FALSE) THEN BEGIN
              REPEAT
                IF NOT ConsTryOutQuote.GET(ConsContactData.Company,TryOutQuoteRec.Code) THEN BEGIN
                  ConsTryOutQuote.INIT;
                  ConsTryOutQuote.Company := ConsContactData.Company;
                  ConsTryOutQuote.TRANSFERFIELDS(TryOutQuoteRec);
                  ConsTryOutQuote.INSERT;
                END;
              UNTIL TryOutQuoteRec.NEXT = 0;
            END;
          END; //kk, 13-05-11: M27123
          TryOutQuoteLineRec.CHANGECOMPANY(ConsContactData.Company);
          IF FPSLicenseManagement.LicenseAndReadPermissionForTable(TryOutQuoteLineRec) THEN BEGIN
            TryOutQuoteLineRec.SETCURRENTKEY("Contact No."); //call 28569
            TryOutQuoteLineRec.SETRANGE("Contact No.", ContactRec."No.");
            IF TryOutQuoteLineRec.FINDSET THEN BEGIN
              TryOutQuoteRec.CHANGECOMPANY(ConsContactData.Company);
              REPEAT
                IF TryOutQuoteRec.GET(TryOutQuoteLineRec."Try-out Quote") THEN BEGIN
                  IF NOT ConsTryOutQuote.GET(ConsContactData.Company, TryOutQuoteRec.Code) THEN BEGIN
                    ConsTryOutQuote.INIT;
                    ConsTryOutQuote.Company := ConsContactData.Company;
                    ConsTryOutQuote.TRANSFERFIELDS(TryOutQuoteRec);
                    ConsTryOutQuote.INSERT;
                  END;
                END;
              UNTIL TryOutQuoteLineRec.NEXT = 0;
            END;
          END;
        UNTIL ConsContactData.NEXT = 0;
      END;
      PAGE.RUN(0,ConsTryOutQuote);
    END;

    PROCEDURE ShowTryOutCC@1100525000(ContactRec@1100485004 : Record 5050) : Integer;
    VAR
      TryOutRec@1100485002 : Record 11012750;
      TryOutLineRec@1100485001 : Record 11012755;
      ConsTryOut@1100485000 : TEMPORARY Record 11020291;
    BEGIN
      ConsContactData.SETRANGE(Table,ConsContactData.Table::"Try-Out");
      IF ConsContactData.FIND('-') THEN BEGIN
        REPEAT
          TryOutLineRec.CHANGECOMPANY(ConsContactData.Company);
          IF FPSLicenseManagement.LicenseAndReadPermissionForTable(TryOutLineRec) THEN BEGIN //kk, 13-05-11: M27123
            TryOutLineRec.SETCURRENTKEY("Contact No."); //call 28569
            TryOutLineRec.SETRANGE("Contact No.",ContactRec."No.");
            IF TryOutLineRec.FIND('-') THEN BEGIN
              TryOutRec.CHANGECOMPANY(ConsContactData.Company);
              REPEAT
                IF TryOutRec.GET(TryOutLineRec."Try-out No.") THEN BEGIN
                  IF NOT ConsTryOut.GET(ConsContactData.Company,TryOutRec.Code) THEN BEGIN
                    ConsTryOut.INIT;
                    ConsTryOut.Company := ConsContactData.Company;
                    ConsTryOut.TRANSFERFIELDS(TryOutRec);
                    ConsTryOut.INSERT;
                  END;
                END;
              UNTIL TryOutLineRec.NEXT = 0;
            END;
          END; //kk, 13-05-11: M27123
        UNTIL ConsContactData.NEXT = 0;
      END;
      EXIT(ConsTryOut.COUNT);
    END;

    PROCEDURE ShowTryOutQuotesCC@1100525001(ContactRec@1100485004 : Record 5050) : Integer;
    VAR
      TryOutQuoteRec@1100485002 : Record 11012758;
      TryOutQuoteLineRec@1100528000 : Record 11125864;
      ConsTryOutQuote@1100485000 : TEMPORARY Record 11072022;
    BEGIN
      ConsContactData.SETRANGE(Table,ConsContactData.Table::"Try-Out Quote");
      IF ConsContactData.FINDSET(FALSE, FALSE) THEN BEGIN
        REPEAT
          TryOutQuoteRec.CHANGECOMPANY(ConsContactData.Company);
          IF FPSLicenseManagement.LicenseAndReadPermissionForTable(TryOutQuoteRec) THEN BEGIN //kk, 13-05-11: M27123
            TryOutQuoteRec.SETCURRENTKEY("Contact No."); //call 28569
            TryOutQuoteRec.SETRANGE("Contact No.", ContactRec."No.");
            IF TryOutQuoteRec.FINDSET(FALSE, FALSE) THEN BEGIN
              REPEAT
                IF NOT ConsTryOutQuote.GET(ConsContactData.Company,TryOutQuoteRec.Code) THEN BEGIN
                  ConsTryOutQuote.INIT;
                  ConsTryOutQuote.Company := ConsContactData.Company;
                  ConsTryOutQuote.TRANSFERFIELDS(TryOutQuoteRec);
                  ConsTryOutQuote.INSERT;
                END;
              UNTIL TryOutQuoteRec.NEXT = 0;
            END;
          END; //kk, 13-05-11: M27123
          TryOutQuoteLineRec.CHANGECOMPANY(ConsContactData.Company);
          IF FPSLicenseManagement.LicenseAndReadPermissionForTable(TryOutQuoteLineRec) THEN BEGIN
            TryOutQuoteLineRec.SETCURRENTKEY("Contact No."); //call 28569
            TryOutQuoteLineRec.SETRANGE("Contact No.", ContactRec."No.");
            IF TryOutQuoteLineRec.FINDSET THEN BEGIN
              TryOutQuoteRec.CHANGECOMPANY(ConsContactData.Company);
              REPEAT
                IF TryOutQuoteRec.GET(TryOutQuoteLineRec."Try-out Quote") THEN BEGIN
                  IF NOT ConsTryOutQuote.GET(ConsContactData.Company, TryOutQuoteRec.Code) THEN BEGIN
                    ConsTryOutQuote.INIT;
                    ConsTryOutQuote.Company := ConsContactData.Company;
                    ConsTryOutQuote.TRANSFERFIELDS(TryOutQuoteRec);
                    ConsTryOutQuote.INSERT;
                  END;
                END;
              UNTIL TryOutQuoteLineRec.NEXT = 0;
            END;
          END;
        UNTIL ConsContactData.NEXT = 0;
      END;
      EXIT(ConsTryOutQuote.COUNT);
    END;

    PROCEDURE ShowTodosCC@1100528500(Contact@1100485004 : Record 5050) : Integer;
    VAR
      ConsolidatedTodos@1100525000 : TEMPORARY Record 11229458;
    BEGIN
      SetConsTodoTemp(ConsolidatedTodos, Contact."No.");
      EXIT(ConsolidatedTodos.COUNT);
    END;

    LOCAL PROCEDURE SetConsTodoTemp@1100525003(VAR ConsolidatedTodos@1100525000 : Record 11229458;ContactNo@1100525001 : Code[20]);
    VAR
      Todo@1100525003 : Record 5080;
    BEGIN
      IF FPSLicenseManagement.LicenseAndReadPermissionForTable(Todo) THEN BEGIN
        Todo.SETRANGE("Contact No.", ContactNo);
        Todo.SETRANGE("System To-do Type", Todo."System To-do Type"::"Contact Attendee");
        IF Todo.FIND('-') THEN BEGIN
          REPEAT
            IF NOT ConsolidatedTodos.GET(COMPANYNAME, Todo."No.") THEN BEGIN
              ConsolidatedTodos.INIT;
              ConsolidatedTodos.Company := COMPANYNAME;
              ConsolidatedTodos.TRANSFERFIELDS(Todo);
              ConsolidatedTodos.INSERT;
            END;
          UNTIL Todo.NEXT = 0;
        END;
      END;
      ConsContactData.SETRANGE(Table, ConsContactData.Table::Activities);
      IF ConsContactData.FIND('-') THEN BEGIN
        REPEAT
          Todo.CHANGECOMPANY(ConsContactData.Company);
          IF FPSLicenseManagement.LicenseAndReadPermissionForTable(Todo) THEN BEGIN
            Todo.SETRANGE("Contact No.", ContactNo);
            Todo.SETRANGE("System To-do Type", Todo."System To-do Type"::"Contact Attendee");
            IF Todo.FIND('-') THEN BEGIN
              REPEAT
                IF NOT ConsolidatedTodos.GET(ConsContactData.Company, Todo."No.") THEN BEGIN
                  ConsolidatedTodos.INIT;
                  ConsolidatedTodos.Company := ConsContactData.Company;
                  ConsolidatedTodos.TRANSFERFIELDS(Todo);
                  ConsolidatedTodos.INSERT;
                END;
              UNTIL Todo.NEXT = 0;
            END;
          END;
        UNTIL ConsContactData.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}

