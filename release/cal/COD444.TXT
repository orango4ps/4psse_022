OBJECT Codeunit 444 Purchase-Post Prepayments
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.05,4PS14.00;
  }
  PROPERTIES
  {
    TableNo=38;
    Permissions=TableData 39=imd,
                TableData 45=rimd,
                TableData 49=imd,
                TableData 93=imd,
                TableData 94=imd,
                TableData 122=imd,
                TableData 123=imd,
                TableData 124=imd,
                TableData 125=imd;
    OnRun=BEGIN
            Execute(Rec);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=is not within your range of allowed posting dates;NOR=er ikke innenfor tillatte bokf›ringsdatoer;SVE=„r inte i ditt till†tna intervall f”r bokf”ringsdatum';
      Text001@1001 : TextConst 'ENU=There is nothing to post.;NOR=Det finnes ingenting † bokf›re.;SVE=Det finns inget att bokf”ra.';
      Text002@1008 : TextConst 'ENU=Posting Prepayment Lines   #2######\;NOR=Bokf›rer forskuddslinjer   #2######\;SVE=Bokf”r f”rskottsbet.rader  #2######\';
      Text003@1010 : TextConst 'ENU=%1 %2 -> Invoice %3;NOR=%1 %2 -> Faktura %3;SVE=%1 %2 -> Faktura %3';
      Text004@1007 : TextConst 'ENU=Posting purchases and VAT  #3######\;NOR=Bokf›rer kj›p og mva.      #3######\;SVE=Bokf. ink”p och moms       #3######\';
      Text005@1006 : TextConst 'ENU=Posting to vendors         #4######\;NOR=Bokf›rer til leverand›rer  #4######\;SVE=Bokf. till leverant”rer    #4######\';
      Text006@1021 : TextConst 'ENU=Posting to bal. account    #5######;NOR=Bokf›rer til motkonto      #5######;SVE=Bokf”ring till motkonto    #5######';
      Text011@1011 : TextConst 'ENU=%1 %2 -> Credit Memo %3;NOR=%1 %2 -> Kreditnota %3;SVE=%1 %2 -> Kreditnota %3';
      Text012@1009 : TextConst 'ENU=Prepayment %1, %2 %3.;NOR=Forskuddsbetaling %1, %2 %3.;SVE=F”rskottsbetalning %1, %2 %3.';
      GLSetup@1013 : Record 98;
      PurchSetup@1031 : Record 312;
      GenPostingSetup@1012 : Record 252;
      TempGlobalPrepmtInvLineBuf@1025 : TEMPORARY Record 461;
      ErrorMessageMgt@1002 : Codeunit 28;
      GenJnlPostLine@1014 : Codeunit 12;
      Text013@1016 : TextConst 'ENU=It is not possible to assign a prepayment amount of %1 to the purchase lines.;NOR=Det er ikke mulig † tilordne et forskuddsbel›p p† %1 til kj›pslinjene.;SVE=Det g†r inte att tilldela en f”rskottsbetalning om %1 till ink”psraderna.';
      Text014@1018 : TextConst 'ENU=VAT Amount;NOR=Mva-bel›p;SVE=Momsbelopp';
      Text015@1017 : TextConst 'ENU=%1% VAT;NOR=%1% Mva.;SVE=%1% moms';
      Text016@1019 : TextConst 'ENU=The new prepayment amount must be between %1 and %2.;NOR=Det nye forskuddsbel›pet m† v‘re mellom %1 og %2.;SVE=Den nya f”rskottsbetalningen m†ste vara mellan %1 och %2.';
      Text017@1020 : TextConst 'ENU=At least one line must have %1 > 0 to distribute prepayment amount.;NOR=Minst ‚n linje m† ha %1 > 0 for † fordele forskuddsbel›p.;SVE=%1 m†ste vara > 0 p† minst en rad f”r att f”rskottsbetalningen ska kunna f”rdelas.';
      text019@1015 : TextConst 'ENU=Invoice,Credit Memo;NOR=Faktura,Kreditnota;SVE=Faktura,Kreditnota';
      SuppressCommit@1022 : Boolean;
      PrepmtDocumentType@1003 : ',,Invoice,Credit Memo';
      PreviewMode@1004 : Boolean;

    [External]
    PROCEDURE SetDocumentType@28(DocumentType@1000 : ',,Invoice,Credit Memo');
    BEGIN
      PrepmtDocumentType := DocumentType;
    END;

    LOCAL PROCEDURE Execute@30(VAR PurchHeader@1000 : Record 38);
    BEGIN
      CASE PrepmtDocumentType OF
        PrepmtDocumentType::Invoice:
          Invoice(PurchHeader);
        PrepmtDocumentType::"Credit Memo":
          CreditMemo(PurchHeader);
      END;
    END;

    [External]
    PROCEDURE Invoice@1(VAR PurchHeader@1000 : Record 38);
    VAR
      Handled@1001 : Boolean;
    BEGIN
      OnBeforeInvoice(PurchHeader,Handled);
      IF NOT Handled THEN
        Code(PurchHeader,0);
    END;

    [External]
    PROCEDURE CreditMemo@2(VAR PurchHeader@1000 : Record 38);
    VAR
      Handled@1001 : Boolean;
    BEGIN
      OnBeforeCreditMemo(PurchHeader,Handled);
      IF NOT Handled THEN
        Code(PurchHeader,1);
    END;

    LOCAL PROCEDURE Code@3(VAR PurchHeader2@1001 : Record 38;DocumentType@1000 : 'Invoice,Credit Memo');
    VAR
      SourceCodeSetup@1008 : Record 242;
      PurchHeader@1031 : Record 38;
      PurchLine@1021 : Record 39;
      PurchInvHeader@1015 : Record 122;
      PurchCrMemoHeader@1014 : Record 124;
      TempPrepmtInvLineBuffer@1023 : TEMPORARY Record 461;
      TotalPrepmtInvLineBuffer@1029 : Record 461;
      TotalPrepmtInvLineBufferLCY@1028 : Record 461;
      GenJnlLine@1017 : Record 81;
      TempVATAmountLine@1022 : TEMPORARY Record 290;
      TempVATAmountLineDeduct@1037 : TEMPORARY Record 290;
      VendLedgEntry@1034 : Record 25;
      TempPurchLines@1039 : TEMPORARY Record 39;
      GenJnlPostPreview@1004 : Codeunit 19;
      Window@1007 : Dialog;
      GenJnlLineDocNo@1006 : Code[20];
      GenJnlLineExtDocNo@1020 : Code[35];
      SrcCode@1009 : Code[10];
      PostingNoSeriesCode@1003 : Code[20];
      ModifyHeader@1002 : Boolean;
      CalcPmtDiscOnCrMemos@1019 : Boolean;
      PostingDescription@1010 : Text[100];
      GenJnlLineDocType@1016 : Integer;
      PrevLineNo@1026 : Integer;
      LineCount@1024 : Integer;
      PostedDocTabNo@1018 : Integer;
      LineNo@1025 : Integer;
    BEGIN
      OnBeforePostPrepayments(PurchHeader2,DocumentType,SuppressCommit);

      PurchHeader := PurchHeader2;
      GLSetup.GET;
      PurchSetup.GET;
      WITH PurchHeader DO BEGIN
        CheckPrepmtDoc(PurchHeader,DocumentType);

        UpdateDocNos(PurchHeader,DocumentType,GenJnlLineDocNo,PostingNoSeriesCode,ModifyHeader);

        IF NOT PreviewMode AND ModifyHeader THEN BEGIN
          MODIFY;
          IF NOT SuppressCommit THEN
            COMMIT;
        END;

        Window.OPEN(
          '#1#################################\\' +
          Text002 +
          Text004 +
          Text005 +
          Text006);
        Window.UPDATE(1,STRSUBSTNO('%1 %2',SELECTSTR(1 + DocumentType,text019),"No."));

        SourceCodeSetup.GET;
        SrcCode := SourceCodeSetup.Purchases;
        IF "Prepmt. Posting Description" <> '' THEN
          PostingDescription := "Prepmt. Posting Description"
        ELSE
          PostingDescription :=
            COPYSTR(
              STRSUBSTNO(Text012,SELECTSTR(1 + DocumentType,text019),"Document Type","No."),
              1,MAXSTRLEN("Posting Description"));

        // Create posted header
        IF PurchSetup."Ext. Doc. No. Mandatory" THEN
          TESTFIELD("Vendor Invoice No.");
        CASE DocumentType OF
          DocumentType::Invoice:
            BEGIN
              InsertPurchInvHeader(PurchInvHeader,PurchHeader,PostingDescription,GenJnlLineDocNo,SrcCode,PostingNoSeriesCode);
              GenJnlLineDocType := GenJnlLine."Document Type"::Invoice;
              PostedDocTabNo := DATABASE::"Purch. Inv. Header";
              GenJnlLineExtDocNo := PurchInvHeader."Vendor Invoice No.";
              Window.UPDATE(1,STRSUBSTNO(Text003,"Document Type","No.",PurchInvHeader."No."));
            END;
          DocumentType::"Credit Memo":
            BEGIN
              IF PurchSetup."Ext. Doc. No. Mandatory" THEN
                TESTFIELD("Vendor Cr. Memo No.");
              CalcPmtDiscOnCrMemos := GetCalcPmtDiscOnCrMemos("Prepmt. Payment Terms Code");
              InsertPurchCrMemoHeader(
                PurchCrMemoHeader,PurchHeader,PostingDescription,GenJnlLineDocNo,SrcCode,PostingNoSeriesCode,
                CalcPmtDiscOnCrMemos);
              GenJnlLineDocType := GenJnlLine."Document Type"::"Credit Memo";
              PostedDocTabNo := DATABASE::"Purch. Cr. Memo Hdr.";
              GenJnlLineExtDocNo := PurchCrMemoHeader."Vendor Cr. Memo No.";
              Window.UPDATE(1,STRSUBSTNO(Text011,"Document Type","No.",PurchCrMemoHeader."No."));
            END;
        END;
        IF PurchSetup."Copy Comments Order to Invoice" THEN
          CopyCommentLines("No.",PostedDocTabNo,GenJnlLineDocNo);
        // Reverse old lines
        IF DocumentType = DocumentType::Invoice THEN BEGIN
          GetPurchLinesToDeduct(PurchHeader,TempPurchLines);
          IF NOT TempPurchLines.ISEMPTY THEN
            CalcVATAmountLines(PurchHeader,TempPurchLines,TempVATAmountLineDeduct,DocumentType::"Credit Memo");
        END;

        // Create Lines
        TempPrepmtInvLineBuffer.DELETEALL;
        CalcVATAmountLines(PurchHeader,PurchLine,TempVATAmountLine,DocumentType);
        TempVATAmountLine.DeductVATAmountLine(TempVATAmountLineDeduct);
        UpdateVATOnLines(PurchHeader,PurchLine,TempVATAmountLine,DocumentType);
        BuildInvLineBuffer(PurchHeader,PurchLine,DocumentType,TempPrepmtInvLineBuffer,TRUE);
        TempPrepmtInvLineBuffer.FIND('-');
        REPEAT
          LineCount := LineCount + 1;
          Window.UPDATE(2,LineCount);
          IF TempPrepmtInvLineBuffer."Line No." <> 0 THEN
            LineNo := PrevLineNo + TempPrepmtInvLineBuffer."Line No."
          ELSE
            LineNo := PrevLineNo + 10000;
          CASE DocumentType OF
            DocumentType::Invoice:
              BEGIN
                InsertPurchInvLine(PurchInvHeader,LineNo,TempPrepmtInvLineBuffer);
                PostedDocTabNo := DATABASE::"Purch. Inv. Line";
              END;
            DocumentType::"Credit Memo":
              BEGIN
                InsertPurchCrMemoLine(PurchCrMemoHeader,LineNo,TempPrepmtInvLineBuffer);
                PostedDocTabNo := DATABASE::"Purch. Cr. Memo Line";
              END;
          END;
          PrevLineNo := LineNo;
          InsertExtendedText(
            PostedDocTabNo,GenJnlLineDocNo,TempPrepmtInvLineBuffer."G/L Account No.","Document Date","Language Code",PrevLineNo);
        UNTIL TempPrepmtInvLineBuffer.NEXT = 0;

        // G/L Posting
        LineCount := 0;
        IF NOT "Compress Prepayment" THEN
          TempPrepmtInvLineBuffer.CompressBuffer;
        TempPrepmtInvLineBuffer.SETRANGE(Adjustment,FALSE);
        TempPrepmtInvLineBuffer.FINDSET(TRUE);
        REPEAT
          IF DocumentType = DocumentType::"Credit Memo" THEN
            TempPrepmtInvLineBuffer.ReverseAmounts;
          RoundAmounts(PurchHeader,TempPrepmtInvLineBuffer,TotalPrepmtInvLineBuffer,TotalPrepmtInvLineBufferLCY);
          IF "Currency Code" = '' THEN BEGIN
            AdjustInvLineBuffers(PurchHeader,TempPrepmtInvLineBuffer,TotalPrepmtInvLineBuffer,DocumentType);
            TotalPrepmtInvLineBufferLCY := TotalPrepmtInvLineBuffer;
          END ELSE
            AdjustInvLineBuffers(PurchHeader,TempPrepmtInvLineBuffer,TotalPrepmtInvLineBufferLCY,DocumentType);
          TempPrepmtInvLineBuffer.MODIFY;
        UNTIL TempPrepmtInvLineBuffer.NEXT = 0;

        TempPrepmtInvLineBuffer.RESET;
        TempPrepmtInvLineBuffer.SETCURRENTKEY(Adjustment);
        TempPrepmtInvLineBuffer.FIND('+');
        REPEAT
          LineCount := LineCount + 1;
          Window.UPDATE(3,LineCount);

          IF TempPrepmtInvLineBuffer."VAT Calculation Type" =
             TempPrepmtInvLineBuffer."VAT Calculation Type"::"Reverse Charge VAT"
          THEN
            TempPrepmtInvLineBuffer.UpdateVATAmounts;

          PostPrepmtInvLineBuffer(
            PurchHeader,TempPrepmtInvLineBuffer,DocumentType,PostingDescription,
            GenJnlLineDocType,GenJnlLineDocNo,GenJnlLineExtDocNo,SrcCode,PostingNoSeriesCode);
        UNTIL TempPrepmtInvLineBuffer.NEXT(-1) = 0;

        // Post vendor entry
        Window.UPDATE(4,1);
        PostVendorEntry(
          PurchHeader,TotalPrepmtInvLineBuffer,TotalPrepmtInvLineBufferLCY,DocumentType,PostingDescription,
          GenJnlLineDocType,GenJnlLineDocNo,GenJnlLineExtDocNo,SrcCode,PostingNoSeriesCode,CalcPmtDiscOnCrMemos);

        UpdatePostedPurchaseDocument(DocumentType,GenJnlLineDocNo);

        // Balancing account
        IF "Bal. Account No." <> '' THEN BEGIN
          Window.UPDATE(5,1);
          VendLedgEntry.FINDLAST;
          PostBalancingEntry(
            PurchHeader,TotalPrepmtInvLineBuffer,TotalPrepmtInvLineBufferLCY,VendLedgEntry,DocumentType,
            GenJnlLineDocType,GenJnlLineDocNo,GenJnlLineExtDocNo,SrcCode,PostingNoSeriesCode);
        END;

        // Update lines & header
        UpdatePurchaseDocument(PurchHeader,PurchLine,DocumentType,GenJnlLineDocNo);
        IF Status <> Status::"Pending Prepayment" THEN
          Status := Status::"Pending Prepayment";
        MODIFY;
      END;

      PurchHeader2 := PurchHeader;
      PurchHeader2.TriggerOnAfterPostPurchaseDoc(GenJnlPostLine,'','',PurchInvHeader."No.",PurchCrMemoHeader."No.");

      IF PreviewMode THEN BEGIN
        Window.CLOSE;
        GenJnlPostPreview.ThrowError;
      END;

      OnAfterPostPrepayments(PurchHeader2,DocumentType,SuppressCommit,PurchInvHeader,PurchCrMemoHeader);
    END;

    [External]
    PROCEDURE CheckPrepmtDoc@40(PurchHeader@1002 : Record 38;DocumentType@1003 : 'Invoice,Credit Memo');
    VAR
      Vend@1001 : Record 23;
      GenJnlCheckLine@1000 : Codeunit 11;
      CheckDimensions@1004 : Codeunit 481;
    BEGIN
      WITH PurchHeader DO BEGIN
        TESTFIELD("Document Type","Document Type"::Order);
        TESTFIELD("Buy-from Vendor No.");
        TESTFIELD("Pay-to Vendor No.");
        TESTFIELD("Posting Date");
        TESTFIELD("Document Date");
        IF GenJnlCheckLine.DateNotAllowed("Posting Date") THEN
          FIELDERROR("Posting Date",Text000);

        IF NOT CheckOpenPrepaymentLines(PurchHeader,DocumentType) THEN
          ERROR(Text001);
        CheckDimensions.CheckPurchPrepmtDim(PurchHeader);
        ErrorMessageMgt.Finish;
        OnCheckPurchasePostRestrictions;
        Vend.GET("Buy-from Vendor No.");
        Vend.CheckBlockedVendOnDocs(Vend,TRUE);
        IF "Pay-to Vendor No." <> "Buy-from Vendor No." THEN BEGIN
          Vend.GET("Pay-to Vendor No.");
          Vend.CheckBlockedVendOnDocs(Vend,TRUE);
        END;
        OnAfterCheckPrepmtDoc(PurchHeader,DocumentType);
      END;
    END;

    LOCAL PROCEDURE UpdateDocNos@24(VAR PurchHeader@1001 : Record 38;DocumentType@1000 : 'Invoice,Credit Memo';VAR DocNo@1003 : Code[20];VAR NoSeriesCode@1004 : Code[20];VAR ModifyHeader@1005 : Boolean);
    VAR
      NoSeriesMgt@1002 : Codeunit 396;
    BEGIN
      WITH PurchHeader DO
        CASE DocumentType OF
          DocumentType::Invoice:
            BEGIN
              TESTFIELD("Prepayment Due Date");
              TESTFIELD("Prepmt. Cr. Memo No.",'');
              IF "Prepayment No." = '' THEN
                IF NOT PreviewMode THEN BEGIN
                  TESTFIELD("Prepayment No. Series");
                  "Prepayment No." := NoSeriesMgt.GetNextNo("Prepayment No. Series","Posting Date",TRUE);
                  ModifyHeader := TRUE;
                END ELSE
                  "Prepayment No." := '***';
              DocNo := "Prepayment No.";
              NoSeriesCode := "Prepayment No. Series";
            END;
          DocumentType::"Credit Memo":
            BEGIN
              TESTFIELD("Prepayment No.",'');
              IF "Prepmt. Cr. Memo No." = '' THEN
                IF NOT PreviewMode THEN BEGIN
                  TESTFIELD("Prepmt. Cr. Memo No. Series");
                  "Prepmt. Cr. Memo No." := NoSeriesMgt.GetNextNo("Prepmt. Cr. Memo No. Series","Posting Date",TRUE);
                  ModifyHeader := TRUE;
                END ELSE
                  "Prepmt. Cr. Memo No." := '***';
              DocNo := "Prepmt. Cr. Memo No.";
              NoSeriesCode := "Prepmt. Cr. Memo No. Series";
            END;
        END;
    END;

    [External]
    PROCEDURE CheckOpenPrepaymentLines@7(PurchHeader@1000 : Record 38;DocumentType@1003 : Option) Found : Boolean;
    VAR
      PurchLine@1001 : Record 39;
    BEGIN
      WITH PurchLine DO BEGIN
        ApplyFilter(PurchHeader,DocumentType,PurchLine);
        IF FIND('-') THEN
          REPEAT
            IF NOT Found THEN
              Found := PrepmtAmount(PurchLine,DocumentType) <> 0;
            IF ("Prepayment VAT Identifier" = '') AND ("Prepmt. Amt. Inv." = 0) THEN BEGIN
              UpdatePrepmtSetupFields;
              MODIFY;
            END;
          UNTIL NEXT = 0;
      END;
      EXIT(Found);
    END;

    LOCAL PROCEDURE RoundAmounts@5(PurchHeader@1001 : Record 38;VAR PrepmtInvLineBuf@1000 : Record 461;VAR TotalPrepmtInvLineBuf@1002 : Record 461;VAR TotalPrepmtInvLineBufLCY@1003 : Record 461);
    VAR
      VAT@1004 : Boolean;
    BEGIN
      TotalPrepmtInvLineBuf.IncrAmounts(PrepmtInvLineBuf);

      WITH PrepmtInvLineBuf DO
        IF PurchHeader."Currency Code" <> '' THEN BEGIN
          VAT := Amount <> "Amount Incl. VAT";
          "Amount Incl. VAT" :=
            AmountToLCY(PurchHeader,TotalPrepmtInvLineBuf."Amount Incl. VAT",TotalPrepmtInvLineBufLCY."Amount Incl. VAT");
          IF VAT THEN
            Amount := AmountToLCY(PurchHeader,TotalPrepmtInvLineBuf.Amount,TotalPrepmtInvLineBufLCY.Amount)
          ELSE
            Amount := "Amount Incl. VAT";
          "VAT Amount" := "Amount Incl. VAT" - Amount;
          IF "VAT Base Amount" <> 0 THEN
            "VAT Base Amount" := Amount;
        END;

      TotalPrepmtInvLineBufLCY.IncrAmounts(PrepmtInvLineBuf);

      OnAfterRoundAmounts(PurchHeader,PrepmtInvLineBuf,TotalPrepmtInvLineBuf,TotalPrepmtInvLineBufLCY);
    END;

    LOCAL PROCEDURE AmountToLCY@6(PurchHeader@1001 : Record 38;TotalAmt@1000 : Decimal;PrevTotalAmt@1002 : Decimal) : Decimal;
    VAR
      CurrExchRate@1003 : Record 330;
    BEGIN
      CurrExchRate.INIT;
      WITH PurchHeader DO
        EXIT(
          ROUND(
            CurrExchRate.ExchangeAmtFCYToLCY(
      //      "Posting Date","Currency Code",TotalAmt,"Currency Factor")) - //**4PS.o
              0, '', "Posting Date","Currency Code",TotalAmt,"Currency Factor",FALSE)) - //**4PS.n
          PrevTotalAmt);
    END;

    LOCAL PROCEDURE AdjustInvLineBuffers@41(PurchHeader@1003 : Record 38;VAR PrepmtInvLineBuf@1002 : Record 461;VAR TotalPrepmtInvLineBuf@1001 : Record 461;DocumentType@1000 : 'Invoice,Credit Memo');
    VAR
      VATAdjustment@1004 : ARRAY [2] OF Decimal;
      VAT@1005 : ',Base,Amount';
    BEGIN
      CalcPrepmtAmtInvLCYInLines(PurchHeader,PrepmtInvLineBuf,DocumentType,VATAdjustment);
      IF ABS(VATAdjustment[VAT::Base]) > GLSetup."Amount Rounding Precision" THEN
        InsertCorrInvLineBuffer(PrepmtInvLineBuf,PurchHeader,VATAdjustment[VAT::Base])
      ELSE
        IF (VATAdjustment[VAT::Base] <> 0) OR (VATAdjustment[VAT::Amount] <> 0) THEN BEGIN
          PrepmtInvLineBuf.AdjustVATBase(VATAdjustment);
          TotalPrepmtInvLineBuf.AdjustVATBase(VATAdjustment);
        END;
    END;

    LOCAL PROCEDURE CalcPrepmtAmtInvLCYInLines@42(PurchHeader@1002 : Record 38;VAR PrepmtInvLineBuf@1003 : Record 461;DocumentType@1001 : 'Invoice,Credit Memo';VAR VATAdjustment@1006 : ARRAY [2] OF Decimal);
    VAR
      PurchLine@1000 : Record 39;
      PrepmtInvBufAmount@1004 : ARRAY [2] OF Decimal;
      TotalAmount@1005 : ARRAY [2] OF Decimal;
      LineAmount@1007 : ARRAY [2] OF Decimal;
      Ratio@1010 : ARRAY [2] OF Decimal;
      PrepmtAmtReminder@1008 : ARRAY [2] OF Decimal;
      PrepmtAmountRnded@1009 : ARRAY [2] OF Decimal;
      VAT@1011 : ',Base,Amount';
    BEGIN
      PrepmtInvLineBuf.AmountsToArray(PrepmtInvBufAmount);
      IF DocumentType = DocumentType::"Credit Memo" THEN
        ReverseDecArray(PrepmtInvBufAmount);

      TempGlobalPrepmtInvLineBuf.SetFilterOnPKey(PrepmtInvLineBuf);
      TempGlobalPrepmtInvLineBuf.CALCSUMS(Amount,"Amount Incl. VAT");
      TempGlobalPrepmtInvLineBuf.AmountsToArray(TotalAmount);
      FOR VAT := VAT::Base TO VAT::Amount DO
        IF TotalAmount[VAT] = 0 THEN
          Ratio[VAT] := 0
        ELSE
          Ratio[VAT] := PrepmtInvBufAmount[VAT] / TotalAmount[VAT];
      IF TempGlobalPrepmtInvLineBuf.FINDSET THEN
        REPEAT
          TempGlobalPrepmtInvLineBuf.AmountsToArray(LineAmount);
          PrepmtAmountRnded[VAT::Base] :=
            CalcRoundedAmount(LineAmount[VAT::Base],Ratio[VAT::Base],PrepmtAmtReminder[VAT::Base]);
          PrepmtAmountRnded[VAT::Amount] :=
            CalcRoundedAmount(LineAmount[VAT::Amount],Ratio[VAT::Amount],PrepmtAmtReminder[VAT::Amount]);

          PurchLine.GET(PurchHeader."Document Type",PurchHeader."No.",TempGlobalPrepmtInvLineBuf."Line No.");
          IF DocumentType = DocumentType::"Credit Memo" THEN BEGIN
            VATAdjustment[VAT::Base] += PurchLine."Prepmt. Amount Inv. (LCY)" - PrepmtAmountRnded[VAT::Base];
            PurchLine."Prepmt. Amount Inv. (LCY)" := 0;
            VATAdjustment[VAT::Amount] += PurchLine."Prepmt. VAT Amount Inv. (LCY)" - PrepmtAmountRnded[VAT::Amount];
            PurchLine."Prepmt. VAT Amount Inv. (LCY)" := 0;
          END ELSE BEGIN
            PurchLine."Prepmt. Amount Inv. (LCY)" += PrepmtAmountRnded[VAT::Base];
            PurchLine."Prepmt. VAT Amount Inv. (LCY)" += PrepmtAmountRnded[VAT::Amount];
          END;
          //**4PS.sn
          PurchLine."Modified by" := USERID; //DP00469
          PurchLine."Last Date Modified" := TODAY;//DP00469
          //**4PS.en
          PurchLine.MODIFY;
        UNTIL TempGlobalPrepmtInvLineBuf.NEXT = 0;
      TempGlobalPrepmtInvLineBuf.DELETEALL;
      ReverseDecArray(VATAdjustment);
    END;

    LOCAL PROCEDURE CalcRoundedAmount@51(LineAmount@1000 : Decimal;Ratio@1001 : Decimal;VAR Reminder@1002 : Decimal) RoundedAmount : Decimal;
    VAR
      Amount@1003 : Decimal;
    BEGIN
      Amount := Reminder + LineAmount * Ratio;
      RoundedAmount := ROUND(Amount);
      Reminder := Amount - RoundedAmount;
    END;

    LOCAL PROCEDURE ReverseDecArray@53(VAR DecArray@1000 : ARRAY [2] OF Decimal);
    VAR
      Idx@1001 : Integer;
    BEGIN
      FOR Idx := 1 TO ARRAYLEN(DecArray) DO
        DecArray[Idx] := -DecArray[Idx];
    END;

    LOCAL PROCEDURE InsertCorrInvLineBuffer@43(VAR PrepmtInvLineBuf@1001 : Record 461;PurchHeader@1002 : Record 38;VATBaseAdjustment@1003 : Decimal);
    VAR
      NewPrepmtInvLineBuf@1000 : Record 461;
      SavedPrepmtInvLineBuf@1004 : Record 461;
      AdjmtAmountACY@1005 : Decimal;
    BEGIN
      SavedPrepmtInvLineBuf := PrepmtInvLineBuf;

      IF PurchHeader."Currency Code" = '' THEN
        AdjmtAmountACY := VATBaseAdjustment
      ELSE
        AdjmtAmountACY := 0;

      NewPrepmtInvLineBuf.FillAdjInvLineBuffer(
        PrepmtInvLineBuf,
        GetPrepmtAccNo(PrepmtInvLineBuf."Gen. Bus. Posting Group",PrepmtInvLineBuf."Gen. Prod. Posting Group"),
        VATBaseAdjustment,AdjmtAmountACY);
      PrepmtInvLineBuf.InsertInvLineBuffer(NewPrepmtInvLineBuf);

      NewPrepmtInvLineBuf.FillAdjInvLineBuffer(
        PrepmtInvLineBuf,
        GetCorrBalAccNo(PurchHeader,VATBaseAdjustment > 0),
        -VATBaseAdjustment,-AdjmtAmountACY);
      PrepmtInvLineBuf.InsertInvLineBuffer(NewPrepmtInvLineBuf);

      PrepmtInvLineBuf := SavedPrepmtInvLineBuf;
    END;

    LOCAL PROCEDURE GetPrepmtAccNo@47(GenBusPostingGroup@1000 : Code[20];GenProdPostingGroup@1001 : Code[20]) : Code[20];
    BEGIN
      IF (GenBusPostingGroup <> GenPostingSetup."Gen. Bus. Posting Group") OR
         (GenProdPostingGroup <> GenPostingSetup."Gen. Prod. Posting Group")
      THEN
        GenPostingSetup.GET(GenBusPostingGroup,GenProdPostingGroup);
      EXIT(GenPostingSetup.GetPurchPrepmtAccount);
    END;

    [External]
    PROCEDURE GetCorrBalAccNo@48(PurchHeader@1000 : Record 38;PositiveAmount@1001 : Boolean) : Code[20];
    VAR
      BalAccNo@1002 : Code[20];
    BEGIN
      IF PurchHeader."Currency Code" = '' THEN
        BalAccNo := GetInvRoundingAccNo(PurchHeader."Vendor Posting Group")
      ELSE
        BalAccNo := GetGainLossGLAcc(PurchHeader."Currency Code",PositiveAmount);
      EXIT(BalAccNo);
    END;

    [External]
    PROCEDURE GetInvRoundingAccNo@49(VendorPostingGroup@1000 : Code[20]) : Code[20];
    VAR
      VendPostingGr@1002 : Record 93;
      GLAcc@1001 : Record 15;
    BEGIN
      VendPostingGr.GET(VendorPostingGroup);
      GLAcc.GET(VendPostingGr.GetInvRoundingAccount);
      EXIT(VendPostingGr."Invoice Rounding Account");
    END;

    LOCAL PROCEDURE GetGainLossGLAcc@50(CurrencyCode@1000 : Code[10];PositiveAmount@1002 : Boolean) : Code[20];
    VAR
      Currency@1001 : Record 4;
    BEGIN
      Currency.GET(CurrencyCode);
      IF PositiveAmount THEN
        EXIT(Currency.GetRealizedGainsAccount);
      EXIT(Currency.GetRealizedLossesAccount);
    END;

    LOCAL PROCEDURE GetCurrencyAmountRoundingPrecision@8(CurrencyCode@1000 : Code[10]) : Decimal;
    VAR
      Currency@1001 : Record 4;
    BEGIN
      Currency.Initialize(CurrencyCode);
      Currency.TESTFIELD("Amount Rounding Precision");
      EXIT(Currency."Amount Rounding Precision");
    END;

    [External]
    PROCEDURE UpdateVATOnLines@36(PurchHeader@1001 : Record 38;VAR PurchLine@1011 : Record 39;VAR VATAmountLine@1003 : Record 290;DocumentType@1000 : 'Invoice,Credit Memo,Statistic');
    VAR
      TempVATAmountLineRemainder@1004 : TEMPORARY Record 290;
      Currency@1005 : Record 4;
      PrepmtAmt@1002 : Decimal;
      NewAmount@1006 : Decimal;
      NewAmountIncludingVAT@1007 : Decimal;
      NewVATBaseAmount@1008 : Decimal;
      VATAmount@1009 : Decimal;
      VATDifference@1012 : Decimal;
      PrepmtAmtToInvTotal@1013 : Decimal;
      RemainderExists@1010 : Boolean;
    BEGIN
      Currency.Initialize(PurchHeader."Currency Code");

      WITH PurchLine DO BEGIN
        ApplyFilter(PurchHeader,DocumentType,PurchLine);
        LOCKTABLE;
        CALCSUMS("Prepmt. Line Amount","Prepmt. Amt. Inv.");
        PrepmtAmtToInvTotal := "Prepmt. Line Amount" - "Prepmt. Amt. Inv.";
        IF FIND('-') THEN
          REPEAT
            PrepmtAmt := PrepmtAmount(PurchLine,DocumentType);
            IF PrepmtAmt <> 0 THEN BEGIN
              VATAmountLine.GET(
                "Prepayment VAT Identifier","Prepmt. VAT Calc. Type","Prepayment Tax Group Code",FALSE,PrepmtAmt >= 0);
              OnUpdateVATOnLinesOnAfterVATAmountLineGet(VATAmountLine);
              IF VATAmountLine.Modified THEN BEGIN
                RemainderExists :=
                  TempVATAmountLineRemainder.GET(
                    "Prepayment VAT Identifier","Prepmt. VAT Calc. Type","Prepayment Tax Group Code",FALSE,PrepmtAmt >= 0);
                OnUpdateVATOnLinesOnAfterGetRemainder(TempVATAmountLineRemainder,RemainderExists);
                IF NOT RemainderExists THEN BEGIN
                  TempVATAmountLineRemainder := VATAmountLine;
                  TempVATAmountLineRemainder.INIT;
                  TempVATAmountLineRemainder.INSERT;
                END;

                IF PurchHeader."Prices Including VAT" THEN BEGIN
                  IF PrepmtAmt = 0 THEN BEGIN
                    VATAmount := 0;
                    NewAmountIncludingVAT := 0;
                  END ELSE BEGIN
                    VATAmount :=
                      TempVATAmountLineRemainder."VAT Amount" +
                      VATAmountLine."VAT Amount" * PrepmtAmt / VATAmountLine."Line Amount";
                    NewAmountIncludingVAT :=
                      TempVATAmountLineRemainder."Amount Including VAT" +
                      VATAmountLine."Amount Including VAT" * PrepmtAmt / VATAmountLine."Line Amount";
                  END;
                  NewAmount :=
                    ROUND(NewAmountIncludingVAT,Currency."Amount Rounding Precision") -
                    ROUND(VATAmount,Currency."Amount Rounding Precision");
                  NewVATBaseAmount :=
                    ROUND(
                      NewAmount * (1 - PurchHeader."VAT Base Discount %" / 100),
                      Currency."Amount Rounding Precision");
                END ELSE BEGIN
                  IF "VAT Calculation Type" = "VAT Calculation Type"::"Full VAT" THEN BEGIN
                    VATAmount := PrepmtAmt;
                    NewAmount := 0;
                    NewVATBaseAmount := 0;
                  END ELSE BEGIN
                    NewAmount := PrepmtAmt;
                    NewVATBaseAmount :=
                      ROUND(
                        NewAmount * (1 - PurchHeader."VAT Base Discount %" / 100),
                        Currency."Amount Rounding Precision");
                    IF VATAmountLine."VAT Base" = 0 THEN
                      VATAmount := 0
                    ELSE
                      VATAmount :=
                        TempVATAmountLineRemainder."VAT Amount" +
                        VATAmountLine."VAT Amount" * NewAmount / VATAmountLine."VAT Base";
                  END;
                  NewAmountIncludingVAT := NewAmount + ROUND(VATAmount,Currency."Amount Rounding Precision");
                END;

                "Prepayment Amount" := NewAmount;
                "Prepmt. Amt. Incl. VAT" :=
                  ROUND(NewAmountIncludingVAT,Currency."Amount Rounding Precision");
                "Prepmt. VAT Base Amt." := NewVATBaseAmount;

                IF (VATAmountLine."Line Amount" - VATAmountLine."Invoice Discount Amount") = 0 THEN
                  VATDifference := 0
                ELSE
                  IF PrepmtAmtToInvTotal = 0 THEN
                    VATDifference :=
                      VATAmountLine."VAT Difference" * ("Prepmt. Line Amount" - "Prepmt. Amt. Inv.") /
                      (VATAmountLine."Line Amount" - VATAmountLine."Invoice Discount Amount")
                  ELSE
                    VATDifference :=
                      VATAmountLine."VAT Difference" * ("Prepmt. Line Amount" - "Prepmt. Amt. Inv.") /
                      PrepmtAmtToInvTotal;
                "Prepayment VAT Difference" := ROUND(VATDifference,Currency."Amount Rounding Precision");

                //**4PS.sn
                "Modified by" := USERID; //DP00469
                "Last Date Modified" := TODAY;//DP00469
                //**4PS.en
                MODIFY;

                TempVATAmountLineRemainder."Amount Including VAT" :=
                  NewAmountIncludingVAT - ROUND(NewAmountIncludingVAT,Currency."Amount Rounding Precision");
                TempVATAmountLineRemainder."VAT Amount" := VATAmount - NewAmountIncludingVAT + NewAmount;
                TempVATAmountLineRemainder."VAT Difference" := VATDifference - "Prepayment VAT Difference";
                TempVATAmountLineRemainder.MODIFY;
              END;
            END;
          UNTIL NEXT = 0;
      END;

      OnAfterUpdateVATOnLines(PurchHeader,PurchLine,VATAmountLine,DocumentType);
    END;

    [External]
    PROCEDURE CalcVATAmountLines@35(VAR PurchHeader@1001 : Record 38;VAR PurchLine@1009 : Record 39;VAR VATAmountLine@1003 : Record 290;DocumentType@1008 : 'Invoice,Credit Memo,Statistic');
    VAR
      Currency@1000 : Record 4;
      NewAmount@1002 : Decimal;
      NewPrepmtVATDiffAmt@1010 : Decimal;
    BEGIN
      Currency.Initialize(PurchHeader."Currency Code");

      VATAmountLine.DELETEALL;

      WITH PurchLine DO BEGIN
        ApplyFilter(PurchHeader,DocumentType,PurchLine);
        IF FIND('-') THEN
          REPEAT
            NewAmount := PrepmtAmount(PurchLine,DocumentType);
            IF NewAmount <> 0 THEN BEGIN
              IF DocumentType = DocumentType::Invoice THEN
                NewAmount := "Prepmt. Line Amount";
              IF "Prepmt. VAT Calc. Type" IN
                 ["VAT Calculation Type"::"Reverse Charge VAT","VAT Calculation Type"::"Sales Tax"]
              THEN
                "VAT %" := 0;
              IF NOT VATAmountLine.GET(
                   "Prepayment VAT Identifier","Prepmt. VAT Calc. Type","Prepayment Tax Group Code",FALSE,NewAmount >= 0)
              THEN
                VATAmountLine.InsertNewLine(
                  "Prepayment VAT Identifier","Prepmt. VAT Calc. Type","Prepayment Tax Group Code",FALSE,
                  "Prepayment VAT %",NewAmount >= 0,TRUE);

              VATAmountLine."Line Amount" := VATAmountLine."Line Amount" + NewAmount;
              NewPrepmtVATDiffAmt := PrepmtVATDiffAmount(PurchLine,DocumentType);
              IF DocumentType = DocumentType::Invoice THEN
                NewPrepmtVATDiffAmt := "Prepayment VAT Difference" + "Prepmt VAT Diff. to Deduct" +
                  "Prepmt VAT Diff. Deducted";
              VATAmountLine."VAT Difference" := VATAmountLine."VAT Difference" + NewPrepmtVATDiffAmt;
              VATAmountLine.MODIFY;
            END;
          UNTIL NEXT = 0;
      END;

      VATAmountLine.UpdateLines(
        NewAmount,Currency,PurchHeader."Currency Factor",PurchHeader."Prices Including VAT",
        PurchHeader."VAT Base Discount %",PurchHeader."Tax Area Code",PurchHeader."Tax Liable",PurchHeader."Posting Date");

      OnAfterCalcVATAmountLines(PurchHeader,PurchLine,VATAmountLine,DocumentType);
    END;

    [External]
    PROCEDURE SumPrepmt@15(PurchHeader@1000 : Record 38;VAR PurchLine@1011 : Record 39;VAR VATAmountLine@1003 : Record 290;VAR TotalAmount@1001 : Decimal;VAR TotalVATAmount@1002 : Decimal;VAR VATAmountText@1004 : Text[30]);
    VAR
      TempPrepmtInvLineBuf@1005 : TEMPORARY Record 461;
      TotalPrepmtInvLineBuf@1007 : Record 461;
      TotalPrepmtInvLineBufLCY@1008 : Record 461;
      DifVATPct@1009 : Boolean;
      PrevVATPct@1010 : Decimal;
    BEGIN
      CalcVATAmountLines(PurchHeader,PurchLine,VATAmountLine,2);
      UpdateVATOnLines(PurchHeader,PurchLine,VATAmountLine,2);
      BuildInvLineBuffer(PurchHeader,PurchLine,2,TempPrepmtInvLineBuf,FALSE);
      IF TempPrepmtInvLineBuf.FIND('-') THEN BEGIN
        PrevVATPct := TempPrepmtInvLineBuf."VAT %";
        REPEAT
          RoundAmounts(PurchHeader,TempPrepmtInvLineBuf,TotalPrepmtInvLineBuf,TotalPrepmtInvLineBufLCY);
          IF TempPrepmtInvLineBuf."VAT %" <> PrevVATPct THEN
            DifVATPct := TRUE;
        UNTIL TempPrepmtInvLineBuf.NEXT = 0;
      END;
      TotalAmount := TotalPrepmtInvLineBuf.Amount;
      TotalVATAmount := TotalPrepmtInvLineBuf."VAT Amount";
      IF DifVATPct OR (TempPrepmtInvLineBuf."VAT %" = 0) THEN
        VATAmountText := Text014
      ELSE
        VATAmountText := STRSUBSTNO(Text015,PrevVATPct);
    END;

    [External]
    PROCEDURE GetPurchLines@16(PurchHeader@1000 : Record 38;DocumentType@1003 : 'Invoice,Credit Memo,Statistic';VAR ToPurchLine@1001 : Record 39);
    VAR
      PurchSetup@1004 : Record 312;
      FromPurchLine@1002 : Record 39;
      InvRoundingPurchLine@1008 : Record 39;
      TempVATAmountLine@1005 : TEMPORARY Record 290;
      TotalAmt@1007 : Decimal;
      NextLineNo@1006 : Integer;
    BEGIN
      ApplyFilter(PurchHeader,DocumentType,FromPurchLine);
      IF FromPurchLine.FIND('-') THEN BEGIN
        REPEAT
          ToPurchLine := FromPurchLine;
          //**4PS.sn
          ToPurchLine."Input by" := USERID; //DP00469
          ToPurchLine."Input Date" := TODAY; //DP00469
          //**4PS.en
          ToPurchLine.INSERT;
        UNTIL FromPurchLine.NEXT = 0;

        PurchSetup.GET;
        IF PurchSetup."Invoice Rounding" THEN BEGIN
          CalcVATAmountLines(PurchHeader,ToPurchLine,TempVATAmountLine,2);
          UpdateVATOnLines(PurchHeader,ToPurchLine,TempVATAmountLine,2);
          ToPurchLine.CALCSUMS("Prepmt. Amt. Incl. VAT");
          TotalAmt := ToPurchLine."Prepmt. Amt. Incl. VAT";
          ToPurchLine.FINDLAST;
          IF InitInvoiceRoundingLine(PurchHeader,TotalAmt,InvRoundingPurchLine) THEN
            WITH ToPurchLine DO BEGIN
              NextLineNo := "Line No." + 1;
              ToPurchLine := InvRoundingPurchLine;
              "Line No." := NextLineNo;

              IF DocumentType <> DocumentType::"Credit Memo" THEN
                "Prepmt. Line Amount" := "Line Amount"
              ELSE
                "Prepmt. Amt. Inv." := "Line Amount";
              "Prepmt. VAT Calc. Type" := "VAT Calculation Type";
              "Prepayment VAT Identifier" := "VAT Identifier";
              "Prepayment Tax Group Code" := "Tax Group Code";
              "Prepayment VAT Identifier" := "VAT Identifier";
              "Prepayment Tax Group Code" := "Tax Group Code";
              "Prepayment VAT %" := "VAT %";
              INSERT;
            END;
        END;
      END;
    END;

    LOCAL PROCEDURE BuildInvLineBuffer@31(PurchHeader@1000 : Record 38;VAR PurchLine@1001 : Record 39;DocumentType@1005 : Option;VAR PrepmtInvLineBuf@1002 : Record 461;UpdateLines@1010 : Boolean);
    VAR
      PrepmtInvLineBuf2@1003 : Record 461;
      TotalPrepmtInvLineBuffer@1009 : Record 461;
      TotalPrepmtInvLineBufferDummy@1008 : Record 461;
      PurchSetup@1011 : Record 312;
    BEGIN
      WITH PurchHeader DO BEGIN
        TempGlobalPrepmtInvLineBuf.RESET;
        TempGlobalPrepmtInvLineBuf.DELETEALL;
        PurchSetup.GET;
        ApplyFilter(PurchHeader,DocumentType,PurchLine);
        IF PurchLine.FIND('-') THEN
          REPEAT
            IF PrepmtAmount(PurchLine,DocumentType) <> 0 THEN BEGIN
              FillInvLineBuffer(PurchHeader,PurchLine,PrepmtInvLineBuf2);
              IF UpdateLines THEN
                TempGlobalPrepmtInvLineBuf.CopyWithLineNo(PrepmtInvLineBuf2,PurchLine."Line No.");
              PrepmtInvLineBuf.InsertInvLineBuffer(PrepmtInvLineBuf2);
              IF PurchSetup."Invoice Rounding" THEN
                RoundAmounts(
                  PurchHeader,PrepmtInvLineBuf2,TotalPrepmtInvLineBuffer,TotalPrepmtInvLineBufferDummy);
            END;
          UNTIL PurchLine.NEXT = 0;
        IF PurchSetup."Invoice Rounding" THEN
          IF InsertInvoiceRounding(
               PurchHeader,PrepmtInvLineBuf2,TotalPrepmtInvLineBuffer,PurchLine."Line No.")
          THEN
            PrepmtInvLineBuf.InsertInvLineBuffer(PrepmtInvLineBuf2);
      END;
    END;

    [External]
    PROCEDURE BuildInvLineBuffer2@9(PurchHeader@1000 : Record 38;VAR PurchLine@1001 : Record 39;DocumentType@1005 : 'Invoice,Credit Memo,Statistic';VAR PrepmtInvLineBuf@1002 : Record 461);
    BEGIN
      BuildInvLineBuffer(PurchHeader,PurchLine,DocumentType,PrepmtInvLineBuf,FALSE);
    END;

    [External]
    PROCEDURE FillInvLineBuffer@4(PurchHeader@1001 : Record 38;PurchLine@1002 : Record 39;VAR PrepmtInvLineBuf@1000 : Record 461);
    BEGIN
      WITH PrepmtInvLineBuf DO BEGIN
        INIT;
        "G/L Account No." := GetPrepmtAccNo(PurchLine."Gen. Bus. Posting Group",PurchLine."Gen. Prod. Posting Group");

        IF NOT PurchHeader."Compress Prepayment" THEN BEGIN
          "Line No." := PurchLine."Line No.";
          Description := PurchLine.Description;
        END;

        CopyFromPurchLine(PurchLine);
        FillFromGLAcc(PurchHeader."Compress Prepayment");

        SetAmounts(
          PurchLine."Prepayment Amount",PurchLine."Prepmt. Amt. Incl. VAT",PurchLine."Prepayment Amount",
          PurchLine."Prepayment Amount",PurchLine."Prepayment Amount",PurchLine."Prepayment VAT Difference");

        "VAT Amount" := PurchLine."Prepmt. Amt. Incl. VAT" - PurchLine."Prepayment Amount";
        "VAT Amount (ACY)" := PurchLine."Prepmt. Amt. Incl. VAT" - PurchLine."Prepayment Amount";
        "VAT Base Before Pmt. Disc." := PurchLine."Prepayment Amount";
      END;

      OnAfterFillInvLineBuffer(PrepmtInvLineBuf,PurchLine,SuppressCommit);
    END;

    LOCAL PROCEDURE InsertInvoiceRounding@25(PurchHeader@1002 : Record 38;VAR PrepmtInvLineBuf@1000 : Record 461;TotalPrepmtInvLineBuf@1001 : Record 461;PrevLineNo@1008 : Integer) : Boolean;
    VAR
      PurchLine@1007 : Record 39;
    BEGIN
      IF InitInvoiceRoundingLine(PurchHeader,TotalPrepmtInvLineBuf."Amount Incl. VAT",PurchLine) THEN BEGIN
        CreateDimensions(PurchLine);
        WITH PrepmtInvLineBuf DO BEGIN
          INIT;
          "Line No." := PrevLineNo + 10000;
          "Invoice Rounding" := TRUE;
          "G/L Account No." := PurchLine."No.";

          CopyFromPurchLine(PurchLine);

          SetAmounts(
            PurchLine."Line Amount",PurchLine."Amount Including VAT",PurchLine."Line Amount",
            PurchLine."Prepayment Amount",PurchLine."Line Amount",0);

          "VAT Amount" := PurchLine."Amount Including VAT" - PurchLine."Line Amount";
          "VAT Amount (ACY)" := PurchLine."Amount Including VAT" - PurchLine."Line Amount";
        END;
        EXIT(TRUE);
      END;

      OnAfterInsertInvoiceRounding(PurchHeader,PrepmtInvLineBuf,TotalPrepmtInvLineBuf,PrevLineNo);
    END;

    LOCAL PROCEDURE InitInvoiceRoundingLine@29(PurchHeader@1000 : Record 38;TotalAmount@1004 : Decimal;VAR PurchLine@1001 : Record 39) : Boolean;
    VAR
      Currency@1007 : Record 4;
      InvoiceRoundingAmount@1002 : Decimal;
    BEGIN
      Currency.Initialize(PurchHeader."Currency Code");
      Currency.TESTFIELD("Invoice Rounding Precision");
      InvoiceRoundingAmount :=
        -ROUND(
          TotalAmount -
          ROUND(
            TotalAmount,
            Currency."Invoice Rounding Precision",
            Currency.InvoiceRoundingDirection),
          Currency."Amount Rounding Precision");

      IF InvoiceRoundingAmount = 0 THEN
        EXIT(FALSE);

      WITH PurchLine DO BEGIN
        "Document Type" := PurchHeader."Document Type";
        "Document No." := PurchHeader."No.";
        "System-Created Entry" := TRUE;
        Type := Type::"G/L Account";
        VALIDATE("No.",GetInvRoundingAccNo(PurchHeader."Vendor Posting Group"));
        VALIDATE(Quantity,1);
        IF PurchHeader."Prices Including VAT" THEN
          VALIDATE("Direct Unit Cost",InvoiceRoundingAmount)
        ELSE
          VALIDATE(
            "Direct Unit Cost",
            ROUND(
              InvoiceRoundingAmount /
              (1 + (1 - PurchHeader."VAT Base Discount %" / 100) * "VAT %" / 100),
              Currency."Amount Rounding Precision"));
        "Prepayment Amount" := "Direct Unit Cost";
        VALIDATE("Amount Including VAT",InvoiceRoundingAmount);
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ApplyFilter@20(PurchHeader@1000 : Record 38;DocumentType@1001 : 'Invoice,Credit Memo,Statistic';VAR PurchLine@1002 : Record 39);
    BEGIN
      WITH PurchLine DO BEGIN
        RESET;
        SETRANGE("Document Type",PurchHeader."Document Type");
        SETRANGE("Document No.",PurchHeader."No.");
        SETFILTER(Type,'<>%1',Type::" ");
        IF DocumentType IN [DocumentType::Invoice,DocumentType::Statistic] THEN
          SETFILTER("Prepmt. Line Amount",'<>0')
        ELSE
          SETFILTER("Prepmt. Amt. Inv.",'<>0');
      END;

      OnAfterApplyFilter(PurchLine,PurchHeader,DocumentType);
    END;

    [External]
    PROCEDURE PrepmtAmount@12(PurchLine@1000 : Record 39;DocumentType@1001 : 'Invoice,Credit Memo,Statistic') : Decimal;
    BEGIN
      WITH PurchLine DO
        CASE DocumentType OF
          DocumentType::Statistic:
            EXIT("Prepmt. Line Amount");
          DocumentType::Invoice:
            EXIT("Prepmt. Line Amount" - "Prepmt. Amt. Inv.");
          ELSE
            EXIT("Prepmt. Amt. Inv." - "Prepmt Amt Deducted");
        END;
    END;

    LOCAL PROCEDURE CopyCommentLines@22(FromNumber@1002 : Code[20];ToDocType@1000 : Integer;ToNumber@1003 : Code[20]);
    VAR
      PurchCommentLine@1004 : Record 43;
    BEGIN
      WITH PurchCommentLine DO
        CASE ToDocType OF
          DATABASE::"Purch. Inv. Header":
            CopyComments("Document Type"::Order,"Document Type"::"Posted Invoice",FromNumber,ToNumber);
          DATABASE::"Purch. Cr. Memo Hdr.":
            CopyComments("Document Type"::Order,"Document Type"::"Posted Credit Memo",FromNumber,ToNumber);
        END;
    END;

    LOCAL PROCEDURE InsertExtendedText@17(TabNo@1000 : Integer;DocNo@1003 : Code[20];GLAccNo@1006 : Code[20];DocDate@1001 : Date;LanguageCode@1009 : Code[10];VAR PrevLineNo@1002 : Integer);
    VAR
      TempExtTextLine@1005 : TEMPORARY Record 280;
      PurchInvLine@1007 : Record 123;
      PurchCrMemoLine@1008 : Record 125;
      TransferExtText@1004 : Codeunit 378;
      NextLineNo@1010 : Integer;
    BEGIN
      TransferExtText.PrepmtGetAnyExtText(GLAccNo,TabNo,DocDate,LanguageCode,TempExtTextLine);
      IF TempExtTextLine.FIND('-') THEN BEGIN
        NextLineNo := PrevLineNo + 10000;
        REPEAT
          CASE TabNo OF
            DATABASE::"Purch. Inv. Line":
              BEGIN
                PurchInvLine.INIT;
                PurchInvLine."Document No." := DocNo;
                PurchInvLine."Line No." := NextLineNo;
                PurchInvLine.Description := TempExtTextLine.Text;
                PurchInvLine.INSERT;
              END;
            DATABASE::"Purch. Cr. Memo Line":
              BEGIN
                PurchCrMemoLine.INIT;
                PurchCrMemoLine."Document No." := DocNo;
                PurchCrMemoLine."Line No." := NextLineNo;
                PurchCrMemoLine.Description := TempExtTextLine.Text;
                PurchCrMemoLine.INSERT;
              END;
          END;
          PrevLineNo := NextLineNo;
          NextLineNo := NextLineNo + 10000;
        UNTIL TempExtTextLine.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE PostPrepmtInvLineBuffer@37(PurchHeader@1000 : Record 38;PrepmtInvLineBuffer@1010 : Record 461;DocumentType@1008 : 'Invoice,Credit Memo';PostingDescription@1007 : Text[100];DocType@1006 : Option;DocNo@1005 : Code[20];ExtDocNo@1004 : Text[35];SrcCode@1003 : Code[10];PostingNoSeriesCode@1002 : Code[20]);
    VAR
      GenJnlLine@1001 : Record 81;
    BEGIN
      WITH GenJnlLine DO BEGIN
        InitNewLine(
          PurchHeader."Posting Date",PurchHeader."Document Date",PostingDescription,
          PrepmtInvLineBuffer."Global Dimension 1 Code",PrepmtInvLineBuffer."Global Dimension 2 Code",
          PrepmtInvLineBuffer."Dimension Set ID",PurchHeader."Reason Code");

        CopyDocumentFields(DocType,DocNo,ExtDocNo,SrcCode,PostingNoSeriesCode);
        CopyFromPurchHeaderPrepmt(PurchHeader);
        CopyFromPrepmtInvoiceBuffer(PrepmtInvLineBuffer);

        Correction := (DocumentType = DocumentType::"Credit Memo") AND GLSetup."Mark Cr. Memos as Corrections";
        IF NOT PrepmtInvLineBuffer.Adjustment THEN
          "Gen. Posting Type" := "Gen. Posting Type"::Purchase;

        OnBeforePostPrepmtInvLineBuffer(GenJnlLine,PrepmtInvLineBuffer,SuppressCommit);
        RunGenJnlPostLine(GenJnlLine);
        OnAfterPostPrepmtInvLineBuffer(GenJnlLine,PrepmtInvLineBuffer,SuppressCommit,GenJnlPostLine);
      END;
    END;

    LOCAL PROCEDURE PostVendorEntry@32(PurchHeader@1000 : Record 38;TotalPrepmtInvLineBuffer@1002 : Record 461;TotalPrepmtInvLineBufferLCY@1003 : Record 461;DocumentType@1004 : 'Invoice,Credit Memo';PostingDescription@1010 : Text[100];DocType@1009 : Option;DocNo@1008 : Code[20];ExtDocNo@1007 : Text[35];SrcCode@1006 : Code[10];PostingNoSeriesCode@1005 : Code[20];CalcPmtDisc@1011 : Boolean);
    VAR
      GenJnlLine@1001 : Record 81;
    BEGIN
      WITH GenJnlLine DO BEGIN
        InitNewLine(
          PurchHeader."Posting Date",PurchHeader."Document Date",PostingDescription,
          PurchHeader."Shortcut Dimension 1 Code",PurchHeader."Shortcut Dimension 2 Code",
          PurchHeader."Dimension Set ID",PurchHeader."Reason Code");

        CopyDocumentFields(DocType,DocNo,ExtDocNo,SrcCode,PostingNoSeriesCode);

        CopyFromPurchHeaderPrepmtPost(PurchHeader,(DocumentType = DocumentType::Invoice) OR CalcPmtDisc);

        Amount := -TotalPrepmtInvLineBuffer."Amount Incl. VAT";
        "Source Currency Amount" := -TotalPrepmtInvLineBuffer."Amount Incl. VAT";
        "Amount (LCY)" := -TotalPrepmtInvLineBufferLCY."Amount Incl. VAT";
        "Sales/Purch. (LCY)" := -TotalPrepmtInvLineBufferLCY.Amount;
        "Profit (LCY)" := -TotalPrepmtInvLineBufferLCY.Amount;

        Correction := (DocumentType = DocumentType::"Credit Memo") AND GLSetup."Mark Cr. Memos as Corrections";

        OnBeforePostVendorEntry(GenJnlLine,TotalPrepmtInvLineBuffer,TotalPrepmtInvLineBufferLCY,SuppressCommit);
        GenJnlPostLine.RunWithCheck(GenJnlLine);
        OnAfterPostVendorEntry(GenJnlLine,TotalPrepmtInvLineBuffer,TotalPrepmtInvLineBufferLCY,SuppressCommit);
      END;
    END;

    LOCAL PROCEDURE PostBalancingEntry@33(PurchHeader@1011 : Record 38;TotalPrepmtInvLineBuffer@1010 : Record 461;TotalPrepmtInvLineBufferLCY@1009 : Record 461;VAR VendLedgEntry@1012 : Record 25;DocumentType@1008 : 'Invoice,Credit Memo';DocType@1006 : Option;DocNo@1005 : Code[20];ExtDocNo@1004 : Text[35];SrcCode@1003 : Code[10];PostingNoSeriesCode@1002 : Code[20]);
    VAR
      GenJnlLine@1000 : Record 81;
    BEGIN
      WITH GenJnlLine DO BEGIN
        InitNewLine(
          PurchHeader."Posting Date",PurchHeader."Document Date",PurchHeader."Posting Description",
          PurchHeader."Shortcut Dimension 1 Code",PurchHeader."Shortcut Dimension 2 Code",
          PurchHeader."Dimension Set ID",PurchHeader."Reason Code");

        IF DocType = "Document Type"::"Credit Memo" THEN
          CopyDocumentFields("Document Type"::Refund,DocNo,ExtDocNo,SrcCode,PostingNoSeriesCode)
        ELSE
          CopyDocumentFields("Document Type"::Payment,DocNo,ExtDocNo,SrcCode,PostingNoSeriesCode);

        CopyFromPurchHeaderPrepmtPost(PurchHeader,FALSE);
        IF PurchHeader."Bal. Account Type" = PurchHeader."Bal. Account Type"::"Bank Account" THEN
          "Bal. Account Type" := "Bal. Account Type"::"Bank Account";
        "Bal. Account No." := PurchHeader."Bal. Account No.";

        Amount := TotalPrepmtInvLineBuffer."Amount Incl. VAT" + VendLedgEntry."Remaining Pmt. Disc. Possible";
        "Source Currency Amount" := Amount;
        VendLedgEntry.CALCFIELDS(Amount);
        IF VendLedgEntry.Amount = 0 THEN
          "Amount (LCY)" := TotalPrepmtInvLineBufferLCY."Amount Incl. VAT"
        ELSE
          "Amount (LCY)" :=
            TotalPrepmtInvLineBufferLCY."Amount Incl. VAT" +
            ROUND(VendLedgEntry."Remaining Pmt. Disc. Possible" / VendLedgEntry."Adjusted Currency Factor");

        Correction := (DocumentType = DocumentType::"Credit Memo") AND GLSetup."Mark Cr. Memos as Corrections";

        "Applies-to Doc. Type" := DocType;
        "Applies-to Doc. No." := DocNo;

        OnBeforePostBalancingEntry(GenJnlLine,VendLedgEntry,TotalPrepmtInvLineBuffer,TotalPrepmtInvLineBufferLCY,SuppressCommit);
        GenJnlPostLine.RunWithCheck(GenJnlLine);
        OnAfterPostBalancingEntry(GenJnlLine,VendLedgEntry,TotalPrepmtInvLineBuffer,TotalPrepmtInvLineBufferLCY,SuppressCommit);
      END;
    END;

    LOCAL PROCEDURE RunGenJnlPostLine@23(VAR GenJnlLine@1000 : Record 81);
    BEGIN
      GenJnlPostLine.RunWithCheck(GenJnlLine);
    END;

    [External]
    PROCEDURE UpdatePrepmtAmountOnPurchLines@13(PurchHeader@1001 : Record 38;NewTotalPrepmtAmount@1000 : Decimal);
    VAR
      Currency@1004 : Record 4;
      PurchLine@1002 : Record 39;
      TotalLineAmount@1003 : Decimal;
      TotalPrepmtAmount@1008 : Decimal;
      TotalPrepmtAmtInv@1010 : Decimal;
      LastLineNo@1009 : Integer;
    BEGIN
      Currency.Initialize(PurchHeader."Currency Code");

      WITH PurchLine DO BEGIN
        SETRANGE("Document Type",PurchHeader."Document Type");
        SETRANGE("Document No.",PurchHeader."No.");
        SETFILTER(Type,'<>%1',Type::" ");
        SETFILTER("Line Amount",'<>0');
        SETFILTER("Prepayment %",'<>0');
        LOCKTABLE;
        IF FIND('-') THEN
          REPEAT
            TotalLineAmount := TotalLineAmount + "Line Amount";
            TotalPrepmtAmtInv := TotalPrepmtAmtInv + "Prepmt. Amt. Inv.";
            LastLineNo := "Line No.";
          UNTIL NEXT = 0
        ELSE
          ERROR(Text017,FIELDCAPTION("Prepayment %"));
        IF TotalLineAmount = 0 THEN
          ERROR(Text013,NewTotalPrepmtAmount);
        IF NOT (NewTotalPrepmtAmount IN [TotalPrepmtAmtInv ..TotalLineAmount]) THEN
          ERROR(Text016,TotalPrepmtAmtInv,TotalLineAmount);
        IF FIND('-') THEN
          REPEAT
            IF "Line No." <> LastLineNo THEN
              VALIDATE(
                "Prepmt. Line Amount",
                ROUND(
                  NewTotalPrepmtAmount * "Line Amount" / TotalLineAmount,
                  Currency."Amount Rounding Precision"))
            ELSE
              VALIDATE("Prepmt. Line Amount",NewTotalPrepmtAmount - TotalPrepmtAmount);
            TotalPrepmtAmount := TotalPrepmtAmount + "Prepmt. Line Amount";
            //**4PS.sn
            "Modified by" := USERID; //DP00469
            "Last Date Modified" := TODAY;//DP00469
            //**4PS.en
            MODIFY;
          UNTIL NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE CreateDimensions@26(VAR PurchLine@1009 : Record 39);
    VAR
      SourceCodeSetup@1006 : Record 242;
      DimMgt@1010 : Codeunit 408;
      TableID@1007 : ARRAY [10] OF Integer;
      No@1008 : ARRAY [10] OF Code[20];
    BEGIN
      SourceCodeSetup.GET;
      TableID[1] := DATABASE::"Work Center";
      No[1] := PurchLine."Work Center No.";
      TableID[2] := DATABASE::"G/L Account";
      No[2] := PurchLine."No.";
      TableID[3] := DATABASE::Job;
      No[3] := PurchLine."Job No.";
      //**4PS.so
      //TableID[4] := DATABASE::"Responsibility Center";
      //No[4] := PurchLine."Responsibility Center";
      //**4PS.eo
      //**4PS.sn
      TableID[4] := DATABASE::"Service Order";  //** 4PS 10-06-2010
      No[4] := PurchLine."Service Order No.";   //** 4PS 10-06-2010
      TableID[5] := DATABASE::"Responsibility Center";
      No[5] := PurchLine."Responsibility Center";
      //**4PS.en
      PurchLine."Shortcut Dimension 1 Code" := '';
      PurchLine."Shortcut Dimension 2 Code" := '';
      PurchLine."Dimension Set ID" :=
        DimMgt.GetRecDefaultDimID(
          PurchLine,0,TableID,No,SourceCodeSetup.Purchases,
          PurchLine."Shortcut Dimension 1 Code",PurchLine."Shortcut Dimension 2 Code",PurchLine."Dimension Set ID",DATABASE::Vendor);
    END;

    [External]
    PROCEDURE GetPurchLinesToDeduct@10(PurchHeader@1000 : Record 38;VAR PurchLines@1002 : Record 39);
    VAR
      PurchLine@1001 : Record 39;
    BEGIN
      ApplyFilter(PurchHeader,1,PurchLine);
      IF PurchLine.FINDSET THEN
        REPEAT
          IF (PrepmtAmount(PurchLine,0) <> 0) AND (PrepmtAmount(PurchLine,1) <> 0) THEN BEGIN
            PurchLines := PurchLine;
            PurchLines.INSERT;
          END;
        UNTIL PurchLine.NEXT = 0;
    END;

    LOCAL PROCEDURE PrepmtVATDiffAmount@21(PurchLine@1000 : Record 39;DocumentType@1001 : 'Invoice,Credit Memo,Statistic') : Decimal;
    BEGIN
      WITH PurchLine DO
        CASE DocumentType OF
          DocumentType::Statistic:
            EXIT("Prepayment VAT Difference");
          DocumentType::Invoice:
            EXIT("Prepayment VAT Difference");
          ELSE
            EXIT("Prepmt VAT Diff. to Deduct");
        END;
    END;

    LOCAL PROCEDURE UpdatePurchaseDocument@11(VAR PurchaseHeader@1000 : Record 38;VAR PurchLine@1001 : Record 39;DocumentType@1002 : 'Invoice,Credit Memo';GenJnlLineDocNo@1003 : Code[20]);
    BEGIN
      WITH PurchaseHeader DO BEGIN
        PurchLine.RESET;
        PurchLine.SETRANGE("Document Type","Document Type");
        PurchLine.SETRANGE("Document No.","No.");
        IF DocumentType = DocumentType::Invoice THEN BEGIN
          "Last Prepayment No." := GenJnlLineDocNo;
          "Prepayment No." := '';
          PurchLine.SETFILTER("Prepmt. Line Amount",'<>0');
          IF PurchLine.FINDSET(TRUE) THEN
            REPEAT
              IF PurchLine."Prepmt. Line Amount" <> PurchLine."Prepmt. Amt. Inv." THEN BEGIN
                PurchLine."Prepmt. Amt. Inv." := PurchLine."Prepmt. Line Amount";
                PurchLine."Prepmt. Amount Inv. Incl. VAT" := PurchLine."Prepmt. Amt. Incl. VAT";
                PurchLine.CalcPrepaymentToDeduct;
                PurchLine."Prepmt VAT Diff. to Deduct" :=
                  PurchLine."Prepmt VAT Diff. to Deduct" + PurchLine."Prepayment VAT Difference";
                PurchLine."Prepayment VAT Difference" := 0;
                //**4PS.sn
                PurchLine."Modified by" := USERID; //DP00469
                PurchLine."Last Date Modified" := TODAY;//DP00469
                //**4PS.en
                PurchLine.MODIFY;
              END;
            UNTIL PurchLine.NEXT = 0;
        END ELSE BEGIN
          "Last Prepmt. Cr. Memo No." := GenJnlLineDocNo;
          "Prepmt. Cr. Memo No." := '';
          PurchLine.SETFILTER("Prepmt. Amt. Inv.",'<>0');
          IF PurchLine.FINDSET(TRUE) THEN
            REPEAT
              PurchLine."Prepmt. Amt. Inv." := PurchLine."Prepmt Amt Deducted";
              IF "Prices Including VAT" THEN
                PurchLine."Prepmt. Amount Inv. Incl. VAT" := PurchLine."Prepmt. Amt. Inv."
              ELSE
                PurchLine."Prepmt. Amount Inv. Incl. VAT" :=
                  ROUND(
                    PurchLine."Prepmt. Amt. Inv." * (100 + PurchLine."Prepayment VAT %") / 100,
                    GetCurrencyAmountRoundingPrecision(PurchLine."Currency Code"));
              PurchLine."Prepmt. Amt. Incl. VAT" := PurchLine."Prepmt. Amount Inv. Incl. VAT";
              PurchLine."Prepayment Amount" := PurchLine."Prepmt. Amt. Inv.";
              PurchLine."Prepmt Amt to Deduct" := 0;
              PurchLine."Prepmt VAT Diff. to Deduct" := 0;
              PurchLine."Prepayment VAT Difference" := 0;
              //**4PS.sn
              PurchLine."Modified by" := USERID; //DP00469
              PurchLine."Last Date Modified" := TODAY;//DP00469
              //**4PS.en
              PurchLine.MODIFY;
            UNTIL PurchLine.NEXT = 0;
        END;
      END;
    END;

    LOCAL PROCEDURE UpdatePostedPurchaseDocument@102(DocumentType@1002 : 'Invoice,Credit Memo';DocumentNo@1004 : Code[20]);
    VAR
      VendorLedgerEntry@1000 : Record 25;
      PurchInvHeader@1001 : Record 122;
      PurchCrMemoHdr@1003 : Record 124;
    BEGIN
      CASE DocumentType OF
        DocumentType::Invoice:
          BEGIN
            VendorLedgerEntry.SETRANGE("Document Type",VendorLedgerEntry."Document Type"::Invoice);
            VendorLedgerEntry.SETRANGE("Document No.",DocumentNo);
            VendorLedgerEntry.FINDFIRST;
            PurchInvHeader.GET(DocumentNo);
            PurchInvHeader."Vendor Ledger Entry No." := VendorLedgerEntry."Entry No.";
            PurchInvHeader.MODIFY;
          END;
        DocumentType::"Credit Memo":
          BEGIN
            VendorLedgerEntry.SETRANGE("Document Type",VendorLedgerEntry."Document Type"::"Credit Memo");
            VendorLedgerEntry.SETRANGE("Document No.",DocumentNo);
            VendorLedgerEntry.FINDFIRST;
            PurchCrMemoHdr.GET(DocumentNo);
            PurchCrMemoHdr."Vendor Ledger Entry No." := VendorLedgerEntry."Entry No.";
            PurchCrMemoHdr.MODIFY;
          END;
      END;

      OnAfterUpdatePostedPurchDocument(DocumentType,DocumentNo,SuppressCommit);
    END;

    LOCAL PROCEDURE InsertPurchInvHeader@14(VAR PurchInvHeader@1001 : Record 122;PurchHeader@1002 : Record 38;PostingDescription@1003 : Text[100];GenJnlLineDocNo@1004 : Code[20];SrcCode@1005 : Code[10];PostingNoSeriesCode@1000 : Code[20]);
    BEGIN
      WITH PurchHeader DO BEGIN
        PurchInvHeader.INIT;
        PurchInvHeader.TRANSFERFIELDS(PurchHeader);
        PurchInvHeader."Posting Description" := PostingDescription;
        PurchInvHeader."Payment Terms Code" := "Prepmt. Payment Terms Code";
        PurchInvHeader."Due Date" := "Prepayment Due Date";
        PurchInvHeader."Pmt. Discount Date" := "Prepmt. Pmt. Discount Date";
        PurchInvHeader."Payment Discount %" := "Prepmt. Payment Discount %";
        PurchInvHeader."No." := GenJnlLineDocNo;
        PurchInvHeader."Pre-Assigned No. Series" := '';
        PurchInvHeader."Source Code" := SrcCode;
        PurchInvHeader."User ID" := USERID;
        PurchInvHeader."No. Printed" := 0;
        PurchInvHeader."Prepayment Invoice" := TRUE;
        PurchInvHeader."Prepayment Order No." := "No.";
        PurchInvHeader."No. Series" := PostingNoSeriesCode;
        OnBeforePurchInvHeaderInsert(PurchInvHeader,PurchHeader,SuppressCommit);
        PurchInvHeader.INSERT;
        OnAfterPurchInvHeaderInsert(PurchInvHeader,PurchHeader,SuppressCommit);
      END;
    END;

    LOCAL PROCEDURE InsertPurchCrMemoHeader@27(VAR PurchCrMemoHdr@1001 : Record 124;PurchHeader@1002 : Record 38;PostingDescription@1003 : Text[100];GenJnlLineDocNo@1004 : Code[20];SrcCode@1005 : Code[10];PostingNoSeriesCode@1000 : Code[20];CalcPmtDiscOnCrMemos@1006 : Boolean);
    BEGIN
      WITH PurchHeader DO BEGIN
        PurchCrMemoHdr.INIT;
        PurchCrMemoHdr.TRANSFERFIELDS(PurchHeader);
        PurchCrMemoHdr."Payment Terms Code" := "Prepmt. Payment Terms Code";
        PurchCrMemoHdr."Pmt. Discount Date" := "Prepmt. Pmt. Discount Date";
        PurchCrMemoHdr."Payment Discount %" := "Prepmt. Payment Discount %";
        IF ("Prepmt. Payment Terms Code" <> '') AND NOT CalcPmtDiscOnCrMemos THEN BEGIN
          PurchCrMemoHdr."Payment Discount %" := 0;
          PurchCrMemoHdr."Pmt. Discount Date" := 0D;
        END;
        PurchCrMemoHdr."Posting Description" := PostingDescription;
        PurchCrMemoHdr."Due Date" := "Prepayment Due Date";
        PurchCrMemoHdr."No." := GenJnlLineDocNo;
        PurchCrMemoHdr."Pre-Assigned No. Series" := '';
        PurchCrMemoHdr."Source Code" := SrcCode;
        PurchCrMemoHdr."User ID" := USERID;
        PurchCrMemoHdr."No. Printed" := 0;
        PurchCrMemoHdr."Prepayment Credit Memo" := TRUE;
        PurchCrMemoHdr."Prepayment Order No." := "No.";
        PurchCrMemoHdr.Correction := GLSetup."Mark Cr. Memos as Corrections";
        PurchCrMemoHdr."No. Series" := PostingNoSeriesCode;
        OnBeforePurchCrMemoHeaderInsert(PurchCrMemoHdr,PurchHeader,SuppressCommit);
        PurchCrMemoHdr.INSERT;
        OnAfterPurchCrMemoHeaderInsert(PurchCrMemoHdr,PurchHeader,SuppressCommit);
      END;
    END;

    LOCAL PROCEDURE GetCalcPmtDiscOnCrMemos@18(PrepmtPmtTermsCode@1000 : Code[10]) : Boolean;
    VAR
      PaymentTerms@1001 : Record 3;
    BEGIN
      IF PrepmtPmtTermsCode = '' THEN
        EXIT(FALSE);
      PaymentTerms.GET(PrepmtPmtTermsCode);
      EXIT(PaymentTerms."Calc. Pmt. Disc. on Cr. Memos");
    END;

    LOCAL PROCEDURE InsertPurchInvLine@19(PurchInvHeader@1001 : Record 122;LineNo@1002 : Integer;PrepmtInvLineBuffer@1004 : Record 461);
    VAR
      PurchInvLine@1000 : Record 123;
    BEGIN
      WITH PrepmtInvLineBuffer DO BEGIN
        PurchInvLine.INIT;
        PurchInvLine."Document No." := PurchInvHeader."No.";
        PurchInvLine."Line No." := LineNo;
        PurchInvLine."Buy-from Vendor No." := PurchInvHeader."Buy-from Vendor No.";
        PurchInvLine."Pay-to Vendor No." := PurchInvHeader."Pay-to Vendor No.";
        PurchInvLine.Type := PurchInvLine.Type::"G/L Account";
        PurchInvLine."No." := "G/L Account No.";
        PurchInvLine."Posting Date" := PurchInvHeader."Posting Date";
        PurchInvLine."Shortcut Dimension 1 Code" := "Global Dimension 1 Code";
        PurchInvLine."Shortcut Dimension 2 Code" := "Global Dimension 2 Code";
        PurchInvLine."Dimension Set ID" := "Dimension Set ID";
        PurchInvLine.Description := Description;
        PurchInvLine.Quantity := 1;
        IF PurchInvHeader."Prices Including VAT" THEN BEGIN
          PurchInvLine."Direct Unit Cost" := "Amount Incl. VAT";
          PurchInvLine."Line Amount" := "Amount Incl. VAT";
        END ELSE BEGIN
          PurchInvLine."Direct Unit Cost" := Amount;
          PurchInvLine."Line Amount" := Amount;
        END;
        PurchInvLine."Gen. Bus. Posting Group" := "Gen. Bus. Posting Group";
        PurchInvLine."Gen. Prod. Posting Group" := "Gen. Prod. Posting Group";
        PurchInvLine."VAT Bus. Posting Group" := "VAT Bus. Posting Group";
        PurchInvLine."VAT Prod. Posting Group" := "VAT Prod. Posting Group";
        PurchInvLine."VAT %" := "VAT %";
        PurchInvLine.Amount := Amount;
        PurchInvLine."VAT Difference" := "VAT Difference";
        PurchInvLine."Amount Including VAT" := "Amount Incl. VAT";
        PurchInvLine."VAT Calculation Type" := "VAT Calculation Type";
        PurchInvLine."VAT Base Amount" := "VAT Base Amount";
        PurchInvLine."VAT Identifier" := "VAT Identifier";
        PurchInvLine."Job No." := "Job No.";
        PurchInvLine."Job Task No." := "Job Task No.";
        OnBeforePurchInvLineInsert(PurchInvLine,PurchInvHeader,PrepmtInvLineBuffer,SuppressCommit);
        PurchInvLine.INSERT;
        OnAfterPurchInvLineInsert(PurchInvLine,PurchInvHeader,PrepmtInvLineBuffer,SuppressCommit);
      END;
    END;

    LOCAL PROCEDURE InsertPurchCrMemoLine@39(PurchCrMemoHdr@1001 : Record 124;LineNo@1002 : Integer;PrepmtInvLineBuffer@1004 : Record 461);
    VAR
      PurchCrMemoLine@1000 : Record 125;
    BEGIN
      WITH PrepmtInvLineBuffer DO BEGIN
        PurchCrMemoLine.INIT;
        PurchCrMemoLine."Document No." := PurchCrMemoHdr."No.";
        PurchCrMemoLine."Line No." := LineNo;
        PurchCrMemoLine."Buy-from Vendor No." := PurchCrMemoHdr."Buy-from Vendor No.";
        PurchCrMemoLine."Pay-to Vendor No." := PurchCrMemoHdr."Pay-to Vendor No.";
        PurchCrMemoLine.Type := PurchCrMemoLine.Type::"G/L Account";
        PurchCrMemoLine."No." := "G/L Account No.";
        PurchCrMemoLine."Posting Date" := PurchCrMemoHdr."Posting Date";
        PurchCrMemoLine."Shortcut Dimension 1 Code" := "Global Dimension 1 Code";
        PurchCrMemoLine."Shortcut Dimension 2 Code" := "Global Dimension 2 Code";
        PurchCrMemoLine."Dimension Set ID" := "Dimension Set ID";
        PurchCrMemoLine.Description := Description;
        PurchCrMemoLine.Quantity := 1;
        IF PurchCrMemoHdr."Prices Including VAT" THEN BEGIN
          PurchCrMemoLine."Direct Unit Cost" := "Amount Incl. VAT";
          PurchCrMemoLine."Line Amount" := "Amount Incl. VAT";
        END ELSE BEGIN
          PurchCrMemoLine."Direct Unit Cost" := Amount;
          PurchCrMemoLine."Line Amount" := Amount;
        END;
        PurchCrMemoLine."Gen. Bus. Posting Group" := "Gen. Bus. Posting Group";
        PurchCrMemoLine."Gen. Prod. Posting Group" := "Gen. Prod. Posting Group";
        PurchCrMemoLine."VAT Bus. Posting Group" := "VAT Bus. Posting Group";
        PurchCrMemoLine."VAT Prod. Posting Group" := "VAT Prod. Posting Group";
        PurchCrMemoLine."VAT %" := "VAT %";
        PurchCrMemoLine.Amount := Amount;
        PurchCrMemoLine."VAT Difference" := "VAT Difference";
        PurchCrMemoLine."Amount Including VAT" := "Amount Incl. VAT";
        PurchCrMemoLine."VAT Calculation Type" := "VAT Calculation Type";
        PurchCrMemoLine."VAT Base Amount" := "VAT Base Amount";
        PurchCrMemoLine."VAT Identifier" := "VAT Identifier";
        PurchCrMemoLine."Job No." := "Job No.";
        PurchCrMemoLine."Job Task No." := "Job Task No.";
        OnBeforePurchCrMemoLineInsert(PurchCrMemoLine,PurchCrMemoHdr,PrepmtInvLineBuffer,SuppressCommit);
        PurchCrMemoLine.INSERT;
        OnAfterPurchCrMemoLineInsert(PurchCrMemoLine,PurchCrMemoHdr,PrepmtInvLineBuffer,SuppressCommit);
      END;
    END;

    [External]
    PROCEDURE GetPreviewMode@77() : Boolean;
    BEGIN
      EXIT(PreviewMode);
    END;

    [External]
    PROCEDURE GetSuppressCommit@78() : Boolean;
    BEGIN
      EXIT(SuppressCommit);
    END;

    [External]
    PROCEDURE SetSuppressCommit@69(NewSuppressCommit@1000 : Boolean);
    BEGIN
      SuppressCommit := NewSuppressCommit;
    END;

    PROCEDURE SetPreviewMode@34(NewPreviewMode@1000 : Boolean);
    BEGIN
      PreviewMode := NewPreviewMode;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterApplyFilter@80(VAR PurchaseLine@1000 : Record 39;PurchaseHeader@1001 : Record 38;DocumentType@1002 : Option);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCalcVATAmountLines@75(PurchaseHeader@1003 : Record 38;VAR PurchaseLine@1002 : Record 39;VAR VATAmountLine@1001 : Record 290;DocumentType@1000 : 'Invoice,Credit Memo,Statistic');
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCheckPrepmtDoc@55(PurchHeader@1001 : Record 38;DocumentType@1000 : 'Invoice,Credit Memo');
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterFillInvLineBuffer@64(VAR PrepmtInvLineBuf@1000 : Record 461;PurchLine@1001 : Record 39;CommitIsSuppressed@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInsertInvoiceRounding@72(PurchaseHeader@1000 : Record 38;VAR PrepmtInvLineBuffer@1001 : Record 461;VAR TotalPrepmtInvLineBuf@1002 : Record 461;VAR PrevLineNo@1003 : Integer);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPostPrepayments@56(VAR PurchHeader@1000 : Record 38;DocumentType@1001 : 'Invoice,Credit Memo';CommitIsSuppressed@1002 : Boolean;VAR PurchInvHeader@1003 : Record 122;VAR PurchCrMemoHdr@1004 : Record 124);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPostBalancingEntry@58(VAR GenJnlLine@1010 : Record 81;VendLedgEntry@1000 : Record 25;TotalPrepmtInvLineBuffer@1009 : Record 461;TotalPrepmtInvLineBufferLCY@1008 : Record 461;CommitIsSupressed@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPostVendorEntry@62(VAR GenJnlLine@1003 : Record 81;TotalPrepmtInvLineBuffer@1001 : Record 461;TotalPrepmtInvLineBufferLCY@1000 : Record 461;CommitIsSupressed@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPostPrepmtInvLineBuffer@59(VAR GenJnlLine@1000 : Record 81;PrepmtInvLineBuffer@1001 : Record 461;CommitIsSuppressed@1002 : Boolean;VAR GenJnlPostLine@1003 : Codeunit 12);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPurchInvHeaderInsert@68(VAR PurchInvHeader@1000 : Record 122;PurchHeader@1001 : Record 38;CommitIsSupressed@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPurchInvLineInsert@67(VAR PurchInvLine@1000 : Record 123;PurchInvHeader@1001 : Record 122;PrepmtInvLineBuffer@1002 : Record 461;CommitIsSupressed@1003 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPurchCrMemoHeaderInsert@63(VAR PurchCrMemoHdr@1000 : Record 124;PurchHeader@1001 : Record 38;CommitIsSupressed@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPurchCrMemoLineInsert@60(VAR PurchCrMemoLine@1000 : Record 125;PurchCrMemoHdr@1001 : Record 124;PrepmtInvLineBuffer@1002 : Record 461;CommitIsSupressed@1003 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterRoundAmounts@73(PurchaseHeader@1002 : Record 38;VAR PrepmtInvLineBuffer@1001 : Record 461;VAR TotalPrepmtInvLineBuf@1000 : Record 461;VAR TotalPrepmtInvLineBufLCY@1004 : Record 461);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterUpdatePostedPurchDocument@70(DocumentType@1000 : 'Invoice,Credit Memo';DocumentNo@1001 : Code[20];CommitIsSuppressed@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterUpdateVATOnLines@71(PurchaseHeader@1000 : Record 38;VAR PurchaseLine@1001 : Record 39;VATAmountLine@1002 : Record 290;DocumentType@1003 : 'Invoice,Credit Memo,Statistic');
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeInvoice@65(VAR PurchaseHeader@1000 : Record 38;VAR Handled@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCreditMemo@66(VAR PurchaseHeader@1001 : Record 38;VAR Handled@1000 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostPrepayments@52(VAR PurchHeader@1000 : Record 38;DocumentType@1001 : 'Invoice,Credit Memo';CommitIsSuppressed@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePurchInvHeaderInsert@38(VAR PurchInvHeader@1000 : Record 122;PurchHeader@1001 : Record 38;CommitIsSupressed@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePurchInvLineInsert@44(VAR PurchInvLine@1000 : Record 123;PurchInvHeader@1001 : Record 122;PrepmtInvLineBuffer@1002 : Record 461;CommitIsSupressed@1003 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePurchCrMemoHeaderInsert@46(VAR PurchCrMemoHdr@1000 : Record 124;PurchHeader@1001 : Record 38;CommitIsSupressed@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePurchCrMemoLineInsert@45(VAR PurchCrMemoLine@1000 : Record 125;PurchCrMemoHdr@1001 : Record 124;PrepmtInvLineBuffer@1002 : Record 461;CommitIsSupressed@1003 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostBalancingEntry@57(VAR GenJnlLine@1010 : Record 81;VendLedgEntry@1000 : Record 25;TotalPrepmtInvLineBuffer@1009 : Record 461;TotalPrepmtInvLineBufferLCY@1008 : Record 461;CommitIsSupressed@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostVendorEntry@61(VAR GenJnlLine@1003 : Record 81;TotalPrepmtInvLineBuffer@1001 : Record 461;TotalPrepmtInvLineBufferLCY@1000 : Record 461;CommitIsSupressed@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostPrepmtInvLineBuffer@54(VAR GenJnlLine@1000 : Record 81;PrepmtInvLineBuffer@1001 : Record 461;CommitIsSupressed@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnUpdateVATOnLinesOnAfterGetRemainder@79(VAR VATAmountLineRemainder@1000 : Record 290;VAR RemainderExists@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnUpdateVATOnLinesOnAfterVATAmountLineGet@81(VAR VATAmountLine@1000 : Record 290);
    BEGIN
    END;

    BEGIN
    END.
  }
}

