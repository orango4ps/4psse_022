OBJECT Codeunit 11012389 IDYN DC - Lookup Doc. Values
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    TableNo=6085593;
    OnRun=VAR
            DimValue@1000000001 : Record 349;
            Job@1000000003 : Record 11072003;
            ServiceOrder@1100528200 : Record 11012823;
            UnitOfMeasure@1000000004 : Record 204;
            Comp@1000000005 : Record 2000000006;
            VendBankAcc@1000000006 : Record 288;
            Document@1000000008 : Record 6085590;
            PurchHeader@1000000009 : Record 38;
            VATProdPostingGroup@1000000011 : Record 324;
            DimValues@1000000002 : Page 537;
            CostObjectList@1000000000 : Page 11012021;
            Companies@1000000010 : Page 357;
            VendorNo@1000000007 : Code[20];
            GeneralLedgerSetup@1100528201 : Record 98;
            PurchInvHeader@1100409005 : Record 122;
            PurchCrMemoHdr@1100409004 : Record 124;
            RepSelect@1100409003 : Report 11020298;
            NotPosted@1100409002 : Boolean;
            PostedInvoice@1100409001 : Boolean;
            PostedCreditMemo@1100409000 : Boolean;
          BEGIN
            CASE Code OF
              'KOSTENPLAATS',
              'DEPARTMENT':
                BEGIN
                  GeneralLedgerSetup.GET;
                  DimValue.SETRANGE("Dimension Code",GeneralLedgerSetup."Global Dimension 1 Code");
                  DimValue.SETRANGE("Global Dimension No.",1);
                  DimValues.SETTABLEVIEW(DimValue);
                  DimValues.LOOKUPMODE(TRUE);
                  IF DimValues.RUNMODAL = ACTION::LookupOK THEN BEGIN
                    DimValues.GETRECORD(DimValue);
                    "Value (Text)" := DimValue.Code;
                  END;
                END;
              'KOSTENDRAGER',
              'COSTOBJECT':
                BEGIN
                  CostObjectList.LOOKUPMODE(TRUE);
                  IF CostObjectList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                    CostObjectList.GETRECORD(DimValue);
                    "Value (Text)" := DimValue.Code;
                  END;
                END;
              'PROJECT':
                BEGIN
                  IF PAGE.RUNMODAL(0, Job) = ACTION::LookupOK THEN
                    "Value (Text)" := Job."No.";
                END;
              'UNITOM',
              'EENHEID':
                BEGIN
                  IF PAGE.RUNMODAL(0, UnitOfMeasure) = ACTION::LookupOK THEN
                    "Value (Text)" := UnitOfMeasure.Code;
                END;
              'BEDRIJF',
              'COMPANY':
                BEGIN
                  Companies.LOOKUPMODE(TRUE);
                  IF Companies.RUNMODAL = ACTION::LookupOK THEN BEGIN
                    Companies.GETRECORD(Comp);
                    "Value (Text)" := Comp.Name;
                    MODIFY;
                  END;
                END;
              'BANKREKNR',
              'BANKACCOUNT',
              'BANKACCOUNTNO':
                BEGIN
                  Document.GET("Document No.");
                  VendorNo := Document.GetSourceID;
                  VendBankAcc.SETRANGE("Vendor No.", VendorNo);
                  IF PAGE.RUNMODAL(0, VendBankAcc) = ACTION::LookupOK THEN
                    IF VendBankAcc.IBAN <> '' THEN
                      "Value (Text)" := VendBankAcc.IBAN
                    ELSE
                      "Value (Text)" := VendBankAcc."Bank Account No.";
                END;
              'FACTUURNR',
              'INVOICENO':
                BEGIN

                  RepSelect.RUNMODAL;
                  RepSelect.GetVariables(NotPosted,PostedInvoice,PostedCreditMemo);
                  Document.GET("Document No.");
                  IF NotPosted THEN BEGIN
                    PurchHeader.SETFILTER("Document Type", '%1|%2',
                                          PurchHeader."Document Type"::Invoice,
                                          PurchHeader."Document Type"::"Credit Memo");
                    PurchHeader.SETRANGE("Pay-to Vendor No.", Document.GetSourceID);
                    IF PAGE.RUNMODAL(PAGE::"Purchase List (Image DC)", PurchHeader) = ACTION::LookupOK THEN
                      "Value (Text)" := PurchHeader."No.";
                  END;
                  IF PostedInvoice THEN BEGIN
                    PurchInvHeader.SETRANGE("Pay-to Vendor No.", Document.GetSourceID);
                    IF PAGE.RUNMODAL(PAGE::"Posted Purchase Invoices(ImDC)", PurchInvHeader) = ACTION::LookupOK THEN
                      "Value (Text)" := PurchInvHeader."No.";
                  END;
                  IF PostedCreditMemo THEN BEGIN
                    PurchCrMemoHdr.SETRANGE("Pay-to Vendor No.", Document.GetSourceID);
                    IF PAGE.RUNMODAL(PAGE::"Posted Purchase Cr M  (ImDC)", PurchCrMemoHdr) = ACTION::LookupOK THEN
                      "Value (Text)" := PurchCrMemoHdr."No.";
                  END;

                END;
              'BTWCODE',
              'VATCODE':
                BEGIN
                  IF PAGE.RUNMODAL(0, VATProdPostingGroup) = ACTION::LookupOK THEN
                    "Value (Text)" := VATProdPostingGroup.Code;
                END;
              'SERVICEORDER':
                BEGIN
                  IF PAGE.RUNMODAL(0, ServiceOrder) = ACTION::LookupOK THEN
                    "Value (Text)" := ServiceOrder."No.";
                END;

            END;
          END;

  }
  CODE
  {

    BEGIN
    {
      New codeunit; lookup possibilities for searching document values for IDYN Document Capture
    }
    END.
  }
}

