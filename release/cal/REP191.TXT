OBJECT Report 191 Create Finance Charge Memos
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PSSE;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create Finance Charge Memos;
               NOR=Opprett rentenotaer;
               SVE=Skapa r„ntefakturor];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  CustLedgEntry.COPY(CustLedgEntry2);
                END;

  }
  DATASET
  {
    { 6836;    ;DataItem;                    ;
               DataItemTable=Table18;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=BEGIN
                               IF FinChrgMemoHeaderReq."Document Date" = 0D THEN
                                 ERROR(Text000,FinChrgMemoHeaderReq.FIELDCAPTION("Document Date"));
                               FILTERGROUP := 2;
                               SETFILTER("Fin. Charge Terms Code",'<>%1','');
                               FILTERGROUP := 0;
                               NoOfRecords := COUNT;
                               IF NoOfRecords = 1 THEN
                                 Window.OPEN(Text001)
                               ELSE BEGIN
                                 Window.OPEN(Text002);
                                 OldDateTime := CURRENTDATETIME;
                               END;
                             END;

               OnAfterGetRecord=BEGIN
                                  RecordNo := RecordNo + 1;
                                  CLEAR(MakeFinChrgMemo);
                                  MakeFinChrgMemo.Set(Customer,CustLedgEntry,FinChrgMemoHeaderReq);
                                  IF NoOfRecords = 1 THEN BEGIN
                                    MakeFinChrgMemo.Code;
                                    MARK := FALSE;
                                  END ELSE BEGIN
                                    NewDateTime := CURRENTDATETIME;
                                    IF (NewDateTime - OldDateTime > 100) OR (NewDateTime < OldDateTime) THEN BEGIN
                                      NewProgress := ROUND(RecordNo / NoOfRecords * 100,1);
                                      IF NewProgress <> OldProgress THEN BEGIN
                                        Window.UPDATE(1,NewProgress * 100);
                                        OldProgress := NewProgress;
                                      END;
                                      OldDateTime := CURRENTDATETIME;
                                    END;
                                    MARK := NOT MakeFinChrgMemo.Code;
                                  END;
                                  COMMIT;
                                END;

               OnPostDataItem=VAR
                                ConfirmManagement@1000 : Codeunit 27;
                              BEGIN
                                Window.CLOSE;
                                MARKEDONLY := TRUE;
                                IF FINDFIRST THEN
                                  IF ConfirmManagement.ConfirmProcessUI(Text003,TRUE) THEN
                                    PAGE.RUNMODAL(0,Customer);
                              END;

               ReqFilterFields=No. }

    { 9065;    ;DataItem;CustLedgEntry2      ;
               DataItemTable=Table21;
               DataItemTableView=SORTING(Customer No.)
                                 WHERE(Blocked for Interest=FILTER(''));
               OnPreDataItem=BEGIN
                               CurrReport.BREAK;
                             END;

               ReqFilterFields=Document Type }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF FinChrgMemoHeaderReq."Document Date" = 0D THEN BEGIN
                     FinChrgMemoHeaderReq."Document Date" := WORKDATE;
                     FinChrgMemoHeaderReq."Posting Date" := WORKDATE;
                   END;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Posting Date;
                             NOR=Bokf›ringsdato;
                             SVE=Bokf”ringsdatum];
                  ToolTipML=[ENU=Specifies the date that will appear as the posting date on the header of the finance charge memo created by the batch job.;
                             NOR=Angir datoen som skal vises som bokf›ringsdato i hodet p† rentenotaen som opprettes av kj›rselen.;
                             SVE=Anger det datum som ska visas som bokf”ringsdatum i huvudet p† r„ntefakturan som skapas av batchjobbet.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=FinChrgMemoHeaderReq."Posting Date" }

      { 1   ;2   ;Field     ;
                  Name=DocumentDate;
                  CaptionML=[ENU=Document Date;
                             NOR=Bilagsdato;
                             SVE=Dokumentdatum];
                  ToolTipML=[ENU="Specifies a document date for the finance charge memo. This date is also used to determine the due date for the finance charge memo. ";
                             NOR="Angir en dokumentdato for rentenotaen. Denne datoen brukes ogs† til † fastsl† forfallsdatoen for rentenotaen. ";
                             SVE="Anger ett dokumentdatum f”r r„ntefakturan. Detta datum anv„nds ocks† f”r att best„mma f”rfallodatum f”r r„ntefakturan. "];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=FinChrgMemoHeaderReq."Document Date" }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 must be specified.;NOR=%1 m† angis.;SVE=%1 m†ste specificeras.';
      Text001@1001 : TextConst 'ENU=Making finance charge memos...;NOR=Oppretter rentenotaer...;SVE=Skapar r„ntefakturor...';
      Text002@1002 : TextConst 'ENU=Making finance charge memos @1@@@@@@@@@@@@@;NOR=Oppretter rentenotaer       @1@@@@@@@@@@@@@;SVE=Skapar r„ntefakturor        @1@@@@@@@@@@@@@';
      Text003@1003 : TextConst 'ENU=It was not possible to create finance charge memos for some of the selected customers.\Do you want to see these customers?;NOR=Det var ikke mulig † opprette rentenotaer for noen av de valgte kundene.\Vil du vise disse kundene?;SVE=Det gick inte att skapa r„ntefakturor f”r n†gra av de valda kunderna.\Vill du visa dem?';
      CustLedgEntry@1005 : Record 21;
      FinChrgMemoHeaderReq@1006 : Record 302;
      MakeFinChrgMemo@1007 : Codeunit 394;
      Window@1008 : Dialog;
      NoOfRecords@1009 : Integer;
      RecordNo@1010 : Integer;
      NewProgress@1011 : Integer;
      OldProgress@1012 : Integer;
      NewDateTime@1013 : DateTime;
      OldDateTime@1014 : DateTime;

    [External]
    PROCEDURE InitializeRequest@1(PostingDate@1000 : Date;DocumentDate@1001 : Date);
    BEGIN
      FinChrgMemoHeaderReq."Posting Date" := PostingDate;
      FinChrgMemoHeaderReq."Document Date" := DocumentDate;
    END;

    BEGIN
    {
      *** 4PSSE.I012 ***
      130904 Added property to block ledger entries if field "Blocked for interest" is true.
    }
    END.
  }
  RDLDATA
  {
  }
}

