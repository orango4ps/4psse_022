OBJECT Codeunit 11072013 Job Jnl.-B.Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00 (1024);
  }
  PROPERTIES
  {
    TableNo=11072011;
    OnRun=BEGIN
            JobJnlBatch.COPY(Rec);
            Code;
            COPY(JobJnlBatch);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Mchten Sie die Buch.-Bltter buchen und den Buchungsbericht drucken?;ENU=Do you want to post the journals and print the posting report?;NLD=Wilt u de dagboeken boeken en het boekingsrapport afdrukken?;NOR=Vil du bokfre journalene og skrive ut bokfringsrapporten?;SVE=Vill du bokfra journalerna och skriva ut bokfringsrapporten?';
      Text001@1001 : TextConst 'DEU=Die Buch.-Bltter sind gebucht worden.;ENU=The journals were successfully posted.;NLD=De dagboeken werden probleemloos geboekt.;NOR=Kladdene ble bokfrt p riktig mte.;SVE=Journalerna r bokfrda.';
      Text002@1002 : TextConst 'DEU="Es konnten nicht alle Buch.-Bltter gebucht werden. ";ENU="It was not possible to post all of the journals. ";NLD="Niet alle dagboeken konden geboekt worden. ";NOR="Det er ikke mulig  bokfre alle kladdene. ";SVE="Det var inte mjligt att bokfra alla journaler. "';
      Text003@1003 : TextConst 'DEU=Die nicht erfolgreich gebuchten Buch.-Bltter sind jetzt markiert.;ENU=The journals that were not successfully posted are now marked.;NLD=De dagboeken die niet zonder problemen konden geboekt worden, zijn gemarkeerd.;NOR=Kladdene som ikke ble bokfrt, er merket.;SVE=Journalerna som inte blev bokfrda r nu markerade.';
      JobJnlTemplate@1004 : Record 11072007;
      JobJnlBatch@1005 : Record 11072011;
      JobJnlLine@1006 : Record 11072008;
      JobReg@1007 : Record 11072012;
      JobJnlPostbatch@1008 : Codeunit 11072004;
      JnlWithErrors@1009 : Boolean;

    LOCAL PROCEDURE Code@1();
    VAR
      HideDialog@1000 : Boolean;
    BEGIN
      WITH JobJnlBatch DO BEGIN
        JobJnlTemplate.GET("Journal Template Name");
        JobJnlTemplate.TESTFIELD("Posting Report ID");

        HideDialog := FALSE;
        OnBeforePostJournalBatch(JobJnlBatch,HideDialog);
        IF NOT HideDialog THEN
          IF NOT CONFIRM(Text000) THEN
            EXIT;

        FIND('-');
        REPEAT
          JobJnlLine."Journal Template Name" := "Journal Template Name";
          JobJnlLine."Journal Batch Name" := Name;
          JobJnlLine."Line No." := 1;
          CLEAR(JobJnlPostbatch);
          IF JobJnlPostbatch.RUN(JobJnlLine) THEN BEGIN
            MARK(FALSE);
            IF JobReg.GET(JobJnlLine."Line No.") THEN BEGIN
              JobReg.SETRECFILTER;
              REPORT.RUN(JobJnlTemplate."Posting Report ID",FALSE,FALSE,JobReg);
            END;
          END ELSE BEGIN
            MARK(TRUE);
            JnlWithErrors := TRUE;
          END;
        UNTIL NEXT = 0;

        IF NOT JnlWithErrors THEN
          MESSAGE(Text001)
        ELSE
          MESSAGE(
            Text002 +
            Text003);

        IF NOT FIND('=><') THEN BEGIN
          RESET;
          Name := '';
        END;
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostJournalBatch@2(VAR JobJournalBatch@1000 : Record 11072011;VAR HideDialog@1001 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

