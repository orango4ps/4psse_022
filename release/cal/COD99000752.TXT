OBJECT Codeunit 99000752 Check Routing Lines
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.01;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Wiederkehrende Referenz im Arbeitsplan %1, wenn %2 berechnet wird. Ermittelte Reihenfolge %3. Maximale Zeile %4.;ENU=Circular reference in routing %1 when calculating %2. Counted sequences %3. Max. lines %4.;NLD=Kringverwijzing in bewerkingsplan %1 bij %2 berekening. Getelde sequenties is %3.  Max. aantal regels is %4.;NOR=Sirkelreferanse i rute %1 i forbindelse med beregning av %2. Opptalte sekvenser %3. Maks. linjer %4.;SVE=Cirkul„r referens i operationsf”ljd i %1 vid ber„kning av %2. R„knade sekvenser %3. Maximalt antal linjer %4.';
      Text001@1001 : TextConst 'DEU=rckw„rts;ENU=back;NLD=achterwaartse;NOR=tilbake;SVE=Tillbaka';
      Text002@1002 : TextConst 'DEU=Aktuelle Anzahl der beendeten Durchl„ufe in Arbeitsplan %1 ist %2. Es sollte 1 sein. Prfen Sie %3.;ENU=Actual number of termination processes in route %1 is %2. They should be 1. Check %3.;NLD=Huidig aantal stopprocessen in bewerkingsplan %1 is %2 in plaats van 1. Controleer %3.;NOR=Faktisk antall avbrudd i rute %1 er %2. Det skulle v‘rt 1. Kontroller %3.;SVE=Faktiskt antal avslutningsprocesser i oper.f”ljd %1 „r %2. Det borde vara 1. Kontrollera %3.';
      Text003@1003 : TextConst 'DEU=Aktuelle Anzahl der begonnenen Durchl„ufe in Arbeitsplan %1 ist %2. Es sollte 1 sein. Prfen Sie %3.;ENU=Actual number of start processes in route %1 is %2. They should be 1. Check %3.;NLD=Huidig aantal startprocessen in bewerkingsplan %1 is %2 in plaats van 1. Controleer %3.;NOR=Faktisk antall startprosedyrer i rute %1 er %2. Det skulle v‘rt 1. Kontroller %3.;SVE=Faktiskt antal startprocesser i oper.f”ljd %1 „r %2. Det borde vara 1. Kontrollera %3.';
      Text004@1004 : TextConst 'DEU=Es sind nicht alle Arbeitsg„nge im Arbeitsplan %1 rckw„rts berechnet. Prfen Sie %2.;ENU=Not all routing lines are sequenced backwards on routing %1. Check %2.;NLD=Niet alle bewerkingsplanregels zijn achterwaarts doorgeschoven voor bewerkingsplan %1. Controleer %2.;NOR=Ikke alle rutelinjene er i rekkef›lge bakover i rute %1. Kontroller %2.;SVE=Inte alla oper.f”ljdsrader „r sekvenserade bak†t i operationsf”ljd %1. Kontrollera %2.';
      Text005@1005 : TextConst 'DEU=Es sind nicht alle Arbeitsg„nge im Arbeitsplan %1 vorw„rts berechnet. Prfen Sie %2.;ENU=Not all routing lines are sequenced forward on routing %1. Check %2.;NLD=Niet alle bewerkingsplanregels zijn voorwaarts doorgeschoven voor bewerkingsplan %1. Controleer %2.;NOR=Ikke alle rutelinjene er i rekkef›lge fremover i rute %1. Kontroller %2.;SVE=Inte alla oper.f”ljdsrader „r sekvenserade fram†t i operationsf”ljd %1. Kontrollera %2.';
      Text006@1006 : TextConst 'DEU=Vorhergehende Arbeitsg„nge fr %1 k”nnen nicht gefunden werden.;ENU=Previous operations for %1 cannot be found.;NLD=Kan vorige bewerkingen voor %1 niet vinden.;NOR=Finner ikke tidligere operasjoner for %1.;SVE=F”reg†ende operationer f”r %1 kan inte hittas.';
      Text007@1007 : TextConst 'DEU=Nachfolgende Arbeitsg„nge fr %1 k”nnen nicht gefunden werden.;ENU=Next operations for %1 cannot be found.;NLD=Kan volgende bewerkingen voor %1 niet vinden.;NOR=Finner ikke neste operasjoner for %1.;SVE=De n„sta operationerna f”r %1 kan inte hittas.';
      UOMMgt@1010 : Codeunit 5402;
      ErrList@1008 : Text[50];
      Text008@1009 : TextConst 'DEU=Fr den Arbeitsgang ''%1'' wurde keine Arbeitsplatzgruppe oder kein Arbeitsplatz definiert.;ENU=Operation %1 does not have a work center or a machine center defined.;NLD=Er is geen afdeling of bewerkingsplaats gedefinieerd voor bewerking %1.;NOR=Det er ikke definert et arbeidssenter eller et maskinsenter for operasjonen %1.;SVE=Operationen %1 har inte en definierad produktionsgrupp eller maskingrupp.';

    LOCAL PROCEDURE ErrorInRouting@1(RoutingCode@1000 : Code[20];Direction@1001 : Text[20];ActualSequence@1002 : Integer;MaxSequences@1003 : Integer);
    BEGIN
      ERROR(
        Text000,
        RoutingCode,
        Direction,
        ActualSequence,
        MaxSequences);
    END;

    LOCAL PROCEDURE InsertInErrList@8(RtngLine@1000 : Record 99000764);
    BEGIN
      IF (STRLEN(ErrList) + STRLEN(RtngLine."Operation No.") + 1) > MAXSTRLEN(ErrList) THEN
        EXIT;

      IF STRLEN(ErrList) > 0 THEN
        ErrList := ErrList + ',' + RtngLine."Operation No."
      ELSE
        ErrList := RtngLine."Operation No.";
    END;

    [External]
    PROCEDURE SetNextOperations@10(RtngHeader@1000 : Record 99000763;VersionCode@1001 : Code[20]);
    VAR
      RtngLine@1002 : Record 99000764;
      RtngLine2@1003 : Record 99000764;
    BEGIN
      RtngLine.SETRANGE("Routing No.",RtngHeader."No.");
      RtngLine.SETRANGE("Version Code",VersionCode);
      RtngLine.MODIFYALL("Next Operation No.",'');

      IF RtngLine.FIND('-') THEN
        REPEAT
          RtngLine2 := RtngLine;
          RtngLine2.SETRANGE("Routing No.",RtngHeader."No.");
          RtngLine2.SETRANGE("Version Code",VersionCode);
          IF RtngLine2.FIND('>') THEN BEGIN
            RtngLine."Next Operation No." := RtngLine2."Operation No.";
            RtngLine.MODIFY;
          END;
        UNTIL RtngLine.NEXT = 0;
    END;

    LOCAL PROCEDURE CalcPreviousOperations@9(RtngHeader@1000 : Record 99000763;VersionCode@1001 : Code[20]);
    VAR
      RtngLine@1002 : Record 99000764;
      RtngLine2@1003 : Record 99000764;
    BEGIN
      RtngLine.SETRANGE("Routing No.",RtngHeader."No.");
      RtngLine.SETRANGE("Version Code",VersionCode);
      RtngLine.MODIFYALL("Previous Operation No.",'');

      IF RtngLine.FIND('-') THEN
        REPEAT
          IF RtngLine."Next Operation No." <> '' THEN BEGIN
            RtngLine2.SETRANGE("Routing No.",RtngHeader."No.");
            RtngLine2.SETRANGE("Version Code",VersionCode);
            RtngLine2.SETFILTER("Operation No.",RtngLine."Next Operation No.");
            IF RtngLine2.FIND('-') THEN
              REPEAT
                IF RtngLine2."Previous Operation No." <> '' THEN
                  RtngLine2."Previous Operation No." :=
                    RtngLine2."Previous Operation No." + '|';
                RtngLine2."Previous Operation No." :=
                  RtngLine2."Previous Operation No." +
                  RtngLine."Operation No.";
                RtngLine2.MODIFY;
              UNTIL RtngLine2.NEXT = 0;
          END;
        UNTIL RtngLine.NEXT = 0;
    END;

    LOCAL PROCEDURE CheckCircularReference@12(ActSequences@1000 : Integer;MaxSequences@1001 : Integer;RoutingNo@1002 : Code[20]);
    BEGIN
      IF ActSequences > MaxSequences THEN
        ErrorInRouting(
          RoutingNo,
          Text001,
          ActSequences,
          MaxSequences);
    END;

    LOCAL PROCEDURE SetRtngLineSequenceBack@6(RoutingType@1000 : 'Serial,Parallel';RoutingNo@1001 : Code[20];VersionCode@1003 : Code[20];Maxsequences@1002 : Integer);
    VAR
      RoutingLine@1004 : Record 99000764;
      SequenceNo@1005 : Integer;
      LinesFound@1006 : Boolean;
    BEGIN
      SequenceNo := 1;
      IF RoutingType = RoutingType::Parallel THEN BEGIN
        SetTerminalOperationSequenceNo(RoutingNo,VersionCode);
        RoutingLine.SETRANGE("Routing No.",RoutingNo);
        RoutingLine.SETRANGE("Version Code",VersionCode);
        REPEAT
          RoutingLine.SETRANGE("Sequence No. (Backward)",SequenceNo);
          LinesFound := RoutingLine.FINDSET;
          IF LinesFound THEN BEGIN
            CheckCircularReference(SequenceNo,Maxsequences,RoutingNo);
            SequenceNo += 1;
            REPEAT
              IF RoutingLine."Previous Operation No." <> '' THEN
                SetSequenceNoOnPreviousOperations(RoutingNo,VersionCode,RoutingLine."Previous Operation No.",SequenceNo);
            UNTIL (RoutingLine.NEXT = 0) OR (RoutingLine."Previous Operation No." = '');
          END;
        UNTIL NOT LinesFound OR (RoutingLine."Previous Operation No." = '');
      END ELSE BEGIN
        RoutingLine.SETRANGE("Routing No.",RoutingNo);
        RoutingLine.SETRANGE("Version Code",VersionCode);
        IF RoutingLine.FIND('+') THEN
          REPEAT
            RoutingLine."Sequence No. (Backward)" := SequenceNo;
            RoutingLine.MODIFY;
            SequenceNo += 1;
          UNTIL RoutingLine.NEXT(-1) = 0;
      END;
    END;

    [External]
    PROCEDURE SetRtngLineSequenceForward@7(RoutingType@1000 : 'Serial,Parallel';RoutingNo@1001 : Code[20];VersionCode@1003 : Code[20];MaxSequences@1002 : Integer);
    VAR
      RoutingLine@1004 : Record 99000764;
      SequenceNo@1005 : Integer;
      LinesFound@1006 : Boolean;
    BEGIN
      SequenceNo := 1;
      IF RoutingType = RoutingType::Parallel THEN BEGIN
        SetStartingOperationSequenceNo(RoutingNo,VersionCode);
        RoutingLine.SETRANGE("Routing No.",RoutingNo);
        RoutingLine.SETRANGE("Version Code",VersionCode);
        REPEAT
          RoutingLine.SETRANGE("Sequence No. (Forward)",SequenceNo);
          LinesFound := RoutingLine.FINDSET;
          IF LinesFound THEN BEGIN
            CheckCircularReference(SequenceNo,MaxSequences,RoutingNo);
            SequenceNo += 1;
            REPEAT
              IF RoutingLine."Next Operation No." <> '' THEN
                SetSequenceNoOnNextOperations(RoutingNo,VersionCode,RoutingLine."Next Operation No.",SequenceNo);
            UNTIL (RoutingLine.NEXT = 0) OR (RoutingLine."Next Operation No." = '');
          END;
        UNTIL NOT LinesFound OR (RoutingLine."Next Operation No." = '');
      END ELSE BEGIN
        RoutingLine.SETRANGE("Routing No.",RoutingNo);
        RoutingLine.SETRANGE("Version Code",VersionCode);
        IF RoutingLine.FIND('-') THEN
          REPEAT
            RoutingLine."Sequence No. (Forward)" := SequenceNo;
            RoutingLine.MODIFY;
            SequenceNo += 1;
          UNTIL RoutingLine.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE SetStartingOperationSequenceNo@25(RoutingNo@1000 : Code[20];VersionCode@1001 : Code[20]);
    VAR
      RoutingLine@1002 : Record 99000764;
    BEGIN
      RoutingLine.SETRANGE("Routing No.",RoutingNo);
      RoutingLine.SETRANGE("Version Code",VersionCode);
      RoutingLine.SETFILTER("Previous Operation No.",'%1','');
      RoutingLine.FINDFIRST;
      RoutingLine."Sequence No. (Forward)" := 1;
      RoutingLine.MODIFY;
    END;

    LOCAL PROCEDURE SetTerminalOperationSequenceNo@26(RoutingNo@1002 : Code[20];VersionCode@1001 : Code[20]);
    VAR
      RoutingLine@1000 : Record 99000764;
    BEGIN
      RoutingLine.SETRANGE("Routing No.",RoutingNo);
      RoutingLine.SETRANGE("Version Code",VersionCode);
      RoutingLine.SETFILTER("Next Operation No.",'%1','');
      RoutingLine.FINDFIRST;
      RoutingLine."Sequence No. (Backward)" := 1;
      RoutingLine.MODIFY;
    END;

    LOCAL PROCEDURE SetSequenceNoOnNextOperations@27(RoutingNo@1001 : Code[20];VersionCode@1002 : Code[20];NextOperationFilter@1003 : Text;SequenceNo@1004 : Integer);
    VAR
      RoutingLine@1000 : Record 99000764;
    BEGIN
      RoutingLine.SETRANGE("Routing No.",RoutingNo);
      RoutingLine.SETRANGE("Version Code",VersionCode);
      RoutingLine.SETFILTER("Operation No.",NextOperationFilter);
      RoutingLine.MODIFYALL("Sequence No. (Forward)",SequenceNo);
    END;

    LOCAL PROCEDURE SetSequenceNoOnPreviousOperations@28(RoutingNo@1003 : Code[20];VersionCode@1002 : Code[20];PreviousOperationFilter@1001 : Text;SequenceNo@1000 : Integer);
    VAR
      RoutingLine@1004 : Record 99000764;
    BEGIN
      RoutingLine.SETRANGE("Routing No.",RoutingNo);
      RoutingLine.SETRANGE("Version Code",VersionCode);
      RoutingLine.SETFILTER("Operation No.",PreviousOperationFilter);
      RoutingLine.MODIFYALL("Sequence No. (Backward)",SequenceNo);
    END;

    LOCAL PROCEDURE CalcSequenceBack@2(RtngHeader@1000 : Record 99000763;VersionCode@1001 : Code[20]);
    VAR
      RtngLine@1002 : Record 99000764;
      MaxSeq@1003 : Integer;
    BEGIN
      RtngLine.SETRANGE("Routing No.",RtngHeader."No.");
      RtngLine.SETRANGE("Version Code",VersionCode);
      IF RtngLine.FIND('-') THEN
        REPEAT
          RtngLine."Sequence No. (Backward)" := 0;
          RtngLine."Fixed Scrap Qty. (Accum.)" := 0;
          RtngLine."Scrap Factor % (Accumulated)" := 0;
          RtngLine.MODIFY;
        UNTIL RtngLine.NEXT = 0;

      MaxSeq := RtngLine.COUNT;

      SetRtngLineSequenceBack(RtngHeader.Type,RtngHeader."No.",VersionCode,MaxSeq);
    END;

    LOCAL PROCEDURE CalcSequenceForward@3(RtngHeader@1000 : Record 99000763;VersionCode@1001 : Code[20]);
    VAR
      RtngLine@1002 : Record 99000764;
      MaxSeq@1003 : Integer;
    BEGIN
      RtngLine.SETRANGE("Routing No.",RtngHeader."No.");
      RtngLine.SETRANGE("Version Code",VersionCode);
      RtngLine.MODIFYALL("Sequence No. (Forward)",0);

      MaxSeq := RtngLine.COUNT;

      SetRtngLineSequenceForward(RtngHeader.Type,RtngHeader."No.",VersionCode,MaxSeq);
    END;

    [External]
    PROCEDURE NeedsCalculation@11(RtngHeader@1000 : Record 99000763;VersionCode@1001 : Code[20]) : Boolean;
    VAR
      RtngLine@1002 : Record 99000764;
    BEGIN
      RtngLine.SETRANGE("Routing No.",RtngHeader."No.");
      RtngLine.SETRANGE("Version Code",VersionCode);
      RtngLine.SETRANGE(Recalculate,TRUE);
      EXIT(RtngLine.FINDFIRST);
    END;

    [External]
    PROCEDURE Calculate@4(RtngHeader@1000 : Record 99000763;VersionCode@1001 : Code[20]);
    VAR
      RtngVersion@1002 : Record 99000786;
      RtngLine@1003 : Record 99000764;
      RtngLine2@1004 : Record 99000764;
      CalcScrapFactor@1005 : Decimal;
      CalcScrapQty@1006 : Decimal;
      ScrapFactorThis@1007 : Decimal;
      ScrapQtyThis@1008 : Decimal;
    BEGIN
      RtngLine.SETCURRENTKEY("Routing No.","Version Code","Sequence No. (Backward)");
      RtngLine.SETRANGE("Routing No.",RtngHeader."No.");
      RtngLine.SETRANGE("Version Code",VersionCode);
      IF RtngLine.ISEMPTY THEN
        EXIT;

      IF VersionCode <> '' THEN BEGIN
        RtngVersion.GET(RtngHeader."No.",VersionCode);
        RtngHeader.Type := RtngVersion.Type;
      END;

      IF RtngHeader.Type = RtngHeader.Type::Serial THEN
        SetNextOperations(RtngHeader,VersionCode);

      CalcPreviousOperations(RtngHeader,VersionCode);
      CalcSequenceBack(RtngHeader,VersionCode);
      CalcSequenceForward(RtngHeader,VersionCode);

      OnBeforeFindRoutingLines(RtngHeader,VersionCode);

      IF RtngLine.FIND('-') THEN
        REPEAT
          IF RtngLine."Next Operation No." <> '' THEN BEGIN
            RtngLine2.SETRANGE("Routing No.",RtngLine."Routing No.");
            RtngLine2.SETRANGE("Version Code",VersionCode);
            RtngLine2.SETFILTER("Operation No.",RtngLine."Next Operation No.");
            CalcScrapFactor := 0;
            CalcScrapQty := 0;
            IF RtngLine2.FIND('-') THEN
              REPEAT
                ScrapFactorThis :=
                  RtngLine2."Scrap Factor % (Accumulated)";
                ScrapQtyThis := ROUND(RtngLine2."Fixed Scrap Qty. (Accum.)",UOMMgt.QtyRndPrecision);
                IF CalcScrapFactor < ScrapFactorThis THEN
                  CalcScrapFactor := ScrapFactorThis;
                IF CalcScrapQty < ScrapQtyThis THEN
                  CalcScrapQty := ScrapQtyThis;
              UNTIL RtngLine2.NEXT = 0;
          END;
          IF CalcScrapFactor <> 0 THEN BEGIN
            IF RtngLine."Scrap Factor %" <> 0 THEN
              CalcScrapFactor :=
                ROUND(
                  (1 + CalcScrapFactor) *
                  (1 + RtngLine."Scrap Factor %" / 100),0.00001) - 1;
          END ELSE
            CalcScrapFactor :=
              ROUND(1 + RtngLine."Scrap Factor %" / 100,0.00001) - 1;
          CalcScrapQty := CalcScrapQty * (1 + RtngLine."Scrap Factor %" / 100) + RtngLine."Fixed Scrap Quantity";
          OnCalculateOnAfterCalcScrapQtyAndFactor(RtngLine,CalcScrapQty,CalcScrapFactor);
          RtngLine."Fixed Scrap Qty. (Accum.)" := CalcScrapQty;
          RtngLine."Scrap Factor % (Accumulated)" := CalcScrapFactor;
          RtngLine.MODIFY;
        UNTIL RtngLine.NEXT = 0;

      RtngLine.MODIFYALL(Recalculate,FALSE);
      Check(RtngHeader,VersionCode);
    END;

    LOCAL PROCEDURE Check@5(RtngHeader@1000 : Record 99000763;VersionCode@1001 : Code[20]);
    VAR
      RtngLine@1002 : Record 99000764;
      RtngLine2@1003 : Record 99000764;
      NoOfProcesses@1004 : Integer;
    BEGIN
      ErrList := '';

      RtngLine.SETRANGE("Routing No.",RtngHeader."No.");
      RtngLine.SETRANGE("Version Code",VersionCode);
      RtngLine.SETRANGE("No.",'');
      IF RtngLine.FINDFIRST THEN
        ERROR(Text008,RtngLine."Operation No.");
      RtngLine.SETRANGE("No.");

      RtngLine.SETFILTER("Next Operation No.",'%1','');

      NoOfProcesses := RtngLine.COUNT;
      IF NoOfProcesses <> 1 THEN BEGIN
        REPEAT
          InsertInErrList(RtngLine);
        UNTIL RtngLine.NEXT = 0;
        ERROR(
          Text002,
          RtngHeader."No.",
          NoOfProcesses,
          ErrList);
      END;

      RtngLine.SETFILTER("Previous Operation No.",'%1','');
      RtngLine.SETRANGE("Next Operation No.");
      NoOfProcesses := RtngLine.COUNT;
      IF NoOfProcesses <> 1 THEN BEGIN
        REPEAT
          InsertInErrList(RtngLine);
        UNTIL RtngLine.NEXT = 0;
        ERROR(
          Text003,
          RtngHeader."No.",
          NoOfProcesses,
          ErrList);
      END;
      RtngLine.SETRANGE("Previous Operation No.");

      RtngLine.SETRANGE("Sequence No. (Backward)",0);
      IF RtngLine.FIND('-') THEN BEGIN
        REPEAT
          InsertInErrList(RtngLine);
        UNTIL RtngLine.NEXT = 0;
        ERROR(
          Text004,
          RtngLine."Routing No.",
          ErrList);
      END;
      RtngLine.SETRANGE("Sequence No. (Backward)");

      RtngLine.SETRANGE("Sequence No. (Forward)",0);
      IF RtngLine.FIND('-') THEN BEGIN
        REPEAT
          InsertInErrList(RtngLine);
        UNTIL RtngLine.NEXT = 0;
        ERROR(
          Text005,
          RtngLine."Routing No.",
          ErrList);
      END;
      RtngLine.SETRANGE("Sequence No. (Forward)");

      RtngLine.SETCURRENTKEY("Routing No.","Version Code","Sequence No. (Backward)");
      RtngLine.SETFILTER("Previous Operation No.",'<>%1','');

      IF RtngLine.FIND('-') THEN
        REPEAT
          RtngLine2.SETRANGE("Routing No.",RtngLine."Routing No.");
          RtngLine2.SETRANGE("Version Code",VersionCode);
          RtngLine2.SETFILTER("Operation No.",RtngLine."Previous Operation No.");
          IF RtngLine2.ISEMPTY THEN
            ERROR(
              Text006,
              RtngLine."Routing No.");
        UNTIL RtngLine.NEXT = 0;

      RtngLine.SETCURRENTKEY("Routing No.","Version Code","Sequence No. (Forward)");
      RtngLine.SETFILTER("Next Operation No.",'<>%1','');
      RtngLine.SETRANGE("Operation No.");

      IF RtngLine.FIND('-') THEN
        REPEAT
          RtngLine2.SETRANGE("Routing No.",RtngLine."Routing No.");
          RtngLine2.SETRANGE("Version Code",VersionCode);
          RtngLine2.SETFILTER("Operation No.",RtngLine."Next Operation No.");
          IF RtngLine2.ISEMPTY THEN
            ERROR(
              Text007,
              RtngLine."Routing No.");
        UNTIL RtngLine.NEXT = 0;
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeFindRoutingLines@13(VAR RoutingHeader@1000 : Record 99000763;VersionCode@1001 : Code[20]);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnCalculateOnAfterCalcScrapQtyAndFactor@14(VAR RoutingLine@1000 : Record 99000764;VAR ScrapQty@1001 : Decimal;VAR ScrapFactor@1002 : Decimal);
    BEGIN
    END;

    BEGIN
    END.
  }
}

