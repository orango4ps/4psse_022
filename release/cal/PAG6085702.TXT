OBJECT Page 6085702 Purch. Invoice Match
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW18.00.00.5.00.05,4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 6085700=rimd;
    CaptionML=[DEU=Rechnungsabgleich;
               ENU=Invoice Matching;
               NLD=Factuur matching];
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table6085590;
    DataCaptionExpr="No." + ' - ' + GetSourceName;
    PageType=Card;
    PromotedActionCategoriesML=[DEU=0, Prozess, 2, Vorlage, Kreditor;
                                ENU=0,Process,2,Template,Vendor;
                                NLD=0,Verwerken,2,Sjabloon,Leverancier];
    OnOpenPage=VAR
                 PurchRcptLine@6085575 : Record 121;
                 PurchLine@6085574 : Record 39;
                 CurrencyCode@6085573 : Code[10];
               BEGIN
                 FILTERGROUP := 2;
                 SETRECFILTER;
                 FILTERGROUP := 0;

                 CurrPage.DocumentLines.PAGE.SetShowMatchedQty(TRUE);
                 CurrPage.OrderLines.PAGE.SetShowOrder(TRUE);

                 Channel := CREATEGUID;
                 CLEAR(EventNotifierSource);

                 IF WebClientMgt.IsWebClient THEN
                   ShowWebInfoPart := TRUE
                 ELSE
                   EventNotifierSource := FORMAT(Channel);

                 GLSetup.GET;
                 DCSetup.GET;

                 ShowDocLines := HasDocumentLines;
                 OurDocNo := COPYSTR(PurchDocMgnt.GetOurDocumentNo(Rec),1,MAXSTRLEN(OurDocNo));

                 UpdateInfoBar;

                 IF (PurchDocMgnt.GetCurrencyCode(Rec) = GLSetup."LCY Code") AND (NOT DCSetup."Fill-out LCY") THEN
                   CurrencyCode := ''
                 ELSE
                   CurrencyCode := PurchDocMgnt.GetCurrencyCode(Rec);

                 PurchRcptLine.SETCURRENTKEY("Pay-to Vendor No.");
                 PurchRcptLine.FILTERGROUP(4);
                 PurchRcptLine.SETRANGE("Pay-to Vendor No.",GetSourceID);
                 //PurchRcptLine.SETFILTER("Qty. Rcd. Not Invoiced",'<>%1',0); //**4PS.o
                 PurchRcptLine.SETFILTER("Amnt. Rcd. Not Invoiced (LCY)",'<>%1',0); //**4PS.n
                 PurchRcptLine.SETRANGE("Currency Code",CurrencyCode);
                 PurchRcptLine.FILTERGROUP(0);

                 CurrPage.ReceiptLines.PAGE.SETTABLEVIEW(PurchRcptLine);
                 CurrPage.ReceiptLines.PAGE.SetDocument(Rec);
                 CurrPage.ReceiptLines.PAGE.SetChannel(Channel);
                 CurrPage.ReceiptLines.PAGE.UpdatePage;

                 PurchLine.SETCURRENTKEY("Document Type","Pay-to Vendor No.","Currency Code");
                 PurchLine.FILTERGROUP(4);
                 PurchLine.SETRANGE("Document Type",PurchLine."Document Type"::Order);
                 PurchLine.SETRANGE("Pay-to Vendor No.",GetSourceID);
                 PurchLine.SETRANGE("Currency Code",CurrencyCode);
                 PurchLine.SETFILTER("Outstanding Quantity",'<>%1',0);
                 PurchLine.FILTERGROUP(0);

                 CurrPage.OrderLines.PAGE.SETTABLEVIEW(PurchLine);
                 CurrPage.OrderLines.PAGE.SetDocument(Rec);
                 CurrPage.OrderLines.PAGE.SetChannel(Channel);
                 CurrPage.OrderLines.PAGE.UpdatePage;

                 CurrPage.DocumentLines.PAGE.SetDocument(Rec);
                 CurrPage.DocumentLines.PAGE.UpdatePage;
               END;

    OnAfterGetRecord=BEGIN
                       UpdateInfoBar;
                     END;

    ActionList=ACTIONS
    {
      { 1000000018;  ;ActionContainer;
                      Name=RelatedInformation;
                      CaptionML=;
                      ActionContainerType=RelatedInformation }
      { 1000000015;1 ;ActionGroup;
                      Name=Template;
                      CaptionML=[DEU=&Vorlage;
                                 ENU=&Template;
                                 NLD=&Sjabloon] }
      { 1000000014;2 ;Action    ;
                      Name=Template Card;
                      ShortCutKey=Ctrl+S;
                      CaptionML=[DEU=&Vorlagenkarte;
                                 ENU=&Template Card;
                                 NLD=&Sjabloonkaart];
                      RunObject=Page 6085584;
                      RunPageView=SORTING(No.);
                      RunPageLink=No.=FIELD(Template No.);
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Edit;
                      PromotedCategory=Category4 }
      { 1000000013;2 ;Action    ;
                      Name=Translations;
                      ShortCutKey=Ctrl+T;
                      CaptionML=[DEU=&öbersetzungen;
                                 ENU=&Translations;
                                 NLD=Vertalingen];
                      Promoted=Yes;
                      Image=Track;
                      PromotedCategory=Category4;
                      OnAction=VAR
                                 Template@1000000001 : Record 6085579;
                               BEGIN
                                 Template.GET("Template No.");
                                 Template.TESTFIELD("Codeunit ID: Transl. (Doc)");
                                 CODEUNIT.RUN(Template."Codeunit ID: Transl. (Doc)",Rec);
                               END;
                                }
      { 1160040004;1 ;ActionGroup;
                      Name=Vendor;
                      CaptionML=[DEU=Kreditor;
                                 ENU=Vendor;
                                 NLD=Leverancier;
                                 SVE=Leverantîr] }
      { 1160040003;2 ;Action    ;
                      Name=Vendor Card;
                      CaptionML=[DEU=Kreditorenkarte;
                                 ENU=Vendor Card;
                                 NLD=Leverancier;
                                 SVE=Leverantîrskort];
                      Promoted=Yes;
                      Image=Vendor;
                      PromotedCategory=Category5;
                      OnAction=VAR
                                 Vendor@1160040000 : Record 23;
                               BEGIN
                                 Vendor.GET(GetSourceID);
                                 PAGE.RUN(PAGE::"Vendor Card",Vendor);
                               END;
                                }
      { 1160040002;2 ;Action    ;
                      Name=Unposted Documents;
                      CaptionML=[DEU=&Nicht gebuchte Belege;
                                 ENU=&Unposted Documents;
                                 NLD=&Niet Geboekte Documenten];
                      Promoted=Yes;
                      Image=Invoice;
                      PromotedCategory=Category5;
                      OnAction=VAR
                                 PurchHeader@1160040000 : Record 38;
                               BEGIN
                                 PurchHeader.SETRANGE("Pay-to Vendor No.",GetSourceID);
                                 PAGE.RUN(PAGE::"DC Purchase Documents",PurchHeader);
                               END;
                                }
      { 1160040001;2 ;Action    ;
                      Name=Ledger Entries;
                      CaptionML=[DEU=&Posten;
                                 ENU=Ledger E&ntries;
                                 NLD=Poste&n;
                                 SVE=Tra&nsaktioner];
                      Promoted=Yes;
                      Image=VendorLedger;
                      PromotedCategory=Category5;
                      OnAction=VAR
                                 VendLedgEntry@1160040000 : Record 25;
                               BEGIN
                                 VendLedgEntry.SETCURRENTKEY("Vendor No.");
                                 VendLedgEntry.SETRANGE("Vendor No.",GetSourceID);
                                 PAGE.RUN(PAGE::"Vendor Ledger Entries",VendLedgEntry);
                               END;
                                }
      { 1000000012;  ;ActionContainer;
                      Name=ActionItems;
                      CaptionML=;
                      ActionContainerType=ActionItems }
      { 1000000011;1 ;ActionGroup;
                      Name=Functions;
                      CaptionML=[DEU=F&unktion;
                                 ENU=F&unctions;
                                 NLD=F&uncties;
                                 SVE=Fu&nktion] }
      { 1160040005;2 ;Action    ;
                      Name=Filter on Order No.;
                      ShortCutKey=Shift+Ctrl+O;
                      CaptionML=[DEU=Nach Auftragsnr. filtern;
                                 ENU=Filter on Order No.;
                                 NLD=Filter op Ordernr.];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=FilterLines;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 FilterOnOrderNo := NOT FilterOnOrderNo;
                                 IF FilterOnOrderNo THEN BEGIN
                                   CurrPage.ReceiptLines.PAGE.SetOrderNo(OurDocNo);
                                   CurrPage.OrderLines.PAGE.SetDocumentNo(OurDocNo);
                                 END ELSE BEGIN
                                   CurrPage.ReceiptLines.PAGE.SetOrderNo('');
                                   CurrPage.OrderLines.PAGE.SetDocumentNo('');
                                 END;

                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 1000000008;2 ;Action    ;
                      Name=Perform Match;
                      ShortCutKey=Ctrl+M;
                      CaptionML=[DEU=&Automatischen Abgleich durchfÅhren;
                                 ENU=Perform Match;
                                 NLD=Voer match uit];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Splitlines;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 TestStatus;

                                 PurchDocMgnt.AutoMatch(Rec);
                                 CurrPage.DocumentLines.PAGE.UpdatePage;
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 1000000006;2 ;Action    ;
                      Name=Remove All Matches;
                      CaptionML=[DEU=&Alle öbereinstimmungen entfernen;
                                 ENU=Remove &All Matches;
                                 NLD=Verwijder &alle matches];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Cancel;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 PurchDocMatch@6085574 : Record 6085700;
                                 Comment@6085573 : Record 6085594;
                               BEGIN
                                 IF NOT CONFIRM(ResetMatchQst) THEN
                                   EXIT;

                                 DeleteComments(Comment.Area::Match);

                                 PurchDocMatch.SETRANGE("Document No.","No.");
                                 PurchDocMatch.DELETEALL;

                                 "Match Status" := "Match Status"::Unmatched;
                                 MODIFY(TRUE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1000000000;;Container;
                Name=ContentArea;
                ContainerType=ContentArea }

    { 6   ;1   ;Group     ;
                Name=WebInfobar;
                CaptionML=[DEU=öbersicht abgleichen;
                           ENU=Match Overview;
                           NLD=Match overzicht];
                Visible=ShowWebInfoPart;
                GroupType=Group }

    { 1   ;2   ;Group     ;
                Name=Control1;
                GroupType=GridLayout;
                Layout=Columns }

    { 5   ;3   ;Field     ;
                Name=OurDocNo;
                SourceExpr=OurDocNo;
                CaptionClass=PurchDocMgnt.GetOurDocNoDesc(Rec);
                Editable=FALSE }

    { 2   ;3   ;Field     ;
                Name=AmountToMatch;
                SourceExpr=AmountToMatch;
                CaptionClass=Text003;
                Editable=FALSE }

    { 3   ;3   ;Field     ;
                Name=MatchedAmount;
                SourceExpr=MatchedAmount;
                CaptionClass=Text004;
                Editable=FALSE }

    { 4   ;3   ;Field     ;
                Name=MatchDiffAmount;
                SourceExpr=MatchDiffAmount;
                CaptionClass=Text005;
                Editable=FALSE }

    { 161024012;1;Field   ;
                Name=InfoBarAddIn;
                SourceExpr=InfoBarAddInSource;
                ControlAddIn=[Akeyto.DocumentCapture.Dynamics.Nav.MatchInfoBar;PublicKeyToken=0c53e51c0afe7001];
                ShowCaption=No }

    { 161024013;1;Field   ;
                Name=EventNotifier;
                SourceExpr=EventNotifierSource;
                ControlAddIn=[Akeyto.DocumentCapture.Dynamics.Nav.EventNotifier;PublicKeyToken=0c53e51c0afe7001];
                OnControlAddIn=BEGIN
                                 UpdateInfoBar;
                               END;
                                }

    { 1000000003;1;Part   ;
                Name=ReceiptLines;
                CaptionML=[DEU=Einkaufslieferzeilen;
                           ENU=Purchase Receipt Lines;
                           NLD=Inkoopontvangst regels];
                PagePartID=Page6085703;
                UpdatePropagation=Both }

    { 1160040000;1;Part   ;
                Name=OrderLines;
                CaptionML=[DEU=Auftragszeilen;
                           ENU=Order Lines;
                           NLD=&Orderregels;
                           SVE=Orderrader];
                PagePartID=Page6085749;
                PartType=Page;
                UpdatePropagation=Both }

    { 1000000007;1;Part   ;
                Name=DocumentLines;
                CaptionML=[DEU=Belegzeilen;
                           ENU=Document Lines;
                           NLD=Documentregels;
                           SVE=Dokumentrader];
                PagePartID=Page6085727;
                Visible=ShowDocLines;
                Editable=FALSE;
                UpdatePropagation=Both }

    { 1000000026;;Container;
                Name=FactBoxArea;
                ContainerType=FactBoxArea }

    { 1000000025;1;Part   ;
                Name=CaptureUI;
                CaptionML=[DEU=Herkunftsbeleg;
                           ENU=Document;
                           NLD=Document;
                           SVE=KÑlldokument];
                SubPageLink=No.=FIELD(No.);
                PagePartID=Page6085593 }

  }
  CODE
  {
    VAR
      GLSetup@1160040000 : Record 98;
      DCSetup@1160040001 : Record 6085573;
      WebClientMgt@1003 : Codeunit 6085662;
      PurchDocMgnt@1000000009 : Codeunit 6085709;
      CaptureAddinLib@161024014 : Codeunit 6085660;
      CaptureXmlDoc@161024013 : Codeunit 6192800;
      InfoBarAddInSource@161024012 : Text;
      EventNotifierSource@161024017 : Text;
      Text003@161024015 : TextConst 'DEU=Abzugleichender Betrag;ENU=Amount to Match;NLD=Bedrag te matchen';
      Text004@161024016 : TextConst 'DEU=Abgeglichener Betrag;ENU=Matched Amount;NLD=Gematcht bedrag';
      Channel@161024018 : GUID;
      Text005@1160040002 : TextConst 'DEU=Differenz;ENU=Difference;NLD=Verschil;SVE=Differens';
      OurDocNo@1160040004 : Code[20];
      FilterOnOrderNo@1160040003 : Boolean;
      ShowWebInfoPart@1004 : Boolean;
      AmountToMatch@1002 : Decimal;
      MatchedAmount@1001 : Decimal;
      MatchDiffAmount@1000 : Decimal;
      ShowDocLines@6085574 : Boolean INDATASET;
      ResetMatchQst@6085573 : TextConst 'DEU=Mîchten Sie die öbereinstimmungen fÅr alle Belegzeilen zurÅcksetzen?;ENU=Do you wish to reset matches for all document lines?;NLD=Wilt u alle gematchte regels van dit document resetten?';

    PROCEDURE SetDocument@1000000004(VAR NewDocument@1000000001 : Record 6085590);
    VAR
      Value@1000000003 : Record 6085593;
      Field@1000000005 : Record 6085580;
      CaptureMgnt@1000000006 : Codeunit 6085576;
      i@1000000000 : Integer;
      NoOfLines@1000000004 : Integer;
      Arr@1000000002 : ARRAY [100] OF Text[30];
    BEGIN
      GET(NewDocument."No.");
    END;

    PROCEDURE UpdateInfoBar@161024012();
    VAR
      AppMgnt@161024012 : Codeunit 45;
    BEGIN
      AmountToMatch := PurchDocMgnt.GetAmountToMatch(Rec);
      MatchedAmount := PurchDocMgnt.GetDocMatchedAmount(Rec);
      MatchDiffAmount := AmountToMatch - MatchedAmount;

      CaptureAddinLib.BuildMatchInfoBarCommand(CaptureXmlDoc,
        PurchDocMgnt.GetOurDocNoDesc(Rec),OurDocNo,
        Text003,FORMAT(AmountToMatch,0,AppMgnt.AutoFormatTranslate(1,PurchDocMgnt.GetCurrencyCode(Rec))),
        Text004,FORMAT(MatchedAmount,0,AppMgnt.AutoFormatTranslate(1,PurchDocMgnt.GetCurrencyCode(Rec))),
        Text005,FORMAT(MatchDiffAmount,0,AppMgnt.AutoFormatTranslate(1,PurchDocMgnt.GetCurrencyCode(Rec))));

      CaptureAddinLib.XmlToText(CaptureXmlDoc,InfoBarAddInSource);
      CLEAR(CaptureXmlDoc);
    END;

    BEGIN
    END.
  }
}

