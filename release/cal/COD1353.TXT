OBJECT Codeunit 1353 Generate Master Data Telemetry
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            OnMasterDataTelemetry;
          END;

  }
  CODE
  {
    VAR
      AlCompanyMasterdataCategoryTxt@1003 : TextConst '@@@=Locked;ENU=AL Company Masterdata;NOR=AL Company Masterdata';
      MasterdataTelemetryMessageTxt@1006 : TextConst '@@@=Locked;ENU=CompanyGUID: %1, IsEvaluationCompany: %2, IsDemoCompany: %3, Customers: %4, Vendors: %5, Items: %6, G/L Accounts: %7, Contacts: %8;NOR=CompanyGUID: %1, IsEvaluationCompany: %2, IsDemoCompany: %3, Customers: %4, Vendors: %5, Items: %6, G/L Accounts: %7, Contacts: %8';

    [EventSubscriber(Codeunit,1353,OnMasterDataTelemetry,"",Skip,Skip)]
    LOCAL PROCEDURE SendTelemetryOnMasterDataTelemetry@5();
    VAR
      Customer@1005 : Record 18;
      Vendor@1004 : Record 23;
      Item@1003 : Record 27;
      GLAccount@1002 : Record 15;
      Contact@1001 : Record 5050;
      Company@1006 : Record 2000000006;
      TableInformation@1007 : Record 2000000028;
      CompanyInformationMgt@1008 : Codeunit 1306;
      CustomerCount@1009 : Integer;
      VendorCount@1010 : Integer;
      ItemCount@1011 : Integer;
      GLAccountCount@1012 : Integer;
      ContactCount@1013 : Integer;
      TelemetryMsg@1000 : Text;
    BEGIN
      IF Company.GET(COMPANYNAME) THEN;
      TableInformation.SETRANGE("Company Name",COMPANYNAME);

      CustomerCount := GetNoOfRecords(TableInformation,Customer.TABLENAME);
      VendorCount := GetNoOfRecords(TableInformation,Vendor.TABLENAME);
      ItemCount := GetNoOfRecords(TableInformation,Item.TABLENAME);
      GLAccountCount := GetNoOfRecords(TableInformation,GLAccount.TABLENAME);
      ContactCount := GetNoOfRecords(TableInformation,Contact.TABLENAME);

      TelemetryMsg := STRSUBSTNO(MasterdataTelemetryMessageTxt,
          Company.Id,FORMAT(Company."Evaluation Company",0,9),FORMAT(CompanyInformationMgt.IsDemoCompany,0,9),
          CustomerCount,VendorCount,ItemCount,GLAccountCount,ContactCount);

      SENDTRACETAG('000018V',AlCompanyMasterdataCategoryTxt,VERBOSITY::Normal,TelemetryMsg,DATACLASSIFICATION::SystemMetadata);
    END;

    [Integration]
    LOCAL PROCEDURE OnMasterDataTelemetry@3();
    BEGIN
    END;

    LOCAL PROCEDURE GetNoOfRecords@1(TableInformation@1001 : Record 2000000028;TableName@1002 : Text[30]) : Integer;
    BEGIN
      TableInformation.SETRANGE("Table Name",TableName);
      IF TableInformation.FINDFIRST THEN
        EXIT(TableInformation."No. of Records");
      EXIT(-1);
    END;

    BEGIN
    END.
  }
}

