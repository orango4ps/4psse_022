OBJECT Table 11125828 Base IFC Property
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Base IFC Property;
  }
  FIELDS
  {
    { 10  ;   ;Table No.           ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Table));
                                                   OnValidate=BEGIN
                                                                IF "Table No." <> xRec."Table No." THEN
                                                                  "Field No." := 0;
                                                                IF NOT ("Table No." IN [DATABASE::"Project Object", DATABASE::"Service-Object",DATABASE::"Plant Object"]) THEN
                                                                  FIELDERROR("Table No.");
                                                              END;

                                                   CaptionML=[ENU=Table No.;
                                                              NOR=Tabellnr;
                                                              SVE=Tabellnr];
                                                   NotBlank=Yes }
    { 20  ;   ;Property            ;Text50        ;TableRelation="Standard IFC Property";
                                                   CaptionML=ENU=Property;
                                                   NotBlank=Yes }
    { 30  ;   ;Description         ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Standard IFC Property".Description WHERE (Property=FIELD(Property)));
                                                   CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning];
                                                   Editable=No }
    { 40  ;   ;Level               ;Option        ;OnValidate=BEGIN
                                                                IF Level <> xRec.Level THEN BEGIN
                                                                  "Field No." := 0;
                                                                  "Unit of Measure" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Level;
                                                              NOR=Niv†;
                                                              SVE=Niv†];
                                                   OptionCaptionML=ENU=Specification Line,Object;
                                                   OptionString=SpecificationLine,Object }
    { 50  ;   ;Field No.           ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table No.),
                                                                                  Class=CONST(Normal),
                                                                                  Enabled=CONST(Yes));
                                                   OnValidate=VAR
                                                                ProjectObject@1100529000 : Record 11229311;
                                                                PlantObject@1100528500 : Record 11125830;
                                                                ServiceObject@1100528501 : Record 11071691;
                                                              BEGIN
                                                                IF "Field No." <> xRec."Field No." THEN
                                                                  "Unit of Measure" := '';
                                                                IF "Field No." = 0 THEN
                                                                  EXIT;
                                                                TESTFIELD(Level, Level::Object);
                                                                CASE "Table No." OF
                                                                  DATABASE::"Project Object":
                                                                    BEGIN
                                                                      //These field must also be support in codeunit 'BIM Viewer Management' (C11125822), function 'DetermineSpecsForProjectObjectLevel'
                                                                      IF NOT ("Field No." IN [
                                                                        ProjectObject.FIELDNO(Element),
                                                                        ProjectObject.FIELDNO("Item No."),
                                                                        ProjectObject.FIELDNO(Manufacturer),
                                                                        ProjectObject.FIELDNO("Vendor (Trade Item)"),
                                                                        ProjectObject.FIELDNO("Trade Item"),
                                                                        ProjectObject.FIELDNO("GTIN Code (Item)"),
                                                                        ProjectObject.FIELDNO("Quantity (External)"),  //DP02375
                                                                        ProjectObject.FIELDNO("Description 2"),
                                                                        ProjectObject.FIELDNO(Phase),
                                                                        ProjectObject.FIELDNO("Plot No."),
                                                                        ProjectObject.FIELDNO(Prefab)
                                                                        ])
                                                                      THEN
                                                                        FIELDERROR("Field No.");
                                                                      IF "Field No." <>  ProjectObject.FIELDNO(Quantity) THEN
                                                                        TESTFIELD("Unit of Measure", '');
                                                                    END;
                                                                  DATABASE::"Plant Object":
                                                                    BEGIN
                                                                      IF NOT ("Field No." IN [
                                                                        PlantObject.FIELDNO("Plant Type"),
                                                                        PlantObject.FIELDNO(Quantity),
                                                                        PlantObject.FIELDNO("Description 2"),
                                                                        PlantObject.FIELDNO(Phase)
                                                                        ])
                                                                        THEN
                                                                          FIELDERROR("Field No.");
                                                                      IF "Field No." <>  PlantObject.FIELDNO(Quantity) THEN
                                                                        TESTFIELD("Unit of Measure", '');
                                                                    END;
                                                                  DATABASE::"Service-Object":
                                                                    BEGIN
                                                                      //These field must also be support in codeunit 'BIM Viewer Management' (C11125822), function 'DetermineSpecsForServiceObjectLevel'
                                                                      IF NOT ("Field No." IN [
                                                                        ServiceObject.FIELDNO(Phase)
                                                                        ])
                                                                      THEN
                                                                        FIELDERROR("Field No.");
                                                                    END;
                                                                ELSE
                                                                END;
                                                                CALCFIELDS("Field Caption");
                                                              END;

                                                   OnLookup=VAR
                                                              ProjectObject@1100529002 : Record 11229311;
                                                              FieldTableMgt@1100529000 : Codeunit 11012372;
                                                              FieldNoFilter@1100529001 : Text;
                                                              PlantObject@1100528500 : Record 11125830;
                                                            BEGIN
                                                              IF Level <> Level::Object THEN
                                                                EXIT;
                                                              CASE "Table No." OF
                                                                DATABASE::"Project Object":
                                                                  FieldNoFilter :=
                                                                    FORMAT(ProjectObject.FIELDNO(Element)) + '|' +
                                                                    FORMAT(ProjectObject.FIELDNO("Item No.")) + '|' +
                                                                    FORMAT(ProjectObject.FIELDNO(Manufacturer)) + '|' +
                                                                    FORMAT(ProjectObject.FIELDNO("Vendor (Trade Item)")) + '|' +
                                                                    FORMAT(ProjectObject.FIELDNO("Trade Item")) + '|' +
                                                                    FORMAT(ProjectObject.FIELDNO("GTIN Code (Item)")) + '|' +
                                                                    FORMAT(ProjectObject.FIELDNO("Quantity (External)")) + '|' +  //DP02375
                                                                    FORMAT(ProjectObject.FIELDNO("Description 2")) + '|' +
                                                                    FORMAT(ProjectObject.FIELDNO(Phase)) + '|' +
                                                                    FORMAT(ProjectObject.FIELDNO("Plot No.")) + '|' +
                                                                    FORMAT(ProjectObject.FIELDNO(Prefab))
                                                                    ;
                                                                DATABASE::"Plant Object":
                                                                  FieldNoFilter :=
                                                                    FORMAT(PlantObject.FIELDNO("Plant Type")) + '|' +
                                                                    FORMAT(PlantObject.FIELDNO(Quantity)) + '|' +
                                                                    FORMAT(PlantObject.FIELDNO("Description 2")) + '|' +
                                                                    FORMAT(PlantObject.FIELDNO(Phase))
                                                                    ;
                                                                DATABASE::"Service-Object":
                                                                  FieldNoFilter := '';
                                                              END;
                                                              IF FieldNoFilter <> '' THEN BEGIN
                                                                FieldTableMgt.SetFieldNoFilter(FieldNoFilter);
                                                                VALIDATE("Field No.", FieldTableMgt.LookupFieldNo("Table No.", "Field No."));
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Field No.;
                                                              NOR=Feltnr.;
                                                              SVE=F„ltnr];
                                                   BlankZero=Yes }
    { 60  ;   ;Field Caption       ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Table No.),
                                                                                                   No.=FIELD(Field No.)));
                                                   CaptionML=[ENU=Field Caption;
                                                              NOR=Felttekst;
                                                              SVE=F„ltrubrik];
                                                   Editable=No }
    { 70  ;   ;Unit of Measure     ;Code10        ;TableRelation="Unit of Measure";
                                                   OnValidate=VAR
                                                                ProjectObject@1100528400 : Record 11229311;
                                                                PlantObject@1100528500 : Record 11125830;
                                                              BEGIN
                                                                IF Level = Level::Object THEN
                                                                  CASE "Table No." OF
                                                                    DATABASE::"Project Object":
                                                                      IF "Unit of Measure" <> '' THEN
                                                                        TESTFIELD("Field No.", ProjectObject.FIELDNO("Quantity (External)"));  //DP02375
                                                                    DATABASE::"Plant Object":
                                                                      IF "Unit of Measure" <> '' THEN
                                                                        TESTFIELD("Field No.", PlantObject.FIELDNO(Quantity));
                                                                    DATABASE::"Service-Object":
                                                                      TESTFIELD(Level, Level::SpecificationLine);
                                                                  END;
                                                              END;

                                                   CaptionML=[ENU=Unit of Measure;
                                                              NOR=Enhet;
                                                              SVE=M†ttenhet] }
    { 80  ;   ;Condition           ;Text50        ;TableRelation="Standard IFC Property";
                                                   CaptionML=[ENU=Condition;
                                                              NOR=Betingelse;
                                                              SVE=Villkor] }
    { 90  ;   ;Condition Value     ;Text50        ;CaptionML=ENU=Condition Value }
  }
  KEYS
  {
    {    ;Table No.,Property                      ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    BEGIN
    END.
  }
}

