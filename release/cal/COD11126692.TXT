OBJECT Codeunit 11126692 Calculate G/L Entry Totals
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            CalcGLTotals
          END;

  }
  CODE
  {

    LOCAL PROCEDURE CalcGLTotals@1100529701();
    VAR
      GLEntryTotals@1100529701 : Record 11129007;
      Company@1100529700 : Record 2000000006;
    BEGIN
      GLEntryTotals.DELETEALL;
      IF Company.FINDSET THEN
        REPEAT
          CalcCompanyGLTotals(Company.Name);
        UNTIL Company.NEXT = 0;
    END;

    LOCAL PROCEDURE CalcCompanyGLTotals@1100529700(CompanyName@1100529700 : Text[30]);
    VAR
      GLEntry@1100529701 : Record 17;
    BEGIN
      WITH GLEntry DO BEGIN
        CHANGECOMPANY(CompanyName);
        SETCURRENTKEY("G/L Account No.","Global Dimension 1 Code","Global Dimension 2 Code","Source Type","Source No.","Job No.","Service Contract No.","Posting Date");
        IF FINDFIRST THEN
          REPEAT
            SETRANGE("G/L Account No.", "G/L Account No.");
            REPEAT
              SETRANGE("Global Dimension 1 Code", "Global Dimension 1 Code");
              REPEAT
                SETRANGE("Global Dimension 2 Code", "Global Dimension 2 Code");
                REPEAT
                  SETRANGE("Source Type", "Source Type");
                  REPEAT
                    SETRANGE("Source No.", "Source No.");
                    REPEAT
                      SETRANGE("Job No.", "Job No.");
                      REPEAT
                        SETRANGE("Service Contract No.", "Service Contract No.");
                        REPEAT
                          SETRANGE("Posting Date", CALCDATE('<-CM>',"Posting Date"), CLOSINGDATE(CALCDATE('<CM>',"Posting Date")));  // DMY2DATE(1,DATE2DMY("Posting Date",2),DATE2DMY("Posting Date",3))
                          CALCSUMS(Amount);
                          InsertTotalLine(CompanyName, GLEntry);
                          FINDLAST;
                          SETRANGE("Posting Date");
                        UNTIL NEXT = 0;
                        FINDLAST;
                        SETRANGE("Service Contract No.");
                      UNTIL NEXT = 0;
                      FINDLAST;
                      SETRANGE("Job No.");
                    UNTIL NEXT = 0;
                    FINDLAST;
                    SETRANGE("Source No.");
                  UNTIL NEXT = 0;
                  FINDLAST;
                  SETRANGE("Source Type");
                UNTIL NEXT = 0;
                FINDLAST;
                SETRANGE("Global Dimension 2 Code");
              UNTIL NEXT = 0;
              FINDLAST;
              SETRANGE("Global Dimension 1 Code");
            UNTIL NEXT = 0;
            FINDLAST;
            SETRANGE("G/L Account No.");
          UNTIL NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE InsertTotalLine@1100529702(CompanyName@1100529702 : Text[30];GLEntry@1100529700 : Record 17);
    VAR
      GLEntryTotals@1100529701 : Record 11129007;
    BEGIN
      GLEntryTotals.INIT;
      GLEntryTotals."Company Name" := CompanyName;
      GLEntryTotals."Posting Date" := NORMALDATE(CALCDATE('<CM>',GLEntry."Posting Date"));
      GLEntryTotals."G/L Account No." := GLEntry."G/L Account No.";
      GLEntryTotals."Global Dimension 1 Code" := GLEntry."Global Dimension 1 Code";
      GLEntryTotals."Global Dimension 2 Code" := GLEntry."Global Dimension 2 Code";
      GLEntryTotals."Source Type" := GLEntry."Source Type";
      GLEntryTotals."Source No." := GLEntry."Source No.";
      GLEntryTotals."Job No." := GLEntry."Job No.";
      GLEntryTotals."Service Contract No." := GLEntry."Service Contract No.";
      GLEntryTotals.Amount := GLEntry.Amount;
      GLEntryTotals.INSERT;
    END;

    BEGIN
    END.
  }
}

