OBJECT Table 11124917 Resource Planning View Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ResourcePlanningViewLine@1100525000 : Record 11124917;
             BEGIN
               ResourcePlanningViewLine.SETCURRENTKEY("Entry No.");
               IF ResourcePlanningViewLine.FINDLAST THEN
                 "Entry No." := ResourcePlanningViewLine."Entry No." + 1
               ELSE
                 "Entry No." := 1;
               ResourcePlanningViewLine.RESET;
               ResourcePlanningViewLine.SETCURRENTKEY("User ID",Code,"Line No.");
               ResourcePlanningViewLine.SETRANGE("User ID", "User ID");
               ResourcePlanningViewLine.SETRANGE(Code, Code);
               IF ResourcePlanningViewLine.FINDLAST THEN
                 "Line No." := ResourcePlanningViewLine."Line No." + 1
               ELSE
                 "Line No." := 1;
               CalculateIndent;
             END;

    OnModify=BEGIN
               CalculateIndent;
             END;

    OnRename=BEGIN
               ERROR(Text001);
             END;

    CaptionML=ENU=Resource Planning View Line;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Entry No.;
                                                              SVE=L”pnr] }
    { 10  ;   ;User ID             ;Code50        ;TableRelation="Resource Planning View"."User ID";
                                                   CaptionML=[ENU=User ID;
                                                              SVE=Anv„ndar-ID];
                                                   NotBlank=Yes }
    { 20  ;   ;Code                ;Code20        ;TableRelation="Resource Planning View".Code;
                                                   CaptionML=[ENU=Code;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 30  ;   ;Line No.            ;Integer       ;DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Line No.;
                                                              SVE=Radnr] }
    { 40  ;   ;Type                ;Option        ;InitValue=Resource;
                                                   OnValidate=BEGIN
                                                                CalculateIndent;
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Type;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=ENU=Group,Resource,Resource Group,Resource View;
                                                   OptionString=Group,Resource,ResourceGroup,ResourceView }
    { 50  ;   ;No.                 ;Code30        ;OnValidate=VAR
                                                                ResourceWOP@1100527654 : Record 11229278;
                                                                ResourceGroupWOP@1100527653 : Record 11229384;
                                                                ResourcePlanningView@1100527652 : Record 11124916;
                                                                ResourceWOP2@1100527651 : Record 11229278;
                                                                LinkedResource@1100527650 : Record 11229324;
                                                                ResourcePlanningViewLine@1100527659 : Record 11124917;
                                                                NewLineNo@1100527655 : Integer;
                                                                Skip@1100527656 : Boolean;
                                                                Confirmed@1100527658 : Boolean;
                                                                MessageString@1100527657 : Text;
                                                              BEGIN
                                                                CASE Type OF
                                                                  Type::Resource:
                                                                    BEGIN
                                                                      ResourceWOP.SETRANGE("Visible in GRP", TRUE);
                                                                      ResourceWOP.SETFILTER("User ID Filter", USERID);
                                                                      ResourceWOP.SetCompanyPlanGroupFilters;
                                                                      ResourceWOP.SETRANGE("No.", "No.");
                                                                      ResourceWOP.FINDFIRST;
                                                                      Description := ResourceWOP.Name;

                                                                      LinkedResource.SETRANGE("Main Resource No.", "No.");
                                                                      IF LinkedResource.FINDSET THEN BEGIN
                                                                        IF "Line No." = 0 THEN
                                                                          INSERT(TRUE);
                                                                        NewLineNo := "Line No.";
                                                                        REPEAT
                                                                          ResourceWOP2.RESET;
                                                                          ResourceWOP2.SETFILTER("User ID Filter", USERID);
                                                                          ResourceWOP2.SetCompanyPlanGroupFilters;
                                                                          ResourceWOP2.SETRANGE("No.", LinkedResource."Resource No.");
                                                                          IF ResourceWOP2.FINDFIRST THEN BEGIN
                                                                            Skip := FALSE;
                                                                            IF NOT Confirmed THEN
                                                                              IF CONFIRM(Text009) THEN
                                                                                Confirmed := TRUE
                                                                              ELSE
                                                                                Skip := TRUE;
                                                                            IF Confirmed THEN BEGIN
                                                                              ResourcePlanningViewLine.INIT;
                                                                              ResourcePlanningViewLine."User ID" := USERID;
                                                                              ResourcePlanningViewLine.Code := Code;
                                                                              ResourcePlanningViewLine.Type := ResourcePlanningViewLine.Type::Resource;
                                                                              ResourcePlanningViewLine."No." := ResourceWOP2."No.";
                                                                              ResourcePlanningViewLine.Description := ResourceWOP2.Name;
                                                                              ResourcePlanningViewLine.IndentPos := 1;
                                                                              ResourcePlanningViewLine.Indent := TRUE;
                                                                              ResourcePlanningViewLine.INSERT(TRUE);
                                                                              IF (ResourcePlanningViewLine."Line No." - 1) <> NewLineNo THEN
                                                                                ResourcePlanningViewLine.MoveUp(NewLineNo + 1);
                                                                              NewLineNo += 1;
                                                                            END;
                                                                          END;
                                                                        UNTIL (LinkedResource.NEXT = 0) OR Skip;
                                                                      END;
                                                                    END;

                                                                  Type::ResourceGroup:
                                                                    BEGIN
                                                                      ResourceGroupWOP.SETFILTER("User ID Filter", USERID);
                                                                      ResourceGroupWOP.SETFILTER("Company Filter", COMPANYNAME);
                                                                      ResourceGroupWOP.SETRANGE("Visible in GRP", TRUE);
                                                                      ResourceGroupWOP.SETRANGE("No.", "No.");
                                                                      ResourceGroupWOP.FINDFIRST;
                                                                      Description := ResourceGroupWOP.Name;
                                                                    END;

                                                                  Type::ResourceView:
                                                                    BEGIN
                                                                      ResourcePlanningView.GET(USERID, "No.");
                                                                      Description := ResourcePlanningView.Description;
                                                                      ResourcePlanningViewLine.RESET;
                                                                      ResourcePlanningViewLine.SETRANGE("User ID", USERID);
                                                                      ResourcePlanningViewLine.SETRANGE(Code, ResourcePlanningView.Code);
                                                                      ResourcePlanningViewLine.SETRANGE(Type, ResourcePlanningViewLine.Type::ResourceView);
                                                                      IF ResourcePlanningViewLine.FINDSET THEN BEGIN
                                                                        REPEAT
                                                                          MessageString += '\' + ResourcePlanningViewLine.Description;
                                                                        UNTIL ResourcePlanningViewLine.NEXT = 0;

                                                                        MESSAGE(Text010 + MessageString);
                                                                      END;
                                                                    END;
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              NoLookUp
                                                            END;

                                                   CaptionML=[ENU=No.;
                                                              SVE=Nr] }
    { 51  ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              SVE=Beskrivning] }
    { 60  ;   ;IndentPos           ;Integer       ;DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Indent Position }
    { 70  ;   ;Indent              ;Boolean       ;OnValidate=BEGIN
                                                                CalculateIndent;
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Indent;
                                                              SVE=Indrag] }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;User ID,Code,Line No.                    }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100525000 : TextConst 'ENU=Rename not allowed.;SVE=Det „r inte till†tet att byta namn.';
      Text002@1100525001 : TextConst 'ENU=Line %1: Resource No. not Valid.';
      Text003@1100525002 : TextConst 'ENU=Line %1: Resource Group No. not Valid.';
      Text004@1100525003 : TextConst 'ENU=Line %1: First Line Must be of the Type Group or Type Resource Set.';
      Text005@1100525004 : TextConst 'ENU=Line %1: Indent not Allowed. Previous Line Must be of the Type of Resource or Resource Group.';
      Text006@1100525005 : TextConst 'ENU=Line %1: Previous Line can not be of the Type of Resource Set.';
      Text007@1100525006 : TextConst 'ENU=Line %1: Resource Set %2 can not be Used.';
      Text008@1100525007 : TextConst 'ENU=Line %1: Resource Set No. not Valid.';
      Text009@1100527651 : TextConst 'ENU=Add Linked Resources?';
      Text010@1100527650 : TextConst 'ENU=The Following Views Inside theis View Will Not be visable in the Resource Planning:\';

    LOCAL PROCEDURE CalculateIndent@1100525005();
    BEGIN
      IF (Type = Type::Group) OR (Type = Type::ResourceView) THEN
        IndentPos := 0
      ELSE
        IF Indent THEN
          IndentPos := 2
        ELSE
          IndentPos := 1;
    END;

    PROCEDURE MoveUp@1100525000(ToPlace@1100525000 : Integer);
    VAR
      ResourcePlanningViewLine@1100525002 : Record 11124917;
      TmpResourcePlanningViewLine@1100525006 : TEMPORARY Record 11124917;
      CurrLine@1100525003 : Integer;
      MoveRestItemsDown@1100525009 : Boolean;
      StartGroupLineNo@1100525004 : Integer;
      EndGroupLineNo@1100525005 : Integer;
      PreviousGroupLineNo@1100525007 : Integer;
      NextGroupLineNo@1100525008 : Integer;
    BEGIN
      IF ToPlace > 0 THEN BEGIN
        ResourcePlanningViewLine.SETCURRENTKEY("User ID",Code,"Line No.");
        ResourcePlanningViewLine.SETRANGE("User ID", "User ID");
        ResourcePlanningViewLine.SETRANGE(Code, Code);
        ResourcePlanningViewLine.SETRANGE("Line No.", ToPlace);
        MoveRestItemsDown := NOT ResourcePlanningViewLine.ISEMPTY;

        IF MoveRestItemsDown THEN BEGIN
          ResourcePlanningViewLine.SETRANGE("Line No.", ToPlace, "Line No."-1);
          IF ResourcePlanningViewLine.FINDSET THEN
            REPEAT
              TmpResourcePlanningViewLine := ResourcePlanningViewLine;
              TmpResourcePlanningViewLine.INSERT;
            UNTIL ResourcePlanningViewLine.NEXT = 0;
        END;

        "Line No." := ToPlace;
        MODIFY;
        CurrLine := ToPlace;

        IF MoveRestItemsDown THEN BEGIN
          TmpResourcePlanningViewLine.SETCURRENTKEY("User ID",Code,"Line No.");
          IF TmpResourcePlanningViewLine.FINDSET THEN
            REPEAT
              CurrLine += 1;
              ResourcePlanningViewLine.GET(TmpResourcePlanningViewLine."Entry No.");
              ResourcePlanningViewLine."Line No." := CurrLine;
              ResourcePlanningViewLine.MODIFY;
            UNTIL TmpResourcePlanningViewLine.NEXT = 0;
        END;
      END ELSE BEGIN
        PreviousGroupLineNo := GetPreviousGroupLineNo;

        IF PreviousGroupLineNo > 0 THEN BEGIN
          GetGroupRange(StartGroupLineNo, EndGroupLineNo);
          IF PreviousGroupLineNo > StartGroupLineNo THEN BEGIN
            NextGroupLineNo := GetNextGroupLineNo;
            IF NextGroupLineNo > EndGroupLineNo THEN
              ChangePositions(PreviousGroupLineNo, "Line No."-1, "Line No.", EndGroupLineNo-1)
            ELSE
              ChangePositions(PreviousGroupLineNo, "Line No."-1, "Line No.", GetNextGroupLineNo-1);
          END;
        END;
      END;
    END;

    PROCEDURE MoveDown@1100525001();
    VAR
      ResourcePlanningViewLine@1100525002 : Record 11124917;
      NextGroupLineNo@1100525006 : Integer;
    BEGIN
      NextGroupLineNo := GetNextGroupLineNo;
      IF NextGroupLineNo < 9999 THEN BEGIN
        ResourcePlanningViewLine.SETCURRENTKEY("User ID",Code,"Line No.");
        ResourcePlanningViewLine.SETRANGE("User ID", "User ID");
        ResourcePlanningViewLine.SETRANGE(Code, Code);
        ResourcePlanningViewLine.SETRANGE("Line No.", NextGroupLineNo);
        IF ResourcePlanningViewLine.FINDFIRST THEN
          ResourcePlanningViewLine.MoveUp(0);
      END;
    END;

    PROCEDURE CheckGroup@1100525002();
    VAR
      ResourcePlanningViewLine@1100525000 : Record 11124917;
      ResourceWOP@1100525003 : Record 11229278;
      ResourceGroupWOP@1100525004 : Record 11229384;
      ResourcePlanningView@1100525005 : Record 11124916;
      FirstLine@1100525001 : Boolean;
      PreviousLineType@1100525002 : Option;
    BEGIN
      ResourcePlanningViewLine.SETCURRENTKEY("User ID",Code,"Line No.");
      ResourcePlanningViewLine.SETRANGE("User ID", "User ID");
      ResourcePlanningViewLine.SETRANGE(Code, Code);
      IF ResourcePlanningViewLine.FINDSET THEN BEGIN
        FirstLine := TRUE;
        REPEAT
          IF FirstLine THEN
            IF (ResourcePlanningViewLine.Type <> ResourcePlanningViewLine.Type::Group) AND
              (ResourcePlanningViewLine.Type <> ResourcePlanningViewLine.Type::ResourceView) THEN
              ERROR(Text004, ResourcePlanningViewLine."Line No.");
          IF NOT FirstLine THEN BEGIN
            IF ResourcePlanningViewLine.Indent = TRUE THEN
              IF PreviousLineType = ResourcePlanningViewLine.Type::Group THEN
                ERROR(Text005, ResourcePlanningViewLine."Line No.");
          END;

          CASE ResourcePlanningViewLine.Type OF
            ResourcePlanningViewLine.Type::Resource:
              BEGIN
                IF PreviousLineType = ResourcePlanningViewLine.Type::ResourceView THEN
                  ERROR(Text006, ResourcePlanningViewLine."Line No.");
                ResourceWOP.SETRANGE("Visible in GRP", TRUE);
                ResourceWOP.SETFILTER("User ID Filter", "User ID");
                ResourceWOP.SetCompanyPlanGroupFilters;
                IF NOT ResourceWOP.GET(ResourcePlanningViewLine."No.") THEN
                  ERROR(Text002, ResourcePlanningViewLine."Line No.");
              END;
            ResourcePlanningViewLine.Type::ResourceGroup:
              BEGIN
                IF PreviousLineType = ResourcePlanningViewLine.Type::ResourceView THEN
                  ERROR(Text006, ResourcePlanningViewLine."Line No.");
                ResourceGroupWOP.SETFILTER("User ID Filter", "User ID");
                ResourceGroupWOP.SETFILTER("Company Filter", COMPANYNAME);
                ResourceGroupWOP.SETRANGE("Visible in GRP", TRUE);
                IF NOT ResourceGroupWOP.GET(ResourcePlanningViewLine."No.") THEN
                  ERROR(Text003, ResourcePlanningViewLine."Line No.");
              END;
            ResourcePlanningViewLine.Type::ResourceView:
              BEGIN
                IF ResourcePlanningViewLine."No." = ResourcePlanningViewLine.Code THEN
                  ERROR(Text007, ResourcePlanningViewLine."Line No.", ResourcePlanningViewLine."No.");
                IF NOT ResourcePlanningView.GET(ResourcePlanningViewLine."User ID", ResourcePlanningViewLine."No.") THEN
                  ERROR(Text008, ResourcePlanningViewLine."Line No.");
              END;
            ELSE
              ;
          END;

          PreviousLineType := ResourcePlanningViewLine.Type;
          FirstLine := FALSE;
        UNTIL ResourcePlanningViewLine.NEXT = 0;
        CleanUpLineNo(Code);
      END;
    END;

    PROCEDURE CleanUpLineNo@1100525003(ViewCode@1100525003 : Code[20]);
    VAR
      ResourcePlanningViewLine@1100525000 : Record 11124917;
      TempResourcePlanningViewLine@1100525002 : TEMPORARY Record 11124917;
      CurrLineNo@1100525001 : Integer;
    BEGIN
      TempResourcePlanningViewLine.DELETEALL;
      ResourcePlanningViewLine.SETCURRENTKEY("User ID",Code,"Line No.");
      ResourcePlanningViewLine.SETRANGE("User ID", "User ID");
      ResourcePlanningViewLine.SETRANGE(Code, ViewCode);
      IF ResourcePlanningViewLine.FINDSET THEN BEGIN
        REPEAT
          TempResourcePlanningViewLine := ResourcePlanningViewLine;
          TempResourcePlanningViewLine.INSERT;
        UNTIL ResourcePlanningViewLine.NEXT = 0;
      END;
      TempResourcePlanningViewLine.SETCURRENTKEY("User ID",Code,"Line No.");
      IF TempResourcePlanningViewLine.FINDSET THEN BEGIN
        CurrLineNo := 1;
        REPEAT
          IF TempResourcePlanningViewLine."Line No." <> CurrLineNo THEN BEGIN
            ResourcePlanningViewLine.GET(TempResourcePlanningViewLine."Entry No.");
            //ResourcePlanningViewLine.MoveUp(CurrLineNo);
            ResourcePlanningViewLine."Line No." := CurrLineNo;
            ResourcePlanningViewLine.MODIFY;
          END;
          CurrLineNo += 1;
        UNTIL TempResourcePlanningViewLine.NEXT = 0;
      END;
    END;

    PROCEDURE NoLookUp@1100527650();
    VAR
      ResourceWOP@1100527652 : Record 11229278;
      ResourceGroupWOP@1100527651 : Record 11229384;
      ResourcePlanningView@1100527650 : Record 11124916;
    BEGIN
      CASE Type OF
        Type::Resource:
          BEGIN
            ResourceWOP.SETRANGE("Visible in GRP", TRUE);
            ResourceWOP.SETFILTER("User ID Filter", USERID);
            ResourceWOP.SetCompanyPlanGroupFilters;
            IF PAGE.RUNMODAL(PAGE::"Resources (WOP)", ResourceWOP) = ACTION::LookupOK THEN
              VALIDATE("No.", ResourceWOP."No.");
          END;
        Type::ResourceGroup:
          BEGIN
            ResourceGroupWOP.SetCompanyPlanGroupFilters;
            ResourceGroupWOP.SETFILTER("User ID Filter", USERID);
            ResourceGroupWOP.SETFILTER("Company Filter", COMPANYNAME);
            ResourceGroupWOP.SETRANGE("Visible in GRP", TRUE);
            IF PAGE.RUNMODAL(PAGE::"Resource Group List (WOP)", ResourceGroupWOP) = ACTION::LookupOK THEN
              VALIDATE("No.", ResourceGroupWOP."No.");
          END;
        Type::ResourceView:
          BEGIN
            ResourcePlanningView.SETRANGE("User ID", USERID);
            ResourcePlanningView.SETFILTER(Code, '<> %1', Code);
            IF PAGE.RUNMODAL(PAGE::"Resource Planning Views", ResourcePlanningView) = ACTION::LookupOK THEN
              VALIDATE("No.", ResourcePlanningView.Code);
          END;
      END;
    END;

    LOCAL PROCEDURE GetGroupRange@1100525004(VAR StartGroupLineNo@1100525001 : Integer;VAR EndGroupLineNo@1100525002 : Integer);
    VAR
      ResourcePlanningViewLine@1100525003 : Record 11124917;
      CurrLineNo@1100525004 : Integer;
    BEGIN
      ResourcePlanningViewLine.SETCURRENTKEY("User ID", Code, "Line No.");
      ResourcePlanningViewLine.SETRANGE("User ID", "User ID");
      ResourcePlanningViewLine.SETRANGE(Code, Code);
      ResourcePlanningViewLine.SETRANGE(IndentPos, IndentPos-1);

      ResourcePlanningViewLine.SETFILTER("Line No.", '<%1', "Line No.");
      IF ResourcePlanningViewLine.FINDLAST THEN
        StartGroupLineNo := ResourcePlanningViewLine."Line No.";
      ResourcePlanningViewLine.SETFILTER("Line No.", '>%1', "Line No.");
      IF ResourcePlanningViewLine.FINDFIRST THEN
        EndGroupLineNo := ResourcePlanningViewLine."Line No."
      ELSE
        EndGroupLineNo := 9999;
    END;

    PROCEDURE GetPreviousGroupLineNo@1100525006() : Integer;
    VAR
      ResourcePlanningViewLine@1100525000 : Record 11124917;
    BEGIN
      ResourcePlanningViewLine.SETCURRENTKEY("User ID", Code, "Line No.");
      ResourcePlanningViewLine.SETRANGE("User ID", "User ID");
      ResourcePlanningViewLine.SETRANGE(Code, Code);
      ResourcePlanningViewLine.SETFILTER("Line No.", '<%1', "Line No.");
      ResourcePlanningViewLine.SETRANGE(IndentPos, IndentPos);
      IF ResourcePlanningViewLine.FINDLAST THEN
        EXIT(ResourcePlanningViewLine."Line No.");
    END;

    PROCEDURE GetNextGroupLineNo@1100525011() : Integer;
    VAR
      ResourcePlanningViewLine@1100525000 : Record 11124917;
    BEGIN
      ResourcePlanningViewLine.SETCURRENTKEY("User ID", Code, "Line No.");
      ResourcePlanningViewLine.SETRANGE("User ID", "User ID");
      ResourcePlanningViewLine.SETRANGE(Code, Code);
      ResourcePlanningViewLine.SETFILTER("Line No.", '>%1', "Line No.");
      ResourcePlanningViewLine.SETRANGE(IndentPos, IndentPos);
      IF ResourcePlanningViewLine.FINDFIRST THEN
        EXIT(ResourcePlanningViewLine."Line No.")
      ELSE
        EXIT(9999)
    END;

    LOCAL PROCEDURE ChangePositions@1100525013(firstGroupStartNode@1100525000 : Integer;firstGroupEndNode@1100525001 : Integer;secondGroupStartNode@1100525003 : Integer;secondGroupEndNode@1100525002 : Integer);
    VAR
      ResourcePlanningViewLine@1100525007 : Record 11124917;
      TmpResourcePlanningViewLine@1100525006 : TEMPORARY Record 11124917;
      firstGrCnt@1100525004 : Integer;
      secGrCnt@1100525008 : Integer;
    BEGIN
      ResourcePlanningViewLine.SETCURRENTKEY("User ID", Code, "Line No.");
      ResourcePlanningViewLine.SETRANGE("User ID", "User ID");
      ResourcePlanningViewLine.SETRANGE(Code, Code);
      ResourcePlanningViewLine.SETRANGE("Line No.", firstGroupStartNode, secondGroupEndNode);
      IF ResourcePlanningViewLine.FINDLAST THEN
        secondGroupEndNode := ResourcePlanningViewLine."Line No.";
      IF ResourcePlanningViewLine.FINDSET THEN
        REPEAT
          TmpResourcePlanningViewLine := ResourcePlanningViewLine;
          TmpResourcePlanningViewLine.INSERT;
        UNTIL ResourcePlanningViewLine.NEXT = 0;
      ResourcePlanningViewLine.DELETEALL;

      firstGrCnt := firstGroupEndNode - firstGroupStartNode + 1;
      secGrCnt := secondGroupEndNode - secondGroupStartNode + 1;

      TmpResourcePlanningViewLine.SETCURRENTKEY("User ID", Code, "Line No.");
      TmpResourcePlanningViewLine.SETRANGE("Line No.", firstGroupStartNode, firstGroupEndNode);
      IF TmpResourcePlanningViewLine.FINDSET THEN
        REPEAT
          ResourcePlanningViewLine := TmpResourcePlanningViewLine;
          ResourcePlanningViewLine."Line No." := ResourcePlanningViewLine."Line No." + secGrCnt;
          ResourcePlanningViewLine.INSERT;
        UNTIL TmpResourcePlanningViewLine.NEXT = 0;

      TmpResourcePlanningViewLine.SETRANGE("Line No.", secondGroupStartNode, secondGroupEndNode);
      IF TmpResourcePlanningViewLine.FINDSET THEN
        REPEAT
          ResourcePlanningViewLine := TmpResourcePlanningViewLine;
          ResourcePlanningViewLine."Line No." := ResourcePlanningViewLine."Line No." - firstGrCnt;
          ResourcePlanningViewLine.INSERT;
        UNTIL TmpResourcePlanningViewLine.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

