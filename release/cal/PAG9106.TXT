OBJECT Page 9106 Customer Ledger Entry FactBox
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Customer Ledger Entry Details;
               NOR=Kundepostdetaljer;
               SVE=Detaljer om kundreskontratransaktion];
    SourceTable=Table21;
    PageType=CardPart;
    OnOpenPage=BEGIN
                 DocumentHeading := GetDocumentHeading(Rec);
                 CalcNoOfRecords;
               END;

    OnFindRecord=BEGIN
                   NoOfReminderFinEntries := 0;
                   NoOfAppliedEntries := 0;
                   DocumentHeading := '';

                   EXIT(FIND(Which));
                 END;

    OnAfterGetRecord=BEGIN
                       DocumentHeading := GetDocumentHeading(Rec);
                       CalcNoOfRecords;
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 13  ;1   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=Document;
                           NOR=Dokument;
                           SVE=Dokument];
                ToolTipML=[ENU=Specifies the document that the entry represents.;
                           NOR=Angir dokumentet som posten representerer.;
                           SVE=Anger dokumentet som transaktionen representerar.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=DocumentHeading;
                OnDrillDown=VAR
                              GLEntry@1006 : Record 17;
                            BEGIN
                              IF NOT ShowDoc THEN BEGIN
                                GLEntry.SETCURRENTKEY("Document No.","Posting Date");
                                GLEntry.SETRANGE("Document No.","Document No.");
                                GLEntry.SETRANGE("Posting Date","Posting Date");

                                PAGE.RUN(PAGE::"General Ledger Entries",GLEntry);
                              END;
                            END;
                             }

    { 1   ;1   ;Field     ;
                ToolTipML=[ENU=Specifies the due date on the entry.;
                           NOR=Angir forfallsdatoen p† posten.;
                           SVE=Anger transaktionens f”rfallodatum.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Due Date" }

    { 3   ;1   ;Field     ;
                ToolTipML=[ENU=Specifies the date on which the amount in the entry must be paid for a payment discount to be granted.;
                           NOR=Angir datoen da bel›pet i posten m† v‘re betalt for at det kan oppn†s kontantrabatt.;
                           SVE=Anger datumet d† transaktionens belopp m†ste vara betalt f”r att kassarabatten p† ordern ska g„lla.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Pmt. Discount Date" }

    { 5   ;1   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=Reminder/Fin. Charge Entries;
                           NOR=Purre-/renteposter;
                           SVE=Bet.p†minnelse-/r„ntetrans.];
                ToolTipML=[ENU=Specifies reminder entries or finance charge entries for the customer ledger entry.;
                           NOR=Angir purreposter eller renteposter for kundeposten.;
                           SVE=Anger p†minnelsetransaktioner eller r„nteavgiftstransaktioner f”r kundreskontratransaktionen.];
                ApplicationArea=#Suite;
                SourceExpr=NoOfReminderFinEntries;
                OnDrillDown=VAR
                              ReminderFinEntry@1001 : Record 300;
                            BEGIN
                              ReminderFinEntry.SETRANGE("Customer Entry No.","Entry No.");

                              PAGE.RUN(PAGE::"Reminder/Fin. Charge Entries",ReminderFinEntry);
                            END;
                             }

    { 8   ;1   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=Applied Entries;
                           NOR=Utlignede poster;
                           SVE=Kopplade transaktioner];
                ToolTipML=[ENU=Specifies applied entries for the customer ledger entry.;
                           NOR=Angir utlignede poster for kundeposten.;
                           SVE=Anger transaktioner som har kopplats till kundreskontratransaktionen.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=NoOfAppliedEntries;
                OnDrillDown=VAR
                              AppliedCustomerEntriesList@1000 : Page 61;
                            BEGIN
                              AppliedCustomerEntriesList.SetTempCustLedgEntry("Entry No.");
                              AppliedCustomerEntriesList.RUN;
                            END;
                             }

    { 2   ;1   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=Detailed Ledger Entries;
                           NOR=Detaljerte poster;
                           SVE=Detaljerad reskontra];
                ToolTipML=[ENU=Specifies detailed ledger entries for the customer ledger entry.;
                           NOR=Angir detaljerte poster for kundeposten.;
                           SVE=Anger detaljerade reskontratransaktioner f”r kundreskontratransaktionen.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=NoOfDetailedCustomerEntries;
                OnDrillDown=VAR
                              DetailedCustLedgEntry@1000 : Record 379;
                            BEGIN
                              DetailedCustLedgEntry.SETRANGE("Cust. Ledger Entry No.","Entry No.");
                              DetailedCustLedgEntry.SETRANGE("Customer No.","Customer No.");
                              PAGE.RUN(PAGE::"Detailed Cust. Ledg. Entries",DetailedCustLedgEntry);
                            END;
                             }

  }
  CODE
  {
    VAR
      NoOfReminderFinEntries@1002 : Integer;
      NoOfAppliedEntries@1000 : Integer;
      NoOfDetailedCustomerEntries@1004 : Integer;
      DocumentHeading@1001 : Text[250];
      Text000@1003 : TextConst 'ENU=Document;NOR=Dokument;SVE=Dokument';

    LOCAL PROCEDURE CalcNoOfRecords@3();
    VAR
      ReminderFinChargeEntry@1005 : Record 300;
      DetailedCustLedgEntry@1000 : Record 379;
    BEGIN
      ReminderFinChargeEntry.RESET;
      ReminderFinChargeEntry.SETRANGE("Customer Entry No.","Entry No.");
      NoOfReminderFinEntries := ReminderFinChargeEntry.COUNT;

      NoOfAppliedEntries := 0;
      IF "Entry No." <> 0 THEN
        NoOfAppliedEntries := GetNoOfAppliedEntries(Rec);

      DetailedCustLedgEntry.RESET;
      DetailedCustLedgEntry.SETRANGE("Cust. Ledger Entry No.","Entry No.");
      DetailedCustLedgEntry.SETRANGE("Customer No.","Customer No.");
      NoOfDetailedCustomerEntries := DetailedCustLedgEntry.COUNT;
    END;

    LOCAL PROCEDURE GetNoOfAppliedEntries@1(CustLedgerEntry@1000 : Record 21) : Integer;
    BEGIN
      GetAppliedEntries(CustLedgerEntry);
      EXIT(CustLedgerEntry.COUNT);
    END;

    LOCAL PROCEDURE GetAppliedEntries@2(VAR CustLedgerEntry@1000 : Record 21);
    VAR
      DtldCustLedgEntry1@1005 : Record 379;
      DtldCustLedgEntry2@1004 : Record 379;
      CreateCustLedgEntry@1002 : Record 21;
    BEGIN
      CreateCustLedgEntry := CustLedgerEntry;

      DtldCustLedgEntry1.SETCURRENTKEY("Cust. Ledger Entry No.");
      DtldCustLedgEntry1.SETRANGE("Cust. Ledger Entry No.",CreateCustLedgEntry."Entry No.");
      DtldCustLedgEntry1.SETRANGE(Unapplied,FALSE);
      IF DtldCustLedgEntry1.FINDSET THEN
        REPEAT
          IF DtldCustLedgEntry1."Cust. Ledger Entry No." =
             DtldCustLedgEntry1."Applied Cust. Ledger Entry No."
          THEN BEGIN
            DtldCustLedgEntry2.INIT;
            DtldCustLedgEntry2.SETCURRENTKEY("Applied Cust. Ledger Entry No.","Entry Type");
            DtldCustLedgEntry2.SETRANGE(
              "Applied Cust. Ledger Entry No.",DtldCustLedgEntry1."Applied Cust. Ledger Entry No.");
            DtldCustLedgEntry2.SETRANGE("Entry Type",DtldCustLedgEntry2."Entry Type"::Application);
            DtldCustLedgEntry2.SETRANGE(Unapplied,FALSE);
            IF DtldCustLedgEntry2.FIND('-') THEN
              REPEAT
                IF DtldCustLedgEntry2."Cust. Ledger Entry No." <>
                   DtldCustLedgEntry2."Applied Cust. Ledger Entry No."
                THEN BEGIN
                  CustLedgerEntry.SETCURRENTKEY("Entry No.");
                  CustLedgerEntry.SETRANGE("Entry No.",DtldCustLedgEntry2."Cust. Ledger Entry No.");
                  IF CustLedgerEntry.FINDFIRST THEN
                    CustLedgerEntry.MARK(TRUE);
                END;
              UNTIL DtldCustLedgEntry2.NEXT = 0;
          END ELSE BEGIN
            CustLedgerEntry.SETCURRENTKEY("Entry No.");
            CustLedgerEntry.SETRANGE("Entry No.",DtldCustLedgEntry1."Applied Cust. Ledger Entry No.");
            IF CustLedgerEntry.FINDFIRST THEN
              CustLedgerEntry.MARK(TRUE);
          END;
        UNTIL DtldCustLedgEntry1.NEXT = 0;

      CustLedgerEntry.SETCURRENTKEY("Entry No.");
      CustLedgerEntry.SETRANGE("Entry No.");

      IF CreateCustLedgEntry."Closed by Entry No." <> 0 THEN BEGIN
        CustLedgerEntry."Entry No." := CreateCustLedgEntry."Closed by Entry No.";
        CustLedgerEntry.MARK(TRUE);
      END;

      CustLedgerEntry.SETCURRENTKEY("Closed by Entry No.");
      CustLedgerEntry.SETRANGE("Closed by Entry No.",CreateCustLedgEntry."Entry No.");
      IF CustLedgerEntry.FINDSET THEN
        REPEAT
          CustLedgerEntry.MARK(TRUE);
        UNTIL CustLedgerEntry.NEXT = 0;

      CustLedgerEntry.SETCURRENTKEY("Entry No.");
      CustLedgerEntry.SETRANGE("Closed by Entry No.");

      CustLedgerEntry.MARKEDONLY(TRUE);
    END;

    LOCAL PROCEDURE GetDocumentHeading@6(CustLedgerEntry@1000 : Record 21) : Text[50];
    VAR
      Heading@1001 : Text[50];
    BEGIN
      IF CustLedgerEntry."Document Type" = 0 THEN
        Heading := Text000
      ELSE
        Heading := FORMAT(CustLedgerEntry."Document Type");
      Heading := Heading + ' ' + CustLedgerEntry."Document No.";
      EXIT(Heading);
    END;

    BEGIN
    END.
  }
}

