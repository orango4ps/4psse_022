OBJECT Codeunit 11125823 BIM Viewer Run Page Control
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    SingleInstance=Yes;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      TmpObjectsPagesRunning@1100529000 : TEMPORARY Record 11020579;
      Text001@1100529001 : TextConst 'DEU=''%1'' hat bereits begonnen/ist bereits aktiv;ENU=''%1'' is already started/active;NLD=''%1'' is al opgestart/actief';
      Text002@1100529002 : TextConst 'DEU=''%1'' ist noch aktiv. Muss zuerst geschlossen werden.\ \Trotzdem schlie·en von ''BIM Viewer» erzwingen?;ENU="''%1'' still active. Must be closed first.\ \Nevertheless force close ''BIM Viewer''? ";NLD="''%1'' nog actief. Moet eerst gesloten worden.\ \Desondanks sluiten ''BIM Viewer'' forceren? "';
      Text003@1100529003 : TextConst 'DEU=''und '';ENU='' and '';NLD="'' en '' "';

    PROCEDURE GetRunningGuid@1100529002() : Code[50];
    BEGIN
      EXIT(FORMAT(CREATEGUID));
    END;

    PROCEDURE BimViewerObjectPagesControl@1100525002(ControlType@1100529000 : Code[20];PageNo@1100529003 : Integer;BimModelNo@1100529002 : Code[20];RunningGuid@1100529006 : Code[50]) : Boolean;
    VAR
      PageText@1100529001 : Text;
    BEGIN
      IF (BimModelNo = '') OR (RunningGuid = '') THEN
        EXIT(TRUE);
      CASE ControlType OF
        'CHECK_RUN_PAGE':
          BEGIN
            IF NOT GetBimViewerObjectsRunning(BimModelNo, RunningGuid, PageNo) THEN
              EXIT(TRUE);
            MESSAGE(Text001, PageCaption(PageNo));
            EXIT(FALSE);
          END;
        'SET_RUN_PAGE':
          SetBimViewerObjectsRunning(BimModelNo, RunningGuid, PageNo, TRUE);
        'RESET_RUN_PAGE':
          SetBimViewerObjectsRunning(BimModelNo, RunningGuid, PageNo, FALSE);
        'PAGE_RUNNING':
          EXIT(GetBimViewerObjectsRunning(BimModelNo, RunningGuid, PageNo));
        'CHECK_CLOSE_VIEWER':
          BEGIN
            PageText := '';
            IF GetBimViewerObjectsRunning(BimModelNo, RunningGuid, PAGE::"BIM Viewer Service Objects") THEN
              PageText := PageCaption(PAGE::"BIM Viewer Service Objects");
            IF GetBimViewerObjectsRunning(BimModelNo, RunningGuid, PAGE::"BIM Viewer Project Objects") THEN BEGIN
              IF PageText <> '' THEN
                PageText := PageText + Text003;
              PageText := PageText + PageCaption(PAGE::"BIM Viewer Project Objects");
            END;
            IF PageText = '' THEN
              EXIT(TRUE);
            IF NOT CONFIRM(Text002, FALSE, PageText) THEN
              EXIT(FALSE);
            SetBimViewerObjectsRunning(BimModelNo, RunningGuid, PAGE::"BIM Viewer Service Objects", FALSE);
            SetBimViewerObjectsRunning(BimModelNo, RunningGuid, PAGE::"BIM Viewer Project Objects", FALSE);
            SetBimViewerObjectsRunning(BimModelNo, RunningGuid, PAGE::"BIM Viewer Plant Objects", FALSE);
            EXIT(TRUE);
          END;
      END;
    END;

    LOCAL PROCEDURE SetBimViewerObjectsRunning@1100529000(BimModelNo@1100529000 : Code[20];RunningGuid@1100529002 : Code[50];PageNo@1100529003 : Integer;Running@1100529001 : Boolean);
    BEGIN
      IF NOT (PageNo IN [PAGE::"BIM Viewer Service Objects", PAGE::"BIM Viewer Project Objects",PAGE::"BIM Viewer Plant Objects"]) THEN
        EXIT;
      WITH TmpObjectsPagesRunning DO BEGIN
        IF GET(BimModelNo, USERID, RunningGuid, '', '' , '') THEN BEGIN
          IF Running THEN BEGIN
            IF ((PageNo = PAGE::"BIM Viewer Service Objects") AND Bool1) OR ((PageNo = PAGE::"BIM Viewer Project Objects") AND Bool2)
              OR ((PageNo = PAGE::"BIM Viewer Plant Objects") AND Bool3) THEN
              EXIT;
            CASE PageNo OF
              PAGE::"BIM Viewer Service Objects": Bool1 := TRUE;
              PAGE::"BIM Viewer Project Objects": Bool2 := TRUE;
              PAGE::"BIM Viewer Plant Objects": Bool3 := TRUE
            END;
            MODIFY;
          END ELSE BEGIN
            CASE PageNo OF
              PAGE::"BIM Viewer Service Objects": Bool1 := FALSE;
              PAGE::"BIM Viewer Project Objects": Bool2 := FALSE;
              PAGE::"BIM Viewer Plant Objects": Bool3 := FALSE;
            END;
            IF (NOT Bool1) AND (NOT Bool2) AND (NOT Bool3) THEN
              DELETE
            ELSE
              MODIFY;
          END;
        END ELSE BEGIN
          IF Running THEN BEGIN
            INIT;
            "Key Code1" := BimModelNo;
            "Key Code2" := USERID;
            "Key Code3" := RunningGuid;
            CASE PageNo OF
              PAGE::"BIM Viewer Service Objects": Bool1 := TRUE;
              PAGE::"BIM Viewer Project Objects": Bool2 := TRUE;
              PAGE::"BIM Viewer Plant Objects" : Bool3 := TRUE;
            END;
            INSERT;
          END;
        END;
      END;
    END;

    LOCAL PROCEDURE GetBimViewerObjectsRunning@1100529001(BimModelNo@1100529000 : Code[20];RunningGuid@1100529001 : Code[50];PageNo@1100529002 : Integer) : Boolean;
    BEGIN
      WITH TmpObjectsPagesRunning DO BEGIN
        IF GET(BimModelNo, USERID, RunningGuid, '', '' , '') THEN BEGIN
          CASE PageNo OF
            PAGE::"BIM Viewer Service Objects": EXIT(Bool1);
            PAGE::"BIM Viewer Project Objects": EXIT(Bool2);
            PAGE::"BIM Viewer Plant Objects": EXIT(Bool3);
          END;
        END;
      END;
      EXIT(FALSE);
    END;

    LOCAL PROCEDURE PageCaption@1100529003(PageNo@1100529000 : Integer) : Text;
    VAR
      AllObjWithCaption@1100529001 : Record 2000000058;
    BEGIN
      IF PageNo <> 0 THEN BEGIN
        IF AllObjWithCaption.GET(AllObjWithCaption."Object Type"::Page, PageNo) THEN
          EXIT(AllObjWithCaption."Object Caption");
      END;
      EXIT('');
    END;

    BEGIN
    {
      SingleInstance codeunit

      //TODO: Tried to close page with Events. But events of BIMViewer are not working(executed) in other pages.
      //      Try again in NAV2016 then events should be supported. (JM/GJK)
      //      Also in pages 11130583/590/591
    }
    END.
  }
}

