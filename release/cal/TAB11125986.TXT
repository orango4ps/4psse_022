OBJECT Table 11125986 QA Deviation
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    DataCaptionFields=No.,Description;
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 QASetup.GET;
                 QASetup.TESTFIELD("Deviation Nos.");
                 NoSeriesMgt.InitSeries(QASetup."Deviation Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;

               ProjectSiteMgt.LogQADeviation(1, Rec); //**4PS.n
             END;

    OnModify=BEGIN
               IF Fixed = xRec.Fixed THEN
                 TESTFIELD(Fixed, FALSE);

               ProjectSiteMgt.LogQADeviation(2, Rec); //**4PS.n
             END;

    OnDelete=VAR
               DeviationTextLine@1100525001 : Record 11125987;
               DevSelectedOption@1100525000 : Record 11125994;
             BEGIN
               TESTFIELD(Fixed, FALSE);

               DeviationTextLine.SETRANGE("Deviation No.", "No.");
               DeviationTextLine.DELETEALL;

               DevSelectedOption.SETRANGE("Deviation No.", "No.");
               DevSelectedOption.DELETEALL;

               ProjectSiteMgt.LogQADeviation(3, Rec); //**4PS.n
             END;

    CaptionML=[ENU=QA Deviation;
               NOR=Avvik kvalitetssikring;
               SVE=KvalitetssÑkringsavvikelse];
    LookupPageID=Page11125987;
    DrillDownPageID=Page11125987;
  }
  FIELDS
  {
    { 10  ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  QASetup.GET;
                                                                  NoSeriesMgt.TestManual(QASetup."Deviation Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              NOR=Nr.;
                                                              SVE=Nr] }
    { 20  ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              NOR=Nr-serie;
                                                              SVE=Nr-serie] }
    { 30  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 40  ;   ;Source Type         ;Option        ;OnValidate=BEGIN
                                                                CheckIfSourceCanBeChanged;
                                                                IF "No." <> '' THEN //C019513
                                                                  IF "Source Type" <> xRec."Source Type" THEN BEGIN
                                                                    "Source No." := '';
                                                                    "Project Element" := '';
                                                                  END;
                                                              END;

                                                   CaptionML=[ENU=Source Type;
                                                              NOR=Kildetype;
                                                              SVE=Ursprungstyp];
                                                   OptionCaptionML=[ENU=Project,Service Order;
                                                                    NOR=Prosjekt,Serviceordre;
                                                                    SVE=Projekt,Serviceorder];
                                                   OptionString=Project,Service Order }
    { 50  ;   ;Source No.          ;Code20        ;TableRelation=IF (Source Type=CONST(Project)) Job
                                                                 ELSE IF (Source Type=CONST(Service Order)) "Service Order";
                                                   OnValidate=BEGIN
                                                                CheckIfSourceCanBeChanged;
                                                                IF "Source No." <> xRec."Source No." THEN BEGIN
                                                                  "Project Element" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Source No.;
                                                              NOR=Kildenr.;
                                                              SVE=Ursprungsnr] }
    { 60  ;   ;Created by          ;Code20        ;TableRelation=Employee;
                                                   OnValidate=BEGIN
                                                                "Name Created by" := GetFullNameOfEmployee("Created by");
                                                              END;

                                                   CaptionML=[ENU=Created by;
                                                              NOR=Opprettet av;
                                                              SVE=Skapades av] }
    { 70  ;   ;Name Created by     ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Full Name" WHERE (No.=FIELD(Created by)));
                                                   CaptionML=[ENU=Name Created by;
                                                              NOR=Navn opprettet av;
                                                              SVE=Namn skapat av];
                                                   Editable=No }
    { 80  ;   ;Creation Date       ;Date          ;CaptionML=[ENU=Creation Date;
                                                              NOR=Opprettelsesdato;
                                                              SVE=UpplÑggningsdatum] }
    { 90  ;   ;Type                ;Code10        ;TableRelation="QA Deviation Type";
                                                   CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Radtyp] }
    { 91  ;   ;Type Description    ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("QA Deviation Type".Description WHERE (Code=FIELD(Type)));
                                                   CaptionML=[ENU=Type Description;
                                                              NOR=Typebeskrivelse;
                                                              SVE=Radtypbeskrivning];
                                                   Editable=No }
    { 100 ;   ;Cause               ;Code10        ;TableRelation="QA Deviation Cause";
                                                   CaptionML=[ENU=Cause;
                                                              NOR=èrsak;
                                                              SVE=Orsak] }
    { 101 ;   ;Cause Description   ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("QA Deviation Cause".Description WHERE (Code=FIELD(Cause)));
                                                   CaptionML=[ENU=Cause Description;
                                                              NOR=èrsaksbeskrivelse;
                                                              SVE=Orsaksbeskrivning];
                                                   Editable=No }
    { 110 ;   ;Project Phase       ;Code10        ;TableRelation="QA Project Phase";
                                                   CaptionML=[ENU=Project Phase;
                                                              NOR=Prosjektfase;
                                                              SVE=Projektfas] }
    { 111 ;   ;Project Phase Description;Text50   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("QA Project Phase".Description WHERE (Code=FIELD(Project Phase)));
                                                   CaptionML=[ENU=Project Phase Description;
                                                              NOR=Prosjektfase beskrivelse;
                                                              SVE=Beskrivning av projektfas];
                                                   Editable=No }
    { 120 ;   ;Calling Contact     ;Code10        ;TableRelation="QA Deviation Calling Contact";
                                                   CaptionML=[ENU=Calling Contact;
                                                              NOR=Samtalekontakt;
                                                              SVE=Samtalskontakt] }
    { 121 ;   ;Calling Contact Description;Text50 ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("QA Deviation Calling Contact".Description WHERE (Code=FIELD(Calling Contact)));
                                                   CaptionML=[ENU=Calling Contact Description;
                                                              NOR=Beskrivelse av samtalekontakt;
                                                              SVE=Samtalskontaktbeskrivning];
                                                   Editable=No }
    { 130 ;   ;Observation Date    ;Date          ;CaptionML=[ENU=Observation Date;
                                                              NOR=Observasjonsdato;
                                                              SVE=Observationsdatum] }
    { 140 ;   ;Settlement Text     ;Text100       ;CaptionML=[ENU=Settlement Text;
                                                              NOR=Oppgjõrstekst;
                                                              SVE=AvrÑkningstext] }
    { 150 ;   ;Cost                ;Decimal       ;CaptionML=[ENU=Cost;
                                                              NOR=Kostnader;
                                                              SVE=Kostnad];
                                                   DecimalPlaces=2:2;
                                                   MinValue=0 }
    { 160 ;   ;Cost Text           ;Text100       ;CaptionML=[ENU=Cost Text;
                                                              NOR=Kostnadstekst;
                                                              SVE=Kostnadstext] }
    { 210 ;   ;Status              ;Code10        ;TableRelation="QA Deviation Status";
                                                   CaptionML=[ENU=Status;
                                                              NOR=Status;
                                                              SVE=Status] }
    { 211 ;   ;Status Description  ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("QA Deviation Status".Description WHERE (Code=FIELD(Status)));
                                                   CaptionML=[ENU=Status Description;
                                                              NOR=Statusbeskrivelse;
                                                              SVE=Statusbeskrivning];
                                                   Editable=No }
    { 220 ;   ;Fixed               ;Boolean       ;CaptionML=[ENU=Fixed;
                                                              NOR=Fast;
                                                              SVE=Fast] }
    { 230 ;   ;Complaint No.       ;Code20        ;TableRelation=IF (Source Type=CONST(Project)) Complaint WHERE (Project No.=FIELD(Source No.));
                                                   OnValidate=BEGIN
                                                                IF "Complaint No." <> '' THEN
                                                                  TESTFIELD("Source Type", "Source Type"::Project);
                                                              END;

                                                   CaptionML=[ENU=Complaint No.;
                                                              NOR=Klagenr;
                                                              SVE=KlagomÜlsnr] }
    { 240 ;   ;Project Element     ;Code20        ;TableRelation=IF (Source Type=CONST(Project)) "Project Element".Element WHERE (Project No.=FIELD(Source No.));
                                                   OnValidate=BEGIN
                                                                IF "Project Element" <> '' THEN
                                                                  TESTFIELD("Source Type", "Source Type"::Project);
                                                              END;

                                                   CaptionML=[ENU=Project Element;
                                                              NOR=Prosjektelement;
                                                              SVE=Projektelement] }
    { 250 ;   ;Description Text Lines Exist;Boolean;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Exist("QA Deviation Text Line" WHERE (Deviation No.=FIELD(No.),
                                                                                                     Type=CONST(Deviation Description)));
                                                   CaptionML=[ENU=Description Text Lines Exist;
                                                              NOR=Tekstrader for beskrivelse eksisterer;
                                                              SVE=Beskrivning textrader finns];
                                                   Editable=No }
    { 260 ;   ;Cause Text Lines Exist;Boolean     ;FieldClass=FlowField;
                                                   CalcFormula=Exist("QA Deviation Text Line" WHERE (Deviation No.=FIELD(No.),
                                                                                                     Type=CONST(Cause)));
                                                   CaptionML=[ENU=Cause Text Lines Exist;
                                                              NOR=Tekstrader for mÜl eksisterer;
                                                              SVE=Orsak textrader finns];
                                                   Editable=No }
    { 270 ;   ;Measure Text Lines Exist;Boolean   ;FieldClass=FlowField;
                                                   CalcFormula=Exist("QA Deviation Text Line" WHERE (Deviation No.=FIELD(No.),
                                                                                                     Type=CONST(Measure)));
                                                   CaptionML=[ENU=Measure Text Lines Exist;
                                                              NOR=Tekstrader for mÜl eksisterer;
                                                              SVE=MÑtning textrader finns];
                                                   Editable=No }
    { 280 ;   ;Impact Text Lines Exist;Boolean    ;FieldClass=FlowField;
                                                   CalcFormula=Exist("QA Deviation Text Line" WHERE (Deviation No.=FIELD(No.),
                                                                                                     Type=CONST(Impact)));
                                                   CaptionML=[ENU=Impact Text Lines Exist;
                                                              NOR=Tekstrader for pÜvirkning eksisterer;
                                                              SVE=Impakt textrader finns];
                                                   Editable=No }
    { 290 ;   ;Applicability Text Lines Exist;Boolean;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Exist("QA Deviation Text Line" WHERE (Deviation No.=FIELD(No.),
                                                                                                     Type=CONST(Applicability)));
                                                   CaptionML=[ENU=Applicability Text Lines Exist;
                                                              NOR=Tekstrader for anvendelighet eksisterer;
                                                              SVE=TillÑmplighet textrader finns];
                                                   Editable=No }
    { 300 ;   ;Prev. Measure Text Lines Exist;Boolean;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Exist("QA Deviation Text Line" WHERE (Deviation No.=FIELD(No.),
                                                                                                     Type=CONST(Preventive Measure)));
                                                   CaptionML=[ENU=Prev. Measure Text Lines Exist;
                                                              NOR=Tekstrader for preventive tiltak eksisterer;
                                                              SVE=Fîreg. MÑtning textrader finns];
                                                   Editable=No }
    { 310 ;   ;Responsible Person  ;Code20        ;TableRelation=Employee;
                                                   OnValidate=BEGIN
                                                                "Name Responsible Person" := GetFullNameOfEmployee("Responsible Person");
                                                              END;

                                                   CaptionML=[ENU=Responsible Person;
                                                              NOR=Ansvarlig person;
                                                              SVE=Ansvarig person] }
    { 320 ;   ;Name Responsible Person;Text100    ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Full Name" WHERE (No.=FIELD(Responsible Person)));
                                                   CaptionML=[ENU=Name Responsible Person;
                                                              NOR=Navn pÜ ansvarlig;
                                                              SVE=Namn pÜ ansvarig];
                                                   Editable=No }
    { 330 ;   ;Action Planned Date ;Date          ;CaptionML=ENU=Action Planned Date }
    { 340 ;   ;Action Date         ;Date          ;CaptionML=ENU=Action Date }
    { 350 ;   ;Inspection Planned Date;Date       ;CaptionML=ENU=Inspection Planned Date }
    { 360 ;   ;Inspection Date     ;Date          ;CaptionML=[ENU=Inspection Date;
                                                              NOR=Inspeksjonsdato;
                                                              SVE=Inspektionsdatum] }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Source Type,Source No.                   }
    {    ;Complaint No.,Source Type,Source No.     }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      QASetup@1100525000 : Record 11126001;
      NoSeriesMgt@1100525001 : Codeunit 396;
      ProjectSiteMgt@1100525002 : Codeunit 11012282;

    PROCEDURE InitRecord@1100525000();
    BEGIN
      "Creation Date" := TODAY;
      VALIDATE("Created by", GetEmployeeNoOfCurrentUser);
    END;

    LOCAL PROCEDURE GetEmployeeNoOfCurrentUser@1100525001() : Code[20];
    VAR
      UserSetup@1100525000 : Record 91;
    BEGIN
      IF UserSetup.GET(USERID) THEN
        EXIT(UserSetup."Employee No.");
    END;

    LOCAL PROCEDURE GetFullNameOfEmployee@1100525005(EmployeeCode@1100525001 : Code[20]) : Text[100];
    VAR
      Employee@1100525000 : Record 5200;
    BEGIN
      IF Employee.GET(EmployeeCode) THEN
        EXIT(Employee."Full Name");
    END;

    PROCEDURE GetSourceNoCaption@1100525008() : Text[100];
    BEGIN
      EXIT(FORMAT("Source Type"));
    END;

    PROCEDURE GetSourceDescription@1100525002() : Text[100];
    VAR
      Proj@1100525000 : Record 11072003;
      ServOrder@1100525001 : Record 11012823;
    BEGIN
      IF "Source No." <> '' THEN BEGIN
        CASE "Source Type" OF
          "Source Type"::Project:
            BEGIN
              IF Proj.GET("Source No.") THEN
                EXIT(Proj.Description);
            END;
          "Source Type"::"Service Order":
            BEGIN
              IF ServOrder.GET("Source No.") THEN
                EXIT(ServOrder.Description);
            END;

        END;
      END;
      EXIT('');
    END;

    PROCEDURE AssistEdit@1100525003(OldQADeviation@1100525000 : Record 11125986) : Boolean;
    VAR
      QADeviation@1100525001 : Record 11125986;
    BEGIN
      WITH QADeviation DO BEGIN
        QADeviation := Rec;
        QASetup.GET;
        QASetup.TESTFIELD("Deviation Nos.");
        IF NoSeriesMgt.SelectSeries(QASetup."Deviation Nos.", OldQADeviation."No. Series", "No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("No.");
          Rec := QADeviation;
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE HasDocumentLinks@1100525004() : Boolean;
    VAR
      DocumentLinkMgt@1100525000 : Codeunit 11012401;
      RecRef@1100525001 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      EXIT(DocumentLinkMgt.EntityHasDocumentLinks(RecRef));
    END;

    LOCAL PROCEDURE CheckIfSourceCanBeChanged@1100525006();
    BEGIN
      // The source of an existing QA deviation that is linked to a complaint is not allowed to be changed.
      IF "No." <> '' THEN
        TESTFIELD("Complaint No.", '');
    END;

    PROCEDURE CheckIfQADeviationCanBeChanged@1100525007(QADeviationNo@1100525001 : Code[20]);
    VAR
      QADeviation@1100525000 : Record 11125986;
    BEGIN
      IF QADeviation.GET(QADeviationNo) THEN
        QADeviation.TESTFIELD(Fixed, FALSE);
    END;

    BEGIN
    END.
  }
}

