OBJECT Codeunit 11128067 VP File BG Foreign UTLI
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=PE8.03 (12048864),4PSSE;
  }
  PROPERTIES
  {
    TableNo=11128079;
    OnRun=VAR
            GiroType@1100570003 : Record 11128061;
          BEGIN
            CLEAR(gTotalAmountLCY);
            CLEAR(gNoOfPayments);

            GiroType.GET(Rec."Giro Type Code");
            GiroType.TESTFIELD(Filename);
            gToFile := GiroType.Filename;

            GiroType.TESTFIELD("Bank Giro No.");
            IF NOT gVPMnm.SumCheck10Control(GiroType."Bank Giro No.") THEN
              ERROR(PEB002);

            OpenFile;

            gVPMnm.SetGiroType(GiroType);

            CreateFileBGForeign(Rec);

            CloseFile(Rec);
          END;

  }
  CODE
  {
    VAR
      gFile@1100570021 : File;
      gVPMnm@1100570002 : Codeunit 11128061;
      PEB001@1100570020 : TextConst 'ENU=The length for %1 is %2 long, %3 characters are maximum;NOR=Lengden p† %1 er %2 tegn. Maksimumslengden er %3 tegn;SVE=L„ngden p† %1 „r %2 tecken. Maximil„ngden „r %3 tecken';
      PEB002@1100570001 : TextConst 'ENU=Bank Giro No. for the Company is not correct!;NOR=Selskapets Bankgironummer er feilaktig!;SVE=F”retagets bankgironummer „r felaktigt!';
      PEB003@1100570018 : TextConst 'ENU=Code for Bank of Sweden  is missing for %1 %2, Document No. %3 Vendor %4 %5;NOR=Riksbankskode mangler for %1 %2, Dokumentnr %3 og leverand›r %4 %5;SVE=Riksbankskod saknas f”r %1 %2, dokumentnr %3 och leverant”r %4 %5';
      PEB004@1100570019 : TextConst 'ENU=%1 must not be %2 for %3, %4, %5;SVE=%1 f†r inte vara %2 f”r %3, %4, %5';
      gTotalAmountLCY@1100570000 : Decimal;
      gFromFile@1100570004 : Text;
      gToFile@1100570006 : Text;
      PEB005@1100570003 : TextConst 'ENU=has wrong format;SVE=har fel format';
      PEB006@1100570005 : TextConst 'ENU=must not be blank;SVE=f†r inte var blankt';
      gNoOfPayments@12047958 : Integer;
      PEB007@12047959 : TextConst 'ENU=All payments are not included in the file, check the currency;SVE=Alla betalningar kom inte med i filen, kontrollera valutorna';

    PROCEDURE CreateFileBGForeign@1070012(pPack@1070000 : Record 11128079);
    VAR
      VendLedgerEntry@1070001 : Record 25;
      VendNo@1070002 : Code[20];
      PmtChargePayedBy@1070004 : Integer;
      CurrencyCode@1070008 : Code[10];
      PaymMethodTxt@1070009 : Text[1];
      Vendor@1100570000 : Record 23;
      Currency@1100570001 : Record 4;
      GiroType@1100570005 : Record 11128061;
      VPPackEntry@1100570003 : Record 11128063;
      VendorBankAccount@1100570002 : Record 288;
      AmountToPay@1100570006 : Decimal;
      AmountToPayLCY@1100570007 : Decimal;
      TypeOfLine@1100570008 : Text;
      TotalAmount@1100570009 : Decimal;
      CompanyInfo@1100570010 : Record 79;
      BPCode@1100570004 : Text;
      BankAccountNo@1100570011 : Code[37];
      "IBAN/BankAccountNo"@12047958 : Text;
    BEGIN
      GiroType.GET(pPack."Giro Type Code");
      GiroType.TESTFIELD("Bank Giro No.");
      IF NOT gVPMnm.SumCheck10Control(GiroType."Bank Giro No.") THEN
        ERROR(PEB002);

      CompanyInfo.GET;
      CompanyInfo.TESTFIELD(Name);
      CompanyInfo.TESTFIELD(Address);
      CLEAR(TotalAmount);

      WITH VendLedgerEntry DO BEGIN
        VPPackEntry.RESET;
        VPPackEntry.SETRANGE("Pack Entry No.",pPack."Entry No.");
        IF NOT VPPackEntry.FINDSET THEN
          EXIT;

        WriteFile(80,'0' +
           gVPMnm.NFL(gVPMnm.ClearText(GiroType."Bank Giro No."),8) +
           gVPMnm.Date2Text(TODAY,6) +
           gVPMnm.BLK(63) +
           '2');

        REPEAT
          gVPMnm.CheckCustomerPaymentAllowed(pPack,VPPackEntry);
          gNoOfPayments += 1;
          GET(VPPackEntry."Ledger Entry No.");  // Get Vendor Leadger Entry
          TESTFIELD(Open,TRUE);
          VendorBankAccount.GET(VPPackEntry."No.",VPPackEntry."Bank Account Code");

          "IBAN/BankAccountNo" := gVPMnm."GetIBAN/BankccountNo"(VendorBankAccount);

          CALCFIELDS("Remaining Amount","Remaining Amt. (LCY)");

          Vendor.GET("Vendor No.");
          Vendor.TESTFIELD(Name);
          IF gVPMnm.FillField(Vendor."Address 2",Vendor.Address) = '' THEN
            Vendor.FIELDERROR(Address,PEB006);

          IF NOT Currency.GET("Currency Code") THEN
            CLEAR(Currency);
          IF gVPMnm.IsLocalCurr("Currency Code") THEN
            CurrencyCode := gVPMnm.GetSEKEURCurrencyCode
          ELSE
            CurrencyCode := "Currency Code";

          IF VPPackEntry."Currency Account" THEN BEGIN
            IF VendorBankAccount."Country Code ISO + Account No." <> '' THEN BEGIN
              BankAccountNo := VendorBankAccount."Country Code ISO + Account No.";
              IF NOT CheckBankAccountNoFormat(BankAccountNo) THEN
                VendorBankAccount.FIELDERROR("Country Code ISO + Account No.",PEB005);
            END ELSE
              IF Currency."Country Code ISO + Account No." <> '' THEN BEGIN
                BankAccountNo := Currency."Country Code ISO + Account No.";
                IF NOT CheckBankAccountNoFormat(BankAccountNo) THEN
                  Currency.FIELDERROR("Country Code ISO + Account No.",PEB005);
              END ELSE BEGIN
                GiroType.TESTFIELD("Country Code ISO + Account No.");
                BankAccountNo := GiroType."Country Code ISO + Account No.";
                IF NOT CheckBankAccountNoFormat(BankAccountNo) THEN
                  GiroType.FIELDERROR("Country Code ISO + Account No.",PEB005);
              END;
          END ELSE BEGIN
            GiroType.TESTFIELD("Country Code ISO + Account No.");
            BankAccountNo := GiroType."Country Code ISO + Account No.";
            IF NOT CheckBankAccountNoFormat(BankAccountNo) THEN
              GiroType.FIELDERROR("Country Code ISO + Account No.",PEB005);
          END;

          IF VPPackEntry."Code for Bank of Sweden" = '' THEN
            ERROR(PEB003,
                  "Document Type","External Document No.","Document No.",Vendor."No.",Vendor.Name);

          VendNo := gVPMnm.GetVendGiroNo(Vendor,Currency);


          CASE VendorBankAccount."Payment Charge Paid by" OF
            VendorBankAccount."Payment Charge Paid by"::"Sender - Swedish Cost": PmtChargePayedBy := 0;
            VendorBankAccount."Payment Charge Paid by"::"Sender - All Cost": PmtChargePayedBy := 9;
            VendorBankAccount."Payment Charge Paid by"::"Express Fee Receiver",
            VendorBankAccount."Payment Charge Paid by"::Receiver: PmtChargePayedBy := 1;
          END;

          CASE VendorBankAccount."Payment Method" OF
            VendorBankAccount."Payment Method"::Normal: PaymMethodTxt := 'B';
            VendorBankAccount."Payment Method"::Express: PaymMethodTxt := 'T';
          END;

          IF ("IBAN/BankAccountNo" <>'') AND
             (VendorBankAccount."SWIFT Code" <>'') THEN
            BPCode := 'BP'
          ELSE
            BPCode := '';

          AmountToPay := gVPMnm.CalcVendAmountToPay(VendLedgerEntry);
          AmountToPayLCY := gVPMnm.CalcVendAmountToPayLCY(VendLedgerEntry);

          IF NOT (VendorBankAccount."Payment Type" IN [VendorBankAccount."Payment Type"::Check,VendorBankAccount."Payment Type"::Account]) THEN
            ERROR(PEB004,VendorBankAccount.FIELDCAPTION("Payment Type"),
                        VendorBankAccount."Payment Type",
                        VendorBankAccount.TABLECAPTION,
                        VendorBankAccount."Vendor No.",
                        VendorBankAccount.Code);

          IF "Remaining Amount" > 0 THEN // Credit Memo
            TypeOfLine := '5'
          ELSE                           // Invoice
            TypeOfLine := '6';

          TotalAmount +=  AmountToPay;
          gTotalAmountLCY += AmountToPayLCY;

          WriteFile(80,'2' +
             gVPMnm.NFL(VendNo,7) +
             gVPMnm.TFR(Vendor.Name,30) +
             gVPMnm.TFR(Vendor."Name 2",35));

          WriteFile(80,'3' +
             gVPMnm.NFL(VendNo,7) +
             gVPMnm.TFR(gVPMnm.FillField(Vendor."Address 2",Vendor.Address),30) +
             gVPMnm.TFR(Vendor."Post Code" + ' ' + Vendor.City,35) +
             gVPMnm.BLK(1) +
             gVPMnm.TFR(Vendor."Country/Region Code",2) +
             gVPMnm.BLK(1) +
             gVPMnm.NFL(FORMAT(PmtChargePayedBy),1) +
             gVPMnm.NFL(PaymMethodTxt,1));

          WriteFile(80,'4' +
             gVPMnm.NFL(VendNo,7) +
             gVPMnm.TFR("IBAN/BankAccountNo",35) +
             gVPMnm.TFR(VendorBankAccount."SWIFT Code",24) +
             gVPMnm.TFR(VendorBankAccount."SWIFT Code",11) +
             BPCode);

          // Invoice and Credit Memo
          IF TypeOfLine = '5' THEN
            WriteFile(80,'5' +
               gVPMnm.NFL(VendNo,7) +
               gVPMnm.TFR("External Document No.",25) +
               gVPMnm.NFL(gVPMnm.Dec2TextBGForeign(-AmountToPayLCY),11) +
               gVPMnm.BLK(10) +
               gVPMnm.TFR(CurrencyCode,3) +
               gVPMnm.Date2Text("Due Date",6) +
               gVPMnm.BLK(2) +
               gVPMnm.NFL(gVPMnm.Dec2TextBGForeign(-AmountToPay),13))
          ELSE
            WriteFile(80,'6' +
               gVPMnm.NFL(VendNo,7) +
               gVPMnm.TFR("External Document No.",25) +
               gVPMnm.NFL(gVPMnm.Dec2TextBGForeign(-AmountToPayLCY),11) +
               gVPMnm.BLK(10) +
               gVPMnm.TFR(CurrencyCode,3) +
               gVPMnm.Date2Text("Due Date",6) +
               gVPMnm.BLK(2) +
               gVPMnm.NFL(gVPMnm.Dec2TextBGForeign(-AmountToPay),13));

          WriteFile(80,'7' +
             gVPMnm.NFL(VendNo,7) +
             gVPMnm.TFR(VPPackEntry."Code for Bank of Sweden",4) +
             gVPMnm.BLK(8) +
             'D' +
             gVPMnm.TFR(BankAccountNo,37) +
             gVPMnm.BLK(2) +
             gVPMnm.NFL("Document No.",20)
             );

        UNTIL VPPackEntry.NEXT = 0;
        WriteFile(80,'9' +
           gVPMnm.NFL(gVPMnm.ClearText(GiroType."Bank Giro No."),8)+
           gVPMnm.NFL(gVPMnm.Dec2TextBGForeign(-gTotalAmountLCY),12) +
           gVPMnm.BLK(42) +
           gVPMnm.NFL(gVPMnm.Dec2TextBGForeign(-TotalAmount),15));

      END;
    END;

    PROCEDURE WriteFile@1070040(pLength@1070000 : Integer;pText@1070001 : Text[250]);
    VAR
      SEUtils@1100285100 : Codeunit 11128000;
    BEGIN
      IF STRLEN(pText) > pLength THEN
        ERROR(PEB001,pText,STRLEN(pText),pLength);
      pText := SEUtils.Ascii2Ansi(pText);
      gFile.WRITE(PADSTR(pText,pLength));
    END;

    PROCEDURE OpenFile@1070005();
    VAR
      FileMgt@1100570001 : Codeunit 419;
    BEGIN
      gFile.TEXTMODE(TRUE);
      gFile.WRITEMODE(TRUE);
      gFromFile := FileMgt.ServerTempFileName('.txt');
      gFile.CREATE(gFromFile);
    END;

    PROCEDURE CloseFile@1070009(pPack@1070000 : Record 11128079);
    VAR
      FileMgt@1100570000 : Codeunit 419;
      TextMgt@12047959 : Codeunit 11128088;
      VPPackEntry@12047958 : Record 11128063;
      Handled@12047960 : Boolean;
    BEGIN
      VPPackEntry.RESET;
      VPPackEntry.SETRANGE("Pack Entry No.",pPack."Entry No.");
      IF VPPackEntry.COUNT <> gNoOfPayments THEN
        ERROR(PEB007);

      pPack.LOCKTABLE;
      pPack.FIND;
      pPack."Total Amount (LCY)" := -gTotalAmountLCY;
      pPack.MODIFY;
      gFile.CLOSE;

      OnBeforeSaveFile(pPack,gFromFile,Handled);

      IF NOT Handled THEN BEGIN
        IF NOT FileMgt.CanRunDotNetOnClient THEN
          FileMgt.DownloadHandler(gFromFile,'','','',gVPMnm.SetFileName(pPack,gToFile))
        ELSE
          FileMgt.DownloadToFile(gFromFile,gVPMnm.SetFileName(pPack,gToFile));
      END;

      IF EXISTS(gFromFile) THEN
        ERASE(gFromFile);
    END;

    PROCEDURE CheckBankAccountNoFormat@1100570002(pAccountNo@1100570000 : Code[37]) : Boolean;
    BEGIN
      // Tv† f”rsta pos. skall ineh†llla landod enligt ISO
      IF gVPMnm.ClearText(COPYSTR(pAccountNo,1,2)) <> '' THEN
        EXIT(FALSE);

      // Pos 3-37 skall ineh†lla kontonr
      IF DELCHR(COPYSTR(pAccountNo,3),'=','1234567890') <> '' THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeSaveFile@12047959(Pack@12047961 : Record 11128079;ServerFilename@12047958 : Text;VAR Handled@12047960 : Boolean);
    BEGIN
    END;

    BEGIN
    {
      PEB *** Begin ***
      PEB0091 Banking
      PEB
      PEB *** End ***

      171129 ITERO.DL export files as ASCII instead of defalut ANSI
      190212 ORANGO.DL upgrade to PE8.08
    }
    END.
  }
}

