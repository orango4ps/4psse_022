OBJECT Report 11012563 Bulk Edit Rental Free Periods
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Kollektiv Kalender „ndern;
               ENU=Change Calendars collectively;
               NLD=Collectief kalenders wijzigen;
               NOR=Endre kalendre kollektivt;
               SVE=Žndra kalendrar kollektivt];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF (DateFrom = 0D) OR (DateTo = 0D) THEN
                    ERROR(Text000);

                  IF ((DateTo - DateFrom + 1) > 1000) THEN
                    ERROR(Text002 + Text003);

                  IF (NOT ModifySaturday) AND
                     (NOT ModifySunday) AND
                     (NOT ModifyOtherDays)
                  THEN
                    ERROR(Text001);

                  IF ProcessType <> ProcessType::Modify THEN
                    ValidateRentalCalendarCopyFrom();

                  Counter := 0;
                  PeriodNoFilterTxt := '';

                  IF ModifyOtherDays THEN
                    PeriodNoFilterTxt := '1|2|3|4|5';
                  IF ModifySaturday THEN BEGIN
                    IF PeriodNoFilterTxt <> '' THEN
                      PeriodNoFilterTxt := PeriodNoFilterTxt + '|';
                    PeriodNoFilterTxt := PeriodNoFilterTxt + '6';
                  END;
                  IF ModifySunday THEN BEGIN
                    IF PeriodNoFilterTxt <> '' THEN
                      PeriodNoFilterTxt := PeriodNoFilterTxt + '|';
                    PeriodNoFilterTxt := PeriodNoFilterTxt + '7';
                  END;
                END;

    OnPostReport=BEGIN
                   IF Counter = 0 THEN
                     MESSAGE(Text004)
                   ELSE
                     MESSAGE(Text005,Counter);
                 END;

  }
  DATASET
  {
    { 3584;    ;DataItem;                    ;
               DataItemTable=Table11012568;
               DataItemTableView=SORTING(Type,Territory Code,Plant Calendar);
               ReqFilterFields=Type,Territory Code,Plant Calendar }

    { 9857;1   ;DataItem;                    ;
               DataItemTable=Table2000000007;
               DataItemTableView=SORTING(Period Type,Period Start)
                                 WHERE(Period Type=CONST(Date));
               OnPreDataItem=BEGIN
                               Date.SETRANGE("Period Start", DateFrom, DateTo);
                               Date.SETFILTER("Period No.", PeriodNoFilterTxt);
                             END;

               OnAfterGetRecord=VAR
                                  RentalCalDayFrom@1100525000 : Record 11012569;
                                BEGIN
                                  IF ProcessType = ProcessType::Modify THEN BEGIN
                                    CASE Date."Period No." OF
                                      1,2,3,4,5:
                                        ChangeRentalCalendarDay(OtherDaysNewValue);
                                      6:
                                        ChangeRentalCalendarDay(SaturdayNewValue);
                                      7:
                                        ChangeRentalCalendarDay(SundayNewValue);
                                    END;
                                  END ELSE BEGIN
                                    IF RentalCalDayFrom.GET(CalendarTypeFrom, TerritoryCodeFrom, PlantCalendarFrom, "Period Start") THEN
                                      ChangeRentalCalendarDay(2)  //* 1=Rental Free Day
                                    ELSE BEGIN
                                      IF ProcessType = ProcessType::CopyAll THEN
                                        ChangeRentalCalendarDay(1)  //* 0=Rental Day
                                    END;
                                  END;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      RefreshOnActivate=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1100485000;2;Field  ;
                  CaptionML=[DEU=Datum ab;
                             ENU=Date From;
                             NLD=Datum van;
                             NOR=Dato fra;
                             SVE=Datum fr†n];
                  SourceExpr=DateFrom;
                  OnValidate=BEGIN
                               IF (DateTo <> 0D) AND (DateTo < DateFrom) THEN
                                 DateTo := DateFrom;
                             END;
                              }

      { 1100485002;2;Field  ;
                  CaptionML=[DEU=Datum bis eins.;
                             ENU=Date To;
                             NLD=Datum t/m;
                             NOR=Dato til;
                             SVE=Datum till];
                  SourceExpr=DateTo;
                  OnValidate=BEGIN
                               IF (DateTo <> 0D) AND (DateTo < DateFrom) THEN
                                 ERROR(Text006);
                             END;
                              }

      { 1210190005;2;Field  ;
                  CaptionML=[DEU=Verarbeitungstyp;
                             ENU=Process Type;
                             NLD=Soort bewerking;
                             NOR=Prosesstype;
                             SVE=Processtyp];
                  OptionCaptionML=[DEU=Žndern,Kopieren alles,Kopieren mietfrei;
                                   ENU=Modify,Copy All,Copy Rental Free;
                                   NLD=Wijzigen,Kopi‰ren alles,Kopi‰ren huurvrij;
                                   NOR=Endre,Kopier alle,Kopier Gratis utleie;
                                   SVE=Žndra,Kopiera alla,Kopiera uthyrning gratis];
                  SourceExpr=ProcessType;
                  OnValidate=BEGIN
                               SetEnabled();
                               RequestOptionsPage.UPDATE(FALSE);
                             END;
                              }

      { 1210190000;1;Group  ;
                  CaptionML=[DEU=Neue Werte;
                             ENU=New Values;
                             NLD=Nieuwe waarden;
                             NOR=Nye verdier;
                             SVE=Nya v„rden];
                  GroupType=Group }

      { 7   ;2   ;Field     ;
                  CaptionML=[DEU=Samstag;
                             ENU=Saturday;
                             NLD=Zaterdag;
                             NOR=L›rdag;
                             SVE=L”rdag];
                  OptionCaptionML=[DEU=Nicht „ndern, ,Mietfrei;
                                   ENU=Don't Change, ,Rental Free;
                                   NLD=Niet veranderen, ,Huurvrij;
                                   NOR=Ikke forandre, ,Gratis utleie;
                                   SVE=Faktureras ej, ,Hyresfritt];
                  SourceExpr=SaturdayNewValue }

      { 8   ;2   ;Field     ;
                  CaptionML=[DEU=Sonntag;
                             ENU=Sunday;
                             NLD=Zondag;
                             NOR=S›ndag;
                             SVE=S”ndag];
                  OptionCaptionML=[DEU=Nicht „ndern, ,Mietfrei;
                                   ENU=Don't Change, ,Rental Free;
                                   NLD=Niet veranderen, ,Huurvrij;
                                   NOR=Ikke forandre, ,Gratis utleie;
                                   SVE=Faktureras ej, ,Hyresfritt];
                  SourceExpr=SundayNewValue }

      { 9   ;2   ;Field     ;
                  CaptionML=[DEU=Andere Tage;
                             ENU=Other Days;
                             NLD=Overige dagen;
                             NOR="Andre dager ";
                             SVE=Andra dagar];
                  OptionCaptionML=[DEU=Nicht „ndern, ,Mietfrei;
                                   ENU=Don't Change, ,Rental Free;
                                   NLD=Niet veranderen, ,Huurvrij;
                                   NOR=Ikke forandre, ,Gratis utleie;
                                   SVE=Faktureras ej, ,Hyresfritt];
                  SourceExpr=OtherDaysNewValue }

      { 1210190001;1;Group  ;
                  CaptionML=[DEU=Kopieren von;
                             ENU=Copy from;
                             NLD=Kopi‰ren van;
                             NOR=Kopier fra;
                             SVE=Kopiera fr†n];
                  GroupType=Group }

      { 1210190002;2;Field  ;
                  CaptionML=[DEU=Art;
                             ENU=Type;
                             NLD=Soort;
                             NOR=Radtype;
                             SVE=Typ];
                  OptionCaptionML=[DEU=Standard,Gebiet,Werkzeug;
                                   ENU=Standard,Region,Plant;
                                   NLD=Standaard,Regio,Materieel;
                                   NOR=Standard,Region,Maskin;
                                   SVE=Standard,Region,Maskin];
                  SourceExpr=CalendarTypeFrom;
                  OnValidate=BEGIN
                               IF CalendarTypeFrom <> CalendarTypeFrom::Region THEN
                                 TerritoryCodeFrom := '';
                               IF CalendarTypeFrom <> CalendarTypeFrom::Plant THEN
                                 PlantCalendarFrom := '';

                               SetEnabled();
                               RequestOptionsPage.UPDATE(FALSE);
                             END;
                              }

      { 1210190003;2;Field  ;
                  CaptionML=[DEU=Gebietscode;
                             ENU=Territory Code;
                             NLD=Regio;
                             NOR=Distriktskode;
                             SVE=Distriktskod];
                  SourceExpr=TerritoryCodeFrom;
                  TableRelation=Territory;
                  OnValidate=BEGIN
                               IF TerritoryCodeFrom <> '' THEN
                                 ValidateRentalCalendarCopyFrom();
                             END;
                              }

      { 1210190004;2;Field  ;
                  CaptionML=[DEU=Werkzeugkalender;
                             ENU=Plant Calendar;
                             NLD=Materieelkalender;
                             NOR=Maskinkalender;
                             SVE=Maskinkalender];
                  SourceExpr=PlantCalendarFrom;
                  TableRelation="Plant Calendar";
                  OnValidate=BEGIN
                               IF PlantCalendarFrom <> '' THEN
                                 ValidateRentalCalendarCopyFrom();
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@11012010 : TextConst 'DEU=Es muss eine Datumauswahl (ab - bis einschl.) eingegeben werden.;ENU=Date selection (from - to) not filled out;NLD=Er moet een datumselectie (van - t/m) worden ingegeven.;NOR=Datoutvalg (fra-til) er ikke fylt ut;SVE=Datumval (fr†n-till) „r inte ifyllt';
      Text001@11012011 : TextConst 'DEU=Treffen Sie zuerst eine Auswahl.;ENU=Make a selection first.;NLD=Maak eerst een selectie in het Optie tabblad.;NOR=Gj›r f›rst et valg p† fanen alternativer;SVE=G”r f”rst ett val p† fliken alternativ';
      Text002@11012012 : TextConst 'DEU=Zur Sicherung der Datenbank wird eine maximale Serie von 1000 Tagen behalten.\;ENU=To Protect the Database a Range of 1000 Days will be maintained. \;NLD=Ter beveiling van de database wordt een maximum range van 1000 dagen aangehouden.\;NOR=For † beskytte databasen vil et intervall p† 1 000 dager opprettholdes. \;SVE=F”r att skydda databasen kommer ett intervall p† 1 000 dagar att uppr„tth†llas. \';
      Text003@11012013 : TextConst 'DEU=Verkleinern der Datumauswahl (ab - bis einschl.);ENU=Reduce the Date Selection (From - To);NLD=Verklein de datumselectie (van - t/m).;NOR=Reduser datoutvalget (fraÍtil);SVE=Minska datumvalet (fr†nÍtill)';
      Text004@11012014 : TextConst 'DEU=Es sind keine Kalenderbewegungen erstellt worden.;ENU=There are no Calendar Entries created.;NLD=Er zijn geen kalendermutaties gemaakt.;NOR=Ingen kalendertransaksjoner er opprettet.;SVE=Inga kalendertransaktioner har skapats.';
      Text005@11012015 : TextConst 'DEU=Es sind %1 Kalenderbewegungen erstellt worden.;ENU=There are %1 Calandar Entries created.;NLD=Er zijn %1 kalendermutaties gemaakt.;NOR=%1 kalendertransaksjoner er opprettet.;SVE=%1 kalendertransaktioner har skapats.';
      RentalCalendarDayRec@1100485007 : Record 11012569;
      DateFrom@1100485000 : Date;
      DateTo@1100485001 : Date;
      ProcessType@1100525000 : 'Modify,CopyAll,CopyRentalFree';
      CalendarTypeFrom@1100525001 : 'Standard,Region,Plant';
      SaturdayNewValue@11012005 : 'Don''t Change, ,Rental Free';
      SundayNewValue@11012006 : 'Don''t Change, ,Rental Free';
      OtherDaysNewValue@11012007 : 'Don''t Change, ,Rental Free';
      Counter@11012009 : Integer;
      Text006@1100485002 : TextConst 'DEU=''Datum bis einschl.'' darf nicht kleiner als ''Datum ab'' sein.;ENU=''Date To'' may not be less than ''Date From'';NLD=''Datum t/m'' mag niet kleiner zijn dan ''Datum van'';NOR=''Dato til'' kan ikke inntreffe tidligere enn ''Dato fra''.;SVE=''Datum till'' f†r inte infalla tidigare „n ''Datum fr†n''.';
      TerritoryCodeFrom@1100525002 : Code[10];
      PlantCalendarFrom@1100525003 : Code[10];
      PeriodNoFilterTxt@1100485003 : Text[20];
      SaturdayNewValueEnabled@1210190000 : Boolean INDATASET;
      SundayNewValueEnabled@1210190001 : Boolean INDATASET;
      RemainingDaysNewValueEnabled@1210190002 : Boolean INDATASET;
      CalendarTypeFromEnabled@1210190003 : Boolean INDATASET;
      TerritoryCodeFromEnabled@1210190004 : Boolean INDATASET;
      PlantCalendarFromEnabled@1210190005 : Boolean INDATASET;

    PROCEDURE SetEnabled@1100525000();
    BEGIN
      SaturdayNewValueEnabled := ((ProcessType = ProcessType::Modify));
      SundayNewValueEnabled := ((ProcessType = ProcessType::Modify));
      RemainingDaysNewValueEnabled := ((ProcessType = ProcessType::Modify));
      CalendarTypeFromEnabled := (ProcessType <> ProcessType::Modify);
      TerritoryCodeFromEnabled := ((ProcessType <> ProcessType::Modify) AND (CalendarTypeFrom = CalendarTypeFrom::Region));
      PlantCalendarFromEnabled := ((ProcessType <> ProcessType::Modify) AND (CalendarTypeFrom = CalendarTypeFrom::Plant));
    END;

    PROCEDURE ValidateRentalCalendarCopyFrom@1100525001();
    VAR
      RentalCalendar@1100525000 : Record 11012568;
    BEGIN
      IF ProcessType = ProcessType::Modify THEN
        EXIT;

      RentalCalendar.GET(CalendarTypeFrom, TerritoryCodeFrom, PlantCalendarFrom);
    END;

    PROCEDURE ChangeRentalCalendarDay@1(RentalFreeValue@11012001 : 'Don''t Change, ,Rental Free');
    BEGIN
      IF RentalCalendarDayRec.GET(
         "Rental Calendar".Type, "Rental Calendar"."Territory Code",
         "Rental Calendar"."Plant Calendar", Date."Period Start")
      THEN BEGIN
        IF RentalFreeValue = RentalFreeValue::" " THEN BEGIN
              RentalCalendarDayRec.DELETE;
              Counter := Counter + 1;
            END;
      END ELSE BEGIN
        IF RentalFreeValue = RentalFreeValue::"Rental Free" THEN BEGIN
          RentalCalendarDayRec.INIT;
          RentalCalendarDayRec.Type := "Rental Calendar".Type;
          RentalCalendarDayRec."Territory Code" := "Rental Calendar"."Territory Code";
          RentalCalendarDayRec."Plant Calendar" := "Rental Calendar"."Plant Calendar";
          RentalCalendarDayRec.Date := Date."Period Start";
          RentalCalendarDayRec.INSERT;
          Counter := Counter + 1;
        END;
      END;
    END;

    PROCEDURE ModifySaturday@1210190004() : Boolean;
    BEGIN
      EXIT(SaturdayNewValue <> SaturdayNewValue::"Don't Change");
    END;

    PROCEDURE ModifySunday@1210190005() : Boolean;
    BEGIN
      EXIT(SundayNewValue <> SundayNewValue::"Don't Change");
    END;

    PROCEDURE ModifyOtherDays@1210190006() : Boolean;
    BEGIN
      EXIT(OtherDaysNewValue <> OtherDaysNewValue::"Don't Change");
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

