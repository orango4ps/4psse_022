OBJECT Page 11071882 Purch. Appr. Req. Entries 4PS
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Einkaufsgenehmigungsanfrageposten 4PS;
               ENU=Purchase Approval Request Entries 4PS;
               NLD=Goedkeuringsverzoek posten 4PS];
    InsertAllowed=No;
    LinksAllowed=No;
    SourceTable=Table454;
    SourceTableView=SORTING(Table ID,Document Type,Document No.,Sequence No.,Record ID to Approve);
    PageType=List;
    OnOpenPage=VAR
                 DummyBoolean@1100529600 : Boolean;
               BEGIN
                 UpdateEditableVisibleApprovalEntry(DummyBoolean, ApprovalPageOption, FALSE);

                 SETRANGE(Status);
                 SETRANGE("Due Date");

                 FILTERGROUP(2);
                 CASE GETFILTER("Table ID") OF
                   FORMAT(DATABASE::"Purch. Inv. Header") :
                     SETRANGE("Table ID", DATABASE::"Purch. Inv. Header");
                   FORMAT(DATABASE::"Purch. Cr. Memo Hdr.") :
                     SETRANGE("Table ID", DATABASE::"Purch. Cr. Memo Hdr.");
                   FORMAT(DATABASE::"Purchase Header") :
                     SETRANGE("Table ID", DATABASE::"Purchase Header");
                 END;
                 FILTERGROUP(0);
               END;

    OnFindRecord=VAR
                   Found@1100525000 : Boolean;
                 BEGIN
                   Found := FindPosition(Which);
                   EXIT(Found);
                 END;

    OnNextRecord=VAR
                   lDocument@1100525001 : Record 454;
                   NextStep@1100525000 : Integer;
                 BEGIN
                   lDocument.COPY(Rec);
                   NextStep := NextPosition(lDocument, Steps);
                   IF NextStep <> 0 THEN
                      Rec.COPY(lDocument);
                   EXIT(NextStep);
                 END;

    OnAfterGetRecord=BEGIN
                       Overdue := Overdue::" ";
                       IF FormatField(Rec) THEN
                         Overdue := Overdue::Yes;

                       CASE "Table ID" OF
                         DATABASE::"Purchase Header" :
                           IF NOT PurchHeader.GET("Document Type", "Document No.") THEN
                             CLEAR(PurchHeader);
                         DATABASE::"Purch. Inv. Header" :
                           IF NOT PurchInvHeader.GET("Document No.") THEN
                             CLEAR(PurchInvHeader);
                         DATABASE::"Purch. Cr. Memo Hdr." :
                           IF NOT PurchCrMemoHdr.GET("Document No.") THEN
                             CLEAR(PurchCrMemoHdr);
                       END;

                       IF PurchHeader."Currency Factor" <> 0 THEN
                         CurrencyFactor := PurchHeader."Currency Factor"
                       ELSE
                         CurrencyFactor := 1;

                       Approver := ApprovalManagement4PSConstr.GetApproverDisplayName("Approver ID");
                     END;

    OnNewRecord=BEGIN
                  FILTERGROUP(2);
                  CASE GETFILTER("Table ID") OF
                    FORMAT(DATABASE::"Purch. Inv. Header") :
                      "Table ID" := DATABASE::"Purch. Inv. Header";
                    FORMAT(DATABASE::"Purch. Cr. Memo Hdr.") :
                      "Table ID" := DATABASE::"Purch. Cr. Memo Hdr.";
                    FORMAT(DATABASE::"Purchase Header") :
                      "Table ID" := DATABASE::"Purchase Header";
                  END;
                  FILTERGROUP(0);
                END;

    OnModifyRecord=BEGIN
                     EXIT(FALSE);
                   END;

    OnDeleteRecord=BEGIN
                     IF "Table ID" IN [DATABASE::"Purch. Inv. Header", DATABASE::"Purch. Cr. Memo Hdr."] THEN
                       ApprovalManagement4PSConstr.ManuallyDeleleteAppvlEntry4PS(Rec)
                     ELSE
                       ApprovalManagement4PSConstr.ManuallyDeleleteAppvlEntry(Rec);
                     EXIT(FALSE)
                   END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 33      ;1   ;ActionGroup;
                      CaptionML=[DEU=An&zeigen;
                                 ENU=&Show;
                                 NLD=&Tonen;
                                 NOR=Vi&s;
                                 SVE=V&isa] }
      { 1100525006;2 ;Action    ;
                      ShortCutKey=Shift+Ctrl+S;
                      CaptionML=[DEU=Gescannter Beleg;
                                 ENU=Scanned Document;
                                 NLD=Gescand document;
                                 NOR=Skannet Dokument;
                                 SVE=Skannade dokument];
                      Promoted=Yes;
                      Image=Document;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 ShowScannedDocument;
                               END;
                                }
      { 35      ;2   ;Action    ;
                      CaptionML=[DEU=Beleg;
                                 ENU=Document;
                                 NLD=Document;
                                 NOR=Dokument;
                                 SVE=Dokument];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Edit;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 ShowRecord;
                               END;
                                }
      { 36      ;2   ;Action    ;
                      CaptionML=[DEU=Bemerkungen;
                                 ENU=Comments;
                                 NLD=Opmerkingen;
                                 NOR=Kommentarer;
                                 SVE=Kommentar];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ViewComments;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 ShowCommentsEdit;
                               END;
                                }
      { 39      ;2   ;Action    ;
                      CaptionML=[DEU=&F„llige Posten;
                                 ENU=O&verdue Entries;
                                 NLD=Vervallen posten;
                                 NOR=&Forfalte poster;
                                 SVE=&F”rfallna transaktioner];
                      Image=OverdueEntries;
                      OnAction=BEGIN
                                 SETFILTER(Status,'%1|%2',Status::Created,Status::Open);
                                 SETFILTER("Due Date",'<%1',TODAY);
                               END;
                                }
      { 40      ;2   ;Action    ;
                      CaptionML=[DEU=Alle Posten;
                                 ENU=All Entries;
                                 NLD=Alle posten;
                                 NOR=Alle poster;
                                 SVE=Alla transaktioner];
                      Image=Entries;
                      OnAction=BEGIN
                                 SETRANGE(Status);
                                 SETRANGE("Due Date");
                               END;
                                }
      { 1100525002;2 ;Action    ;
                      CaptionML=[DEU=Filter;
                                 ENU=Filters;
                                 NLD=Filters;
                                 NOR=Filtre;
                                 SVE=Filter];
                      OnAction=BEGIN
                                 FilterSetup;
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100525007;1 ;Action    ;
                      Name=Delegate;
                      CaptionML=[DEU=Delegieren;
                                 ENU=Delegate;
                                 NLD=Delegeren;
                                 NOR=Deleger;
                                 SVE=Delegera];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Delegate;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ApprovalEntry@1100525000 : Record 454;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(ApprovalEntry);
                                 DelegateDetailedApprovalEntries(ApprovalEntry, TRUE, TempCompressedApprovalEntry, ApprovalPageOption);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 37  ;2   ;Field     ;
                CaptionML=[DEU=šberf„llig;
                           ENU=Overdue;
                           NLD=Vervallen;
                           NOR=Forfalt;
                           SVE=F”rfallet];
                ToolTipML=[DEU=F„lliger Posten;
                           ENU=Overdue Entry;
                           NLD=Vervallen post;
                           NOR=Forfalt post;
                           SVE=F”rfallen transaktion];
                OptionCaptionML=[DEU="Ja, ";
                                 ENU="Yes, ";
                                 NLD="Ja, ";
                                 SVE="Ja, "];
                SourceExpr=Overdue;
                Visible=FALSE;
                Editable=FALSE }

    { 4   ;2   ;Field     ;
                CaptionML=[DEU=Belegart;
                           ENU=Document Type;
                           NLD=Documentsoort;
                           NOR=Bilagstype;
                           SVE=Dokumenttyp];
                SourceExpr="Document Type";
                Editable=FALSE }

    { 6   ;2   ;Field     ;
                SourceExpr="Document No.";
                Editable=FALSE }

    { 1100528401;2;Field  ;
                SourceExpr="Document Line No." }

    { 161024013;2;Field   ;
                CaptionML=[DEU=Eink. von Kred.-Nr.;
                           ENU=Buy-from Vendor No.;
                           NLD=Orderleveranciersnr.;
                           NOR=Kj›p fra-leverand›rnr.;
                           SVE=Ink”psleverant”rsnr];
                SourceExpr=GetBuyFromVendor;
                Editable=FALSE }

    { 161024012;2;Field   ;
                CaptionML=[DEU=Eink. von Name;
                           ENU=Buy-from Vendor Name;
                           NLD=Leveranciersnaam;
                           NOR=Kj›p fra-leverand›rnavn;
                           SVE=Ink”psnamn];
                SourceExpr=GetBuyFromVendorName;
                Editable=FALSE }

    { 16  ;2   ;Field     ;
                CaptionML=[DEU=Genehmigung durch;
                           ENU=Approval by;
                           NLD=Goed te keuren door;
                           SVE=Godk„nnande av];
                SourceExpr=Approver;
                OnValidate=BEGIN
                             "Approver ID" := Approver;
                             ApprovalManagement4PSConstr.ManuallyEditAppvlEntry(Rec);
                             CurrPage.UPDATE(FALSE);
                           END;

                OnLookup=VAR
                           UserSetup@1160040000 : Record 91;
                         BEGIN
                           IF UserSetup.GET("Approver ID") THEN ;
                           IF PAGE.RUNMODAL(0, UserSetup) = ACTION::LookupOK THEN BEGIN
                             "Approver ID" := UserSetup."User ID";
                             ApprovalManagement4PSConstr.ManuallyEditAppvlEntry(Rec);
                             CurrPage.UPDATE(FALSE);
                           END;
                         END;
                          }

    { 18  ;2   ;Field     ;
                CaptionML=[DEU=Status;
                           ENU=Status;
                           NLD=Status;
                           NOR=Status;
                           SVE=Status];
                SourceExpr=Status;
                Editable=FALSE }

    { 1160040010;2;Field  ;
                CaptionML=[DEU=Abwarten;
                           ENU=On Hold;
                           NLD=Afwachten;
                           NOR=Avvent;
                           SVE=Stoppad];
                SourceExpr=GetOnHold;
                Editable=FALSE }

    { 1160040003;2;Field  ;
                CaptionML=[DEU=W„hrungscode;
                           ENU=Currency Code;
                           NLD=Valutacode;
                           NOR=Valutakode;
                           SVE=Valutakod];
                SourceExpr="Currency Code";
                Editable=FALSE }

    { 1160040008;2;Field  ;
                CaptionML=[DEU=Betrag;
                           ENU=Amount;
                           NLD=Bedrag;
                           NOR=Bel›p;
                           SVE=Belopp];
                SourceExpr=Amount;
                Editable=FALSE }

    { 1160040007;2;Field  ;
                CaptionML=[DEU=MwSt.-Betrag;
                           ENU=VAT Amount;
                           NLD=BTW-bedrag;
                           NOR=Mva-bel›p;
                           SVE=Momsbelopp];
                SourceExpr="Amount Incl. VAT" - Amount;
                Visible=FALSE;
                Editable=FALSE }

    { 1160040006;2;Field  ;
                CaptionML=[DEU=Betrag inkl. MwSt.;
                           ENU=Amount Incl. VAT;
                           NLD=Bedrag incl. BTW;
                           NOR=Bel›p inkl. mva.;
                           SVE=Belopp inkl moms];
                SourceExpr="Amount Incl. VAT";
                Editable=FALSE }

    { 1160040005;2;Field  ;
                CaptionML=[DEU=Betrag (MW);
                           ENU=Amount (LCY);
                           NLD=Bedrag (LV);
                           NOR=Bel›p (NOK);
                           SVE=Belopp (BVA)];
                SourceExpr="Amount (LCY)";
                Visible=FALSE;
                Editable=FALSE }

    { 1160040004;2;Field  ;
                CaptionML=[DEU=MwSt.-Betrag (MW);
                           ENU=VAT Amount (LCY);
                           NLD=BTW-bedrag (LV);
                           NOR=Mva-bel›p (NOK);
                           SVE=Momsbelopp (BVA)];
                SourceExpr="Amount Incl. VAT (LCY)" - "Amount (LCY)";
                Visible=FALSE;
                Editable=FALSE }

    { 1160040002;2;Field  ;
                CaptionML=[DEU=Betrag einschl. MwSt. (LCY);
                           ENU=Amount Incl. VAT (LCY);
                           NLD=Bedrag incl. BTW (LV);
                           SVE=Belopp inklusive moms (BVA)];
                SourceExpr="Amount Incl. VAT (LCY)";
                Visible=FALSE;
                Editable=FALSE }

    { 14  ;2   ;Field     ;
                CaptionML=[DEU=Verk.-/Eink„ufercode;
                           ENU=Salespers./Purch. Code;
                           NLD=Verkoper/Inkoper;
                           NOR=Selger/innkj›per - kode;
                           SVE=S„ljare/ink”pare kod];
                SourceExpr="Salespers./Purch. Code";
                Visible=FALSE;
                Editable=FALSE }

    { 1160040001;2;Field  ;
                CaptionML=[DEU=ID des ursprnglichen Genehmigers;
                           ENU=Original Approver ID;
                           NLD=Oorspronkelijke fiatteur-ID];
                SourceExpr=ApprovalManagement4PSConstr.GetApproverDisplayName("Original Approver ID");
                Visible=FALSE;
                Editable=FALSE }

    { 20  ;2   ;Field     ;
                CaptionML=[DEU=Datum-Uhrzeit der Einsendung zur Genehmigung;
                           ENU=Date-Time Sent for Approval;
                           NLD=Verzonden voor goedkeuring (datum/tijd);
                           NOR=Dato/klokkeslett sendt til godkjenning;
                           SVE=Skickades f”r godk„nnande (datum/tid)];
                SourceExpr="Date-Time Sent for Approval";
                Visible=FALSE;
                Editable=FALSE }

    { 29  ;2   ;Field     ;
                CaptionML=[DEU=Bemerkung;
                           ENU=Comment;
                           NLD=Opmerking;
                           NOR=Merknad;
                           SVE=Kommentar];
                SourceExpr=Comment;
                Editable=FALSE }

    { 31  ;2   ;Field     ;
                CaptionML=[DEU=F„lligkeitsdatum;
                           ENU=Due Date;
                           NLD=Vervaldatum;
                           NOR=Forfallsdato;
                           SVE=F”rfallodatum];
                SourceExpr="Due Date";
                Editable=FALSE }

    { 11012000;2;Field    ;
                CaptionML=[DEU=Verfgbares Kreditlimit (MW);
                           ENU=Available Credit Limit (LCY);
                           NLD=Beschikbare kredietlimiet (LV);
                           NOR=Tilgjengelig kredittgrense (NOK);
                           SVE=Tillg„nglig kreditlimit (BVA)];
                SourceExpr="Available Credit Limit (LCY)";
                Editable=FALSE }

    { 1100525004;2;Field  ;
                SourceExpr="Last Modified By User ID" }

    { 1100525005;2;Field  ;
                SourceExpr="Last Date-Time Modified" }

    { 1100528400;2;Field  ;
                CaptionML=[DEU=ID des zu genehmigenden Datensatzes;
                           ENU=Record ID to Approve;
                           NLD=Goed te keuren record-id;
                           NOR=Post-ID som skal godkjennes;
                           SVE=Post-ID att godk„nna];
                SourceExpr=FORMAT("Record ID to Approve") }

    { 1100525008;2;Field  ;
                SourceExpr="Vendor No." }

  }
  CODE
  {
    VAR
      PurchHeader@161024012 : Record 38;
      ApprovalManagement4PSConstr@1100525003 : Codeunit 11125349;
      Overdue@1005 : 'Yes, ';
      Approver@1160040000 : Text[250];
      CurrencyFactor@1100528204 : Decimal;
      PurchInvHeader@1100528203 : Record 122;
      PurchCrMemoHdr@1100528202 : Record 124;
      VendorNameFilter@1100525000 : Text[250];
      VendorNoFilter@1100525001 : Text[250];
      TempCompressedApprovalEntry@1100525004 : TEMPORARY Record 454;
      ApprovalPageOption@1100525002 : 'All,Hours,All Except Hours';

    PROCEDURE FormatField@1(Rec@1000 : Record 454) : Boolean;
    BEGIN
      IF (Status IN [Status::Created, Status::Open]) THEN
        IF (Rec."Due Date" < TODAY) THEN
          EXIT(TRUE)
        ELSE
          EXIT(FALSE);
    END;

    PROCEDURE GetBuyFromVendor@1100528203() : Text[30];
    BEGIN
      CASE "Table ID" OF
        DATABASE::"Purchase Header" :
          EXIT(PurchHeader."Buy-from Vendor No.");
        DATABASE::"Purch. Inv. Header" :
          EXIT(PurchInvHeader."Buy-from Vendor No.");
        DATABASE::"Purch. Cr. Memo Hdr." :
          EXIT(PurchCrMemoHdr."Buy-from Vendor No.");
      END;
    END;

    PROCEDURE GetBuyFromVendorName@1100528202() : Text[80];
    BEGIN
      CASE "Table ID" OF
        DATABASE::"Purchase Header" :
          EXIT(PurchHeader."Buy-from Vendor Name");
        DATABASE::"Purch. Inv. Header" :
          EXIT(PurchInvHeader."Buy-from Vendor Name");
        DATABASE::"Purch. Cr. Memo Hdr." :
          EXIT(PurchCrMemoHdr."Buy-from Vendor Name");
      END;
    END;

    PROCEDURE GetOnHold@1100528200() : Text[30];
    BEGIN
      CASE "Table ID" OF
        DATABASE::"Purchase Header" :
          EXIT(PurchHeader."On Hold");
        DATABASE::"Purch. Inv. Header" :
          EXIT(PurchInvHeader."On Hold");
        DATABASE::"Purch. Cr. Memo Hdr." :
          EXIT(PurchCrMemoHdr."On Hold");
      END;
    END;

    PROCEDURE FilterSetup@1100409000();
    VAR
      SetFilterDCDocument@1100525002 : Page 11126591;
      lVendorNameFilter@1100525001 : Text[250];
      lVendorNoFilter@1100525000 : Text[250];
    BEGIN
      CLEAR(SetFilterDCDocument);
      SetFilterDCDocument.FillDefaults(VendorNoFilter, VendorNameFilter);
      SetFilterDCDocument.RUNMODAL;

      lVendorNoFilter := VendorNoFilter;
      lVendorNameFilter := VendorNameFilter;

      IF SetFilterDCDocument.GetDefaults(lVendorNoFilter, lVendorNameFilter) THEN BEGIN
        VendorNoFilter := lVendorNoFilter;
        VendorNameFilter := lVendorNameFilter;
        CurrPage.UPDATE(FALSE);
      END;
      CLEAR(SetFilterDCDocument);
    END;

    PROCEDURE FindPosition@1100409003(Which@1100409001 : Text) : Boolean;
    VAR
      lDocument@1100409000 : Record 454;
      i@1100409002 : Integer;
    BEGIN
      lDocument.COPY(Rec);
      IF Which = '' THEN
        Which := '=';

      FOR i := 1 TO STRLEN(Which) DO
        CASE Which[i] OF
          '-' :
            IF Rec.FIND('-') THEN
              REPEAT
                IF ShowThisRecord(Rec) THEN
                  EXIT(TRUE);
              UNTIL Rec.NEXT = 0;
          '+' :
            IF Rec.FIND('+') THEN
              REPEAT
                IF ShowThisRecord(Rec) THEN
                  EXIT(TRUE);
              UNTIL Rec.NEXT(-1) = 0;
          '=' :
            IF Rec.FIND THEN
              IF ShowThisRecord(Rec) THEN
                EXIT(TRUE);
          '>' :
            IF NextPosition(Rec, 1) <> 0 THEN
              EXIT(TRUE);
          '<' :
            IF NextPosition(Rec, -1) <> 0 THEN
              EXIT(TRUE);
        END;

      Rec.COPY(lDocument);
      EXIT(FALSE);
    END;

    PROCEDURE NextPosition@1100409004(VAR lDocument@1100409000 : Record 454;Steps@1100409001 : Integer) StepsCount : Integer;
    VAR
      RecRef2@1100409003 : Record 454;
      RecRef3@1100409004 : Record 454;
      Direction@1100409002 : Integer;
    BEGIN
      IF Steps = 0 THEN
        EXIT;

      IF Steps > 0 THEN
        Direction := 1
      ELSE
        Direction := -1;

      RecRef2.COPY(lDocument);
      RecRef3.COPY(lDocument);
      REPEAT
        IF lDocument.NEXT(Direction) = 0 THEN BEGIN
          lDocument.COPY(RecRef3);
          EXIT;
        END;
        IF ShowThisRecord(lDocument) THEN BEGIN
          RecRef3.COPY(lDocument);
          StepsCount += Direction;
        END;
      UNTIL ABS(StepsCount) >= ABS(Steps);

      IF StepsCount = 0 THEN
        lDocument.COPY(RecRef2)
      ELSE
        lDocument.COPY(RecRef3);
    END;

    PROCEDURE ShowThisRecord@1100525002(lApprovalEntry@1100528200 : Record 454) : Boolean;
    BEGIN
      IF (VendorNoFilter <> '') THEN
        IF NOT ApprovalManagement4PSConstr.GetBuyFromVendorTest(lApprovalEntry, VendorNoFilter) THEN
          EXIT(FALSE);

      IF (VendorNameFilter <> '') THEN
        IF NOT ApprovalManagement4PSConstr.GetBuyFromVendorNameTest(lApprovalEntry, VendorNameFilter) THEN
          EXIT(FALSE);

      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

