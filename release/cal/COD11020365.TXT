OBJECT Codeunit 11020365 SAF-T Export Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVNO14.04 (10675);
  }
  PROPERTIES
  {
    TableNo=11128518;
    OnRun=BEGIN
            StartExport(Rec);
          END;

  }
  CODE
  {
    VAR
      ExportIsInProgressMsg@1100285100 : TextConst 'ENU=The export is in progress. Starting a new job cancels the current progress.\;NOR=Eksporten p†g†r. Hvis du starter et nytt prosjekt, annulleres gjeldende fremgang.\';
      LinesInProgressOrCompletedMsg@1101285000 : TextConst 'ENU=One or more export lines are in progress or completed.\;NOR=n eller flere eksportlinjer p†g†r eller er fullf›rt.\';
      CancelExportIsInProgressQst@1100285101 : TextConst 'ENU=Do you want to cancel all export jobs and restart?;NOR=Vil du avbryte alle eksportprosjekter og starte p† nytt?';
      DeleteExportIsInProgressQst@1100285102 : TextConst 'ENU=Do you want to delete the export entry?;NOR=Vil du slette eksportposten?';
      RestartExportLineQst@1100285103 : TextConst 'ENU=Do you want to restart the export for this line?;NOR=Vil du starte eksporten for denne linjen p† nytt?';
      ExportIsCompletedQst@1100285104 : TextConst 'ENU=The export was completed. You can download the export result choosing the Download SAF-T File action.\;NOR=Eksporten er fullf›rt. Du kan laste ned eksportresultatet ved † velge handlingen Last ned SAF-T-fil.\';
      RestartExportQst@1100285105 : TextConst 'ENU=Do you want to restart the export to get a new SAF-T file?;NOR=Vil du starte eksporten p† nytt for † f† en ny SAF-T-fil?';
      SetStartDateTimeAsCurrentQst@1100285106 : TextConst 'ENU=The Earliest Start Date/Time field is not filled in. Do you want to proceed and start the export immediately?;NOR=Feltet Tidligste startdato/-klokkeslett er ikke fylt ut. Vil du fortsette og starte eksporten umiddelbart?';
      SAFTExportTxt@1100285107 : TextConst 'ENU=SAF-T Export;NOR=SAF-T-eksport';
      StartingExportTxt@1100285108 : TextConst '@@@="%1 - integer; %2,%3 - boolean";ENU=Starting SAF-T export with ID: %1, Parallel: %2, Split By Month: %3;NOR=Starter SAF-T-eksport med ID: %1, parallell: %2, delt p† m†ned: %3';
      CancellingExportTxt@1100285109 : TextConst '@@@="%1 - integer; %2 - GUID";ENU=Cancelling SAF-T Export with ID: %1, Task ID: %2;NOR=Avbryter SAF-T-eksport med ID: %1, oppgave-ID: %2';
      NotPossibleToScheduleMsg@1100285110 : TextConst 'ENU=You are not allowed to schedule the SAF-T file generation;NOR=Det er ikke tillatt † planlegge SAF-T-filgenerering';
      GenerateSAFTFileImmediatelyQst@1100285111 : TextConst 'ENU=Since you did not schedule the SAF-T file generation, it will be generated immediately which can take a while. Do you want to continue?;NOR=Siden du ikke planla † bruke SAF-T-filgenerering, blir den generert umiddelbart, noe som kan ta litt tid. Vil du fortsette?';
      NoErrorMessageErr@1100285112 : TextConst 'ENU=The generation of a SAF-T file failed but no error message was logged.;NOR=Genereringen av en SAF-T-fil mislyktes, men ingen feilmelding ble loggf›rt.';
      FilesExistsInFolderErr@1100285113 : TextConst 'ENU=One or more files exist in the folder that you want to export the SAF-T file to. Specify a folder with no files in it.;NOR=Det finnes ‚n eller flere filer i mappen som du vil eksportere SAF-T-filen til. Angi en tom mappe.';
      SAFTFileGeneratedTxt@1100285114 : TextConst 'ENU=SAF-T file generated.;NOR=SAF-T-fil generert.';
      SAFTFileNotGeneratedTxt@1100285115 : TextConst 'ENU=SAF-T file not generated.;NOR=SAF-T-fil er ikke generert.';
      ParallelSAFTFileGenerationTxt@1100285116 : TextConst 'ENU=Parallel SAF-T file generation;NOR=Parallell SAF-T-filgenerering';
      ZipArchiveFilterTxt@1100285117 : TextConst '@@@={Locked};ENU=Zip File (*.zip)|*.zip';
      SAFTZipFileTxt@1100285118 : TextConst '@@@={Locked};ENU=SAF-T Financial.zip';
      ZipArchiveSaveDialogTxt@1100285119 : TextConst 'ENU=Export SAF-T archive;NOR=Eksporter SAF-T-arkiv';
      MasterDataMsg@1100285120 : TextConst 'ENU=MasterData;NOR=MasterData';
      GLEntriesMsg@1100285121 : TextConst 'ENU=General ledger entries from %1 to %2;NOR=Finansposter fra %1 til %2';
      NoZipFileGeneratedErr@1100285122 : TextConst 'ENU=No zip file generated.;NOR=Ingen zip-fil er generert.';
      NoOfJobsInProgressTxt@1100285123 : TextConst '@@@="%1 = number";ENU=No of jobs in progress: %1;NOR=Antall prosjekter i arbeid: %1';
      JobsStartedOrFailedTxt@1100285124 : TextConst '@@@="%1 = number";ENU=There are %1 jobs not started or failed;NOR=%1s prosjekter er ikke startet eller mislyktes';
      SessionLostTxt@1100285125 : TextConst '@@@="%1 = number";ENU=The task for line %1 was lost.;NOR=Oppgaven for linje %1 ble brutt.';
      NotPossibleToScheduleTxt@1100285126 : TextConst '@@@="%1,%2 = numbers";ENU=It is not possible to schedule the task for line %1 because the Max. No. of Jobs field contains %2.;NOR=Det er ikke mulig † planlegge oppgaven for linje %1 fordi feltet Maks. antall prosjekter inneholder %2.';
      ScheduleTaskForLineTxt@1100285127 : TextConst '@@@="%1 = number";ENU=Schedule a task for line %1.;NOR=Planlegg en oppgave for linje %1.';

    LOCAL PROCEDURE StartExport@1100285101(SAFTExportHeader@1100285100 : Record 11128518);
    BEGIN
      IF NOT PrepareForExport(SAFTExportHeader) THEN
        EXIT;

      SendTraceTagOfExport(SAFTExportTxt, GetStartTraceTagMessage(SAFTExportHeader));
      CreateExportLines(SAFTExportHeader);

      SAFTExportHeader.VALIDATE(Status, SAFTExportHeader.Status::"In Progress");
      SAFTExportHeader.VALIDATE("Execution Start Date/Time", CURRENTDATETIME);
      SAFTExportHeader.VALIDATE("Execution End Date/Time", 0DT);
      CLEAR(SAFTExportHeader."SAF-T File");
      SAFTExportHeader.MODIFY(TRUE);
      COMMIT;

      StartExportLines(SAFTExportHeader);
      SAFTExportHeader.FIND;
    END;

    PROCEDURE DeleteExport@1100285102(VAR SAFTExportHeader@1100285100 : Record 11128518);
    VAR
      SAFTExportLine@1100285101 : Record 11128519;
    BEGIN
      IF NOT CheckStatus(SAFTExportHeader.Status, DeleteExportIsInProgressQst) THEN
        EXIT;
      SAFTExportLine.SETRANGE(ID, SAFTExportHeader.ID);
      SAFTExportLine.SETRANGE(Status, SAFTExportLine.Status::"In Progress");
      IF SAFTExportLine.FINDSET THEN
        REPEAT
          CancelTask(SAFTExportLine);
        UNTIL SAFTExportLine.NEXT = 0;
      SAFTExportLine.SETRANGE(Status);
      SAFTExportLine.DELETEALL(TRUE);
    END;

    PROCEDURE ThrowNoParallelExecutionNotification@1100285103();
    VAR
      ParallelExecutionNotification@1100285100 : Notification;
    BEGIN
      ParallelExecutionNotification.MESSAGE := NotPossibleToScheduleMsg;
      ParallelExecutionNotification.SCOPE := NOTIFICATIONSCOPE::LocalScope;
      ParallelExecutionNotification.SEND;
    END;

    PROCEDURE RestartTaskOnExportLine@1100285104(VAR SAFTExportLine@1100285100 : Record 11128519);
    VAR
      SAFTExportHeader@1100285101 : Record 11128518;
      DummyNoOfJobs@1100285102 : Integer;
      NotBefore@1100285103 : DateTime;
    BEGIN
      IF NOT CheckLineStatusForRestart(SAFTExportLine) THEN
        EXIT;
      IF NOT SAFTExportLine.FINDSET THEN
          EXIT;
      REPEAT
          SAFTExportLine.SETRANGE(ID, SAFTExportLine.ID);
          REPEAT
              CancelTask(SAFTExportLine);
              SAFTExportHeader.GET(SAFTExportLine.ID);
              NotBefore := CURRENTDATETIME;
              RunGenerateSAFTFileOnSingleLine(SAFTExportLine, DummyNoOfJobs, NotBefore, SAFTExportHeader);
          UNTIL SAFTExportLine.NEXT = 0;
          SAFTExportHeader.FIND;
          UpdateExportStatus(SAFTExportHeader);
          SAFTExportLine.SETRANGE(ID);
      UNTIL SAFTExportLine.NEXT = 0;
    END;

    PROCEDURE SendTraceTagOfExport@1100285105(Category@1100285100 : Text;TraceTagMessage@1100285101 : Text);
    BEGIN
      // SendTraceTag('0000A4J', Category, Verbosity::Normal, TraceTagMessage, DataClassification::SystemMetadata);
    END;

    PROCEDURE UpdateExportStatus@1100285106(VAR SAFTExportHeader@1100285100 : Record 11128518);
    VAR
      SAFTExportLine@1100285101 : Record 11128519;
      TotalCount@1100285102 : Integer;
      Status@1100285103 : Integer;
    BEGIN
      IF SAFTExportHeader.ID = 0 THEN
        EXIT;

      SAFTExportLine.SETRANGE(ID, SAFTExportHeader.ID);
      TotalCount := SAFTExportLine.COUNT;
      SAFTExportLine.SETRANGE(Status, SAFTExportLine.Status::Completed);
      IF SAFTExportLine.COUNT = TotalCount THEN BEGIN
        SAFTExportHeader.VALIDATE(Status, SAFTExportHeader.Status::Completed);
        SAFTExportHeader.VALIDATE("Execution End Date/Time", CURRENTDATETIME);
        SAFTExportHeader.MODIFY(TRUE);
        EXIT;
      END;

      SAFTExportLine.SETRANGE(Status, SAFTExportLine.Status::Failed);
      IF SAFTExportLine.ISEMPTY THEN
        Status := SAFTExportHeader.Status::"In Progress"
      ELSE
        Status := SAFTExportHeader.Status::Failed;

      SAFTExportHeader.VALIDATE(Status, Status);
      SAFTExportHeader.MODIFY(TRUE);
    END;

    PROCEDURE StartExportLinesNotStartedYet@1100285107(SAFTExportHeader@1100285100 : Record 11128518);
    VAR
      SAFTExportLine@1100285101 : Record 11128519;
      NoOfJobs@1100285102 : Integer;
      NotBefore@1100285103 : DateTime;
      RunThisLine@1100285104 : Boolean;
    BEGIN
      IF NOT SAFTExportHeader."Parallel Processing" THEN
        EXIT;

      NoOfJobs := GetNoOfJobsInProgress;
      LogState(SAFTExportLine, STRSUBSTNO(NoOfJobsInProgressTxt, NoOfJobs), FALSE);
      IF NoOfJobs > SAFTExportHeader."Max No. Of Jobs" THEN
        EXIT;

      SAFTExportLine.LOCKTABLE;
      SAFTExportLine.SETRANGE(ID, SAFTExportHeader.ID);
      SAFTExportLine.SETFILTER("No. Of Retries", '<>%1', 0);
      SAFTExportLine.SETFILTER(Status, '<>%1', SAFTExportLine.Status::Completed);
      LogState(SAFTExportLine, STRSUBSTNO(JobsStartedOrFailedTxt, SAFTExportLine.COUNT), FALSE);
      IF NOT SAFTExportLine.FINDSET THEN
        EXIT;

      NotBefore := CURRENTDATETIME;
      REPEAT
        RunThisLine := FALSE;
        IF SAFTExportLine.Status = SAFTExportLine.Status::"In Progress" THEN BEGIN
          RunThisLine := NOT localIsSessionActive(SAFTExportLine);
          IF RunThisLine THEN
            LogState(SAFTExportLine, STRSUBSTNO(SessionLostTxt, SAFTExportLine."Line No."), TRUE);
        END ELSE
          RunThisLine := TRUE;
        IF RunThisLine THEN
          RunGenerateSAFTFileOnSingleLine(SAFTExportLine, NoOfJobs, NotBefore, SAFTExportHeader);
      UNTIL SAFTExportLine.NEXT = 0;
    END;

    PROCEDURE ShowActivityLog@1100285108(SAFTExportLine@1100285100 : Record 11128519);
    VAR
      ActivityLog@1100285102 : Record 710;
      ActivityLogPage@1100285101 : Page 710;
    BEGIN
      ActivityLog.SETRANGE("Record ID", SAFTExportLine.RECORDID);
      ActivityLogPage.SETTABLEVIEW(ActivityLog);
      ActivityLogPage.RUN;
    END;

    PROCEDURE ShowErrorOnExportLine@1100285109(SAFTExportLine@1100285100 : Record 11128519);
    VAR
      ActivityLog@1100285101 : Record 710;
      Stream@1100285102 : InStream;
      ErrorMessage@1100285103 : Text;
    BEGIN
      WITH ActivityLog DO BEGIN
        SETRANGE("Record ID", SAFTExportLine.RECORDID);
        IF NOT FINDLAST OR (Status <> Status::Failed) THEN
          EXIT;
        CALCFIELDS("Detailed Info");
        IF NOT "Detailed Info".HASVALUE THEN
          ERROR(NoErrorMessageErr);
        "Detailed Info".CREATEINSTREAM(Stream);
        Stream.READTEXT(ErrorMessage);
        IF ErrorMessage = '' THEN
          ERROR(NoErrorMessageErr);
        MESSAGE(ErrorMessage);
      END;
    END;

    PROCEDURE LogSuccess@1100285110(SAFTExportLine@1100285100 : Record 11128519);
    VAR
      ActivityLog@1100285101 : Record 710;
    BEGIN
      ActivityLog.LogActivity(SAFTExportLine.RECORDID, ActivityLog.Status::Success, '', SAFTFileGeneratedTxt, '');
      SendTraceTagOfExport(SAFTExportTxt, SAFTFileGeneratedTxt);
    END;

    PROCEDURE LogError@1100285111(SAFTExportLine@1100285100 : Record 11128519);
    VAR
      ActivityLog@1100285101 : Record 710;
      ErrorMessage@1100285102 : Text;
    BEGIN
      ErrorMessage := GETLASTERRORTEXT;
      ActivityLog.LogActivity(SAFTExportLine.RECORDID, ActivityLog.Status::Failed, '', SAFTFileNotGeneratedTxt, ErrorMessage);
      ActivityLog.SetDetailedInfoFromText(ErrorMessage);
    END;

    LOCAL PROCEDURE LogState@1100285112(SAFTExportLine@1100285100 : Record 11128519;Description@1100285101 : Text[250];SetTraceTag@1100285102 : Boolean);
    VAR
      ActivityLog@1100285103 : Record 710;
    BEGIN
      ActivityLog.LogActivity(SAFTExportLine.RECORDID, ActivityLog.Status::Success, '', ParallelSAFTFileGenerationTxt, Description);
      IF SetTraceTag THEN
        SendTraceTagOfExport(ParallelSAFTFileGenerationTxt, Description);
    END;

    LOCAL PROCEDURE StartExportLines@1100285114(SAFTExportHeader@1100285100 : Record 11128518);
    VAR
      SAFTExportLine@1100285101 : Record 11128519;
      NoOfJobs@1100285102 : Integer;
      NotBefore@1100285103 : DateTime;
    BEGIN
      SAFTExportLine.LOCKTABLE;
      SAFTExportLine.SETRANGE(ID, SAFTExportHeader.ID);
      SAFTExportLine.FINDSET;
      NoOfJobs := 1;
      NotBefore := SAFTExportHeader."Earliest Start Date/Time";
      REPEAT
        RunGenerateSAFTFileOnSingleLine(SAFTExportLine, NoOfJobs, NotBefore, SAFTExportHeader);
      UNTIL SAFTExportLine.NEXT = 0;
    END;

    LOCAL PROCEDURE RunGenerateSAFTFileOnSingleLine@1100285115(VAR SAFTExportLine@1100285100 : Record 11128519;VAR NoOfJobs@1100285101 : Integer;VAR NotBefore@1100285102 : DateTime;SAFTExportHeader@1100285103 : Record 11128518);
    VAR
      DoNotScheduleTask@1100285104 : Boolean;
      TaskID@1100285105 : GUID;
    BEGIN
      IF SAFTExportHeader."Parallel Processing" AND (NoOfJobs > SAFTExportHeader."Max No. Of Jobs") THEN BEGIN
        LogState(
          SAFTExportLine, STRSUBSTNO(NotPossibleToScheduleTxt, SAFTExportLine."Line No.", NoOfJobs), FALSE);
        EXIT;
      END;

      SAFTExportLine.VALIDATE(Status, SAFTExportLine.Status::"In Progress");
      CLEAR(SAFTExportLine."SAF-T File");
      SAFTExportLine.VALIDATE(Progress, 0);
      IF SAFTExportHeader."Parallel Processing" THEN BEGIN
        LogState(SAFTExportLine, STRSUBSTNO(ScheduleTaskForLineTxt, SAFTExportLine."Line No."), TRUE);
        NotBefore += 3000; // have a delay between running jobs to avoid deadlocks
        OnBeforeScheduleTask(DoNotScheduleTask, TaskID);
        IF DoNotScheduleTask THEN
          SAFTExportLine."Task ID" := TaskID
        ELSE
          SAFTExportLine."Task ID" :=
              TASKSCHEDULER.CREATETASK(
                  CODEUNIT::"Generate SAF-T File", CODEUNIT::"SAF-T Export Error Handler", TRUE, COMPANYNAME,
                  NotBefore, SAFTExportLine.RECORDID);
        SAFTExportLine.MODIFY(TRUE);
        COMMIT;
        NoOfJobs += 1;
        EXIT;
      END;
      SAFTExportLine."Task ID" := CREATEGUID;
      SAFTExportLine.MODIFY(TRUE);
      COMMIT;

      CLEARLASTERROR;
      IF NOT CODEUNIT.RUN(CODEUNIT::"Generate SAF-T File", SAFTExportLine) THEN
        CODEUNIT.RUN(CODEUNIT::"SAF-T Export Error Handler", SAFTExportLine);
      COMMIT;
    END;

    LOCAL PROCEDURE PrepareForExport@1100285116(VAR SAFTExportHeader@1100285100 : Record 11128518) : Boolean;
    VAR
      SAFTExportCheck@1100285101 : Codeunit 11020367;
      ErrorMessageHandler@1100285102 : Codeunit 29;
      ErrorMessageManagement@1100285103 : Codeunit 28;
    BEGIN
      ErrorMessageManagement.Activate(ErrorMessageHandler);
      SAFTExportCheck.RUN(SAFTExportHeader);
      IF ErrorMessageManagement.GetLastErrorID <> 0 THEN BEGIN
        ErrorMessageHandler.ShowErrors;
        EXIT(FALSE);
      END;

      IF SAFTExportHeader.Status = SAFTExportHeader.Status::"In Progress" THEN
        IF HandleConfirm(STRSUBSTNO('%1%2', ExportIsInProgressMsg, CancelExportIsInProgressQst)) THEN
          RemoveExportLines(SAFTExportHeader)
        ELSE
          EXIT(FALSE);
      IF SAFTExportHeader.Status = SAFTExportHeader.Status::Completed THEN
        IF NOT HandleConfirm(STRSUBSTNO('%1%2', ExportIsCompletedQst, RestartExportQst)) THEN
          EXIT(FALSE);

      IF (SAFTExportHeader."Parallel Processing") AND (SAFTExportHeader."Earliest Start Date/Time" = 0DT) THEN BEGIN
        IF NOT HandleConfirm(SetStartDateTimeAsCurrentQst) THEN
          EXIT(FALSE);
        SAFTExportHeader."Earliest Start Date/Time" := CURRENTDATETIME;
      END;
      IF NOT SAFTExportHeader."Parallel Processing" THEN
        IF NOT HandleConfirm(GenerateSAFTFileImmediatelyQst) THEN
          EXIT(FALSE);
      EXIT(TRUE)
    END;

    LOCAL PROCEDURE CreateExportLines@1100285117(SAFTExportHeader@1100285100 : Record 11128518);
    VAR
      SAFTExportLine@1100285101 : Record 11128519;
      GLEntry@1100285102 : Record 17;
      StartingDate@1100285103 : Date;
      EndingDate@1100285104 : Date;
      StopExportEntriesByPeriod@1100285105 : Boolean;
      LineNo@1100285106 : Integer;
    BEGIN
        SAFTExportLine.SETRANGE(ID, SAFTExportHeader.ID);
        SAFTExportLine.DELETEALL(TRUE);

        // Master data
        InsertSAFTExportLine(SAFTExportLine, LineNo, SAFTExportHeader, TRUE, MasterDataMsg, SAFTExportHeader."Starting Date", SAFTExportHeader."Ending Date");
        IF NOT SAFTExportHeader."Split by Month" THEN BEGIN
            // General ledger entries
            InsertSAFTExportLine(
                SAFTExportLine, LineNo, SAFTExportHeader, FALSE,
                STRSUBSTNO(GLEntriesMsg, SAFTExportHeader."Starting Date", SAFTExportHeader."Ending Date"),
                SAFTExportHeader."Starting Date", SAFTExportHeader."Ending Date");
            EXIT;
        END;

        StartingDate := SAFTExportHeader."Starting Date";
        EndingDate := CALCDATE('<CM>', SAFTExportHeader."Starting Date");
        REPEAT
            StopExportEntriesByPeriod := EndingDate >= SAFTExportHeader."Ending Date";
            IF CALCDATE('<CM>', EndingDate) >= SAFTExportHeader."Ending Date" THEN
                EndingDate := CLOSINGDATE(EndingDate);

            GLEntry.SETRANGE("Posting Date", StartingDate, EndingDate);
            IF NOT GLEntry.ISEMPTY THEN
                InsertSAFTExportLine(
                    SAFTExportLine, LineNo, SAFTExportHeader, FALSE,
                    STRSUBSTNO(GLEntriesMsg, StartingDate, EndingDate), StartingDate, EndingDate);
            StartingDate := NORMALDATE(EndingDate) + 1;
            EndingDate := CALCDATE('<CM>', StartingDate);
        UNTIL StopExportEntriesByPeriod;
    END;

    LOCAL PROCEDURE InsertSAFTExportLine@1100285118(VAR SAFTExportLine@1100285100 : Record 11128519;VAR LineNo@1100285101 : Integer;SAFTExportHeader@1100285102 : Record 11128518;MasterData@1100285103 : Boolean;Description@1100285104 : Text;StartingDate@1100285105 : Date;EndingDate@1100285106 : Date);
    BEGIN
      SAFTExportLine.INIT;
      SAFTExportLine.VALIDATE(ID, SAFTExportHeader.ID);
      LineNo += 1;
      SAFTExportLine.VALIDATE("Line No.", LineNo);
      SAFTExportLine.VALIDATE("Master Data", MasterData);
      SAFTExportLine.VALIDATE(Description, COPYSTR(Description, 1, MAXSTRLEN(Description)));
      SAFTExportLine.VALIDATE("Starting Date", StartingDate);
      SAFTExportLine.VALIDATE("Ending Date", EndingDate);
      SAFTExportLine.INSERT(TRUE);
    END;

    LOCAL PROCEDURE GetStartTraceTagMessage@1100285119(SAFTExportHeader@1100285100 : Record 11128518) : Text;
    BEGIN
      EXIT(STRSUBSTNO(StartingExportTxt, SAFTExportHeader.ID, SAFTExportHeader."Parallel Processing", SAFTExportHeader."Split by Month"));
    END;

    LOCAL PROCEDURE GetCancelTraceTagMessage@1100285120(SAFTExportLine@1100285100 : Record 11128519) : Text;
    BEGIN
      EXIT(STRSUBSTNO(CancellingExportTxt, SAFTExportLine.ID, SAFTExportLine."Task ID"));
    END;

    LOCAL PROCEDURE RemoveExportLines@1100285121(VAR SAFTExportHeader@1100285100 : Record 11128518);
    VAR
      SAFTExportLine@1100285101 : Record 11128519;
    BEGIN
      SAFTExportLine.SETRANGE(ID, SAFTExportHeader.ID);
      IF NOT SAFTExportLine.FINDSET THEN
        EXIT;

      REPEAT
        RemoveExportLine(SAFTExportLine);
      UNTIL SAFTExportLine.NEXT = 0;
    END;

    LOCAL PROCEDURE RemoveExportLine@1100285122(VAR SAFTExportLine@1100285100 : Record 11128519);
    BEGIN
      CancelTask(SAFTExportLine);
      SAFTExportLine.DELETE(TRUE);
      SendTraceTagOfExport(SAFTExportTxt, GetCancelTraceTagMessage(SAFTExportLine));
    END;

    LOCAL PROCEDURE CancelTask@1100285123(SAFTExportLine@1100285100 : Record 11128519);
    VAR
      DoNotCancelTask@1100285101 : Boolean;
    BEGIN
      IF ISNULLGUID(SAFTExportLine."Task ID") THEN
        EXIT;

      OnBeforeCancelTask(DoNotCancelTask);
      IF NOT DoNotCancelTask THEN
        IF TASKSCHEDULER.TASKEXISTS(SAFTExportLine."Task ID") THEN
           TASKSCHEDULER.CANCELTASK(SAFTExportLine."Task ID");
      SendTraceTagOfExport(SAFTExportTxt, GetCancelTraceTagMessage(SAFTExportLine));
    END;

    PROCEDURE BuildZipFilesWithAllRelatedXmlFiles@1100285124(SAFTExportHeader@1100285100 : Record 11128518);
    VAR
      CompanyInformation@1100285101 : Record 79;
      SAFTExportLine@1100285102 : Record 11128519;
      FileMgt@1100285103 : Codeunit 419;
      SAFTXMLHelper@1100285104 : Codeunit 11020364;
      ServerDestinationFolder@1100285105 : Text;
      TotalNumberOfFiles@1100285106 : Integer;
    BEGIN
      CompanyInformation.GET;
      ServerDestinationFolder := FileMgt.ServerCreateTempSubDirectory;
      SAFTExportLine.SETRANGE(ID, SAFTExportHeader.ID);
      SAFTExportLine.FINDSET;
      TotalNumberOfFiles := SAFTExportLine.COUNT;
      REPEAT
        SAFTXMLHelper.ExportSAFTExportLineBlobToFile(
            SAFTExportLine,
            SAFTXMLHelper.GetFilePath(
                ServerDestinationFolder, CompanyInformation."VAT Registration No.", SAFTExportLine."Created Date/Time",
                SAFTExportLine."Line No.", TotalNumberOfFiles));
      UNTIL SAFTExportLine.NEXT = 0;
      ZipMultipleXMLFilesInServerFolder(SAFTExportHeader, ServerDestinationFolder);
    END;

    PROCEDURE GenerateZipFileFromSavedFiles@1100285125(SAFTExportHeader@1100285100 : Record 11128518);
    BEGIN
      IF NOT SAFTExportHeader.AllowedToExportIntoFolder THEN
        EXIT;

      SaveZipOfMultipleXMLFiles(SAFTExportHeader);
    END;

    PROCEDURE DownloadZipFileFromExportHeader@1100285126(SAFTExportHeader@1100285100 : Record 11128518);
    VAR
      ZipFileInStream@1100285101 : InStream;
      FileName@1100285102 : Text;
    BEGIN
      SAFTExportHeader.CALCFIELDS("SAF-T File");
      IF NOT SAFTExportHeader."SAF-T File".HASVALUE THEN
        ERROR(NoZipFileGeneratedErr);
      SAFTExportHeader."SAF-T File".CREATEINSTREAM(ZipFileInStream);
      FileName := SAFTZipFileTxt;
      DOWNLOADFROMSTREAM(ZipFileInStream, ZipArchiveSaveDialogTxt, '', ZipArchiveFilterTxt, FileName);
    END;

    PROCEDURE ZipMultipleXMLFilesInServerFolder@1100285127(VAR SAFTExportHeader@1100285100 : Record 11128518;ServerDestinationFolder@1100285101 : Text);
    VAR
      TempNameValueBuffer@1100285102 : TEMPORARY Record 823;
      FileMgt@1100285103 : Codeunit 419;
      ZipArchiveFile@1100285104 : File;
      ZipOutStream@1100285105 : OutStream;
      ZipInStream@1100285106 : InStream;
      ZipArchive@1100285107 : Text;
    BEGIN
      FileMgt.GetServerDirectoryFilesListInclSubDirs(TempNameValueBuffer, ServerDestinationFolder);
      ZipArchive := FileMgt.CreateZipArchiveObject;
      TempNameValueBuffer.FINDSET;
      REPEAT
        FileMgt.AddFileToZipArchive(TempNameValueBuffer.Name, FileMgt.GetFileName(TempNameValueBuffer.Name));
      UNTIL TempNameValueBuffer.NEXT = 0;
      FileMgt.CloseZipArchive;

      ZipArchiveFile.OPEN(ZipArchive);
      ZipArchiveFile.CREATEINSTREAM(ZipInStream);
      SAFTExportHeader."SAF-T File".CREATEOUTSTREAM(ZipOutStream);
      COPYSTREAM(ZipOutStream, ZipInStream);
      ZipArchiveFile.CLOSE;
      SAFTExportHeader.MODIFY(TRUE);

      FileMgt.GetServerDirectoryFilesListInclSubDirs(TempNameValueBuffer, ServerDestinationFolder);
      REPEAT
        FileMgt.DeleteServerFile(TempNameValueBuffer.Name);
      UNTIL TempNameValueBuffer.NEXT = 0;
    END;

    LOCAL PROCEDURE SaveZipOfMultipleXMLFiles@1100285128(SAFTExportHeader@1100285100 : Record 11128518);
    VAR
      TempNameValueBuffer@1100285106 : TEMPORARY Record 823;
      FileMgt@1100285105 : Codeunit 419;
      ZipArchiveFile@1100285104 : File;
      ZipClientFile@1100285107 : File;
      ZipOutStream@1100285103 : OutStream;
      ZipInStream@1100285102 : InStream;
      ZipArchive@1100285101 : Text;
    BEGIN
      FileMgt.GetServerDirectoryFilesListInclSubDirs(TempNameValueBuffer, SAFTExportHeader."Folder Path 1"+SAFTExportHeader."Folder Path 2");
      ZipArchive := FileMgt.CreateZipArchiveObject;
      TempNameValueBuffer.FINDSET;
      REPEAT
        FileMgt.AddFileToZipArchive(TempNameValueBuffer.Name, FileMgt.GetFileName(TempNameValueBuffer.Name));
      UNTIL TempNameValueBuffer.NEXT = 0;
      FileMgt.CloseZipArchive;

      ZipArchiveFile.OPEN(ZipArchive);
      ZipArchiveFile.CREATEINSTREAM(ZipInStream);
      ZipClientFile.CREATE(SAFTExportHeader."Folder Path 1"+SAFTExportHeader."Folder Path 2" + '\' + SAFTZipFileTxt);
      ZipClientFile.CREATEOUTSTREAM(ZipOutStream);
      COPYSTREAM(ZipOutStream, ZipInStream);
      ZipArchiveFile.CLOSE;
      ZipClientFile.CLOSE;
    END;

    PROCEDURE CheckNoFilesInFolder@1100285129(SAFTExportHeader@1100285100 : Record 11128518);
    VAR
      TempNameValueBuffer@1100285102 : TEMPORARY Record 823;
      FileMgt@1100285101 : Codeunit 419;
      ErrorMessageManagement@1100285103 : Codeunit 28;
    BEGIN
      IF NOT SAFTExportHeader.AllowedToExportIntoFolder THEN
        EXIT;

      FileMgt.GetServerDirectoryFilesListInclSubDirs(TempNameValueBuffer, SAFTExportHeader."Folder Path 1"+SAFTExportHeader."Folder Path 2");
      IF TempNameValueBuffer.COUNT <> 0 THEN
        ErrorMessageManagement.LogError(SAFTExportHeader, FilesExistsInFolderErr, '');
    END;

    PROCEDURE SaveXMLDocToFolder@1100285130(SAFTExportHeader@1100285100 : Record 11128518;XMLDoc@1100285103 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";FileNumber@1100285102 : Integer) : Boolean;
    VAR
      SAFTExportLine@1100285101 : Record 11128519;
      CompanyInformation@1100285104 : Record 79;
      SAFTXMLHelper@1100285105 : Codeunit 11020364;
      File@1100285106 : File;
      Stream@1100285107 : OutStream;
      FilePath@1100285108 : Text;
      TotalNumberOfFiles@1100285109 : Integer;
    BEGIN
      IF NOT SAFTExportHeader.AllowedToExportIntoFolder THEN
        EXIT(FALSE);
      TotalNumberOfFiles := SAFTExportLine.COUNT;
      CompanyInformation.GET;
      FilePath :=
        SAFTXMLHelper.GetFilePath(
            SAFTExportHeader."Folder Path 1"+SAFTExportHeader."Folder Path 2", CompanyInformation."VAT Registration No.", SAFTExportLine."Created Date/Time", FileNumber, TotalNumberOfFiles);
      File.CREATE(FilePath);
      File.CREATEOUTSTREAM(Stream);
      XMLDoc.Save(Stream);
      File.CLOSE;
    END;

    PROCEDURE GetAmountInfoFromGLEntry@1100285131(VAR AmountXMLNode@1100285100 : Text;VAR Amount@1100285101 : Decimal;GLEntry@1100285102 : Record 17);
    BEGIN
      IF GLEntry."Debit Amount" = 0 THEN BEGIN
        AmountXMLNode := 'CreditAmount';
        Amount := GLEntry."Credit Amount";
      END ELSE BEGIN
        AmountXMLNode := 'DebitAmount';
        Amount := GLEntry."Debit Amount";
      END;
    END;

    PROCEDURE GetNotApplicationVATCode@1100285132() : Code[10];
    VAR
      SAFTSetup@1100285100 : Record 11072490;
      VATCode@1101285000 : Record 11128501;
    BEGIN
      SAFTSetup.GET;
      EXIT(COPYSTR(SAFTSetup."Not Applicable VAT Code", 1, MAXSTRLEN(VATCode.Code)));
    END;

    PROCEDURE GetISOCurrencyCode@1100285133(CurrencyCode@1100285100 : Code[10]) : Code[10];
    VAR
      GeneralLedgerSetup@1100285101 : Record 98;
      Currency@1100285102 : Record 4;
    BEGIN
      IF CurrencyCode = '' THEN BEGIN
        GeneralLedgerSetup.GET;
        EXIT(GeneralLedgerSetup."LCY Code");
      END;
      Currency.GET(CurrencyCode);
      EXIT(Currency."ISO Code");
    END;

    LOCAL PROCEDURE localIsSessionActive@1100285134(SAFTExportLine@1100285100 : Record 11128519) : Boolean;
    VAR
      ActiveSession@1100285101 : Record 2000000110;
    BEGIN
      IF SAFTExportLine."Server Instance ID" = SERVICEINSTANCEID THEN
        EXIT(ActiveSession.GET(SAFTExportLine."Server Instance ID", SAFTExportLine."Session ID"));
      IF SAFTExportLine."Server Instance ID" <= 0 THEN
        EXIT(FALSE);
      EXIT(NOT IsSessionLoggedOff(SAFTExportLine));
    END;

    LOCAL PROCEDURE IsSessionLoggedOff@1100285135(SAFTExportLine@1100285100 : Record 11128519) : Boolean;
    VAR
      SessionEvent@1100285101 : Record 2000000111;
      SAFTExportHeader@1100285102 : Record 11128518;
    BEGIN
      SessionEvent.SETRANGE("Server Instance ID", SAFTExportLine."Server Instance ID");
      SessionEvent.SETRANGE("Session ID", SAFTExportLine."Session ID");
      SessionEvent.SETRANGE("Event Type", SessionEvent."Event Type"::Logoff);
      SAFTExportHeader.GET(SAFTExportLine.ID);
      SessionEvent.SETFILTER("Event Datetime", '>%1', SAFTExportHeader."Earliest Start Date/Time");
      SessionEvent.SETRANGE("User SID", USERSECURITYID);
      EXIT(NOT SessionEvent.ISEMPTY);
    END;

    LOCAL PROCEDURE GetNoOfJobsInProgress@1100285136() : Integer;
    VAR
      ScheduledTask@1100285100 : Record 2000000175;
    BEGIN
      ScheduledTask.SETRANGE("Run Codeunit", CODEUNIT::"Generate SAF-T File");
      EXIT(ScheduledTask.COUNT);
    END;

    LOCAL PROCEDURE HandleConfirm@1100285137(ConfirmText@1100285100 : Text) : Boolean;
    BEGIN
      IF NOT GUIALLOWED THEN
        EXIT(TRUE);
      EXIT(CONFIRM(ConfirmText, FALSE));
    END;

    LOCAL PROCEDURE CheckStatus@1100285138(Status@1100285100 : Option;Question@1100285101 : Text) : Boolean;
    VAR
      SAFTExportHeader@1100285102 : Record 11128518;
      StatusMessage@1100285103 : Text;
    BEGIN
      IF Status = SAFTExportHeader.Status::"In Progress" THEN
        StatusMessage := ExportIsInProgressMsg;
      IF Status = SAFTExportHeader.Status::Completed THEN
        StatusMessage := ExportIsCompletedQst;
      IF StatusMessage <> '' THEN
        EXIT(HandleConfirm(StatusMessage + Question));
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE CheckLineStatusForRestart@1100285141(VAR SAFTExportLine@1101285000 : Record 11128519) : Boolean;
    BEGIN
        SAFTExportLine.SETFILTER(Status, '%1|%2', SAFTExportLine.Status::Failed, SAFTExportLine.Status::Completed);
        IF NOT SAFTExportLine.ISEMPTY THEN
            EXIT(HandleConfirm(LinesInProgressOrCompletedMsg + RestartExportLineQst));
        EXIT(TRUE);
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeScheduleTask@1100285139(VAR DoNotScheduleTask@1100285100 : Boolean;VAR TaskID@1100285101 : GUID);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCancelTask@1100285140(VAR DoNotCancelTask@1100285100 : Boolean);
    BEGIN
    END;

    BEGIN
    {
      191003 ORANGO.DL RFC194 implement SAF-T from BC14 CU04 (NAVNO14.04) NorwegianSAFT-extension
      191112 ORANGO.DL updates from BC14.CU06
    }
    END.
  }
}

