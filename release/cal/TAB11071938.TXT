OBJECT Table 11071938 Service Signal Rule
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Service Signal Rule;
  }
  FIELDS
  {
    { 10  ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Typ];
                                                   OptionCaptionML=ENU=Service Call,Service Location,Service Order;
                                                   OptionString=Service Call,Service Location,Service Order }
    { 20  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 30  ;   ;Table Type          ;Option        ;OnValidate=BEGIN
                                                                CASE "Table Type" OF
                                                                  "Table Type"::Customer:
                                                                    "Table No." := DATABASE::Customer;
                                                                  "Table Type"::"Service Contract":
                                                                    "Table No." := DATABASE::"Service-Contract";
                                                                  "Table Type"::"Service Location":
                                                                    "Table No." := DATABASE::"Service Location";
                                                                  "Table Type"::"Service Object":
                                                                    "Table No." := DATABASE::"Service-Object";
                                                                  "Table Type"::"Contract Object":
                                                                    "Table No." := DATABASE::"Contract Object";
                                                                  "Table Type"::"Location Specification":
                                                                    "Table No." := DATABASE::"Location Specification Group";
                                                                  "Table Type"::"Contract Terms",
                                                                  "Table Type"::"Report Procedure":
                                                                    "Table No." := DATABASE::"Service Text";
                                                                  "Table Type"::"Service Contract Arrangement":
                                                                    "Table No." := DATABASE::"Service Contract Arrangement";
                                                                  "Table Type"::"Standard Object":
                                                                    "Table No." := DATABASE::"Standard Object";
                                                                  "Table Type"::"Customer Terms":
                                                                    "Table No." := DATABASE::"General Text";
                                                                  "Table Type"::"Comment Line":
                                                                    "Table No." := DATABASE::"Comment Line";
                                                                END;
                                                                VALIDATE("Field No.", 0);
                                                                ClearValueFilter;
                                                              END;

                                                   CaptionML=[ENU=Table;
                                                              NOR=Tabell;
                                                              SVE=Tabell];
                                                   OptionCaptionML=ENU=Customer,Service Location,Service Contract,Service Object,Object Specification,Contract Object,Location Specification,Contract Terms,Report Procedure,Service Contract Arrangement,Standard Object,Customer Terms,Comment Line;
                                                   OptionString=Customer,Service Location,Service Contract,Service Object,Object Specification,Contract Object,Location Specification,Contract Terms,Report Procedure,Service Contract Arrangement,Standard Object,Customer Terms,Comment Line }
    { 40  ;   ;Table No.           ;Integer       ;InitValue=18;
                                                   CaptionML=[ENU=Table No.;
                                                              NOR=Tabellnr;
                                                              SVE=Tabellnr];
                                                   Editable=No }
    { 50  ;   ;Field No.           ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table No.),
                                                                                  Type=FILTER(<>BLOB));
                                                   OnValidate=BEGIN
                                                                IF "Field No." <> 0 THEN
                                                                  IF "Table Type" IN ["Table Type"::"Object Specification", "Table Type"::"Location Specification"] THEN
                                                                    FIELDERROR("Field No.");
                                                              END;

                                                   OnLookup=VAR
                                                              Field@1100528600 : Record 2000000041;
                                                              FieldTableManagement@1100529200 : Codeunit 11012372;
                                                            BEGIN
                                                              Field.SETRANGE(TableNo, "Table No.");
                                                              Field.SETFILTER(Class, '%1|%2', Field.Class::Normal, Field.Class::FlowField);
                                                              Field.SETFILTER(Type, '<>%1', Field.Type::BLOB);
                                                              FieldTableManagement.SelectFieldFromList("Table No.","Field No.",Field);
                                                              VALIDATE("Field No.");
                                                            END;

                                                   CaptionML=[ENU=Field No.;
                                                              NOR=Feltnr.;
                                                              SVE=F„ltnr] }
    { 60  ;   ;Value Filter        ;Text250       ;OnValidate=BEGIN
                                                                EvaluateFilter(FIELDNO("Value Filter"));
                                                                "Value Filter (Lang. Indep.)" := ConvertToOptionFilter;
                                                              END;

                                                   OnLookup=VAR
                                                              BaseObjectSpecificationOpt@1100528600 : Record 11071780;
                                                              BaseLocationSpecOption@1100528601 : Record 11071960;
                                                            BEGIN
                                                              CASE "Table Type" OF
                                                                "Table Type"::"Object Specification":
                                                                  BEGIN
                                                                    IF IsSpecOptionType THEN BEGIN
                                                                      BaseObjectSpecificationOpt.SETRANGE("Base Object Spec. Line Code", "Specification Code");
                                                                      IF PAGE.RUNMODAL(0, BaseObjectSpecificationOpt) = ACTION::LookupOK THEN
                                                                        VALIDATE("Value Filter", BaseObjectSpecificationOpt.Description);
                                                                    END;
                                                                  END;
                                                                "Table Type"::"Location Specification":
                                                                  BEGIN
                                                                    IF IsSpecOptionType THEN BEGIN
                                                                      BaseLocationSpecOption.SETRANGE("Base Location Spec. Line Code", "Specification Code");
                                                                      IF PAGE.RUNMODAL(0, BaseLocationSpecOption) = ACTION::LookupOK THEN
                                                                        VALIDATE("Value Filter", BaseLocationSpecOption.Description);
                                                                    END;
                                                                  END;
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Value Filter;
                                                              NOR=Verdifilter;
                                                              SVE=V„rdefilter] }
    { 70  ;   ;Value Filter (Lang. Indep.);Text250;CaptionML=[ENU=Value Filter (Language Independent);
                                                              NOR=Verdifilter (uavhengig av spr†k)] }
    { 100 ;   ;Field Name          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field.FieldName WHERE (TableNo=FIELD(Table No.),
                                                                                             No.=FIELD(Field No.)));
                                                   CaptionML=[ENU=Field Name;
                                                              NOR=Feltnavn;
                                                              SVE=F„ltnamn];
                                                   Editable=No }
    { 110 ;   ;Field Caption       ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Table No.),
                                                                                                   No.=FIELD(Field No.)));
                                                   CaptionML=[ENU=Field Caption;
                                                              NOR=Felttekst;
                                                              SVE=F„ltrubrik];
                                                   Editable=No }
    { 120 ;   ;Field Type          ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Type Name" WHERE (TableNo=FIELD(Table No.),
                                                                                               No.=FIELD(Field No.)));
                                                   CaptionML=[ENU=Field Type;
                                                              NOR=Field Type;
                                                              SVE=Field Type];
                                                   Editable=No }
    { 130 ;   ;Priority            ;Integer       ;CaptionML=[ENU=Priority;
                                                              NOR=Prioritet;
                                                              SVE=Prioritet] }
    { 140 ;   ;Specification Code  ;Code10        ;TableRelation=IF (Table Type=CONST(Object Specification)) "Base Object Specification Line".Code
                                                                 ELSE IF (Table Type=CONST(Location Specification)) "Base Location Spec. Line".Code;
                                                   OnValidate=BEGIN
                                                                IF "Specification Code" <> '' THEN BEGIN
                                                                  IF NOT ("Table Type" IN ["Table Type"::"Object Specification", "Table Type"::"Location Specification"]) THEN
                                                                    FIELDERROR("Table Type");
                                                                  ClearValueFilter;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Specification Code;
                                                              NOR=Spesifikasjonskode;
                                                              SVE=Specifikationskod] }
    { 150 ;   ;Comment             ;Text50        ;CaptionML=[ENU=Comment;
                                                              NOR=Kommentar;
                                                              SVE=Kommentar] }
    { 160 ;   ;Display Rule        ;Boolean       ;CaptionML=[ENU=Display Rule;
                                                              SVE=Specifikationsf„lttyp] }
    { 170 ;   ;Period              ;DateFormula   ;OnValidate=VAR
                                                                ServiceLocation@1100528600 : Record 11012801;
                                                                ServiceContractArrangement@1100528601 : Record 11071705;
                                                              BEGIN
                                                                CASE "Table Type" OF
                                                                  "Table Type"::"Service Location":
                                                                    CASE "Field No." OF
                                                                      ServiceLocation.FIELDNO("Total Cost Amount"),
                                                                      ServiceLocation.FIELDNO("No. of Service Calls"):
                                                                        EXIT;
                                                                    END;
                                                                  "Table Type"::"Service Contract Arrangement":
                                                                    CASE "Field No." OF
                                                                      ServiceContractArrangement.FIELDNO("Theoretical Maintenance Date"):
                                                                        EXIT;
                                                                    END;
                                                                END;
                                                                FIELDERROR(Period);
                                                              END;

                                                   CaptionML=[ENU=Period;
                                                              NOR=Periode;
                                                              SVE=Period] }
  }
  KEYS
  {
    {    ;Type,Line No.                           ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528600 : TextConst 'ENU=t;NOR=t;SVE=t';

    PROCEDURE EvaluateFilter@1210190000(ICalledBy@1100528600 : Integer);
    VAR
      TableRef@1210190001 : RecordRef;
      FieldRef@1210190002 : FieldRef;
    BEGIN
      GetTableFieldRef(TableRef, FieldRef);
      FieldRef.SETFILTER(GetValueFilter(ICalledBy));
      IF NOT IsDateType OR NOT FilterHasSpecialDateValue THEN
        "Value Filter" := FieldRef.GETFILTER;
    END;

    PROCEDURE ClearValueFilter@1210190001();
    BEGIN
      IF ("Table Type" <> xRec."Table Type") OR
        ("Table No." <> xRec."Table No.") OR
        ("Field No." <> xRec."Field No.") OR
        ("Specification Code" <> xRec."Specification Code")
      THEN BEGIN
        "Value Filter" := '';
        "Value Filter (Lang. Indep.)" := '';
        CLEAR(Period);
      END;
    END;

    PROCEDURE ConvertDateFilter@1210190003() OValueFilter : Text[250];
    VAR
      Pos@1210190001 : Integer;
    BEGIN
      Pos := STRPOS(LOWERCASE("Value Filter (Lang. Indep.)"), 't');
      IF Pos <> 0 THEN BEGIN
        OValueFilter := DELSTR("Value Filter (Lang. Indep.)", Pos, 1);
        OValueFilter := INSSTR(OValueFilter, FORMAT(TODAY), Pos);
      END ELSE
        OValueFilter := "Value Filter (Lang. Indep.)";
    END;

    PROCEDURE GetValueFilter@1100528601(ICalledBy@1100528600 : Integer) : Text[250];
    BEGIN
      IF IsDateType THEN
        EXIT(ConvertDateFilter)
      ELSE
        IF NOT IsOptionType OR (ICalledBy = FIELDNO("Value Filter")) THEN
          EXIT("Value Filter")
        ELSE
          EXIT("Value Filter (Lang. Indep.)");
    END;

    PROCEDURE IsDateType@1100528600() : Boolean;
    VAR
      BaseObjectSpecificationLine@1100528600 : Record 11071779;
      BaseLocationSpecLine@1100528601 : Record 11071959;
    BEGIN
      CALCFIELDS("Field Type");
      IF FORMAT("Field Type") = 'Date' THEN
        EXIT(TRUE);
      CASE "Table Type" OF
        "Table Type"::"Object Specification":
          IF BaseObjectSpecificationLine.GET("Specification Code") THEN
            EXIT(BaseObjectSpecificationLine."Field Type" = BaseObjectSpecificationLine."Field Type"::Date);
        "Table Type"::"Location Specification":
          IF BaseLocationSpecLine.GET("Specification Code") THEN
            EXIT(BaseLocationSpecLine."Field Type" = BaseLocationSpecLine."Field Type"::Date);
      END;
    END;

    PROCEDURE IsSpecValueType@1100528610() : Boolean;
    VAR
      BaseObjectSpecificationLine@1100528600 : Record 11071779;
      BaseLocationSpecLine@1100528601 : Record 11071959;
    BEGIN
      CASE "Table Type" OF
        "Table Type"::"Object Specification":
          IF BaseObjectSpecificationLine.GET("Specification Code") THEN
            EXIT(BaseLocationSpecLine."Field Type" = BaseLocationSpecLine."Field Type"::Value);
        "Table Type"::"Location Specification":
          IF BaseLocationSpecLine.GET("Specification Code") THEN
            EXIT(BaseLocationSpecLine."Field Type" = BaseLocationSpecLine."Field Type"::Value);
      END;
    END;

    PROCEDURE IsOptionType@1100528604() : Boolean;
    BEGIN
      CALCFIELDS("Field Type");
      IF FORMAT("Field Type") = 'Option' THEN
        EXIT(TRUE);
    END;

    PROCEDURE IsSpecOptionType@1100528609() : Boolean;
    VAR
      BaseObjectSpecificationLine@1100528600 : Record 11071779;
      BaseLocationSpecLine@1100528601 : Record 11071959;
    BEGIN
      CASE "Table Type" OF
        "Table Type"::"Object Specification":
          IF BaseObjectSpecificationLine.GET("Specification Code") THEN
            EXIT(BaseLocationSpecLine."Field Type" = BaseLocationSpecLine."Field Type"::Option);
        "Table Type"::"Location Specification":
          IF BaseLocationSpecLine.GET("Specification Code") THEN
            EXIT(BaseLocationSpecLine."Field Type" = BaseLocationSpecLine."Field Type"::Option);
      END;
    END;

    PROCEDURE FilterHasSpecialDateValue@1100528602() : Boolean;
    BEGIN
      EXIT(STRPOS(LOWERCASE("Value Filter"), Text000) <> 0);
    END;

    PROCEDURE GetTableFieldRef@1100528605(VAR OTableRef@1100528600 : RecordRef;VAR OFieldRef@1100528601 : FieldRef);
    VAR
      Customer@1100528607 : Record 18;
      ServiceContract@1100528606 : Record 11012812;
      ServiceLocation@1100528605 : Record 11012801;
      ServiceObject@1100528604 : Record 11071691;
      ObjectSpecificationLine@1100528603 : Record 11071781;
      LocationSpecificationLine@1100528608 : Record 11071961;
      ContractObject@1100528602 : Record 11071702;
      ServiceText@1100528609 : Record 11012882;
      ServiceContractArrangement@1100528610 : Record 11071705;
      GeneralText@1100528611 : Record 11020260;
      CommentLine@1100527000 : Record 97;
    BEGIN
      CASE "Table Type" OF
        "Table Type"::Customer:
          OTableRef.GETTABLE(Customer);
        "Table Type"::"Service Contract":
          OTableRef.GETTABLE(ServiceContract);
        "Table Type"::"Service Location":
          OTableRef.GETTABLE(ServiceLocation);
        "Table Type"::"Service Object":
          OTableRef.GETTABLE(ServiceObject);
        "Table Type"::"Object Specification":
          BEGIN
            ObjectSpecificationLine.SETRANGE(Code, "Specification Code");
            OTableRef.GETTABLE(ObjectSpecificationLine);
            IF IsDateType THEN
              OFieldRef := OTableRef.FIELD(ObjectSpecificationLine.FIELDNO("Value (Date)"))
            ELSE
              IF IsSpecValueType THEN
                OFieldRef := OTableRef.FIELD(ObjectSpecificationLine.FIELDNO("Value (Value)"))
              ELSE
                OFieldRef := OTableRef.FIELD(ObjectSpecificationLine.FIELDNO(Value));
          END;
        "Table Type"::"Contract Object":
          OTableRef.GETTABLE(ContractObject);
        "Table Type"::"Location Specification":
          BEGIN
            LocationSpecificationLine.SETRANGE("Service Location No.", "Specification Code");
            OTableRef.GETTABLE(LocationSpecificationLine);
            IF IsDateType THEN
              OFieldRef := OTableRef.FIELD(LocationSpecificationLine.FIELDNO("Value (Date)"))
            ELSE
              IF IsSpecValueType THEN
                OFieldRef := OTableRef.FIELD(LocationSpecificationLine.FIELDNO("Value (Value)"))
              ELSE
                OFieldRef := OTableRef.FIELD(LocationSpecificationLine.FIELDNO(Value));
          END;
        "Table Type"::"Contract Terms",
        "Table Type"::"Report Procedure":
          OTableRef.GETTABLE(ServiceText);
        "Table Type"::"Service Contract Arrangement":
          OTableRef.GETTABLE(ServiceContractArrangement);
        "Table Type"::"Customer Terms":
          OTableRef.GETTABLE(GeneralText);
        "Table Type"::"Comment Line":
          OTableRef.GETTABLE(CommentLine);
      END;
      IF NOT ("Table Type" IN ["Table Type"::"Object Specification", "Table Type"::"Location Specification"]) THEN
        OFieldRef := OTableRef.FIELD("Field No.");
    END;

    PROCEDURE ConvertToOptionFilter@1100528603() ConvertedValueFilter : Text[250];
    VAR
      OptionFieldTools@1100528602 : Codeunit 11012271;
      TableRef@1100528600 : RecordRef;
      FieldRef@1100528601 : FieldRef;
      I@1100528603 : Integer;
      P@1100528604 : Integer;
      ValueFilter@1100528605 : Text[250];
      OptionText@1100528606 : Text[250];
      OptionValue@1100528607 : Integer;
      QuotedOption@1100528608 : Boolean;
    BEGIN
      CALCFIELDS("Field Type");
      CASE FORMAT("Field Type") OF
        'Option':
          BEGIN
            ValueFilter := "Value Filter";
            GetTableFieldRef(TableRef, FieldRef);

            P := 1;
            FOR I := 1 TO STRLEN(ValueFilter) DO BEGIN
              IF ValueFilter[I] = '''' THEN
                IF COPYSTR(ValueFilter, I, 2) <> '''''' THEN
                  QuotedOption := NOT QuotedOption
                ELSE
                  I += 1;
              IF NOT QuotedOption AND (ValueFilter[I] IN ['|', '.', '&', '<', '>', '=']) THEN BEGIN
                IF (ValueFilter[I] <> '.') OR (COPYSTR(ValueFilter, I, 2) = '..') THEN BEGIN
                  IF P < I THEN BEGIN
                    OptionText := COPYSTR(ValueFilter, P, I-P);
                    OptionFieldTools.ReadableOptionToOptionValue(FieldRef.OPTIONCAPTION, OptionText, OptionValue);
                    ConvertedValueFilter += FORMAT(OptionValue);
                  END;
                  IF COPYSTR(ValueFilter, I, 2) = '..' THEN BEGIN
                    ConvertedValueFilter += '..';
                    I += 1;
                  END ELSE
                    ConvertedValueFilter += FORMAT(ValueFilter[I]);
                  P := I +1;
                END;
              END;
            END;
            IF P < I THEN BEGIN
              OptionText := COPYSTR(ValueFilter, P, I-P+1);
              OptionFieldTools.ReadableOptionToOptionValue(FieldRef.OPTIONCAPTION, OptionText, OptionValue);
              IF OptionValue = -1 THEN BEGIN
                OptionText := RemoveQuotes(OptionText);
                OptionFieldTools.ReadableOptionToOptionValue(FieldRef.OPTIONCAPTION, OptionText, OptionValue);
              END;
              ConvertedValueFilter += FORMAT(OptionValue);
            END;
          END;
        'Date':
          BEGIN
            P := STRPOS(LOWERCASE("Value Filter"), Text000);
            IF P <> 0 THEN BEGIN
              ConvertedValueFilter := DELSTR("Value Filter", P, STRLEN(Text000));
              ConvertedValueFilter := INSSTR(ConvertedValueFilter, 't', P);
            END ELSE
              ConvertedValueFilter := "Value Filter";
          END;
      END;
    END;

    PROCEDURE RemoveQuotes@1100528606(ITextValue@1100528600 : Text[250]) : Text[250];
    BEGIN
      IF ITextValue = '' THEN
        EXIT(ITextValue);
      IF (ITextValue[1] = '''') AND (ITextValue[STRLEN(ITextValue)] = '''') THEN
        EXIT(DELCHR(ITextValue, '=', ''''))
      ELSE
        EXIT(ITextValue);
    END;

    PROCEDURE GetSpecificationDescription@1100528607() : Text[50];
    VAR
      BaseObjectSpecificationLine@1100528601 : Record 11071779;
      BaseLocationSpecLine@1100528600 : Record 11071959;
    BEGIN
      CASE "Table Type" OF
        "Table Type"::"Object Specification":
          IF BaseObjectSpecificationLine.GET("Specification Code") THEN
            EXIT(BaseObjectSpecificationLine.Description);
        "Table Type"::"Location Specification":
          IF BaseLocationSpecLine.GET("Specification Code") THEN
            EXIT(BaseLocationSpecLine.Description);
      END;
    END;

    PROCEDURE GetSpecificationFieldType@1100528608() : Text[50];
    VAR
      BaseObjectSpecificationLine@1100528601 : Record 11071779;
      BaseLocationSpecLine@1100528600 : Record 11071959;
    BEGIN
      CASE "Table Type" OF
        "Table Type"::"Object Specification":
          IF BaseObjectSpecificationLine.GET("Specification Code") THEN
            EXIT(FORMAT(BaseObjectSpecificationLine."Field Type"));
        "Table Type"::"Location Specification":
          IF BaseLocationSpecLine.GET("Specification Code") THEN
            EXIT(FORMAT(BaseLocationSpecLine."Field Type"));
      END;
    END;

    BEGIN
    END.
  }
}

