OBJECT Table 11072333 DPT Def Resp Pers Perm Project
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnModify=BEGIN
               CALCFIELDS("Subsite Structure");
               IF ("Subsite Structure" < "Subsite Structure"::PermissionPerDocType) AND (Type = Type::Access) THEN
                 "Document Type" := '';
               CheckPermissions;
             END;

    CaptionML=ENU=DPT Def Responsible Person Permission (Project);
  }
  FIELDS
  {
    { 10  ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Typ];
                                                   OptionCaptionML=ENU=,Access;
                                                   OptionString=,Access;
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 20  ;   ;Responsibility      ;Code10        ;TableRelation=Responsibility;
                                                   CaptionML=[ENU=Responsibility;
                                                              NOR=Ansvar;
                                                              SVE=Ansvar];
                                                   NotBlank=Yes }
    { 21  ;   ;Responsibility Description;Text50  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Responsibility.Description WHERE (Code=FIELD(Responsibility)));
                                                   CaptionML=[ENU=Responsibility Description;
                                                              NOR=Ansvar, Beskrivelse;
                                                              SVE=Ansvar, beskrivning];
                                                   Editable=No }
    { 30  ;   ;Subsite Code        ;Code20        ;TableRelation="DPT Default Subsite".Code WHERE (Source Type=CONST(Project),
                                                                                                   Quote Request Subsite=CONST(No));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Subsite Structure", "Quote Request Subsite");
                                                              END;

                                                   CaptionML=ENU=Document Portal Subsite }
    { 31  ;   ;Subsite Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Default Subsite".Description WHERE (Source Type=CONST(Project),
                                                                                                               Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Subsite Description;
                                                   Editable=No }
    { 32  ;   ;Subsite Structure   ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Default Subsite"."Subsite Structure" WHERE (Source Type=CONST(Project),
                                                                                                                       Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Subsite Structure;
                                                   OptionCaptionML=ENU=1 Document Library,Document Library per Document Type,Permission per Document Type;
                                                   OptionString=OneDocLibrary,DocLibraryPerDocType,PermissionPerDocType;
                                                   Editable=No }
    { 33  ;   ;Quote Request Subsite;Boolean      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Default Subsite"."Quote Request Subsite" WHERE (Source Type=CONST(Project),
                                                                                                                           Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Quote Request Subsite;
                                                   Editable=No }
    { 40  ;   ;Document Type       ;Code20        ;TableRelation=IF (Type=CONST(Access),
                                                                     Subsite Structure=CONST(PermissionPerDocType)) "DPT Default Subsite Doc Type"."Document Type" WHERE (Source Type=CONST(Project),
                                                                                                                                                                          Subsite Code=FIELD(Subsite Code));
                                                   CaptionML=[ENU=Document Type;
                                                              NOR=Dokumenttype;
                                                              SVE=Dokumenttyp] }
    { 41  ;   ;Document Type Description;Text50   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Document Type".Description WHERE (Type=FIELD(Document Type)));
                                                   CaptionML=[ENU=Document Type Description;
                                                              NOR=Dokumenttype, Beskrivelse;
                                                              SVE=Dokumenttyp, beskrivning];
                                                   Editable=No }
    { 50  ;   ;Permission Level    ;Option        ;OnValidate=BEGIN
                                                                CheckPermissions;
                                                                IF ("Permission Level" < "Private Permission Level") THEN
                                                                  "Private Permission Level" := "Permission Level";
                                                                IF ("Permission Level" = "Permission Level"::Owner) AND ("Subsite Code" <> '') AND ("Document Type" = '') THEN
                                                                  "Private Permission Level" := "Permission Level";
                                                              END;

                                                   CaptionML=ENU=Permission Level;
                                                   OptionCaptionML=ENU=None,Visitor,Member,Owner;
                                                   OptionString=None,Visitor,Member,Owner }
    { 60  ;   ;Show in View OLD    ;Boolean       ;CaptionML=;
                                                   Editable=No }
    { 70  ;   ;Private Permission Level;Option    ;OnValidate=BEGIN
                                                                TESTFIELD("Subsite Code");
                                                                TESTFIELD("Document Type", '');
                                                                IF ("Permission Level" = "Permission Level"::Owner) THEN
                                                                  "Private Permission Level" := "Permission Level";
                                                                IF ("Permission Level" < "Private Permission Level") THEN
                                                                  FIELDERROR("Private Permission Level");
                                                              END;

                                                   CaptionML=ENU=Private Permission Level;
                                                   OptionCaptionML=ENU=None,Visitor,Member,Owner;
                                                   OptionString=None,Visitor,Member,Owner }
  }
  KEYS
  {
    {    ;Type,Responsibility,Subsite Code,Document Type;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE AddPortalPermissions@1100528302(CodeFilter@1100528303 : Code[20]);
    VAR
      Responsibility@1100528300 : Record 11012007;
      DPTDefaultSubsite@1100528301 : Record 11229941;
      DPTDefaultSubsiteDocType@1100528302 : Record 11229942;
    BEGIN
      IF (NOT Responsibility.FINDSET) THEN
        EXIT;
      REPEAT
        InsertRecord(1, Responsibility.Code, '', '');

        DPTDefaultSubsite.SETRANGE("Source Type", DPTDefaultSubsite."Source Type"::Project);
        IF (CodeFilter <> '') THEN
          DPTDefaultSubsite.SETRANGE(Code, CodeFilter);
        DPTDefaultSubsite.SETRANGE("Quote Request Subsite", FALSE);
        DPTDefaultSubsite.SETRANGE("Subsite Structure", DPTDefaultSubsite."Subsite Structure"::PermissionPerDocType);
        IF (DPTDefaultSubsite.FINDSET) THEN BEGIN
          REPEAT
            DPTDefaultSubsiteDocType.SETRANGE("Source Type", DPTDefaultSubsite."Source Type");
            DPTDefaultSubsiteDocType.SETRANGE("Subsite Code", DPTDefaultSubsite.Code);
            IF (DPTDefaultSubsiteDocType.FINDSET) THEN BEGIN
              InsertRecord(1, Responsibility.Code, DPTDefaultSubsite.Code, '');
              REPEAT
                InsertRecord(1, Responsibility.Code, DPTDefaultSubsite.Code, DPTDefaultSubsiteDocType."Document Type");
              UNTIL (DPTDefaultSubsiteDocType.NEXT = 0);
            END;
          UNTIL (DPTDefaultSubsite.NEXT = 0);
        END;

        DPTDefaultSubsite.SETFILTER("Subsite Structure", '<%1', DPTDefaultSubsite."Subsite Structure"::PermissionPerDocType);
        IF (DPTDefaultSubsite.FINDSET) THEN BEGIN
          REPEAT
            InsertRecord(1, Responsibility.Code, DPTDefaultSubsite.Code, '');
          UNTIL (DPTDefaultSubsite.NEXT = 0);
        END;

        DPTDefaultSubsite.SETRANGE("Quote Request Subsite", TRUE);
        IF (DPTDefaultSubsite.FINDSET) THEN BEGIN
          REPEAT
            InsertRecord(1, Responsibility.Code, DPTDefaultSubsite.Code, '');
          UNTIL (DPTDefaultSubsite.NEXT = 0);
        END;
      UNTIL (Responsibility.NEXT = 0);
    END;

    LOCAL PROCEDURE InsertRecord@1100528301(Type@1100528301 : Option;Responsibility@1100528302 : Code[20];SubsiteCode@1100528303 : Code[20];DocumentType@1100528304 : Code[20]);
    VAR
      JobsSetup@1100409000 : Record 315;
      DPTDefRespPersPermProject@1100528300 : Record 11072333;
    BEGIN
      IF (DPTDefRespPersPermProject.GET(Type, Responsibility, SubsiteCode, DocumentType)) THEN
        EXIT;
      DPTDefRespPersPermProject.INIT;
      DPTDefRespPersPermProject.Type := Type;
      DPTDefRespPersPermProject.Responsibility := Responsibility;
      DPTDefRespPersPermProject."Subsite Code" := SubsiteCode;
      DPTDefRespPersPermProject."Document Type" := DocumentType;
      CASE Type OF
        1: BEGIN
          JobsSetup.GET;
          IF (Responsibility = JobsSetup."Responsibility Project Manager") THEN
            DPTDefRespPersPermProject."Permission Level" := DPTDefRespPersPermProject."Permission Level"::Owner
          ELSE
            DPTDefRespPersPermProject."Permission Level" := DPTDefRespPersPermProject."Permission Level"::Visitor;
        END;
      END;
      DPTDefRespPersPermProject.INSERT;
    END;

    PROCEDURE CheckPermissions@1100409000();
    BEGIN
      CheckQuoteSitePermissionLevel;
      CheckPermissionLevel;
    END;

    LOCAL PROCEDURE CheckPermissionLevel@1100528303();
    VAR
      DPTDefRespPersPermProject@1100528300 : Record 11072333;
    BEGIN
      IF (Type <> Type::Access) THEN
        EXIT;
      IF (("Subsite Code" = '') AND ("Document Type" = '') AND ("Permission Level" = "Permission Level"::None)) THEN BEGIN
        DPTDefRespPersPermProject.SETRANGE(Type , DPTDefRespPersPermProject.Type::Access);
        DPTDefRespPersPermProject.SETRANGE(Responsibility, Responsibility);
        DPTDefRespPersPermProject.SETFILTER("Subsite Code", '<>%1', "Subsite Code");
        DPTDefRespPersPermProject.MODIFYALL("Permission Level", "Permission Level"::None);
        EXIT;
      END;
      CALCFIELDS("Subsite Structure");
      IF ("Subsite Structure" < "Subsite Structure"::PermissionPerDocType) THEN
        EXIT;

      IF ("Document Type" = '') THEN BEGIN
        DPTDefRespPersPermProject.SETRANGE(Type, Type);
        DPTDefRespPersPermProject.SETRANGE(Responsibility, Responsibility);
        DPTDefRespPersPermProject.SETRANGE("Subsite Code", "Subsite Code");
        DPTDefRespPersPermProject.SETFILTER("Document Type", '<>%1', "Document Type");
        DPTDefRespPersPermProject.MODIFYALL("Permission Level", "Permission Level");
      END ELSE BEGIN
        DPTDefRespPersPermProject.GET(Type, Responsibility, "Subsite Code", '');
        IF (DPTDefRespPersPermProject."Permission Level" = DPTDefRespPersPermProject."Permission Level"::None) THEN BEGIN
          "Permission Level" := "Permission Level"::Visitor;
        END;
        IF (DPTDefRespPersPermProject."Permission Level" < "Permission Level") THEN BEGIN
          DPTDefRespPersPermProject."Permission Level" := "Permission Level";
          DPTDefRespPersPermProject.MODIFY;
        END;
      END;
    END;

    LOCAL PROCEDURE CheckQuoteSitePermissionLevel@1100409001();
    BEGIN
      CALCFIELDS("Quote Request Subsite");
      IF (NOT "Quote Request Subsite") THEN
        EXIT;
    END;

    PROCEDURE MakePrivatePermEqualToPerm@1100529900();
    VAR
      DPTDefRespPersPermProject@1100529900 : Record 11072333;
    BEGIN
      DPTDefRespPersPermProject.SETRANGE(Type, Type::Access);
      DPTDefRespPersPermProject.SETFILTER("Subsite Code", '%1', '');
      DPTDefRespPersPermProject.MODIFYALL("Private Permission Level", "Private Permission Level"::None, FALSE);

      DPTDefRespPersPermProject.SETFILTER("Subsite Code", '<>%1', '');
      DPTDefRespPersPermProject.SETFILTER("Document Type", '<>%1', '');
      DPTDefRespPersPermProject.MODIFYALL("Private Permission Level", "Private Permission Level"::None, FALSE);

      DPTDefRespPersPermProject.SETFILTER("Document Type", '%1', '');
      IF (NOT DPTDefRespPersPermProject.FINDSET) THEN
        EXIT;
      REPEAT
        DPTDefRespPersPermProject."Private Permission Level" := DPTDefRespPersPermProject."Permission Level";
        DPTDefRespPersPermProject.MODIFY;
      UNTIL (DPTDefRespPersPermProject.NEXT = 0);
    END;

    BEGIN
    END.
  }
}

