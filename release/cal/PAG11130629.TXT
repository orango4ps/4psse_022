OBJECT Page 11130629 DPT Admin Tool ServLoc
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=DPT-Admin-Tool (Service);
               ENU=DPT Admin Tool (Service);
               NLD=DPT Admin tool (Service)];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table11012801;
    PageType=Worksheet;
    OnOpenPage=BEGIN
                 DPTSetup.GET;
                 DPTSetup.TESTFIELD("Admin Tool Password");
                 SETRANGE("Generate Document Portal Site", TRUE);
                 DPTChangeLogEntry.AddLogEntry(RECORDID, CurrPage.OBJECTID, TRUE);
               END;

    OnClosePage=BEGIN
                  DPTChangeLogEntry.AddLogEntry(RECORDID, CurrPage.OBJECTID, FALSE);
                END;

    OnAfterGetRecord=BEGIN
                       CheckPermission;
                     END;

    ActionList=ACTIONS
    {
      { 1100528307;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100528308;1 ;Action    ;
                      CaptionML=[DEU=Servicestandort bereinigen;
                                 ENU=Cleanup Service Location;
                                 NLD=Servicelocatie opschonen];
                      Image=Restore;
                      OnAction=VAR
                                 ServiceLocation@1100528300 : Record 11012801;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(ServiceLocation);
                                 CleanupLocationsite(ServiceLocation);
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 1100528309;1 ;Action    ;
                      CaptionML=[DEU=≥Site generiert  auf TRUE setzen;
                                 ENU=Set 'Site Generated' to TRUE;
                                 NLD='Site aangemaakt' op WAAR zetten];
                      Image=Restore;
                      OnAction=VAR
                                 ServiceLocation@1100528300 : Record 11012801;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(ServiceLocation);
                                 ToggleGenerateSiteFlag(ServiceLocation, TRUE);
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 1100528311;1 ;Action    ;
                      CaptionML=[DEU=≥Site generiert  auf FALSE setzen;
                                 ENU=Set 'Site Generated' to FALSE;
                                 NLD='Site aangemaakt' op ONWAAR zetten];
                      Image=Restore;
                      OnAction=VAR
                                 ServiceLocation@1100528300 : Record 11012801;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(ServiceLocation);
                                 ToggleGenerateSiteFlag(ServiceLocation, FALSE);
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100528300;0;Container;
                ContainerType=ContentArea }

    { 1100528310;1;Field  ;
                ExtendedDatatype=Masked;
                CaptionML=[DEU=Kennwort;
                           ENU=Password;
                           NLD=Wachtwoord;
                           NOR=Passord;
                           SVE=Lîsenord];
                SourceExpr=Password;
                OnValidate=BEGIN
                             CheckPassword;
                           END;
                            }

    { 1100528301;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1100528302;2;Field  ;
                SourceExpr="No." }

    { 1100528303;2;Field  ;
                SourceExpr=Description }

    { 1100528304;2;Field  ;
                SourceExpr="Generate Document Portal Site" }

    { 1100528305;2;Field  ;
                SourceExpr="Document Portal Site Generated" }

    { 1100528306;2;Field  ;
                SourceExpr="Document Portal URL" }

  }
  CODE
  {
    VAR
      DPTSetup@1100528303 : Record 11229823;
      DPTChangeLogEntry@1100528308 : Record 11012207;
      Window@1100528300 : Dialog;
      Password@1100528302 : Text[30];
      CLEANPROJECTTEXT@1100528309 : TextConst 'ENU=Cleanup Service Location';
      TOGGLEGENERATEFLAGTEXT@1100528310 : TextConst 'ENU=Toggle Flag';
      Text000@1100528306 : TextConst 'DEU=Falsches Passwort.;ENU=Incorrect Password.;NLD=Wachtwoord niet goed.;NOR=Feil passord.;SVE=Ogiltigt lîsenord.';
      Text001@1100528307 : TextConst 'ENU=#1############################;NOR=#1############################;SVE=#1############################';
      Text002@1100528304 : TextConst 'DEU=Sie sind dabei, markierte Servicestandorte zurÅckzusetzen!\Dadurch werden alle verbundenen Belege gelîscht!\\Sind Sie sicher?;ENU=You are about to reset marked Service Locations!\This will delete all related Documents!!\\Are you sure?;NLD=U gaat alle gemarkeerde servicelocaties resetten!\Hierdoor worden alle gerelateerde documenten verwijderd!!\\Weet u dit zeker?';
      Text003@1100528301 : TextConst 'DEU=Sind Sie ganz sicher?\\Diese Handlung hat rechtliche Konsequenzen!!;ENU=Are you very sure?\\This action does have legal consequences!!;NLD=Weet u het heel zeker?\\Deze actie heeft strafrechterlijke consequenties!!';
      Text005@1100529400 : TextConst 'DEU=Sie haben keine Berechtigung zur AusfÅhrung dieser Seite.;ENU=You have no Permission to run this Page.;NLD=U hebt geen rechten om deze pagina uit te voeren.';

    LOCAL PROCEDURE CheckPermission@1100529400();
    BEGIN
      IF (NOT DPTSetup.WRITEPERMISSION) THEN BEGIN
        MESSAGE(Text005);
        CurrPage.CLOSE;
      END;
    END;

    LOCAL PROCEDURE CheckPassword@1100409010();
    BEGIN
      CheckPermission;
      IF (DPTSetup."Admin Tool Password" = '') OR (Password <> DPTSetup."Admin Tool Password") THEN
        ERROR(Text000);
    END;

    LOCAL PROCEDURE CleanupLocationsite@1100528300(VAR ServiceLocation@1100528304 : Record 11012801);
    VAR
      DPTSubsiteServLoc@1100528301 : Record 11072344;
      DPTSubsiteDocTypeServLoc@1100528300 : Record 11072345;
      DocumentLink@1100528306 : Record 11012747;
      DocumentProperties@1100528307 : Record 11012746;
      DPTHeaderText@1100528312 : Record 11229943;
      DPTDocumentVersion@1100529900 : Record 11229944;
      RecRef@1100528305 : RecordRef;
    BEGIN
      CheckPassword;
      IF (NOT ServiceLocation.FINDSET(TRUE)) THEN
        EXIT;
      IF (NOT CONFIRM(Text002, FALSE)) THEN
        EXIT;
      IF (NOT CONFIRM(Text003, FALSE)) THEN
        EXIT;

      Window.OPEN(Text001);
      REPEAT
        DPTChangeLogEntry.AddLogEntry(ServiceLocation.RECORDID, CLEANPROJECTTEXT, TRUE);
        Window.UPDATE(1, ServiceLocation.RECORDID);
        RecRef.GETTABLE(ServiceLocation);

        DocumentLink.SETCURRENTKEY("Record ID");
        DocumentLink.SETRANGE("Record ID", RecRef.RECORDID);
        DocumentLink.SETRANGE("Table No.", DATABASE::"Service Location");
        IF (DocumentLink.FINDSET(TRUE, TRUE)) THEN BEGIN
          REPEAT
            DocumentLink.DELETE;

            DPTDocumentVersion.SETRANGE("Source Type", DPTDocumentVersion."Source Type"::ServiceLocation);
            DPTDocumentVersion.SETRANGE("Source Code", DocumentLink."Document No.");
            DPTDocumentVersion.DELETEALL;

            IF (DocumentProperties.GET(DocumentLink."Document No.")) THEN
              DocumentProperties.DELETE;
          UNTIL (DocumentLink.NEXT = 0);
        END;

        DPTHeaderText.SETRANGE("Table No.", DATABASE::"Service Location");
        DPTHeaderText.SETRANGE("Field No.", ServiceLocation.FIELDNO("Document Portal Site Generated"));
        DPTHeaderText.SETRANGE("Source No.", ServiceLocation."No.");
        DPTHeaderText.DELETEALL;

        DPTHeaderText.SETRANGE("Table No.", DATABASE::"DPT Subsite ServLoc");
        DPTHeaderText.SETRANGE("Field No.", DPTSubsiteServLoc.FIELDNO(Code));
        DPTHeaderText.SETRANGE("Source No.", ServiceLocation."No.");
        DPTHeaderText.DELETEALL;

        ServiceLocation."Document Portal Site Generated" := FALSE;
        ServiceLocation. "Document Portal URL" := '';
        ServiceLocation.MODIFY;

        DPTSubsiteServLoc.SETRANGE("Service Location No.", ServiceLocation."No.");
        DPTSubsiteServLoc.MODIFYALL("Synchronized On", 0DT);

        DPTSubsiteDocTypeServLoc.SETRANGE("Service Location No.", ServiceLocation."No.");
        DPTSubsiteDocTypeServLoc.MODIFYALL("Synchronized On", 0DT);

        DPTChangeLogEntry.AddLogEntry(ServiceLocation.RECORDID, CLEANPROJECTTEXT, FALSE);
      UNTIL (ServiceLocation.NEXT = 0);
      Window.CLOSE;
    END;

    LOCAL PROCEDURE ToggleGenerateSiteFlag@1100528301(VAR ServiceLocation@1100528301 : Record 11012801;BoolIn@1100528300 : Boolean);
    BEGIN
      CheckPassword;
      IF (NOT ServiceLocation.FINDSET(TRUE)) THEN
        EXIT;

      Window.OPEN(Text001);
      REPEAT
        DPTChangeLogEntry.AddLogEntry(ServiceLocation.RECORDID, TOGGLEGENERATEFLAGTEXT, TRUE);
        Window.UPDATE(1, ServiceLocation.RECORDID);
        ServiceLocation."Document Portal Site Generated" := BoolIn;
        ServiceLocation.MODIFY;
        DPTChangeLogEntry.AddLogEntry(ServiceLocation.RECORDID, TOGGLEGENERATEFLAGTEXT, FALSE);
      UNTIL (ServiceLocation.NEXT = 0);
      Window.CLOSE;
    END;

    BEGIN
    END.
  }
}

