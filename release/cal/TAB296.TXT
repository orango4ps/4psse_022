OBJECT Table 296 Reminder Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04,4PS14.00,NAVSE.FI.DK.NO;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ReminderHeader.GET("Reminder No.");
               "Attached to Line No." := 0;
             END;

    OnModify=BEGIN
               TESTFIELD("System-Created Entry",FALSE);
             END;

    OnDelete=BEGIN
               ReminderLine.SETRANGE("Reminder No.","Reminder No.");
               ReminderLine.SETRANGE("Attached to Line No.","Line No.");
               ReminderLine.DELETEALL;
             END;

    CaptionML=[ENU=Reminder Line;
               NOR=Purrelinje;
               SVE=BetalningspÜminnelserad];
  }
  FIELDS
  {
    { 1   ;   ;Reminder No.        ;Code20        ;TableRelation="Reminder Header";
                                                   CaptionML=[ENU=Reminder No.;
                                                              NOR=Purrenr.;
                                                              SVE=BetalningspÜminnelsenr] }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr];
                                                   NotBlank=Yes }
    { 3   ;   ;Attached to Line No.;Integer       ;TableRelation="Reminder Line"."Line No." WHERE (Reminder No.=FIELD(Reminder No.));
                                                   CaptionML=[ENU=Attached to Line No.;
                                                              NOR=Knyttet til linjenr.;
                                                              SVE=Bifogad till radnr];
                                                   Editable=No }
    { 4   ;   ;Type                ;Option        ;OnValidate=VAR
                                                                CustPostingGr@1000 : Record 92;
                                                              BEGIN
                                                                IF Type <> xRec.Type THEN BEGIN
                                                                  ReminderLine := Rec;
                                                                  INIT;
                                                                  Type := ReminderLine.Type;
                                                                  "Account Code DK" := ReminderLine."Account Code DK";  //NAVDK
                                                                  "Account Code NO" := ReminderLine."Account Code NO"; //NAVNO
                                                                  GetReminderHeader;
                                                                  IF Type = Type::"Line Fee" THEN BEGIN
                                                                    "Line Type" := "Line Type"::"Line Fee";
                                                                    CustPostingGr.GET(ReminderHeader."Customer Posting Group");
                                                                    IF CustPostingGr."Add. Fee per Line Account" <> '' THEN
                                                                      VALIDATE("No.",CustPostingGr."Add. Fee per Line Account");
                                                                  END;
                                                                  //NAVDK
                                                                  IF (Type <> Type::" ") AND ("Account Code DK" = '') THEN
                                                                    "Account Code DK" := ReminderHeader."Account Code DK"
                                                                  ELSE
                                                                    "Account Code DK" := '';
                                                                  //>>NAVNO
                                                                  IF (Type <> Type::" ") AND ("Account Code NO" = '') THEN
                                                                    "Account Code NO" := ReminderHeader."Account Code NO"
                                                                  ELSE
                                                                    "Account Code NO" := '';
                                                                  //<<NAVNO
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              NOR=Type;
                                                              SVE=Typ];
                                                   OptionCaptionML=[ENU=" ,G/L Account,Customer Ledger Entry,Line Fee";
                                                                    NOR=" ,Finanskonto,Kundepost,linjegebyr";
                                                                    SVE=" ,Redov.konto,Kundreskontratransaktion,Radavgift"];
                                                   OptionString=[ ,G/L Account,Customer Ledger Entry,Line Fee] }
    { 5   ;   ;Entry No.           ;Integer       ;TableRelation="Cust. Ledger Entry";
                                                   OnValidate=VAR
                                                                IsHandled@1000 : Boolean;
                                                              BEGIN
                                                                IsHandled := FALSE;
                                                                OnBeforeValidateEntryNo(Rec,xRec,CurrFieldNo,IsHandled);
                                                                IF IsHandled THEN
                                                                  EXIT;

                                                                TESTFIELD(Type,Type::"Customer Ledger Entry");
                                                                GetReminderHeader;
                                                                CustLedgEntry.GET("Entry No.");
                                                                CustLedgEntry.TESTFIELD(Open,TRUE);
                                                                CustLedgEntry.TESTFIELD("Customer No.",ReminderHeader."Customer No.");
                                                                IF CustLedgEntry."Currency Code" <> ReminderHeader."Currency Code" THEN
                                                                  ERROR(
                                                                    MustBeSameErr,
                                                                    ReminderHeader.FIELDCAPTION("Currency Code"),
                                                                    ReminderHeader.TABLECAPTION,CustLedgEntry.TABLECAPTION);
                                                                "Posting Date" := CustLedgEntry."Posting Date";
                                                                "Document Date" := CustLedgEntry."Document Date";
                                                                "Due Date" := CustLedgEntry."Due Date";
                                                                "Document Type" := CustLedgEntry."Document Type";
                                                                "Document No." := CustLedgEntry."Document No.";
                                                                Description := CustLedgEntry.Description;
                                                                CustLedgEntry.CALCFIELDS(Amount,"Remaining Amount");
                                                                "Original Amount" := CustLedgEntry.Amount;
                                                                // >> ORANGO.PR
                                                                //"Remaining Amount" := CustLedgEntry."Remaining Amount";
                                                                "Remaining Amount" := CustLedgEntry."Remaining Amount" - CustLedgEntry."Amount ROT" - CustLedgEntry."Amount RUT";
                                                                // << ORANGO.PR
                                                                "Reference No." := CustLedgEntry."Reference No.";  //NAVFI

                                                                OnAfterCopyFromCustLedgEntry(Rec,CustLedgEntry);

                                                                "No. of Reminders" := GetNoOfReminderForCustLedgEntry("Entry No.");

                                                                CalcFinChrg;
                                                              END;

                                                   OnLookup=BEGIN
                                                              IF Type <> Type::"Customer Ledger Entry" THEN
                                                                EXIT;
                                                              SetCustLedgEntryView;
                                                              IF CustLedgEntry.GET("Entry No.") THEN;
                                                              LookupCustLedgEntry;
                                                            END;

                                                   CaptionML=[ENU=Entry No.;
                                                              NOR=Lõpenr.;
                                                              SVE=Lîpnr];
                                                   BlankZero=Yes }
    { 6   ;   ;No. of Reminders    ;Integer       ;OnValidate=BEGIN
                                                                IF Type = Type::"Line Fee" THEN
                                                                  VALIDATE("Applies-to Document No.");
                                                              END;

                                                   CaptionML=[ENU=No. of Reminders;
                                                              NOR=Purregrad;
                                                              SVE=Antal pÜminnelser] }
    { 7   ;   ;Posting Date        ;Date          ;CaptionML=[ENU=Posting Date;
                                                              NOR=Bokfõringsdato;
                                                              SVE=Bokfîringsdatum];
                                                   Editable=No }
    { 8   ;   ;Document Date       ;Date          ;CaptionML=[ENU=Document Date;
                                                              NOR=Bilagsdato;
                                                              SVE=Dokumentdatum];
                                                   Editable=No }
    { 9   ;   ;Due Date            ;Date          ;CaptionML=[ENU=Due Date;
                                                              NOR=Forfallsdato;
                                                              SVE=Fîrfallodatum];
                                                   Editable=No }
    { 10  ;   ;Document Type       ;Option        ;OnValidate=BEGIN
                                                                TESTFIELD(Type,Type::"Customer Ledger Entry");
                                                                VALIDATE("Document No.");
                                                              END;

                                                   CaptionML=[ENU=Document Type;
                                                              NOR=Bilagstype;
                                                              SVE=Dokumenttyp];
                                                   OptionCaptionML=[ENU=" ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund";
                                                                    NOR=" ,Betaling,Faktura,Kreditnota,Rentenota,Purring,Refusjon";
                                                                    SVE=" ,Betalning,Faktura,Kreditnota,RÑntefaktura,BetalningspÜminnelse,èterbetalning"];
                                                   OptionString=[ ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund] }
    { 11  ;   ;Document No.        ;Code20        ;OnValidate=VAR
                                                                IsHandled@1000 : Boolean;
                                                              BEGIN
                                                                IsHandled := FALSE;
                                                                OnBeforeValidateDocumentNo(Rec,xRec,CurrFieldNo,IsHandled);
                                                                IF IsHandled THEN
                                                                  EXIT;

                                                                TESTFIELD(Type,Type::"Customer Ledger Entry");
                                                                "Entry No." := 0;
                                                                IF "Document No." <> '' THEN BEGIN
                                                                  SetCustLedgEntryView;
                                                                  IF "Document Type" <> 0 THEN
                                                                    CustLedgEntry.SETRANGE("Document Type","Document Type");
                                                                  CustLedgEntry.SETRANGE("Document No.","Document No.");
                                                                  IF CustLedgEntry.FINDFIRST THEN
                                                                    VALIDATE("Entry No.",CustLedgEntry."Entry No.")
                                                                  ELSE
                                                                    ERROR(NoOpenEntriesErr,FORMAT(Type),FIELDCAPTION("Document No."),"Document No.");
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupDocNo;
                                                            END;

                                                   CaptionML=[ENU=Document No.;
                                                              NOR=Bilagsnr.;
                                                              SVE=Dokumentnr] }
    { 12  ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 13  ;   ;Original Amount     ;Decimal       ;CaptionML=[ENU=Original Amount;
                                                              NOR=Opprinnelig belõp;
                                                              SVE=Ursprungligt belopp];
                                                   BlankZero=Yes;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCodeFromHeader }
    { 14  ;   ;Remaining Amount    ;Decimal       ;CaptionML=[ENU=Remaining Amount;
                                                              NOR=Restbelõp;
                                                              SVE=èterstÜende belopp];
                                                   BlankZero=Yes;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCodeFromHeader }
    { 15  ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST(" ")) "Standard Text"
                                                                 ELSE IF (Type=CONST(G/L Account)) "G/L Account"
                                                                 ELSE IF (Type=CONST(Line Fee)) "G/L Account";
                                                   OnValidate=BEGIN
                                                                IF "No." <> '' THEN
                                                                  CASE Type OF
                                                                    Type::" ":
                                                                      BEGIN
                                                                        StdTxt.GET("No.");
                                                                        Description := StdTxt.Description;
                                                                      END;
                                                                    Type::"Customer Ledger Entry":
                                                                      BEGIN
                                                                        ReminderLine.Type := ReminderLine.Type::" ";
                                                                        ReminderLine2.Type := ReminderLine2.Type::"G/L Account";
                                                                        ERROR(
                                                                          MustBeErr,
                                                                          FIELDCAPTION(Type),ReminderLine.Type,ReminderLine2.Type);
                                                                      END;
                                                                    Type::"G/L Account":
                                                                      FillLineWithGLAccountData("No.");
                                                                    Type::"Line Fee":
                                                                      FillLineWithGLAccountData("No.");
                                                                  END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              NOR=Nr.;
                                                              SVE=Nr] }
    { 16  ;   ;Amount              ;Decimal       ;OnValidate=BEGIN
                                                                IF Type = Type::" " THEN BEGIN
                                                                  ReminderLine.Type := ReminderLine.Type::"G/L Account";
                                                                  ReminderLine2.Type := ReminderLine.Type::"Customer Ledger Entry";
                                                                  ERROR(
                                                                    MustBeErr,
                                                                    FIELDCAPTION(Type),ReminderLine.Type,ReminderLine2.Type);
                                                                END;
                                                                IF (Type = Type::"Line Fee") AND (Amount < 0) THEN
                                                                  ERROR(MustBePositiveErr,FIELDCAPTION(Amount));

                                                                GetReminderHeader;
                                                                Amount := ROUND(Amount,Currency."Amount Rounding Precision");
                                                                CASE "VAT Calculation Type" OF
                                                                  "VAT Calculation Type"::"Normal VAT",
                                                                  "VAT Calculation Type"::"Reverse Charge VAT",
                                                                  "VAT Calculation Type"::"Full VAT":
                                                                    "VAT Amount" := Amount * ("VAT %" / 100);
                                                                  "VAT Calculation Type"::"Sales Tax":
                                                                    BEGIN
                                                                      "VAT Amount" :=
                                                                        SalesTaxCalculate.CalculateTax(
                                                                          ReminderHeader."Tax Area Code","Tax Group Code",ReminderHeader."Tax Liable",
                                                                          ReminderHeader."Posting Date",Amount,0,0);
                                                                      IF Amount - "VAT Amount" <> 0 THEN
                                                                        "VAT %" := ROUND(100 * "VAT Amount" / Amount,0.00001)
                                                                      ELSE
                                                                        "VAT %" := 0;
                                                                    END;
                                                                  4:
                                                                    BEGIN
                                                                      "VAT Amount" := 0;
                                                                      "VAT %" := 0;
                                                                    END;
                                                                END;
                                                                "VAT Amount" := ROUND("VAT Amount",Currency."Amount Rounding Precision");
                                                              END;

                                                   CaptionML=[ENU=Amount;
                                                              NOR=Belõp;
                                                              SVE=Belopp];
                                                   BlankZero=Yes;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCodeFromHeader }
    { 17  ;   ;Interest Rate       ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD(Type,Type::"Customer Ledger Entry");
                                                                TESTFIELD("Entry No.");
                                                                CalcFinChrg;
                                                              END;

                                                   CaptionML=[ENU=Interest Rate;
                                                              NOR=Rentesats;
                                                              SVE=RÑntesats];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 18  ;   ;Gen. Prod. Posting Group;Code20    ;TableRelation="Gen. Product Posting Group";
                                                   OnValidate=BEGIN
                                                                IF xRec."Gen. Prod. Posting Group" <> "Gen. Prod. Posting Group" THEN
                                                                  IF GenProdPostingGrp.ValidateVatProdPostingGroup(GenProdPostingGrp,"Gen. Prod. Posting Group") THEN
                                                                    VALIDATE("VAT Prod. Posting Group",GenProdPostingGrp."Def. VAT Prod. Posting Group");
                                                              END;

                                                   CaptionML=[ENU=Gen. Prod. Posting Group;
                                                              NOR=Bokfõringsgruppe - vare;
                                                              SVE=Produktbokfîringsmall] }
    { 19  ;   ;VAT %               ;Decimal       ;CaptionML=[ENU=VAT %;
                                                              NOR=Mva-%;
                                                              SVE=Momssats %];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 20  ;   ;VAT Calculation Type;Option        ;CaptionML=[ENU=VAT Calculation Type;
                                                              NOR=Mva-beregningstype;
                                                              SVE=MomsberÑkningstyp];
                                                   OptionCaptionML=[ENU=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax;
                                                                    NOR=Normal mva.,Snudd avregning,Full mva.,Salgsmva.;
                                                                    SVE=Normal moms,OmvÑnd EG-moms,Enbart moms,OmsÑttningsskatt];
                                                   OptionString=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax;
                                                   Editable=No }
    { 21  ;   ;VAT Amount          ;Decimal       ;CaptionML=[ENU=VAT Amount;
                                                              NOR=Mva-belõp;
                                                              SVE=Momsbelopp];
                                                   BlankZero=Yes;
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCodeFromHeader }
    { 22  ;   ;Tax Group Code      ;Code20        ;TableRelation="Tax Group";
                                                   OnValidate=BEGIN
                                                                VALIDATE("VAT Prod. Posting Group");
                                                              END;

                                                   CaptionML=[ENU=Tax Group Code;
                                                              NOR=Mva-gruppekode;
                                                              SVE=Skattegruppskod] }
    { 23  ;   ;VAT Prod. Posting Group;Code20     ;TableRelation="VAT Product Posting Group";
                                                   OnValidate=BEGIN
                                                                GetReminderHeader;
                                                                VATPostingSetup.GET(ReminderHeader."VAT Bus. Posting Group","VAT Prod. Posting Group");
                                                                OnValidateVATProdPostingGroupOnAfterVATPostingSetupGet(VATPostingSetup);
                                                                "VAT %" := VATPostingSetup."VAT %";
                                                                "VAT Calculation Type" := VATPostingSetup."VAT Calculation Type";
                                                                "VAT Identifier" := VATPostingSetup."VAT Identifier";
                                                                "VAT Clause Code" := VATPostingSetup."VAT Clause Code";
                                                                CASE "VAT Calculation Type" OF
                                                                  "VAT Calculation Type"::"Reverse Charge VAT":
                                                                    "VAT %" := 0;
                                                                  "VAT Calculation Type"::"Full VAT":
                                                                    BEGIN
                                                                      TESTFIELD(Type,Type::"G/L Account");
                                                                      TESTFIELD("No.",VATPostingSetup.GetSalesAccount(FALSE));
                                                                    END;
                                                                  "VAT Calculation Type"::"Sales Tax":
                                                                    BEGIN
                                                                      "VAT Amount" :=
                                                                        SalesTaxCalculate.CalculateTax(
                                                                          ReminderHeader."Tax Area Code","Tax Group Code",ReminderHeader."Tax Liable",
                                                                          ReminderHeader."Posting Date",Amount,0,0);
                                                                      IF Amount - "VAT Amount" <> 0 THEN
                                                                        "VAT %" := ROUND(100 * "VAT Amount" / Amount,0.00001)
                                                                      ELSE
                                                                        "VAT %" := 0;
                                                                      "VAT Amount" := ROUND("VAT Amount",Currency."Amount Rounding Precision");
                                                                    END;
                                                                END;
                                                                VALIDATE(Amount);
                                                              END;

                                                   CaptionML=[ENU=VAT Prod. Posting Group;
                                                              NOR=Mva-bokfõringsgruppe - vare;
                                                              SVE=Moms produktbokfîringsmall] }
    { 24  ;   ;VAT Identifier      ;Code20        ;CaptionML=[ENU=VAT Identifier;
                                                              NOR=Mva-type;
                                                              SVE=Moms-ID];
                                                   Editable=No }
    { 25  ;   ;Line Type           ;Option        ;CaptionML=[ENU=Line Type;
                                                              NOR=Linjetype;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=[ENU=Reminder Line,Not Due,Beginning Text,Ending Text,Rounding,On Hold,Additional Fee,Line Fee;
                                                                    NOR=Purrelinje,Ikke forfalt,Starttekst,Sluttekst,Avrunding,Avventende,Tilleggsgebyr,Linjegebyr;
                                                                    SVE=Betal.pÜminnelserad,Ej fîrfallen,Inledande text,Avslutande text,Avrundning,Stoppad,Avgift,Radavgift];
                                                   OptionString=Reminder Line,Not Due,Beginning Text,Ending Text,Rounding,On Hold,Additional Fee,Line Fee }
    { 26  ;   ;VAT Clause Code     ;Code20        ;TableRelation="VAT Clause";
                                                   CaptionML=[ENU=VAT Clause Code;
                                                              NOR=Mva-setningskode;
                                                              SVE=Kod fîr momsklausul] }
    { 27  ;   ;Applies-to Document Type;Option    ;OnValidate=BEGIN
                                                                TESTFIELD(Type,Type::"Line Fee");
                                                                VALIDATE("Applies-to Document No.");
                                                              END;

                                                   CaptionML=[ENU=Applies-to Document Type;
                                                              NOR=Utligningsbilagstype;
                                                              SVE=Kopplas till dokumenttyp];
                                                   OptionCaptionML=[ENU=" ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund";
                                                                    NOR=" ,Betaling,Faktura,Kreditnota,Rentenota,Purring,Refusjon";
                                                                    SVE=" ,Betalning,Faktura,Kreditnota,RÑntefaktura,BetalningspÜminnelse,èterbetalning"];
                                                   OptionString=[ ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund] }
    { 28  ;   ;Applies-to Document No.;Code20     ;OnValidate=VAR
                                                                NextLineFeeLevel@1000 : Integer;
                                                              BEGIN
                                                                TESTFIELD(Type,Type::"Line Fee");
                                                                "Entry No." := 0;
                                                                IF "Applies-to Document No." <> '' THEN BEGIN
                                                                  SetCustLedgEntryView;
                                                                  IF "Applies-to Document Type" <> 0 THEN
                                                                    CustLedgEntry.SETRANGE("Document Type","Applies-to Document Type");
                                                                  CustLedgEntry.SETRANGE("Document No.","Applies-to Document No.");
                                                                  IF NOT CustLedgEntry.FINDFIRST THEN
                                                                    ERROR(NoOpenEntriesErr,CustLedgEntry.TABLENAME,FIELDCAPTION("Document No."),"Applies-to Document No.");
                                                                  "Applies-to Document Type" := CustLedgEntry."Document Type";

                                                                  IF CustLedgEntry."Due Date" >= ReminderHeader."Document Date" THEN
                                                                    ERROR(EntryNotOverdueErr,CustLedgEntry.FIELDCAPTION("Document No."),"Applies-to Document No.",CustLedgEntry.TABLENAME);

                                                                  IF "No. of Reminders" <> 0 THEN
                                                                    NextLineFeeLevel := "No. of Reminders"
                                                                  ELSE
                                                                    NextLineFeeLevel := GetNoOfReminderForCustLedgEntry(CustLedgEntry."Entry No.");

                                                                  IF LineFeeIssuedForReminderLevel(CustLedgEntry,NextLineFeeLevel) THEN
                                                                    ERROR(LineFeeAlreadyIssuedErr,"Applies-to Document Type","Applies-to Document No.",NextLineFeeLevel);

                                                                  GetReminderHeader;
                                                                  IF CustLedgEntry."Currency Code" <> ReminderHeader."Currency Code" THEN
                                                                    ERROR(
                                                                      MustBeSameErr,
                                                                      ReminderHeader.FIELDCAPTION("Currency Code"),
                                                                      ReminderHeader.TABLECAPTION,CustLedgEntry.TABLECAPTION);

                                                                  GetReminderLevel(ReminderLevel,NextLineFeeLevel,NextLineFeeLevel);
                                                                  "Posting Date" := ReminderHeader."Posting Date";
                                                                  "Document Date" := ReminderHeader."Document Date";
                                                                  "Due Date" := ReminderHeader."Due Date";
                                                                  "No. of Reminders" := NextLineFeeLevel;

                                                                  CustLedgEntry.CALCFIELDS("Remaining Amount");
                                                                  VALIDATE(Amount,ReminderLevel.GetAdditionalFee(
                                                                      CustLedgEntry."Remaining Amount",
                                                                      ReminderHeader."Currency Code",
                                                                      TRUE,
                                                                      ReminderHeader."Posting Date"));

                                                                  Description := '';
                                                                  IF (Amount <> 0) AND (ReminderLevel."Add. Fee per Line Description" <> '') THEN
                                                                    Description := STRSUBSTNO(ReminderLevel."Add. Fee per Line Description",
                                                                        "Reminder No.",
                                                                        "No. of Reminders",
                                                                        "Document Date",
                                                                        "Posting Date",
                                                                        "No.",
                                                                        Amount,
                                                                        "Applies-to Document Type",
                                                                        "Applies-to Document No.",
                                                                        ReminderLevel."No.")
                                                                  ELSE
                                                                    IF GLAcc.GET("No.") THEN
                                                                      Description := GLAcc.Name;
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              IF Type <> Type::"Line Fee" THEN
                                                                EXIT;
                                                              SetCustLedgEntryView;
                                                              IF "Applies-to Document Type" <> 0 THEN
                                                                CustLedgEntry.SETRANGE("Document Type","Applies-to Document Type");
                                                              IF "Applies-to Document No." <> '' THEN
                                                                CustLedgEntry.SETRANGE("Document No.","Applies-to Document No.");
                                                              IF CustLedgEntry.FINDFIRST THEN;
                                                              CustLedgEntry.SETRANGE("Document Type");
                                                              CustLedgEntry.SETRANGE("Document No.");
                                                              LookupCustLedgEntry;
                                                            END;

                                                   CaptionML=[ENU=Applies-to Document No.;
                                                              NOR=Bilagsnr. for utligning;
                                                              SVE=Kopplas till dokumentnr] }
    { 30  ;   ;Detailed Interest Rates Entry;Boolean;
                                                   CaptionML=[ENU=Detailed Interest Rates Entry;
                                                              NOR=Registrering av detaljerte rentesatser] }
    { 101 ;   ;System-Created Entry;Boolean       ;CaptionML=[ENU=System-Created Entry;
                                                              NOR=Opprettet automatisk;
                                                              SVE=Systemkreerad trans.];
                                                   Editable=No }
    { 10601;  ;Account Code NO     ;Text30        ;OnValidate=BEGIN
                                                                IF (Type = Type::" ") AND ("Account Code NO" <> '') THEN
                                                                  ERROR(Text10600,FIELDCAPTION("Account Code NO"),FIELDCAPTION(Type),Type);
                                                              END;

                                                   CaptionML=[ENU=Account Code;
                                                              NOR=Kontokode;
                                                              SVE=Kontokod (DK)];
                                                   Description=NAVNO (10601) }
    { 13600;  ;Account Code DK     ;Text30        ;OnValidate=BEGIN
                                                                IF (Type = Type::" ") AND ("Account Code DK" <> '') THEN
                                                                  ERROR(Text13600,FIELDCAPTION("Account Code DK"),FIELDCAPTION(Type),Type);
                                                              END;

                                                   CaptionML=[ENU=Account Code;
                                                              NOR=Kontokode;
                                                              SVE=Kontokod (DK)];
                                                   Description=NAVDK (13600) }
    { 11012000;;Reminder Level     ;Integer       ;CaptionML=[ENU=Reminder Level;
                                                              NOR=BetalingspÜminnelsenivÜ;
                                                              SVE=BetalningspÜminnelsenivÜ];
                                                   Editable=No }
    { 32000000;;Reference No.      ;Code20        ;CaptionML=[ENU=Reference No.;
                                                              NOR=Referansenr;
                                                              SVE=Referensnummer];
                                                   Description=NAVFI (32000000) }
  }
  KEYS
  {
    {    ;Reminder No.,Line No.                   ;MaintainSIFTIndex=No;
                                                   Clustered=Yes }
    {    ;Reminder No.,Type,Line Type,Detailed Interest Rates Entry;
                                                   SumIndexFields=Amount,VAT Amount,Remaining Amount;
                                                   MaintainSQLIndex=No;
                                                   MaintainSIFTIndex=No }
    {    ;Reminder No.,Detailed Interest Rates Entry;
                                                   SumIndexFields=Amount,VAT Amount,Remaining Amount }
    {    ;Reminder No.,Type                       ;SumIndexFields=VAT Amount }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      MustBeSameErr@1000 : TextConst 'ENU=The %1 on the %2 and the %3 must be the same.;NOR=%1 pÜ %2 og %3 mÜ vëre lik.;SVE=%1 pÜ %2 och pÜ %3 mÜste vara samma.';
      MustBeErr@1001 : TextConst 'ENU=%1 must be %2 or %3.;NOR=%1 mÜ vëre %2 eller %3.;SVE=%1 mÜste vara %2 eller %3.';
      FinChrgTerms@1002 : Record 5;
      ReminderTerms@1003 : Record 292;
      ReminderLevel@1004 : Record 293;
      ReminderHeader@1005 : Record 295;
      ReminderLine@1006 : Record 296;
      ReminderLine2@1007 : Record 296;
      ReminderEntry@1008 : Record 300;
      CustLedgEntry@1009 : Record 21;
      Currency@1011 : Record 4;
      VATPostingSetup@1012 : Record 325;
      CustPostingGr@1013 : Record 92;
      GLAcc@1014 : Record 15;
      StdTxt@1015 : Record 7;
      GenProdPostingGrp@1016 : Record 251;
      SalesTaxCalculate@1017 : Codeunit 398;
      InterestCalcDate@1018 : Date;
      CalcInterest@1019 : Boolean;
      NoOpenEntriesErr@1010 : TextConst '@@@="%1 = Table name, %2 = Document Type, %3 = Document No.";ENU=There is no open %1 with %2 %3.;NOR=Det finnes ingen Üpen %1 med %2 %3.;SVE=Det finns ingen îppen %1 med %2 %3.';
      EntryNotOverdueErr@1020 : TextConst '@@@="%1 = Document Type, %2 = Document No., %3 = Table name";ENU=%1 %2 in %3 is not overdue.;NOR=%1 %2 i %3 er ikke forfalt.;SVE=%1 %2 i %3 har inte fîrfallit.';
      LineFeeAlreadyIssuedErr@1021 : TextConst '@@@="%1 = Document TYpe, %2 = Document No, %3 = Level number";ENU=The line fee for %1 %2 on reminder level %3 has already been issued.;NOR=Linjegebyret for %1 %2 pÜ purregrad %3 er allerede utstedt.;SVE=Radavgiften fîr %1 %2 pÜ betalningspÜminnelsenivÜ %3 har redan utfÑrdats.';
      MustBePositiveErr@1022 : TextConst 'ENU=%1 must be positive.;NOR=%1 mÜ vëre positiv.;SVE=%1 mÜste vara positivt.';
      NrOfLinesToInsert@1023 : Integer;
      NotEnoughSpaceToInsertErr@1025 : TextConst 'ENU=There is not enough space to insert lines with additional interest rates.;NOR=Det er ikke nok plass til Ü sette inn linjer med ytterligere rentesatser.';
      InvalidInterestRateDateErr@1024 : TextConst '@@@=%1 - date;ENU=Create interest rate with start date prior to %1.;NOR=Opprett rentesats med startdato fõr %1.;SVE=Skapa en rÑntesats med startdatum tidigare Ñn %1.';
      Text3010590@1080000 : TextConst 'ENU=There is not enough space to insert lines with additional interest rates;NOR=Det er ikke nok plass til Ü sette inn linjer med ytterligere rentesatser.;SVE=Det finns inte plats att infoga rader med ytterligare rÑntesatser';
      Text3010591@1040000 : TextConst 'ENU=Create interest rate with start date prior to %1.;NOR=Opprette en rentesats med startdato tidligere enn %1.;SVE=Skapa en rÑntesats med startdatum tidigare Ñn %1.';
      Text10600@1080010 : TextConst 'ENU=You cannot enter %1 if %2 is "%3".;NOR=Du kan ikke angi %1 hvis %2 er %3.;SVE=Du kan inte ange %1 om %2 Ñr "%3".';
      Text13600@1101100000 : TextConst 'ENU=You cannot enter %1 if %2 is "%3".;NOR=Du kan ikke angi %1 om %2 er "%3".;SVE=Du kan inte ange %1 om %2 Ñr "%3".';

    LOCAL PROCEDURE CalcFinChrg@6();
    VAR
      DetailedCustLedgEntry@1001 : Record 379;
      IssuedReminderHeader@1004 : Record 297;
      FinanceChargeInterestRate@1000 : Record 572;
      ExtraReminderLine@1005 : Record 296;
      InterestStartDate@1002 : Date;
      LineFee@1003 : Decimal;
      UseDueDate@1014 : Date;
      UseCalcDate@1013 : Date;
      UseInterestRate@1009 : Decimal;
      CumAmount@1007 : Decimal;
      IsHandled@1006 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeCalcFinChrg(Rec,ReminderHeader,IsHandled);
      IF IsHandled THEN
        EXIT;

      GetReminderHeader;
      "Interest Rate" := 0;
      Amount := 0;
      "VAT Amount" := 0;
      "VAT Calculation Type" := "VAT Calculation Type"::"Normal VAT";
      "Gen. Prod. Posting Group" := '';
      "VAT Prod. Posting Group" := '';
      ExtraReminderLine := Rec;
      ExtraReminderLine.SETRANGE("Reminder No.","Reminder No.");
      ExtraReminderLine.SETRANGE("Detailed Interest Rates Entry",TRUE);
      ExtraReminderLine.SETRANGE("Entry No.","Entry No.");
      ExtraReminderLine.DELETEALL;
      CustLedgEntry.GET("Entry No.");
      IF (CustLedgEntry."On Hold" <> '') OR ("Due Date" >= ReminderHeader."Document Date") THEN
        EXIT;

      ReminderLevel.SETRANGE("Reminder Terms Code",ReminderHeader."Reminder Terms Code");
      IF ReminderHeader."Use Header Level" THEN
        ReminderLevel.SETRANGE("No.",1,ReminderHeader."Reminder Level")
      ELSE
        ReminderLevel.SETRANGE("No.",1,"No. of Reminders");
      IF NOT ReminderLevel.FINDLAST THEN
        ReminderLevel.INIT;
      IF (NOT ReminderLevel."Calculate Interest") OR (ReminderHeader."Fin. Charge Terms Code" = '') THEN
        EXIT;
      FinChrgTerms.GET(ReminderHeader."Fin. Charge Terms Code");

      CalcFinanceChargeInterestRate(FinanceChargeInterestRate,UseDueDate,UseInterestRate,UseCalcDate);

      CASE FinChrgTerms."Interest Calculation Method" OF
        FinChrgTerms."Interest Calculation Method"::"Average Daily Balance":
          BEGIN
            CalcInterest := FALSE;
            IF NrOfLinesToInsert = 0 THEN
              FinChrgTerms.TESTFIELD("Interest Period (Days)")
            ELSE
              FinanceChargeInterestRate.TESTFIELD("Interest Period (Days)");
            InterestCalcDate := CustLedgEntry."Due Date";
            ReminderEntry.SETCURRENTKEY("Customer Entry No.");
            ReminderEntry.SETRANGE("Customer Entry No.","Entry No.");
            ReminderEntry.SETRANGE(Type,ReminderEntry.Type::Reminder);
            ReminderEntry.SETRANGE("Interest Posted",TRUE);
            IF ReminderEntry.FINDLAST THEN
              InterestCalcDate := ReminderEntry."Document Date";
            ReminderEntry.SETRANGE(Type,ReminderEntry.Type::"Finance Charge Memo");
            ReminderEntry.SETRANGE("Interest Posted");
            IF ReminderEntry.FINDLAST THEN
              IF ReminderEntry."Document Date" > InterestCalcDate THEN
                InterestCalcDate := ReminderEntry."Document Date";
            IF InterestCalcDate < ReminderHeader."Document Date" THEN BEGIN
              CalcInterest := TRUE;
                REPEAT
                  IF DetailedCustLedgEntry."Entry Type" = DetailedCustLedgEntry."Entry Type"::"Initial Entry" THEN
                    InterestStartDate := CustLedgEntry."Due Date"
                  ELSE
                    InterestStartDate := DetailedCustLedgEntry."Posting Date";
                  IF InterestCalcDate > InterestStartDate THEN
                    InterestStartDate := InterestCalcDate;
                  Amount := Amount + DetailedCustLedgEntry.Amount * (ReminderHeader."Document Date" - InterestStartDate);
                UNTIL DetailedCustLedgEntry.NEXT = 0;
              IF NOT FinChrgTerms."Add. Line Fee in Interest" THEN
                IF CustLedgEntry."Document Type" = CustLedgEntry."Document Type"::Reminder THEN
                  IF IssuedReminderHeader.GET(CustLedgEntry."Document No.") THEN BEGIN
                    IssuedReminderHeader.CALCFIELDS("Add. Fee per Line");
                    LineFee := IssuedReminderHeader."Add. Fee per Line" + IssuedReminderHeader.CalculateLineFeeVATAmount;
                    Amount := Amount - LineFee * (ReminderHeader."Document Date" - InterestStartDate);
                    IF Amount < 0 THEN
                      Amount := 0;
                  END;
            END;
            IF CalcInterest THEN
              Amount := Amount / FinChrgTerms."Interest Period (Days)" * "Interest Rate" / 100
            ELSE
              Amount := 0;
            IF (InterestCalcDate < ReminderHeader."Document Date") AND (NrOfLinesToInsert = 0) THEN
              IF NrOfLinesToInsert = 0 THEN
                CumulateDetailedEntries(
                  Amount,UseDueDate,UseCalcDate,UseInterestRate,FinChrgTerms."Interest Period (Days)")
              ELSE
                CumulateDetailedEntries(
                  Amount,UseDueDate,UseCalcDate,UseInterestRate,FinanceChargeInterestRate."Interest Period (Days)");
            IF (NrOfLinesToInsert > 0) AND
               (FinChrgTerms."Interest Calculation Method" = FinChrgTerms."Interest Calculation Method"::"Average Daily Balance")
            THEN
              CreateMulitplyInterestRateEntries(
                ExtraReminderLine,FinanceChargeInterestRate,UseDueDate,UseInterestRate,UseCalcDate,CumAmount);

            IF CumAmount <> 0 THEN
              VALIDATE(Amount,CumAmount);
          END;
        FinChrgTerms."Interest Calculation Method"::"Balance Due":
          IF "Due Date" < ReminderHeader."Document Date" THEN
            Amount := "Remaining Amount" * "Interest Rate" / 100;
      END;
      IF Amount <> 0 THEN BEGIN
        CustPostingGr.GET(ReminderHeader."Customer Posting Group");
        GLAcc.GET(CustPostingGr.GetInterestAccount);
        GLAcc.TESTFIELD("Gen. Prod. Posting Group");
        "Gen. Prod. Posting Group" := GLAcc."Gen. Prod. Posting Group";
        VALIDATE("VAT Prod. Posting Group",GLAcc."VAT Prod. Posting Group");
      END;

      OnAfterCalcFinChrg(Rec,ReminderHeader);
    END;

    LOCAL PROCEDURE SetCustLedgEntryView@9();
    BEGIN
      GetReminderHeader;
      CustLedgEntry.SETCURRENTKEY("Customer No.",Open);
      CustLedgEntry.SETRANGE("Customer No.",ReminderHeader."Customer No.");
      CustLedgEntry.SETRANGE(Open,TRUE);

      OnAfterSetCustLedgEntryView(ReminderHeader,Rec,CustLedgEntry);
    END;

    LOCAL PROCEDURE LookupCustLedgEntry@12();
    BEGIN
      IF PAGE.RUNMODAL(0,CustLedgEntry) = ACTION::LookupOK THEN
        IF Type = Type::"Line Fee" THEN BEGIN
          VALIDATE("Applies-to Document Type",CustLedgEntry."Document Type");
          VALIDATE("Applies-to Document No.",CustLedgEntry."Document No.");
        END ELSE
          VALIDATE("Entry No.",CustLedgEntry."Entry No.");
    END;

    LOCAL PROCEDURE GetReminderHeader@2();
    BEGIN
      IF "Reminder No." <> ReminderHeader."No." THEN BEGIN
        ReminderHeader.GET("Reminder No.");
        ProcessReminderHeader;
      END;
    END;

    LOCAL PROCEDURE ProcessReminderHeader@1();
    BEGIN
      ReminderHeader.TESTFIELD("Customer No.");
      ReminderHeader.TESTFIELD("Document Date");
      ReminderHeader.TESTFIELD("Customer Posting Group");
      ReminderHeader.TESTFIELD("Reminder Terms Code");
      ReminderTerms.GET(ReminderHeader."Reminder Terms Code");
      IF ReminderHeader."Currency Code" = '' THEN
        Currency.InitRoundingPrecision
      ELSE BEGIN
        Currency.GET(ReminderHeader."Currency Code");
        Currency.TESTFIELD("Amount Rounding Precision");
      END;
    END;

    [External]
    PROCEDURE GetCurrencyCodeFromHeader@4() : Code[10];
    VAR
      ReminderHeader@1000 : Record 295;
    BEGIN
      IF "Reminder No." = ReminderHeader."No." THEN
        EXIT(ReminderHeader."Currency Code");

      IF ReminderHeader.GET("Reminder No.") THEN
        EXIT(ReminderHeader."Currency Code");

      EXIT('');
    END;

    LOCAL PROCEDURE FillLineWithGLAccountData@1023(GLAccountNo@1000 : Code[20]);
    BEGIN
      GLAcc.GET(GLAccountNo);
      GLAcc.CheckGLAcc;
      IF NOT "System-Created Entry" THEN
        GLAcc.TESTFIELD("Direct Posting",TRUE);
      GLAcc.TESTFIELD("Gen. Prod. Posting Group");
      IF Description = '' THEN
        Description := GLAcc.Name;
      GetReminderHeader;
      "Tax Group Code" := GLAcc."Tax Group Code";
      "Gen. Prod. Posting Group" := GLAcc."Gen. Prod. Posting Group";
      VALIDATE("VAT Prod. Posting Group",GLAcc."VAT Prod. Posting Group");
    END;

    LOCAL PROCEDURE GetNoOfReminderForCustLedgEntry@1024(EntryNo@1002 : Integer) : Integer;
    VAR
      CustLedgerEntry@1000 : Record 21;
      NoOfReminders@1001 : Integer;
    BEGIN
      CustLedgerEntry.GET(EntryNo);
      NoOfReminders := 0;
      ReminderEntry.RESET;
      ReminderEntry.SETCURRENTKEY("Customer Entry No.");
      ReminderEntry.SETRANGE("Customer Entry No.",EntryNo);
      ReminderEntry.SETRANGE(Type,ReminderEntry.Type::Reminder);
      IF ReminderEntry.FINDLAST THEN
        NoOfReminders := ReminderEntry."Reminder Level";
      IF (CustLedgerEntry."On Hold" = '') AND (CustLedgerEntry."Due Date" < ReminderHeader."Document Date") THEN
        NoOfReminders := NoOfReminders + 1;

      EXIT(NoOfReminders);
    END;

    LOCAL PROCEDURE LineFeeIssuedForReminderLevel@1025(VAR CustLedgEntry@1002 : Record 21;IssuedNoOfReminders@1000 : Integer) : Boolean;
    VAR
      IssuedReminderLine@1001 : Record 298;
    BEGIN
      IssuedReminderLine.SETRANGE("Applies-To Document Type",CustLedgEntry."Document Type");
      IssuedReminderLine.SETRANGE("Applies-To Document No.",CustLedgEntry."Document No.");
      IssuedReminderLine.SETRANGE(Type,IssuedReminderLine.Type::"Line Fee");
      IssuedReminderLine.SETRANGE("No. of Reminders",IssuedNoOfReminders);
      EXIT(IssuedReminderLine.FINDFIRST);
    END;

    LOCAL PROCEDURE GetReminderLevel@1026(VAR ReminderLevel@1001 : Record 293;LevelStart@1000 : Integer;LevelEnd@1002 : Integer);
    BEGIN
      ReminderLevel.SETRANGE("Reminder Terms Code",ReminderHeader."Reminder Terms Code");
      IF ReminderHeader."Use Header Level" THEN
        ReminderLevel.SETRANGE("No.",LevelStart,ReminderHeader."Reminder Level")
      ELSE
        ReminderLevel.SETRANGE("No.",LevelStart,LevelEnd);
      IF NOT ReminderLevel.FINDLAST THEN
        ReminderLevel.INIT;
    END;

    LOCAL PROCEDURE CumulateDetailedEntries@3(VAR CumAmount@1004 : Decimal;UseDueDate@1003 : Date;UseCalcDate@1002 : Date;UseInterestRate@1001 : Decimal;UseInterestPeriod@1000 : Decimal);
    VAR
      IssuedReminderHeader@1008 : Record 297;
      DetailedCustLedgEntry@1006 : Record 379;
      InterestStartDate@1005 : Date;
      LineFee@1007 : Decimal;
    BEGIN
      CalcInterest := TRUE;
      DetailedCustLedgEntry.SETCURRENTKEY("Cust. Ledger Entry No.","Entry Type","Posting Date");
      DetailedCustLedgEntry.SETRANGE("Cust. Ledger Entry No.",CustLedgEntry."Entry No.");
      DetailedCustLedgEntry.SETFILTER("Entry Type",'%1|%2|%3|%4|%5',
        DetailedCustLedgEntry."Entry Type"::"Initial Entry",
        DetailedCustLedgEntry."Entry Type"::Application,
        DetailedCustLedgEntry."Entry Type"::"Payment Tolerance",
        DetailedCustLedgEntry."Entry Type"::"Payment Discount Tolerance (VAT Excl.)",
        DetailedCustLedgEntry."Entry Type"::"Payment Discount Tolerance (VAT Adjustment)");
      DetailedCustLedgEntry.SETRANGE("Posting Date",0D,ReminderHeader."Document Date");
      CumAmount := 0;
      IF DetailedCustLedgEntry.FIND('-') THEN
        REPEAT
          IF DetailedCustLedgEntry."Entry Type" = DetailedCustLedgEntry."Entry Type"::"Initial Entry" THEN
            InterestStartDate := UseDueDate
          ELSE
            IF UseDueDate < DetailedCustLedgEntry."Posting Date" THEN
              InterestStartDate := DetailedCustLedgEntry."Posting Date";
          IF InterestCalcDate > InterestStartDate THEN
            InterestStartDate := InterestCalcDate;
          IF InterestStartDate < UseCalcDate THEN
            CumAmount := CumAmount + (DetailedCustLedgEntry.Amount * (UseCalcDate - InterestStartDate));
        UNTIL DetailedCustLedgEntry.NEXT = 0;
      IF NOT FinChrgTerms."Add. Line Fee in Interest" THEN
        IF CustLedgEntry."Document Type" = CustLedgEntry."Document Type"::Reminder THEN
          IF IssuedReminderHeader.GET(CustLedgEntry."Document No.") THEN BEGIN
            IssuedReminderHeader.CALCFIELDS("Add. Fee per Line");
            LineFee := IssuedReminderHeader."Add. Fee per Line" + IssuedReminderHeader.CalculateLineFeeVATAmount;
            CumAmount := CumAmount - LineFee * (ReminderHeader."Document Date" - InterestStartDate);
            IF CumAmount < 0 THEN
              CumAmount := 0;
          END;
      IF CalcInterest THEN
        CumAmount := ROUND(CumAmount / UseInterestPeriod * UseInterestRate / 100,Currency."Amount Rounding Precision")
      ELSE
        CumAmount := 0;
    END;

    [External]
    PROCEDURE LookupDocNo@5();
    VAR
      IsHandled@1000 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeLookupDocNo(Rec,IsHandled);
      IF IsHandled THEN
        EXIT;

      IF Type <> Type::"Customer Ledger Entry" THEN
        EXIT;

      SetCustLedgEntryView;
      IF "Document Type" <> 0 THEN
        CustLedgEntry.SETRANGE("Document Type","Document Type");
      IF "Document No." <> '' THEN
        CustLedgEntry.SETRANGE("Document No.","Document No.");
      IF CustLedgEntry.FINDFIRST THEN;
      CustLedgEntry.SETRANGE("Document Type");
      CustLedgEntry.SETRANGE("Document No.");
      LookupCustLedgEntry;
    END;

    LOCAL PROCEDURE CalcFinanceChargeInterestRate@17(VAR FinanceChargeInterestRate@1003 : Record 572;VAR UseDueDate@1000 : Date;VAR UseInterestRate@1001 : Decimal;VAR UseCalcDate@1002 : Date);
    VAR
      LastRateFound@1004 : Boolean;
    BEGIN
      UseDueDate := CustLedgEntry."Due Date";
      UseInterestRate := FinChrgTerms."Interest Rate";
      UseCalcDate := 0D;
      NrOfLinesToInsert := 0;

      FinanceChargeInterestRate.INIT;
      FinanceChargeInterestRate.SETRANGE("Fin. Charge Terms Code",ReminderHeader."Fin. Charge Terms Code");
      FinanceChargeInterestRate."Fin. Charge Terms Code" := ReminderHeader."Fin. Charge Terms Code";
      IF FinChrgTerms."Interest Calculation Method" = FinChrgTerms."Interest Calculation Method"::"Average Daily Balance" THEN
        FinanceChargeInterestRate."Start Date" := CALCDATE('<+1D>',CustLedgEntry."Due Date")
      ELSE
        FinanceChargeInterestRate."Start Date" := ReminderHeader."Document Date";
      NrOfLinesToInsert := 0;
      LastRateFound := FALSE;
      IF FinanceChargeInterestRate.FIND('=<') THEN BEGIN
        UseInterestRate := FinanceChargeInterestRate."Interest Rate";
        IF FinChrgTerms."Interest Calculation Method" = FinChrgTerms."Interest Calculation Method"::"Average Daily Balance" THEN
          REPEAT
            IF FinanceChargeInterestRate."Start Date" <= ReminderHeader."Document Date" THEN
              NrOfLinesToInsert := NrOfLinesToInsert + 1
            ELSE
              LastRateFound := TRUE;
          UNTIL LastRateFound OR (FinanceChargeInterestRate.NEXT = 0);
        IF UseCalcDate = 0D THEN BEGIN
          FinanceChargeInterestRate.NEXT(-1);
          UseCalcDate := FinanceChargeInterestRate."Start Date";
        END;
      END ELSE
        IF FinanceChargeInterestRate.COUNT > 0 THEN
          ERROR(InvalidInterestRateDateErr,FinanceChargeInterestRate."Start Date");

      IF (UseCalcDate = 0D) OR (UseCalcDate < ReminderHeader."Document Date") THEN
        UseCalcDate := ReminderHeader."Document Date";
      "Interest Rate" := UseInterestRate;
    END;

    LOCAL PROCEDURE CreateMulitplyInterestRateEntries@7(VAR ExtraReminderLine@1000 : Record 296;VAR FinanceChargeInterestRate@1003 : Record 572;VAR UseDueDate@1005 : Date;VAR UseInterestRate@1007 : Decimal;VAR UseCalcDate@1008 : Date;VAR CumAmount@1010 : Decimal);
    VAR
      LineSpacing@1001 : Integer;
      NextLineNo@1002 : Integer;
      UseInterestPeriod@1004 : Integer;
      CurrInterestRateStartDate@1006 : Date;
    BEGIN
      ExtraReminderLine.RESET;
      ExtraReminderLine.SETRANGE("Reminder No.","Reminder No.");
      ExtraReminderLine := Rec;
      IF ExtraReminderLine.FIND('>') THEN BEGIN
        LineSpacing := (ExtraReminderLine."Line No." - "Line No.") DIV
          (1 + NrOfLinesToInsert);
        IF LineSpacing = 0 THEN
          ERROR(NotEnoughSpaceToInsertErr);
      END ELSE
        LineSpacing := 10000;
      NextLineNo := "Line No." + LineSpacing;
      FinanceChargeInterestRate.INIT;
      FinanceChargeInterestRate.SETRANGE("Fin. Charge Terms Code",ReminderHeader."Fin. Charge Terms Code");
      FinanceChargeInterestRate."Fin. Charge Terms Code" := ReminderHeader."Fin. Charge Terms Code";
      FinanceChargeInterestRate."Start Date" := CALCDATE('<+1D>',CustLedgEntry."Due Date");
      IF FinanceChargeInterestRate.FIND('=<') THEN
        REPEAT
          FinanceChargeInterestRate.TESTFIELD("Interest Period (Days)");
          UseInterestPeriod := FinanceChargeInterestRate."Interest Period (Days)";
          UseDueDate := CALCDATE('<-1D>',FinanceChargeInterestRate."Start Date");
          CurrInterestRateStartDate := FinanceChargeInterestRate."Start Date";
          UseInterestRate := FinanceChargeInterestRate."Interest Rate";
          IF FinanceChargeInterestRate.NEXT <> 0 THEN BEGIN
            IF FinanceChargeInterestRate."Start Date" <= ReminderHeader."Document Date" THEN
              UseCalcDate := CALCDATE('<-1D>',FinanceChargeInterestRate."Start Date")
            ELSE
              UseCalcDate := ReminderHeader."Document Date";
          END ELSE
            UseCalcDate := ReminderHeader."Document Date";
          ExtraReminderLine := Rec;
          ExtraReminderLine."Line No." := NextLineNo;
          ExtraReminderLine."Due Date" := CALCDATE('<+1D>',InterestCalcDate);
          IF CurrInterestRateStartDate > ExtraReminderLine."Due Date" THEN
            ExtraReminderLine."Due Date" := CurrInterestRateStartDate;
          ExtraReminderLine."Interest Rate" := UseInterestRate;
          CumulateDetailedEntries(ExtraReminderLine.Amount,UseDueDate,UseCalcDate,UseInterestRate,UseInterestPeriod);
          IF ExtraReminderLine.Amount <> 0 THEN BEGIN
            CumAmount := CumAmount + ExtraReminderLine.Amount;
            ExtraReminderLine."Detailed Interest Rates Entry" := TRUE;
            ExtraReminderLine.INSERT;
            NextLineNo := ExtraReminderLine."Line No." + LineSpacing;
          END;
          NrOfLinesToInsert := NrOfLinesToInsert - 1;
        UNTIL NrOfLinesToInsert = 0;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyFromCustLedgEntry@8(VAR ReminderLine@1000 : Record 296;CustLedgerEntry@1001 : Record 21);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCalcFinChrg@13(VAR ReminderLine@1000 : Record 296;VAR ReminderHeader@1001 : Record 295);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSetCustLedgEntryView@16(ReminderHeader@1001 : Record 295;ReminderLine@1002 : Record 296;VAR CustLedgEntry@1000 : Record 21);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCalcFinChrg@10(VAR ReminderLine@1000 : Record 296;VAR ReminderHeader@1001 : Record 295;VAR IsHandled@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeLookupDocNo@15(VAR ReminderLine@1000 : Record 296;VAR IsHandled@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeValidateDocumentNo@14(VAR ReminderLine@1000 : Record 296;VAR xReminderLine@1001 : Record 296;CurrentFieldNo@1002 : Integer;VAR IsHandled@1003 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeValidateEntryNo@11(VAR ReminderLine@1000 : Record 296;VAR xReminderLine@1001 : Record 296;CurrentFieldNo@1002 : Integer;VAR IsHandled@1003 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnValidateVATProdPostingGroupOnAfterVATPostingSetupGet@18(VAR VATPostingSetup@1000 : Record 325);
    BEGIN
    END;

    BEGIN
    {
      190423 ORANGO.PR (RFC-1187) - Exclude ROT and RUT from Remaining amount
    }
    END.
  }
}

