OBJECT Codeunit 2142 O365 Template Management
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [External]
    PROCEDURE GetDefaultVATBusinessPostingGroup@5() : Code[20];
    VAR
      O365SalesInitialSetup@1001 : Record 2110;
      ConfigTemplateLine@1000 : Record 8619;
      VATBusinessPostingGroup@1003 : Record 323;
      DummyCustomer@1002 : Record 18;
    BEGIN
      IF NOT O365SalesInitialSetup.GET THEN
        EXIT;

      IF NOT ConfigTemplateLine.GetLine(ConfigTemplateLine,O365SalesInitialSetup."Default Customer Template",
           DummyCustomer.FIELDNO("VAT Bus. Posting Group"))
      THEN
        EXIT;

      IF NOT VATBusinessPostingGroup.GET(ConfigTemplateLine."Default Value") THEN
        EXIT;

      EXIT(VATBusinessPostingGroup.Code);
    END;

    [External]
    PROCEDURE GetDefaultVATProdPostingGroup@4() : Code[20];
    VAR
      O365SalesInitialSetup@1001 : Record 2110;
      ConfigTemplateLine@1000 : Record 8619;
      VATProductPostingGroup@1003 : Record 324;
      DummyItem@1002 : Record 27;
    BEGIN
      IF NOT O365SalesInitialSetup.GET THEN
        EXIT;

      IF NOT ConfigTemplateLine.GetLine(ConfigTemplateLine,O365SalesInitialSetup."Default Item Template",
           DummyItem.FIELDNO("VAT Prod. Posting Group"))
      THEN
        EXIT;

      IF NOT VATProductPostingGroup.GET(ConfigTemplateLine."Default Value") THEN
        EXIT;

      EXIT(VATProductPostingGroup.Code);
    END;

    [External]
    PROCEDURE SetDefaultVATProdPostingGroup@45(VATProdPostingGroupCode@1005 : Code[20]);
    VAR
      O365SalesInitialSetup@1001 : Record 2110;
      DummyItem@1002 : Record 27;
      ConfigTemplateManagement@1004 : Codeunit 8612;
    BEGIN
      IF NOT O365SalesInitialSetup.GET THEN
        EXIT;

      ConfigTemplateManagement.ReplaceDefaultValueForAllTemplates(
        DATABASE::Item,DummyItem.FIELDNO("VAT Prod. Posting Group"),VATProdPostingGroupCode);
    END;

    [External]
    PROCEDURE GetDefaultBaseUnitOfMeasure@3() : Code[10];
    VAR
      O365SalesInitialSetup@1001 : Record 2110;
      ConfigTemplateLine@1000 : Record 8619;
      UnitOfMeasure@1003 : Record 204;
      DummyItem@1002 : Record 27;
    BEGIN
      IF NOT O365SalesInitialSetup.GET THEN
        EXIT;

      ConfigTemplateLine.SETRANGE("Data Template Code",O365SalesInitialSetup."Default Item Template");
      ConfigTemplateLine.SETRANGE("Field ID",DummyItem.FIELDNO("Base Unit of Measure"));
      IF NOT ConfigTemplateLine.FINDFIRST THEN
        EXIT;

      IF NOT UnitOfMeasure.GET(ConfigTemplateLine."Default Value") THEN
        EXIT;

      EXIT(UnitOfMeasure.Code);
    END;

    BEGIN
    END.
  }
}

