OBJECT Table 6085630 Approval User Group
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.4.50;
  }
  PROPERTIES
  {
    DataCaptionFields=Code,Name;
    OnInsert=BEGIN
               TESTFIELD(Code);
             END;

    OnDelete=VAR
               ApprovalUserGroupMember@1160040000 : Record 6085631;
               ApprovalUserGroupPermission@1160040001 : Record 6085632;
             BEGIN
               ApprovalUserGroupMember.SETRANGE("Approval User Group Code",Code);
               IF NOT ApprovalUserGroupMember.ISEMPTY THEN
                 ERROR(Text000,TABLECAPTION);

               ApprovalUserGroupPermission.SETRANGE("Approval User Group Code",Code);
               ApprovalUserGroupPermission.DELETEALL(TRUE);
             END;

    CaptionML=[DEU=Genehmigungsbenutzergruppe;
               ENU=Approval User Group;
               NLD=Goedkeuringsgebruikersgroep];
    LookupPageID=Page6085651;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;CaptionML=[DEU=Code;
                                                              ENU=Code;
                                                              NLD=Code;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Text50        ;CaptionML=[DEU=Name;
                                                              ENU=Name;
                                                              NLD=Naam;
                                                              SVE=Namn] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1160040000 : TextConst 'DEU=Sie k”nnen dieses %1 nicht l”schen, weil es Mitglieder hat.;ENU=You cannot delete this %1 because it has members.;NLD=U kunt deze %1 niet verwijderen omdat het leden heeft.';
      CurrCompanyName@1160040002 : Text[30];

    PROCEDURE SetCurrCompanyNameTbl@1160040019(NewCompanyName@1160040000 : Text[30]);
    BEGIN
      CurrCompanyName := NewCompanyName;
    END;

    PROCEDURE GetCurrCompanyNameTbl@1160040024() : Text[30];
    BEGIN
      IF CurrCompanyName = '' THEN
        EXIT(COMPANYNAME)
      ELSE
        EXIT(CurrCompanyName);
    END;

    PROCEDURE IsUserMember@1160040000(UserID@1160040000 : Code[50]) : Boolean;
    VAR
      ApprovalUserGroupMember@1160040001 : Record 6085631;
    BEGIN
      ApprovalUserGroupMember.CHANGECOMPANY(GetCurrCompanyNameTbl);
      ApprovalUserGroupMember.SETRANGE("Approval User Group Code",Code);
      ApprovalUserGroupMember.SETRANGE("Continia User ID",UserID);
      EXIT(NOT ApprovalUserGroupMember.ISEMPTY);
    END;

    PROCEDURE SetUserGroupMembership@1160040001(UserID@1160040000 : Code[50];NewUserGroupMembership@1160040001 : Boolean);
    VAR
      ApprovalUserGroupMember@1160040002 : Record 6085631;
    BEGIN
      ApprovalUserGroupMember.CHANGECOMPANY(GetCurrCompanyNameTbl);
      ApprovalUserGroupMember.SetCurrCompanyNameTbl(GetCurrCompanyNameTbl);

      IF NewUserGroupMembership THEN BEGIN
        IF NOT ApprovalUserGroupMember.GET(Code,UserID) THEN BEGIN
          ApprovalUserGroupMember.INIT;
          ApprovalUserGroupMember."Approval User Group Code" := Code;
          ApprovalUserGroupMember."Continia User ID" := UserID;
          ApprovalUserGroupMember.INSERT(TRUE);
        END;
      END ELSE BEGIN
        IF ApprovalUserGroupMember.GET(Code,UserID) THEN
          ApprovalUserGroupMember.DELETE(TRUE);
      END;
    END;

    BEGIN
    END.
  }
}

