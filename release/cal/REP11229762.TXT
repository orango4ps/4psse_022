OBJECT Report 11229762 Export Sales Inv. eVerb. 2.1
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 112=m;
    CaptionML=[DEU=Export Verkaufsrechnungen eVerbinding 2.1;
               ENU=Export Sales Invoices eVerbinding 2.1;
               NLD=Export verkoopfacturen eVerbinding 2.1];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  Window.OPEN(Text000);
                END;

    OnPostReport=BEGIN
                   IF (NumOfExports <> 0) THEN
                     MESSAGE(Text001, NumOfExports)
                   ELSE
                     MESSAGE(Text002);
                 END;

  }
  DATASET
  {
    { 5581;    ;DataItem;                    ;
               DataItemTable=Table112;
               OnPreDataItem=BEGIN
                               IF NOT IncludeSentInvoices THEN BEGIN
                                 FILTERGROUP(10);
                                 SETFILTER("Exported on", '%1', 0D);
                                 FILTERGROUP(0);
                               END;

                               NumOfInvoices := COUNT;
                             END;

               OnAfterGetRecord=VAR
                                  ElectronicDocumentFormat@1100528502 : Record 61;
                                  DummySalesHeader@1100528503 : Record 36;
                                  SalesInvHeader@1100525002 : Record 112;
                                  ExportSalesInveVerb21@1100528500 : Codeunit 11229762;
                                  eVerbindingConnectionMgt@1100528501 : Codeunit 11229765;
                                BEGIN
                                  NumOfRec := NumOfRec + 1;
                                  Window.UPDATE(1,100 * ROUND(NumOfRec / NumOfInvoices * 100,1));

                                  SalesInvHeader := "Sales Invoice Header";
                                  SalesInvHeader.SETRECFILTER;
                                  SalesInvHeader.TESTFIELD("E-Mail (Invoices)");

                                  ExportSalesInveVerb21.RUN(SalesInvHeader);
                                  eVerbindingConnectionMgt.SendDocument(
                                    ExportSalesInveVerb21.GetServerFilePath,
                                    ElectronicDocumentFormat.GetAttachmentFileName("No.",FORMAT(DummySalesHeader."Document Type"::Invoice),''),
                                    eVerbindingConnectionMgt.GetRecipient("Bill-to Customer No."),
                                    SalesInvHeader."E-Mail (Invoices)");

                                  "Exported on" := TODAY;
                                  "Invoice Exported by XML" := TRUE;
                                  NumOfExports := NumOfExports + 1;

                                  MODIFY;
                                END;

               ReqFilterFields=No.,Exported on }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1100525004;2;Field  ;
                  CaptionML=[DEU=Einschliesslich bereits versendete Rechnungen;
                             ENU=Include Already Sent Invoices;
                             NLD=Inclusief reeds verstuurde facturen;
                             NOR=Inkluder allerede sendte fakturaer;
                             SVE=Inklusive redan skickade fakturor];
                  SourceExpr=IncludeSentInvoices }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Window@1100525008 : Dialog;
      IncludeSentInvoices@1100525000 : Boolean;
      Text000@1100525001 : TextConst 'DEU=Rechnungen      @1@@@@@@@@\;ENU=Invoices            @1@@@@@@@@\;NLD=Facturen           @1@@@@@@@@\;NOR=Fakturaer            @1@@@@@@@@\;SVE=Fakturor            @1@@@@@@@@\';
      Text001@1100525002 : TextConst 'DEU=%1 Rechnungen wurden exportiert.;ENU=%1 Invoices are exported.;NLD=%1 facturen zijn ge‰xporteerd.;NOR=%1 fakturaer er eksportert.;SVE=%1 fakturor har exporterats.';
      Text002@1100525003 : TextConst 'DEU=Nichts exportiert;ENU=Nothing exported;NLD=Niets ge‰xporteerd;NOR=Ingenting er eksportert;SVE=Inget har exporterats';
      NumOfInvoices@1100525004 : Integer;
      NumOfRec@1100525006 : Integer;
      NumOfExports@1100525005 : Integer;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

