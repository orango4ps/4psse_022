OBJECT Codeunit 12013587 ExflowJnlManagement
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF400000;
  }
  PROPERTIES
  {
    Permissions=TableData 12013589=imd,
                TableData 12013590=imd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=%1 journal;SVE=%1 journal';
      Text004@1004 : TextConst 'ENU=DEFAULT;SVE=STANDARD';
      Text005@1005 : TextConst 'ENU=Default Journal;SVE=Standardjournal';
      OpenFromBatch@1007 : Boolean;
      Text006@1100285000 : TextConst 'ENU=No journal selected.;SVE=Ingen journal vald.';

    PROCEDURE TemplateSelection@1(FormID@1100285000 : Integer;VAR ExfPurchDoc@1004 : Record 12013587;VAR JnlSelected@1003 : Boolean);
    VAR
      ExfJnlTemplate@1002 : Record 12013589;
    BEGIN
      JnlSelected := TRUE;

      ExfJnlTemplate.RESET;
      ExfJnlTemplate.SETRANGE("Form ID",FormID);

      CASE ExfJnlTemplate.COUNT OF
        0:
          BEGIN
            ExfJnlTemplate.INIT;
            ExfJnlTemplate.Name := Text004;
            ExfJnlTemplate.Description := STRSUBSTNO(Text001,Text004);
            IF FormID = 0 THEN
              ExfJnlTemplate."Form ID" := PAGE::"ExFlow Purch. Doc. Worksheet"
            ELSE
              ExfJnlTemplate."Form ID" := FormID;

            ExfJnlTemplate.INSERT;
            COMMIT;
          END;
        1:
          ExfJnlTemplate.FINDFIRST;
        ELSE
          JnlSelected := PAGE.RUNMODAL(0,ExfJnlTemplate) = ACTION::LookupOK;
      END;
      IF JnlSelected THEN BEGIN
        ExfPurchDoc.FILTERGROUP := 2;
        ExfPurchDoc.SETRANGE("Journal Template Name",ExfJnlTemplate.Name);
        ExfPurchDoc.FILTERGROUP := 0;
        IF ExfPurchDoc.FINDFIRST THEN;

        IF OpenFromBatch THEN BEGIN
          ExfPurchDoc."Journal Template Name" := '';
          PAGE.RUN(ExfJnlTemplate."Form ID",ExfPurchDoc);
        END;
      END;
    END;

    PROCEDURE TemplateSelectionFromBatch@9(VAR ExfJnlBatch@1000 : Record 12013590);
    VAR
      ExfPurchDoc@1002 : Record 12013587;
      ExfJnlTemplate@1003 : Record 12013589;
    BEGIN
      OpenFromBatch := TRUE;
      ExfJnlTemplate.GET(ExfJnlBatch."Journal Template Name");
      ExfJnlTemplate.TESTFIELD("Form ID");
      ExfJnlBatch.TESTFIELD(Name);

      ExfPurchDoc.FILTERGROUP := 2;
      ExfPurchDoc.SETRANGE("Journal Template Name",ExfJnlTemplate.Name);
      ExfPurchDoc.FILTERGROUP := 0;
      IF ExfPurchDoc.FINDFIRST THEN;

      ExfPurchDoc."Journal Template Name" := '';
      ExfPurchDoc."Journal Batch Name" := ExfJnlBatch.Name;
      PAGE.RUN(ExfJnlTemplate."Form ID",ExfPurchDoc);
    END;

    PROCEDURE OpenJnl@2(VAR CurrentJnlBatchName@1000 : Code[10];VAR ExfPurchDoc@1001 : Record 12013587);
    BEGIN
      CheckTemplateName(ExfPurchDoc.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName);
      ExfPurchDoc.FILTERGROUP := 2;
      ExfPurchDoc.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      ExfPurchDoc.FILTERGROUP := 0;
      IF ExfPurchDoc.FINDFIRST THEN;
    END;

    PROCEDURE OpenJnlBatch@10(VAR ExfJnlBatch@1000 : Record 12013590);
    VAR
      ExfJnlTemplate@1002 : Record 12013589;
      ExfPurchDoc@1004 : Record 12013587;
      JnlSelected@1003 : Boolean;
    BEGIN
      IF ExfJnlBatch.GETFILTER("Journal Template Name") <> '' THEN
        EXIT;

      ExfJnlBatch.FILTERGROUP(2);
      IF ExfJnlBatch.GETFILTER("Journal Template Name") <> '' THEN BEGIN
        ExfJnlBatch.FILTERGROUP(0);
        EXIT;
      END;
      ExfJnlBatch.FILTERGROUP(0);

      IF NOT ExfJnlBatch.FIND('-') THEN BEGIN
        IF NOT ExfJnlTemplate.FINDFIRST THEN
          TemplateSelection(0,ExfPurchDoc,JnlSelected);
        IF ExfJnlTemplate.FINDFIRST THEN
          CheckTemplateName(ExfJnlTemplate.Name,ExfJnlBatch.Name);
      END;
      ExfJnlBatch.FIND('-');
      JnlSelected := TRUE;
      IF ExfJnlBatch.GETFILTER("Journal Template Name") <> '' THEN
        ExfJnlTemplate.SETRANGE(Name,ExfJnlBatch.GETFILTER("Journal Template Name"));
      CASE ExfJnlTemplate.COUNT OF
        1:
          ExfJnlTemplate.FINDFIRST;
        ELSE
          JnlSelected := PAGE.RUNMODAL(0,ExfJnlTemplate) = ACTION::LookupOK;
      END;
      IF NOT JnlSelected THEN
        ERROR(Text006);

      ExfJnlBatch.FILTERGROUP(0);
      ExfJnlBatch.SETRANGE("Journal Template Name",ExfJnlTemplate.Name);
      ExfJnlBatch.FILTERGROUP(2);
    END;

    PROCEDURE CheckTemplateName@3(CurrentJnlTemplateName@1000 : Code[10];VAR CurrentJnlBatchName@1001 : Code[10]);
    VAR
      ExfJnlBatch@1002 : Record 12013590;
    BEGIN
      ExfJnlBatch.SETRANGE("Journal Template Name",CurrentJnlTemplateName);
      IF NOT ExfJnlBatch.GET(CurrentJnlTemplateName,CurrentJnlBatchName) THEN BEGIN
        IF NOT ExfJnlBatch.FINDFIRST THEN BEGIN
          ExfJnlBatch.INIT;
          ExfJnlBatch."Journal Template Name" := CurrentJnlTemplateName;
          ExfJnlBatch.SetupNewBatch;
          ExfJnlBatch.Name := Text004;
          ExfJnlBatch.Description := Text005;
          ExfJnlBatch.INSERT(TRUE);
          COMMIT;
        END;
        CurrentJnlBatchName := ExfJnlBatch.Name
      END;
    END;

    PROCEDURE CheckName@4(CurrentJnlBatchName@1000 : Code[10];VAR ExfPurchDoc@1001 : Record 12013587);
    VAR
      ExfJnlBatch@1002 : Record 12013590;
    BEGIN
      ExfJnlBatch.GET(ExfPurchDoc.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName);
    END;

    PROCEDURE SetName@5(CurrentJnlBatchName@1000 : Code[10];VAR ExfPurchDoc@1001 : Record 12013587);
    BEGIN
      ExfPurchDoc.FILTERGROUP := 2;
      ExfPurchDoc.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      ExfPurchDoc.FILTERGROUP := 0;
      IF ExfPurchDoc.FINDFIRST THEN;
    END;

    PROCEDURE LookupName@6(VAR CurrentJnlBatchName@1000 : Code[10];VAR ExfPurchDoc@1001 : Record 12013587);
    VAR
      ExfJnlBatch@1002 : Record 12013590;
    BEGIN
      COMMIT;
      ExfJnlBatch."Journal Template Name" := ExfPurchDoc.GETRANGEMAX("Journal Template Name");
      ExfJnlBatch.Name := ExfPurchDoc.GETRANGEMAX("Journal Batch Name");
      ExfJnlBatch.FILTERGROUP(2);
      ExfJnlBatch.SETRANGE("Journal Template Name",ExfJnlBatch."Journal Template Name");
      ExfJnlBatch.FILTERGROUP(0);
      IF PAGE.RUNMODAL(0,ExfJnlBatch) = ACTION::LookupOK THEN BEGIN
        CurrentJnlBatchName := ExfJnlBatch.Name;
        SetName(CurrentJnlBatchName,ExfPurchDoc);
      END;
    END;

    BEGIN
    END.
  }
}

