OBJECT XMLport 11012343 Export ICM Orders
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Export ICM Orders;
               SVE=Exportera ICM-order];
    Direction=Export;
    DefaultFieldsValidation=No;
    PreserveWhiteSpace=Yes;
    TextEncoding=WINDOWS;
    OnPreXMLport=BEGIN
                   VendorRec.GET(gVendor);
                   IF gTrialOrder THEN
                     FileName := VendorRec."Pathname Purchase Exchange" + VendorRec."Directory ICK" + gDocument + '.ick'
                   ELSE
                     FileName := VendorRec."Pathname Purchase Exchange" + VendorRec."Directory ICM" + gDocument + '.icm';

                   currXMLport.FILENAME(FileName);
                 END;

    Format=Variable Text;
    FieldDelimiter=<None>;
    FieldSeparator=<NewLine>;
    RecordSeparator=<NewLine>;
    TableSeparator=<NewLine>;
    UseRequestPage=No;
  }
  ELEMENTS
  {
    { [{1D0A0D36-3AB8-4266-9E41-9CA24275B91F}];  ;Root                ;Element ;Text     }

    { [{C213DE45-8477-4774-BEB0-CFE83EA81895}];1 ;PurchaseHeader      ;Element ;Table   ;
                                                  SourceTable=Table38;
                                                  Export::OnPreXMLItem=BEGIN
                                                                         "Purchase Header".SETRANGE("Document Type","Purchase Header"."Document Type"::Order);
                                                                         "Purchase Header".SETFILTER("No.",gDocument);
                                                                       END;

                                                  Export::OnAfterGetRecord=BEGIN
                                                                             RecRef.GETTABLE("Purchase Header");
                                                                             XMLportStatusDialogMgt.ShowExportStatus(RecRef);

                                                                             UpdateICMStatus;
                                                                           END;
                                                                            }

    { [{24E10207-DD9E-43E3-82E5-ABA7115C0D73}];2 ;InlogCode           ;Element ;Text     }

    { [{722C1516-D42C-44DB-8493-736316DB773E}];2 ;No                  ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=Purchase Header::No. }

    { [{01D60C62-5FFC-48E1-A91B-62271147B9C5}];2 ;ExtraRefTxt         ;Element ;Text     }

    { [{B0778A1C-5D52-466F-8FF4-9998752090D2}];2 ;RefOrder            ;Element ;Text     }

    { [{58986C01-558B-47B6-B8B1-D671AA26C628}];2 ;CustCode            ;Element ;Text     }

    { [{DA0E91A6-6330-4273-8E96-E16A809B7C1C}];2 ;IcmProcedureTxt     ;Element ;Text     }

    { [{178F8897-716D-4130-903D-418CC64778B9}];2 ;AddressCode         ;Element ;Text     }

    { [{CB4A6B97-DD0D-4923-95B2-CE805EE4D41D}];2 ;VarName1Txt         ;Element ;Text     }

    { [{412B3703-ACD9-4112-AA95-5C28B258B775}];2 ;VarName2Txt         ;Element ;Text     }

    { [{9BA4FEE2-74B8-4DA0-A258-4CD26BC00DD8}];2 ;VarStreetTxt        ;Element ;Text     }

    { [{F82F4FE3-32BB-4BE1-BC35-CFD76E0B7E22}];2 ;VarCityTxt          ;Element ;Text     }

    { [{78A30ADB-8B9B-4C1B-BA1B-7046B346A7A1}];2 ;VarPostCode         ;Element ;Text     }

    { [{4D7017F0-41FA-475A-9121-8BF1FC0D8F81}];2 ;VarHouseNumber      ;Element ;Text     }

    { [{F31B4A7E-F4BB-41F1-8FBE-26F589D2F607}];2 ;VarPhoneTxt         ;Element ;Text     }

    { [{8F973849-539D-4B86-B918-1142BDBE7FBD}];2 ;ShipmentDateTxt     ;Element ;Text     }

    { [{EECAA963-7F0A-4637-8A99-B7D4765F3C20}];2 ;ShipmMethodCde      ;Element ;Text     }

    { [{32C88E5E-79AF-4550-BA57-A4EC7FFC2B1F}];2 ;ShipmInstruct1Txt   ;Element ;Text     }

    { [{861CAC31-D81B-4596-8010-F60DAB2B7B03}];2 ;ShipmInstruct2Txt   ;Element ;Text     }

    { [{B013BA28-512C-4C5E-A694-E5936664A2DB}];2 ;OrderLinesTxt       ;Element ;Text     }

    { [{2AA687D9-D5B2-4399-8BD7-952B761B1FD4}];1 ;PurchaseLine        ;Element ;Table   ;
                                                  SourceTable=Table39;
                                                  Export::OnPreXMLItem=BEGIN
                                                                         "Purchase Line".SETRANGE("Document Type","Purchase Line"."Document Type"::Order);
                                                                         "Purchase Line".SETFILTER("Document No.",gDocument);
                                                                       END;

                                                  Export::OnAfterGetRecord=VAR
                                                                             CurrItem@1100528902 : Text[30];
                                                                             CurrQuan@1100528901 : Decimal;
                                                                             PosDecPoint@1100528900 : Integer;
                                                                           BEGIN
                                                                             //db, 30-08-12: 31238 (review function)
                                                                             WITH "Purchase Line" DO BEGIN
                                                                               RecRef.GETTABLE("Purchase Line");
                                                                               XMLportStatusDialogMgt.ShowExportStatus(RecRef);

                                                                               //Regarding text lines, these will be skipped
                                                                               IF Quantity = 0 THEN
                                                                                 currXMLport.SKIP;

                                                                               IF "Trade Item" <> '' THEN
                                                                                 CurrItem := "Trade Item"
                                                                               ELSE
                                                                                 IF "Vendor Item No." <> '' THEN
                                                                                   CurrItem := "Vendor Item No."
                                                                                 ELSE
                                                                                   currXMLport.SKIP;

                                                                               ItemCode := COPYSTR(CurrItem, 1, MAXSTRLEN(ItemCode));  //db, 07-03-17
                                                                               ItemDescTxt := COPYSTR(Description, 1, MAXSTRLEN(ItemDescTxt));

                                                                               IF "ICM Quantity" <> 0 THEN BEGIN
                                                                                 CurrQuan :=  "ICM Quantity";
                                                                                 ICMQuanBln := TRUE;
                                                                               END ELSE BEGIN
                                                                                 CurrQuan := Quantity;
                                                                                 ICMQuanBln := FALSE;
                                                                               END;

                                                                               ItemQuanTxt := STRSUBSTNO('%1',CurrQuan);
                                                                               ItemQuanTxt := DELCHR(ItemQuanTxt,'=','.');  //db, 21-11-12: C004026 (Remove dot for Thousands)
                                                                               IF CurrQuan MOD 1 <> 0 THEN
                                                                                 ItemQuanTxt := CONVERTSTR(ItemQuanTxt, ',', '.');

                                                                               IF STRLEN(ItemQuanTxt) < 7 THEN
                                                                                 ItemQuanTxt := PADSTR('',7 - STRLEN(ItemQuanTxt),'0') + ItemQuanTxt;

                                                                               IF NOT ICMQuanBln THEN
                                                                                 UnitCostDec := "Direct Unit Cost"
                                                                               ELSE
                                                                                 UnitCostDec := ("Direct Unit Cost" * Quantity) / "ICM Quantity";

                                                                               //SalesPriceTxt := STRSUBSTNO('%1', ROUND(UnitCostDec)); //C011921.o
                                                                               SalesPriceTxt := FORMAT(ROUND(UnitCostDec,0.01),12,'<Standard Format,4>'); //C011921.n
                                                                               SalesPriceTxt := DELCHR(SalesPriceTxt,'=','. '); //remove dots and trailing spaces
                                                                               SalesPriceTxt := CONVERTSTR(SalesPriceTxt, ',', '.');
                                                                               //db.sn, 25-02-13: C005835
                                                                               PosDecPoint := STRPOS(SalesPriceTxt, '.');
                                                                               IF PosDecPoint = 0 THEN
                                                                                 SalesPriceTxt := SalesPriceTxt + '.00'
                                                                               ELSE
                                                                                 IF STRLEN(SalesPriceTxt) - PosDecPoint = 1 THEN
                                                                                   SalesPriceTxt := SalesPriceTxt + '0';
                                                                               //db.en, 25-02-13: C005835
                                                                               IF STRLEN(SalesPriceTxt) < 9 THEN
                                                                                 SalesPriceTxt := PADSTR('',9 - STRLEN(SalesPriceTxt),'0') + SalesPriceTxt;
                                                                             END; //Belongs to With statement.
                                                                           END;
                                                                            }

    { [{979E137B-0573-4B26-90B2-DE73BC8F2325}];2 ;ItemQuanTxt         ;Element ;Text     }

    { [{2EBA932E-AC79-4A64-8AE4-A4DA0F171876}];2 ;ItemCode            ;Element ;Text     }

    { [{AEF67644-AA75-4554-AAF3-E3EA8ADB03E2}];2 ;ItemDescTxt         ;Element ;Text     }

    { [{5712DEDC-F719-47C2-A43B-2D0063D2A376}];2 ;SalesPriceTxt       ;Element ;Text     }

    { [{CE320B53-9D4E-4165-8C76-C58915D3BF93}];1 ;Integer             ;Element ;Table   ;
                                                  SourceTable=Table2000000026;
                                                  Export::OnPreXMLItem=BEGIN
                                                                         Integer.SETRANGE(Number, 1);
                                                                       END;

                                                  Export::OnAfterGetRecord=BEGIN
                                                                             NewLineTxt := ''; //This is to make sure that the last characters of the
                                                                             //export file are CrLf (new line). In the Dataport Export ICM Orders this was done
                                                                             //by writing Cr and Lf to CurrFile, but this is not possible in an XMLport.
                                                                           END;
                                                                            }

    { [{2DDB070D-B582-4D51-928B-48C270522E6B}];2 ;NewLineTxt          ;Element ;Text     }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      VendorRec@1100525034 : Record 23;
      PurchLineRec@1100525033 : Record 39;
      ErrorRec@1100525032 : Record 11012051;
      RecRef@1100525001 : RecordRef;
      XMLportStatusDialogMgt@1100525002 : Codeunit 11130006;
      ProjectCode@1100525028 : Code[20];
      gDocument@1100525008 : Code[20];
      gVendor@1100525007 : Code[20];
      gTrialOrder@1100525006 : Boolean;
      gTruncateAddress@1100525005 : Boolean;
      ICMQuanBln@1100525004 : Boolean;
      UnitCostDec@1100525003 : Decimal;
      FileName@1100525000 : Text[1000];
      Text006@1100525024 : TextConst 'ENU=Post Code %1 must be max. 6 positions in ICM format for order %2;SVE=Postnumret %1 m†ste vara max. 6 positioner i ICM-format f”r order %2';
      Text007@1100525023 : TextConst 'ENU=No or to long house number specified for ship-to location for order %1;SVE=Husnumret i leveransadressen saknas eller s† „r det f”r l†ngt i order %1';
      Text008@1100525022 : TextConst 'ENU=More then 70 order lines for order %1, not allowed for ICM;SVE=Det „r inte till†tet att ha fler „n 70 orderrader f”r order %1 i ICM';
      Text009@1100525021 : TextConst 'ENU=Quantity %1 is smaller then 0, not allowed in ICM for order %2;SVE=Antalet %1 „r mindre „n 0, inte till†tet i ICM f”r order %2';
      Text011@1100525019 : TextConst 'ENU=Vendor Item must be filled in ICM for order %1;SVE=Leverant”rsartikel m†ste anges i ICM f”r order %1';
      Text012@1100525018 : TextConst 'ENU=Expected Receipt date for order %1 may not be in the past';
      Text013@1100525017 : TextConst 'ENU=Login Code not present for Vendor %1;SVE=Inloggningskod finns inte f”r leverant”r %1';
      Text014@1100525016 : TextConst 'ENU=Vendor %1 not present;SVE=Leverant”r %1 finns inte';
      Text015@1100525015 : TextConst 'ENU=Our ICM customer number not present for Vendor %1;SVE=V†rt ICM-kundnr finns inte f”r leverant”r %1';
      Text018@1100525012 : TextConst 'ENU=Expected delivery date not present for Order %1;SVE=F”rv„ntat leveransdatum finns inte f”r order %1';
      Text019@1100525011 : TextConst 'ENU=Our ICM customer number by Project (Location) differs on line and header in orderno. %1;SVE=V†rt ICM-kundnr efter projekt (st„lle) skiljer sig †t i ordernumrets rad och rubrik. %1';
      Text020@1100525009 : TextConst 'ENU=Trade Item %1 of Vendor %2 is expired for order %3';

    PROCEDURE Alphanumeric@6(Text@11012001 : Text[250]) Res@11012000 : Boolean;
    BEGIN
      EXIT(DELCHR(Text,'=',' 0123456789')<>'');
    END;

    PROCEDURE SetSelection@1(iDocument@11012000 : Code[20];iVendor@11012001 : Code[20];iTrialOrder@1100525000 : Boolean;iTruncateAddress@1100525001 : Boolean);
    BEGIN
      gDocument := iDocument;
      gVendor := iVendor;
      gTrialOrder := iTrialOrder;
      gTruncateAddress := iTruncateAddress;
    END;

    PROCEDURE StoreMessage@5(lvDesc@11012000 : Text[50];lvMess@11012001 : Text[250];lvDoc@1210190000 : Code[20]);
    VAR
      lvError@11012002 : Integer;
    BEGIN
      WITH ErrorRec DO BEGIN
        SETRANGE("User ID", USERID);
        SETRANGE("Source Type", "Source Type"::"ICM Export");
        IF FIND('+') THEN
          lvError := "Line No." + 1
        ELSE
          lvError := 1;

        INIT;
        "User ID" := USERID;
        "Source Type" := "Source Type"::"ICM Export";
        "Line No." := lvError;
        "Document No." := lvDoc;
        "Error message" := lvMess;
        Description := lvDesc;
        INSERT;
      END;
    END;

    PROCEDURE CheckPurchHeader@1210190000(VAR lPurchHeaderRec@1210190000 : Record 38) HeaderOkay@1210190001 : Boolean;
    VAR
      ProjLocRec@1210190002 : Record 11012011;
      lvICMCustomerRec@1100485000 : Record 11012129;
      lvICMDeliverRec@1100485001 : Record 11012130;
      lvNumLines@1100485002 : Integer;
      MonthInt@1100525004 : Integer;
      DayInt@1100525003 : Integer;
      YearInt@1100525002 : Integer;
      DayCde@1100525001 : Code[2];
      MonthCde@1100525000 : Code[2];
      Position@1100525006 : Integer;
      Finished@1100525005 : Boolean;
      ICMCustCodeFoundOnProjectLevel@1100528700 : Boolean;
      ServOrderRec@1100525009 : Record 11012823;
      ServOrderLocRec@1100525008 : Record 11071688;
      PhoneTxt@1100525007 : Text[30];
    BEGIN
      //db, 01-07-04: called from report 11012308 (Create ICM Files)

      WITH lPurchHeaderRec DO BEGIN
        IF NOT VendorRec.GET("Buy-from Vendor No.") THEN
          StoreMessage('', STRSUBSTNO(Text014, "Buy-from Vendor No."), "No.");

        IF VendorRec."ICM Login Code" = '' THEN
          StoreMessage('', STRSUBSTNO(Text013, VendorRec."No."), "No.")
        ELSE
          InlogCode := VendorRec."ICM Login Code";

      //IF "Expected Receipt Date" <= TODAY THEN //C014883.o
        IF "Expected Receipt Date" < TODAY THEN //C014883.n
          StoreMessage('', STRSUBSTNO(Text012, "No."), "No.");

        //db.sn, 10-12-09: M16598
        RefOrder := GetRefOrderNo(lPurchHeaderRec); //C027875.n
        ProjectCode := "Job No.";
        //db.en, 10-12-09: M16598

        //SR.sn, 01-12-2014: C014023
        IF "Ship-to Address" <> '' THEN
          "Ship-to Address" := DELCHR("Ship-to Address",'>');
        //SR.en, 01-12-2014: C014023

        IF VendorRec."ICM Customer No." = '' THEN
          StoreMessage('', STRSUBSTNO(Text015, VendorRec."No."), "No.");

        HandlePhoneICM(0, VarPhoneTxt);  //db, 26-02-10: M18007
        HandleShipmentInstruction(0, ShipmInstruct1Txt, ShipmInstruct2Txt);  //db, 22-10-09: M13984
        ExtraRefTxt := "Reference (ICM)";
        CustCode := VendorRec."ICM Customer No.";

        lvICMCustomerRec.RESET;
        lvICMCustomerRec.SETRANGE(Vendor, "Buy-from Vendor No.");
        lvICMCustomerRec.SETFILTER(Project, '%1', '');
        lvICMCustomerRec.SETFILTER("Global Dimension 1 Code", '%1', '');
        lvICMCustomerRec.SETFILTER("ICM Customer No.", '<>%1', '');
        IF lvICMCustomerRec.FIND('-') THEN
          CustCode := lvICMCustomerRec."ICM Customer No.";

        ICMCustCodeFoundOnProjectLevel := FALSE;
        IF ProjectCode <> '' THEN BEGIN
          lvICMCustomerRec.SETRANGE(Project, ProjectCode);  //db, 10-12-09
          IF (Deliver = Deliver::"Project Location") AND
             ("Ship-to Location" <> '') THEN BEGIN
            lvICMCustomerRec.SETRANGE("Project Location", "Ship-to Location");
            IF lvICMCustomerRec.FIND('-') THEN BEGIN
              CustCode := lvICMCustomerRec."ICM Customer No.";
              ICMCustCodeFoundOnProjectLevel := TRUE;
            END ELSE BEGIN
              lvICMCustomerRec.SETFILTER("Project Location", '%1', '');
              IF lvICMCustomerRec.FIND('-') THEN BEGIN
                CustCode := lvICMCustomerRec."ICM Customer No.";
                ICMCustCodeFoundOnProjectLevel := TRUE;
              END;
            END;
          END ELSE BEGIN
            lvICMCustomerRec.SETFILTER("Project Location", '%1', '');
            IF lvICMCustomerRec.FIND('-') THEN BEGIN
              CustCode := lvICMCustomerRec."ICM Customer No.";
              ICMCustCodeFoundOnProjectLevel := TRUE;
            END;
          END;
        END;

        IF (NOT ICMCustCodeFoundOnProjectLevel) AND (lPurchHeaderRec."Shortcut Dimension 1 Code" <> '') THEN BEGIN
          lvICMCustomerRec.RESET;
          lvICMCustomerRec.SETRANGE(Vendor, "Buy-from Vendor No.");
          lvICMCustomerRec.SETFILTER(Project, '%1', '');
          lvICMCustomerRec.SETFILTER("Global Dimension 1 Code", lPurchHeaderRec."Shortcut Dimension 1 Code");
          lvICMCustomerRec.SETFILTER("ICM Customer No.", '<>%1', '');
          IF lvICMCustomerRec.FINDFIRST THEN
            CustCode := lvICMCustomerRec."ICM Customer No.";
        END;

        //CustCode := PADSTR(CustCode,10,' '); C015324 16-05-14

        IF "ICM Procedure" = "ICM Procedure"::Send THEN
          IcmProcedureTxt := 'B'
        ELSE
          IcmProcedureTxt := 'A';

        AddressCode := '';

        IF (Deliver <> Deliver::"Default (Company Address)") AND
           ("Ship-to Location" <> '') THEN
        BEGIN
          lvICMDeliverRec.SETRANGE("ICM Vendor No.", "Buy-from Vendor No.");
          lvICMDeliverRec.SETRANGE(Deliver, Deliver);
          lvICMDeliverRec.SETRANGE("Ship To Location", "Ship-to Location");
          lvICMDeliverRec.SETFILTER("ICM Deliver Code", '<>%1', '');

          IF Deliver = Deliver::"Project Location" THEN
            lvICMDeliverRec.SETRANGE(Project, ProjectCode)  //db, 10-12-09
          ELSE
            lvICMDeliverRec.SETFILTER(Project, '%1', '');

          IF lvICMDeliverRec.FIND('-') THEN
            AddressCode := PADSTR('',3 - STRLEN(lvICMDeliverRec."ICM Deliver Code"),'0') + lvICMDeliverRec."ICM Deliver Code";
        END;

        IF AddressCode = '' THEN BEGIN
          AddressCode := '993';
          IF "Ship-to Name" = '' THEN
            StoreMessage('', MissingField(lPurchHeaderRec, FIELDNO("Ship-to Name")), "No.");
          IF "Ship-to Post Code" = '' THEN
            StoreMessage('', MissingField(lPurchHeaderRec, FIELDNO("Ship-to Post Code")), "No.");
          IF "Ship-to Address" = '' THEN
            StoreMessage('', MissingField(lPurchHeaderRec, FIELDNO("Ship-to Address")), "No.");
          IF "Ship-to City" = '' THEN
            StoreMessage('', MissingField(lPurchHeaderRec, FIELDNO("Ship-to City")), "No.");
          //db.sn, 16-12-08
          VarName1Txt := CheckFormat(lPurchHeaderRec,FIELDCAPTION("Ship-to Name"),"Ship-to Name",30,TRUE);
          VarName2Txt := CheckFormat(lPurchHeaderRec,FIELDCAPTION("Ship-to Contact"),"Ship-to Contact",30,TRUE);
          VarStreetTxt := CheckFormat(lPurchHeaderRec,FIELDCAPTION("Ship-to Address"),"Ship-to Address",30,TRUE);
          VarCityTxt := CheckFormat(lPurchHeaderRec,FIELDCAPTION("Ship-to City"),"Ship-to City",18,TRUE);
          //db.en, 16-12-08
          VarPostCode := DELCHR("Ship-to Post Code",'=',' ');
          IF STRLEN(VarPostCode) > 6 THEN
            StoreMessage('', STRSUBSTNO(Text006, VarPostCode, "No."), "No.");
          Position := STRLEN("Ship-to Address");
          Finished := FALSE;
          IF Deliver = Deliver::"Project Location" THEN BEGIN
            IF ProjLocRec.GET(ProjectCode, "Ship-to Location") THEN BEGIN
              PhoneTxt := DELCHR(ProjLocRec."Phone No.",'=',' -+');
              IF PhoneTxt <> '' THEN
                VarPhoneTxt := CheckFormat(lPurchHeaderRec,ProjLocRec.FIELDCAPTION("Phone No."),PhoneTxt,13,FALSE);
            END;
          END;
          IF Deliver = Deliver::"Service Location" THEN BEGIN  //db, 31-08-15: C025213
            IF NOT ServOrderRec.GET("Service Order No.") THEN ServOrderRec.INIT;
            IF ServOrderLocRec.GET("Service Order No.", "Ship-to Location") THEN BEGIN
              PhoneTxt := DELCHR(ServOrderLocRec."Phone No.",'=',' -+');
              IF PhoneTxt <> '' THEN
                VarPhoneTxt := CheckFormat(lPurchHeaderRec,ServOrderLocRec.FIELDCAPTION("Phone No."),PhoneTxt,13,FALSE);
            END;
          END;
          IF VarStreetTxt <> '' THEN BEGIN
            WHILE Finished = FALSE DO BEGIN
              IF COPYSTR(VarStreetTxt, Position - 1, 1) <> ' ' THEN BEGIN
                Position := Position - 1;
                IF Position = 1 THEN
                  Finished := TRUE;
              END ELSE BEGIN
                Finished := TRUE;
              END;
            END;
          END;
          IF (STRLEN(VarStreetTxt) - Position < 5) AND (Position > 1) THEN BEGIN
            VarHouseNumber := COPYSTR(VarStreetTxt, Position, STRLEN(VarStreetTxt) - (Position - 1));
            VarStreetTxt := COPYSTR(VarStreetTxt, 1, Position - 1);
          END ELSE BEGIN
            StoreMessage('', STRSUBSTNO(Text007, "No."), "No.");
          END;
        END;

        IF "Expected Receipt Date" = 0D THEN BEGIN
          StoreMessage('', STRSUBSTNO(Text018, "No."), "No.");
        END ELSE BEGIN
          DayInt := DATE2DMY("Expected Receipt Date", 1);
          IF DayInt < 10 THEN
            DayCde := STRSUBSTNO('0%1',DayInt)
          ELSE
            DayCde := STRSUBSTNO('%1',DayInt);
          MonthInt := DATE2DMY("Expected Receipt Date", 2);
          IF MonthInt < 10 THEN
            MonthCde := STRSUBSTNO('0%1', MonthInt)
          ELSE
            MonthCde := STRSUBSTNO('%1', MonthInt);
          YearInt := DATE2DMY("Expected Receipt Date", 3);
          ShipmentDateTxt := STRSUBSTNO('%1-%2-%3', DayCde, MonthCde, YearInt);
        END;

        PurchLineRec.SETRANGE("Document Type", "Document Type"::Order);
        PurchLineRec.SETRANGE("Document No.", "No.");
        PurchLineRec.SETRANGE("Removal Contribution",FALSE);  //db, 13-05-08: same filter as report 11012308
        //db.sn, 05-05-09: M15035 (evaluate Skip-conditions PurchLine)
        PurchLineRec.SETFILTER(Quantity, '<>%1', 0);
        PurchLineRec.SETFILTER("Trade Item", '<>%1', '');
        //db.en, 05-05-09: M15035
        lvNumLines := PurchLineRec.COUNT;
        IF lvNumLines > 999 THEN  //instead of 70 as determined by TU; field length is 3 positions
          StoreMessage('', STRSUBSTNO(Text008, "No."), "No.")
        ELSE
          IF lvNumLines < 10 THEN
            OrderLinesTxt := STRSUBSTNO('00%1', lvNumLines)
          ELSE
            IF lvNumLines < 100 THEN
              OrderLinesTxt := STRSUBSTNO('0%1', lvNumLines)
            ELSE
              OrderLinesTxt := STRSUBSTNO('%1', lvNumLines);
      END;

      WITH ErrorRec DO BEGIN
        SETRANGE("User ID", USERID);
        SETRANGE("Source Type", "Source Type"::"ICM Export");
        IF FIND('-') THEN
          EXIT(FALSE)
        ELSE
          EXIT(TRUE);
      END;
    END;

    LOCAL PROCEDURE GetRefOrderNo@1100528400(PurchaseHeader@1100528400 : Record 38) RefOrderNo : Text;
    BEGIN
      //C027875
      IF PurchaseHeader."Service Order No." <> '' THEN
        RefOrderNo := PurchaseHeader."Service Order No."
      ELSE
        RefOrderNo := PurchaseHeader."Job No.";
      IF STRLEN(RefOrderNo) > 10 THEN
        RefOrderNo := '';
    END;

    PROCEDURE CheckPurchLine@1210190001(lPurchLineRec@1210190000 : Record 39) LineOkay@1210190002 : Boolean;
    VAR
      lvICMCustomerRec@1100485000 : Record 11012129;
      Item@1100525000 : Record 27;
      TradeItem@1100525001 : Record 11012317;
    BEGIN
      //db, 01-07-04: called from report 11012308 (Create ICM Files)

      WITH lPurchLineRec DO BEGIN
        IF "ICM Quantity" < 0 THEN
          IF Quantity < 0 THEN
            StoreMessage('', STRSUBSTNO(Text009, Quantity, "Document No."), "Document No.");

        IF "ICM Quantity" <> 0 THEN
          ItemQuanTxt := STRSUBSTNO('%1', "ICM Quantity")
        ELSE
          ItemQuanTxt := STRSUBSTNO('%1', Quantity);

        //Remove dot for Thousands
        ItemQuanTxt := DELCHR(ItemQuanTxt,'=','.');

        //check if it concerns an integer/whole number based on the present of a comma
        //IF STRPOS(ItemQuanTxt, ',' ) <> 0 THEN  //db, 30-08-12: 31238
        //  StoreMessage('', STRSUBSTNO(Text010, Quantity, "Document No."), "Document No.");

        IF Quantity <> 0 THEN BEGIN
          IF "Trade Item" = '' THEN BEGIN
            IF "Vendor Item No." = '' THEN BEGIN //db, 30-08-12: 31238
              StoreMessage('', STRSUBSTNO(Text011, "Document No."), "Document No.");
            END ELSE BEGIN
              //C048827.sn
              IF Item.GET("Item No.") THEN BEGIN
                Item.CALCFIELDS("Item Vendor", "Vendor (Trade Item)");
                IF "Vendor Item No." = Item."Item Vendor" THEN
                  IF TradeItem.GET(Item."Vendor (Trade Item)", Item."Item Vendor") THEN
                    IF TradeItem.Expired THEN
                      StoreMessage('', STRSUBSTNO(Text020, TradeItem."Item Code", TradeItem.Vendor, "Document No."), "Document No.");
              END;
              //C048827.en
            END;
          END ELSE BEGIN
            //C048827.sn
            IF TradeItem.GET("Vendor (Trade Item)", "Trade Item") THEN
              IF TradeItem.Expired THEN
                StoreMessage('', STRSUBSTNO(Text020, TradeItem."Item Code", "Document No."), "Document No.");
            //C048827.en
          END;
        END;

        IF ProjectCode <> "Job No." THEN BEGIN
          lvICMCustomerRec.SETRANGE(Vendor, VendorRec."No.");
          lvICMCustomerRec.SETRANGE(Project, "Job No.");
          IF lvICMCustomerRec.FIND('-') THEN BEGIN
            //There are specific customers numbers per project, but does headerline "Klantnummer ICM" match?
            lvICMCustomerRec.SETRANGE("ICM Customer No.", CustCode);
            IF NOT (lvICMCustomerRec.FIND('-')) THEN
              StoreMessage('', STRSUBSTNO(Text019, "Document No."), "Document No.");
          END ELSE BEGIN
            //On the line no special customer number ICM, does this match the header?
            //IF CustCode <> VendorRec."ICM Customer No." THEN
            //  StoreMessage('', STRSUBSTNO(Text019, "Document No."), "Document No.");  //db, 05-08-09: M16060
          END;
        END;
      END;

      WITH ErrorRec DO BEGIN
        SETRANGE("User ID", USERID);
        SETRANGE("Source Type", "Source Type"::"ICM Export");
        IF FIND('-') THEN
          EXIT(FALSE)
        ELSE
          EXIT(TRUE);
      END;
    END;

    PROCEDURE UpdateICMStatus@1210190002();
    BEGIN
      IF gTrialOrder THEN EXIT;  //db, 22-12-08

      WITH "Purchase Header" DO BEGIN
        VALIDATE("ICM Status", "ICM Status"::Created);
        "ICM File Created on" := TODAY;
        MODIFY(TRUE);
      END;
    END;

    PROCEDURE CheckFormat@1100525003(iPurchHeaderRec@1100525004 : Record 38;iField@1100525002 : Text[250];iData@1100525000 : Text[250];iLength@1100525001 : Integer;iTruncateField@1100525005 : Boolean) Result : Text[250];
    VAR
      lvText000@1100525003 : TextConst 'ENU=%1 %2 exceeds maximum of %3 characters in ICM format for  %4 %5;SVE=%1 %2 ”verskrider maximalt %3 tecken i ICM-format f”r  %4 %5';
    BEGIN
      WITH iPurchHeaderRec DO BEGIN
        IF STRLEN(iData) > iLength THEN BEGIN
          IF (gTruncateAddress = FALSE) OR (iTruncateField = FALSE) THEN  //db, 02-03-10: M18108
            ERROR(lvText000, iField, iData, iLength, "Document Type", "No.");
        END;
      END;

      EXIT(COPYSTR(iData,1,iLength));
    END;

    PROCEDURE MissingField@1100525000(iPurchHeaderRec@1100525001 : Record 38;iField@1100525000 : Integer) Result : Text[250];
    VAR
      lvText000@1100525002 : TextConst 'ENU=%1 not present for %2 %3;SVE=%1 finns inte f”r %2 %3';
    BEGIN
      WITH iPurchHeaderRec DO BEGIN
        CASE iField OF
          FIELDNO("Ship-to Name"): Result := STRSUBSTNO(lvText000, FIELDCAPTION("Ship-to Name"), TABLECAPTION, "No.");
          FIELDNO("Ship-to Address"): Result := STRSUBSTNO(lvText000, FIELDCAPTION("Ship-to Address"), TABLECAPTION, "No.");
          FIELDNO("Ship-to Post Code"): Result := STRSUBSTNO(lvText000, FIELDCAPTION("Ship-to Post Code"), TABLECAPTION, "No.");
          FIELDNO("Ship-to City"): Result := STRSUBSTNO(lvText000, FIELDCAPTION("Ship-to City"), TABLECAPTION, "No.");
        END;
      END;
      EXIT(Result);
    END;

    BEGIN
    {
      CC: Dataport 11012323

      db, 10-12-09:
       - check on "Received on Project" changed to "Job No." (ProjectCode)
       - new dataportfield (RefOrder) introduced: filled with "Service Order No." or "Job No."
      db, 28-02-17: C036065
       - solution 31238 (nav2009) copied to nav2017
    }
    END.
  }
}

