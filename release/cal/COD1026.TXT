OBJECT Codeunit 1026 Job Link Usage
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.01,4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 1020=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text001@1002 : TextConst '@@@=The specified Job Planning Line does not have Usage Link enabled.;ENU=The specified %1 does not have %2 enabled.;NOR=Angitt %1 har ikke %2 aktivert.;SVE=I den angivna %1 „r inte %2 aktiv.';
      ConfirmUsageWithBlankLineTypeQst@1000 : TextConst 'ENU=Usage will not be linked to the job planning line because the Line Type field is empty.\\Do you want to continue?;NOR=Bruken blir ikke koblet til prosjektplanleggingslinjen fordi Linjetype-feltet er tomt.\\Vil du fortsette?;SVE=Anv„ndning kopplas inte till projektplaneringsraden eftersom f„ltet Radtyp „r tomt.\\Vill du forts„tta?';

    [External]
    PROCEDURE ApplyUsage@1(JobLedgerEntry@1001 : Record 11072005;JobJournalLine@1004 : Record 11072008);
    BEGIN
      IF JobJournalLine."Job Planning Line No." = 0 THEN
        MatchUsageUnspecified(JobLedgerEntry,JobJournalLine."Line Type" = JobJournalLine."Line Type"::" ")
      ELSE
        MatchUsageSpecified(JobLedgerEntry,JobJournalLine);
    END;

    LOCAL PROCEDURE MatchUsageUnspecified@11(JobLedgerEntry@1000 : Record 11072005;EmptyLineType@1005 : Boolean);
    VAR
      JobPlanningLine@1001 : Record 11020401;
      JobUsageLink@1004 : Record 1020;
      Confirmed@1008 : Boolean;
      MatchedQty@1006 : Decimal;
      MatchedTotalCost@1002 : Decimal;
      MatchedLineAmount@1003 : Decimal;
      RemainingQtyToMatch@1007 : Decimal;
    BEGIN
      RemainingQtyToMatch := JobLedgerEntry."Quantity (Base)";
      REPEAT
        IF NOT FindMatchingJobPlanningLine(JobPlanningLine,JobLedgerEntry) THEN
          IF EmptyLineType THEN BEGIN
            OnMatchUsageUnspecifiedOnBeforeConfirm(JobPlanningLine,JobLedgerEntry,Confirmed);
            IF NOT Confirmed THEN
              Confirmed := CONFIRM(ConfirmUsageWithBlankLineTypeQst,FALSE);
            IF NOT Confirmed THEN
              ERROR('');
            RemainingQtyToMatch := 0;
          END ELSE
            CreateJobPlanningLine(JobPlanningLine,JobLedgerEntry,RemainingQtyToMatch);

        IF RemainingQtyToMatch <> 0 THEN BEGIN
          JobUsageLink.Create(JobPlanningLine,JobLedgerEntry);
          IF ABS(RemainingQtyToMatch) > ABS(JobPlanningLine."Remaining Qty. (Base)") THEN
            MatchedQty := JobPlanningLine."Remaining Qty. (Base)"
          ELSE
            MatchedQty := RemainingQtyToMatch;
          MatchedTotalCost := (JobLedgerEntry."Total Cost" / JobLedgerEntry."Quantity (Base)") * MatchedQty;
          MatchedLineAmount := (JobLedgerEntry."Line Amount" / JobLedgerEntry."Quantity (Base)") * MatchedQty;

          OnBeforeJobPlanningLineUse(JobPlanningLine,JobLedgerEntry);
          JobPlanningLine.SetUsagePostingDateAndCurrencyFactor(JobLedgerEntry."Posting Date",JobLedgerEntry."Currency Factor");
          JobPlanningLine.Use(CalcQtyFromBaseQty(MatchedQty,JobPlanningLine."Qty. per Unit of Measure"),
            MatchedTotalCost,MatchedLineAmount);
          RemainingQtyToMatch -= MatchedQty;
        END;
      UNTIL RemainingQtyToMatch = 0;
    END;

    LOCAL PROCEDURE MatchUsageSpecified@8(JobLedgerEntry@1000 : Record 11072005;JobJournalLine@1001 : Record 11072008);
    VAR
      JobPlanningLine@1002 : Record 11020401;
      JobUsageLink@1003 : Record 1020;
      TotalRemainingQtyPrePostBase@1004 : Decimal;
      PostedQtyBase@1007 : Decimal;
      TotalQtyBase@1005 : Decimal;
    BEGIN
      JobPlanningLine.GET(JobLedgerEntry."Job No.",JobLedgerEntry."Job Task No.",JobJournalLine."Job Planning Line No.");
      IF NOT JobPlanningLine."Usage Link" THEN
        ERROR(Text001,JobPlanningLine.TABLECAPTION,JobPlanningLine.FIELDCAPTION("Usage Link"));

      PostedQtyBase := JobPlanningLine."Quantity (Base)" - JobPlanningLine."Remaining Qty. (Base)";
      TotalRemainingQtyPrePostBase := JobJournalLine."Quantity (Base)" + JobJournalLine."Remaining Qty. (Base)";
      TotalQtyBase := PostedQtyBase + TotalRemainingQtyPrePostBase;
      JobPlanningLine.SetBypassQtyValidation(TRUE);
      JobPlanningLine.VALIDATE(Quantity,CalcQtyFromBaseQty(TotalQtyBase,JobPlanningLine."Qty. per Unit of Measure"));
      JobPlanningLine.VALIDATE("Serial No.",JobLedgerEntry."Serial No.");
      JobPlanningLine.VALIDATE("Lot No.",JobLedgerEntry."Lot No.");
      JobPlanningLine.SetUsagePostingDateAndCurrencyFactor(JobLedgerEntry."Posting Date",JobLedgerEntry."Currency Factor");
      JobPlanningLine.Use(CalcQtyFromBaseQty(JobLedgerEntry."Quantity (Base)",JobPlanningLine."Qty. per Unit of Measure"),
        JobLedgerEntry."Total Cost",JobLedgerEntry."Line Amount");
      JobUsageLink.Create(JobPlanningLine,JobLedgerEntry);
    END;

    [External]
    PROCEDURE FindMatchingJobPlanningLine@5(VAR JobPlanningLine@1000 : Record 11020401;JobLedgerEntry@1001 : Record 11072005) : Boolean;
    VAR
      Resource@1003 : Record 156;
      Filter@1002 : Text;
      JobPlanningLineFound@1004 : Boolean;
    BEGIN
      JobPlanningLine.RESET;
      JobPlanningLine.SETCURRENTKEY("Job No.","Schedule Line",Type,"No.","Planning Date");
      JobPlanningLine.SETRANGE("Job No.",JobLedgerEntry."Job No.");
      JobPlanningLine.SETRANGE("Job Task No.",JobLedgerEntry."Job Task No.");
      JobPlanningLine.SETRANGE(Type,JobLedgerEntry.Type);
      JobPlanningLine.SETRANGE("No.",JobLedgerEntry."No.");
      JobPlanningLine.SETRANGE("Location Code",JobLedgerEntry."Location Code");
      JobPlanningLine.SETRANGE("Schedule Line",TRUE);
      JobPlanningLine.SETRANGE("Usage Link",TRUE);

      IF JobLedgerEntry.Type = JobLedgerEntry.Type::Resource THEN BEGIN
        Filter := Resource.GetUnitOfMeasureFilter(JobLedgerEntry."No.",JobLedgerEntry."Unit of Measure Code");
        JobPlanningLine.SETFILTER("Unit of Measure Code",Filter);
      END;

      IF (JobLedgerEntry."Line Type" = JobLedgerEntry."Line Type"::Billable) OR
         (JobLedgerEntry."Line Type" = JobLedgerEntry."Line Type"::"Both Budget and Billable")
      THEN
        JobPlanningLine.SETRANGE("Contract Line",TRUE);

      IF JobLedgerEntry.Quantity > 0 THEN
        JobPlanningLine.SETFILTER("Remaining Qty.",'>0')
      ELSE
        JobPlanningLine.SETFILTER("Remaining Qty.",'<0');

      CASE JobLedgerEntry.Type OF
        JobLedgerEntry.Type::Item:
          JobPlanningLine.SETRANGE("Variant Code",JobLedgerEntry."Variant Code");
        JobLedgerEntry.Type::Resource:
          JobPlanningLine.SETRANGE("Work Type Code",JobLedgerEntry."Work Type Code");
      END;

      // Match most specific Job Planning Line.
      IF JobPlanningLine.FINDFIRST THEN
        EXIT(TRUE);

      JobPlanningLine.SETRANGE("Variant Code",'');
      JobPlanningLine.SETRANGE("Work Type Code",'');

      // Match Location Code, while Variant Code and Work Type Code are blank.
      IF JobPlanningLine.FINDFIRST THEN
        EXIT(TRUE);

      JobPlanningLine.SETRANGE("Location Code",'');

      CASE JobLedgerEntry.Type OF
        JobLedgerEntry.Type::Item:
          JobPlanningLine.SETRANGE("Variant Code",JobLedgerEntry."Variant Code");
        JobLedgerEntry.Type::Resource:
          JobPlanningLine.SETRANGE("Work Type Code",JobLedgerEntry."Work Type Code");
      END;

      // Match Variant Code / Work Type Code, while Location Code is blank.
      IF JobPlanningLine.FINDFIRST THEN
        EXIT(TRUE);

      JobPlanningLine.SETRANGE("Variant Code",'');
      JobPlanningLine.SETRANGE("Work Type Code",'');

      // Match unspecific Job Planning Line.
      IF JobPlanningLine.FINDFIRST THEN
        EXIT(TRUE);

      JobPlanningLineFound := FALSE;
      OnAfterFindMatchingJobPlanningLine(JobPlanningLine,JobLedgerEntry,JobPlanningLineFound);
      EXIT(JobPlanningLineFound);
    END;

    LOCAL PROCEDURE CreateJobPlanningLine@4(VAR JobPlanningLine@1000 : Record 11020401;JobLedgerEntry@1002 : Record 11072005;RemainingQtyToMatch@1003 : Decimal);
    VAR
      Job@1001 : Record 11072003;
      JobPostLine@1004 : Codeunit 11072006;
    BEGIN
      RemainingQtyToMatch := CalcQtyFromBaseQty(RemainingQtyToMatch,JobLedgerEntry."Qty. per Unit of Measure");

      CASE JobLedgerEntry."Line Type" OF
        JobLedgerEntry."Line Type"::" ":
          JobLedgerEntry."Line Type" := JobLedgerEntry."Line Type"::Budget;
        JobLedgerEntry."Line Type"::Billable:
          JobLedgerEntry."Line Type" := JobLedgerEntry."Line Type"::"Both Budget and Billable";
      END;
      JobPlanningLine.RESET;
      JobPostLine.InsertPlLineFromLedgEntry(JobLedgerEntry);
      // Retrieve the newly created Job PlanningLine.
      JobPlanningLine.SETRANGE("Job No.",JobLedgerEntry."Job No.");
      JobPlanningLine.SETRANGE("Job Task No.",JobLedgerEntry."Job Task No.");
      JobPlanningLine.SETRANGE("Schedule Line",TRUE);
      JobPlanningLine.FINDLAST;
      JobPlanningLine.VALIDATE("Usage Link",TRUE);
      JobPlanningLine.VALIDATE(Quantity,RemainingQtyToMatch);
      OnBeforeModifyJobPlanningLine(JobPlanningLine,JobLedgerEntry);
      JobPlanningLine.MODIFY;

      // If type is Both Budget And Billable and that type isn't allowed,
      // retrieve the Billabe line and modify the quantity as well.
      // Do the same if the type is G/L Account (Job Planning Lines will always be split in one Budget and one Billable line).
      Job.GET(JobLedgerEntry."Job No.");
      IF (JobLedgerEntry."Line Type" = JobLedgerEntry."Line Type"::"Both Budget and Billable") AND
         ((NOT Job."Allow Schedule/Contract Lines") OR (JobLedgerEntry.Type = JobLedgerEntry.Type::"G/L Account"))
      THEN BEGIN
        JobPlanningLine.GET(JobLedgerEntry."Job No.",JobLedgerEntry."Job Task No.",JobPlanningLine."Line No." + 10000);
        JobPlanningLine.VALIDATE(Quantity,RemainingQtyToMatch);
        JobPlanningLine.MODIFY;
        JobPlanningLine.GET(JobLedgerEntry."Job No.",JobLedgerEntry."Job Task No.",JobPlanningLine."Line No." - 10000);
      END;
    END;

    LOCAL PROCEDURE CalcQtyFromBaseQty@14(BaseQty@1000 : Decimal;QtyPerUnitOfMeasure@1001 : Decimal) : Decimal;
    BEGIN
      IF QtyPerUnitOfMeasure <> 0 THEN
        EXIT(ROUND(BaseQty / QtyPerUnitOfMeasure,0.00001));
      EXIT(BaseQty);
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterFindMatchingJobPlanningLine@2(VAR JobPlanningLine@1000 : Record 11020401;JobLedgerEntry@1001 : Record 11072005;VAR JobPlanningLineFound@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeModifyJobPlanningLine@3(VAR JobPlanningLine@1000 : Record 11020401;JobLedgerEntry@1001 : Record 11072005);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeJobPlanningLineUse@6(VAR JobPlanningLine@1000 : Record 11020401;JobLedgerEntry@1001 : Record 11072005);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnMatchUsageUnspecifiedOnBeforeConfirm@7(JobPlanningLine@1001 : Record 11020401;JobLedgerEntry@1000 : Record 11072005;VAR Confirmed@1002 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

