OBJECT Codeunit 11012893 Create SObject WS
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE CreateAYNOContract@1100528302(ContractTemplateCode@1100528300 : Code[20];CustomerCode@1100528306 : Code[20];StartDate@1100528308 : Date;Description@1100528307 : Text) serviceContractNo : Code[20];
    VAR
      ServSetup@1100528200 : Record 11012800;
      ServiceContractTemplate@1100528301 : Record 11071754;
      ServiceContract@1100528302 : Record 11012812;
    BEGIN
      ServiceContractTemplate.GET(ContractTemplateCode);
      ServSetup.GET;
      ServiceContract.INIT;
      ServiceContract."No. Series" := ServiceContractTemplate."No. Series";
      ServiceContract.INSERT(TRUE);
      ServiceContract.VALIDATE("Contract Template Code", ContractTemplateCode);
      ServiceContract.Description := Description;
      ServiceContract.VALIDATE("Customer No.", CustomerCode);
      ServiceContract.VALIDATE("Service Package", ServiceContractTemplate."Service Package");
      ServiceContract.VALIDATE("Starting Date", StartDate);
      IF ServiceContract.Description = '' THEN
        ServiceContract.Description := ServSetup."Service Contract Description 1";
      IF ServiceContract."Description 2" = '' THEN
        ServiceContract."Description 2" := ServSetup."Service Contract Description 2";
      ServiceContract.MODIFY(TRUE);
      serviceContractNo := ServiceContract."No.";
    END;

    PROCEDURE CreateAYNOObject@1100528303(objectNo@1100528200 : Code[20];standardObjectNo@1100409003 : Code[20];companyTxt@1100528301 : Text[50];objectDescription@1100528201 : Text[50];objectDescription2@1100528202 : Text[50];startDate@1100528203 : Date;customerCode@1100528302 : Code[20];locationCode@1100528204 : Code[20];ProjectNo@1100528205 : Code[20];aYNOYardNo@1100528206 : Code[20];aYNOEntityType@1100528207 : Integer;aYNOSystemNo@1100528208 : Code[20];aYNOEntityNo@1100528209 : Code[20]) serviceObjectNo : Code[20];
    VAR
      ServiceObject@1100528300 : Record 11071691;
    BEGIN
      ServiceObject.INIT;
      ServiceObject."No." := objectNo;
      ServiceObject.INSERT(TRUE);
      ServiceObject."Standard Object No." := standardObjectNo;
      ServiceObject."Company Name" := companyTxt;
      ServiceObject.Description := objectDescription;
      ServiceObject."Description 2" := objectDescription2;
      ServiceObject."In use since" := startDate;
      ServiceObject.VALIDATE("Object Customer", customerCode);
      ServiceObject."Service Location No." := locationCode;
      ServiceObject.Quantity := 1;
      ServiceObject."Project No." := ProjectNo;
      ServiceObject."Yard No. (AYNO)" := aYNOYardNo;
      ServiceObject."Entity Type (AYNO)" := aYNOEntityType;
      ServiceObject."System No. (AYNO)" := aYNOSystemNo;
      ServiceObject."Entity No. (AYNO)" := aYNOEntityNo;
      ServiceObject.MODIFY(TRUE);
      serviceObjectNo := ServiceObject."No.";
    END;

    PROCEDURE CreateAYNOContractObject@1100528305(serviceContractCode@1100528301 : Code[20];serviceObjectCode@1100528302 : Code[20];startDate@1100525000 : Date) contractObjectLink : Code[50];
    VAR
      ContractObject@1100528300 : Record 11071702;
      ServiceContract@1100528304 : Record 11012812;
    BEGIN
      ServiceContract.GET(serviceContractCode);
      ContractObject.INIT;
      ContractObject.VALIDATE("Service Contract No.", serviceContractCode);
      ContractObject.VALIDATE("Service Object No.", serviceObjectCode);
      ContractObject."Starting Date" := startDate;
      ContractObject.VALIDATE("Service Package", ServiceContract."Service Package");
      ContractObject.Quantity := 1;
      ContractObject.INSERT(TRUE);
      contractObjectLink := ContractObject."Service Contract No." + ' - ' + ContractObject."Service Object No.";
    END;

    PROCEDURE CreateAYNOLocation@1100525000(description@1100525000 : Text[50];customerCode@1100525001 : Code[20];startDate@1100525002 : Date;address@1100525003 : Text[50];postCode@1100525004 : Code[20];city@1100525005 : Text[30]) serviceLocationNo : Code[20];
    VAR
      ServiceLocation@1100525006 : Record 11012801;
    BEGIN
      ServiceLocation.INIT;
      ServiceLocation.INSERT(TRUE);
      ServiceLocation.VALIDATE(Description, description);
      ServiceLocation.VALIDATE("Customer No.", customerCode);
      ServiceLocation.VALIDATE("Starting Date", startDate);
      ServiceLocation.Address := address;
      ServiceLocation."Post Code" := postCode;
      ServiceLocation.City := city;
      ServiceLocation.MODIFY(TRUE);
      serviceLocationNo := ServiceLocation."No.";
    END;

    BEGIN
    END.
  }
}

