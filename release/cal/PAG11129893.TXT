OBJECT Page 11129893 Compress Budget Lines
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Budgetzeilen komprimieren;
               ENU=Compress Budget Lines;
               NLD=Werkbegrotingregels verdichten];
    PageType=StandardDialog;
  }
  CONTROLS
  {
    { 1100528400;0;Container;
                ContainerType=ContentArea }

    { 1100528401;1;Field  ;
                Name=CostObject;
                CaptionML=[DEU=Kostentr„ger;
                           ENU=Cost Object;
                           NLD=Kostendrager;
                           NOR=Kostnadsobjekt;
                           SVE=Kostnadsobjekt];
                SourceExpr=CostObject;
                OnValidate=BEGIN
                             ValidateCostObject;
                           END;

                OnLookup=BEGIN
                           LookupCostObject;
                         END;
                          }

    { 1100528403;1;Field  ;
                Name=Element;
                CaptionML=[DEU=Element;
                           ENU=Element;
                           NLD=Element;
                           NOR=Element;
                           SVE=Element];
                SourceExpr=Element;
                OnValidate=BEGIN
                             ValidateProjectElement;
                           END;

                OnLookup=BEGIN
                           LookupProjectElement;
                         END;
                          }

    { 1100528405;1;Field  ;
                Name=AmountLCY;
                CaptionML=[DEU=Betrag (MW);
                           ENU=Amount (LCY);
                           NLD=Bedrag (LV);
                           NOR=Bel›p (NOK);
                           SVE=Belopp (BVA)];
                SourceExpr=TempCompressedBudgetLine."Amount (LCY)";
                AutoFormatType=1;
                Editable=FALSE }

  }
  CODE
  {
    VAR
      TempCompressedBudgetLine@1100528403 : TEMPORARY Record 11012001;
      CostObject@1100528401 : Code[20];
      Element@1100528400 : Code[20];

    PROCEDURE SetTempCompressedBudgetLine@1100528401(TempCompressedBudgetLine2@1100528400 : TEMPORARY Record 11012001);
    BEGIN
      TempCompressedBudgetLine := TempCompressedBudgetLine2;
      CostObject := TempCompressedBudgetLine."Cost Object";
      Element := TempCompressedBudgetLine.Element;
    END;

    PROCEDURE GetUpdatedTempCompressedBudgetLine@1100528402(VAR TempCompressedBudgetLine2@1100528400 : TEMPORARY Record 11012001);
    BEGIN
      // Create a new TempCompressedBudgetLine, to avoid that STRMENU is called when cost object of
      // the existing TempCompressedBudgetLine is validated.
      CLEAR(TempCompressedBudgetLine2);
      TempCompressedBudgetLine2.InitTempCompressedBudgetLine(
        TempCompressedBudgetLine."Project No.", TempCompressedBudgetLine."Line No.",
        TempCompressedBudgetLine."Cost Type", CostObject, Element,
        TempCompressedBudgetLine2);
      TempCompressedBudgetLine2.VALIDATE("Amount (LCY)", TempCompressedBudgetLine."Amount (LCY)");
      //C055355.sn
      IF TempCompressedBudgetLine."Cost Type"= TempCompressedBudgetLine."Cost Type"::Labor THEN
        TempCompressedBudgetLine2.VALIDATE("Rate (LCY)", TempCompressedBudgetLine."Amount (LCY)")
      ELSE
        TempCompressedBudgetLine2.VALIDATE("Price (LCY)", TempCompressedBudgetLine."Amount (LCY)");
      //C055355.en
    END;

    LOCAL PROCEDURE LookupCostObject@1100528400();
    VAR
      DimensionValue@1100528401 : Record 349;
      CostObjectList@1100528400 : Page 11012021;
    BEGIN
      GetCostObject(DimensionValue);
      DimensionValue.SETRANGE("Cost Type", TempCompressedBudgetLine."Cost Type");
      CostObjectList.SETTABLEVIEW(DimensionValue);
      CostObjectList.LOOKUPMODE(TRUE);
      CostObjectList.SETRECORD(DimensionValue);
      IF CostObjectList.RUNMODAL = ACTION::LookupOK THEN BEGIN
        CostObjectList.GETRECORD(DimensionValue);
        CostObject := DimensionValue.Code;
      END;
      ValidateCostObject;
    END;

    LOCAL PROCEDURE ValidateCostObject@1100528403();
    VAR
      DimensionValue@1100528401 : Record 349;
      TempCompressedBudgetLine2@1100528400 : TEMPORARY Record 11012001;
    BEGIN
      GetCostObject(DimensionValue);
      GetUpdatedTempCompressedBudgetLine(TempCompressedBudgetLine2);
    END;

    LOCAL PROCEDURE GetCostObject@1100528418(VAR DimensionValue@1100528400 : Record 349);
    BEGIN
      IF CostObject <> '' THEN BEGIN
        DimensionValue.SETRANGE(Code, CostObject);
        DimensionValue.SETRANGE("Global Dimension No.", 2);
        DimensionValue.FINDFIRST;
        DimensionValue.SETRANGE(Code);
        DimensionValue.SETRANGE("Global Dimension No.");
      END ELSE
        CLEAR(DimensionValue);
    END;

    LOCAL PROCEDURE LookupProjectElement@1100528426();
    VAR
      ProjectElement@1100528401 : Record 11012010;
      ProjectElementList@1100528400 : Page 11012029;
    BEGIN
      GetProjectElement(ProjectElement);
      ProjectElement.SETRANGE("Project No.", TempCompressedBudgetLine."Project No.");
      ProjectElementList.SETTABLEVIEW(ProjectElement);
      ProjectElementList.LOOKUPMODE(TRUE);
      ProjectElementList.SETRECORD(ProjectElement);
      IF ProjectElementList.RUNMODAL = ACTION::LookupOK THEN BEGIN
        ProjectElementList.GETRECORD(ProjectElement);
        Element := ProjectElement.Element;
      END;
      ValidateProjectElement;
    END;

    LOCAL PROCEDURE ValidateProjectElement@1100528425();
    VAR
      ProjectElement@1100528401 : Record 11012010;
      TempCompressedBudgetLine2@1100528400 : TEMPORARY Record 11012001;
    BEGIN
      GetProjectElement(ProjectElement);
      GetUpdatedTempCompressedBudgetLine(TempCompressedBudgetLine2);
    END;

    LOCAL PROCEDURE GetProjectElement@1100528413(VAR ProjectElement@1100528400 : Record 11012010);
    BEGIN
      IF Element <> '' THEN
        ProjectElement.GET(TempCompressedBudgetLine."Project No.", Element)
      ELSE
        CLEAR(ProjectElement);
    END;

    BEGIN
    END.
  }
}

