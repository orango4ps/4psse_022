OBJECT Page 9815 Std. Password Dialog
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Set Password;
               NOR=Angi passord;
               SVE=Ange l”senord];
    PageType=StandardDialog;
    OnInit=BEGIN
             ValidatePassword := TRUE;
             RequiresPasswordConfirmation := TRUE;
           END;

    OnOpenPage=BEGIN
                 ValidPassword := FALSE;
               END;

    OnQueryClosePage=BEGIN
                       ValidPassword := FALSE;
                       IF CloseAction = ACTION::OK THEN BEGIN
                         IF RequiresPasswordConfirmation AND (SetPassword <> ConfirmPassword) THEN
                           ERROR(PasswordMismatchErr);
                         IF EnableBlankPasswordState AND (SetPassword = '') THEN BEGIN
                           IF NOT CONFIRM(ConfirmBlankPasswordQst) THEN
                             ERROR(PasswordTooSimpleErr);
                         END ELSE BEGIN
                           IF SetPassword = '' THEN
                             ERROR(PasswordBlankIsNotAllowedErr);
                           IF ValidatePassword AND NOT IdentityManagement.ValidatePasswordStrength(SetPassword) THEN
                             ERROR(PasswordTooSimpleErr);
                         END;
                         ValidPassword := TRUE;
                       END
                     END;

  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Field     ;
                Name=SetPassword;
                ExtendedDatatype=Masked;
                CaptionML=[ENU=Password;
                           NOR=Passord;
                           SVE=L”senord];
                ToolTipML=[ENU=Specifies the password for this task. The password must consist of 8 or more characters, at least one uppercase letter, one lowercase letter, and one number.;
                           NOR=Angir passordet for denne oppgaven. Passordet m† best† av 8 eller flere tegn, minst ‚n stor bokstav, ‚n liten bokstav og ett tall.;
                           SVE=Anger l”senordet f”r denna uppgift. L”senordet m†ste inneh†lla minst 8 tecken, minst en versal, minst en gemen och minst en siffra.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=SetPassword;
                OnValidate=BEGIN
                             IF ValidatePassword AND NOT IdentityManagement.ValidatePasswordStrength(SetPassword) THEN
                               ERROR(PasswordTooSimpleErr);
                           END;
                            }

    { 3   ;1   ;Field     ;
                Name=ConfirmPassword;
                ExtendedDatatype=Masked;
                CaptionML=[ENU=Confirm Password;
                           NOR=Bekreft passord;
                           SVE=Bekr„fta l”senord];
                ToolTipML=[ENU=Specifies the password repeated.;
                           NOR=Angir passordet to ganger.;
                           SVE=Anger l”senordet en g†ng till.];
                ApplicationArea=#All;
                SourceExpr=ConfirmPassword;
                Visible=RequiresPasswordConfirmation;
                OnValidate=BEGIN
                             IF RequiresPasswordConfirmation AND (SetPassword <> ConfirmPassword) THEN
                               ERROR(PasswordMismatchErr);
                           END;
                            }

  }
  CODE
  {
    VAR
      PasswordMismatchErr@1003 : TextConst 'ENU=The specified passwords are not the same.;NOR=De angitte passordene er ikke identiske.;SVE=De angivna l”senorden matchar inte.';
      PasswordTooSimpleErr@1004 : TextConst 'ENU=The specified password does not meet the requirements. It must be at least 8 characters long and contain at least one uppercase letter, one lowercase letter, and one number.;NOR=Det angitte passordet oppfyller ikke kravene. Det m† ha minst 8 tegn og inneholde minst ‚n stor bokstav, ‚n liten bokstav og ett tall.;SVE=Det angivna l”senordet uppfyller inte kraven. Det m†ste vara minst †tta tecken l†ngt och inneh†lla minst en versal, en gemen och en siffra.';
      PasswordNotValidatedErr@1007 : TextConst 'ENU=The password did not validate correctly, or it was not accepted.;NOR=Passordet kan ikke valideres, eller ble ikke godtatt.;SVE=L”senordet klarade inte valideringen eller ocks† accepterades det inte.';
      PasswordBlankIsNotAllowedErr@1012 : TextConst 'ENU=You must enter a password.;NOR=Du m† angi et passord.;SVE=Du m†ste ange ett l”senord.';
      ConfirmBlankPasswordQst@1008 : TextConst 'ENU=Do you want to exit without entering a password?;NOR=Vil du avslutte uten † angi et passord?;SVE=Vill du avsluta utan att ange ett l”senord?';
      IdentityManagement@1002 : Codeunit 9801;
      SetPassword@1000 : Text[250] INDATASET;
      ConfirmPassword@1001 : Text[250] INDATASET;
      ValidPassword@1005 : Boolean;
      EnableBlankPasswordState@1006 : Boolean;
      ValidatePassword@1009 : Boolean;
      GetPasswordCaptionTxt@1010 : TextConst 'ENU=Enter Password;NOR=Angi passord;SVE=Ange l”senordet';
      RequiresPasswordConfirmation@1011 : Boolean;

    [External]
    PROCEDURE GetPasswordValue@1() : Text;
    BEGIN
      IF ValidPassword = TRUE THEN
        EXIT(SetPassword);

      ERROR(PasswordNotValidatedErr);
    END;

    [External]
    PROCEDURE EnableBlankPassword@2(enable@1000 : Boolean);
    BEGIN
      EnableBlankPasswordState := enable;
    END;

    [External]
    PROCEDURE EnableGetPasswordMode@3(NewValidatePassword@1000 : Boolean);
    BEGIN
      ValidatePassword := NewValidatePassword;
      CurrPage.CAPTION := GetPasswordCaptionTxt;
    END;

    [External]
    PROCEDURE DisablePasswordConfirmation@5();
    BEGIN
      RequiresPasswordConfirmation := FALSE;
    END;

    BEGIN
    END.
  }
}

