OBJECT Codeunit 5671 FA. Jnl.-B.Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    TableNo=5620;
    OnRun=BEGIN
            FAJnlBatch.COPY(Rec);
            Code;
            COPY(FAJnlBatch);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to post the journals and print the posting report?;NOR=Vil du bokfre kladdene og skrive ut bokfringsrapporten?;SVE=Vill du bokfra journalerna och skriva ut bokfringsrapporten?';
      Text001@1001 : TextConst 'ENU=The journals were successfully posted.;NOR=Kladdene ble bokfrt p riktig mte.;SVE=Journalerna r bokfrda.';
      Text002@1002 : TextConst 'ENU="It was not possible to post all of the journals. ";NOR="Det er ikke mulig  bokfre alle kladdene. ";SVE="Det var inte mjligt att bokfra alla journaler. "';
      Text003@1003 : TextConst 'ENU=The journals that were not successfully posted are now marked.;NOR=Kladdene som ikke ble bokfrt, er merket.;SVE=Journalerna som inte blev bokfrda r nu markerade.';
      FAJnlTemplate@1004 : Record 5619;
      FAJnlBatch@1005 : Record 5620;
      FAJnlLine@1006 : Record 5621;
      FAReg@1007 : Record 5617;
      JournalWithErrors@1009 : Boolean;

    LOCAL PROCEDURE Code@1();
    VAR
      HideDialog@1000 : Boolean;
    BEGIN
      WITH FAJnlBatch DO BEGIN
        FAJnlTemplate.GET("Journal Template Name");
        FAJnlTemplate.TESTFIELD("Posting Report ID");
        FAJnlTemplate.TESTFIELD("Maint. Posting Report ID");

        HideDialog := FALSE;
        OnBeforePostJournalBatch(FAJnlBatch,HideDialog);
        IF NOT HideDialog THEN
          IF NOT CONFIRM(Text000,FALSE) THEN
            EXIT;

        FIND('-');
        REPEAT
          FAJnlLine."Journal Template Name" := "Journal Template Name";
          FAJnlLine."Journal Batch Name" := Name;
          FAJnlLine."Line No." := 1;
          IF CODEUNIT.RUN(CODEUNIT::"FA Jnl.-Post Batch",FAJnlLine) THEN BEGIN
            IF FAReg.GET(FAJnlLine."Line No.") THEN BEGIN
              FAReg.SETRECFILTER;
              IF FAReg."From Entry No." > 0 THEN
                REPORT.RUN(FAJnlTemplate."Posting Report ID",FALSE,FALSE,FAReg);
              IF FAReg."From Maintenance Entry No." > 0 THEN
                REPORT.RUN(FAJnlTemplate."Maint. Posting Report ID",FALSE,FALSE,FAReg);
            END;
            MARK(FALSE);
          END
          ELSE BEGIN
            MARK(TRUE);
            JournalWithErrors := TRUE;
          END;
        UNTIL NEXT = 0;

        IF NOT JournalWithErrors THEN
          MESSAGE(Text001)
        ELSE
          MESSAGE(
            Text002 +
            Text003);

        IF NOT FIND('=><') THEN BEGIN
          RESET;
          FILTERGROUP := 2;
          SETRANGE("Journal Template Name","Journal Template Name");
          FILTERGROUP := 0;
          Name := '';
        END;
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostJournalBatch@2(VAR FAJournalBatch@1000 : Record 5620;VAR HideDialog@1001 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

