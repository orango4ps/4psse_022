OBJECT Table 11012597 Plant Purchase Price
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               PlantVendRec.GET("Plant Type", "Vendor No.", "Price Type");
             END;

    OnRename=BEGIN
               PlantVendRec.GET("Plant Type", "Vendor No.", "Price Type");
             END;

    CaptionML=[ENU=Plant Purchase Price;
               NOR=Innkj›pspris for maskin;
               SVE=Maskin ink”pspris];
  }
  FIELDS
  {
    { 5   ;   ;Price Type          ;Option        ;OnValidate=BEGIN
                                                                CALCFIELDS("Vendor Plant No.");  //DP00241 (31081)
                                                              END;

                                                   CaptionML=[ENU=Price Type;
                                                              NOR=Pristype;
                                                              SVE=Pristyp];
                                                   OptionCaptionML=[ENU=Purchase,Rent;
                                                                    NOR=Innkj›p,Leie;
                                                                    SVE=Ink”p,Hyra];
                                                   OptionString=Purchase,Rent }
    { 10  ;   ;Plant Type          ;Code20        ;TableRelation="Plant Type";
                                                   OnValidate=BEGIN
                                                                IF "Plant Type" <> xRec."Plant Type" THEN
                                                                  "Unit of Measure Code" := '';
                                                                CALCFIELDS("Vendor Plant No.");  //DP00241 (31081)
                                                              END;

                                                   CaptionML=[ENU=Plant Type;
                                                              NOR=Maskintype;
                                                              SVE=Maskintyp];
                                                   NotBlank=Yes }
    { 15  ;   ;Plant No.           ;Code10        ;TableRelation="Plant Number".No. WHERE (Plant Type=FIELD(Plant Type));
                                                   OnValidate=BEGIN
                                                                IF "Plant No." = '' THEN
                                                                  EXIT;

                                                                PlantNoRec.GET("Plant Type", "Plant No.");
                                                                IF PlantNoRec.Vendor <> '' THEN
                                                                  VALIDATE("Vendor No.", PlantNoRec.Vendor);
                                                              END;

                                                   CaptionML=[ENU=Plant No.;
                                                              NOR=Maskinnr.;
                                                              SVE=Maskinnr] }
    { 20  ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor;
                                                   OnValidate=BEGIN
                                                                IF VendRec.GET("Vendor No.") THEN
                                                                  "Currency Code" := VendRec."Currency Code";

                                                                IF ("Plant Type" <> '') AND ("Vendor No." <> '') THEN
                                                                  PlantVendRec.GET("Plant Type", "Vendor No.", "Price Type");
                                                                CALCFIELDS("Vendor Plant No.");  //DP00241 (31081)
                                                              END;

                                                   CaptionML=[ENU=Vendor No.;
                                                              NOR=Leverand›rnr.;
                                                              SVE=Leverant”rsnr];
                                                   NotBlank=Yes }
    { 30  ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=[ENU=Currency Code;
                                                              NOR=Valutakode;
                                                              SVE=Valutakod] }
    { 35  ;   ;Unit of Measure Code;Code10        ;TableRelation="Plant Unit of Measure".Code WHERE (Plant Type=FIELD(Plant Type));
                                                   OnValidate=BEGIN
                                                                IF "Unit of Measure Code" <> '' THEN BEGIN
                                                                  IF ("Price Type" = "Price Type"::Rent) AND ("Plant Type" <> '') THEN BEGIN
                                                                    PlantSetup.GET;
                                                                    IF "Unit of Measure Code" = PlantSetup."Plant Hours Unit of Measure" THEN BEGIN
                                                                      PlantUnit.RESET;
                                                                      PlantUnit.SETRANGE("Plant Type", "Plant Type");
                                                                      IF NOT PlantUnit.ISEMPTY THEN BEGIN
                                                                        UnitOfMeas.GET("Unit of Measure Code");
                                                                        EXIT;
                                                                      END;
                                                                    END;
                                                                  END;
                                                                  PlantUnit.GET("Plant Type", "Unit of Measure Code");
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              TmpPlantUnit@1210190000 : TEMPORARY Record 11012599;
                                                            BEGIN
                                                              PlantUnit.RESET;
                                                              PlantUnit.FILTERGROUP(9);
                                                              PlantUnit.SETRANGE("Plant Type", "Plant Type");
                                                              PlantUnit.FILTERGROUP(0);

                                                              IF ("Price Type" = "Price Type"::Rent) AND ("Plant Type" <> '') THEN BEGIN
                                                                PlantSetup.GET;
                                                                IF PlantSetup."Plant Hours Unit of Measure" <> '' THEN BEGIN
                                                                  IF PlantUnit.FINDSET THEN BEGIN
                                                                    REPEAT
                                                                      TmpPlantUnit := PlantUnit;
                                                                      TmpPlantUnit.INSERT;
                                                                    UNTIL PlantUnit.NEXT = 0;
                                                                    IF NOT TmpPlantUnit.GET("Plant Type", PlantSetup."Plant Hours Unit of Measure") THEN BEGIN
                                                                      TmpPlantUnit.INIT;
                                                                      TmpPlantUnit."Plant Type" := "Plant Type";
                                                                      TmpPlantUnit.Code := PlantSetup."Plant Hours Unit of Measure";
                                                                      TmpPlantUnit.INSERT;
                                                                    END;
                                                                    TmpPlantUnit."Plant Type" := "Plant Type";
                                                                    TmpPlantUnit.Code := "Unit of Measure Code";
                                                                    IF PAGE.RUNMODAL(0, TmpPlantUnit) = ACTION::LookupOK THEN
                                                                      VALIDATE("Unit of Measure Code", TmpPlantUnit.Code);
                                                                    EXIT;
                                                                  END;
                                                                END;
                                                              END;

                                                              PlantUnit."Plant Type" := "Plant Type";
                                                              PlantUnit.Code := "Unit of Measure Code";
                                                              IF PAGE.RUNMODAL(0, PlantUnit) = ACTION::LookupOK THEN
                                                                VALIDATE("Unit of Measure Code", PlantUnit.Code);
                                                            END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              NOR=M†leenhetskode;
                                                              SVE=Enhetskod] }
    { 40  ;   ;Minimum Quantity    ;Decimal       ;CaptionML=[ENU=Minimum Quantity;
                                                              NOR=Minimumsantall;
                                                              SVE=Minimiantal];
                                                   MinValue=0;
                                                   Description=Editable switched off, field may not be used;
                                                   Editable=No }
    { 50  ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                IF ("Starting Date" > "Ending Date") AND ("Ending Date" <> 0D) THEN
                                                                  ERROR(Text000,FIELDCAPTION("Starting Date"),FIELDCAPTION("Ending Date"));
                                                              END;

                                                   CaptionML=[ENU=Starting Date;
                                                              NOR=Startdato;
                                                              SVE=Startdatum] }
    { 60  ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                VALIDATE("Starting Date");
                                                              END;

                                                   CaptionML=[ENU=Ending Date;
                                                              NOR=Sluttdato;
                                                              SVE=Slutdatum] }
    { 70  ;   ;Direct Unit Cost    ;Decimal       ;CaptionML=[ENU=Direct Unit Cost;
                                                              NOR=Innkj›pspris;
                                                              SVE=Ink”pspris];
                                                   MinValue=0;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Currency Code" }
    { 80  ;   ;Vendor Plant No.    ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Vendor"."Vendor Plant No." WHERE (Plant Type=FIELD(Plant Type),
                                                                                                               Vendor No.=FIELD(Vendor No.),
                                                                                                               Type=FIELD(Price Type)));
                                                   CaptionML=[ENU=Vendor Plant No.;
                                                              NOR=Leverand›r maskinnr.;
                                                              SVE=Leverant”r Maskinnr];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Price Type,Plant Type,Plant No.,Vendor No.,Starting Date,Currency Code,Unit of Measure Code,Minimum Quantity;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PlantSetup@1210190001 : Record 11012550;
      VendRec@1000 : Record 23;
      Text000@1004 : TextConst 'ENU=%1 cannot be after %2;NOR=%1 kan ikke v‘re etter %2;SVE=%1 kan inte vara efter %2';
      PlantVendRec@1100485000 : Record 11012596;
      PlantNoRec@1100485001 : Record 11012552;
      PlantUnit@1210190000 : Record 11012599;
      UnitOfMeas@1210190002 : Record 204;

    PROCEDURE LookupVendor@1100485000(IPlantTypeFilter@1100485002 : Code[250];IVendNoFilter@1100485003 : Code[250]);
    VAR
      lvVendRec@1100485001 : Record 23;
      lvPlantVendRec@1100485000 : Record 11012596;
    BEGIN
      IF ("Plant Type" <> '') OR (IPlantTypeFilter <> '') OR (IVendNoFilter <> '') THEN BEGIN
        IF (IPlantTypeFilter <> '') AND ("Plant Type" = '') THEN
          lvPlantVendRec.SETFILTER("Plant Type", IPlantTypeFilter);
        IF ("Plant Type" <> '') THEN
          lvPlantVendRec.SETRANGE("Plant Type", "Plant Type");
        IF (IVendNoFilter <> '') THEN
          lvPlantVendRec.SETFILTER("Vendor No.", IVendNoFilter);
        lvPlantVendRec.SETRANGE(Type, "Price Type");
        IF lvPlantVendRec.FINDSET THEN BEGIN
          REPEAT
            IF lvVendRec.GET(lvPlantVendRec."Vendor No.") THEN
              lvVendRec.MARK(TRUE);
          UNTIL lvPlantVendRec.NEXT = 0;
        END;
        lvVendRec.MARKEDONLY(TRUE);
      END;

      lvVendRec."No." := "Vendor No.";
      IF PAGE.RUNMODAL(0, lvVendRec) = ACTION::LookupOK THEN
        VALIDATE("Vendor No.", lvVendRec."No.");
    END;

    BEGIN
    END.
  }
}

