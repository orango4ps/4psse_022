OBJECT Table 11012145 Optional Merge Field
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Optional Merge Field;
               NOR=Valgfritt koblingsfelt;
               SVE=Valfritt kopplingsf„lt];
  }
  FIELDS
  {
    { 10  ;   ;Interaction Template Code;Code10   ;TableRelation="Interaction Template";
                                                   CaptionML=[ENU=Interaction Template Code;
                                                              NOR=Interaksjonsmalkode;
                                                              SVE=Interaktionsmallkod] }
    { 20  ;   ;Extra Merge Field No.;Integer      ;CaptionML=[ENU=Extra Merge Field No.;
                                                              NOR=Ekstra koblingsfeltnr.;
                                                              SVE=Extra kopplingsf„ltnr] }
    { 30  ;   ;Table               ;Option        ;OnValidate=BEGIN
                                                                CASE Table OF
                                                                  Table::Contact: "Table ID" := DATABASE::Contact;
                                                                  Table::"Salesperson/Purchaser": "Table ID" := DATABASE::"Salesperson/Purchaser";
                                                                  Table::"Segment Line": "Table ID" := DATABASE::"Segment Line";
                                                                  Table::"Company Information": "Table ID" := DATABASE::"Company Information";
                                                                  Table::Project: "Table ID" := DATABASE::Job;
                                                                  Table::Plot: "Table ID" := DATABASE::Plot;
                                                                  Table::"To-do": "Table ID" := DATABASE::"To-do"; //mg, 06-04-11: M25969
                                                                  Table::Delivery: "Table ID" := DATABASE::Delivery;
                                                                  ELSE BEGIN
                                                                    "Table ID" := 0;
                                                                    "Field No." := 0;
                                                                  END;
                                                                END;
                                                                VALIDATE("Table ID");
                                                              END;

                                                   CaptionML=[ENU=Table;
                                                              NOR=Tabell;
                                                              SVE=Tabell];
                                                   OptionCaptionML=ENU=" ,Contact,Salesperson/Purchaser,Segment Line,Company Information,Project,Plot,To-do,Delivery";
                                                   OptionString=[ ,Contact,Salesperson/Purchaser,Segment Line,Company Information,Project,Plot,To-do,Delivery] }
    { 40  ;   ;Table ID            ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Table));
                                                   OnValidate=BEGIN
                                                                VALIDATE("Field No.");
                                                              END;

                                                   CaptionML=[ENU=Table ID;
                                                              NOR=Tabell-ID;
                                                              SVE=Tabell-ID];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 50  ;   ;Field No.           ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table ID),
                                                                                  Class=FILTER(<>FlowFilter));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Field Caption");
                                                                IF "Field No." > 0 THEN
                                                                  "Merge Field Name" := COPYSTR(STRSUBSTNO('%1',Table) + ' ' +"Field Caption",1,MAXSTRLEN("Merge Field Name"));
                                                              END;

                                                   OnLookup=VAR
                                                              FieldTableManagement@1100529200 : Codeunit 11012372;
                                                            BEGIN
                                                              VALIDATE("Field No.",FieldTableManagement.LookupFieldNo("Table ID","Field No."));
                                                            END;

                                                   CaptionML=[ENU=Field No.;
                                                              NOR=Feltnr.;
                                                              SVE=F„ltnr];
                                                   BlankZero=Yes }
    { 55  ;   ;Field Caption       ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Table ID),
                                                                                                   No.=FIELD(Field No.)));
                                                   CaptionML=[ENU=Field Caption;
                                                              NOR=Felttekst;
                                                              SVE=F„ltrubrik];
                                                   Editable=No }
    { 60  ;   ;Merge Field Name    ;Text50        ;CaptionML=[ENU=Merge Field Name;
                                                              NOR=Koble feltnavn;
                                                              SVE=Koppla f„ltnamn] }
  }
  KEYS
  {
    {    ;Interaction Template Code,Extra Merge Field No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE GetFixedMergeFieldValue@1210190005(SegLine@1210190000 : Record 11072040) : Text[80];
    VAR
      RecRef@1210190001 : RecordRef;
      FldRef@1210190008 : FieldRef;
      Contact@1210190002 : Record 5050;
      Salesperson@1210190003 : Record 13;
      CompanyInfo@1210190004 : Record 79;
      ProjRec@1210190005 : Record 11072003;
      PlotRec@1210190006 : Record 11012500;
      ToDoRec@1210190009 : Record 5080;
      FieldRec@1210190007 : Record 2000000041;
      Delivery@1100527300 : Record 11012507;
    BEGIN
      //**4PS
      IF Table = Table::" " THEN
        EXIT('');

      CASE Table OF
        Table::Contact:
          BEGIN
            IF NOT Contact.GET(SegLine."Contact No.") THEN
              EXIT('');
            RecRef.GETTABLE(Contact);
          END;
        Table::"Salesperson/Purchaser":
          BEGIN
            IF NOT Salesperson.GET(SegLine."Salesperson Code") THEN
              EXIT('');
            RecRef.GETTABLE(Salesperson);
          END;
        Table::"Segment Line":
          BEGIN
            RecRef.GETTABLE(SegLine);
          END;
        Table::"Company Information":
          BEGIN
            IF NOT CompanyInfo.GET THEN
              EXIT('');
            RecRef.GETTABLE(CompanyInfo);
          END;
        Table::Project:
          BEGIN
            IF NOT ProjRec.GET(SegLine."Project No.") THEN
              EXIT('');
            RecRef.GETTABLE(ProjRec);
          END;
        Table::Plot:
          BEGIN
            IF NOT PlotRec.GET(SegLine."Project No.", SegLine."Plot No.") THEN
              EXIT('');
            RecRef.GETTABLE(PlotRec);
          END;
        //mg.sn, 06-04-11: M25969
        Table::"To-do":
          BEGIN
            IF NOT ToDoRec.GET(SegLine."To-do No.") THEN
              EXIT('');
            RecRef.GETTABLE(ToDoRec);
          END;
        //mg.en, 06-04-11: M25969
        Table::Delivery:
          BEGIN
            IF NOT Delivery.GET(SegLine."Project No.", SegLine."Plot No.", SegLine."Delivery No.") THEN
              EXIT('');
            RecRef.GETTABLE(Delivery);
          END;
      END;

      IF FieldRec.GET("Table ID", "Field No.") THEN BEGIN
        FldRef := RecRef.FIELD("Field No.");
        IF FieldRec.Class = FieldRec.Class::FlowField THEN
          FldRef.CALCFIELD;
        EXIT(FORMAT(FldRef));
      END;
    END;

    PROCEDURE GetFixedMFValueIntLogEntry@1210190000(InteractionLogEntry@1210190000 : Record 11072027) : Text[80];
    VAR
      lSegLine@1210190001 : Record 11072040;
    BEGIN
      lSegLine."Contact No." := InteractionLogEntry."Contact No.";
      lSegLine."Salesperson Code" := InteractionLogEntry."Salesperson Code";
      lSegLine."Project No." := InteractionLogEntry."Project No.";
      lSegLine."Plot No." := InteractionLogEntry."Plot No.";
      lSegLine."To-do No." := InteractionLogEntry."To-do No."; //call 31936

      EXIT(GetFixedMergeFieldValue(lSegLine));
    END;

    BEGIN
    END.
  }
}

