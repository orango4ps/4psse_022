OBJECT Table 11020635 Subcontractor Contract
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               PurchSetup.GET;
               IF PurchSetup."Subcontract No. Type" = PurchSetup."Subcontract No. Type"::"Contract No. = Job No." THEN BEGIN
                 IF "No." <> '' THEN
                   TESTFIELD("No.","Project No.")
                 ELSE
                   "No." := "Project No.";
                 "Contract No. Type" := "Contract No. Type"::"Contract No. = Job No.";
               END ELSE BEGIN
                 IF "No." = '' THEN
                   PurchSetup.TESTFIELD("Subcontract No. Nos.");
                 NoSeriesMgt.InitSeries(PurchSetup."Subcontract No. Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;
             END;

    CaptionML=ENU=Subcontractor Contract;
    LookupPageID=Page11124952;
  }
  FIELDS
  {
    { 10  ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                IF xRec."No." <> '' THEN
                                                                  IF "Contract No. Type" = "Contract No. Type"::"Contract No. = Job No." THEN BEGIN
                                                                    TESTFIELD("No.","Project No.");
                                                                END;

                                                                //call 29728
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  PurchSetup.GET;
                                                                  NoSeriesMgt.TestManual(PurchSetup."Subcontract No. Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              NOR=Nr;
                                                              SVE=Nr] }
    { 20  ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor;
                                                   OnValidate=VAR
                                                                Vend@1000000001 : Record 23;
                                                              BEGIN
                                                                IF NOT Vend.GET("Vendor No.") THEN
                                                                  Vend.INIT;
                                                                "Currency Code" := Vend."Currency Code";
                                                              END;

                                                   CaptionML=[ENU=Vendor No.;
                                                              NOR=Leverand›rnr.;
                                                              SVE=Leverant”rsnr];
                                                   NotBlank=Yes }
    { 30  ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   OnValidate=VAR
                                                                Job@1100528500 : Record 11072003;
                                                              BEGIN
                                                                IF Job.GET("Project No.") THEN BEGIN
                                                                  Job.CheckProjectStatus(2);
                                                                  IF Description = '' THEN
                                                                    Description := Job.Description;
                                                                END;

                                                                PurchSetup.GET;

                                                                IF PurchSetup."Subcontract No. Type" = PurchSetup."Subcontract No. Type"::"Contract No. = Job No." THEN BEGIN
                                                                  IF "No." <> '' THEN
                                                                    TESTFIELD("No.","Project No.")
                                                                  ELSE
                                                                    "No." := "Project No.";
                                                                  "Contract No. Type" := "Contract No. Type"::"Contract No. = Job No.";
                                                                END ELSE
                                                                  IF ("No." = '') AND (CurrFieldNo = FIELDNO("Project No.")) THEN BEGIN
                                                                    PurchSetup.TESTFIELD("Subcontract No. Nos.");
                                                                  END;
                                                              END;

                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr];
                                                   NotBlank=Yes }
    { 45  ;   ;Project Description ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Job.Description WHERE (No.=FIELD(Project No.)));
                                                   CaptionML=[ENU=Project Description;
                                                              NOR=Prosjektbeskrivelse;
                                                              SVE=Projektbeskrivning];
                                                   Editable=No }
    { 60  ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              NOR=Nr-serie;
                                                              SVE=Nr-serie];
                                                   Editable=No }
    { 70  ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 80  ;   ;Retention %         ;Decimal       ;CaptionML=[ENU=Retention %;
                                                              NOR=Gjenv‘rende %;
                                                              SVE=Kvarst†ende procent];
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 90  ;   ;Retention % 2       ;Decimal       ;CaptionML=[ENU=Retention % 2;
                                                              NOR=Gjenv‘rende % 2;
                                                              SVE=Kvarst†ende procent 2];
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 100 ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=[ENU=Currency Code;
                                                              NOR=Valutakode;
                                                              SVE=Valutakod] }
    { 160 ;   ;Retention Payment Terms Code;Code10;TableRelation="Payment Terms";
                                                   CaptionML=[ENU=Retention Payment Terms Code;
                                                              NOR=Betalingsvilk†rskode for gjenv‘rende;
                                                              SVE=Kvarst†ende betalningsvillkorskod] }
    { 170 ;   ;Retention Payment Terms Code 2;Code10;
                                                   TableRelation="Payment Terms";
                                                   CaptionML=[ENU=Retention Payment Terms Code 2;
                                                              NOR=Betalingsvilk†rskode 2 for gjenv‘rende;
                                                              SVE=Kvarst†ende betalningsvillkorskod 2] }
    { 190 ;   ;Contract No. Type   ;Option        ;CaptionML=[ENU=Contract No. Type;
                                                              NOR=Kontraktsnr, type;
                                                              SVE=Kontraktsnrtyp];
                                                   OptionCaptionML=[ENU="Contract No.,Contract No. = Job No.";
                                                                    NOR="Kontraktsnr,Kontraktsnr = jobbnr";
                                                                    SVE="Kontraktsnr,Kontrakatsnr = Jobbnr"];
                                                   OptionString=Contract No.,Contract No. = Job No. }
    { 11128900;;Subcontract Type   ;Option        ;CaptionML=ENU=Subcontract Type;
                                                   OptionCaptionML=[ENU=Vendor,Customer;
                                                                    SVE=Leverant”r,Kund];
                                                   OptionString=Vendor,Customer;
                                                   Description=4PSNA }
  }
  KEYS
  {
    {    ;Vendor No.,No.                          ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PurchSetup@1100528500 : Record 312;
      NoSeriesMgt@1100528501 : Codeunit 396;

    PROCEDURE ShowLedgerEntries@1100528505();
    VAR
      VendLedgerEntry@1100528500 : Record 25;
    BEGIN
      VendLedgerEntry.SETCURRENTKEY("Vendor No.");
      VendLedgerEntry.SETRANGE(VendLedgerEntry."Vendor No.","Vendor No.");
      VendLedgerEntry.SETRANGE(VendLedgerEntry."Subcontract No.","No.");

      PAGE.RUNMODAL(0,VendLedgerEntry);
    END;

    PROCEDURE ShowRetentionLedgerEntries@1100528504();
    VAR
      RetentionLedgerEntry@1100528500 : Record 11020636;
    BEGIN
      RetentionLedgerEntry.SETCURRENTKEY("Related No.","Subcontract No.");
      RetentionLedgerEntry.SETRANGE("Related No.","Vendor No.");
      RetentionLedgerEntry.SETRANGE("Subcontract No.","No.");

      PAGE.RUNMODAL(0,RetentionLedgerEntry);
    END;

    PROCEDURE ShowPostedInvoices@1100528503();
    VAR
      PurchInvoice@1100528500 : Record 122;
    BEGIN
      PurchInvoice.SETCURRENTKEY("Buy-from Vendor No.");
      PurchInvoice.SETRANGE("Buy-from Vendor No.","Vendor No.");
      PurchInvoice.SETRANGE("Subcontract No.","No.");

      PAGE.RUNMODAL(0,PurchInvoice);
    END;

    PROCEDURE ShowPayments@1100528501();
    VAR
      VendLedgEntry@1100528503 : Record 25;
      AppliedVendLedgEntry@1100528502 : Record 25;
    BEGIN
      VendLedgEntry.SETCURRENTKEY("Vendor No.");
      VendLedgEntry.SETRANGE("Vendor No.","Vendor No.");
      VendLedgEntry.SETRANGE("Subcontract No.","No.");

      IF VendLedgEntry.FIND('-') THEN
        REPEAT

          IF VendLedgEntry."Closed by Entry No." <> 0 THEN BEGIN
            AppliedVendLedgEntry.GET(VendLedgEntry."Closed by Entry No.");
            IF NOT (AppliedVendLedgEntry."Document Type" IN
              [AppliedVendLedgEntry."Document Type"::Invoice,AppliedVendLedgEntry."Document Type"::"Credit Memo"])
            THEN
              AppliedVendLedgEntry.MARK(TRUE);
          END;

          AppliedVendLedgEntry.SETCURRENTKEY("Closed by Entry No.");
          AppliedVendLedgEntry.SETRANGE("Closed by Entry No.",VendLedgEntry."Entry No.");
          IF AppliedVendLedgEntry.FIND('-') THEN
            REPEAT
              IF NOT (AppliedVendLedgEntry."Document Type" IN
                [AppliedVendLedgEntry."Document Type"::Invoice,AppliedVendLedgEntry."Document Type"::"Credit Memo"])
              THEN
                AppliedVendLedgEntry.MARK(TRUE);
            UNTIL AppliedVendLedgEntry.NEXT = 0;
         AppliedVendLedgEntry.SETRANGE("Closed by Entry No.");
         AppliedVendLedgEntry.SETCURRENTKEY("Entry No.");

        UNTIL VendLedgEntry.NEXT = 0;

      AppliedVendLedgEntry.MARKEDONLY(TRUE);
      PAGE.RUNMODAL(0,AppliedVendLedgEntry);
    END;

    BEGIN
    END.
  }
}

