OBJECT Table 5998 Standard Service Item Gr. Code
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Service Item Group Code;
    OnDelete=BEGIN
               StdServiceLine.RESET;
               StdServiceLine.SETRANGE("Standard Service Code",Code);
               IF NOT StdServiceLine.ISEMPTY THEN
                 StdServiceLine.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=Standard Service Item Gr. Code;
               NOR=Standard servicevaregruppekode];
  }
  FIELDS
  {
    { 1   ;   ;Service Item Group Code;Code10     ;TableRelation="Service Item Group";
                                                   CaptionML=[ENU=Service Item Group Code;
                                                              NOR=Servicevaregruppekode;
                                                              SVE=Serviceartikelgruppkod] }
    { 2   ;   ;Code                ;Code10        ;TableRelation="Standard Service Code";
                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 3   ;   ;Description         ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Standard Service Code".Description WHERE (Code=FIELD(Code)));
                                                   CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Service Item Group Code,Code            ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=%1 of the standard service code must be equal to %2 on the %3.;NOR=%1 i standard servicekode m† v‘re lik %2 i %3.';
      StdServCode@1001 : Record 5996;
      StdServiceLine@1002 : Record 5997;

    [External]
    PROCEDURE InsertServiceLines@1(ServItemLine@1000 : Record 5901);
    VAR
      Currency@1001 : Record 4;
      ServHeader@1003 : Record 5900;
      ServLine@1004 : Record 5902;
      StdServLine@1006 : Record 5997;
      StdServItemGrCode@1007 : Record 5998;
      StdServItemGrCodesForm@1009 : Page 5959;
      Factor@1008 : Integer;
    BEGIN
      ServItemLine.TESTFIELD("Line No.");

      CLEAR(StdServItemGrCodesForm);
      StdServItemGrCode.RESET;
      IF ServItemLine."Service Item Group Code" <> '' THEN
        StdServItemGrCode.SETRANGE("Service Item Group Code",ServItemLine."Service Item Group Code");
      StdServItemGrCodesForm.SETRECORD(StdServItemGrCode);
      StdServItemGrCodesForm.SETTABLEVIEW(StdServItemGrCode);
      StdServItemGrCodesForm.LOOKUPMODE := TRUE;

      IF NOT (StdServItemGrCodesForm.RUNMODAL = ACTION::LookupOK) THEN
        EXIT;
      StdServItemGrCodesForm.GETRECORD(StdServItemGrCode);
      StdServItemGrCode.TESTFIELD(Code);
      StdServCode.GET(StdServItemGrCode.Code);

      StdServCode.TESTFIELD(Code);
      ServHeader.GET(ServItemLine."Document Type",ServItemLine."Document No.");
      IF StdServCode."Currency Code" <> ServHeader."Currency Code" THEN
        ERROR(
          Text001,
          StdServCode.FIELDCAPTION("Currency Code"),
          ServHeader.FIELDCAPTION("Currency Code"),ServHeader.TABLECAPTION);
      StdServLine.SETRANGE("Standard Service Code",StdServCode.Code);
      Currency.Initialize(StdServCode."Currency Code");
      ServLine."Document Type" := ServItemLine."Document Type";
      ServLine."Document No." := ServItemLine."Document No.";
      ServLine.SETRANGE("Document Type",ServItemLine."Document Type");
      ServLine.SETRANGE("Document No.",ServItemLine."Document No.");
      IF ServHeader."Prices Including VAT" THEN
        Factor := 1
      ELSE
        Factor := 0;
      ServLine.LOCKTABLE;
      StdServLine.LOCKTABLE;
      IF StdServLine.FIND('-') THEN
        REPEAT
          ServLine.INIT;
          ServLine."Line No." := 0;
          ServLine.VALIDATE(Type,StdServLine.Type);
          IF ServHeader."Link Service to Service Item" THEN
            ServLine.VALIDATE("Service Item Line No.",ServItemLine."Line No.");
          IF StdServLine.Type = StdServLine.Type::" " THEN BEGIN
            ServLine.VALIDATE("No.",StdServLine."No.");
            ServLine.Description := StdServLine.Description
          END ELSE
            IF NOT StdServLine.EmptyLine THEN BEGIN
              StdServLine.TESTFIELD("No.");
              ServLine.VALIDATE("No.",StdServLine."No.");
              IF StdServLine."Variant Code" <> '' THEN
                ServLine.VALIDATE("Variant Code",StdServLine."Variant Code");
              ServLine.VALIDATE(Quantity,StdServLine.Quantity);
              IF StdServLine."Unit of Measure Code" <> '' THEN
                ServLine.VALIDATE("Unit of Measure Code",StdServLine."Unit of Measure Code");
              ServLine.Description := StdServLine.Description;
              IF StdServLine.Type = StdServLine.Type::"G/L Account" THEN
                ServLine.VALIDATE(
                  "Unit Price",
                  ROUND(StdServLine."Amount Excl. VAT" *
                    (ServLine."VAT %" / 100 * Factor + 1),Currency."Unit-Amount Rounding Precision"));
            END;

          ServLine."Shortcut Dimension 1 Code" := StdServLine."Shortcut Dimension 1 Code";
          ServLine."Shortcut Dimension 2 Code" := StdServLine."Shortcut Dimension 2 Code";

          CombineDimensions(ServLine,StdServLine);

          IF StdServLine.InsertLine THEN BEGIN
            ServLine."Line No." := GetNextLineNo(ServLine);
            OnBeforeInsertServLine(ServLine);
            ServLine.INSERT(TRUE);
            InsertExtendedText(ServLine);
          END;
        UNTIL StdServLine.NEXT = 0;
    END;

    LOCAL PROCEDURE InsertExtendedText@3(ServLine@1000 : Record 5902);
    VAR
      TransferExtendedText@1001 : Codeunit 378;
    BEGIN
      IF TransferExtendedText.ServCheckIfAnyExtText(ServLine,FALSE) THEN
        TransferExtendedText.InsertServExtText(ServLine);
    END;

    LOCAL PROCEDURE GetNextLineNo@4(ServLine@1001 : Record 5902) : Integer;
    BEGIN
      ServLine.SETRANGE("Document Type",ServLine."Document Type");
      ServLine.SETRANGE("Document No.",ServLine."Document No.");
      IF ServLine.FINDLAST THEN
        EXIT(ServLine."Line No." + 10000);
      EXIT(10000);
    END;

    LOCAL PROCEDURE CombineDimensions@5(VAR ServLine@1001 : Record 5902;StdServLine@1002 : Record 5997);
    VAR
      DimensionManagement@1000 : Codeunit 408;
      DimensionSetIDArr@1005 : ARRAY [10] OF Integer;
    BEGIN
      DimensionSetIDArr[1] := ServLine."Dimension Set ID";
      DimensionSetIDArr[2] := StdServLine."Dimension Set ID";

      ServLine."Dimension Set ID" :=
        DimensionManagement.GetCombinedDimensionSetID(
          DimensionSetIDArr,ServLine."Shortcut Dimension 1 Code",ServLine."Shortcut Dimension 2 Code");
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeInsertServLine@2(VAR ServiceLine@1000 : Record 5902);
    BEGIN
    END;

    BEGIN
    END.
  }
}

