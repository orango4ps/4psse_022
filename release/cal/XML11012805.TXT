OBJECT XMLport 11012805 Import TRAD-TXT Service (6.20)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Import IBIS-Trad TXT Budget (6.20);
               SVE=Import IBIS-Trad TXT Budget (6.20)];
    Direction=Import;
    DefaultFieldsValidation=No;
    TextEncoding=WINDOWS;
    OnPreXMLport=BEGIN
                   ProjSetup.GET;
                   ServOrder.GET(ServOrderNo);

                   CreateRateTable;
                   SumReached := FALSE;
                 END;

    Format=Fixed Text;
    UseRequestPage=No;
  }
  ELEMENTS
  {
    { [{D83C7E5F-3E9F-4810-9DE9-1C89EBA7CC78}];  ;Root                ;Element ;Text     }

    { [{9B61C620-C498-44F7-AB10-16730623E1FD}];1 ;BudgetLine          ;Element ;Table   ;
                                                  SourceTable=Table11012875;
                                                  Import::OnAfterInitRecord=BEGIN
                                                                              "Service Order Budget Line".INIT;
                                                                              CLEAR(ExternLineNoTxt);
                                                                              CLEAR(RecordType);
                                                                              CLEAR(MeansCde);
                                                                              CLEAR(StuurCde);
                                                                              CLEAR(SttCde);
                                                                              CLEAR(QuantityTxt);
                                                                              CLEAR(SurchPercQuanTxt);
                                                                              CLEAR(TimeQuantityTxt);
                                                                              CLEAR(NormTxt);
                                                                              CLEAR(MatPriceTxt);
                                                                              CLEAR(PlntPriceTxt);
                                                                              CLEAR(SubPriceTxt);
                                                                              CLEAR(SurchPercLaborTxt);
                                                                              CLEAR(SurchPercMaterialTxt);
                                                                              CLEAR(SurchPercPlantTxt);
                                                                              CLEAR(SurchPercSubcTxt);
                                                                              CLEAR(AdmiGenCde);
                                                                              CLEAR(AdmiLaborCde);
                                                                              CLEAR(AdmiMatCde);
                                                                              CLEAR(AdmiPlntCde);
                                                                              CLEAR(AdmiSubCde);
                                                                              CLEAR(TenderCde);
                                                                              CLEAR(PlanCde);
                                                                              CLEAR(LocationCde);
                                                                            END;

                                                  Import::OnBeforeInsertRecord=BEGIN
                                                                                 WITH "Service Order Budget Line" DO BEGIN
                                                                                   IF NOT CheckRecordType() THEN
                                                                                     currXMLport.SKIP;

                                                                                   IF TestSummaryStart(StuurCde) THEN
                                                                                     SumReached := TRUE;
                                                                                   IF TestSummaryEnd(StuurCde) THEN
                                                                                     SumReached := FALSE;
                                                                                   IF SumReached THEN
                                                                                     currXMLport.SKIP;

                                                                                   IF NOT CheckSttCode()  THEN
                                                                                     currXMLport.SKIP;

                                                                                   LineCreated := FALSE;
                                                                                   "Budget Type" := "Budget Type"::Order;
                                                                                   "Service Order No." := ServOrderNo;
                                                                                   "Service Contract No." := ServOrder."Service Contract No.";
                                                                                   "Cost Object" := '';
                                                                                   "Cost Type":= "Cost Type"::Sundry;
                                                                                   "Time Quantity" := 0;
                                                                                   Quantity := 0;
                                                                                   Price := 0;
                                                                                   Norm := 0;
                                                                                   Rate := 0;
                                                                                   Amount := 0;
                                                                                   Hours := 0;
                                                                                   "Line No. External Estimate" := ExternLineNo;

                                                                                   RecRef.GETTABLE("Service Order Budget Line");
                                                                                   XMLportStatusDialogMgt.ShowImportStatus(RecRef);

                                                                                   CASE RecordType OF
                                                                                     'ELT':
                                                                                       BEGIN
                                                                                         InsertTextLines;  //* Element only as text line (no elements in Service)
                                                                                         LastProcessedExternLineNo := ExternLineNo;
                                                                                       END;
                                                                                     'KTP':
                                                                                       BEGIN
                                                                                         ConvertAmounts;
                                                                                         Quantity := QuantityDec + ((SurchPercQuanDec/100) * QuantityDec);
                                                                                         "Time Quantity" := TimeQuantityDec;
                                                                                         IF "Unit of Measure" <> '' THEN
                                                                                           IF NOT UnitRec.GET("Unit of Measure") THEN
                                                                                             StoreMessage(Description, STRSUBSTNO(Text999, FIELDCAPTION("Unit of Measure"), "Unit of Measure"));
                                                                                         IF "Unit of Time" <> '' THEN
                                                                                           IF NOT UnitRec.GET("Unit of Time") THEN
                                                                                             StoreMessage(Description, STRSUBSTNO(Text999, FIELDCAPTION("Unit of Time"), "Unit of Time"));
                                                                                         InsertLines;
                                                                                         LastProcessedExternLineNo := ExternLineNo;
                                                                                       END;
                                                                                     'CMT':
                                                                                       BEGIN
                                                                                         IF CmtWithQuantities THEN BEGIN
                                                                                           ConvertAmounts;
                                                                                           Quantity := QuantityDec + ((SurchPercQuanDec/100) * QuantityDec);
                                                                                           "Time Quantity" := TimeQuantityDec;
                                                                                         END;
                                                                                         InsertTextLines;
                                                                                         LastProcessedExternLineNo := ExternLineNo;
                                                                                       END;
                                                                                   END;

                                                                                   currXMLport.SKIP;
                                                                                 END;
                                                                               END;
                                                                                }

    { [{6CB3DABC-027E-4C9C-9112-40F8B8FB3B46}];2 ;Filler1             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=1 }

    { [{84B74A83-A1AD-481A-8621-10C585B14C63}];2 ;ExternLineNoTxt     ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  FillEmptyTextWithZero(ExternLineNoTxt);
                                                                                  EVALUATE(ExternLineNo, ExternLineNoTxt);
                                                                                END;

                                                  Width=6 }

    { [{FC49153A-D1CC-4608-A2D6-FC3E16A99C32}];2 ;RecordType          ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=3 }

    { [{159DA4B9-190F-40A5-B57B-B6F16CDC598C}];2 ;MeansCde            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  MeansCde := COPYSTR(MeansCde,1,20);
                                                                                END;

                                                  Width=22 }

    { [{15C2B593-6949-4E7B-9822-ED61C6760A78}];2 ;StuurCde            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=1 }

    { [{9EBF833B-C501-46BA-88BC-748DA4F1D1EE}];2 ;SttCde              ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=5 }

    { [{40F80AA8-EFB6-41B8-A02D-1A04BD88F2B0}];2 ;Description         ;Element ;Field   ;
                                                  DataType=Text;
                                                  SourceField=Service Order Budget Line::Description;
                                                  MinOccurs=Zero;
                                                  Width=50 }

    { [{6C07109B-1A64-4AEC-8493-B4877F1E4541}];2 ;Filler2             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=3 }

    { [{7F6A2CAF-BFC2-4430-92A9-B5C2CBFCBDA8}];2 ;QuantityTxt         ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=17 }

    { [{2B0D4DD2-70DC-4FE6-8897-5F218F8F10A1}];2 ;UnitOfMeasure       ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=Service Order Budget Line::Unit of Measure;
                                                  MinOccurs=Zero;
                                                  Width=4 }

    { [{F8EE4E7F-DD89-48E7-9802-21F27650BA40}];2 ;SurchPercQuanTxt    ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=17 }

    { [{C8AC4FC4-5347-483E-B2E7-CA6A4F100C09}];2 ;TimeQuantityTxt     ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=17 }

    { [{932F247C-ED3D-41C1-94EF-B77CAD1BC37D}];2 ;UnitOfTime          ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=Service Order Budget Line::Unit of Time;
                                                  MinOccurs=Zero;
                                                  Width=4 }

    { [{D5031E37-CA40-474F-B541-CB9F404BE3D9}];2 ;NormTxt             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=17 }

    { [{95D4C864-6B21-4E4D-B65D-861D2E289912}];2 ;MatPriceTxt         ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=17 }

    { [{34E4F36F-5965-4EF7-9A03-EEF349799A76}];2 ;PlntPriceTxt        ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=17 }

    { [{F122D1FB-694A-4A44-BCA3-82E693DB84D6}];2 ;SubPriceTxt         ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=17 }

    { [{504D710B-E1C6-4DAF-ADB2-12131B77F8D4}];2 ;RateCode            ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=Service Order Budget Line::Rate Code;
                                                  MinOccurs=Zero;
                                                  Width=2 }

    { [{721C0CBF-80F8-4D83-8451-623AF4052B40}];2 ;SurchPercLaborTxt   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=17 }

    { [{2245C602-B5DF-4B19-85D8-1218C278E3E7}];2 ;SurchPercMaterialTxt;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=17 }

    { [{5302C9AD-D1E4-429B-B996-8DA16394FCAA}];2 ;SurchPercPlantTxt   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=17 }

    { [{666B4B1C-1077-4674-8449-269771921F76}];2 ;SurchPercSubcTxt    ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=17 }

    { [{2D7E364E-50AC-44EF-BDAB-B01211148D58}];2 ;Filler3             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=17 }

    { [{BFFBF916-69D4-4AA8-A251-A7AC0025FBA1}];2 ;Filler4             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=17 }

    { [{543E1898-27C3-4513-8496-0442550DE28C}];2 ;AdmiGenCde          ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=10 }

    { [{170DC36D-CC52-4847-BC7C-6128A62DDDB7}];2 ;AdmiLaborCde        ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=8 }

    { [{94E76E16-B539-480B-81C5-1F7631E1CE12}];2 ;AdmiMatCde          ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=8 }

    { [{12F5FBB5-6F5F-436C-83E1-4ECC1DA2D78B}];2 ;AdmiPlntCde         ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=8 }

    { [{1B1FC5F8-1F7F-4C82-B7ED-3F94AA0DD390}];2 ;AdmiSubCde          ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=8 }

    { [{C907F6C0-3AE1-469D-80AF-4BC447A05293}];2 ;TenderCde           ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  TenderCde := COPYSTR(TenderCde,1,20);
                                                                                END;

                                                  Width=22 }

    { [{19CE38C2-5B4A-4C0E-92CE-1FEFAD0E123A}];2 ;Filler5             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=13 }

    { [{51549CDF-C2B0-4674-BE66-0F00B7DEAD10}];2 ;PlanCde             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=12 }

    { [{5B0E6E78-ECFE-4073-BEC5-8DB2F1AA4D0F}];2 ;LocationCde         ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=12 }

    { [{A42CEBFC-8629-4723-BB95-5B5D603FA034}];2 ;Filler6             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=8 }

    { [{5D0D13B6-94E6-43BB-BD74-19F5153B860B}];2 ;Filler7             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=8 }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      ProjSetup@1100525079 : Record 315;
      ServOrder@1100525078 : Record 11012823;
      DimValRec@1100525073 : Record 349;
      TmpProjRateRec@1100525072 : TEMPORARY Record 11020231;
      UnitRec@1100525071 : Record 204;
      AdmiRec@1100525070 : Record 11012390;
      RateTableDataTempBlob@1100528600 : TEMPORARY Record 99008535;
      RecRef@1100525002 : RecordRef;
      XMLportStatusDialogMgt@1100525013 : Codeunit 11130006;
      DimMgt@1100525069 : Codeunit 408;
      gBudgetType@1100525065 : Code[3];
      ServOrderNo@1100525055 : Code[20];
      LineCreated@1100525046 : Boolean;
      CmtWithQuantities@1100525044 : Boolean;
      PPriceDec@1100525038 : Decimal;
      MPriceDec@1100525037 : Decimal;
      OPriceDec@1100525036 : Decimal;
      QuantityDec@1100525035 : Decimal;
      NormDec@1100525034 : Decimal;
      SumReached@1100525033 : Boolean;
      gVersion@1100525025 : Text[10];
      LastProcessedExternLineNo@1100525023 : Integer;
      TimeQuantityDec@1100525008 : Decimal;
      SurchPercQuanDec@1100525007 : Decimal;
      SurchPercLaborDec@1100525006 : Decimal;
      SurchPercMaterialDec@1100525005 : Decimal;
      SurchPercPlantDec@1100525004 : Decimal;
      SurchPercSubcDec@1100525003 : Decimal;
      Text001@1100525030 : TextConst 'ENU=Line not coded for cost type %1;SVE=Rad inte kodad f”r kostnadstyp %1';
      Text004@1100525029 : TextConst 'ENU=Cost Object %1 not present for Cost Type %2;SVE=Kostnadsobjekt %1 finns inte f”r kostnadstypen %2';
      Text011@1100525026 : TextConst 'ENU=Cost type %1 does not match Cost Object %2;SVE=Kostnadstypen %1 matchar inte kostnadsobjektet %2';
      Text013@1210190000 : TextConst 'ENU=Cost Object %1 is blocked;SVE=Kostnadsobjekt %1 „r blockerad';
      Text100@1100525011 : TextConst 'ENU=External Line No. %1;SVE=Externt radnr %1';
      Text999@1100525009 : TextConst 'ENU=%1 %2 not present;SVE=%1 %2 finns inte';
      ExternLineNo@1100525001 : Integer;

    PROCEDURE SetSelections@1(ServiceOrderNo@11012000 : Code[20];VAR IRateTableDataTempBlob@1100528600 : TEMPORARY Record 99008535;IVersion@1100485002 : Text[10];IBudgetType@1100485003 : Code[3]);
    BEGIN
      ServOrderNo := ServiceOrderNo;
      gVersion := IVersion;
      gBudgetType := IBudgetType;  //This is Budget Type of TRAD
      RateTableDataTempBlob := IRateTableDataTempBlob;
    END;

    LOCAL PROCEDURE CreateRateTable@1210190000();
    VAR
      InStreamStrm@1210190001 : InStream;
      Txt@1210190002 : Text[250];
      RateTxt@1210190003 : Text[17];
    BEGIN
      RateTableDataTempBlob.Blob.CREATEINSTREAM(InStreamStrm);
      WHILE NOT (InStreamStrm.EOS()) DO BEGIN
        InStreamStrm.READTEXT(Txt);
        TmpProjRateRec.INIT;
        TmpProjRateRec."Project No." := ServOrderNo; //Table is only used as temporary table. So can also be used for import in Service
        TmpProjRateRec."Rate Code" := COPYSTR(Txt,2,2);
        TmpProjRateRec."Starting Date" := 0D;
        RateTxt := COPYSTR(Txt,42,17);
        TmpProjRateRec."Rate (LCY)" := Alphanumeric(RateTxt);
        TmpProjRateRec.INSERT;
      END;
    END;

    LOCAL PROCEDURE Alphanumeric@2(Text@11012001 : Text[250]) Result@11012000 : Decimal;
    VAR
      dummy@11012002 : Decimal;
    BEGIN
      Text := CONVERTSTR(Text, '.', ',');
      IF DELCHR(Text, '=', ' -,0123456789') <> '' THEN EXIT(0);
      IF DELCHR(Text, '=', ' ') = '' THEN EXIT(0);
      EVALUATE(dummy, Text);
      EXIT(dummy);
    END;

    LOCAL PROCEDURE ConvertAmounts@1210190002();
    BEGIN
      QuantityDec := Alphanumeric(QuantityTxt);
      NormDec   := Alphanumeric(NormTxt);
      MPriceDec := Alphanumeric(MatPriceTxt);
      PPriceDec := Alphanumeric(PlntPriceTxt);
      OPriceDec := Alphanumeric(SubPriceTxt);

      TimeQuantityDec := Alphanumeric(TimeQuantityTxt);
      SurchPercQuanDec := Alphanumeric(SurchPercQuanTxt);
      SurchPercLaborDec := Alphanumeric(SurchPercLaborTxt);
      SurchPercMaterialDec := Alphanumeric(SurchPercMaterialTxt);
      SurchPercPlantDec := Alphanumeric(SurchPercPlantTxt);
      SurchPercSubcDec := Alphanumeric(SurchPercSubcTxt);
    END;

    LOCAL PROCEDURE InsertLines@1210190003();
    VAR
      BudgetRateRec@1210190000 : Record 11012000;
    BEGIN
      WITH "Service Order Budget Line" DO BEGIN
        IF NormDec <> 0 THEN BEGIN
          "Cost Type" := "Cost Type"::Labor;
          TmpProjRateRec.GET(ServOrderNo, "Rate Code", 0D);
          IF "Rate Code" <> '' THEN BEGIN
            BudgetRateRec.SETRANGE(Code, "Rate Code");
            BudgetRateRec.SETRANGE("Starting Date", 0D, TODAY);
            BudgetRateRec.SETFILTER("Ending Date",'%1|>=%2',0D, TODAY);
            IF BudgetRateRec.ISEMPTY THEN
              StoreMessage(Description, STRSUBSTNO(Text999, FIELDCAPTION("Rate Code"), "Rate Code"));
          END;
          Rate := TmpProjRateRec."Rate (LCY)" + ((SurchPercLaborDec/100) * TmpProjRateRec."Rate (LCY)");
          Norm := NormDec;
          Price := 0;
          CalcTotals;
          "Cost Object" := FillCostObject("Cost Type");
          "Cost Component" := DefaultCostComponent("Cost Object");
          CheckConvPriceFromLaborToOther();
          "Line No." := GetBudgetLineNo("Service Order Budget Line");
          INSERT(TRUE);
          LineCreated := TRUE;
        END;

        IF MPriceDec <> 0 THEN BEGIN
          "Cost Type" := "Cost Type"::Material;
          Rate := 0;
          Hours := 0;
          Price := MPriceDec + ((SurchPercMaterialDec/100) * MPriceDec);
          "Rate Code" := '';
          Norm := 0;
          CalcTotals;
          "Cost Object" := FillCostObject("Cost Type");
          "Cost Component" := DefaultCostComponent("Cost Object");
          CheckConvPriceFromOtherToLabor();
          "Line No." := GetBudgetLineNo("Service Order Budget Line");
          INSERT(TRUE);
          LineCreated := TRUE;
        END;

        IF PPriceDec <> 0 THEN BEGIN
          "Cost Type" := "Cost Type"::Plant;
          Rate := 0;
          Hours := 0;
          Price := PPriceDec + ((SurchPercPlantDec/100) * PPriceDec);
          "Rate Code" := '';
          Norm := 0;
          CalcTotals;
          "Cost Object" := FillCostObject("Cost Type");
          "Cost Component" := DefaultCostComponent("Cost Object");
          CheckConvPriceFromOtherToLabor();
          "Line No." := GetBudgetLineNo("Service Order Budget Line");
          INSERT(TRUE);
          LineCreated := TRUE;
        END;

        IF OPriceDec <> 0 THEN BEGIN
          "Cost Type" := "Cost Type"::Subcontracting;
          Rate := 0;
          Hours := 0;
          Price := OPriceDec + ((SurchPercSubcDec/100) * OPriceDec);
          "Rate Code" := '';
          Norm := 0;
          CalcTotals;
          "Cost Object" := FillCostObject("Cost Type");
          "Cost Component" := DefaultCostComponent("Cost Object");
          CheckConvPriceFromOtherToLabor();
          "Line No." := GetBudgetLineNo("Service Order Budget Line");
          INSERT(TRUE);
          LineCreated := TRUE;
        END;

      END;
    END;

    LOCAL PROCEDURE InsertTextLines@1210190010();
    BEGIN
      WITH "Service Order Budget Line" DO BEGIN
        "Cost Type" := "Cost Type"::Sundry;
        Rate := 0;
        Hours := 0;
        Price := 0;
        "Rate Code" := '';
        Norm := 0;
        CalcTotals;
        "Cost Object" := '';
        "Cost Component" := '';
        "Line No." := GetBudgetLineNo("Service Order Budget Line");
        INSERT(TRUE);
      END;
    END;

    LOCAL PROCEDURE GetBudgetLineNo@1100409004(BudgetLine@1100409001 : Record 11012875) : Integer;
    VAR
      BudgetLine2@1100409000 : Record 11012875;
    BEGIN
      BudgetLine2.SETRANGE("Budget Type", BudgetLine."Budget Type");
      BudgetLine2.SETRANGE("Service Order No.", BudgetLine."Service Order No.");
      BudgetLine2.SETRANGE("Service Contract No.", BudgetLine."Service Contract No.");
      BudgetLine2.SETRANGE("Object No.", BudgetLine."Object No.");
      BudgetLine2.SETRANGE("Maintenance Scheme", BudgetLine."Maintenance Scheme");
      IF BudgetLine2.FINDLAST THEN
        EXIT(BudgetLine2."Line No." + 10000);
      EXIT(10000);
    END;

    LOCAL PROCEDURE CheckConvPriceFromLaborToOther@1100485009();
    BEGIN
      //* Melding 11194
      //* Als kostensoort gewijzigd is van arbeid in 1 van de 4 ander kostensoorten dan prijs bepalen.
      //* Dat kostensoort gewijzigd is kan alleen via admicode-tabel met 'Gerelateerde kostensoort'.
      WITH "Service Order Budget Line" DO BEGIN
        IF "Cost Type" <> DimValRec."Cost Type"::Labor THEN BEGIN
          Price := Norm * Rate;
          Rate := 0;
          Hours := 0;
          "Rate Code" := '';
          Norm := 0;
          CalcTotals;
        END;
      END;
    END;

    LOCAL PROCEDURE CheckConvPriceFromOtherToLabor@1100485010();
    BEGIN
      //* Melding 11194
      //* Als kostensoort gewijzigd is naar arbeid dan norm en tarief bepalen (kan alleen bij 'Gerelateerde kostensoort').
      //* NB: Als kostensoort wel gewijzigd is, maar het is geen 'Arbeid' dan verder geen actie nodig.
      WITH "Service Order Budget Line" DO BEGIN
        IF "Cost Type" = DimValRec."Cost Type"::Labor THEN BEGIN
          Norm := DimValRec.Norm;
          IF Norm <> 0 THEN
            Rate := Price / Norm
          ELSE BEGIN
            Norm := 1;
            Rate := Price;
          END;
          Price := 0;
          CalcTotals;
        END;
      END;
    END;

    LOCAL PROCEDURE StoreMessage@5(Desc@11012000 : Text[100];Mess@11012001 : Text[250]);
    VAR
      Errors@1210190000 : Record 11012051;
      NextLineNo@11012002 : Integer;
    BEGIN
      WITH Errors DO BEGIN
        SETRANGE("User ID", USERID);
        SETRANGE("Source Type", "Source Type"::"Import Budget");
        IF FIND('+') THEN
          NextLineNo := "Line No." + 1
        ELSE
          NextLineNo := 1;

        INIT;
        "User ID" := USERID;
        "Source Type" := "Source Type"::"Import Budget";
        "Line No." := NextLineNo;
        "Service Order No." := ServOrderNo;
        "Error message" := Mess;
        IF ExternLineNo <> 0 THEN
          "Error message" := "Error message" + ' (' + STRSUBSTNO(Text100,ExternLineNo) + ')';
        Description := Desc;
        INSERT;
      END;
    END;

    LOCAL PROCEDURE FillCostObject@1210190020(VAR CostTypeOpt@1210190000 : Option) lCostObject@1210190001 : Code[20];
    VAR
      lAdmicde@1210190002 : Code[20];
    BEGIN
      CASE ProjSetup."Fill Cost Object with (TRAD)" OF
        ProjSetup."Fill Cost Object with (TRAD)"::"Alternative Code":
          BEGIN
            lAdmicde := AdmiGenCde;
            CheckAdmiSubstituteCostObject(lAdmicde, CostTypeOpt);
            EXIT(GetCostObject(lAdmicde,CostTypeOpt));
          END;
        ProjSetup."Fill Cost Object with (TRAD)"::"Admi Code":
          BEGIN
            CASE CostTypeOpt OF
              DimValRec."Cost Type"::Labor:
                lAdmicde := AdmiLaborCde;
              DimValRec."Cost Type"::Material:
                lAdmicde := AdmiMatCde;
              DimValRec."Cost Type"::Plant:
                lAdmicde := AdmiPlntCde;
              DimValRec."Cost Type"::Subcontracting:
                lAdmicde := AdmiSubCde;
            END;
            CheckAdmiSubstituteCostObject(lAdmicde, CostTypeOpt);
            EXIT(GetCostObject(lAdmicde,CostTypeOpt));
          END;
        ProjSetup."Fill Cost Object with (TRAD)"::"Middel Code":
           EXIT(GetCostObject(MeansCde,CostTypeOpt));
        ProjSetup."Fill Cost Object with (TRAD)"::"Tender Code":
           EXIT(GetCostObject(TenderCde,CostTypeOpt));
      END;
    END;

    LOCAL PROCEDURE CheckAdmiSubstituteCostObject@1100485002(VAR VarAdmicde@1100485000 : Code[20];VAR CostTypeOpt@1100485001 : Option);
    BEGIN
      IF AdmiRec.GET(VarAdmicde) THEN BEGIN
        IF AdmiRec."Admi Related Cost Type" AND (VarAdmicde <> '') THEN BEGIN
          IF AdmiRec."Cost Object Labor" <> '' THEN BEGIN
            CostTypeOpt := DimValRec."Cost Type"::Labor;
            VarAdmicde := AdmiRec."Cost Object Labor";
          END;
          IF AdmiRec."Cost Object Material" <> '' THEN BEGIN
            CostTypeOpt := DimValRec."Cost Type"::Material;
            VarAdmicde := AdmiRec."Cost Object Material";
          END;
          IF AdmiRec."Cost Object Plant" <> '' THEN BEGIN
            CostTypeOpt := DimValRec."Cost Type"::Plant;
            VarAdmicde := AdmiRec."Cost Object Plant";
          END;
          IF AdmiRec."Cost Object Subcontracting" <> '' THEN BEGIN
            CostTypeOpt := DimValRec."Cost Type"::Subcontracting;
            VarAdmicde := AdmiRec."Cost Object Subcontracting";
          END;
          IF AdmiRec."Cost Object Sundry" <> '' THEN BEGIN
            CostTypeOpt := DimValRec."Cost Type"::Sundry;
            VarAdmicde := AdmiRec."Cost Object Sundry";
          END;
          EXIT;
        END;

        CASE CostTypeOpt OF
          DimValRec."Cost Type"::Labor:
            IF AdmiRec."Cost Object Labor" <> '' THEN
              VarAdmicde := AdmiRec."Cost Object Labor";
          DimValRec."Cost Type"::Material:
            IF AdmiRec."Cost Object Material" <> '' THEN
              VarAdmicde := AdmiRec."Cost Object Material";
          DimValRec."Cost Type"::Plant:
            IF AdmiRec."Cost Object Plant" <> '' THEN
              VarAdmicde := AdmiRec."Cost Object Plant";
          DimValRec."Cost Type"::Subcontracting:
            IF AdmiRec."Cost Object Subcontracting" <> '' THEN
              VarAdmicde := AdmiRec."Cost Object Subcontracting";
        END;
      END;
    END;

    LOCAL PROCEDURE GetCostObject@1210190026(ladmicde@1210190000 : Code[20];lCostTypeOpt@1210190001 : Option) Result@1210190002 : Code[20];
    BEGIN
      IF ladmicde = '' THEN BEGIN
        StoreMessage("Service Order Budget Line".Description, STRSUBSTNO(Text001,"Service Order Budget Line"."Cost Type"));
        EXIT('');
      END ELSE BEGIN
        DimMgt.GetDimValueRec(2, ladmicde, DimValRec, FALSE,'');
        IF DimValRec.Code <> '' THEN BEGIN
          IF DimValRec."Cost Type" = lCostTypeOpt THEN BEGIN
            IF DimValRec.Blocked THEN BEGIN
              StoreMessage("Service Order Budget Line".Description, STRSUBSTNO(Text013,ladmicde));
              EXIT('');
            END;
            EXIT(ladmicde)
          END ELSE BEGIN
            StoreMessage("Service Order Budget Line".Description, STRSUBSTNO(Text011,"Service Order Budget Line"."Cost Type",ladmicde));
            EXIT('');
          END;
        END ELSE BEGIN
          StoreMessage("Service Order Budget Line".Description, STRSUBSTNO(Text004,ladmicde,"Service Order Budget Line"."Cost Type"));
          EXIT('');
        END;
      END;
    END;

    LOCAL PROCEDURE TestSummaryStart@1210190008(Text@1210190000 : Text[250]) Result@1210190001 : Boolean;
    BEGIN
      IF Text = '/' THEN
        EXIT(TRUE)
      ELSE
        EXIT(FALSE);
    END;

    LOCAL PROCEDURE TestSummaryEnd@1210190009(Text@1210190000 : Text[250]) Result@1210190001 : Boolean;
    BEGIN
      IF Text = '\' THEN
        EXIT(TRUE)
      ELSE
        EXIT(FALSE);
    END;

    LOCAL PROCEDURE CheckRecordType@1100485008() : Boolean;
    BEGIN
      //* Recordtype KTP=Kostenpost, CMT=Commentaar en ELT=Element
      CASE RecordType OF
        'ELT',
        'KTP',
        'CMT':
          EXIT(TRUE);
      END;

      EXIT(FALSE);
    END;

    LOCAL PROCEDURE CheckSttCode@1100485000() : Boolean;
    BEGIN
      SttCde := DELCHR(SttCde, '<>', ' ');
      CmtWithQuantities := FALSE;

      CASE RecordType OF
        'ELT':
          //IF (SttCde IN ['0','3','16']) THEN  //*C-029234   For ELT lines no check on this code anymore
            EXIT(TRUE);
        'KTP':
          BEGIN
            IF (SttCde IN ['0','1','8','9','10','16','17','24','25','32', '33', '40', '41', '48', '49', '56', '57']) THEN
              EXIT(TRUE);
            IF (SttCde IN ['4','6', '68', '132']) THEN BEGIN
              RecordType := 'CMT';
              CmtWithQuantities := TRUE;
              EXIT(TRUE);
            END;
          END;
        'CMT':
          BEGIN
            IF (SttCde IN ['0','1','2','16','17','18']) THEN
              EXIT(TRUE);
            IF (SttCde = '') AND (ExternLineNo = LastProcessedExternLineNo) THEN
              EXIT(TRUE);
          END;
      END;

      EXIT(FALSE);
    END;

    LOCAL PROCEDURE DefaultCostComponent@1100485004(lvCostObject@1100485000 : Code[20]) lvCostComponent : Code[20];
    VAR
      lvDimValRec@1100485002 : Record 349;
    BEGIN
      lvCostComponent := '';
      IF lvCostObject <> '' THEN BEGIN
        DimMgt.GetDimValueRec(2, lvCostObject, lvDimValRec, FALSE, '');
        IF NOT lvDimValRec.Blocked THEN
          lvCostComponent := lvDimValRec."Cost Component";
      END;
    END;

    LOCAL PROCEDURE FillEmptyTextWithZero@1100525013(VAR Text@1100525000 : Text[1024]);
    BEGIN
      IF DELCHR(Text,'<>=',' ') = '' THEN
        Text := '0';
    END;

    BEGIN
    {
      DP00693  Copied from Project (X11012396). Modified for Import TRAD-TXT in 'Service Order Budget Lines'.

      Unused field from file (Dataport Fields with field 'Filler'):
      Pos  Len  Name
      001 - 01  Spatie
      089 - 03  Doorreken indicatie
      289 - 17  Eenheidsprijs
      306 - 17  Bruto totaal
      387 - 13  EAN artikelcode
      400 - 12  Planningscode
      412 - 12  Locatiecode
      424 - 08  Wijzigingsdatum
      432 - 08  Object ID
    }
    END.
  }
}

