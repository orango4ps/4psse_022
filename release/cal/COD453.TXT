OBJECT Codeunit 453 Job Queue - Enqueue
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW113.00,4PS14.00,4PSSE;
  }
  PROPERTIES
  {
    TableNo=472;
    Permissions=TableData 472=rimd;
    OnRun=BEGIN
            EnqueueJobQueueEntry(Rec);
          END;

  }
  CODE
  {

    PROCEDURE EnqueueJobQueueEntry@4(VAR JobQueueEntry@1000 : Record 472);
    VAR
      SavedStatus@1001 : Option;
      User@1100285100 : Record 2000000120;
      ScheduledTask@1100285101 : Record 2000000175;
      UserPersonalization@1100285102 : Record 2000000073;
    BEGIN
      WITH JobQueueEntry DO BEGIN
        SavedStatus := Status;
        InitEntryForSchedulerWithDelayInSec(JobQueueEntry,1);
        IF ISNULLGUID(ID) THEN
          INSERT(TRUE)
        ELSE BEGIN
          IF CanScheduleTask(JobQueueEntry) THEN
            CancelTask; // clears "System Task ID"
          MODIFY;
        END;

        IF CanScheduleTask(JobQueueEntry) AND NOT UpdateStatusOnHoldWithInactivityTimeout(JobQueueEntry,SavedStatus) THEN
          "System Task ID" := ScheduleTask;

        IF NOT ISNULLGUID("System Task ID") THEN BEGIN
          IF SavedStatus = Status::"On Hold with Inactivity Timeout" THEN
            Status := SavedStatus
          ELSE
            Status := Status::Ready;
          MODIFY;
        END;
      END;

      //<<4PSSE
      User.SETRANGE("User Name",JobQueueEntry."User ID");
      IF ScheduledTask.GET(JobQueueEntry."System Task ID") AND User.FINDFIRST THEN BEGIN
        ScheduledTask."User ID" := User."User Security ID";
        ScheduledTask."User Name" := User."User Name";
        IF UserPersonalization.GET(User."User Security ID") THEN BEGIN
          ScheduledTask."User Language ID" := UserPersonalization."Language ID";
          ScheduledTask."User Format ID" := UserPersonalization."Locale ID";
          ScheduledTask."User Time Zone" := UserPersonalization."Time Zone";
          JobQueueEntry."User Language ID" := UserPersonalization."Language ID";
        END;
        ScheduledTask.MODIFY;
      END;
      //<<4PSSE
      OnAfterEnqueueJobQueueEntry(JobQueueEntry);
    END;

    LOCAL PROCEDURE InitEntryForSchedulerWithDelayInSec@3(VAR JobQueueEntry@1001 : Record 472;DelayInSec@1000 : Integer);
    BEGIN
      WITH JobQueueEntry DO BEGIN
        Status := Status::"On Hold";
        "User Session Started" := 0DT;
        IF "Earliest Start Date/Time" < (CURRENTDATETIME + 1000) THEN
          "Earliest Start Date/Time" := CURRENTDATETIME + DelayInSec * 1000;
      END;
    END;

    LOCAL PROCEDURE UpdateStatusOnHoldWithInactivityTimeout@1(VAR JobQueueEntry@1000 : Record 472;SavedStatus@1001 : Integer) : Boolean;
    BEGIN
      WITH JobQueueEntry DO
        IF (SavedStatus = Status::"On Hold with Inactivity Timeout") AND ("Inactivity Timeout Period" = 0) THEN BEGIN
          IF Status <> Status::"On Hold with Inactivity Timeout" THEN BEGIN
            Status := Status::"On Hold with Inactivity Timeout";
            MODIFY;
          END;
          EXIT(TRUE);
        END;

      EXIT(FALSE);
    END;

    LOCAL PROCEDURE CanScheduleTask@5(VAR JobQueueEntry@1001 : Record 472) : Boolean;
    VAR
      DoNotScheduleTask@1000 : Boolean;
    BEGIN
      OnBeforeJobQueueScheduleTask(JobQueueEntry,DoNotScheduleTask);
      EXIT(NOT DoNotScheduleTask);
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterEnqueueJobQueueEntry@6(VAR JobQueueEntry@1000 : Record 472);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeJobQueueScheduleTask@2(VAR JobQueueEntry@1001 : Record 472;VAR DoNotScheduleTask@1000 : Boolean);
    BEGIN
    END;

    BEGIN
    {
      ORANGO.DL EnqueueJobQueueEntry: global no->yes
    }
    END.
  }
}

