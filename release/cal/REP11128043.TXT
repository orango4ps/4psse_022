OBJECT Report 11128043 Copy Cust. Co Ob Wag Sls Price
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PSSE;
  }
  PROPERTIES
  {
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 6836;    ;DataItem;                    ;
               DataItemTable=Table18;
               OnPreDataItem=BEGIN
                               NoOfUpdates := 0;

                               IF NewStartingDate = 0D THEN
                                 ERROR(Text003);

                               ToCustFilter := GETFILTER("No.");
                               IF (FromCustNo = '') AND (ToCustFilter = '') THEN
                                 ERROR(Text002);

                               IF FromCustNo = '' THEN
                                 UseBasePriceTable := TRUE
                               ELSE
                                 UseBasePriceTable := FALSE;

                               IF ToCustFilter = '' THEN
                                 SETRANGE("No.",FromCustNo);

                               CostObjWageCompBuffer.DELETEALL;

                               IF UseBasePriceTable THEN BEGIN

                                 //////////////////////////////////////////////
                                 // From Base table
                                 IF CostObjWageCompSPrice.FINDSET THEN REPEAT

                                   CostObjWageCompBuffer.SETRANGE("Cost Object",CostObjWageCompSPrice."Cost Object");
                                   CostObjWageCompBuffer.SETRANGE("Wage Component",CostObjWageCompSPrice."Wage Component");
                                   IF NOT CostObjWageCompBuffer.FINDFIRST THEN BEGIN
                                     CostObjWageCompBuffer.INIT;
                                     CostObjWageCompBuffer."Cost Object" := CostObjWageCompSPrice."Cost Object";
                                     CostObjWageCompBuffer."Wage Component" := CostObjWageCompSPrice."Wage Component";
                                     CostObjWageCompBuffer."Sales Price" := CostObjWageCompSPrice."Sales Price";
                                     CostObjWageCompBuffer.INSERT;
                                   END ELSE BEGIN
                                     // Update if newer price
                                     CostObjWageCompBuffer."Sales Price" := CostObjWageCompSPrice."Sales Price";
                                     CostObjWageCompBuffer.MODIFY;
                                   END;
                                 UNTIL CostObjWageCompSPrice.NEXT = 0;

                               END ELSE BEGIN

                                 //////////////////////////////////////////////
                                 // From within same table
                                 IF From_CustCostObWageSPrice.FINDSET THEN REPEAT

                                   CostObjWageCompBuffer.SETRANGE("Cost Object",From_CustCostObWageSPrice."Cost Object");
                                   CostObjWageCompBuffer.SETRANGE("Wage Component",From_CustCostObWageSPrice."Wage Component");
                                   IF NOT CostObjWageCompBuffer.FINDFIRST THEN BEGIN
                                     CostObjWageCompBuffer."Cost Object" := From_CustCostObWageSPrice."Cost Object";
                                     CostObjWageCompBuffer."Wage Component" := From_CustCostObWageSPrice."Wage Component";
                                     CostObjWageCompBuffer."Sales Price" := From_CustCostObWageSPrice."Sales Price";
                                     CostObjWageCompBuffer.INSERT;
                                   END ELSE BEGIN
                                     // Update if newer price
                                     CostObjWageCompBuffer."Sales Price" := From_CustCostObWageSPrice."Sales Price";
                                     CostObjWageCompBuffer.MODIFY;
                                   END;

                                 UNTIL From_CustCostObWageSPrice.NEXT = 0;

                               END;

                               CustCostObWageSPriceBuffer.DELETEALL;
                             END;

               OnAfterGetRecord=BEGIN
                                  //////////////////////////////////////////////
                                  // From Base price table
                                  CostObjWageCompBuffer.RESET;

                                  IF CostObjWageCompBuffer.FINDSET THEN REPEAT

                                    // Insert new line with new date and price in the buffer
                                    CustCostObWageSPriceBuffer.INIT;
                                    CustCostObWageSPriceBuffer.VALIDATE("Customer No.","No.");
                                    CustCostObWageSPriceBuffer.VALIDATE("Cost Object",CostObjWageCompBuffer."Cost Object");
                                    CustCostObWageSPriceBuffer.VALIDATE("Wage Component",CostObjWageCompBuffer."Wage Component");
                                    CustCostObWageSPriceBuffer.VALIDATE("Starting Date",NewStartingDate);

                                    CASE UpdateOption OF
                                    UpdateOption::"Fixed Amount":
                                      NewAmount := CostObjWageCompBuffer."Sales Price" + NewValue;
                                    UpdateOption::Percent:
                                      NewAmount := CostObjWageCompBuffer."Sales Price" * ((100 + NewValue) / 100);
                                    END;

                                    IF RoundAmount <> 0 THEN
                                      NewAmount := ROUND(NewAmount, RoundAmount)
                                    ELSE
                                      NewAmount := ROUND(NewAmount, 0.01);

                                    IF NewAmount < 0 THEN
                                      IF NOT CONFIRM(STRSUBSTNO(Text001,
                                        NewAmount,
                                        CustCostObWageSPriceBuffer."Customer No.",
                                        CustCostObWageSPriceBuffer."Cost Object",
                                        CustCostObWageSPriceBuffer."Wage Component")) THEN
                                        ERROR(Text007);

                                    CustCostObWageSPriceBuffer.VALIDATE("Sales Price",NewAmount);
                                    CustCostObWageSPriceBuffer.INSERT;

                                  UNTIL CostObjWageCompBuffer.NEXT = 0;
                                END;

               OnPostDataItem=BEGIN
                                // Update the real table from the buffer
                                CustCostObWageSPriceBuffer.RESET;
                                IF CustCostObWageSPriceBuffer.FINDSET THEN REPEAT
                                  To_CustCostObWageSPrice.SETRANGE("Customer No.",CustCostObWageSPriceBuffer."Customer No.");
                                  To_CustCostObWageSPrice.SETRANGE("Cost Object",CustCostObWageSPriceBuffer."Cost Object");
                                  To_CustCostObWageSPrice.SETRANGE("Wage Component",CustCostObWageSPriceBuffer."Wage Component");
                                  To_CustCostObWageSPrice.SETRANGE("Starting Date",CustCostObWageSPriceBuffer."Starting Date");
                                  IF NOT To_CustCostObWageSPrice.FINDFIRST THEN BEGIN

                                    To_CustCostObWageSPrice.VALIDATE("Customer No.",CustCostObWageSPriceBuffer."Customer No.");
                                    To_CustCostObWageSPrice.VALIDATE("Cost Object",CustCostObWageSPriceBuffer."Cost Object");
                                    To_CustCostObWageSPrice.VALIDATE("Wage Component",CustCostObWageSPriceBuffer."Wage Component");
                                    To_CustCostObWageSPrice.VALIDATE("Starting Date",CustCostObWageSPriceBuffer."Starting Date");
                                    To_CustCostObWageSPrice.VALIDATE("Sales Price",CustCostObWageSPriceBuffer."Sales Price");
                                    To_CustCostObWageSPrice.INSERT(TRUE);
                                    NoOfUpdates += 1;
                                  END;

                                UNTIL CustCostObWageSPriceBuffer.NEXT = 0;

                                MESSAGE(Text006,NoOfUpdates);
                              END;

               ReqFilterFields=No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100285005;;Container;
                  CaptionML=[ENU=Options;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  ContainerType=ContentArea }

      { 1100285004;1;Field  ;
                  CaptionML=[ENU=Update Option;
                             NOR=Oppdater Alternativ;
                             SVE=Uppdateringsalternativ];
                  OptionCaptionML=[ENU=Fixed Amount,Percent;
                                   NOR=Fast bel›p,Prosent;
                                   SVE=Fast belopp,Procent];
                  SourceExpr=UpdateOption }

      { 1100285003;1;Field  ;
                  CaptionML=[ENU=From Cust. No.;
                             NOR=Fra Kundenr;
                             SVE=Fr†n Kundnr];
                  SourceExpr=FromCustNo;
                  TableRelation=Customer;
                  OnValidate=BEGIN
                               IF FromCustNo <> '' THEN BEGIN
                                 From_CustCostObWageSPrice.SETRANGE("Customer No.",FromCustNo);
                                 IF NOT From_CustCostObWageSPrice.FINDLAST THEN
                                   ERROR(STRSUBSTNO(Text005,FromCustNo));
                               END;
                             END;
                              }

      { 1100285002;1;Field  ;
                  CaptionML=[ENU=Update w. Amount/Percentage;
                             NOR=Oppdater med bel›p/Prosent;
                             SVE=Uppdatera med belopp/procent];
                  SourceExpr=NewValue }

      { 1100285001;1;Field  ;
                  CaptionML=[ENU=Round Amount;
                             NOR=Avrundet bel›p;
                             SVE=Avrundat belopp];
                  SourceExpr=RoundAmount }

      { 1100285000;1;Field  ;
                  CaptionML=[ENU=New Starting Date;
                             NOR=Ny Startdato;
                             SVE=Nytt startdatum];
                  SourceExpr=NewStartingDate }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      From_CustCostObWageSPrice@1100285008 : Record 11020379;
      CustCostObWageSPriceBuffer@1100285022 : TEMPORARY Record 11020379;
      To_CustCostObWageSPrice@1100285011 : Record 11020379;
      CostObjWageCompSPrice@1100285000 : Record 11020658;
      CostObjWageCompBuffer@1100285012 : TEMPORARY Record 11020658;
      ToCustFilter@1100285023 : Text[250];
      FromCustNo@1100285024 : Code[20];
      UpdateOption@1100285007 : 'Fixed Amount,Percent';
      NoOfUpdates@1100285006 : Integer;
      NewAmount@1100285005 : Decimal;
      NewValue@1100285004 : Decimal;
      RoundAmount@1100285003 : Decimal;
      NewStartingDate@1100285002 : Date;
      UseBasePriceTable@1100285001 : Boolean;
      Text001@1100285020 : TextConst 'ENU=There will be a negative amount (%1) for Project No. %2, Cost Object %3. Would you like to proceed anyway?;NOR=Bel›pet kommer til † bli negativt (%1) for prosjektnr %2, Kostobjekt %3. Vil du fortsette likevel?;SVE=Beloppet kommer att bli negativt (%1) f”r projektnr %2, kostnadsobjekt %3. Vill du „nd† forts„tta?';
      Text002@1100285019 : TextConst 'ENU=From Project No. cannot be blank if To Project No. is blank.;NOR=Fra prosjektnr kan ikke v‘re blankt hivs Til prosjektnr er blankt.;SVE=Fr†nprojektnr f†r inte l„mnas blankt om Tillprojektnr inte anges.';
      Text003@1100285018 : TextConst 'ENU=New Starting Date must not be blank.;NOR=Ny Startdato kan ikke v‘re blank.;SVE=Nytt startdatum f†r inte vara blankt.';
      Text005@1100285017 : TextConst 'ENU=There is no information for Project No. %1 in the table.;NOR=Det finnes ingen informasjon i tabellen for prosjektnr %1.;SVE=Det finns ingen information i tabellen f”r projektnr %1.';
      Text006@1100285016 : TextConst 'ENU=%1 new line(s) were inserted.;NOR=%1 ny(e) rad(er) ble satt inn.;SVE=%1 ny(a) rad(er) skapades.';
      Text007@1100285014 : TextConst 'ENU=The operation was aborted.;NOR=Operasjonen ble avbrutt.;SVE=Operationen avbr”ts.';

    BEGIN
    {
      4PSSE
      130910 ITERO.DJ New ProcessingOnly report to copy data from tbl 11020658 to tbl 11020379
    }
    END.
  }
  RDLDATA
  {
  }
}

