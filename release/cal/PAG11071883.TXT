OBJECT Page 11071883 Set-up Out of Office 4PS
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Einrichtung Out of Office 4PS;
               ENU=Set-up Out of Office 4PS;
               NLD=Stel afwezigheid in 4PS;
               SVE=Fr†nvaroinst„llningar 4PS];
    OnOpenPage=VAR
                 UserSetup@161024012 : Record 91;
               BEGIN
                 IF NOT ApprovalManagement4PSConstr.GetOutOfOffice(USERID,ToUserId,FromDate,ToDate) THEN BEGIN
                   IF UserSetup.GET(USERID) THEN
                     ToUserId := UserSetup."Approver ID";
                 END;

                 ToUserName := GetApproverName;
               END;

  }
  CONTROLS
  {
    { 161024012;;Container;
                ContainerType=ContentArea }

    { 161024013;1;Group   ;
                GroupType=Group }

    { 161024014;2;Field   ;
                CaptionML=[DEU=Weiterleiten ab;
                           ENU=Forward from date;
                           NLD=Doorsturen vanafdatum];
                SourceExpr=FromDate;
                OnValidate=BEGIN
                             UpdateOutOfOffice;
                           END;
                            }

    { 161024015;2;Field   ;
                CaptionML=[DEU=Weiterleiten bis;
                           ENU=Forward to date;
                           NLD=Doorsturen totdatum];
                SourceExpr=ToDate;
                OnValidate=BEGIN
                             UpdateOutOfOffice;
                           END;
                            }

    { 161024016;2;Field   ;
                CaptionML=[DEU=Weiterleiten an;
                           ENU=Forward to;
                           NLD=Doorsturen naar];
                SourceExpr=ToUserId;
                OnValidate=VAR
                             UserSetup@161024013 : Record 91;
                           BEGIN
                             IF UPPERCASE(ToUserId) = UPPERCASE(USERID) THEN
                               ERROR(Text001);

                             IF ToUserId <> '' THEN
                               UserSetup.GET(ToUserId);

                             UpdateOutOfOffice;
                             ToUserName := GetApproverName
                           END;

                OnLookup=VAR
                           Approver@1100529801 : Record 11012229;
                           UserSetup@1100528501 : Record 91;
                         BEGIN
                           IF Approver.FINDFIRST THEN
                             REPEAT
                               IF UserSetup.GET(Approver."User ID") THEN
                                 IF UserSetup."Approver ID" <> '' THEN
                                   Approver.MARK(TRUE);
                             UNTIL Approver.NEXT = 0;
                           Approver.MARKEDONLY(TRUE);
                           IF PAGE.RUNMODAL(0, Approver) = ACTION::LookupOK THEN BEGIN
                             Text := Approver."User ID";
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 161024017;2;Field   ;
                CaptionML=[DEU=Weiterleiten an Name;
                           ENU=Forward to name;
                           NLD=Doorsturen naar naam];
                SourceExpr=ToUserName;
                Editable=FALSE }

  }
  CODE
  {
    VAR
      ApprovalManagement4PSConstr@1100525000 : Codeunit 11125349;
      FromDate@161024014 : Date;
      ToDate@161024013 : Date;
      ToUserId@161024012 : Code[50];
      Text001@161024016 : TextConst 'DEU=Sie k”nnen Dokumente nicht an sich selbst weiterleiten.;ENU=You can not forward documents to yourself;NLD=U kunt geen document naar uzelf doorsturen.;SVE="Det g†r inte att vidarebefordra dokument till sig sj„lv "';
      ToUserName@161024017 : Text[250];

    PROCEDURE UpdateOutOfOffice@161024035();
    BEGIN
      ApprovalManagement4PSConstr.SetOutOfOffice(USERID,ToUserId,FromDate,ToDate);
    END;

    PROCEDURE GetApproverName@161024014() : Text[1024];
    VAR
      UserSetup@161024012 : Record 91;
      Purchaser@161024013 : Record 13;
    BEGIN
      IF UserSetup.GET(ToUserId) THEN
        IF Purchaser.GET(UserSetup."Salespers./Purch. Code") THEN;

      IF Purchaser.Name <> '' THEN
        EXIT(Purchaser.Name);
    END;

    BEGIN
    END.
  }
}

