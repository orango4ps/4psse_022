OBJECT Page 11127210 Resource Planning View
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Ressourcenplanung Wiedergabe;
               ENU=Resource Planning View;
               NLD=Resourceplanning Weergave];
    SourceTable=Table11124917;
    DelayedInsert=Yes;
    SourceTableView=SORTING(User ID,Code,Line No.);
    PageType=List;
    ShowFilter=No;
    OnOpenPage=BEGIN
                 FILTERGROUP(9);
                 SETRANGE("User ID", USERID);
                 SETRANGE(Code, gResourcePlanningViewNo);
                 FILTERGROUP(0);
               END;

    OnInsertRecord=BEGIN
                     "User ID" := USERID;
                     Code := gResourcePlanningViewNo;
                   END;

    OnQueryClosePage=BEGIN
                       CurrPage.UPDATE;
                       CheckGroup;
                     END;

    ActionList=ACTIONS
    {
      { 1100525002;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100525007;1 ;Action    ;
                      Name=MoveDownAction;
                      CaptionML=[DEU=Runter;
                                 ENU=Move down;
                                 NLD=Omlaag];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=MoveDown;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ResourcePlanningViewLine@1100525001 : Record 11124917;
                                 TempResourcePlanningViewLine@1100525000 : TEMPORARY Record 11124917;
                                 LastEntryNo@1100525004 : Integer;
                               BEGIN
                                 TempResourcePlanningViewLine.RESET;
                                 TempResourcePlanningViewLine.DELETEALL;
                                 CurrPage.SETSELECTIONFILTER(ResourcePlanningViewLine);
                                 ResourcePlanningViewLine.SETCURRENTKEY("User ID",Code,"Line No.");
                                 IF ResourcePlanningViewLine.FINDSET THEN BEGIN
                                   REPEAT
                                     TempResourcePlanningViewLine := ResourcePlanningViewLine;
                                     TempResourcePlanningViewLine.INSERT;
                                   UNTIL ResourcePlanningViewLine.NEXT = 0;
                                   LastEntryNo := ResourcePlanningViewLine."Entry No.";
                                 END;
                                 TempResourcePlanningViewLine.RESET;
                                 TempResourcePlanningViewLine.SETCURRENTKEY("User ID",Code,"Line No.");
                                 TempResourcePlanningViewLine.SETASCENDING("Line No.", FALSE);
                                 IF TempResourcePlanningViewLine.FINDSET THEN BEGIN
                                   REPEAT
                                     ResourcePlanningViewLine.GET(TempResourcePlanningViewLine."Entry No.");
                                     ResourcePlanningViewLine.MoveDown;
                                   UNTIL TempResourcePlanningViewLine.NEXT = 0;
                                 END;
                                 GET(LastEntryNo);
                               END;
                                }
      { 1100525003;1 ;Action    ;
                      Name=MoveUpAction;
                      CaptionML=[DEU=Auf;
                                 ENU=Move up;
                                 NLD=Omhoog];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=MoveUp;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ResourcePlanningViewLine@1100525000 : Record 11124917;
                                 TempResourcePlanningViewLine@1100525001 : TEMPORARY Record 11124917;
                               BEGIN
                                 TempResourcePlanningViewLine.RESET;
                                 TempResourcePlanningViewLine.DELETEALL;
                                 CurrPage.SETSELECTIONFILTER(ResourcePlanningViewLine);
                                 ResourcePlanningViewLine.SETCURRENTKEY("User ID",Code,"Line No.");
                                 IF ResourcePlanningViewLine.FINDSET THEN BEGIN
                                   REPEAT
                                     TempResourcePlanningViewLine := ResourcePlanningViewLine;
                                     TempResourcePlanningViewLine.INSERT;
                                   UNTIL ResourcePlanningViewLine.NEXT = 0;
                                 END;
                                 TempResourcePlanningViewLine.RESET;
                                 TempResourcePlanningViewLine.SETCURRENTKEY("User ID",Code,"Line No.");
                                 IF TempResourcePlanningViewLine.FINDSET THEN BEGIN
                                   REPEAT
                                     ResourcePlanningViewLine.GET(TempResourcePlanningViewLine."Entry No.");
                                     ResourcePlanningViewLine.MoveUp(0);
                                   UNTIL TempResourcePlanningViewLine.NEXT = 0;
                                 END;
                                 GET(TempResourcePlanningViewLine."Entry No.");
                               END;
                                }
      { 1100525010;1 ;Action    ;
                      Name=UpdateView;
                      CaptionML=[DEU=Aktualisieren;
                                 ENU=Update;
                                 NLD=Bijwerken];
                      Promoted=Yes;
                      Visible=NOT gEditable;
                      PromotedIsBig=Yes;
                      Image=Refresh;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ResourcePlanningViewMgt@1100525000 : Codeunit 11124905;
                               BEGIN
                                 ResourcePlanningViewMgt.InitView(gResourcePlanningViewNo);
                                 CurrPage.UPDATE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100525000;;Container;
                Name=ResourcePlanningGroup;
                CaptionML=[DEU=Ressourcenplanung Gruppe;
                           ENU=Resource Planning Group;
                           NLD=Resourceplanning groep];
                ContainerType=ContentArea }

    { 1100525004;1;Group  ;
                Name=Resources;
                CaptionML=[DEU=Ressourcen;
                           ENU=Resources;
                           NLD=Resources];
                IndentationColumnName=IndentPos;
                IndentationControls=No.;
                GroupType=Repeater }

    { 1100525009;2;Field  ;
                SourceExpr="Line No.";
                Editable=False }

    { 1100525005;2;Field  ;
                SourceExpr=Type;
                Style=Strong;
                StyleExpr=Type = Type::Group }

    { 1100525006;2;Field  ;
                SourceExpr="No.";
                Editable=Type <> Type::Group;
                Style=Strong;
                StyleExpr=Type = Type::Group;
                OnValidate=VAR
                             ResourceWOP@1100525000 : Record 11229278;
                             ResourceGroupWOP@1100525001 : Record 11229384;
                             ResourcePlanningView@1100525002 : Record 11124916;
                           BEGIN
                             CASE Type OF
                               Type::Resource:
                                 BEGIN
                                   ResourceWOP.SETRANGE("Visible in GRP", TRUE);
                                   ResourceWOP.SETFILTER("User ID Filter", USERID);
                                   ResourceWOP.SetCompanyPlanGroupFilters;
                                   ResourceWOP.GET("No.");
                                   Description := ResourceWOP.Name;
                                 END;
                               Type::ResourceGroup:
                                 BEGIN
                                   ResourceGroupWOP.SETFILTER("User ID Filter", USERID);
                                   ResourceGroupWOP.SETFILTER("Company Filter", COMPANYNAME);
                                   ResourceGroupWOP.SETRANGE("Visible in GRP", TRUE);
                                   ResourceGroupWOP.GET("No.");
                                   Description := ResourceGroupWOP.Name;
                                 END;
                               Type::ResourceView:
                                 BEGIN
                                   ResourcePlanningView.GET(USERID, "No.");
                                   Description := ResourcePlanningView.Description;
                                 END;
                               ELSE
                                 ;
                             END;
                           END;

                OnLookup=VAR
                           ResourceWOP@1100525003 : Record 11229278;
                           ResourceWOP2@1100525005 : Record 11229278;
                           LinkedResource@1100525002 : Record 11229324;
                           ResourceGroupWOP@1100525008 : Record 11229384;
                           ResourcePlanningView@1100525009 : Record 11124916;
                           ResourcePlanningViewLine@1100525001 : Record 11124917;
                           NewLineNo@1100525000 : Integer;
                           Confirmed@1100525006 : Boolean;
                           Skip@1100525007 : Boolean;
                           MessageString@1100525010 : Text;
                         BEGIN
                           CASE Type OF
                             Type::Resource:
                               BEGIN
                                 //ResourceWOP.FILTERGROUP(9);
                                 ResourceWOP.SETRANGE("Visible in GRP", TRUE);
                                 ResourceWOP.SETFILTER("User ID Filter", USERID);
                                 ResourceWOP.SetCompanyPlanGroupFilters;
                                 //ResourceWOP.FILTERGROUP(0);
                                 IF PAGE.RUNMODAL(PAGE::"Resources (WOP)", ResourceWOP) = ACTION::LookupOK THEN BEGIN
                                   "No." := ResourceWOP."No.";
                                   Description := ResourceWOP.Name;
                                   LinkedResource.SETRANGE("Main Resource No.", "No.");
                                   IF LinkedResource.FINDSET THEN BEGIN
                                     CurrPage.UPDATE;
                                     NewLineNo := Rec."Line No.";
                                     REPEAT
                                       ResourceWOP2.RESET;
                                       ResourceWOP2.SETFILTER("User ID Filter", USERID);
                                       ResourceWOP2.SetCompanyPlanGroupFilters;
                                       ResourceWOP2.SETRANGE("No.", LinkedResource."Resource No.");
                                       IF ResourceWOP2.FINDFIRST THEN BEGIN
                                         Skip := FALSE;
                                         IF NOT Confirmed THEN
                                           IF CONFIRM(Text001) THEN
                                             Confirmed := TRUE
                                           ELSE
                                             Skip := TRUE;
                                         IF Confirmed THEN BEGIN
                                           ResourcePlanningViewLine.INIT;
                                           ResourcePlanningViewLine."User ID" := USERID;
                                           ResourcePlanningViewLine.Code := Code;
                                           ResourcePlanningViewLine.Type := ResourcePlanningViewLine.Type::Resource;
                                           ResourcePlanningViewLine."No." := ResourceWOP2."No.";
                                           ResourcePlanningViewLine.Description := ResourceWOP2.Name;
                                           ResourcePlanningViewLine.IndentPos := 1;
                                           ResourcePlanningViewLine.Indent := TRUE;
                                           ResourcePlanningViewLine.INSERT(TRUE);
                                           IF (ResourcePlanningViewLine."Line No." - 1) <> NewLineNo THEN
                                             ResourcePlanningViewLine.MoveUp(NewLineNo + 1);
                                           NewLineNo += 1;
                                         END;
                                       END;
                                     UNTIL (LinkedResource.NEXT = 0) OR Skip;
                                     CurrPage.UPDATE;
                                   END;
                                 END;
                               END;
                             Type::ResourceGroup:
                               BEGIN
                                 //ResourceGroupWOP.FILTERGROUP(9);
                                 ResourceGroupWOP.SetCompanyPlanGroupFilters;
                                 ResourceGroupWOP.SETFILTER("User ID Filter", USERID);
                                 ResourceGroupWOP.SETFILTER("Company Filter", COMPANYNAME);
                                 ResourceGroupWOP.SETRANGE("Visible in GRP", TRUE);
                                 //ResourceGroupWOP.FILTERGROUP(0);
                                 IF PAGE.RUNMODAL(PAGE::"Resource Group List (WOP)", ResourceGroupWOP) = ACTION::LookupOK THEN BEGIN
                                   "No." := ResourceGroupWOP."No.";
                                   Description := ResourceGroupWOP.Name;
                                 END;
                               END;
                             Type::ResourceView:
                               BEGIN
                                 //ResourcePlanningView.FILTERGROUP(9);
                                 ResourcePlanningView.SETRANGE("User ID", USERID);
                                 ResourcePlanningView.SETFILTER(Code, '<> %1', gResourcePlanningViewNo);
                                 //ResourcePlanningView.FILTERGROUP(0);
                                 IF PAGE.RUNMODAL(PAGE::"Resource Planning Views", ResourcePlanningView) = ACTION::LookupOK THEN BEGIN
                                   "No." := ResourcePlanningView.Code;
                                   Description := ResourcePlanningView.Description;
                                   ResourcePlanningViewLine.RESET;
                                   ResourcePlanningViewLine.SETRANGE("User ID", USERID);
                                   ResourcePlanningViewLine.SETRANGE(Code, ResourcePlanningView.Code);
                                   ResourcePlanningViewLine.SETRANGE(Type, ResourcePlanningViewLine.Type::ResourceView);
                                   IF ResourcePlanningViewLine.FINDSET THEN BEGIN
                                     REPEAT
                                       MessageString += '\' + ResourcePlanningViewLine.Description;
                                     UNTIL ResourcePlanningViewLine.NEXT = 0;
                                     MESSAGE(Text002 + MessageString);
                                   END;
                                 END;
                               END;
                             ELSE
                               ;
                           END;
                         END;
                          }

    { 1100525001;2;Field  ;
                SourceExpr=Description;
                Editable=Type = Type::Group;
                Style=Strong;
                StyleExpr=Type = Type::Group;
                OnValidate=BEGIN
                             IF Type = Type::Group THEN
                               "No." := FORMAT(Description, 20);
                           END;
                            }

    { 1100525008;2;Field  ;
                SourceExpr=Indent;
                Editable=(Type <> Type::Group) AND (Type <> Type::ResourceView) }

  }
  CODE
  {
    VAR
      gResourcePlanningViewNo@1100525001 : Code[20];
      Text001@1100525003 : TextConst 'DEU=VerknÅpfte Ressourcen hinzufÅgen?;ENU=Add Linked Resources?;NLD=Gekoppelde resources toevoegen?';
      gEditable@1100525004 : Boolean;
      Text002@1100525005 : TextConst 'DEU=Die folgenden Wiedergaben innerhalb dieser Wiedergabe werden nicht in der Ressourcenplanung angezeigt:\;ENU=The Following Views Inside theis View Will Not be visable in the Resource Planning:\;NLD=De volgende weergaven binnen deze weergave worden niet getoond in de Resourceplanning:\';

    PROCEDURE SetGlobals@1100525000(iResourcePlanningViewNo@1100525001 : Code[20];iEditable@1100525002 : Boolean);
    BEGIN
      gResourcePlanningViewNo := iResourcePlanningViewNo;
      gEditable := iEditable;
    END;

    BEGIN
    END.
  }
}

