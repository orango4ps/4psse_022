OBJECT Page 11129115 Grouped Plant Comments SF
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=;
    SourceTable=Table11072666;
    PageType=ListPart;
    OnAfterGetRecord=BEGIN
                       GetCommentGroup;
                     END;

  }
  CONTROLS
  {
    { 1100528000;0;Container;
                ContainerType=ContentArea }

    { 1100528001;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1100528009;2;Field  ;
                Name=SeqNo;
                SourceExpr=GroupSeqNo }

    { 1100528002;2;Field  ;
                Name=CreationDate;
                SourceExpr=GroupDate }

    { 1100528003;2;Field  ;
                Name=CreatedBy;
                SourceExpr="Created by" }

    { 1100528008;2;Field  ;
                Name=Comment;
                SourceExpr=GroupComment }

    { 1100526101;2;Field  ;
                Name=CreatedByName;
                SourceExpr=GetCreatedByName;
                Visible=FALSE }

  }
  CODE
  {
    VAR
      GroupSeqNo@1100528002 : Integer;
      GroupComment@1100528000 : Text;
      GroupDate@1100528001 : DateTime;

    LOCAL PROCEDURE GetCommentGroup@1100528002();
    VAR
      PrevEndsWithLineBreak@1100528000 : Boolean;
    BEGIN
      GroupSeqNo := GroupSeqNo + 1;
      GroupDate := CREATEDATETIME(Date, "Time Created");
      GroupComment := Comment;

      WHILE IsNextLineSameGroup DO BEGIN
        IF "Line Break" THEN
          GroupComment := GroupComment + CRLF;
        PrevEndsWithLineBreak := "Line Break";

        NEXT;

        IF NOT PrevEndsWithLineBreak THEN
          GroupComment := GroupComment + ' ';
        GroupComment := GroupComment + Comment;
      END;
    END;

    LOCAL PROCEDURE IsNextLineSameGroup@1100528001() : Boolean;
    VAR
      NextLine@1100528001 : Record 11072666;
    BEGIN
      NextLine.COPY(Rec);

      IF NextLine.NEXT = 0 THEN
        EXIT(FALSE);

      IF (NextLine."Created by" <> "Created by") OR (NextLine."Created by App User" <> "Created by App User") THEN
        EXIT(FALSE);

      IF (NextLine.Date <> 0D) AND (NextLine.Date <> Date) THEN
        EXIT(FALSE);

      IF (NextLine."Time Created" <> 0T) AND (ABS(NextLine."Time Created" - "Time Created") > 60000) THEN
        EXIT(FALSE);

      EXIT(TRUE)
    END;

    LOCAL PROCEDURE CRLF@1100528003() ReturnValue : Text[2];
    BEGIN
      ReturnValue[1] := 13;
      ReturnValue[2] := 10;
    END;

    LOCAL PROCEDURE LF@1100528007() ReturnValue : Text[1];
    BEGIN
      ReturnValue[1] := 10;
    END;

    PROCEDURE ResetGroupSeqNo@1100528000();
    BEGIN
      GroupSeqNo := 0;
      GetCreatedByName
    END;

    PROCEDURE GetCreatedByName@1100526101() : Text;
    VAR
      UserSetup@1100526100 : Record 91;
      Employee@1100526101 : Record 5200;
    BEGIN
      UserSetup.SETRANGE("User ID", "Created by");
      UserSetup.SETFILTER("Employee No.", '<>%1', '');
      IF UserSetup.FINDFIRST THEN
        IF Employee.GET(UserSetup."Employee No.") THEN
          EXIT(Employee.FullName);

      EXIT(USERID);
    END;

    BEGIN
    END.
  }
}

