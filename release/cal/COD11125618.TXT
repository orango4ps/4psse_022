OBJECT Codeunit 11125618 BaseRecipeLine-Explode BOM
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    TableNo=11020582;
    OnRun=VAR
            BaseRecipeLine@1100525003 : Record 11020582;
            GenericItem@1100525000 : Record 11125615;
            ExplodeGenericItem@1100525005 : Report 11125617;
          BEGIN
            BaseRecipeLine := Rec;
            BaseRecipeLine.SETRECFILTER;
            BaseRecipeLine.SETRANGE("Line No.");

            IF "Item No." <> '' THEN BEGIN
              OrigLineNo := "Line No.";
              OrigQuantity := Quantity;
              ExplodeItem(BaseRecipeLine);
            END ELSE BEGIN
              GenericItem.SETRANGE(Status,GenericItem.Status::Certified);
              IF PAGE.RUNMODAL(0,GenericItem) = ACTION::LookupOK THEN BEGIN
                GenericItem.TESTFIELD(Status,GenericItem.Status::Certified);
                ExplodeGenericItem.RUNMODAL;
                ExplodeGenItem(BaseRecipeLine,GenericItem,ExplodeGenericItem.GetQuantity);
              END;
            END;
          END;

  }
  CODE
  {
    VAR
      Text001@1100525007 : TextConst 'DEU=Artikel %1 ist keine StÅckliste.;ENU=Item %1 is not a bill of materials.;NLD=Artikel %1 is geen stuklijst.;NOR=Artikkel %1 er ikke en stykkliste;SVE=Artikel %1 Ñr inte en struktur.';
      Text003@1100525005 : TextConst 'DEU=Es ist nicht genÅgend Platz, um die StÅckliste zu entfalten.;ENU=There is not enough space to explode the BOM.;NLD=Er is niet voldoende ruimte om de stuklijst weer te geven.;NOR=Det er ikke nok plass til Ü utfolde stykklisten.;SVE=Det finns inte tillrÑckligt med utrymme fîr att expandera strukturen.';
      OrigLineNo@1100525001 : Integer;
      OrigQuantity@1100525000 : Decimal;
      OrigItem@1100525003 : Code[20];
      LevelBOM@1100525002 : Integer;

    PROCEDURE ExplodeGenItem@1100525000(VAR BaseRecipeLine@1100525000 : Record 11020582;GenericItem@1100525006 : Record 11125615;QuantityGenItem@1100525007 : Decimal);
    VAR
      ToBaseRecipeLine@1100525001 : Record 11020582;
      GenericBOMLine@1100525005 : Record 11125616;
      NoOfBOMComp@1100525003 : Integer;
      NextLineNo@1100525004 : Integer;
      LineSpacing@1100525002 : Integer;
    BEGIN
      //explode for generic item
      WITH BaseRecipeLine DO BEGIN
        GenericBOMLine.SETRANGE("Generic Item No.",GenericItem."No.");
        NoOfBOMComp := GenericBOMLine.COUNT;
        IF NoOfBOMComp = 0 THEN
          ERROR(Text001,GenericItem."No.");

        IF GenericBOMLine.FINDSET THEN BEGIN
          LineSpacing := GetLineSpacing(BaseRecipeLine, NoOfBOMComp);
          NextLineNo := "Line No.";
          REPEAT
            NextLineNo := NextLineNo + LineSpacing;
            ToBaseRecipeLine := BaseRecipeLine;
            ToBaseRecipeLine.INIT;
            ToBaseRecipeLine."Line No." := NextLineNo;

            ToBaseRecipeLine.VALIDATE("Item No.",GenericBOMLine."Item No.");
            ToBaseRecipeLine.VALIDATE(Quantity,(GenericBOMLine.Quantity * QuantityGenItem));
            ToBaseRecipeLine."Unit of Measure" := GenericBOMLine."Unit of Measure Code";
            ToBaseRecipeLine.Description := GenericBOMLine.Description;
            ToBaseRecipeLine."Part of Line" := OrigLineNo;
            ToBaseRecipeLine.INSERT;
          UNTIL GenericBOMLine.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE ExplodeItem@1100525001(VAR BaseRecipeLine@1100525000 : Record 11020582);
    VAR
      ToBaseRecipeLine@1100525005 : Record 11020582;
      NoOfBOMComp@1100525003 : Integer;
      LineSpacing@1100525002 : Integer;
      NextLineNo@1100525001 : Integer;
      FromBOMComp@1100525008 : Record 90;
      BomComp@1100525007 : Record 90;
      TempBaseRecipeLine@1100525006 : TEMPORARY Record 11020582;
    BEGIN
      //explode for item
      WITH BaseRecipeLine DO BEGIN
        OrigLineNo := "Line No.";
        OrigQuantity := Quantity;
        IF OrigItem = '' THEN
          OrigItem := "Item No.";

        ToBaseRecipeLine := BaseRecipeLine;
        ToBaseRecipeLine.INIT;
        ToBaseRecipeLine.Description := Description;
        ToBaseRecipeLine."Original Quantity" := OrigQuantity;
        ToBaseRecipeLine."Part of Line" := "Part of Line";
        ToBaseRecipeLine."Level BOM" := "Level BOM";
        ToBaseRecipeLine."BOM Item No." := OrigItem;
        ToBaseRecipeLine.MODIFY;
        LevelBOM := "Level BOM" + 1;

        FromBOMComp.SETRANGE("Parent Item No.","Item No.");
        FromBOMComp.SETFILTER(Type, '<>%1', FromBOMComp.Type::Resource);
        NoOfBOMComp := FromBOMComp.COUNT;
        IF NoOfBOMComp = 0 THEN BEGIN
          ExplodeItemProdBOM(BaseRecipeLine);
        END;

        IF FromBOMComp.FINDSET THEN BEGIN
          LineSpacing := GetLineSpacing(BaseRecipeLine, NoOfBOMComp);
          NextLineNo := "Line No.";
          REPEAT
            NextLineNo := NextLineNo + LineSpacing;
            ToBaseRecipeLine := BaseRecipeLine;
            ToBaseRecipeLine.INIT;
            ToBaseRecipeLine."Line No." := NextLineNo;

            IF FromBOMComp.Type = FromBOMComp.Type::Item THEN BEGIN
              FromBOMComp.TESTFIELD("No.");
              ToBaseRecipeLine.VALIDATE("Item No.",FromBOMComp."No.");
              //DP02339.sn
              ToBaseRecipeLine."Original Quantity" := FromBOMComp."Quantity per";
              ToBaseRecipeLine.VALIDATE(Quantity,Quantity * ToBaseRecipeLine."Original Quantity");
              //DP02339.en
            END;
            ToBaseRecipeLine."Unit of Measure" := FromBOMComp."Unit of Measure Code";
            ToBaseRecipeLine.Description := FromBOMComp.Description;
            ToBaseRecipeLine."Part of Line" := OrigLineNo;
            ToBaseRecipeLine."Level BOM" := LevelBOM;
            ToBaseRecipeLine."BOM Item No." := OrigItem;
            ToBaseRecipeLine.INSERT;

            IF FromBOMComp.Type = FromBOMComp.Type::Item THEN BEGIN
              BomComp.SETRANGE("Parent Item No.", ToBaseRecipeLine."Item No.");
              IF NOT BomComp.ISEMPTY THEN BEGIN
                TempBaseRecipeLine := ToBaseRecipeLine;
                TempBaseRecipeLine.INSERT;
              END;
            END;
          UNTIL FromBOMComp.NEXT = 0;
        END;
      END;

      IF TempBaseRecipeLine.FINDSET THEN BEGIN
        //Explode BOMS
        COMMIT;
        REPEAT
          ExplodeItem(TempBaseRecipeLine);
        UNTIL TempBaseRecipeLine.NEXT = 0;
      END;
    END;

    PROCEDURE ExplodeItemProdBOM@1100525002(VAR BaseRecipeLine@1100525000 : Record 11020582);
    VAR
      Item@1100525006 : Record 27;
      ToBaseRecipeLine@1100525005 : Record 11020582;
      ProductionBOMLine@1100525004 : Record 99000772;
      NoOfBOMComp@1100525003 : Integer;
      LineSpacing@1100525002 : Integer;
      NextLineNo@1100525001 : Integer;
    BEGIN
      //explode for item with prod bom
      WITH BaseRecipeLine DO BEGIN
        Item.GET("Item No.");
        ProductionBOMLine.SETRANGE("Production BOM No.",Item."Production BOM No.");
        ProductionBOMLine.SETRANGE(Type,ProductionBOMLine.Type::Item);
        NoOfBOMComp := ProductionBOMLine.COUNT;
        IF NoOfBOMComp = 0 THEN
          ERROR(Text001,"Item No.");

        IF ProductionBOMLine.FINDSET THEN BEGIN
          LineSpacing := GetLineSpacing(BaseRecipeLine, NoOfBOMComp);
          NextLineNo := "Line No.";
          REPEAT
            NextLineNo := NextLineNo + LineSpacing;
            ToBaseRecipeLine := BaseRecipeLine;
            ToBaseRecipeLine.INIT;
            ToBaseRecipeLine."Line No." := NextLineNo;

            IF ProductionBOMLine.Type = ProductionBOMLine.Type::Item THEN BEGIN
              ProductionBOMLine.TESTFIELD("No.");
              ToBaseRecipeLine.VALIDATE("Item No.",ProductionBOMLine."No.");
              //DP02339.sn
              ToBaseRecipeLine."Original Quantity" := ProductionBOMLine."Quantity per";
              ToBaseRecipeLine.VALIDATE(Quantity,Quantity * ToBaseRecipeLine."Original Quantity");
              //DP02339.en
            END;
            ToBaseRecipeLine."Unit of Measure" := ProductionBOMLine."Unit of Measure Code";
            ToBaseRecipeLine.Description := ProductionBOMLine.Description;
            ToBaseRecipeLine."Part of Line" := OrigLineNo;
            ToBaseRecipeLine."Level BOM" := LevelBOM;
            ToBaseRecipeLine."BOM Item No." := OrigItem;
            ToBaseRecipeLine.INSERT;
          UNTIL ProductionBOMLine.NEXT = 0;
        END;
      END;
    END;

    LOCAL PROCEDURE GetLineSpacing@1100525003(VAR BaseRecipeLine@1100525000 : Record 11020582;NoOfBOMComp@1100525002 : Integer) LineSpacing : Decimal;
    VAR
      ToBaseRecipeLine@1100525001 : Record 11020582;
    BEGIN
      LineSpacing := 10000;

      ToBaseRecipeLine := BaseRecipeLine;
      ToBaseRecipeLine.SETRECFILTER;
      ToBaseRecipeLine.SETRANGE("Line No.");
      IF ToBaseRecipeLine.FIND('>') THEN BEGIN
        LineSpacing := (ToBaseRecipeLine."Line No." - BaseRecipeLine."Line No.") DIV (1 + NoOfBOMComp);
        IF LineSpacing = 0 THEN
          ERROR(Text003);
      END;

      EXIT(LineSpacing);
    END;

    BEGIN
    {
      db, 04-02-16: C028603 (use same procedure for exploding different type of items)
      - calculate line spacing and determine next linenumber
      - use init instead of copy
      - validate item and quantity
    }
    END.
  }
}

