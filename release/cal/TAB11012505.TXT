OBJECT Table 11012505 Option Sub Group
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               Plot@1100528201 : Record 11012500;
               OptionSubGroup@1100528200 : Record 11012505;
             BEGIN
               IF ("Project No." <> '' ) AND ("Plot No." = '') THEN BEGIN
                 Plot.SETRANGE("Project No.", "Project No.");
                 IF Plot.FINDSET THEN
                   REPEAT
                     OptionSubGroup := Rec;
                     OptionSubGroup."Plot No." := Plot."Plot No.";
                     IF OptionSubGroup.INSERT THEN;
                   UNTIL Plot.NEXT = 0;
               END;
             END;

    OnModify=VAR
               OptionSubGroup@1100528200 : Record 11012505;
             BEGIN
               IF ("Project No." <> '' ) AND ("Plot No." = '') THEN BEGIN
                 IF "Closing Date" <> xRec."Closing Date" THEN BEGIN
                   OptionSubGroup.SETRANGE("Project No.", "Project No.");
                   OptionSubGroup.SETFILTER("Plot No.", '<>%1', '');
                   OptionSubGroup.SETRANGE(Code, Code);
                   OptionSubGroup.SETRANGE("Closing Date", xRec."Closing Date");
                   IF OptionSubGroup.FINDFIRST THEN BEGIN
                     IF CONFIRM(
                       Text003, TRUE,
                       FIELDCAPTION("Closing Date"), FIELDCAPTION("Plot No."),
                       FIELDCAPTION("Closing Date"), xRec."Closing Date") THEN
                       OptionSubGroup.MODIFYALL("Closing Date", "Closing Date");
                   END;
                 END;
                 IF Description <> xRec.Description THEN BEGIN
                   OptionSubGroup.RESET;
                   OptionSubGroup.SETRANGE("Project No.", "Project No.");
                   OptionSubGroup.SETFILTER("Plot No.", '<>%1', '');
                   OptionSubGroup.SETRANGE(Code, Code);
                   IF OptionSubGroup.FINDFIRST THEN
                     OptionSubGroup.MODIFYALL(Description, Description);
                 END;
               END;
             END;

    OnDelete=VAR
               lvBuyerMgtTextRec@1210190000 : Record 11012511;
               OptionSubGroup@1100528201 : Record 11012505;
               Option@1100528200 : Record 11012502;
             BEGIN
               IF ("Project No." <> '' )  THEN BEGIN
                 Option.SETRANGE("Sub Group", Code);
                 Option.SETRANGE("Project No.", "Project No.");
                 IF "Plot No." <> '' THEN
                   Option.SETRANGE("Plot No.","Plot No.");

                 IF Option.FINDFIRST THEN
                   IF Option."Plot No." <> '' THEN
                     ERROR(
                       Text001, TABLECAPTION, Option.TABLECAPTION,
                       Option.FIELDCAPTION("Project No."), Option."Project No.",
                       Option.FIELDCAPTION("Plot No."), Option."Plot No.",
                       Option.FIELDCAPTION("Sub Group"), Option."Sub Group")
                   ELSE
                         ERROR(
                     Text002, TABLECAPTION, Option.TABLECAPTION,
                     Option.FIELDCAPTION("Project No."), Option."Project No.",
                     Option.FIELDCAPTION("Sub Group"), Option."Sub Group");

                 IF "Plot No." = '' THEN BEGIN
                   OptionSubGroup.SETRANGE("Project No.");
                   OptionSubGroup.SETFILTER("Plot No.", '<>%1', '');
                   OptionSubGroup.SETRANGE(Code, Code);
                   OptionSubGroup.DELETEALL(TRUE);
                 END;
               END;

               lvBuyerMgtTextRec.SETRANGE(Table, lvBuyerMgtTextRec.Table::"Option Sub Group");
               lvBuyerMgtTextRec.SETRANGE("Project No.", "Project No.");
               lvBuyerMgtTextRec.SETRANGE("Plot No.", "Plot No.");
               lvBuyerMgtTextRec.SETRANGE("Option Sub Group", Code);
               lvBuyerMgtTextRec.DELETEALL;
             END;

    CaptionML=[ENU=Option Sub Group;
               NOR=Alternativundergrupp;
               SVE=Alternativundergrupp];
    LookupPageID=Page11012542;
  }
  FIELDS
  {
    { 5   ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr] }
    { 10  ;   ;Code                ;Code20        ;TableRelation="Option Sub Group".Code WHERE (Project No.=FIELD(Project No.));
                                                   OnValidate=VAR
                                                                SubGroupRec@1100528200 : Record 11012505;
                                                              BEGIN
                                                                IF ("Project No." <> '') AND (Description = '') THEN BEGIN
                                                                  IF "Plot No." <>'' THEN BEGIN
                                                                    IF SubGroupRec.GET("Project No.",'',Code) THEN
                                                                      Description := SubGroupRec.Description;
                                                                  END;
                                                                  IF (Description = '') AND SubGroupRec.GET('', '', Code) THEN
                                                                    Description := SubGroupRec.Description;
                                                                END;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 20  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 30  ;   ;Closing Date        ;Date          ;OnValidate=VAR
                                                                Option@1210190000 : Record 11012502;
                                                              BEGIN
                                                                IF ("Closing Date" <> xRec."Closing Date") AND ("Project No." <> '' ) THEN BEGIN
                                                                  Option.SETRANGE("Sub Group", Code);
                                                                  Option.SETRANGE("Project No.", "Project No.");
                                                                  IF "Plot No." <> '' THEN
                                                                    Option.SETRANGE("Plot No.","Plot No.");
                                                                  IF (NOT Option.ISEMPTY) THEN
                                                                    MESSAGE(Text000, TABLECAPTION, FIELDCAPTION("Closing Date"), Option.TABLECAPTION);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Closing Date;
                                                              NOR=Sluttdato;
                                                              SVE=Avslutsdatum] }
    { 40  ;   ;Plot No.            ;Code10        ;TableRelation=Plot."Plot No." WHERE (Project No.=FIELD(Project No.));
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Plot No.;
                                                              SVE=Ritningnr] }
    { 70  ;   ;Block No.           ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Plot."Block No." WHERE (Project No.=FIELD(Project No.),
                                                                                              Plot No.=FIELD(Plot No.)));
                                                   CaptionML=[ENU=Block No.;
                                                              SVE=Sp„rrnr];
                                                   Editable=No }
    { 80  ;   ;Floor No.           ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Plot."Floor No." WHERE (Project No.=FIELD(Project No.),
                                                                                              Plot No.=FIELD(Plot No.)));
                                                   CaptionML=[ENU=Floor No.;
                                                              SVE=V†ning nr];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Project No.,Plot No.,Code               ;Clustered=Yes }
    {    ;Description                              }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Code,Description                         }
  }
  CODE
  {
    VAR
      Text000@1210190001 : TextConst 'ENU=This %1 is already used, changing the %2 will not effect the %3 lines.;NOR=Dette %1 er allerede brukt, † endre %2 p†virker ikke %3-radene.;SVE=Detta %1 har redan anv„nts, att „ndra %2 p†verkar inte %3-raderna.';
      Text001@1100528202 : TextConst 'ENU=The %1 is in use for %2 -  %3 %4, %5 %6, %7 %8.';
      Text002@1100528201 : TextConst 'ENU=The %1 is in use for %2 -  %3 %4, %5 %6.';
      Text003@1100528200 : TextConst 'ENU="Modify the %1 per %2  with %3 %4 too? "';

    BEGIN
    END.
  }
}

