OBJECT Table 6085749 Approval Sharing
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.5.00.07;
  }
  PROPERTIES
  {
    Permissions=TableData 6085749=rimd;
    DataCaptionFields=Owner User ID,Shared to User ID;
    OnInsert=BEGIN
               TESTFIELD("Owner User ID");
               TESTFIELD("Shared to User ID");
               IF "Owner User ID" = "Shared to User ID" THEN
                 ERROR(OwnerEqualSharedToErr,FIELDCAPTION("Owner User ID"),FIELDCAPTION("Shared to User ID"),"Owner User ID");

               CASE "Sharing Type" OF
                 "Sharing Type"::Normal:
                   "Display Order" := 1;
                 "Sharing Type"::"Out of Office":
                   "Display Order" := 0;
               END;

               IF "Copy to All Companies" THEN
                 InsertInAllCompanies;
             END;

    OnModify=BEGIN
               IF "Copy to All Companies" AND xRec."Copy to All Companies" THEN
                 ModifyInAllCompanies;
             END;

    OnDelete=BEGIN
               IF "Copy to All Companies" THEN BEGIN
                 IF NOT GUIALLOWED THEN
                   DeleteFromAllCompanies
                 ELSE
                   IF "Owner User ID" = USERID THEN
                     DeleteFromAllCompanies
                   ELSE BEGIN
                     IF CONFIRM(Text002,TRUE,TABLECAPTION) THEN
                       DeleteFromAllCompanies
                     ELSE BEGIN
                       "Copy to All Companies" := FALSE;
                       ModifyInAllCompanies;
                     END;
                   END;
               END;
             END;

    OnRename=BEGIN
               IF "Copy to All Companies" THEN
                 RenameInAllCompanies;
             END;

    CaptionML=[DEU=Genehmigungsteilung;
               ENU=Approval Sharing;
               NLD=Goedkeuring delen;
               SVE=GodkÑnnandedelning];
    LookupPageID=Page6085737;
    DrillDownPageID=Page6085737;
  }
  FIELDS
  {
    { 1   ;   ;Owner User ID       ;Code50        ;TableRelation="Continia User Setup";
                                                   OnValidate=BEGIN
                                                                IF "Owner User ID" <> '' THEN
                                                                  IF "Owner User ID" = "Shared to User ID" THEN
                                                                    ERROR(OwnerEqualSharedToErr,FIELDCAPTION("Owner User ID"),FIELDCAPTION("Shared to User ID"),"Owner User ID");
                                                              END;

                                                   CaptionML=[DEU=Benutzer-ID des Besitzers;
                                                              ENU=Owner User ID;
                                                              NLD=Eigenaar gebruikers-id;
                                                              NOR=Bruker-ID for eier;
                                                              SVE=égarens anvÑndar-ID];
                                                   NotBlank=Yes }
    { 2   ;   ;Shared to User ID   ;Code50        ;TableRelation="Continia User Setup";
                                                   OnValidate=BEGIN
                                                                IF "Shared to User ID" <> '' THEN
                                                                  IF "Owner User ID" = "Shared to User ID" THEN
                                                                    ERROR(OwnerEqualSharedToErr,FIELDCAPTION("Owner User ID"),FIELDCAPTION("Shared to User ID"),"Owner User ID");
                                                              END;

                                                   CaptionML=[DEU=Weitergegeben an Benutzer-ID;
                                                              ENU=Shared to User ID;
                                                              NLD=Gedeeld met gebruikers-id;
                                                              SVE=Delad med anvÑndar-ID];
                                                   NotBlank=Yes }
    { 3   ;   ;Sharing Type        ;Option        ;CaptionML=[DEU=Weitergabeart;
                                                              ENU=Sharing Type;
                                                              NLD=Deeltype;
                                                              SVE=Delningstyp];
                                                   OptionCaptionML=[DEU=Normal,Out of Office;
                                                                    ENU=Normal,Out of Office;
                                                                    NLD=Normaal,Afwezigheid;
                                                                    SVE=Normal,FrÜnvaro];
                                                   OptionString=Normal,Out of Office }
    { 4   ;   ;Valid From          ;Date          ;OnValidate=BEGIN
                                                                CheckValidDates;
                                                              END;

                                                   CaptionML=[DEU=GÅltigkeit ab;
                                                              ENU=Valid From;
                                                              NLD=Geldig van;
                                                              NOR=Valid From;
                                                              SVE=Giltig frÜn] }
    { 5   ;   ;Valid To            ;Date          ;OnValidate=BEGIN
                                                                CheckValidDates;
                                                              END;

                                                   CaptionML=[DEU=GÅltig bis;
                                                              ENU=Valid To;
                                                              NLD=Geldig tot;
                                                              NOR=Valid To;
                                                              SVE=Giltig till] }
    { 6   ;   ;Use Owners Limts & Permissions;Boolean;
                                                   CaptionML=[DEU=EigentÅmer-Grenzwerte und Berechtigungen verwenden;
                                                              ENU=Use Owners Limts && Permissions;
                                                              NLD=Gebruik eigenaarlimieten && rechten;
                                                              SVE=AnvÑnd Ñgarens begrÑnsningar och behîrigheter] }
    { 7   ;   ;Display Order       ;Integer       ;CaptionML=[DEU=Auftrag anzeigen;
                                                              ENU=Display Order;
                                                              NLD=Toon order;
                                                              SVE=Visningsordning] }
    { 8   ;   ;Forward E-mails     ;Boolean       ;CaptionML=[DEU=E-Mails weiterleiten;
                                                              ENU=Forward E-mails;
                                                              NLD=E-mails doorsturen;
                                                              SVE=Vidarebefordra E-post] }
    { 9   ;   ;Copy to All Companies;Boolean      ;OnValidate=VAR
                                                                ApprovalSharing@1160040000 : Record 6085749;
                                                              BEGIN
                                                                ApprovalSharing := Rec;
                                                                IF NOT ApprovalSharing.FIND('=') THEN // only continue if the record is already inserted in the table
                                                                  EXIT;

                                                                IF "Copy to All Companies" THEN
                                                                  InsertInAllCompanies
                                                                ELSE BEGIN
                                                                  IF CONFIRM(Text002,TRUE,TABLECAPTION) THEN
                                                                    DeleteFromAllCompanies
                                                                  ELSE
                                                                    ModifyInAllCompanies;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Kopiere in alle Unternehmen;
                                                              ENU=Copy to All Companies;
                                                              NLD=Kopieer naar alle bedrijven] }
  }
  KEYS
  {
    {    ;Owner User ID,Shared to User ID,Sharing Type,Valid From,Valid To;
                                                   Clustered=Yes }
    {    ;Shared to User ID,Sharing Type,Valid From,Valid To }
    {    ;Display Order                            }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1160040001 : TextConst 'DEU=Mîchten Sie den %1 in alle Unternehmen kopieren?;ENU=Do you want to copy this %1 to all companies?;NLD=Wilt u deze %1 kopieren naar alle bedrijven?';
      Text002@1160040002 : TextConst 'DEU=Mîchten Sie diesen %1 aus allen Unternehmen lîschen?;ENU=Do you want to delete this %1 from all other companies?;NLD=Wilt u deze %1 verwijderen uit alle bedrijven?';
      Text003@1160040000 : TextConst 'DEU=Sie kînnen eine %1 nicht umbenennen.;ENU=You cannot rename a %1.;NLD=U kunt een %1 niet hernoemen.;SVE=Du kan inte byta namn pÜ %1.';
      OwnerEqualSharedToErr@1160040003 : TextConst 'DEU=%1 (%3) darf %2 (%3) nicht gleich sein;ENU=%1 (%3) must not be equal to %2 (%3).;NLD=%1 (%3) mag niet gelijk zijn aan %2 (%3)';
      ValidDatesErr@6085573 : TextConst 'DEU=%1 ist hîher als %2.;ENU=%1 is higher than %2.;NLD=%1 is hoger dan %2.';

    PROCEDURE InsertInAllCompanies@1160040000();
    VAR
      Company@1160040001 : Record 2000000006;
      ContiniaUserSetup@1160040002 : Record 6086002;
      ApprovalSharing@1160040000 : Record 6085749;
      DCSetup@6085573 : Record 6085573;
    BEGIN
      Company.SETFILTER(Name,'<>%1',COMPANYNAME);
      IF Company.FINDSET THEN
        REPEAT
          DCSetup.CHANGECOMPANY(Company.Name);
          IF DCSetup.READPERMISSION THEN
            IF DCSetup.GET AND (DCSetup."Document Nos." <> '') THEN BEGIN
              ContiniaUserSetup.CHANGECOMPANY(Company.Name);
              IF ContiniaUserSetup.GET("Owner User ID") AND ContiniaUserSetup.GET("Shared to User ID") THEN BEGIN
                ApprovalSharing.CHANGECOMPANY(Company.Name);
                IF NOT ApprovalSharing.GET("Owner User ID","Shared to User ID","Sharing Type","Valid From","Valid To") THEN BEGIN
                  ApprovalSharing := Rec;
                  ApprovalSharing.INSERT;
                END;
              END;
            END;
        UNTIL Company.NEXT = 0;
    END;

    PROCEDURE ModifyInAllCompanies@1160040002();
    VAR
      Company@1160040001 : Record 2000000006;
      ContiniaUserSetup@1160040002 : Record 6086002;
      ApprovalSharing@1160040000 : Record 6085749;
    BEGIN
      Company.SETFILTER(Name,'<>%1',COMPANYNAME);
      IF Company.FINDSET THEN
        REPEAT
          ApprovalSharing.CHANGECOMPANY(Company.Name);
          IF ApprovalSharing.GET("Owner User ID","Shared to User ID","Sharing Type","Valid From","Valid To") THEN BEGIN
            ApprovalSharing."Use Owners Limts & Permissions" := "Use Owners Limts & Permissions";
            ApprovalSharing."Display Order" := "Display Order";
            ApprovalSharing."Forward E-mails" := "Forward E-mails";
            ApprovalSharing."Copy to All Companies" := "Copy to All Companies";
            ApprovalSharing.MODIFY;
          END;
        UNTIL Company.NEXT = 0;
    END;

    PROCEDURE DeleteFromAllCompanies@1160040004();
    VAR
      Company@1160040001 : Record 2000000006;
      ApprovalSharing@1160040000 : Record 6085749;
    BEGIN
      Company.SETFILTER(Name,'<>%1',COMPANYNAME);
      IF Company.FINDSET THEN
        REPEAT
          ApprovalSharing.CHANGECOMPANY(Company.Name);
          IF ApprovalSharing.GET("Owner User ID","Shared to User ID","Sharing Type","Valid From","Valid To") THEN
            ApprovalSharing.DELETE;
        UNTIL Company.NEXT = 0;
    END;

    PROCEDURE RenameInAllCompanies@1160040001();
    VAR
      Company@1160040001 : Record 2000000006;
      ContiniaUserSetup@1160040002 : Record 6086002;
      ApprovalSharing@1160040000 : Record 6085749;
    BEGIN
      Company.SETFILTER(Name,'<>%1',COMPANYNAME);
      IF Company.FINDSET THEN
        REPEAT
          ApprovalSharing.CHANGECOMPANY(Company.Name);
          IF ApprovalSharing.GET(xRec."Owner User ID",xRec."Shared to User ID",
            xRec."Sharing Type",xRec."Valid From",xRec."Valid To")
          THEN
            ApprovalSharing.DELETE;

          IF ApprovalSharing.GET("Owner User ID","Shared to User ID","Sharing Type","Valid From","Valid To") THEN
            ApprovalSharing.DELETE;

          ApprovalSharing := Rec;
          ApprovalSharing.INSERT;
        UNTIL Company.NEXT = 0;
    END;

    LOCAL PROCEDURE CheckValidDates@6085573();
    BEGIN
      IF ("Valid From" = 0D) OR ("Valid To" = 0D) THEN
        EXIT;
      IF "Valid From" > "Valid To" THEN
        ERROR(ValidDatesErr,FIELDCAPTION("Valid From"),FIELDCAPTION("Valid To"));
    END;

    BEGIN
    END.
  }
}

