OBJECT Table 11020513 Purch. Order Line Rental Rate
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CheckFillRentalRate();
             END;

    OnModify=BEGIN
               CheckFillRentalRate();
             END;

    CaptionML=[ENU=Purch. Order Line Rental Rate;
               NOR=Innkj›psordrerad, leiesats;
               SVE=Ink”p Orderrad, uthyrningsavgift];
  }
  FIELDS
  {
    { 10  ;   ;Purchase Order No.  ;Code20        ;TableRelation="Purchase Header".No. WHERE (Document Type=CONST(Order));
                                                   CaptionML=[ENU=Purchase Order No.;
                                                              NOR=Innkj›psordrenr.;
                                                              SVE=Ink”psordernr] }
    { 20  ;   ;Purch. Order Line No.;Integer      ;TableRelation="Purchase Line"."Line No." WHERE (Document Type=CONST(Order),
                                                                                                   Document No.=FIELD(Purchase Order No.));
                                                   CaptionML=[ENU=Purch. Order Line No.;
                                                              NOR=Bestillingslinjenr.;
                                                              SVE=Radnr p† ink”psorder] }
    { 40  ;   ;Plant Type          ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purchase Line"."Plant Type" WHERE (Document Type=CONST(Order),
                                                                                                          Document No.=FIELD(Purchase Order No.),
                                                                                                          Line No.=FIELD(Purch. Order Line No.)));
                                                   CaptionML=[ENU=Plant Type;
                                                              NOR=Maskintype;
                                                              SVE=Maskintyp];
                                                   Editable=No }
    { 50  ;   ;Rental Rate (Days)  ;Decimal       ;CaptionML=[ENU=Rental Rate (Day);
                                                              NOR=Leiesats (Dag);
                                                              SVE=Uthyrningsavgift (dag)];
                                                   MinValue=0;
                                                   BlankZero=Yes;
                                                   AutoFormatType=2 }
    { 55  ;   ;Rental Rate External (Days);Decimal;CaptionML=[ENU=External Rental Rate (Day);
                                                              NOR=Ekstern leiesats (Dag);
                                                              SVE=Extern uthyrningsavgift (dag)];
                                                   MinValue=0;
                                                   BlankZero=Yes;
                                                   AutoFormatType=2 }
    { 60  ;   ;Plant Location      ;Code20        ;TableRelation="Plant Location" WHERE (Depot=CONST(No));
                                                   OnValidate=BEGIN
                                                                IF "Plant Location" = '' THEN
                                                                  "Loc. - Rental Rate (Days)" := 0;
                                                              END;

                                                   CaptionML=[ENU=Plant Location;
                                                              SVE=Maskinutplacering] }
    { 65  ;   ;Loc. - Rental Rate (Days);Decimal  ;OnValidate=BEGIN
                                                                IF "Loc. - Rental Rate (Days)" <> 0 THEN
                                                                  TESTFIELD("Plant Location");
                                                              END;

                                                   CaptionML=ENU=Location - Rental Rate (Day);
                                                   MinValue=0;
                                                   BlankZero=Yes;
                                                   AutoFormatType=2 }
    { 70  ;   ;Customer Price Group;Code20        ;TableRelation="Customer Price Group";
                                                   OnValidate=BEGIN
                                                                IF "Customer Price Group" = '' THEN
                                                                  "Cust. - Rental Rate (Days)" := 0;
                                                              END;

                                                   CaptionML=[ENU=Customer Price Group;
                                                              SVE=Kund prisgrupp] }
    { 75  ;   ;Cust. - Rental Rate (Days);Decimal ;OnValidate=BEGIN
                                                                IF "Cust. - Rental Rate (Days)" <> 0 THEN
                                                                  TESTFIELD("Customer Price Group");
                                                              END;

                                                   CaptionML=ENU=Customer - Rental Rate (Day);
                                                   MinValue=0;
                                                   BlankZero=Yes;
                                                   AutoFormatType=2 }
  }
  KEYS
  {
    {    ;Purchase Order No.,Purch. Order Line No.;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE CheckFillRentalRate@1100485000();
    BEGIN
      IF "Rental Rate (Days)" <> 0 THEN BEGIN
        IF "Rental Rate External (Days)" = 0 THEN
          "Rental Rate External (Days)" := "Rental Rate (Days)";
      END ELSE BEGIN
        IF "Rental Rate External (Days)" <> 0 THEN
          "Rental Rate (Days)" := "Rental Rate External (Days)";
      END;
    END;

    PROCEDURE LookupCustPriceGroupViaCustomer@1100528503();
    VAR
      Customer@1100528500 : Record 18;
      PlantLocation@1100528501 : Record 11012554;
    BEGIN
      Customer.SETFILTER("Customer Price Group", '<>%1', '');
      IF "Plant Location" <> '' THEN BEGIN
        IF PlantLocation.GET("Plant Location") THEN BEGIN
          IF PlantLocation."Customer No." <> '' THEN
            Customer."No." := PlantLocation."Customer No."
        END;
      END;
      IF PAGE.RUNMODAL(0, Customer) = ACTION::LookupOK THEN BEGIN
        IF (Customer."Customer Price Group" <> '') AND (Customer."Customer Price Group" <> "Customer Price Group") THEN
          VALIDATE("Customer Price Group", Customer."Customer Price Group");
      END;
    END;

    PROCEDURE InsertDefaultPurchOrderLineRentalRates@1100528502(PurchaseOrderLine@1100528501 : Record 39);
    VAR
      PurchaseOrderHeader@1100528502 : Record 38;
      PurchOrderLineRentalRate@1100528500 : Record 11020513;
      PlantOrder@1100528503 : Record 11012556;
      PlantLocation@1100528504 : Record 11012554;
      Customer@1100528505 : Record 18;
      PlantLocationNo@1100528506 : Code[20];
      CustomerPriceGroup@1100528507 : Code[20];
    BEGIN
      PlantLocationNo := '';
      CustomerPriceGroup := '';
      IF NOT PurchaseOrderHeader.GET(PurchaseOrderHeader."Document Type"::Order, PurchaseOrderLine."Document No.") THEN
        PurchaseOrderHeader.INIT;
      IF PurchaseOrderLine."Plant Order No." <> '' THEN BEGIN
        IF PlantOrder.GET(PurchaseOrderLine."Plant Order No.") THEN BEGIN
          IF PlantLocation.GET(PlantOrder."To Location") AND (PlantOrder."To Location" <> '') THEN BEGIN
            IF NOT PlantLocation.Depot THEN
              PlantLocationNo := PlantOrder."To Location";
          END;
        END;
      END;
      IF PlantLocationNo = '' THEN BEGIN
        IF PurchaseOrderHeader.Deliver = PurchaseOrderHeader.Deliver::"Plant Location" THEN BEGIN
          IF PlantLocation.GET(PurchaseOrderHeader."Ship-to Location") AND (PurchaseOrderHeader."Ship-to Location" <> '') THEN BEGIN
            IF NOT PlantLocation.Depot THEN
              PlantLocationNo := PurchaseOrderHeader."Ship-to Location";
          END;
        END;
      END;
      IF (PlantLocationNo <> '') AND (PlantLocation."Customer No." <> '') THEN BEGIN
        IF Customer.GET(PlantLocation."Customer No.") THEN
          CustomerPriceGroup := Customer."Customer Price Group";
      END;

      PurchOrderLineRentalRate."Purchase Order No." := PurchaseOrderLine."Document No.";
      PurchOrderLineRentalRate."Purch. Order Line No." := PurchaseOrderLine."Line No.";
      IF PlantLocationNo <> '' THEN
        PurchOrderLineRentalRate."Plant Location" := PlantLocationNo;
      IF CustomerPriceGroup <> '' THEN
        PurchOrderLineRentalRate."Customer Price Group" := CustomerPriceGroup;
      PurchOrderLineRentalRate.INSERT;
    END;

    PROCEDURE InsertPlantNumberRentalRates@1100528500(PurchOrderLineRentalRate@1100528502 : Record 11020513;PlantType@1100528500 : Code[20];PlantNo@1100528501 : Code[10]);
    BEGIN
      PurchOrderLineRentalRate.CALCFIELDS("Plant Type");
      IF (PurchOrderLineRentalRate."Plant Type" <> PlantType) THEN
        EXIT;
      PurchOrderLineRentalRate.CheckFillRentalRate();
      IF (PurchOrderLineRentalRate."Rental Rate (Days)" <> 0) OR (PurchOrderLineRentalRate."Rental Rate External (Days)" <> 0) THEN
        InsertStandardRentalRate(PurchOrderLineRentalRate, PlantType, PlantNo);
      IF (PurchOrderLineRentalRate."Plant Location" <> '') AND (PurchOrderLineRentalRate."Loc. - Rental Rate (Days)" <> 0) THEN
        InsertPlantLocationRentalRate(PurchOrderLineRentalRate, PlantType, PlantNo);
      IF (PurchOrderLineRentalRate."Customer Price Group" <> '') AND (PurchOrderLineRentalRate."Cust. - Rental Rate (Days)" <> 0) THEN
        InsertCustomerPriceGroupRentalRate(PurchOrderLineRentalRate, PlantType, PlantNo);
    END;

    LOCAL PROCEDURE InsertStandardRentalRate@1100528501(PurchOrderLineRentalRate@1100528503 : Record 11020513;PlantType@1100528502 : Code[20];PlantNo@1100528501 : Code[10]);
    VAR
      RentalRate@1100528500 : Record 11012567;
      PlantNumber@1100529000 : Record 11012552;
    BEGIN
      RentalRate.INIT;
      RentalRate."Rental Type" := RentalRate."Rental Type"::Rental;
      RentalRate."Plant Type" := PlantType;
      RentalRate."Plant No." := PlantNo;
      RentalRate."Starting Date" := 0D;
      RentalRate."Rental Rate (Days)" := PurchOrderLineRentalRate."Rental Rate (Days)";
      RentalRate."Rental Rate External (Days)" := PurchOrderLineRentalRate."Rental Rate External (Days)";
      RentalRate.INSERT;
      IF PlantNumber.GET(PlantType, PlantNo) THEN
        PlantNumber.GetDefaultExternalRateComponent;
    END;

    LOCAL PROCEDURE InsertPlantLocationRentalRate@1100528506(PurchOrderLineRentalRate@1100528503 : Record 11020513;PlantType@1100528502 : Code[20];PlantNo@1100528501 : Code[10]);
    VAR
      RentalRateLocation@1100528500 : Record 11012583;
    BEGIN
      RentalRateLocation.INIT;
      RentalRateLocation.Location := PurchOrderLineRentalRate."Plant Location";
      RentalRateLocation."Rental Type" := RentalRateLocation."Rental Type"::Rental;
      RentalRateLocation."Plant Type" := PlantType;
      RentalRateLocation."Plant No." := PlantNo;
      RentalRateLocation."Starting Date" := 0D;
      RentalRateLocation."Rental Rate (Day)" := PurchOrderLineRentalRate."Loc. - Rental Rate (Days)";
      RentalRateLocation.INSERT;
    END;

    LOCAL PROCEDURE InsertCustomerPriceGroupRentalRate@1100528507(PurchOrderLineRentalRate@1100528503 : Record 11020513;PlantType@1100528502 : Code[20];PlantNo@1100528501 : Code[10]);
    VAR
      RentalRateCustPriceGroup@1100528500 : Record 11012541;
    BEGIN
      RentalRateCustPriceGroup.INIT;
      RentalRateCustPriceGroup."Customer Price Group" := PurchOrderLineRentalRate."Customer Price Group";
      RentalRateCustPriceGroup."Rental Type" := RentalRateCustPriceGroup."Rental Type"::Rental;
      RentalRateCustPriceGroup."Plant Type" := PlantType;
      RentalRateCustPriceGroup."Plant No." := PlantNo;
      RentalRateCustPriceGroup."Starting Date" := 0D;
      RentalRateCustPriceGroup."Rental Rate (Days)" := PurchOrderLineRentalRate."Cust. - Rental Rate (Days)";
      RentalRateCustPriceGroup.INSERT;
    END;

    BEGIN
    END.
  }
}

