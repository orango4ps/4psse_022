OBJECT Table 11012299 Vendor Assessment Vendor
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               VendorAssessmentLineNew@1100528500 : Record 11072420;
               VendorAssessmentOrder@1100528501 : Record 11072421;
             BEGIN
               VendorAssessmentLineNew.SETRANGE(Date, Date);
               VendorAssessmentLineNew.SETRANGE("Vendor No.", "Vendor No.");
               VendorAssessmentOrder.SETRANGE(Date, Date);
               VendorAssessmentOrder.SETRANGE("Vendor No.", "Vendor No.");
               VendorAssessmentLineNew.DELETEALL;
               VendorAssessmentOrder.DELETEALL;
             END;

    OnRename=VAR
               VendorAssessmentLineNew@1100528500 : Record 11072420;
             BEGIN
               VendorAssessmentLineNew.SETRANGE(Date, xRec.Date);
               VendorAssessmentLineNew.SETRANGE("Vendor No.", xRec."Vendor No.");
               IF VendorAssessmentLineNew.FIND('-') THEN
                 ERROR(Text000);
             END;

    CaptionML=[ENU=Vendor Assessment;
               SVE=Leverant”rsbed”mning];
    LookupPageID=Page11012294;
  }
  FIELDS
  {
    { 20  ;   ;Date                ;Date          ;TableRelation="Vendor Assessment (New)".Date;
                                                   CaptionML=[ENU=Date;
                                                              SVE=Per datum];
                                                   NotBlank=Yes }
    { 30  ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Vendor Name", "Vendor Qualification");
                                                              END;

                                                   CaptionML=[ENU=Vendor No.;
                                                              SVE=Leverant”rsnr];
                                                   NotBlank=Yes }
    { 31  ;   ;Vendor Name         ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor.Name WHERE (No.=FIELD(Vendor No.)));
                                                   CaptionML=[ENU=Vendor Name;
                                                              SVE=Leverant”rsnamn];
                                                   Editable=No }
    { 32  ;   ;Vendor Qualification;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor."Vendor Qualification" WHERE (No.=FIELD(Vendor No.)));
                                                   TableRelation="Vendor Qualification";
                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Vendor Qualification;
                                                              SVE=Leverant”rskompetens];
                                                   Editable=No }
    { 40  ;   ;Purchase Orders     ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Vendor Assessment Order" WHERE (Date=FIELD(Date),
                                                                                                      Vendor No.=FIELD(Vendor No.),
                                                                                                      Department=FILTER(''),
                                                                                                      Employee=FILTER('')));
                                                   CaptionML=[ENU=Purchase Orders;
                                                              SVE=Ink”psorder];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 50  ;   ;Assessment Lines    ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Vendor Assessment Line (New)" WHERE (Date=FIELD(Date),
                                                                                                           Vendor No.=FIELD(Vendor No.),
                                                                                                           Criterion=FILTER(<>'')));
                                                   CaptionML=ENU=Assessment Lines;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 60  ;   ;Lines to Assess     ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Vendor Assessment Line (New)" WHERE (Date=FIELD(Date),
                                                                                                           Vendor No.=FIELD(Vendor No.),
                                                                                                           Criterion=FILTER(<>''),
                                                                                                           Score=FILTER(0),
                                                                                                           Not Apply=FILTER(No)));
                                                   CaptionML=ENU=Lines to Assess;
                                                   BlankZero=Yes;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Date,Vendor No.                         ;Clustered=Yes }
    {    ;Vendor No.,Date                          }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PurSetupRec@1100485000 : Record 312;
      Text000@1210190001 : TextConst 'ENU=Not allowed because there are lines present.;SVE=Inte till†tet eftersom det finns rader.';
      ActiveCompany@1100528800 : Text[30];

    PROCEDURE GetAssScore@1100485000(VAR OScoreDescription@1100485003 : Text[30]) RetResultScore : Decimal;
    VAR
      VendorAssessmentLineNew@1100528500 : Record 11072420;
      lvTotalScore@1100485000 : Decimal;
      lvMaxScore@1100485001 : Decimal;
    BEGIN
      VendorAssessmentLineNew.CHANGECOMPANY(ActiveCompany);
      RetResultScore := 0;
      OScoreDescription := '';

      VendorAssessmentLineNew.RESET;
      VendorAssessmentLineNew.SETRANGE(Date, Date);
      VendorAssessmentLineNew.SETRANGE("Vendor No.", "Vendor No.");
      VendorAssessmentLineNew.SETFILTER(Score, '<>%1', 0);
      VendorAssessmentLineNew.SETFILTER("Weight Factor", '<>%1', 0);
      IF VendorAssessmentLineNew.FIND('-') THEN BEGIN
        REPEAT
          lvTotalScore := lvTotalScore + (VendorAssessmentLineNew."Weight Factor" * VendorAssessmentLineNew.Score);
          lvMaxScore := lvMaxScore + (VendorAssessmentLineNew."Weight Factor" * PurSetupRec."Max. Score Assessment");
        UNTIL VendorAssessmentLineNew.NEXT = 0;
      END;

      IF lvMaxScore <> 0 THEN
        RetResultScore := lvTotalScore / lvMaxScore * PurSetupRec."Max. Score Assessment";

      OScoreDescription := GetAssessmentDescription(RetResultScore);
    END;

    PROCEDURE GetAverageAssessmentScore@1100528600(IVendorNo@1100528600 : Code[20];VAR OScoreDescription@1100528603 : Text[30]) AverageScore : Decimal;
    VAR
      Assessment@1100528604 : Record 11012294;
      VendorAssessmentVendor@1100528601 : Record 11012299;
      PurchasesPayablesSetup@1100528602 : Record 312;
      MathematicalFunctions@1100528607 : Codeunit 11020204;
      ScoreDescription@1100528605 : Text[30];
      TotalScore@1100528606 : Decimal;
      ScoreCalculationPeriod@1100528608 : DateFormula;
    BEGIN
      PurchasesPayablesSetup.CHANGECOMPANY(ActiveCompany);
      VendorAssessmentVendor.CHANGECOMPANY(ActiveCompany);
      Assessment.CHANGECOMPANY(ActiveCompany);

      OScoreDescription := '';
      PurchasesPayablesSetup.GET;
      IF FORMAT(PurchasesPayablesSetup."Vendor Assessment Period") = '' THEN
        EXIT;
      VendorAssessmentVendor.SETCURRENTKEY("Vendor No.");
      VendorAssessmentVendor.SETRANGE("Vendor No.", IVendorNo);
      ScoreCalculationPeriod := PurchasesPayablesSetup."Vendor Assessment Period";
      IF CALCDATE(ScoreCalculationPeriod, WORKDATE) > WORKDATE THEN
        MathematicalFunctions.ReverseDateFormula(ScoreCalculationPeriod, ScoreCalculationPeriod);
      VendorAssessmentVendor.SETRANGE(Date, CALCDATE(ScoreCalculationPeriod, WORKDATE), WORKDATE);
      IF VendorAssessmentVendor.FINDSET THEN BEGIN
        REPEAT
          TotalScore += VendorAssessmentVendor.GetAssScore(ScoreDescription);
        UNTIL VendorAssessmentVendor.NEXT = 0;
        AverageScore := TotalScore / VendorAssessmentVendor.COUNT;
      END;
      OScoreDescription := GetAssessmentDescription(AverageScore);
    END;

    PROCEDURE GetAssessmentDescription@1100528601(IScore@1100528601 : Decimal) : Text[30];
    VAR
      Assessment@1100528600 : Record 11012294;
    BEGIN
      Assessment.CHANGECOMPANY(ActiveCompany);
      IF NOT Assessment.GET(ROUND(IScore, 1)) THEN
        Assessment.INIT;
      EXIT(Assessment.Assessment);
    END;

    PROCEDURE SetCompany@1100528800(ICompany@1100528800 : Text[30]);
    BEGIN
      IF (ICompany = '') OR (ICompany = COMPANYNAME) THEN
        ActiveCompany := ''
      ELSE
        ActiveCompany := ICompany;
    END;

    BEGIN
    END.
  }
}

