OBJECT Table 11072679 Base Plant Inspection Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD(Code);
             END;

    OnDelete=VAR
               BasePlantInspectionOption@1100528600 : Record 11072681;
             BEGIN
               IF IsInUse THEN
                 ERROR(Text002);
               BasePlantInspectionOption.SETRANGE("Base Inspection Line Code", Code);
               BasePlantInspectionOption.DELETEALL(TRUE);
             END;

    CaptionML=ENU=Base Plant Inspection Line;
    LookupPageID=Page11229863;
    DrillDownPageID=Page11229863;
  }
  FIELDS
  {
    { 10  ;   ;Code                ;Code20        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 20  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 30  ;   ;Inspection Group Code;Code10       ;TableRelation="Plant Inspection Group";
                                                   CaptionML=[ENU=Inspection Group Code;
                                                              NOR=Inspeksjonsguppekode;
                                                              SVE=Inspektionsgruppskod] }
    { 40  ;   ;Field Type          ;Option        ;OnValidate=VAR
                                                                BasePlantInspectionOption@1100525000 : Record 11072681;
                                                              BEGIN
                                                                UpdatePlantInspectionLines;
                                                                IF (xRec."Field Type" IN ["Field Type"::Option, "Field Type"::"Yes/No"]) AND (xRec."Field Type" <> "Field Type") THEN BEGIN
                                                                  IF IsInUse THEN
                                                                    ERROR(Text003);
                                                                  BasePlantInspectionOption.SETRANGE("Base Inspection Line Code", Code);
                                                                  BasePlantInspectionOption.DELETEALL(TRUE);
                                                                END;

                                                                IF "Field Type" = "Field Type"::"Yes/No" THEN BEGIN
                                                                  BasePlantInspectionOption.INIT;
                                                                  BasePlantInspectionOption."Base Inspection Line Code" := Code;
                                                                  BasePlantInspectionOption.Code := 'YES';
                                                                  BasePlantInspectionOption.Description := Text000;
                                                                  BasePlantInspectionOption.INSERT(TRUE);

                                                                  BasePlantInspectionOption."Base Inspection Line Code" := Code;
                                                                  BasePlantInspectionOption.Code := 'NO';
                                                                  BasePlantInspectionOption.Description := Text001;
                                                                  BasePlantInspectionOption.INSERT(TRUE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Field Type;
                                                              NOR=Felttype;
                                                              SVE=Field Type];
                                                   OptionCaptionML=[ENU=Date,Value,Text,Option,Yes/No;
                                                                    SVE=Datum,V„rde,Text,Alternativ,Ja/Nej];
                                                   OptionString=Date,Value,Text,Option,Yes/No }
    { 50  ;   ;Unit Of Measure     ;Code10        ;TableRelation="Unit of Measure";
                                                   CaptionML=[ENU=Unit Of Measure;
                                                              NOR=Enhet;
                                                              SVE=Enhet] }
    { 60  ;   ;Mandatory           ;Boolean       ;OnValidate=VAR
                                                                BasePlantInspectionOption@1100528600 : Record 11072681;
                                                              BEGIN
                                                                IF Mandatory AND ("Field Type" IN ["Field Type"::Option, "Field Type"::"Yes/No"]) THEN BEGIN
                                                                  BasePlantInspectionOption.SETRANGE("Base Inspection Line Code", Code);
                                                                  IF BasePlantInspectionOption.ISEMPTY THEN
                                                                    ERROR(Text004);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Mandatory;
                                                              NOR=Obligatorisk;
                                                              SVE=Obligatoriskt] }
    { 70  ;   ;Description 2       ;Text50        ;CaptionML=[ENU=Description 2;
                                                              NOR=Beskrivelse 2;
                                                              SVE=Beskrivning 2] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100525001 : TextConst 'ENU=Yes;NOR=Ja;SVE=Ja';
      Text001@1100525000 : TextConst 'ENU=No;NOR=Nr.;SVE=Nej';
      Text002@1100528600 : TextConst 'ENU=Inspection Code in use, removing not allowed';
      Text003@1100528601 : TextConst 'ENU=Inspection Code in use, modifying not allowed';
      Text004@1100528602 : TextConst 'ENU=No Options available';

    PROCEDURE UpdatePlantInspectionLines@1100528600();
    VAR
      PlantInspectionLine@1100528600 : Record 11072684;
    BEGIN
      IF ((xRec."Field Type" IN ["Field Type"::Option, "Field Type"::"Yes/No"]) OR
          ("Field Type" IN ["Field Type"::Option, "Field Type"::"Yes/No"]) AND
         (xRec."Field Type" <> "Field Type"))
      THEN BEGIN
        PlantInspectionLine.SETRANGE(Type, PlantInspectionLine.Type::Line);
        PlantInspectionLine.SETRANGE("Inspection Code", Code);
        IF PlantInspectionLine.FINDSET THEN
          REPEAT
            PlantInspectionLine."Field Type" := "Field Type";
            PlantInspectionLine.MODIFY(TRUE);
          UNTIL PlantInspectionLine.NEXT = 0;
      END;
    END;

    PROCEDURE IsInUse@1100528602() : Boolean;
    VAR
      ServiceOrderInspectionLine@1100528600 : Record 11071753;
      PlantInspectionLine@1100528601 : Record 11072684;
    BEGIN
      ServiceOrderInspectionLine.SETRANGE(Type, ServiceOrderInspectionLine.Type::Line);
      ServiceOrderInspectionLine.SETRANGE("Inspection Code", Code);
      PlantInspectionLine.SETRANGE(Type, PlantInspectionLine.Type::Line);
      PlantInspectionLine.SETRANGE("Inspection Code", Code);
      EXIT(NOT ServiceOrderInspectionLine.ISEMPTY OR NOT PlantInspectionLine.ISEMPTY);
    END;

    BEGIN
    END.
  }
}

