OBJECT Table 11020694 Subcontracting Hours Invoice
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 25=rm,
                TableData 122=rm,
                TableData 124=rm;
    OnInsert=BEGIN
               TESTFIELD("Employee No.");
               GetDefaults;
               UpdateTotals;
               "Record ID" := RECORDID;
             END;

    OnModify=BEGIN
               TESTFIELD("Employee No.");
               UpdateTotals;
             END;

    CaptionML=[ENU=Subcontracting Hours Invoice;
               NOR=Faktura for underentrepenõr (Timer);
               SVE=Faktura fîr underentreprenadstimmar];
    LookupPageID=Page11125042;
  }
  FIELDS
  {
    { 5   ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Type];
                                                   OptionCaptionML=ENU=Invoice,Posted Invoice,Order,Posted Receipt;
                                                   OptionString=Invoice,Posted Invoice,Order,Posted Receipt }
    { 10  ;   ;Document No.        ;Code20        ;TableRelation=IF (Type=CONST(Invoice)) "Purchase Header".No. WHERE (Document Type=CONST(Invoice))
                                                                 ELSE IF (Type=CONST(Posted Invoice)) "Purch. Inv. Header".No.;
                                                   CaptionML=[ENU=Document No.;
                                                              NOR=Dokumentnr.;
                                                              SVE=Verifikationsnr];
                                                   NotBlank=Yes }
    { 20  ;   ;Year                ;Integer       ;OnValidate=VAR
                                                                Date@1100528300 : Record 2000000007;
                                                              BEGIN
                                                                Date.SETRANGE("Period Type", Date."Period Type"::Year);
                                                                Date.SETRANGE("Period No.", Year);
                                                                IF NOT Date.FINDFIRST THEN
                                                                  FIELDERROR(Year);

                                                                StartDateYear := Date."Period Start";
                                                                YearEndDate := Date."Period End";
                                                              END;

                                                   CaptionML=[ENU=Year;
                                                              NOR=èr;
                                                              SVE=èr];
                                                   MinValue=1;
                                                   NotBlank=Yes }
    { 30  ;   ;Week                ;Integer       ;OnValidate=VAR
                                                                Date@1100528300 : Record 2000000007;
                                                              BEGIN
                                                                IF Year = 0 THEN
                                                                  Year := DATE2DMY(TODAY, 3);
                                                                VALIDATE(Year, Year);

                                                                Date.SETRANGE("Period Type", Date."Period Type"::Week);
                                                                Date.SETRANGE("Period No.", Week);
                                                                Date.SETRANGE("Period Start", CALCDATE('<-3D>', StartDateYear), YearEndDate);
                                                                Date.SETRANGE("Period End", StartDateYear, CALCDATE('<+3D>', YearEndDate));
                                                                IF NOT Date.FINDFIRST THEN
                                                                  FIELDERROR(Week);
                                                              END;

                                                   OnLookup=VAR
                                                              DateSelectionManagement@1100409000 : Codeunit 11130005;
                                                            BEGIN
                                                              DateSelectionManagement.GetWeekYear(Week, Year);
                                                              VALIDATE(Week);
                                                            END;

                                                   CaptionML=[ENU=Week;
                                                              NOR=Uke;
                                                              SVE=Vecka];
                                                   MinValue=1;
                                                   MaxValue=53;
                                                   NotBlank=Yes }
    { 35  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr];
                                                   Editable=No }
    { 40  ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Vendor Name");
                                                              END;

                                                   CaptionML=[ENU=Vendor No.;
                                                              NOR=Leverandõrnr.;
                                                              SVE=Lev.nr] }
    { 41  ;   ;Vendor Name         ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor.Name WHERE (No.=FIELD(Vendor No.)));
                                                   CaptionML=[ENU=Vendor Name;
                                                              NOR=Leverandõrsnavn;
                                                              SVE=Leverantîrsnamn];
                                                   Editable=No }
    { 50  ;   ;Employee No.        ;Code20        ;TableRelation="Employee per Vendor"."Employee No." WHERE (Vendor No.=FIELD(Vendor No.),
                                                                                                             Employee No.=FIELD(Employee No.));
                                                   OnValidate=BEGIN
                                                                IF CurrFieldNo <> 0 THEN BEGIN
                                                                  CALCFIELDS("Employee Name");
                                                                  CALCFIELDS("Employee Social Security No.");
                                                                END;

                                                                CheckEmployeeValidity;
                                                                IF ("Hour Rate" = 0) AND ("B Amount %" = 0) THEN
                                                                  GetDefaults;
                                                              END;

                                                   CaptionML=[ENU=Employee No.;
                                                              NOR=Ansattnr.;
                                                              SVE=AnstÑllningsnr] }
    { 51  ;   ;Employee Name       ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Employee per Vendor"."Employee Name" WHERE (Vendor No.=FIELD(Vendor No.),
                                                                                                                   Employee No.=FIELD(Employee No.)));
                                                   CaptionML=[ENU=Employee Name;
                                                              NOR=Navn pÜ ansatt;
                                                              SVE=Namn pÜ anstÑlld];
                                                   Editable=No }
    { 54  ;   ;Employee Social Security No.;Text30;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Employee per Vendor"."Social Security No." WHERE (Vendor No.=FIELD(Vendor No.),
                                                                                                                         Employee No.=FIELD(Employee No.)));
                                                   CaptionML=ENU=Employee Social Security No.;
                                                   Editable=No }
    { 60  ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr] }
    { 70  ;   ;Service Order No.   ;Code20        ;TableRelation="Service Order";
                                                   CaptionML=[ENU=Service Order No.;
                                                              NOR=Serviceordrenr.;
                                                              SVE=Serviceordernr] }
    { 80  ;   ;Extension Contract  ;Code10        ;TableRelation="Extension Contract"."Contract No." WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=[ENU=Extension Contract;
                                                              NOR=ETA;
                                                              SVE=éTA] }
    { 90  ;   ;Cost Object         ;Code20        ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2),
                                                                                               Cost Type=FILTER(Labor|Subcontracting|Sundry));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Cost Object Description");
                                                              END;

                                                   CaptionML=[ENU=Global Dimension 2 Code;
                                                              NOR=Kostobjekt;
                                                              SVE=Kostnadsobjekt];
                                                   CaptionClass='1,1,2' }
    { 91  ;   ;Cost Object Description;Text50     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Dimension Value".Name WHERE (Global Dimension No.=FILTER(2),
                                                                                                    Code=FIELD(Cost Object)));
                                                   CaptionML=[ENU=Cost Object Description;
                                                              NOR=Kostnadsobjekt, Beskrivelse;
                                                              SVE=Kostnadsobjekt, beskrivning];
                                                   Editable=No;
                                                   CaptionClass='1,11012000,2, , ' + Text004 }
    { 100 ;   ;Element             ;Code20        ;TableRelation="Project Element".Element WHERE (Project No.=FIELD(Project No.));
                                                   OnValidate=VAR
                                                                ProjectElement@1210190000 : Record 11012010;
                                                                ElementFormat@1210190001 : Codeunit 11012022;
                                                              BEGIN
                                                                IF Element = '' THEN
                                                                  EXIT;

                                                                ElementFormat.FormatElem(Element, 2, COMPANYNAME);
                                                                ProjectElement.GET("Project No.", Element);

                                                                CALCFIELDS("Element Description");
                                                              END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Element;
                                                              NOR=Element;
                                                              SVE=Element] }
    { 101 ;   ;Element Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Element".Description WHERE (Project No.=FIELD(Project No.),
                                                                                                           Element=FIELD(Element)));
                                                   CaptionML=[ENU=Element Description;
                                                              NOR=Elementbeskrivelse;
                                                              SVE=Elementbeskrivning];
                                                   Editable=No }
    { 150 ;   ;Hour Rate           ;Decimal       ;OnValidate=BEGIN
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=[ENU=Hour Rate;
                                                              NOR=Timesats;
                                                              SVE=Timtaxa] }
    { 160 ;   ;B Amount %          ;Decimal       ;OnValidate=BEGIN
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=[ENU=B Amount %;
                                                              NOR=B-belõpsprosent;
                                                              SVE=B-beloppsprocent] }
    { 200 ;   ;Monday              ;Decimal       ;OnValidate=BEGIN
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=[ENU=Monday;
                                                              NOR=Mandag;
                                                              SVE=MÜndag] }
    { 210 ;   ;Tuesday             ;Decimal       ;OnValidate=BEGIN
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=[ENU=Tuesday;
                                                              NOR=Tirsdag;
                                                              SVE=Tisdag] }
    { 220 ;   ;Wednesday           ;Decimal       ;OnValidate=BEGIN
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=[ENU=Wednesday;
                                                              NOR=Onsdag;
                                                              SVE=Onsdag] }
    { 230 ;   ;Thursday            ;Decimal       ;OnValidate=BEGIN
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=[ENU=Thursday;
                                                              NOR=Torsdag;
                                                              SVE=Torsdag] }
    { 240 ;   ;Friday              ;Decimal       ;OnValidate=BEGIN
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=[ENU=Friday;
                                                              NOR=Fredag;
                                                              SVE=Fredag] }
    { 250 ;   ;Saturday            ;Decimal       ;OnValidate=BEGIN
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=[ENU=Saturday;
                                                              NOR=Lõrdag;
                                                              SVE=Lîrdag] }
    { 260 ;   ;Sunday              ;Decimal       ;OnValidate=BEGIN
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=[ENU=Sunday;
                                                              NOR=Sõndag;
                                                              SVE=Sîndag] }
    { 300 ;   ;Total Hours         ;Decimal       ;CaptionML=[ENU=Total Hours;
                                                              NOR=Timer totalt;
                                                              SVE=Timmar totalt];
                                                   Editable=No }
    { 310 ;   ;Total Amount        ;Decimal       ;CaptionML=[ENU=Total Amount;
                                                              NOR=Totalt belõp;
                                                              SVE=Total Amount];
                                                   Editable=No }
    { 320 ;   ;Total B Amount      ;Decimal       ;CaptionML=[ENU=Total B Amount;
                                                              NOR=Totalt B-belõp;
                                                              SVE=Totalt B-belopp];
                                                   Editable=No }
    { 330 ;   ;Fixed B Amount per Hour;Decimal    ;CaptionML=[ENU=Fixed B Amount per Hour;
                                                              SVE=Fast B-belopp per timme] }
    { 340 ;   ;Copy from Record ID ;RecordID      ;CaptionML=ENU=Copy from Record ID }
    { 345 ;   ;Record ID           ;RecordID      ;CaptionML=[ENU=Record ID;
                                                              NOR=Post-ID;
                                                              SVE=Post-ID] }
    { 350 ;   ;Copy Present        ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Subcontracting Hours Invoice" WHERE (Copy from Record ID=FIELD(Record ID)));
                                                   CaptionML=ENU=Copy Present }
    { 400 ;   ;Purchase Order No.  ;Code20        ;CaptionML=[ENU=Purchase Order No.;
                                                              NOR=Innkjõpsordrenr.;
                                                              SVE=Inkîpsordernr];
                                                   NotBlank=Yes;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Type,Document No.,Year,Week,Line No.    ;Clustered=Yes }
    {    ;Project No.,Year,Week,Employee No.       }
    {    ;Copy from Record ID                      }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      StartDateYear@1100528301 : Date;
      YearEndDate@1100528300 : Date;
      Text000@1100528302 : TextConst 'ENU=There is no %1 present.;NOR=Det finnes ingen %1.;SVE=Det finns ingen %1.';
      Text001@1100528303 : TextConst 'ENU=%1 is expired.;NOR=%1 er utgÜtt.;SVE=%1 har fîrfallit.';
      Text002@1100529400 : TextConst 'ENU=No match voor %1 hours found.';
      Text003@1100529401 : TextConst 'ENU=Getting Receipt Lines @1@@@@@@@@@@';
      Text004@1100527350 : TextConst 'ENU=Description;SVE=Beskrivning';

    PROCEDURE UpdateTotals@1100528300();
    BEGIN
      "Total Hours" := Monday + Tuesday + Wednesday + Thursday + Friday + Saturday + Sunday;
      "Total Amount" := "Total Hours" * "Hour Rate";
      IF "Fixed B Amount per Hour" <> 0  THEN
        "Total B Amount" := "Fixed B Amount per Hour" * "Total Hours"
      ELSE
        "Total B Amount" := "Total Amount" * ("B Amount %" / 100);

      IF ("Hour Rate" <> 0) THEN
        TESTFIELD("Total Hours");
    END;

    PROCEDURE GetDefaults@1100528302();
    VAR
      EmployeePerVendor@1100528300 : Record 11020693;
      PurchaseHeader@1100528200 : Record 38;
      PurchInvHeader@1100528201 : Record 122;
      PaymentAgreement@1100528202 : Record 11012028;
    BEGIN
      IF (EmployeePerVendor.GET("Vendor No.", "Employee No.")) THEN BEGIN
        IF EmployeePerVendor."B Values from Document" THEN BEGIN
          CASE Type OF
          Type::Order:
            BEGIN
              IF (PurchaseHeader.GET(PurchaseHeader."Document Type"::Order, "Document No.")) THEN BEGIN
                IF PaymentAgreement.GET(PurchaseHeader."Payment Agreement") THEN BEGIN
                  "Hour Rate" := PaymentAgreement."Avg. Wage Rate";
                  "B Amount %" := PaymentAgreement."Contribution Percentage Labor";
                  "Fixed B Amount per Hour" := PaymentAgreement."Fixed B Amount per Hour";
                END;
              END;
            END;
          Type::Invoice:
            BEGIN
              IF (PurchaseHeader.GET(PurchaseHeader."Document Type"::Invoice, "Document No.")) THEN BEGIN
                IF PaymentAgreement.GET(PurchaseHeader."Payment Agreement") THEN BEGIN
                  "Hour Rate" := PaymentAgreement."Avg. Wage Rate";
                  "B Amount %" := PaymentAgreement."Contribution Percentage Labor";
                  "Fixed B Amount per Hour" := PaymentAgreement."Fixed B Amount per Hour";
                END;
              END;
            END;
          Type::"Posted Invoice":
            BEGIN
              IF (PurchInvHeader.GET("Document No.")) THEN BEGIN
                IF PaymentAgreement.GET(PurchInvHeader."Payment Agreement") THEN BEGIN
                  "Hour Rate" := PaymentAgreement."Avg. Wage Rate";
                  "B Amount %" := PaymentAgreement."Contribution Percentage Labor";
                  "Fixed B Amount per Hour" := PaymentAgreement."Fixed B Amount per Hour";
                END;
              END;
            END;
          END;
        END ELSE BEGIN
          "Hour Rate" := EmployeePerVendor."Hour Rate";
          "B Amount %" := EmployeePerVendor."B Amount %";
          "Fixed B Amount per Hour" := EmployeePerVendor."Fixed B Amount per Hour";
        END;
      END;
    END;

    PROCEDURE CheckEmployeeValidity@1100528301();
    VAR
      EmployeePerVendor@1100528300 : Record 11020693;
    BEGIN
      EmployeePerVendor.GET("Vendor No.", "Employee No.");

      IF (NOT EmployeePerVendor."ID Copy Present") THEN
        MESSAGE(Text000, EmployeePerVendor.FIELDCAPTION("ID Copy Present"))
      ELSE
        IF (EmployeePerVendor."ID Valid Till" < TODAY) THEN
          MESSAGE(Text001, EmployeePerVendor.FIELDCAPTION("ID Valid Till"));

      CASE EmployeePerVendor.Type OF
        EmployeePerVendor.Type::Soletrader: BEGIN
          IF (NOT EmployeePerVendor."Labour Declaration") THEN
            MESSAGE(Text000, EmployeePerVendor.FIELDCAPTION("Labour Declaration"))
          ELSE
            IF (EmployeePerVendor."Labour Declaration Valid Till" < TODAY) THEN
              MESSAGE(Text001, EmployeePerVendor.FIELDCAPTION("Labour Declaration Valid Till"));
        END;
      END;
    END;

    PROCEDURE UpdateAmounts@1100409000(EntryNo@1100409000 : Integer);
    VAR
      PurchaseHeader@1100528300 : Record 38;
    BEGIN
      CASE Type OF
      Type::Order:
        BEGIN
          IF (PurchaseHeader.GET(PurchaseHeader."Document Type"::Order, "Document No.")) THEN
            UpdateReceiptsFromPurchaseOrder;
        END;
      Type::Invoice:
        BEGIN
          IF (PurchaseHeader.GET(PurchaseHeader."Document Type"::Invoice, "Document No.")) THEN
            UpdateAmountsFromPurchInvoice;
        END;
      Type::"Posted Invoice":
        BEGIN
          IF (EntryNo = 0) THEN
            UpdateAmountsFromPurchInvHdr
          ELSE
            UpdateAmountsFromVendLedgerEnt(EntryNo);
        END;
      END;
    END;

    PROCEDURE UpdateAmountsFromPurchInvoice@1100528303();
    VAR
      PurchaseHeader@1100528300 : Record 38;
      SubcontractingHoursInvoice@1100528303 : Record 11020694;
      TotalAmount@1100528301 : Decimal;
      TotalBAmount@1100528302 : Decimal;
      TotalHours@1100525000 : Decimal;
    BEGIN
      SubcontractingHoursInvoice.SETRANGE(Type, Type::Invoice);
      SubcontractingHoursInvoice.SETRANGE("Document No.", "Document No.");
      IF (SubcontractingHoursInvoice.FINDFIRST) THEN BEGIN
        REPEAT
          TotalHours += SubcontractingHoursInvoice."Total Hours";
          TotalAmount += SubcontractingHoursInvoice."Total Amount";
          TotalBAmount += SubcontractingHoursInvoice."Total B Amount";
        UNTIL (SubcontractingHoursInvoice.NEXT = 0);
      END;

      IF (TotalHours <> 0) THEN BEGIN
        PurchaseHeader.GET(PurchaseHeader."Document Type"::Invoice, "Document No.");
        PurchaseHeader.VALIDATE("Number of Hours", TotalHours);
        PurchaseHeader.VALIDATE("Labor Amount", TotalAmount);
        PurchaseHeader.VALIDATE("Wage Charge (to B Account)", TotalBAmount);
        PurchaseHeader."Avg. Wage Rate" := ROUND(TotalAmount / TotalHours, 0.00001);
        PurchaseHeader.MODIFY;
      END;
    END;

    PROCEDURE UpdateAmountsFromPurchInvHdr@1100409001();
    VAR
      PurchInvHeader@1100528300 : Record 122;
      SubcontractingHoursInvoice@1100528303 : Record 11020694;
      VendorLedgerEntry@1100528304 : Record 25;
      TotalAmount@1100528301 : Decimal;
      TotalBAmount@1100528302 : Decimal;
      TotalHours@1100525000 : Decimal;
    BEGIN
      SubcontractingHoursInvoice.SETRANGE(Type, Type::"Posted Invoice");
      SubcontractingHoursInvoice.SETRANGE("Document No.", "Document No.");
      IF (SubcontractingHoursInvoice.FINDFIRST) THEN BEGIN
        REPEAT
          TotalHours += SubcontractingHoursInvoice."Total Hours";
          TotalAmount += SubcontractingHoursInvoice."Total Amount";
          TotalBAmount += SubcontractingHoursInvoice."Total B Amount";
        UNTIL (SubcontractingHoursInvoice.NEXT = 0);
      END;

      IF (TotalHours <> 0) THEN BEGIN
        PurchInvHeader.GET("Document No.");
        PurchInvHeader.VALIDATE("Number of Hours", TotalHours);
        PurchInvHeader.VALIDATE("Labor Amount", TotalAmount);
        PurchInvHeader.VALIDATE("Wage Charge (to B Account)", TotalBAmount);
        PurchInvHeader."Avg. Wage Rate" := ROUND(TotalAmount / TotalHours, 0.00001);
        PurchInvHeader.MODIFY;

        VendorLedgerEntry.SETRANGE("Vendor No.", PurchInvHeader."Buy-from Vendor No.");
        VendorLedgerEntry.SETRANGE("Document Type", VendorLedgerEntry."Document Type"::Invoice);
        VendorLedgerEntry.SETRANGE("Document No.", PurchInvHeader."No.");
        IF (VendorLedgerEntry.FINDFIRST) THEN
          UpdateVendorLedgerEntry(VendorLedgerEntry."Entry No.", TotalAmount, TotalBAmount, TotalHours);
      END;
    END;

    PROCEDURE UpdateAmountsFromVendLedgerEnt@1100528308(EntryNo@1100409000 : Integer);
    VAR
      SubcontractingHoursInvoice@1100528303 : Record 11020694;
      TotalAmount@1100528301 : Decimal;
      TotalBAmount@1100528302 : Decimal;
      TotalHours@1100525000 : Decimal;
    BEGIN
      IF (EntryNo = 0) THEN
        EXIT;

      SubcontractingHoursInvoice.SETRANGE(Type, Type::"Posted Invoice");
      SubcontractingHoursInvoice.SETRANGE("Document No.", "Document No.");
      IF (SubcontractingHoursInvoice.FINDFIRST) THEN BEGIN
        REPEAT
          TotalHours += SubcontractingHoursInvoice."Total Hours";
          TotalAmount += SubcontractingHoursInvoice."Total Amount";
          TotalBAmount += SubcontractingHoursInvoice."Total B Amount";
        UNTIL (SubcontractingHoursInvoice.NEXT = 0);
      END;

      IF (TotalHours <> 0) THEN
        UpdateVendorLedgerEntry(EntryNo, TotalAmount, TotalBAmount, TotalHours);
    END;

    PROCEDURE RunPageFromPurchaseHdrOrder@1100528204(PurchaseHeader@1100528303 : Record 38);
    BEGIN
      PurchaseHeader.TESTFIELD("Document Type", PurchaseHeader."Document Type"::Order);
      PurchaseHeader.TESTFIELD("Subcontracting Invoice", TRUE);

      RunPage(2, PurchaseHeader."No.", PurchaseHeader."Posting Date",
        PurchaseHeader."Buy-from Vendor No.", PurchaseHeader."Job No.",
        PurchaseHeader."Service Order No.", (PurchaseHeader.Status <= PurchaseHeader.Status::Released), 0);
    END;

    PROCEDURE RunPageFromPurchaseHdrInvoice@1100528304(PurchaseHeader@1100528303 : Record 38);
    BEGIN
      PurchaseHeader.TESTFIELD("Document Type", PurchaseHeader."Document Type"::Invoice);
      PurchaseHeader.TESTFIELD("Subcontracting Invoice", TRUE);

      RunPage(0, PurchaseHeader."No.", PurchaseHeader."Posting Date",
        PurchaseHeader."Buy-from Vendor No.", PurchaseHeader."Job No.",
        PurchaseHeader."Service Order No.", (PurchaseHeader.Status < PurchaseHeader.Status::Closed), 0);
    END;

    PROCEDURE RunPageFromPurchInvHeader@1100525004(PurchInvHeader@1100528303 : Record 122);
    BEGIN
      PurchInvHeader.TESTFIELD("Subcontracting Invoice", TRUE);

      RunPage(1, PurchInvHeader."No.", PurchInvHeader."Posting Date",
        PurchInvHeader."Buy-from Vendor No.", PurchInvHeader."Job No.",
        PurchInvHeader."Service Order No.", (NOT PurchInvHeader."Invoice Approved"), 0);
    END;

    PROCEDURE RunPageFromVendorLedgerEntry@1100528305(VendorLedgerEntry@1100528303 : Record 25);
    BEGIN
      VendorLedgerEntry.TESTFIELD("Document Type", VendorLedgerEntry."Document Type"::Invoice);
      VendorLedgerEntry.TESTFIELD("Subcontracting Invoice", TRUE);

      RunPage(1, VendorLedgerEntry."Document No.", VendorLedgerEntry."Posting Date",
        VendorLedgerEntry."Buy-from Vendor No.", VendorLedgerEntry."Project No.",
        '', (VendorLedgerEntry."Closed at Date" = 0D), VendorLedgerEntry."Entry No.");
    END;

    LOCAL PROCEDURE RunPage@1100525000(DocumentType@1100409000 : Option;DocumentNo@1100525000 : Code[20];PostingDate@1100525001 : Date;VendorNo@1100525002 : Code[20];ProjectNo@1100525003 : Code[20];ServiceOrderNo@1100525004 : Code[20];FormEditable@1100525005 : Boolean;EntryNo@1100409001 : Integer);
    VAR
      SubcontractingHoursInvoice@1100528300 : Record 11020694;
      SubcontractingHoursInvoiceFrm@1100528301 : Page 11125042;
    BEGIN
      SubcontractingHoursInvoice.FILTERGROUP(9);
      SubcontractingHoursInvoice.SETRANGE(Type, DocumentType);
      SubcontractingHoursInvoice.SETRANGE("Document No.", DocumentNo);
      SubcontractingHoursInvoice.FILTERGROUP(0);

      SubcontractingHoursInvoiceFrm.SetGlobalVars(DATE2DMY(PostingDate, 3), DATE2DWY(PostingDate, 2),
        VendorNo, ProjectNo, ServiceOrderNo, EntryNo);
      SubcontractingHoursInvoiceFrm.SETTABLEVIEW(SubcontractingHoursInvoice);
      SubcontractingHoursInvoiceFrm.EDITABLE := FormEditable;
      SubcontractingHoursInvoiceFrm.LOOKUPMODE := FormEditable;
      SubcontractingHoursInvoiceFrm.RUNMODAL;
    END;

    LOCAL PROCEDURE UpdateVendorLedgerEntry@1100528306(EntryNo@1100528301 : Integer;TotalAmount@1100528302 : Decimal;TotalBAmount@1100528303 : Decimal;TotalHours@1100528304 : Decimal);
    VAR
      VendorLedgerEntry@1100528300 : Record 25;
    BEGIN
      VendorLedgerEntry.GET(EntryNo);
      VendorLedgerEntry.VALIDATE("Number of Hours", TotalHours);
      VendorLedgerEntry.VALIDATE("Labor Amount", TotalAmount);
      VendorLedgerEntry.VALIDATE("Wage Charge (to B Account)", TotalBAmount);
      VendorLedgerEntry."Avg. Wage Rate" := ROUND(TotalAmount / TotalHours, 0.00001);
      VendorLedgerEntry.MODIFY;
    END;

    PROCEDURE UpdateReceiptsFromPurchaseOrder@1100529400();
    VAR
      SubcontractingHoursInvoice@1100528303 : Record 11020694;
      PurchaseLine@1100529400 : Record 39;
      RestQuantity@1100529401 : Decimal;
      MaxQtyToReceive@1100529402 : Decimal;
      FullMatch@1100529403 : Boolean;
      RestSum@1100529404 : Decimal;
    BEGIN
      PurchaseLine.SETRANGE("Document Type",PurchaseLine."Document Type"::Order);
      PurchaseLine.SETRANGE("Document No.", "Document No.");
      IF  PurchaseLine.FINDSET(TRUE,FALSE) THEN
        REPEAT
          PurchaseLine.VALIDATE("Qty. to Receive", 0);
          PurchaseLine.MODIFY(TRUE);
        UNTIL PurchaseLine.NEXT = 0;

      SubcontractingHoursInvoice.SETRANGE(Type, Type::Order);
      SubcontractingHoursInvoice.SETRANGE("Document No.", "Document No.");
      FullMatch := TRUE;
      IF SubcontractingHoursInvoice.FINDSET THEN BEGIN
        REPEAT
          PurchaseLine.SETRANGE("Document Type",PurchaseLine."Document Type"::Order);
          PurchaseLine.SETRANGE("Document No.", SubcontractingHoursInvoice."Document No.");
          PurchaseLine.SETRANGE("Job No.", SubcontractingHoursInvoice."Project No.");
          PurchaseLine.SETRANGE("Service Order No.", SubcontractingHoursInvoice."Service Order No.");
          PurchaseLine.SETRANGE("Extension Contract", SubcontractingHoursInvoice."Extension Contract");
          PurchaseLine.SETRANGE(Element, SubcontractingHoursInvoice.Element);
          PurchaseLine.SETRANGE("Shortcut Dimension 2 Code", SubcontractingHoursInvoice."Cost Object");
          PurchaseLine.SETFILTER(Quantity, '>%1', 0);
          RestQuantity := SubcontractingHoursInvoice."Total Hours";
          IF  PurchaseLine.FINDSET(TRUE,FALSE) THEN BEGIN
            REPEAT
              IF PurchaseLine.Quantity - (PurchaseLine."Quantity Received" + PurchaseLine."Qty. to Receive") > 0 THEN BEGIN
                MaxQtyToReceive := PurchaseLine.Quantity - (PurchaseLine."Quantity Received" + PurchaseLine."Qty. to Receive");
                IF MaxQtyToReceive >= RestQuantity THEN BEGIN
                  PurchaseLine.VALIDATE("Qty. to Receive", PurchaseLine."Qty. to Receive" + RestQuantity);
                  RestQuantity := 0;
                  PurchaseLine.MODIFY(TRUE);
                END ELSE BEGIN
                  RestQuantity := RestQuantity - MaxQtyToReceive;
                  PurchaseLine.VALIDATE("Qty. to Receive", PurchaseLine."Qty. to Receive" + MaxQtyToReceive);
                  PurchaseLine.MODIFY(TRUE);
                END;
              END;
            UNTIL ( PurchaseLine.NEXT = 0) OR (RestQuantity = 0);
            IF RestQuantity > 0 THEN BEGIN
              PurchaseLine.VALIDATE("Qty. to Receive", PurchaseLine."Qty. to Receive" + RestQuantity);
              RestQuantity := 0;
              PurchaseLine.MODIFY(TRUE)
            END;
          END;
          IF RestQuantity <> 0 THEN BEGIN
            FullMatch := FALSE;
            RestSum += RestQuantity;
          END;
        UNTIL (SubcontractingHoursInvoice.NEXT = 0);
      END;
      IF NOT FullMatch THEN
        MESSAGE(Text002, RestSum );
    END;

    PROCEDURE MoveSubcRcptToInvLines@1(VAR SubcontractingHoursInvoice@1000 : Record 11020694;DocumentType@1100529403 : Option;DocumentNo@1100529402 : Code[20];ShowProgress@1100529401 : Boolean);
    VAR
      SubcontractingHoursInvoice2@1100528200 : Record 11020694;
      SubcontractingHoursInvoice3@1100528201 : Record 11020694;
      Window@1100528403 : Dialog;
      NoOfRecords@1100528401 : Integer;
      Progress@1100528402 : Integer;
      LineNo@1100529400 : Integer;
    BEGIN
      IF GUIALLOWED AND ShowProgress THEN
        Window.OPEN(Text003);

      NoOfRecords := SubcontractingHoursInvoice.COUNT;
      SubcontractingHoursInvoice3.SETCURRENTKEY("Copy from Record ID");

      IF SubcontractingHoursInvoice.FINDSET(TRUE,TRUE) THEN BEGIN
        LineNo := GetNextSubcLineNo(SubcontractingHoursInvoice.Year, SubcontractingHoursInvoice.Week,DocumentType,DocumentNo);

        REPEAT
          Progress := Progress + 1;
          IF GUIALLOWED AND ShowProgress AND
            (NoOfRecords > 0)
          THEN
            Window.UPDATE(1, 100 * ROUND(100 * Progress / NoOfRecords, 1));
          LineNo := GetNextSubcLineNo(SubcontractingHoursInvoice.Year, SubcontractingHoursInvoice.Week,DocumentType,DocumentNo);
          SubcontractingHoursInvoice3.SETRANGE("Copy from Record ID", SubcontractingHoursInvoice.RECORDID);
          IF NOT SubcontractingHoursInvoice3.FINDFIRST THEN BEGIN
            SubcontractingHoursInvoice2 := SubcontractingHoursInvoice;
            SubcontractingHoursInvoice2.Type := DocumentType;
            SubcontractingHoursInvoice2."Document No." := DocumentNo;
            SubcontractingHoursInvoice2."Line No." := LineNo;
            SubcontractingHoursInvoice2.INSERT(TRUE);
            SubcontractingHoursInvoice2."Copy from Record ID" := SubcontractingHoursInvoice.RECORDID;
            SubcontractingHoursInvoice2.MODIFY;
          END ELSE BEGIN
            IF GUIALLOWED AND ShowProgress THEN
              MESSAGE(Text002, SubcontractingHoursInvoice.RECORDID, SubcontractingHoursInvoice3.RECORDID);
          END;
        UNTIL SubcontractingHoursInvoice.NEXT = 0;
      END;
      IF GUIALLOWED AND ShowProgress THEN
        Window.CLOSE;
    END;

    LOCAL PROCEDURE GetNextSubcLineNo@1100528200(lYear@1100528201 : Integer;lWeek@1100528202 : Integer;DocumentType@1100529402 : Option;DocumentNo@1100529401 : Code[20]) : Integer;
    VAR
      lSubcontractingHoursInvoice@1100528200 : Record 11020694;
    BEGIN
      lSubcontractingHoursInvoice.SETRANGE(Type, DocumentType);
      lSubcontractingHoursInvoice.SETRANGE("Document No.", DocumentNo);
      lSubcontractingHoursInvoice.SETRANGE(Year, lYear);
      lSubcontractingHoursInvoice.SETRANGE(Week, lWeek);
      IF lSubcontractingHoursInvoice.FINDLAST THEN
        EXIT(lSubcontractingHoursInvoice."Line No." + 10000)
      ELSE
        EXIT(10000);
    END;

    BEGIN
    END.
  }
}

