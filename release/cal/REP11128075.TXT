OBJECT Report 11128075 Approval reg. PG Autogiro A01
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=PE7.02.01 (12047970);
  }
  PROPERTIES
  {
    CaptionML=[ENU=Approval reg. PG Auto Giro A01;
               SVE=Medgiv.reg. PG Autogiro A01];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF EXISTS(gFile.NAME) THEN
                    ERASE(gFile.NAME);
                  gFile.TEXTMODE(TRUE);
                  gFile.WRITEMODE(TRUE);
                  gServerTempFilename := gFileMgt.ServerTempFileName('');
                  gFile.CREATE(gServerTempFilename);

                  gCompanyInfo.GET;
                  gCompanyInfo.TESTFIELD("Plus Giro No.");
                  gCompanyInfo.TESTFIELD("Plus Giro Ref. Account No.");
                  gFile.WRITE('00'+AdjustRight(gCompanyInfo."Plus Giro Ref. Account No.",6,' ')+
                          FORMAT(TODAY,0,'<Year,2><Month,2><Day,2>')+PADSTR('',66,' '));
                  gWindow.OPEN(PEB001);
                END;

    OnPostReport=BEGIN
                   gFile.WRITE('09'+AdjustRight(gCompanyInfo."Plus Giro Ref. Account No.",6,' ')+
                           AdjustRight(FORMAT(gNoOfLines),7,'0')+PADSTR('',65,' '));
                   gFile.CLOSE;
                   IF NOT gFileMgt.CanRunDotNetOnClient THEN
                     gFileMgt.DownloadHandler(gServerTempFilename,'','','','')
                   ELSE
                     gFileMgt.DownloadToFile(gServerTempFilename,gFile.NAME);
                 END;

    UsageCategory=ReportsAndAnalysis;
  }
  DATASET
  {
    { 6836;    ;DataItem;                    ;
               DataItemTable=Table18;
               DataItemTableView=SORTING(No.)
                                 WHERE(Auto Giro Type=CONST(PG Sweden));
               OnPreDataItem=BEGIN
                               IF gFile.NAME = '' THEN
                                 ERROR(PEB003);

                               Customer.SETFILTER("Approval Status",'%1|%2',Customer."Approval Status"::"Apply for approval",
                                              Customer."Approval Status"::"Apply for cancellation");
                               IF Customer.COUNT = 0 THEN
                                 ERROR(PEB004);
                             END;

               OnAfterGetRecord=BEGIN
                                  gWindow.UPDATE(1,"No.");
                                  TESTFIELD("Plus Giro No.");
                                  CLEAR(gTransferType);

                                  IF "Approval Status" = "Approval Status"::"Apply for approval" THEN
                                    gTransferType := '02';
                                  IF "Approval Status" = "Approval Status"::"Apply for cancellation" THEN
                                    gTransferType := '03';
                                  IF gTransferType = '' THEN
                                    ERROR(PEB005);

                                  gFile.WRITE('01'+AdjustRight("Plus Giro No.",10,' ')+PADSTR('',68,' '));
                                  gFile.WRITE(gTransferType+AdjustRight("No.",18,' ')+PADSTR('',5,' ')+AdjustRight("Plus Giro No.",13,' ')+PADSTR('',42,' '));
                                  gFile.WRITE('08'+AdjustRight("Plus Giro No.",10,' ')+AdjustRight('1',7,'0')+PADSTR('',61,' '));

                                  "Approval Status" := "Approval Status"::"Sent for approval";
                                  MODIFY;

                                  gNoOfLines := gNoOfLines + 1;
                                END;

               ReqFilterFields=No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   gFileNameVisible := gFileMgt.CanRunDotNetOnClient;
                 END;

    }
    CONTROLS
    {
      { 1000000000;;Container;
                  Name=Cont;
                  ContainerType=ContentArea }

      { 1100570000;1;Group  ;
                  CaptionML=[ENU=Options;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1000000001;2;Field  ;
                  CaptionML=[ENU=File Name;
                             NOR=Filnavn;
                             SVE=Filnamn];
                  SourceExpr=gFileName;
                  Visible=gFileNameVisible;
                  OnAssistEdit=BEGIN
                                 gFileName := gFileMgt.SaveFileDialog(PEB007,gFileName,PEB006);
                               END;
                                }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      gFileName@1000 : Text[250];
      gFile@1001 : File;
      gCompanyInfo@1002 : Record 79;
      gTransferType@1003 : Text[2];
      gWindow@1004 : Dialog;
      gNoOfLines@1005 : Integer;
      PEB001@1007 : TextConst 'ENU=Customer     #1#############;SVE=Kund     #1#############';
      PEB002@1008 : TextConst 'ENU=Field content %1 is longer than %2, which is max length for field content;SVE=F„ltinneh†llet %1 „r l„ngre „n %2, vilket „r maxl„ngden p† f„ltinneh†llet.';
      PEB003@1009 : TextConst 'ENU=Fill in file name;SVE=Skriv in filnamn.';
      PEB004@1010 : TextConst 'ENU=No value exists.;SVE=Det finns inget att l„sa ut.';
      PEB005@1011 : TextConst 'ENU=Incorrect choice of approval status has been done!;SVE=Felaktigt urval av medgivandestatus har skett!';
      gServerTempFilename@1000000000 : Text[1024];
      PEB006@1100570002 : TextConst 'ENU=All files|*.*|TXT-files (*.txt)|*.txt|WRI-files (*.wri)|*.wri;SVE=Alla filer|*.*|TXT-filer (*.txt)|*.txt|WRI-filer (*.wri)|*.wri';
      PEB007@1100570001 : TextConst 'ENU=PG Autogiro;SVE=PG Autogiro';
      gFileMgt@12047958 : Codeunit 419;
      TextMgt@12047959 : Codeunit 11128088;
      gFileNameVisible@12047960 : Boolean INDATASET;

    PROCEDURE SumCheck@5(CheckNo@1000 : Code[20]) : Code[20];
    VAR
      ChSum@1001 : Integer;
      i@1002 : Integer;
      C@1003 : Integer;
    BEGIN
      CheckNo := ClearNo(CheckNo);
      CheckNo := COPYSTR(PADSTR('', 10 - STRLEN(CheckNo), '0') + CheckNo, 1, 9);
      CLEAR(ChSum);
      FOR i := 1 TO 9 DO BEGIN
        EVALUATE(C, COPYSTR(CheckNo, i, 1));
        ChSum := ChSum + ((C * (( i MOD 2) + 1)) MOD 10);
        ChSum := ChSum + ((C * (( i MOD 2) + 1)) DIV 10);
      END;
      ChSum := (10 - (ChSum MOD 10)) MOD 10;
      EXIT(FORMAT(ChSum));
    END;

    PROCEDURE ClearNo@2(TextIn@1000 : Text[250]) : Text[250];
    VAR
      NotNumeric@1001 : Text[250];
    BEGIN
      NotNumeric := DELCHR(TextIn,'=','0123456789');
      EXIT(DELCHR(TextIn,'=',NotNumeric));
    END;

    PROCEDURE AdjustRight@1(TextIn@1000 : Text[250];Length@1001 : Integer;FillCharacter@1002 : Text[1]) : Text[250];
    BEGIN
      TextIn := ClearNo(TextIn);
      IF STRLEN(TextIn) > Length THEN
        ERROR(PEB002,TextIn,Length);
      EXIT(PADSTR('', Length - STRLEN(TextIn), FillCharacter) + TextIn);
    END;

    BEGIN
    {
      PEB *** Begin ***
      PEB0006 Autogiro
      PEB
      PEB *** End ***
    }
    END.
  }
  RDLDATA
  {
  }
}

