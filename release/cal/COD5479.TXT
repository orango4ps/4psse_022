OBJECT Codeunit 5479 Graph Mgt - Customer Payments
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      GraphMgtJournal@1000 : Codeunit 5482;

    [External]
    PROCEDURE SetCustomerPaymentsTemplateAndBatch@1(VAR GenJournalLine@1001 : Record 81;CustomerPaymentBatchName@1003 : Code[10]);
    BEGIN
      GenJournalLine.VALIDATE("Journal Template Name",GraphMgtJournal.GetDefaultCustomerPaymentsTemplateName);
      GenJournalLine.SETRANGE("Journal Template Name",GenJournalLine."Journal Template Name");

      GenJournalLine.VALIDATE("Journal Batch Name",CustomerPaymentBatchName);
      GenJournalLine.SETRANGE("Journal Batch Name",CustomerPaymentBatchName);
    END;

    [External]
    PROCEDURE SetCustomerPaymentsFilters@9(VAR GenJournalLine@1000 : Record 81);
    BEGIN
      GenJournalLine.SETRANGE("Document Type",GenJournalLine."Document Type"::Payment);
      GenJournalLine.SETRANGE("Account Type",GenJournalLine."Account Type"::Customer);
      GenJournalLine.SETRANGE("Applies-to Doc. Type",GenJournalLine."Applies-to Doc. Type"::Invoice);
      GenJournalLine.SETRANGE("Journal Template Name",GraphMgtJournal.GetDefaultCustomerPaymentsTemplateName);
    END;

    [External]
    PROCEDURE SetCustomerPaymentsValues@10(VAR GenJournalLine@1000 : Record 81;TempGenJournalLine@1001 : TEMPORARY Record 81);
    VAR
      GenJournalBatch@1003 : Record 232;
      DummyDate@1002 : Date;
    BEGIN
      GenJournalLine.VALIDATE("Account Type",GenJournalLine."Account Type"::Customer);
      IF NOT ISNULLGUID(TempGenJournalLine."Account Id") THEN
        GenJournalLine.VALIDATE("Account Id",TempGenJournalLine."Account Id");
      GenJournalLine.VALIDATE("Account No.",TempGenJournalLine."Account No.");
      IF TempGenJournalLine."Contact Graph Id" <> '' THEN
        GenJournalLine.VALIDATE("Contact Graph Id",TempGenJournalLine."Contact Graph Id");
      IF TempGenJournalLine."Posting Date" <> DummyDate THEN
        GenJournalLine.VALIDATE("Posting Date",TempGenJournalLine."Posting Date");
      GenJournalLine.VALIDATE("Document Type",GenJournalLine."Document Type"::Payment);
      GenJournalLine.VALIDATE("Document No.",TempGenJournalLine."Document No.");
      GenJournalLine.VALIDATE("External Document No.",TempGenJournalLine."External Document No.");
      GenJournalLine.VALIDATE(Amount,TempGenJournalLine.Amount);
      GenJournalLine.VALIDATE("Applies-to Doc. Type",GenJournalLine."Applies-to Doc. Type"::Invoice);
      GenJournalLine."Applies-to Doc. No." := TempGenJournalLine."Applies-to Doc. No.";
      IF NOT ISNULLGUID(TempGenJournalLine."Applies-to Invoice Id") THEN
        GenJournalLine.VALIDATE("Applies-to Invoice Id",TempGenJournalLine."Applies-to Invoice Id");
      IF TempGenJournalLine.Description <> '' THEN
        GenJournalLine.VALIDATE(Description,TempGenJournalLine.Description);
      GenJournalLine.VALIDATE(Comment,TempGenJournalLine.Comment);
      GenJournalLine.VALIDATE("Bal. Account No.",'');
      IF GenJournalBatch.GET(GenJournalLine."Journal Template Name",GenJournalLine."Journal Batch Name") THEN BEGIN
        GenJournalLine.VALIDATE("Bal. Account Type",GenJournalBatch."Bal. Account Type");
        GenJournalLine.VALIDATE("Bal. Account No.",GenJournalBatch."Bal. Account No.");
      END
    END;

    [External]
    PROCEDURE UpdateIntegrationRecords@2(OnlyItemsWithoutId@1000 : Boolean);
    VAR
      GenJnlLine@1003 : Record 81;
      GraphMgtGeneralTools@1002 : Codeunit 5465;
      GenJnlLineRecordRef@1001 : RecordRef;
    BEGIN
      GenJnlLineRecordRef.OPEN(DATABASE::"Gen. Journal Line");
      GraphMgtGeneralTools.UpdateIntegrationRecords(GenJnlLineRecordRef,GenJnlLine.FIELDNO(Id),OnlyItemsWithoutId);
    END;

    LOCAL PROCEDURE EnableAccountODataWebService@1008();
    BEGIN
      UpdateIntegrationRecords(FALSE);
    END;

    [EventSubscriber(Codeunit,5465,ApiSetup)]
    LOCAL PROCEDURE HandleApiSetup@1003();
    BEGIN
      EnableAccountODataWebService;
      UpdateIds;
    END;

    [External]
    PROCEDURE UpdateIds@4();
    VAR
      GenJournalLine@1000 : Record 81;
    BEGIN
      WITH GenJournalLine DO BEGIN
        SETRANGE("Account Type","Account Type"::Customer);

        IF FINDSET THEN
          REPEAT
            UpdateCustomerID;
            UpdateGraphContactId;
            UpdateAppliesToInvoiceID;
            UpdateJournalBatchID;
            MODIFY(FALSE);
          UNTIL NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}

