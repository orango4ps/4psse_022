OBJECT Table 11229805 Hour Consent Rule
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Consent Level");
             END;

    OnModify=BEGIN
               TESTFIELD("Consent Level");
             END;

    OnDelete=VAR
               HourConsentRuleFilter@1100528400 : Record 11229806;
             BEGIN
               HourConsentRuleFilter.SETRANGE("Hour Consent Rule Line No.", "Line No.");
               HourConsentRuleFilter.DELETEALL;
             END;

    CaptionML=ENU=Hour Consent Rule;
  }
  FIELDS
  {
    { 10  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 20  ;   ;Consent Level       ;Integer       ;CaptionML=[ENU=Consent Level;
                                                              NOR=Attestniv†;
                                                              SVE=Godk„nnandeniv†] }
    { 30  ;   ;Process             ;Option        ;OnValidate=BEGIN
                                                                IF Process = Process::"Hour Accounting" THEN BEGIN
                                                                  IF (Type = Type::"Unmanned Plant - Project") OR
                                                                    (Type = Type::"Unmanned Plant - Service") THEN
                                                                    FIELDERROR(Process);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Process;
                                                              NOR=Behandle;
                                                              SVE=Process];
                                                   OptionCaptionML=ENU=Hour Accounting,Site Management Hour Accounting;
                                                   OptionString=Hour Accounting,Site Management Hour Accounting }
    { 40  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF (Type <> Type::Project) AND
                                                                  (Type <> Type::"Unmanned Plant - Project") THEN
                                                                  "Project Role" := 0;

                                                                IF (Type <> Type::Service) AND
                                                                  (Type <> Type::"Unmanned Plant - Service") THEN
                                                                  "Service Role" := 0;

                                                                IF Process = Process::"Hour Accounting" THEN BEGIN
                                                                  IF (Type = Type::"Unmanned Plant - Project") OR
                                                                    (Type = Type::"Unmanned Plant - Service") THEN
                                                                    FIELDERROR(Type);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=ENU=,Project,Service,Indirect,,Plant,Unmanned Plant - Project,Unmanned Plant - Service,,,ResourceRequest,,,,,All;
                                                   OptionString=,Project,Service,Indirect,,Plant,Unmanned Plant - Project,Unmanned Plant - Service,,,ResourceRequest,,,,,All }
    { 50  ;   ;Filters Active      ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Hour Consent Rule Filter" WHERE (Hour Consent Rule Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=Filters Active;
                                                              NOR=Filtre aktiv;
                                                              SVE=Filter aktiv];
                                                   Editable=No }
    { 60  ;   ;Approver            ;Code50        ;TableRelation=Approver."User ID";
                                                   OnValidate=BEGIN
                                                                IF Approver <> '' THEN BEGIN
                                                                  TESTFIELD("Project Role", 0);
                                                                  TESTFIELD("Service Role", 0);
                                                                  TESTFIELD("Approver Group Code", '');
                                                                  TESTFIELD(Manager, FALSE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Approver;
                                                              NOR=Godkjenner;
                                                              SVE=Godk„nnare] }
    { 70  ;   ;Approver Group Code ;Code10        ;TableRelation="Approver Group".Code;
                                                   OnValidate=BEGIN
                                                                IF "Approver Group Code" <> '' THEN BEGIN
                                                                  TESTFIELD("Project Role", 0);
                                                                  TESTFIELD("Service Role", 0);
                                                                  TESTFIELD(Approver, '');
                                                                  TESTFIELD(Manager, FALSE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Approver Group Code;
                                                              SVE=Godk„nnandegruppkod] }
    { 80  ;   ;Manager             ;Boolean       ;OnValidate=BEGIN
                                                                IF Approver <> '' THEN BEGIN
                                                                  TESTFIELD("Project Role", 0);
                                                                  TESTFIELD("Service Role", 0);
                                                                  TESTFIELD("Approver Group Code", '');
                                                                  TESTFIELD(Approver, '');
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Manager;
                                                              NOR=Manager;
                                                              SVE=Manager] }
    { 90  ;   ;Project Role        ;Option        ;OnValidate=BEGIN
                                                                IF "Project Role" > 0 THEN BEGIN
                                                                  TESTFIELD(Approver, '');
                                                                  TESTFIELD("Approver Group Code", '');
                                                                  TESTFIELD("Service Role", 0);
                                                                  TESTFIELD(Manager, FALSE);
                                                                  IF (Type <> Type::"Unmanned Plant - Project") AND
                                                                    (Type <> Type::Project) THEN
                                                                    FIELDERROR("Project Role");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Project Role;
                                                              NOR=Prosjektroll;
                                                              SVE=Projektroll];
                                                   OptionCaptionML=[ENU=" ,Project Manager,Project Engineer,Purchaser,Site Manager,Administrator,Estimator";
                                                                    SVE=" ,Platschef,Projektingenj”r,Ink”pare,Arbetschef,Administrat”r,Kalkylator"];
                                                   OptionString=[ ,Project Manager,Project Engineer,Purchaser,Site Manager,Administrator,Estimator];
                                                   BlankZero=Yes }
    { 100 ;   ;Service Role        ;Option        ;OnValidate=BEGIN
                                                                IF "Service Role" > 0 THEN BEGIN
                                                                  TESTFIELD(Approver, '');
                                                                  TESTFIELD("Approver Group Code", '');
                                                                  TESTFIELD("Project Role", 0);
                                                                  TESTFIELD(Manager, FALSE);
                                                                  IF (Type <> Type::"Unmanned Plant - Service") AND
                                                                    (Type <> Type::Service) THEN
                                                                    FIELDERROR("Service Role");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Service Role;
                                                              NOR=Serviceroll;
                                                              SVE=Serviceroll];
                                                   OptionCaptionML=[ENU=" ,Order Manager,Assigned to,Employee,Location Manager,Contract Manager";
                                                                    NOR=" ,Ordermanager,Tilornet,Anst„lld,Lokasjonsleder,Kontraktleder";
                                                                    SVE=" ,Orderchef,Tilldelat till,Anst„lld,Platschef,Kontrakthanterare"];
                                                   OptionString=[ ,Order Manager,Assigned to,Employee,Location Manager,Contract Manager];
                                                   BlankZero=Yes }
    { 110 ;   ;No Consent          ;Boolean       ;CaptionML=[ENU=No Consent;
                                                              NOR=Ikke attest;
                                                              SVE=Ingen godk„nnande] }
    { 120 ;   ;Autom. Consent Own Lines;Boolean   ;CaptionML=[ENU=Autom. Consent Own Lines;
                                                              NOR=Autom. godkjenne egne linjer;
                                                              SVE=Autom. godk„nna egna rader] }
    { 220 ;   ;Super Approver      ;Code50        ;TableRelation=Approver."User ID" WHERE (Super Approver Hours=CONST(Yes));
                                                   CaptionML=[ENU=Super Approver;
                                                              SVE="™verordnad godk„nnare "] }
  }
  KEYS
  {
    {    ;Line No.                                ;Clustered=Yes }
    {    ;Consent Level                            }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100529500 : TextConst 'ENU=You have selected multiple hour consent rules. Would you like to delete the currently applied filters and replace them with new filters?';
      Text002@1100529501 : TextConst 'ENU=Select multiple hour consent rule rows to use this action.';

    PROCEDURE ShowConsentRuleFilters@1100528401(ReadOnly@1100528400 : Boolean);
    VAR
      HourConsentRuleFilter@1100528401 : Record 11229806;
      HourConsentRuleFilters@1100528402 : Page 11229788;
    BEGIN
      HourConsentRuleFilter.SETRANGE("Hour Consent Rule Line No.", "Line No.");
      IF ReadOnly THEN
        HourConsentRuleFilters.EDITABLE(FALSE);
      HourConsentRuleFilters.SETTABLEVIEW(HourConsentRuleFilter);
      HourConsentRuleFilters.RUNMODAL;
    END;

    PROCEDURE GetSuperApprover@1100528405() SuperApprover : Code[50];
    BEGIN
      //FIXME
    END;

    PROCEDURE ApplyMultipleConsentRuleFilters@1100529500(selectedLine@1100529500 : Integer;VAR SelectedHourConsentRules@1100529502 : Record 11229805);
    VAR
      HourConsentRuleFilter@1100529503 : Record 11229806;
      HourConsentRuleFilterNew@1100529504 : Record 11229806;
    BEGIN
      IF SelectedHourConsentRules.COUNT > 1 THEN
        ShowConsentRuleFilters(FALSE)
      ELSE
        ERROR(Text002);

      IF NOT CONFIRM(Text001) THEN
        EXIT;

      IF SelectedHourConsentRules.FINDSET THEN REPEAT
        IF SelectedHourConsentRules."Line No." <> selectedLine THEN BEGIN
          HourConsentRuleFilter.SETRANGE("Hour Consent Rule Line No.", SelectedHourConsentRules."Line No.");
          HourConsentRuleFilter.DELETEALL;
        END;
      UNTIL SelectedHourConsentRules.NEXT = 0;

      HourConsentRuleFilter.SETRANGE("Hour Consent Rule Line No.", selectedLine);
      IF HourConsentRuleFilter.FINDSET THEN
        REPEAT
          IF SelectedHourConsentRules.FINDSET THEN
            REPEAT
              IF SelectedHourConsentRules."Line No." <> selectedLine THEN BEGIN
                HourConsentRuleFilterNew.INIT;
                HourConsentRuleFilterNew.TRANSFERFIELDS(HourConsentRuleFilter);
                HourConsentRuleFilterNew."Hour Consent Rule Line No." := SelectedHourConsentRules."Line No.";
                HourConsentRuleFilterNew.INSERT;
              END;
            UNTIL SelectedHourConsentRules.NEXT = 0;
        UNTIL HourConsentRuleFilter.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

