OBJECT Table 12013656 ExFlow Default Dimension
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF405000;
  }
  PROPERTIES
  {
    OnRename=BEGIN
               ERROR(Text000,TABLECAPTION);
             END;

    CaptionML=[ENU=Default Dimension;
               SVE=Standarddimension];
  }
  FIELDS
  {
    { 1   ;   ;Table ID            ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   OnValidate=VAR
                                                                TempObject@1000 : TEMPORARY Record 2000000001;
                                                              BEGIN
                                                                CALCFIELDS("Table Caption");
                                                                SetupObjectNoList(TempObject);
                                                                TempObject.Type := TempObject.Type::Table;
                                                                TempObject.ID := "Table ID";
                                                                IF NOT TempObject.FIND THEN
                                                                  FIELDERROR("Table ID");
                                                              END;

                                                   OnLookup=VAR
                                                              TempObject@1000 : TEMPORARY Record 2000000001;
                                                            BEGIN
                                                              CLEAR(TempObject);
                                                              SetupObjectNoList(TempObject);
                                                              IF PAGE.RUNMODAL(358,TempObject) = ACTION::LookupOK THEN BEGIN
                                                                "Table ID" := TempObject.ID;
                                                                VALIDATE("Table ID");
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Table ID;
                                                              SVE=Tabell-ID];
                                                   NotBlank=Yes }
    { 2   ;   ;No.                 ;Code50        ;TableRelation=IF (Table ID=CONST(12013606)) "EX User Group"
                                                                 ELSE IF (Table ID=CONST(12013632)) "EX Reference";
                                                   CaptionML=[ENU=No.;
                                                              SVE=Nr] }
    { 3   ;   ;Dimension Code      ;Code20        ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                IF NOT DimMgt.CheckDim("Dimension Code") THEN
                                                                  ERROR(DimMgt.GetDimErr);
                                                              END;

                                                   CaptionML=[ENU=Dimension Code;
                                                              SVE=Dimensionskod];
                                                   NotBlank=Yes }
    { 4   ;   ;Dimension Value Code;Code20        ;TableRelation="Dimension Value".Code WHERE (Dimension Code=FIELD(Dimension Code));
                                                   OnValidate=BEGIN
                                                                IF NOT DimMgt.CheckDimValue("Dimension Code","Dimension Value Code") THEN
                                                                  ERROR(DimMgt.GetDimErr);
                                                              END;

                                                   CaptionML=[ENU=Dimension Value Code;
                                                              SVE=Dimensionsv„rdekod] }
    { 6   ;   ;Table Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table ID)));
                                                   CaptionML=[ENU=Table Caption;
                                                              SVE=Tabellrubrik];
                                                   Editable=No }
    { 7   ;   ;Multi Selection Action;Option      ;CaptionML=[ENU=Multi Selection Action;
                                                              SVE=Flervals†tg„rd];
                                                   OptionCaptionML=[ENU=" ,Change,Delete";
                                                                    SVE=" ,Žndra,Ta bort"];
                                                   OptionString=[ ,Change,Delete] }
    { 9   ;   ;No. 2               ;Code50        ;CaptionML=SVE=Nr 2 }
  }
  KEYS
  {
    {    ;Table ID,No.,No. 2,Dimension Code       ;Clustered=Yes }
    {    ;Dimension Code                           }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You can''t rename a %1.;SVE=Du kan inte byta namn p† %1.';
      DimMgt@1002 : Codeunit 408;

    PROCEDURE GetCaption@3() : Text[250];
    VAR
      ObjTransl@1000 : Record 377;
      CurrTableID@1001 : Integer;
      NewTableID@1002 : Integer;
      NewNo@1003 : Code[50];
      SourceTableName@1004 : Text[100];
    BEGIN
      IF NOT EVALUATE(NewTableID,GETFILTER("Table ID")) THEN
        EXIT('');

      IF NewTableID = 0 THEN
        IF GETRANGEMIN("Table ID") = GETRANGEMAX("Table ID") THEN
          NewTableID := GETRANGEMIN("Table ID")
        ELSE
          NewTableID := 0;

      IF NewTableID <> CurrTableID THEN
        SourceTableName := ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,NewTableID);
      CurrTableID := NewTableID;

      IF GETFILTER("No.") <> '' THEN
        IF GETRANGEMIN("No.") = GETRANGEMAX("No.") THEN
          NewNo := GETRANGEMIN("No.")
        ELSE
          NewNo := '';

      IF NewTableID <> 0 THEN
        EXIT(STRSUBSTNO('%1 %2',SourceTableName,NewNo));

      EXIT('');
    END;

    LOCAL PROCEDURE SetupObjectNoList@40(VAR TempObject@1000 : TEMPORARY Record 2000000001);
    VAR
      Object@1001 : Record 2000000001;
      TableIDArray@1003 : ARRAY [25] OF Integer;
      Index@1004 : Integer;
    BEGIN
      TableIDArray[1] := DATABASE::"EX User Group";
      TableIDArray[2] := DATABASE::"EX Reference";

      Object.SETRANGE(Type,Object.Type::Table);

      FOR Index := 1 TO ARRAYLEN(TableIDArray) DO BEGIN
        Object.SETRANGE(ID,TableIDArray[Index]);
        IF Object.FINDFIRST THEN BEGIN
          TempObject := Object;
          TempObject.INSERT;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

