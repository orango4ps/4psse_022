OBJECT Table 11071936 Serv. Disc. Filter Group Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD(Code);
               VALIDATE("Table Type");
             END;

    OnDelete=VAR
               ServDiscFilterGroupLine@1100528600 : Record 11071936;
             BEGIN
               ServDiscFilterGroupLine.SETRANGE(Code, Code);
               ServDiscFilterGroupLine.SETRANGE("Linked To Line No.", "Line No.");
               ServDiscFilterGroupLine.MODIFYALL("Linked To Line No.", 0, TRUE);
             END;

    CaptionML=[ENU=Service Discount Filter Group Line;
               NOR=Rad for servicerabattfiltergruppe];
  }
  FIELDS
  {
    { 10  ;   ;Code                ;Code20        ;TableRelation="Serv. Discount Filter Group";
                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   Editable=No }
    { 20  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnummer];
                                                   Editable=No }
    { 30  ;   ;Table Type          ;Option        ;OnValidate=VAR
                                                                ServDiscountFilterGroup@1100528600 : Record 11071935;
                                                              BEGIN
                                                                ServDiscountFilterGroup.GET(Code);
                                                                CASE "Table Type" OF
                                                                  "Table Type"::Item:
                                                                    BEGIN
                                                                      ServDiscountFilterGroup.TESTFIELD("Filter Type", ServDiscountFilterGroup."Filter Type"::"Cost Plus");
                                                                      "Table No." := DATABASE::Item;
                                                                    END;
                                                                  "Table Type"::"Trade Item":
                                                                    BEGIN
                                                                      ServDiscountFilterGroup.TESTFIELD("Filter Type", ServDiscountFilterGroup."Filter Type"::"Cost Plus");
                                                                      "Table No." := DATABASE::"Trade Item";
                                                                    END;
                                                                  "Table Type"::"Basic Item":
                                                                    BEGIN
                                                                      ServDiscountFilterGroup.TESTFIELD("Filter Type", ServDiscountFilterGroup."Filter Type"::"Cost Plus");
                                                                      "Table No." := DATABASE::"Basic Item";
                                                                    END;
                                                                  "Table Type"::"Service Order":
                                                                    "Table No." := DATABASE::"Service Order";
                                                                  "Table Type"::"Service Contract":
                                                                    BEGIN
                                                                      ServDiscountFilterGroup.TESTFIELD("Filter Type", ServDiscountFilterGroup."Filter Type"::"Invoice Base");
                                                                      "Table No." := DATABASE::"Service-Contract";
                                                                    END;
                                                                  "Table Type"::"Service Location":
                                                                    BEGIN
                                                                      ServDiscountFilterGroup.TESTFIELD("Filter Type", ServDiscountFilterGroup."Filter Type"::"Invoice Base");
                                                                      "Table No." := DATABASE::"Service Location";
                                                                    END;
                                                                  "Table Type"::"Service Object":
                                                                    BEGIN
                                                                      ServDiscountFilterGroup.TESTFIELD("Filter Type", ServDiscountFilterGroup."Filter Type"::"Invoice Base");
                                                                      "Table No." := DATABASE::"Service-Object";
                                                                    END;
                                                                  "Table Type"::"Standard Object":
                                                                    BEGIN
                                                                      ServDiscountFilterGroup.TESTFIELD("Filter Type", ServDiscountFilterGroup."Filter Type"::"Invoice Base");
                                                                      "Table No." := DATABASE::"Standard Object";
                                                                    END;
                                                                  "Table Type"::"Object Specification":
                                                                    BEGIN
                                                                      ServDiscountFilterGroup.TESTFIELD("Filter Type", ServDiscountFilterGroup."Filter Type"::"Invoice Base");
                                                                      "Table No." := 0;
                                                                    END;
                                                                END;
                                                                VALIDATE("Field No.", 0);
                                                                ClearValueFilter;
                                                                VALIDATE("Specification Code", '');
                                                              END;

                                                   CaptionML=[ENU=Table Type;
                                                              NOR=Tabelltype;
                                                              SVE=Tabelltyp];
                                                   OptionCaptionML=ENU=Navision Item,Trade Item,Basic Item,,,,,Service Order,Service Contract,Service Location,Service Object,Standard Object,Object Specification;
                                                   OptionString=Item,Trade Item,Basic Item,,,,,Service Order,Service Contract,Service Location,Service Object,Standard Object,Object Specification }
    { 40  ;   ;Table No.           ;Integer       ;InitValue=27;
                                                   OnValidate=BEGIN
                                                                ClearValueFilter;
                                                              END;

                                                   CaptionML=[ENU=Table No.;
                                                              NOR=Tabellnr.;
                                                              SVE=Tabellnr] }
    { 50  ;   ;Field No.           ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table No.),
                                                                                  Class=CONST(Normal),
                                                                                  Type=FILTER(<>BLOB));
                                                   OnValidate=BEGIN
                                                                ClearValueFilter;
                                                              END;

                                                   OnLookup=VAR
                                                              Field@1100529200 : Record 2000000041;
                                                              FieldTableManagement@1100529201 : Codeunit 11012372;
                                                            BEGIN
                                                              Field.SETRANGE(TableNo, "Table No.");
                                                              Field.SETRANGE(Class, Field.Class::Normal);
                                                              Field.SETFILTER(Type, '<>%1', Field.Type::BLOB);
                                                              FieldTableManagement.SelectFieldFromList("Table No.","Field No.",Field);
                                                              VALIDATE("Field No.");
                                                            END;

                                                   CaptionML=[ENU=Field No.;
                                                              NOR=Feltnr.;
                                                              SVE=F„ltnr];
                                                   BlankZero=Yes }
    { 60  ;   ;Value Filter        ;Text250       ;OnValidate=BEGIN
                                                                EvaluateFilter();
                                                              END;

                                                   CaptionML=[ENU=Value Filter;
                                                              NOR=Verdifilter;
                                                              SVE=V„rdefilter] }
    { 70  ;   ;Field Name          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field.FieldName WHERE (TableNo=FIELD(Table No.),
                                                                                             No.=FIELD(Field No.)));
                                                   CaptionML=[ENU=Field Name;
                                                              NOR=Feltnavn;
                                                              SVE=F„ltnamn];
                                                   Editable=No }
    { 80  ;   ;Field Caption       ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Table No.),
                                                                                                   No.=FIELD(Field No.)));
                                                   CaptionML=[ENU=Field Caption;
                                                              NOR=Felttekst;
                                                              SVE=F„ltrubrik];
                                                   Editable=No }
    { 90  ;   ;Field Type          ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Type Name" WHERE (TableNo=FIELD(Table No.),
                                                                                               No.=FIELD(Field No.)));
                                                   CaptionML=[ENU=Field Type;
                                                              NOR=Felttype;
                                                              SVE=Field Type];
                                                   Editable=No }
    { 100 ;   ;Linked To Line No.  ;Integer       ;TableRelation="Serv. Disc. Filter Group Line"."Line No." WHERE (Code=FIELD(Code),
                                                                                                                   Linked To Line No.=CONST(0),
                                                                                                                   Table Type=FIELD(Table Type));
                                                   OnValidate=BEGIN
                                                                TestLinkedLineNo;
                                                                TestInfiniteLoop;
                                                              END;

                                                   CaptionML=ENU=Linked To Line No. }
    { 110 ;   ;Specification Code  ;Code10        ;TableRelation=IF (Table Type=CONST(Object Specification)) "Base Object Specification Line".Code;
                                                   OnValidate=VAR
                                                                ServDiscountFilterGroup@1100528600 : Record 11071935;
                                                              BEGIN
                                                                IF "Specification Code" <> '' THEN BEGIN
                                                                  ServDiscountFilterGroup.GET(Code);
                                                                  ServDiscountFilterGroup.TESTFIELD("Filter Type", ServDiscountFilterGroup."Filter Type"::"Invoice Base");
                                                                END;
                                                                ClearValueFilter;
                                                              END;

                                                   CaptionML=[ENU=Specification Code;
                                                              NOR=Spesifikasjonskode;
                                                              SVE=Specifikationskod] }
    { 120 ;   ;Specification Description;Text50   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Base Object Specification Line".Description WHERE (Code=FIELD(Specification Code)));
                                                   CaptionML=[ENU=Specification Description;
                                                              NOR=Spesifikasjonsbeskrivelse;
                                                              SVE=Specifikationsbeskrivning];
                                                   Editable=No }
    { 130 ;   ;Specification Field Type;Option    ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Base Object Specification Line"."Field Type" WHERE (Code=FIELD(Specification Code)));
                                                   CaptionML=[ENU=Specification Field Type;
                                                              NOR=Spesifikasjonsfelt, type;
                                                              SVE=Specifikationsf„lttyp];
                                                   OptionCaptionML=[ENU="Date,Value,Text,Option, ";
                                                                    NOR="Dato,Verdi,Tekst,Alternativ, ";
                                                                    SVE="Datum,V„rde,Text,Alternativ, "];
                                                   OptionString=[Date,Value,Text,Option, ];
                                                   Editable=No }
    { 140 ;   ;Specification Unit Of Measure;Code10;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Base Object Specification Line"."Unit Of Measure" WHERE (Code=FIELD(Specification Code)));
                                                   TableRelation="Unit of Measure";
                                                   CaptionML=[ENU=Specification Unit Of Measure;
                                                              NOR=Spesifikasjonsenhet;
                                                              SVE=Specifikationsenhet];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Code,Line No.                           ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528600 : TextConst 'ENU=t;NOR=t;SVE=t';
      Text001@1100528601 : TextConst 'ENU=This creates a loop';
      Text002@1100528602 : TextConst 'ENU=Only Links between same tables allowed';
      Text003@1100528603 : TextConst 'ENU=There are lines linked to this line, linking not allowed';

    PROCEDURE EvaluateFilter@1210190000();
    VAR
      ObjectSpecificationLine@1100528602 : Record 11071781;
      TableRef@1210190001 : RecordRef;
      FieldRef@1210190002 : FieldRef;
    BEGIN
      CASE "Table Type" OF
        "Table Type"::"Object Specification":
          BEGIN
            CALCFIELDS("Specification Field Type");
            ObjectSpecificationLine.SETRANGE(Code, "Specification Code");
            CASE "Specification Field Type" OF
              "Specification Field Type"::Date:
                BEGIN
                  ConvertDateFilter("Value Filter");
                  ObjectSpecificationLine.SETFILTER("Value (Date)", "Value Filter");
                  "Value Filter" := ObjectSpecificationLine.GETFILTER("Value (Date)");
                END;
              "Specification Field Type"::Value:
                BEGIN
                  ObjectSpecificationLine.SETFILTER("Value (Value)", "Value Filter");
                  "Value Filter" := ObjectSpecificationLine.GETFILTER("Value (Value)");
                END;
              "Specification Field Type"::Option:;
            ELSE
              ObjectSpecificationLine.SETFILTER(Value, "Value Filter");
            END;
          END;
      ELSE
        TableRef.OPEN("Table No.");
        FieldRef := TableRef.FIELD("Field No.");
        IF FORMAT(FieldRef.TYPE) = 'Date' THEN
          ConvertDateFilter("Value Filter");
        FieldRef.SETFILTER("Value Filter");
        "Value Filter" := FieldRef.GETFILTER;
      END;
    END;

    PROCEDURE ClearValueFilter@1210190001();
    BEGIN
      IF ("Table Type" <> xRec."Table Type") OR
        ("Table No." <> xRec."Table No.") OR
        ("Field No." <> xRec."Field No.")
      THEN
        "Value Filter" := '';
    END;

    PROCEDURE ConvertDateFilter@1210190003(VAR OValueFilter@1210190000 : Text[250]);
    VAR
      Pos@1210190001 : Integer;
    BEGIN
      Pos := STRPOS(LOWERCASE(OValueFilter), Text000);
      IF Pos <> 0 THEN BEGIN
        OValueFilter := DELSTR(OValueFilter, Pos, STRLEN(Text000));
        OValueFilter := INSSTR(OValueFilter, FORMAT(TODAY), Pos);
      END;
    END;

    PROCEDURE TestInfiniteLoop@1100528600();
    VAR
      TmpServDiscFilterGroupLine@1100528600 : TEMPORARY Record 11071936;
      ServDiscFilterGroupLine@1100528601 : Record 11071936;
    BEGIN
      IF "Linked To Line No." = 0 THEN
        EXIT;
      MODIFY;
      ServDiscFilterGroupLine := Rec;
      REPEAT
        TmpServDiscFilterGroupLine := ServDiscFilterGroupLine;
        IF NOT TmpServDiscFilterGroupLine.INSERT THEN
          ERROR(Text001);
        IF ServDiscFilterGroupLine."Linked To Line No." <> 0 THEN
          ServDiscFilterGroupLine.GET(Code, ServDiscFilterGroupLine."Linked To Line No.");
      UNTIL ServDiscFilterGroupLine."Linked To Line No." = 0;
    END;

    PROCEDURE TestLinkedLineNo@1100528601();
    VAR
      ServDiscFilterGroupLine@1100528600 : Record 11071936;
    BEGIN
      IF "Linked To Line No." = 0 THEN
        EXIT;
      ServDiscFilterGroupLine.GET(Code, "Linked To Line No.");
      IF ServDiscFilterGroupLine."Table Type" <> "Table Type" THEN
        ERROR(Text002);
      ServDiscFilterGroupLine.SETRANGE(Code, Code);
      ServDiscFilterGroupLine.SETRANGE("Linked To Line No.", "Line No.");
      IF NOT ServDiscFilterGroupLine.ISEMPTY THEN
        ERROR(Text003);
    END;

    BEGIN
    END.
  }
}

