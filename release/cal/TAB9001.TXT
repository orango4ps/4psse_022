OBJECT Table 9001 User Group Member
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnInsert=VAR
               UserGroupAccessControl@1000 : Record 9002;
             BEGIN
               UserGroupAccessControl.AddUserGroupMember("User Group Code","User Security ID","Company Name");
               CopyDefaultProfileFromUserGroupToUser("User Group Code",FALSE);
             END;

    OnModify=BEGIN
               IF ("User Group Code" <> xRec."User Group Code") OR
                  ("User Security ID" <> xRec."User Security ID") OR
                  ("Company Name" <> xRec."Company Name")
               THEN
                 ModifyUserGroupMembership;
             END;

    OnDelete=VAR
               UserGroupAccessControl@1000 : Record 9002;
               PermissionManager@1001 : Codeunit 9002;
             BEGIN
               UserGroupAccessControl.RemoveUserGroupMember("User Group Code","User Security ID","Company Name");

               // In SaaS the default profile comes from the plan and not from the user group
               IF NOT PermissionManager.SoftwareAsAService THEN
                 UpdateDefaultProfileOfUser("User Group Code");
             END;

    OnRename=BEGIN
               ModifyUserGroupMembership;
             END;

    ReplicateData=No;
    CaptionML=[ENU=User Group Member;
               NOR=Brukergruppemedlem];
  }
  FIELDS
  {
    { 1   ;   ;User Group Code     ;Code20        ;TableRelation="User Group";
                                                   DataClassification=EndUserIdentifiableInformation;
                                                   CaptionML=[ENU=User Group Code;
                                                              NOR=Brukergruppekode;
                                                              SVE=Kod f”r anv„ndargrupp];
                                                   NotBlank=Yes }
    { 2   ;   ;User Security ID    ;GUID          ;TableRelation=User;
                                                   DataClassification=EndUserPseudonymousIdentifiers;
                                                   CaptionML=[ENU=User Security ID;
                                                              NOR=Sikkerhets-ID for bruker;
                                                              SVE=S„kerhets-ID f”r anv„ndare];
                                                   NotBlank=Yes }
    { 3   ;   ;Company Name        ;Text30        ;TableRelation=Company;
                                                   CaptionML=[ENU=Company Name;
                                                              NOR=Selskapsnavn;
                                                              SVE=F”retagsnamn] }
    { 4   ;   ;User Name           ;Code50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(User."User Name" WHERE (User Security ID=FIELD(User Security ID)));
                                                   CaptionML=[ENU=User Name;
                                                              NOR=Brukernavn;
                                                              SVE=Anv„ndarnamn];
                                                   Editable=No }
    { 5   ;   ;User Full Name      ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(User."Full Name" WHERE (User Security ID=FIELD(User Security ID)));
                                                   CaptionML=[ENU=User Full Name;
                                                              NOR=Fullt brukernavn];
                                                   Editable=No }
    { 6   ;   ;User Group Name     ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("User Group".Name WHERE (Code=FIELD(User Group Code)));
                                                   CaptionML=[ENU=User Group Name;
                                                              NOR=Brukergruppenavn;
                                                              SVE=Namn p† anv„ndargrupp];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;User Group Code,User Security ID,Company Name;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ConfirmPersonalizationChangeQst@1002 : TextConst '@@@="%1 = User Group Code";ENU=Do you want to change the user''s current personalization profile to the default profile used by user group %1?;NOR=Vil du endre brukerens gjeldende tilpasningsprofil til standardprofilen som brukes av brukergruppen %1?';
      ConfirmPersonalizationChangeDefaultQst@1001 : TextConst 'ENU=Do you want to change the user''s current personalization profile to the default value?;NOR=Vil du endre brukerens gjeldende tilpasningsprofil til standardverdien?';
      UserProfileChangeMsg@1000 : TextConst '@@@="%1 = Profile ID";ENU=The user''s personalization profile was changed to %1.;NOR=Brukerens tilpasningsprofil ble endret til %1.';
      UserProfileChangeFailedMsg@1003 : TextConst '@@@="%1 = User Group Code";ENU=User group %1 has no default profile defined.;NOR=Brukergruppe %1 har ingen definert standardprofil.';

    [External]
    PROCEDURE AddUsers@1(SelectedCompany@1002 : Text[30]);
    VAR
      User@1001 : Record 2000000120;
      UserLookup@1003 : Page 9843;
    BEGIN
      IF GETFILTER("User Group Code") = '' THEN
        EXIT;

      UserLookup.LOOKUPMODE := TRUE;
      IF UserLookup.RUNMODAL = ACTION::LookupOK THEN BEGIN
        UserLookup.GetSelectionFilter(User);
        IF User.FINDSET THEN
          REPEAT
            "User Group Code" := GETRANGEMIN("User Group Code");
            "User Security ID" := User."User Security ID";
            "Company Name" := SelectedCompany;
            IF INSERT(TRUE) THEN;
          UNTIL User.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE ModifyUserGroupMembership@5();
    VAR
      UserGroupAccessControl@1001 : Record 9002;
      UserGroupMember@1003 : Record 9001;
      DefaultProfile@1004 : Record 2000000178;
      ConfPersonalizationMgt@1002 : Codeunit 9170;
      NewProfileID@1000 : Code[30];
    BEGIN
      IF ISNULLGUID("User Security ID") OR ("User Group Code" = '') THEN
        EXIT;
      UserGroupAccessControl.RemoveUserGroupMember(xRec."User Group Code",xRec."User Security ID",xRec."Company Name");
      UserGroupAccessControl.AddUserGroupMember("User Group Code","User Security ID","Company Name");
      IF (xRec."User Group Code" = "User Group Code") AND (xRec."User Security ID" = "User Security ID") THEN
        EXIT;
      // If there is more than one User group assigned to a user, then use the company default profile
      UserGroupMember.SETRANGE("User Security ID","User Security ID");
      UserGroupMember.SETFILTER("User Group Code",'<>%1',xRec."User Group Code");
      IF NOT UserGroupMember.ISEMPTY THEN BEGIN
        // When there are more than two user groups assigned to this user, assign to him the default profile
        IF NOT UserGroupIsAssociatedWithProfile(xRec."User Group Code",xRec."User Security ID") THEN
          EXIT;
        IF UserHasOtherUserGroupsSupportingProfile(UserGroupMember,xRec."User Security ID") THEN
          EXIT;
        IF NOT CONFIRM(ConfirmPersonalizationChangeDefaultQst) THEN
          EXIT;
        ConfPersonalizationMgt.GetDefaultProfile(DefaultProfile);
        CopyDefaultProfileToUser(DefaultProfile,TRUE);
        MESSAGE(UserProfileChangeMsg,DefaultProfile."Profile ID");
        EXIT;
      END;

      // Else ask confirmation whether to change personalization
      IF CONFIRM(STRSUBSTNO(ConfirmPersonalizationChangeQst,"User Group Code")) THEN BEGIN
        UpdateDefaultProfileOfUser(xRec."User Group Code");
        NewProfileID := CopyDefaultProfileFromUserGroupToUser("User Group Code",TRUE);
        IF NewProfileID <> '' THEN
          MESSAGE(UserProfileChangeMsg,NewProfileID)
        ELSE
          MESSAGE(UserProfileChangeFailedMsg,"User Group Code");
      END;
    END;

    LOCAL PROCEDURE UpdateDefaultProfileOfUser@12(UserGroupCode@1003 : Code[20]);
    VAR
      UserGroupMember@1002 : Record 9001;
      UserPersonalization@1001 : Record 2000000073;
    BEGIN
      UserGroupMember.SETRANGE("User Security ID","User Security ID");
      UserGroupMember.SETFILTER("User Group Code",'<>%1',UserGroupCode);
      IF NOT UserGroupMember.FINDFIRST THEN BEGIN
        IF NOT UserPersonalization.GET("User Security ID") THEN
          EXIT;
        UserPersonalization."Profile ID" := '';
        UserPersonalization.MODIFY(TRUE);
      END ELSE
        CopyDefaultProfileFromUserGroupToUser(UserGroupMember."User Group Code",TRUE);
    END;

    LOCAL PROCEDURE CopyDefaultProfileFromUserGroupToUser@16(UserGroupCode@1003 : Code[20];Force@1000 : Boolean) : Code[30];
    VAR
      UserGroup@1001 : Record 9000;
      DefaultProfile@1002 : Record 2000000178;
    BEGIN
      // Updates the user's personalization, if empty, with the current default profile
      IF UserGroup.GET(UserGroupCode) THEN
        IF DefaultProfile.GET(UserGroup."Default Profile Scope",UserGroup."Default Profile App ID",UserGroup."Default Profile ID") THEN
          EXIT(CopyDefaultProfileToUser(DefaultProfile,Force));
    END;

    LOCAL PROCEDURE CopyDefaultProfileToUser@9(DefaultProfile@1003 : Record 2000000178;Force@1000 : Boolean) : Code[30];
    VAR
      UserPersonalization@1002 : Record 2000000073;
    BEGIN
      IF DefaultProfile."Profile ID" = '' THEN
        EXIT('');
      // Force = TRUE overwrites the current default profile
      IF NOT UserPersonalization.GET("User Security ID") THEN BEGIN
        UserPersonalization.INIT;
        UserPersonalization."User SID" := "User Security ID";
        UserPersonalization."Profile ID" := DefaultProfile."Profile ID";
        UserPersonalization."App ID" := DefaultProfile."App ID";
        UserPersonalization.Scope := DefaultProfile.Scope;
        UserPersonalization.INSERT(TRUE);
        EXIT(DefaultProfile."Profile ID");
      END;
      IF (UserPersonalization."Profile ID" = '') OR Force THEN BEGIN
        UserPersonalization."Profile ID" := DefaultProfile."Profile ID";
        UserPersonalization."App ID" := DefaultProfile."App ID";
        UserPersonalization.Scope := DefaultProfile.Scope;
        UserPersonalization.MODIFY(TRUE);
        EXIT(DefaultProfile."Profile ID");
      END;
      EXIT('');
    END;

    LOCAL PROCEDURE UserGroupIsAssociatedWithProfile@8(UserGroupCode@1000 : Code[20];UserSecurityID@1001 : GUID) : Boolean;
    VAR
      UserPersonalization@1002 : Record 2000000073;
      UserGroup@1003 : Record 9000;
    BEGIN
      IF UserPersonalization.GET(UserSecurityID) THEN BEGIN
        IF UserGroup.GET(UserGroupCode) THEN
          EXIT(UserPersonalization."Profile ID" = UserGroup."Default Profile ID");
      END;
      EXIT(FALSE);
    END;

    LOCAL PROCEDURE UserHasOtherUserGroupsSupportingProfile@11(VAR UserGroupMember@1000 : Record 9001;UserSecurityID@1001 : GUID) : Boolean;
    VAR
      UserPersonalization@1002 : Record 2000000073;
      UserGroup@1003 : Record 9000;
    BEGIN
      IF UserPersonalization.GET(UserSecurityID) THEN BEGIN
        UserGroupMember.FINDSET;
        REPEAT
          IF UserGroup.GET(UserGroupMember."User Group Code") AND
             (UserGroup."Default Profile ID" = UserPersonalization."Profile ID")
          THEN
            EXIT(TRUE);
        UNTIL UserGroupMember.NEXT = 0
      END;
      EXIT(FALSE);
    END;

    BEGIN
    END.
  }
}

