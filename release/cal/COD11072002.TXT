OBJECT Codeunit 11072002 Job Jnl.-Check Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03,4PS14.00 (1011);
  }
  PROPERTIES
  {
    TableNo=11072008;
    OnRun=BEGIN
            RunCheck(Rec);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=kann kein Ultimodatum sein.;ENU=cannot be a closing date.;NLD=mag geen ultimodatum zijn.;NOR=kan ikke vëre en sluttdato;SVE=kan inte vara ett avslutsdatum.';
      Text001@1001 : TextConst 'DEU=liegt nicht innerhalb des zugelassenen Buchungszeitraums.;ENU=is not within your range of allowed posting dates.;NLD=is geen geldige boekingsdatum. De datum valt buiten het toegestane bereik.;NOR=er ikke i ditt tillatte intervall for bokfõringsdato;SVE=Ñr inte i ditt tillÜtna intervall fîr bokfîringsdatum.';
      CombinationBlockedErr@1002 : TextConst '@@@="%1 = table name, %2 = template name, %3 = batch name, %4 = line no., %5 - error text";DEU=Die Kombination von Dimensionen in %1 %2, %3, %4 ist gesperrt. %5.;ENU=The combination of dimensions used in %1 %2, %3, %4 is blocked. %5.;NLD=De combinatie van gebruikte dimensies in %1 %2, %3, %4 is geblokkeerd. %5.;NOR=Kombinasjonen av dimensjonene som brukes i %1 %2, %3, %4 er sperret. %5;SVE=Kombinationen av dimensioner i %1 %2, %3, %4 Ñr spÑrrad. %5';
      DimensionCausedErr@1003 : TextConst '@@@="%1 = table name, %2 = template name, %3 = batch name, %4 = line no., %5 - error text";DEU=Eine im %1 %2, %3, %4 verwendete Dimension hat einen Fehler verursacht. %5.;ENU=A dimension used in %1 %2, %3, %4 has caused an error. %5.;NLD=Een dimensie in %1 %2, %3, %4 heeft een fout veroorzaakt. %5.;NOR=En dimensjon som brukes i %1 %2, %3, %4 har forÜrsaket en feil. %5;SVE=En dimension anvÑnd i %1 %2, %3, %4 har orsakat ett fel. %5';
      JobSetup@1100525000 : Record 315;
      Location@1006 : Record 14;
      DimMgt@1007 : Codeunit 408;
      TimeSheetMgt@1011 : Codeunit 950;
      Text004@1010 : TextConst '@@@="%1=Item;%2=JobJnlline.""No."";%3=Job;%4=JobJnlline.""Job No."";%5=JobJnlline.""Journal Batch Name"";%6=""Line No"";%7=JobJnlline.""Line No.""";DEU="Sie mÅssen mehr Verbrauch von %1 %2 in %3 %4 buchen, bevor Sie das Projekt Buch.-Blatt %5 %6 = %7 buchen kînnen.";ENU="You must post more usage of %1 %2 in %3 %4 before you can post job journal %5 %6 = %7.";NLD="U moet meer verbruik van %1 %2 in %3 %4 boeken voordat u projectpost %5 %6 = %7 kunt boeken.";NOR="Du mÜ bokfõre mer forbruk av %1 %2 i %3 %4 fõr du kan bokfõre prosjektkladd %5 %6 = %7.";SVE="Du mÜste bokfîra mer fîrbrukning av %1 %2 i %3 %4 innan du kan bokfîra projektjournalen %5 %6 = %7."';

    [External]
    PROCEDURE RunCheck@1(VAR JobJnlLine@1000 : Record 11072008);
    VAR
      Job@1004 : Record 11072003;
      UserSetupManagement@1001 : Codeunit 5700;
    BEGIN
      OnBeforeRunCheck(JobJnlLine);

      WITH JobJnlLine DO BEGIN
        IF EmptyLine THEN
          EXIT;

        TestJobJnlLine(JobJnlLine);

        Job.GET("Job No.");
        //Job.TESTFIELD(Status,Job.Status::Open); //**4PS.o (db, 26-10-10: M23295)

        IF NORMALDATE("Posting Date") <> "Posting Date" THEN
          FIELDERROR("Posting Date",Text000);

        IF ("Document Date" <> 0D) THEN
          IF ("Document Date" <> NORMALDATE("Document Date")) THEN
            FIELDERROR("Document Date",Text000);

        IF NOT UserSetupManagement.IsPostingDateValid("Posting Date") THEN
          FIELDERROR("Posting Date",Text001);

        IF "Time Sheet No." <> '' THEN
          TimeSheetMgt.CheckJobJnlLine(JobJnlLine);

        //**4PS.sn
        JobSetup.GET;
        IF (JobSetup."Cost Component Mandatory") AND
           ("Job No." <> '') AND
           ("Shortcut Dimension 2 Code" <> '')
        THEN
          TESTFIELD("Cost Component");
        //**4PS.en

        CheckDim(JobJnlLine);

        IF Type = Type::Item THEN BEGIN
          IF ("Quantity (Base)" < 0) AND ("Entry Type" = "Entry Type"::Usage) THEN
            CheckItemQuantityJobJnl(JobJnlLine);
          GetLocation("Location Code");
          IF Location."Directed Put-away and Pick" THEN
            TESTFIELD("Bin Code",'')
          ELSE
            IF Location."Bin Mandatory" THEN
              TESTFIELD("Bin Code");
        END;
        IF "Line Type" IN ["Line Type"::Billable,"Line Type"::"Both Budget and Billable"] THEN
          TESTFIELD(Chargeable,TRUE);

        //**4PS.sn
        IF "Total Cost (LCY)" <> 0 THEN
          TESTFIELD("Entry Type", "Entry Type"::Usage);
        IF "Total Price (LCY)" <> 0 THEN
          TESTFIELD("Entry Type", "Entry Type"::Sale);
        //**4PS.en
      END;

      OnAfterRunCheck(JobJnlLine);
    END;

    LOCAL PROCEDURE GetLocation@7300(LocationCode@1000 : Code[10]);
    BEGIN
      IF LocationCode = '' THEN
        CLEAR(Location)
      ELSE
        IF Location.Code <> LocationCode THEN
          Location.GET(LocationCode);
    END;

    LOCAL PROCEDURE CheckDim@4(JobJnlLine@1000 : Record 11072008);
    VAR
      TableID@1003 : ARRAY [10] OF Integer;
      No@1002 : ARRAY [10] OF Code[20];
      IsHandled@1001 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeCheckDim(JobJnlLine,IsHandled);
      IF IsHandled THEN
        EXIT;

      WITH JobJnlLine DO BEGIN
        IF NOT DimMgt.CheckDimIDComb("Dimension Set ID") THEN
          ERROR(
            CombinationBlockedErr,
            TABLECAPTION,"Journal Template Name","Journal Batch Name","Line No.",
            DimMgt.GetDimCombErr);

        TableID[1] := DATABASE::Job;
        No[1] := "Job No.";
        TableID[2] := DimMgt.TypeToTableID2(Type);
        No[2] := "No.";
        TableID[3] := DATABASE::"Resource Group";
        No[3] := "Resource Group No.";
        IF NOT DimMgt.CheckDimValuePosting(TableID,No,"Dimension Set ID") THEN BEGIN
          IF "Line No." <> 0 THEN
            ERROR(
              DimensionCausedErr,
              TABLECAPTION,"Journal Template Name","Journal Batch Name","Line No.",
              DimMgt.GetDimValuePostingErr);
          ERROR(DimMgt.GetDimValuePostingErr);
        END;
      END;
    END;

    LOCAL PROCEDURE CheckItemQuantityJobJnl@15(VAR JobJnlline@1001 : Record 11072008);
    VAR
      Item@1003 : Record 27;
      Job@1007 : Record 11072003;
      IsHandled@1000 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeCheckItemQuantityJobJnl(JobJnlline,IsHandled);
      IF IsHandled THEN
        EXIT;

      IF JobJnlline.IsNonInventoriableItem THEN
        EXIT;

      Job.GET(JobJnlline."Job No.");
      IF (Job.GetQuantityAvailable(JobJnlline."No.",JobJnlline."Location Code",JobJnlline."Variant Code",0,2) +
          JobJnlline."Quantity (Base)") < 0
      THEN
        ERROR(
          Text004,Item.TABLECAPTION,JobJnlline."No.",Job.TABLECAPTION,
          JobJnlline."Job No.",JobJnlline."Journal Batch Name",
          JobJnlline.FIELDCAPTION("Line No."),JobJnlline."Line No.");
    END;

    LOCAL PROCEDURE TestJobJnlLine@6(JobJournalLine@1000 : Record 11072008);
    VAR
      IsHandled@1001 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeTestJobJnlLine(JobJournalLine,IsHandled);
      IF IsHandled THEN
        EXIT;

      WITH JobJournalLine DO BEGIN
        TESTFIELD("Job No.");
      //TESTFIELD("Job Task No.");  //**4PS.o  Not used by 4PS
        TESTFIELD("No.");
        TESTFIELD("Posting Date");
      //TESTFIELD(Quantity); //**4PS.o
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterRunCheck@3(VAR JobJnlLine@1000 : Record 11072008);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeRunCheck@16(VAR JobJnlLine@1000 : Record 11072008);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCheckDim@2(VAR JobJnlLine@1000 : Record 11072008;VAR IsHandled@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCheckItemQuantityJobJnl@5(VAR JobJnlLine@1000 : Record 11072008;VAR IsHandled@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeTestJobJnlLine@7(JobJournalLine@1000 : Record 11072008;VAR IsHandled@1001 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

