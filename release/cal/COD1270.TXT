OBJECT Codeunit 1270 Exp. Launcher Gen. Jnl.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    TableNo=81;
    Permissions=TableData 1220=rimd;
    OnRun=VAR
            BankAccount@1005 : Record 270;
            CreditTransferRegister@1003 : Record 1205;
            GenJnlLine@1000 : Record 81;
            GenJnlLine2@1008 : Record 81;
            DataExch@1004 : Record 1220;
            DataExchDef@1006 : Record 1222;
            DataExchMapping@1007 : Record 1224;
            PaymentExportMgt@1002 : Codeunit 1210;
          BEGIN
            GenJnlLine.COPYFILTERS(Rec);
            GenJnlLine.FINDFIRST;

            BankAccount.GET(GenJnlLine."Bal. Account No.");
            BankAccount.GetDataExchDefPaymentExport(DataExchDef);

            CreditTransferRegister.CreateNew(DataExchDef.Code,GenJnlLine."Bal. Account No.");
            COMMIT;

            IF DataExchDef."Data Handling Codeunit" > 0 THEN
              CODEUNIT.RUN(DataExchDef."Data Handling Codeunit",GenJnlLine);

            IF DataExchDef."Validation Codeunit" > 0 THEN
              CODEUNIT.RUN(DataExchDef."Validation Codeunit",GenJnlLine);

            PaymentExportMgt.CreateDataExch(DataExch,GenJnlLine."Bal. Account No.");
            GenJnlLine2.COPYFILTERS(GenJnlLine);
            GenJnlLine2.MODIFYALL("Data Exch. Entry No.",DataExch."Entry No.",TRUE);

            DataExchMapping.SETRANGE("Data Exch. Def Code",DataExchDef.Code);
            DataExchMapping.SETRANGE("Table ID",DATABASE::"Payment Export Data");
            DataExchMapping.FINDFIRST;

            DataExch.ExportFromDataExch(DataExchMapping);
          END;

  }
  CODE
  {

    BEGIN
    END.
  }
}

