OBJECT Report 11012111 Update Project Addresses
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Aktualisieren Projektadressen;
               ENU=Update Project Addresses;
               NLD=Werkadressen bijwerken;
               NOR=Oppdater prosjektadresser;
               SVE=Uppdatera projektadresser];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF (NOT UpdateCustomer) AND (NOT UpdateEstateOwner) THEN
                    ERROR(Text000);

                  IF ("Ship-to Address".GETFILTER("Customer No.") = '') THEN
                    ERROR(Text007);

                  CustRec.RESET;
                  "Ship-to Address".COPYFILTER("Customer No.", CustRec."No.");
                  IF (CustRec.COUNT <> 1) THEN
                    ERROR(Text008)
                  ELSE BEGIN
                    CustRec.FINDFIRST;
                    CustomerOld := CustRec."No.";
                  END;

                  IF (CustomerOld = '') THEN
                    ERROR(Text001, ShipToAddressRec.FIELDCAPTION("Customer No."));

                  IF UpdateCustomer THEN BEGIN
                    IF (CustomerNew = '') THEN
                      ERROR(Text001, Text002);
                    IF (CustomerNew = CustomerOld) THEN
                      ERROR(Text006);
                    CustRec.GET(CustomerNew);
                  END;

                  IF UpdateEstateOwner THEN BEGIN
                    IF (EstateOwnerNew = EstateOwnerOld) THEN
                      ERROR(Text003);
                    IF (EstateOwnerNew <> '') THEN
                      ContRec.GET(EstateOwnerNew)
                    ELSE BEGIN
                      IF NOT CONFIRM(Text004) THEN
                        ERROR('');
                    END;
                  END;
                END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 8635;    ;DataItem;                    ;
               DataItemTable=Table222;
               DataItemTableView=SORTING(Customer No.,Code);
               OnAfterGetRecord=BEGIN
                                  IF UpdateCustomer THEN BEGIN
                                    IF NOT ShipToAddressRec.GET(CustomerNew, Code) THEN
                                      NewAddressCode := Code
                                    ELSE BEGIN
                                      ShipToAddressRec.SETRANGE("Customer No.", CustomerNew);
                                      ShipToAddressRec.FIND('+');
                                      NewAddressCode := INCSTR(ShipToAddressRec.Code);
                                      IF (NewAddressCode = '') OR (STRLEN(NewAddressCode) > 10) THEN
                                        ERROR(Text005);
                                    END;
                                    ShipToAddressRec.INIT;
                                    ShipToAddressRec."Customer No." := CustomerNew;
                                    ShipToAddressRec.Code := NewAddressCode;
                                    ShipToAddressRec.INSERT(TRUE);
                                    //C016337.so
                                    //ShipToAddressRec := "Ship-to Address";
                                    //ShipToAddressRec."Customer No." := CustomerNew;
                                    //ShipToAddressRec.Code := NewAddressCode;
                                    //C016337.eo
                                    ShipToAddressRec.TRANSFERFIELDS("Ship-to Address", FALSE);  //C016337.n
                                    IF UpdateEstateOwner AND (ShipToAddressRec."Estate Owner" = EstateOwnerOld) THEN
                                      ShipToAddressRec."Estate Owner" := EstateOwnerNew;
                                    ShipToAddressRec.MODIFY(TRUE);
                                    DELETE(TRUE);
                                  END;

                                  IF (NOT UpdateCustomer) AND UpdateEstateOwner AND ("Estate Owner" = EstateOwnerOld) THEN BEGIN
                                    "Estate Owner" := EstateOwnerNew;
                                    MODIFY(TRUE);
                                  END
                                END;

               ReqFilterFields=Customer No.,Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1210190008;2;Field  ;
                  CaptionML=[DEU=Aktualisieren Debitorennr.;
                             ENU=Update Customer No.;
                             NLD=Bijwerken klantnr.;
                             NOR=Oppdater kundenr;
                             SVE=Uppdatera kundnr];
                  SourceExpr=UpdateCustomer;
                  OnValidate=BEGIN
                               IF NOT UpdateCustomer THEN
                                 CustomerNew := '';
                             END;
                              }

      { 1210190002;2;Field  ;
                  CaptionML=[DEU=Neue Debitorennr.;
                             ENU=New Customer No.;
                             NLD=Nieuwe klantnr.;
                             NOR=Nytt kundenr.;
                             SVE=Nytt kundnr];
                  SourceExpr=CustomerNew;
                  TableRelation=Customer;
                  OnValidate=BEGIN
                               IF NOT UpdateCustomer THEN
                                 CustomerNew := '';
                             END;
                              }

      { 1210190010;2;Field  ;
                  CaptionML=[DEU=Aktualsieren Komplexinhaber;
                             ENU=Update Complex Owner;
                             NLD=Bijwerken complexeigenaar;
                             NOR=Oppdater komplekseier;
                             SVE=Uppdatera komplexÑgare];
                  SourceExpr=UpdateEstateOwner;
                  OnValidate=BEGIN
                               IF NOT UpdateEstateOwner THEN BEGIN
                                 EstateOwnerOld := '';
                                 EstateOwnerNew := '';
                               END;
                             END;
                              }

      { 1210190005;2;Field  ;
                  CaptionML=[DEU=FrÅherer Komplexinhaber;
                             ENU=Old Complex Owner;
                             NLD=Oude complexeigenaar;
                             NOR=Gammel komplekseier;
                             SVE=Gammal komplexÑgare];
                  SourceExpr=EstateOwnerOld;
                  TableRelation=Contact;
                  OnValidate=BEGIN
                               IF NOT UpdateEstateOwner THEN
                                 EstateOwnerOld := '';
                             END;
                              }

      { 1210190004;2;Field  ;
                  CaptionML=[DEU=Neuer Komplexinhaber;
                             ENU=New Complex Owner;
                             NLD=Nieuwe complexeigenaar;
                             NOR=Ny komplekseier;
                             SVE=Ny komplexÑgare];
                  SourceExpr=EstateOwnerNew;
                  TableRelation=Contact;
                  OnValidate=BEGIN
                               IF NOT UpdateEstateOwner THEN
                                 EstateOwnerNew := '';
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      CustRec@1210190008 : Record 18;
      ContRec@1210190009 : Record 5050;
      ShipToAddressRec@1210190006 : Record 222;
      UpdateCustomer@1210190004 : Boolean;
      UpdateEstateOwner@1210190005 : Boolean;
      CustomerOld@1210190000 : Code[20];
      CustomerNew@1210190001 : Code[20];
      EstateOwnerOld@1210190003 : Code[20];
      EstateOwnerNew@1210190002 : Code[20];
      NewAddressCode@1210190007 : Code[11];
      Text000@1210190010 : TextConst 'DEU=Nichts ausgewÑhlt.;ENU=Nothing selected.;NLD=Niets geselecteerd.;NOR=Ingenting er valgt.;SVE=Inget har valts.';
      Text001@1210190011 : TextConst 'DEU=''%1'' muss ausgefÅllt werden.;ENU=%1 must be filled in.;NLD=%1 moet worden ingevuld.;NOR=%1 mÜ fylles ut.;SVE=%1 mÜste vara ifylld.';
      Text002@1210190012 : TextConst 'DEU=Neue Debitorennr.;ENU=New Customer No.;NLD=Nieuwe klantnr.;NOR=Nytt kundenr.;SVE=Nytt kundnr';
      Text003@1210190013 : TextConst 'DEU=FrÅherer und neuer Komplexinhaber dÅrfen nicht gleich sein.;ENU=Old and new Complex Owner may not be the same.;NLD=Oude en nieuwe complexeigenaar mogen niet gelijk zijn.;NOR=Gammel og ny komplekseier kan ikke vëre lik.;SVE=Gammal och ny komplexÑgare fÜr inte vara samma.';
      Text004@1210190014 : TextConst 'DEU=Komplexinhaber wird leergemacht. Sind Sie sich sicher?;ENU="Estate Owner will be made empty. Are you sure ? ";NLD=Complexeigenaar zal leeg gemaakt worden. Weet u het zeker?;NOR=Eiendomseier vil bli stÜende tom. Er du sikker?;SVE="FastighetsÑgare kommer att vara tomt. ér du sÑker? "';
      Text005@1210190015 : TextConst 'DEU=Bei der neuen Debitorennr. kann der erste freie Adressencode nicht bestimmt werden.;ENU=The first free address code of the new customer can not be determined.;NLD=Bij het nieuwe klantnr. kan de eerst vrije adrescode niet bepaald worden.;NOR=Den fõrste ledige adressekoden for den nye kunden kan ikke fastsettes.;SVE=Den fîrsta lediga adresskoden fîr den nya kunden kan inte faststÑllas.';
      Text006@1210190016 : TextConst 'DEU=Alte und neue Debitorennr. dÅrfen nicht gleich sein.;ENU=Old and new Customer may not be the same.;NLD=Oude en nieuwe klantnr. mogen niet gelijk zijn.;NOR=Gamle og nye kunder kan ikke vëre like.;SVE=Gamla och nya kunder fÜr inte vara samma.';
      Text007@1210190017 : TextConst 'DEU=Debitorennr. muss eingegeben werden. WÑhlen Sie nur einen Debitor aus.;ENU=Customer No. must be filled in, select one customer.;NLD=Klantnr. moet worden ingegeven, selecteer ÇÇn klant.;NOR=Kundenr mÜ fylles ut. Velg en kunde.;SVE=Kundnr mÜste fyllas i. VÑlj en kund.';
      Text008@1210190018 : TextConst 'DEU=Keine oder mehrere Debitorennummern ausgewÑhlt. Es muss eine Debitorennummer asugewÑhlt werden.;ENU=None or more than one customers selected, there must be one customer.;NLD=Geen of meerdere klannrs. geselecteerd, er moet ÇÇn klantnr. geselecteerd worden.;NOR="Ingen eller flere enn en kunde er valgt. Velg en kunde ";SVE=Ingen eller fler Ñn en kund har valts. VÑlj en kund.';

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

