OBJECT Report 11012947 Copy Rental Unit
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Vermietungseinheit kopieren;
               ENU=Copy Rental Unit;
               NLD=Verhuureenheid kopi‰ren];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   Created := FALSE;
                 END;

    OnPreReport=BEGIN
                  CheckSelections;
                  CopyRentalUnit;
                  IF GUIALLOWED AND Created THEN
                    MESSAGE(Text006);
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   IF NoOfRentalUnitsToCreate < 1 THEN
                     NoOfRentalUnitsToCreate := 1;
                   ValidateNoOfRentalUnitsToCreate(FALSE);
                 END;

      OnQueryClosePage=BEGIN
                         IF CloseAction <> ACTION::Cancel THEN
                           CheckSelections;
                       END;

    }
    CONTROLS
    {
      { 1100529001;0;Container;
                  ContainerType=ContentArea }

      { 1100529000;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1100529002;2;Field  ;
                  SourceExpr=CopyFromProjectNo;
                  CaptionClass=CaptionCopyFromProjectNo;
                  OnValidate=BEGIN
                               IF CopyFromProjectNo <> '' THEN BEGIN
                                 ValidateCopyFromProjectNo;
                                 IF CopyToProjectNo = '' THEN BEGIN
                                   CopyToProjectNo := CopyFromProjectNo;
                                   ValidateNoOfRentalUnitsToCreate(FALSE);
                                 END;
                               END;
                             END;

                  OnLookup=VAR
                             Project@1100529000 : Record 11072003;
                           BEGIN
                             Project."No." := CopyFromProjectNo;
                             IF PAGE.RUNMODAL(0, Project) = ACTION::LookupOK THEN BEGIN
                               Text := Project."No.";
                               EXIT(TRUE);
                             END;
                           END;
                            }

      { 1100529003;2;Field  ;
                  SourceExpr=CopyFromRentalUnit;
                  CaptionClass=CaptionCopyFromRentalUnit;
                  OnValidate=BEGIN
                               IF CopyFromRentalUnit <> '' THEN
                                 ValidateCopyFromRentalUnit;
                             END;

                  OnLookup=VAR
                             RentalUnit@1100529000 : Record 11012940;
                           BEGIN
                             IF CopyFromProjectNo = '' THEN
                               EXIT;
                             RentalUnit.FILTERGROUP(2);
                             RentalUnit.SETRANGE("Project No.", CopyFromProjectNo);
                             RentalUnit.FILTERGROUP(0);
                             RentalUnit."Project No." := CopyFromProjectNo;
                             RentalUnit."Rental Unit" := CopyFromRentalUnit;
                             IF PAGE.RUNMODAL(0, RentalUnit) = ACTION::LookupOK THEN BEGIN
                               Text := RentalUnit."Rental Unit";
                               EXIT(TRUE);
                             END;
                           END;
                            }

      { 1100529004;2;Field  ;
                  SourceExpr=CopyToProjectNo;
                  CaptionClass=CaptionCopyToProjectNo;
                  OnValidate=BEGIN
                               IF CopyToProjectNo <> '' THEN
                                 ValidateCopyToProjectNo(TRUE);
                               ValidateNoOfRentalUnitsToCreate(FALSE);
                             END;

                  OnLookup=VAR
                             Project@1100529000 : Record 11072003;
                           BEGIN
                             Project."No." := CopyToProjectNo;
                             IF PAGE.RUNMODAL(0, Project) = ACTION::LookupOK THEN BEGIN
                               Text := Project."No.";
                               EXIT(TRUE);
                             END;
                           END;
                            }

      { 1100529005;2;Field  ;
                  SourceExpr=CopyToRentalUnitFirst;
                  CaptionClass=CaptionCopyToRentalUnitFirst;
                  OnValidate=BEGIN
                               IF CopyToRentalUnitFirst <> '' THEN
                                 ValidateCopyToRentalUnitFirst(TRUE);
                               ValidateNoOfRentalUnitsToCreate(FALSE);
                             END;

                  OnLookup=VAR
                             RentalUnit@1100529001 : Record 11012940;
                           BEGIN
                             IF CopyToProjectNo = '' THEN
                               EXIT;
                             RentalUnit.FILTERGROUP(2);
                             RentalUnit.SETRANGE("Project No.", CopyToProjectNo);
                             RentalUnit.FILTERGROUP(0);
                             RentalUnit."Project No." := CopyToProjectNo;
                             RentalUnit."Rental Unit" := CopyToRentalUnitFirst;
                             IF PAGE.RUNMODAL(0, RentalUnit) = ACTION::LookupOK THEN BEGIN
                               Text := RentalUnit."Rental Unit";
                               RentalUnit."Rental Unit" := INCSTR(RentalUnit."Rental Unit");
                               IF (RentalUnit."Rental Unit" <> '') AND (NOT RentalUnit.GET(CopyToProjectNo, RentalUnit."Rental Unit")) THEN
                                 Text := RentalUnit."Rental Unit";
                               EXIT(TRUE);
                             END;
                           END;
                            }

      { 1100529006;2;Field  ;
                  SourceExpr=NoOfRentalUnitsToCreate;
                  CaptionClass=CaptionNoOfRentalUnitsToCreate;
                  MinValue=1;
                  MaxValue=999;
                  OnValidate=BEGIN
                               ValidateNoOfRentalUnitsToCreate(FALSE);
                               RequestOptionsPage.UPDATE(FALSE);
                             END;
                              }

      { 1100529007;2;Field  ;
                  CaptionML=[DEU=Zu Vermieteinheit kopieren (Letzte);
                             ENU=Copy to Rental Unit (Last);
                             NLD=Kopi‰ren naar verhuureenheid (laatste)];
                  SourceExpr=CopyToRentalUnitLast;
                  Editable=FALSE }

      { 1100529008;2;Field  ;
                  SourceExpr=NewSeriesInfo;
                  Editable=FALSE;
                  Style=Attention;
                  StyleExpr=TRUE }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      CopyFromProjectNo@1100529000 : Code[20];
      CopyFromRentalUnit@1100529001 : Code[20];
      CopyToProjectNo@1100529002 : Code[20];
      CopyToRentalUnitFirst@1100529003 : Code[20];
      CopyToRentalUnitLast@1100529014 : Code[20];
      Created@1100529018 : Boolean;
      NoOfRentalUnitsToCreate@1100529004 : Integer;
      CaptionCopyFromProjectNo@1100529005 : TextConst 'DEU=Kopieren von Projektnr.;ENU=Copy from Project No.;NLD=Kopi‰ren van projectnr.';
      CaptionCopyFromRentalUnit@1100529006 : TextConst 'DEU=Kopieren von Vermieteinheit;ENU=Copy from Rental Unit;NLD=Kopi‰ren van verhuureenheid';
      CaptionCopyToProjectNo@1100529007 : TextConst 'DEU=Kopieren zu Projektnr.;ENU=Copy to Project No.;NLD=Kopi‰ren naar projectnr.';
      CaptionCopyToRentalUnitFirst@1100529008 : TextConst 'DEU=Zu Vermieteinheit kopieren (Erste);ENU=Copy to Rental Unit (First);NLD=Kopi‰ren naar verhuureenheid (eerste)';
      CaptionNoOfRentalUnitsToCreate@1100529013 : TextConst 'DEU=Anzahl der zu erstellenden Vermieteinheiten;ENU=Number of Rental Units to be created;NLD=Aantal aan te maken verhuureenheden';
      Text001@1100529009 : TextConst 'DEU=''%1'' darf nicht leer sein;ENU=''%1'' may not be empty;NLD=''%1'' mag niet leeg zijn';
      Text002@1100529010 : TextConst 'DEU=(''%1'' %2) existiert nicht.;ENU=''%1'' (%2'''') does not exist;NLD=''%1'' (''%2'') bestaat niet';
      Text003@1100529011 : TextConst 'DEU=(''%1'' %2) ist bereits vorhanden.;ENU=''%1'' (%2'''') already exist;NLD=''%1'' (''%2'') bestaat al';
      Text004@1100529012 : TextConst 'DEU=''%1'' muss einen numerischen Teil enthalten;ENU=''%1'' must contain a numeric part;NLD=''%1'' moet een numeriek deel bevatten';
      Text005@1100529015 : TextConst 'DEU=In der neuen Serie ³%1...%2Ê sind bereits %3 von %4 vorhanden;ENU=In new series ''%1..%2'' already %3 of %4 present;NLD=In nieuwe reeks ''%1..%2'' al %3 van %4 aanwezig';
      NewSeriesInfo@1100529016 : Text;
      Text006@1100529017 : TextConst 'DEU=Erstellt;ENU=Created;NLD=Aangemaakt;NOR=Opprettet;SVE=Skapad';

    PROCEDURE SetSelection@1100529000(ProjectNo@1100529000 : Code[20];RentalUnit@1100529001 : Code[20]);
    BEGIN
      CopyFromProjectNo := ProjectNo;
      CopyFromRentalUnit := RentalUnit;
      CopyToProjectNo := ProjectNo;
    END;

    LOCAL PROCEDURE ValidateCopyFromProjectNo@1100529005();
    VAR
      Project@1100529000 : Record 11072003;
    BEGIN
      IF NOT Project.GET(CopyFromProjectNo) THEN
        ERROR(Text002, CaptionCopyFromProjectNo, CopyFromProjectNo);
    END;

    LOCAL PROCEDURE ValidateCopyFromRentalUnit@1100529004();
    VAR
      RentalUnit@1100529000 : Record 11012940;
    BEGIN
      IF NOT RentalUnit.GET(CopyFromProjectNo, CopyFromRentalUnit) THEN
        ERROR(Text002, CaptionCopyFromRentalUnit, CopyFromProjectNo + '-' + CopyFromRentalUnit);
    END;

    LOCAL PROCEDURE ValidateCopyToProjectNo@1100529006(ErrorMode@1100529001 : Boolean) : Boolean;
    VAR
      Project@1100529000 : Record 11072003;
    BEGIN
      IF NOT Project.GET(CopyToProjectNo) THEN BEGIN
        IF NOT ErrorMode THEN
          EXIT(FALSE);
        ERROR(Text002, CaptionCopyToProjectNo, CopyToProjectNo);
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ValidateCopyToRentalUnitFirst@1100529003(ErrorMode@1100529001 : Boolean) : Boolean;
    VAR
      RentalUnit@1100529000 : Record 11012940;
    BEGIN
      IF RentalUnit.GET(CopyToProjectNo, CopyToRentalUnitFirst) THEN BEGIN
        IF NOT ErrorMode THEN
          EXIT(FALSE);
        ERROR(Text003, CaptionCopyToRentalUnitFirst, CopyToProjectNo + '-' + CopyToRentalUnitFirst);
      END;
      IF STRLEN(CopyToRentalUnitFirst) = STRLEN(DELCHR(CopyToRentalUnitFirst, '=', '0123456789')) THEN BEGIN
        IF NOT ErrorMode THEN
          EXIT(FALSE);
        ERROR(Text004, CaptionCopyToRentalUnitFirst);
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ValidateNoOfRentalUnitsToCreate@1100529009(ErrorMode@1100529002 : Boolean);
    VAR
      RentalUnit@1100529000 : Record 11012940;
      Number@1100529001 : Integer;
      CounterExist@1100529003 : Integer;
    BEGIN
      CopyToRentalUnitLast := '';
      NewSeriesInfo := '';
      IF (CopyToProjectNo = '') OR (CopyToRentalUnitFirst = '') THEN
        EXIT;
      IF NOT ValidateCopyToProjectNo(FALSE) THEN
        EXIT;
      IF NOT ValidateCopyToRentalUnitFirst(FALSE) THEN
        EXIT;
      CopyToRentalUnitLast := CopyToRentalUnitFirst;
      FOR Number := 2 TO NoOfRentalUnitsToCreate DO BEGIN
        CopyToRentalUnitLast := INCSTR(CopyToRentalUnitLast);
        IF RentalUnit.GET(CopyToProjectNo, CopyToRentalUnitLast) THEN
          CounterExist := CounterExist + 1;
      END;
      IF CounterExist > 0 THEN BEGIN
        NewSeriesInfo := STRSUBSTNO(Text005, CopyToRentalUnitFirst, CopyToRentalUnitLast, CounterExist, NoOfRentalUnitsToCreate);
        IF ErrorMode THEN
          ERROR(NewSeriesInfo);
      END;
    END;

    LOCAL PROCEDURE CheckSelections@1100529007();
    BEGIN
      IF CopyFromProjectNo= '' THEN
        ERROR(Text001, CaptionCopyFromProjectNo);
      IF CopyFromRentalUnit= '' THEN
        ERROR(Text001, CaptionCopyFromRentalUnit);
      IF CopyToProjectNo= '' THEN
        ERROR(Text001, CaptionCopyToProjectNo);
      IF CopyToRentalUnitFirst= '' THEN
        ERROR(Text001, CaptionCopyToRentalUnitFirst);
      IF NoOfRentalUnitsToCreate < 1 THEN
        ERROR(Text001, CaptionNoOfRentalUnitsToCreate);
      ValidateCopyFromProjectNo;
      ValidateCopyFromRentalUnit;
      ValidateCopyToProjectNo(TRUE);
      ValidateCopyToRentalUnitFirst(TRUE);
      ValidateNoOfRentalUnitsToCreate(TRUE);
    END;

    LOCAL PROCEDURE CopyRentalUnit@1100529001();
    VAR
      RentalUnitOrig@1100529000 : Record 11012940;
      RentalUnit@1100529001 : Record 11012940;
      Number@1100529002 : Integer;
    BEGIN
      RentalUnitOrig.GET(CopyFromProjectNo, CopyFromRentalUnit);
      RentalUnit.INIT;
      RentalUnit."Project No." := CopyToProjectNo;
      RentalUnit."Rental Unit" := CopyToRentalUnitFirst;
      RentalUnit.TRANSFERFIELDS(RentalUnitOrig, FALSE);
      RentalUnit."Service Location No." := '';  // 1:1 relation
      RentalUnit.INSERT(TRUE);
      CopyRentalUnitObjects(RentalUnitOrig, RentalUnit);
      FOR Number := 2 TO NoOfRentalUnitsToCreate DO BEGIN
        RentalUnit."Rental Unit" := INCSTR(RentalUnit."Rental Unit");
        RentalUnit.INSERT(TRUE);
        CopyRentalUnitObjects(RentalUnitOrig, RentalUnit);
      END;
      Created := TRUE;
    END;

    LOCAL PROCEDURE CopyRentalUnitObjects@1100529002(RentalUnitOrig@1100529003 : Record 11012940;RentalUnit@1100529002 : Record 11012940);
    VAR
      RentalUnitObjectOrig@1100529000 : Record 11012999;
      RentalUnitObject@1100529001 : Record 11012999;
      NextLineNo@1100529004 : Integer;
    BEGIN
      RentalUnitObjectOrig.SETRANGE("Project No.", RentalUnitOrig."Project No.");
      RentalUnitObjectOrig.SETRANGE("Rental Unit", RentalUnitOrig."Rental Unit");
      IF RentalUnitObjectOrig.FINDSET THEN BEGIN
        NextLineNo := 10000;
        REPEAT
          RentalUnitObject."Project No." := RentalUnit."Project No.";
          RentalUnitObject."Rental Unit" := RentalUnit."Rental Unit";
          RentalUnitObject."Line No." := NextLineNo;
          RentalUnitObject.TRANSFERFIELDS(RentalUnitObjectOrig, FALSE);
          RentalUnitObject.INSERT(TRUE);
          NextLineNo := NextLineNo + 10000;
        UNTIL RentalUnitObjectOrig.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

