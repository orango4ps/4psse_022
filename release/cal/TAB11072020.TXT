OBJECT Table 11072020 Project Recording
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               lvProjectRecLineRec@1100485000 : Record 11072021;
             BEGIN
               //mg.sn, 13-10-10: C-025692
               IF Fixed THEN
                 ERROR(Text002);
               //mg.en, 13-10-10: C-025692

               lvProjectRecLineRec.SETRANGE("Project Manager", "Project Manager");
               lvProjectRecLineRec.SETRANGE("Recording Date", "Recording Date");
               lvProjectRecLineRec.DELETEALL;
             END;

    CaptionML=[ENU=Project Recording;
               NOR=Prosjektregistrering;
               SVE=Projektregistrering];
    LookupPageID=Page11072436;
  }
  FIELDS
  {
    { 10  ;   ;Project Manager     ;Code20        ;TableRelation=Employee;
                                                   CaptionML=[ENU=Project Manager;
                                                              NOR=Prosjektleder;
                                                              SVE=Platschef] }
    { 15  ;   ;Name Project Manager;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Full Name" WHERE (No.=FIELD(Project Manager)));
                                                   CaptionML=[ENU=Name Project Manager;
                                                              NOR=Prosjektlederens navn;
                                                              SVE=Platschefens namn];
                                                   Editable=No }
    { 20  ;   ;Recording Date      ;Date          ;CaptionML=[ENU=Recording Date;
                                                              NOR=Registreringsdato;
                                                              SVE=Registreringsdatum];
                                                   NotBlank=Yes }
    { 30  ;   ;Fixed               ;Boolean       ;CaptionML=[ENU=Fixed;
                                                              NOR=Fast;
                                                              SVE=Fast] }
  }
  KEYS
  {
    {    ;Project Manager,Recording Date          ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100525000 : TextConst 'ENU=Process finished;NOR=Prosess fullf›rt;SVE=Process f„rdig';
      Text002@1100525002 : TextConst 'ENU=Project Recording is fixed, Changes not allowed.;NOR=Prosjektregistreringen er fast, endringer tillates ikke.;SVE=ProjektregistrerIngen „r fast, „ndringar till†ts inte.';

    PROCEDURE Process@1100485000();
    VAR
      ProjectRecLineRec@1100485000 : Record 11072021;
      ProjectPreclosureResult@1100529600 : Record 11012093;
    BEGIN
      ProjectPreclosureResult.CheckOldPreclosureResultReversed;

      ProjectRecLineRec.SETRANGE("Project Manager", "Project Manager");
      ProjectRecLineRec.SETRANGE("Recording Date", "Recording Date");
      IF ProjectRecLineRec.FINDSET THEN BEGIN
        REPEAT
          ProjectRecLineRec.Process();
        UNTIL ProjectRecLineRec.NEXT = 0;
      END;
      MESSAGE(Text001);
    END;

    PROCEDURE CreateProjectRecordingLine@1100528502(ForAllProjectManagers@1100528500 : Boolean);
    VAR
      Job@1100528506 : Record 11072003;
      ProjectRecordingLine@1100528505 : Record 11072021;
      DepartmentAuthorizObject@1100528504 : Record 11012466;
      DepartmentAuthorizationMgt@1100528503 : Codeunit 11012783;
      FillProjectRecordingLines@1100528502 : Report 11020226;
      FPSLicenseManagement@1100528501 : Codeunit 11229289;
    BEGIN
      IF Fixed THEN
        ERROR(Text002);

      IF FPSLicenseManagement.LicenseAndReadPermissionForTable(DepartmentAuthorizObject) THEN
        IF DepartmentAuthorizationMgt.AuthorizationActive THEN BEGIN
          DepartmentAuthorizationMgt.InitAccessFilter(ProjectRecordingLine.TABLENAME,11020295);
          IF DepartmentAuthorizationMgt.IsHardFilter THEN
            Job.FILTERGROUP(2);
          IF DepartmentAuthorizationMgt.GetAccessFilter <> '' THEN
            Job.SETFILTER("Global Dimension 1 Code",DepartmentAuthorizationMgt.GetAccessFilter);
          Job.FILTERGROUP(0);
        END;

      Job.SETFILTER(
        "Single/Main/Sub Project", '%1|%2', Job."Single/Main/Sub Project"::Single,
        Job."Single/Main/Sub Project"::"Main Project");
      Job.SETRANGE("Prognosis Level", Job."Prognosis Level"::Rough);
      FillProjectRecordingLines.SETTABLEVIEW(Job);
      FillProjectRecordingLines.SetDefaults("Project Manager", "Recording Date", ForAllProjectManagers);
      FillProjectRecordingLines.RUNMODAL;
    END;

    PROCEDURE ProcessSelectedProjectRecording@1100528500(VAR ProjectRecording@1100528500 : Record 11072020);
    VAR
      ProjectRecordingLine@1100485000 : Record 11072021;
      ProjectPreclosureResult@1100529600 : Record 11012093;
    BEGIN
      ProjectPreclosureResult.CheckOldPreclosureResultReversed;

      IF ProjectRecording.FINDSET THEN BEGIN
        REPEAT
          ProjectRecordingLine.SETRANGE("Project Manager", ProjectRecording."Project Manager");
          ProjectRecordingLine.SETRANGE("Recording Date", ProjectRecording."Recording Date");
          IF ProjectRecordingLine.FINDSET THEN BEGIN
            REPEAT
              ProjectRecordingLine.Process();
            UNTIL ProjectRecordingLine.NEXT = 0;
          END;
        UNTIL ProjectRecording.NEXT = 0;
      END;
      MESSAGE(Text001);
    END;

    BEGIN
    END.
  }
}

