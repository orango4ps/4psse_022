OBJECT Table 352 Default Dimension
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 11020711=rimd;
    OnInsert=BEGIN
               GLSetup.GET;
               IF "Dimension Code" = GLSetup."Global Dimension 1 Code" THEN
                 IF CheckDimensionAuthorisation(1,"Table ID") THEN //**4PS 20100420.n
                   UpdateGlobalDimCode(1,"Table ID","No.","Dimension Value Code");
               IF "Dimension Code" = GLSetup."Global Dimension 2 Code" THEN
                 IF CheckDimensionAuthorisation(2,"Table ID") THEN //**4PS 20100420.n
                   UpdateGlobalDimCode(2,"Table ID","No.","Dimension Value Code");
               DimMgt.DefaultDimOnInsert(Rec);
               UpdateParentId;

               CentralDataManagement.UpdateDefaultDimension(Rec, 1, xRec); //**4PS
             END;

    OnModify=BEGIN
               GLSetup.GET;
               IF "Dimension Code" = GLSetup."Global Dimension 1 Code" THEN
                 IF CheckDimensionAuthorisation(1,"Table ID") THEN //**4PS 20100420.n
                   UpdateGlobalDimCode(1,"Table ID","No.","Dimension Value Code");
               IF "Dimension Code" = GLSetup."Global Dimension 2 Code" THEN
                 IF CheckDimensionAuthorisation(2,"Table ID") THEN //**4PS 20100420.n
                   UpdateGlobalDimCode(2,"Table ID","No.","Dimension Value Code");
               DimMgt.DefaultDimOnModify(Rec);
               CentralDataManagement.UpdateDefaultDimension(Rec, 2, xRec); //**4PS
             END;

    OnDelete=BEGIN
               GLSetup.GET;
               IF "Dimension Code" = GLSetup."Global Dimension 1 Code" THEN
                 IF CheckDimensionAuthorisation(1,"Table ID") THEN //**4PS 20100420.n
                   UpdateGlobalDimCode(1,"Table ID","No.",'');
               IF "Dimension Code" = GLSetup."Global Dimension 2 Code" THEN
                 IF CheckDimensionAuthorisation(2,"Table ID") THEN //**4PS 20100420.n
                   UpdateGlobalDimCode(2,"Table ID","No.",'');
               DimMgt.DefaultDimOnDelete(Rec);
               CentralDataManagement.UpdateDefaultDimension(Rec, 4, xRec); //**4PS
             END;

    OnRename=BEGIN
               IF ("Table ID" <> xRec."Table ID") OR ("Dimension Code" <> xRec."Dimension Code") THEN
                 ERROR(Text000,TABLECAPTION);
             END;

    CaptionML=[ENU=Default Dimension;
               NOR=Standarddimensjon;
               SVE=Standarddimension];
  }
  FIELDS
  {
    { 1   ;   ;Table ID            ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Table));
                                                   OnValidate=VAR
                                                                TempAllObjWithCaption@1000 : TEMPORARY Record 2000000058;
                                                              BEGIN
                                                                CALCFIELDS("Table Caption");
                                                                DimMgt.DefaultDimObjectNoList(TempAllObjWithCaption);
                                                                TempAllObjWithCaption."Object Type" := TempAllObjWithCaption."Object Type"::Table;
                                                                TempAllObjWithCaption."Object ID" := "Table ID";
                                                                IF NOT TempAllObjWithCaption.FIND THEN
                                                                  FIELDERROR("Table ID");
                                                              END;

                                                   OnLookup=VAR
                                                              TempAllObjWithCaption@1000 : TEMPORARY Record 2000000058;
                                                            BEGIN
                                                              CLEAR(TempAllObjWithCaption);
                                                              DimMgt.DefaultDimObjectNoList(TempAllObjWithCaption);
                                                              IF PAGE.RUNMODAL(PAGE::Objects,TempAllObjWithCaption) = ACTION::LookupOK THEN BEGIN
                                                                "Table ID" := TempAllObjWithCaption."Object ID";
                                                                VALIDATE("Table ID");
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Table ID;
                                                              NOR=Tabell-ID;
                                                              SVE=Tabell-ID];
                                                   NotBlank=Yes }
    { 2   ;   ;No.                 ;Code40        ;OnValidate=VAR
                                                                RecRef@1002 : RecordRef;
                                                              BEGIN
                                                                IF "No." = '' THEN
                                                                  EXIT;
                                                                RecRef.OPEN("Table ID");
                                                                SetRangeToLastFieldInPrimaryKey(RecRef,"No.");
                                                                IF RecRef.ISEMPTY THEN
                                                                  ERROR(NoValidateErr,"No.",RecRef.CAPTION);
                                                                RecRef.CLOSE;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              NOR=Nr.;
                                                              SVE=Nr] }
    { 3   ;   ;Dimension Code      ;Code20        ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                CheckDimension("Dimension Code");
                                                                UpdateDimensionId;
                                                                IF "Dimension Code" <> xRec."Dimension Code" THEN
                                                                  VALIDATE("Dimension Value Code",'');
                                                              END;

                                                   CaptionML=[ENU=Dimension Code;
                                                              NOR=Dimensjonskode;
                                                              SVE=Dimensionskod];
                                                   NotBlank=Yes }
    { 4   ;   ;Dimension Value Code;Code20        ;TableRelation="Dimension Value".Code WHERE (Dimension Code=FIELD(Dimension Code));
                                                   OnValidate=BEGIN
                                                                CheckDimensionValue("Dimension Code","Dimension Value Code");
                                                                UpdateDimensionValueId;
                                                              END;

                                                   CaptionML=[ENU=Dimension Value Code;
                                                              NOR=Dimensjonsverdikode;
                                                              SVE=Dimensionsv„rdekod] }
    { 5   ;   ;Value Posting       ;Option        ;OnValidate=BEGIN
                                                                IF "Value Posting" = "Value Posting"::"No Code" THEN
                                                                  TESTFIELD("Dimension Value Code",'');
                                                              END;

                                                   CaptionML=[ENU=Value Posting;
                                                              NOR=Verdibokf›ring;
                                                              SVE=Bokf”rs med];
                                                   OptionCaptionML=[ENU=" ,Code Mandatory,Same Code,No Code";
                                                                    NOR=" ,Obligatorisk kode,Samme kode,Ingen kode";
                                                                    SVE=" ,Kod alltid,Samma kod,Ingen kod"];
                                                   OptionString=[ ,Code Mandatory,Same Code,No Code] }
    { 6   ;   ;Table Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table ID)));
                                                   CaptionML=[ENU=Table Caption;
                                                              NOR=Tabelloverskrift;
                                                              SVE=Tabellrubrik];
                                                   Editable=No }
    { 7   ;   ;Multi Selection Action;Option      ;CaptionML=[ENU=Multi Selection Action;
                                                              NOR=Handlingsvalg;
                                                              SVE=Flervals†tg„rd];
                                                   OptionCaptionML=[ENU=" ,Change,Delete";
                                                                    NOR=" ,Endre,Slett";
                                                                    SVE=" ,Žndra,Ta bort"];
                                                   OptionString=[ ,Change,Delete] }
    { 8000;   ;ParentId            ;GUID          ;TableRelation=IF (Table ID=CONST(15)) "G/L Account".Id
                                                                 ELSE IF (Table ID=CONST(18)) Customer.Id
                                                                 ELSE IF (Table ID=CONST(23)) Vendor.Id
                                                                 ELSE IF (Table ID=CONST(5200)) Employee.Id;
                                                   OnValidate=BEGIN
                                                                UpdateTableIdAndNo(ParentId);
                                                              END;

                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=ParentId;
                                                              NOR=ParentId;
                                                              SVE=parentId] }
    { 8001;   ;DimensionId         ;GUID          ;TableRelation=Dimension.Id;
                                                   OnValidate=VAR
                                                                Dimension@1000 : Record 348;
                                                              BEGIN
                                                                Dimension.SETRANGE(Id,DimensionId);
                                                                IF NOT Dimension.FINDFIRST THEN
                                                                  ERROR(DimensionIdDoesNotMatchADimensionErr);

                                                                CheckDimension(Dimension.Code);
                                                                "Dimension Code" := Dimension.Code;
                                                              END;

                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=DimensionId;
                                                              NOR=DimensionId;
                                                              SVE=dimensionId] }
    { 8002;   ;DimensionValueId    ;GUID          ;TableRelation="Dimension Value".Id;
                                                   OnValidate=VAR
                                                                DimensionValue@1000 : Record 349;
                                                              BEGIN
                                                                IF ISNULLGUID(DimensionValueId) THEN BEGIN
                                                                  "Dimension Value Code" := '';
                                                                  EXIT;
                                                                END;

                                                                DimensionValue.SETRANGE(Id,DimensionValueId);
                                                                IF NOT DimensionValue.FINDFIRST THEN
                                                                  ERROR(DimensionValueIdDoesNotMatchADimensionValueErr);

                                                                IF "Dimension Code" = '' THEN
                                                                  "Dimension Code" := DimensionValue."Dimension Code"
                                                                ELSE
                                                                  IF "Dimension Code" <> DimensionValue."Dimension Code" THEN
                                                                    ERROR(DimensionIdMismatchErr);
                                                                CheckDimensionValue("Dimension Code",DimensionValue.Code);
                                                                "Dimension Value Code" := DimensionValue.Code;
                                                              END;

                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=DimensionValueId;
                                                              NOR=DimensionValueId;
                                                              SVE=dimensionValueId] }
  }
  KEYS
  {
    {    ;Table ID,No.,Dimension Code             ;Clustered=Yes }
    {    ;Dimension Code                           }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You can''t rename a %1.;NOR=Du kan ikke gi nytt navn til en %1.;SVE=Du kan inte byta namn p† %1.';
      GLSetup@1001 : Record 98;
      DimMgt@1002 : Codeunit 408;
      DimensionIdDoesNotMatchADimensionErr@1003 : TextConst '@@@={Locked};ENU=The "dimensionId" does not match to a Dimension.;NOR=The "dimensionId" does not match to a Dimension.';
      DimensionValueIdDoesNotMatchADimensionValueErr@1005 : TextConst '@@@={Locked};ENU=The "dimensionValueId" does not match to a Dimension Value.;NOR=The "dimensionValueId" does not match to a Dimension Value.';
      DimensionIdMismatchErr@1006 : TextConst '@@@={Locked};ENU=The "dimensionId" and "dimensionValueId" match to different Dimension records.;NOR=The "dimensionId" and "dimensionValueId" match to different Dimension records.';
      ParentIdDoesNotMatchAnIntegrationRecordErr@1007 : TextConst '@@@={Locked};ENU=The "parenteId" does not match to any entity.;NOR=The "parenteId" does not match to any entity.';
      RequestedRecordIsNotSupportedErr@1008 : TextConst '@@@={Locked};ENU=Images are not supported for requested entity - %1.;NOR=Images are not supported for requested entity - %1.';
      NoValidateErr@1004 : TextConst '@@@="%1 - a master table record key value; %2 - table caption. ";ENU=The field No. of table Default Dimension contains a value (%1) that cannot be found in the related table (%2).;NOR=Feltet Nr. i tabellen Standarddimensjon inneholder en verdi (%1) som ikke finnes i den relaterte tabellen (%2).';
      Text11012001@1210190000 : TextConst 'ENU="Changes to %1 are only valid by %2 card. ";SVE="Žndringar av %1 „r endast giltiga med %2-kort. "';
      CentralDataManagement@1100528600 : Codeunit 11012010;

    [External]
    PROCEDURE GetCaption@3() : Text[250];
    VAR
      ObjTransl@1000 : Record 377;
      CurrTableID@1001 : Integer;
      NewTableID@1002 : Integer;
      NewNo@1003 : Code[20];
      SourceTableName@1004 : Text[100];
    BEGIN
      IF NOT EVALUATE(NewTableID,GETFILTER("Table ID")) THEN
        EXIT('');

      IF NewTableID = 0 THEN
        IF GETRANGEMIN("Table ID") = GETRANGEMAX("Table ID") THEN
          NewTableID := GETRANGEMIN("Table ID")
        ELSE
          NewTableID := 0;

      IF NewTableID <> CurrTableID THEN
        SourceTableName := ObjTransl.TranslateObject(ObjTransl."Object Type"::Table,NewTableID);
      CurrTableID := NewTableID;

      IF GETFILTER("No.") <> '' THEN
        IF GETRANGEMIN("No.") = GETRANGEMAX("No.") THEN
          NewNo := GETRANGEMIN("No.")
        ELSE
          NewNo := '';

      IF NewTableID <> 0 THEN
        EXIT(STRSUBSTNO('%1 %2',SourceTableName,NewNo));

      EXIT('');
    END;

    LOCAL PROCEDURE UpdateGlobalDimCode@25(GlobalDimCodeNo@1000 : Integer;TableID@1001 : Integer;AccNo@1002 : Code[20];NewDimValue@1003 : Code[20]);
    BEGIN
      CASE TableID OF
        DATABASE::"G/L Account":
          UpdateGLAccGlobalDimCode(GlobalDimCodeNo,AccNo,NewDimValue);
        DATABASE::Customer:
          UpdateCustGlobalDimCode(GlobalDimCodeNo,AccNo,NewDimValue);
        DATABASE::Vendor:
          UpdateVendGlobalDimCode(GlobalDimCodeNo,AccNo,NewDimValue);
        DATABASE::Item:
          UpdateItemGlobalDimCode(GlobalDimCodeNo,AccNo,NewDimValue);
        DATABASE::"Resource Group":
          UpdateResGrGlobalDimCode(GlobalDimCodeNo,AccNo,NewDimValue);
        DATABASE::Resource:
          UpdateResGlobalDimCode(GlobalDimCodeNo,AccNo,NewDimValue);
        DATABASE::Job:
          UpdateJobGlobalDimCode(GlobalDimCodeNo,AccNo,NewDimValue);
        DATABASE::"Bank Account":
          UpdateBankGlobalDimCode(GlobalDimCodeNo,AccNo,NewDimValue);
        DATABASE::Employee:
          UpdateEmpoyeeGlobalDimCode(GlobalDimCodeNo,AccNo,NewDimValue);
        DATABASE::"Fixed Asset":
          UpdateFAGlobalDimCode(GlobalDimCodeNo,AccNo,NewDimValue);
        DATABASE::Insurance:
          UpdateInsuranceGlobalDimCode(GlobalDimCodeNo,AccNo,NewDimValue);
        DATABASE::"Responsibility Center":
          UpdateRespCenterGlobalDimCode(GlobalDimCodeNo,AccNo,NewDimValue);
        DATABASE::"Work Center":
          UpdateWorkCenterGlobalDimCode(GlobalDimCodeNo,AccNo,NewDimValue);
        DATABASE::"Salesperson/Purchaser":
          UpdateSalesPurchGlobalDimCode(GlobalDimCodeNo,AccNo,NewDimValue);
        DATABASE::Campaign:
          UpdateCampaignGlobalDimCode(GlobalDimCodeNo,AccNo,NewDimValue);
        DATABASE::"Customer Template":
          UpdateCustTempGlobalDimCode(GlobalDimCodeNo,AccNo,NewDimValue);
        DATABASE::"Cash Flow Manual Expense":
          UpdateNeutrPayGlobalDimCode(GlobalDimCodeNo,AccNo,NewDimValue);
        DATABASE::"Cash Flow Manual Revenue":
          UpdateNeutrRevGlobalDimCode(GlobalDimCodeNo,AccNo,NewDimValue);
      //**4PS.sn
        DATABASE::"Vendor Template":
           UpdateVendorTemplateGLobalDimCode(GlobalDimCodeNo,AccNo,NewDimValue);
        DATABASE::"Plant Type":
          UpdatePlantTypeGLobalDimCode(GlobalDimCodeNo,AccNo,NewDimValue);
        DATABASE::"Plant Number":
          UpdatePlantNoGLobalDimCode(GlobalDimCodeNo,AccNo,NewDimValue);
        DATABASE::"Service Order":
          UpdateServOrderGLobalDimCode(GlobalDimCodeNo,AccNo,NewDimValue);
        DATABASE::"Service-Contract":
          UpdateServContractrGLobalDimCode(GlobalDimCodeNo,AccNo,NewDimValue);
        DATABASE::"Loan Header":
          UpdateLoanHeaderGLobalDimCode(GlobalDimCodeNo,AccNo,NewDimValue);
      //**4PS.en
        ELSE
          OnAfterUpdateGlobalDimCode(GlobalDimCodeNo,TableID,AccNo,NewDimValue);
      END;
    END;

    LOCAL PROCEDURE UpdateGLAccGlobalDimCode@26(GlobalDimCodeNo@1003 : Integer;GLAccNo@1001 : Code[20];NewDimValue@1000 : Code[20]);
    VAR
      GLAcc@1004 : Record 15;
    BEGIN
      IF GLAcc.GET(GLAccNo) THEN BEGIN
        CASE GlobalDimCodeNo OF
          1:
            GLAcc."Global Dimension 1 Code" := NewDimValue;
          2:
            GLAcc."Global Dimension 2 Code" := NewDimValue;
        END;
        GLAcc.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE UpdateCustGlobalDimCode@27(GlobalDimCodeNo@1003 : Integer;CustNo@1001 : Code[20];NewDimValue@1000 : Code[20]);
    VAR
      Cust@1004 : Record 18;
    BEGIN
      IF Cust.GET(CustNo) THEN BEGIN
        CASE GlobalDimCodeNo OF
          1:
            Cust."Global Dimension 1 Code" := NewDimValue;
          2:
            Cust."Global Dimension 2 Code" := NewDimValue;
        END;
        Cust.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE UpdateVendGlobalDimCode@28(GlobalDimCodeNo@1003 : Integer;VendNo@1001 : Code[20];NewDimValue@1000 : Code[20]);
    VAR
      Vend@1004 : Record 23;
    BEGIN
      IF Vend.GET(VendNo) THEN BEGIN
        CASE GlobalDimCodeNo OF
          1:
            Vend."Global Dimension 1 Code" := NewDimValue;
          2:
            Vend."Global Dimension 2 Code" := NewDimValue;
        END;
        Vend.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE UpdateItemGlobalDimCode@29(GlobalDimCodeNo@1003 : Integer;ItemNo@1001 : Code[20];NewDimValue@1000 : Code[20]);
    VAR
      Item@1004 : Record 27;
    BEGIN
      IF Item.GET(ItemNo) THEN BEGIN
        CASE GlobalDimCodeNo OF
          1:
            Item."Global Dimension 1 Code" := NewDimValue;
          2:
            Item."Global Dimension 2 Code" := NewDimValue;
        END;
        Item.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE UpdateResGrGlobalDimCode@30(GlobalDimCodeNo@1003 : Integer;ResGrNo@1001 : Code[20];NewDimValue@1000 : Code[20]);
    VAR
      ResGr@1004 : Record 152;
    BEGIN
      IF ResGr.GET(ResGrNo) THEN BEGIN
        CASE GlobalDimCodeNo OF
          1:
            ResGr."Global Dimension 1 Code" := NewDimValue;
          2:
            ResGr."Global Dimension 2 Code" := NewDimValue;
        END;
        ResGr.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE UpdateResGlobalDimCode@31(GlobalDimCodeNo@1003 : Integer;ResNo@1001 : Code[20];NewDimValue@1000 : Code[20]);
    VAR
      Res@1004 : Record 156;
    BEGIN
      IF Res.GET(ResNo) THEN BEGIN
        CASE GlobalDimCodeNo OF
          1:
            Res."Global Dimension 1 Code" := NewDimValue;
          2:
            Res."Global Dimension 2 Code" := NewDimValue;
        END;
        Res.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE UpdateJobGlobalDimCode@32(GlobalDimCodeNo@1003 : Integer;JobNo@1001 : Code[20];NewDimValue@1000 : Code[20]);
    VAR
      Job@1004 : Record 11072003;
    BEGIN
      IF Job.GET(JobNo) THEN BEGIN
        CASE GlobalDimCodeNo OF
          1:
            IF Job."Global Dimension 1 Code" <> NewDimValue THEN BEGIN //**4PS.n
              Job."Global Dimension 1 Code" := NewDimValue;
            //**4PS.sn
              Job.UpdatePrefLocations;
            END;
            //**4PS.en
          2:
            Job."Global Dimension 2 Code" := NewDimValue;
        END;
        Job.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE UpdateBankGlobalDimCode@33(GlobalDimCodeNo@1003 : Integer;BankAccNo@1001 : Code[20];NewDimValue@1000 : Code[20]);
    VAR
      BankAcc@1004 : Record 270;
    BEGIN
      IF BankAcc.GET(BankAccNo) THEN BEGIN
        CASE GlobalDimCodeNo OF
          1:
            BankAcc."Global Dimension 1 Code" := NewDimValue;
          2:
            BankAcc."Global Dimension 2 Code" := NewDimValue;
        END;
        BankAcc.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE UpdateEmpoyeeGlobalDimCode@34(GlobalDimCodeNo@1003 : Integer;EmployeeNo@1001 : Code[20];NewDimValue@1000 : Code[20]);
    VAR
      Employee@1004 : Record 5200;
    BEGIN
      IF Employee.GET(EmployeeNo) THEN BEGIN
        CASE GlobalDimCodeNo OF
          1:
            Employee."Global Dimension 1 Code" := NewDimValue;
          2:
            Employee."Global Dimension 2 Code" := NewDimValue;
        END;
        Employee.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE UpdateFAGlobalDimCode@35(GlobalDimCodeNo@1003 : Integer;FANo@1001 : Code[20];NewDimValue@1000 : Code[20]);
    VAR
      FA@1004 : Record 5600;
    BEGIN
      IF FA.GET(FANo) THEN BEGIN
        CASE GlobalDimCodeNo OF
          1:
            FA."Global Dimension 1 Code" := NewDimValue;
          2:
            FA."Global Dimension 2 Code" := NewDimValue;
        END;
        FA.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE UpdateInsuranceGlobalDimCode@36(GlobalDimCodeNo@1003 : Integer;InsuranceNo@1001 : Code[20];NewDimValue@1000 : Code[20]);
    VAR
      Insurance@1004 : Record 11072087;
    BEGIN
      IF Insurance.GET(InsuranceNo) THEN BEGIN
        CASE GlobalDimCodeNo OF
          1:
            Insurance."Global Dimension 1 Code" := NewDimValue;
          2:
            Insurance."Global Dimension 2 Code" := NewDimValue;
        END;
        Insurance.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE UpdateRespCenterGlobalDimCode@37(GlobalDimCodeNo@1003 : Integer;RespCenterNo@1001 : Code[20];NewDimValue@1000 : Code[20]);
    VAR
      RespCenter@1004 : Record 5714;
    BEGIN
      IF RespCenter.GET(RespCenterNo) THEN BEGIN
        CASE GlobalDimCodeNo OF
          1:
            RespCenter."Global Dimension 1 Code" := NewDimValue;
          2:
            RespCenter."Global Dimension 2 Code" := NewDimValue;
        END;
        RespCenter.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE UpdateWorkCenterGlobalDimCode@38(GlobalDimCodeNo@1003 : Integer;WorkCenterNo@1001 : Code[20];NewDimValue@1000 : Code[20]);
    VAR
      WorkCenter@1004 : Record 99000754;
    BEGIN
      IF WorkCenter.GET(WorkCenterNo) THEN BEGIN
        CASE GlobalDimCodeNo OF
          1:
            WorkCenter."Global Dimension 1 Code" := NewDimValue;
          2:
            WorkCenter."Global Dimension 2 Code" := NewDimValue;
        END;
        WorkCenter.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE UpdateSalesPurchGlobalDimCode@39(GlobalDimCodeNo@1003 : Integer;SalespersonPurchaserNo@1001 : Code[20];NewDimValue@1000 : Code[20]);
    VAR
      SalespersonPurchaser@1004 : Record 13;
    BEGIN
      IF SalespersonPurchaser.GET(SalespersonPurchaserNo) THEN BEGIN
        CASE GlobalDimCodeNo OF
          1:
            SalespersonPurchaser."Global Dimension 1 Code" := NewDimValue;
          2:
            SalespersonPurchaser."Global Dimension 2 Code" := NewDimValue;
        END;
        SalespersonPurchaser.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE UpdateCampaignGlobalDimCode@40(GlobalDimCodeNo@1003 : Integer;CampaignNo@1001 : Code[20];NewDimValue@1000 : Code[20]);
    VAR
      Campaign@1004 : Record 11072034;
    BEGIN
      IF Campaign.GET(CampaignNo) THEN BEGIN
        CASE GlobalDimCodeNo OF
          1:
            Campaign."Global Dimension 1 Code" := NewDimValue;
          2:
            Campaign."Global Dimension 2 Code" := NewDimValue;
        END;
        Campaign.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE UpdateCustTempGlobalDimCode@41(GlobalDimCodeNo@1003 : Integer;CustTemplateNo@1001 : Code[20];NewDimValue@1000 : Code[20]);
    VAR
      CustTemplate@1004 : Record 5105;
    BEGIN
      IF CustTemplate.GET(CustTemplateNo) THEN BEGIN
        CASE GlobalDimCodeNo OF
          1:
            CustTemplate."Global Dimension 1 Code" := NewDimValue;
          2:
            CustTemplate."Global Dimension 2 Code" := NewDimValue;
        END;
        CustTemplate.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE UpdateNeutrPayGlobalDimCode@42(GlobalDimCodeNo@1003 : Integer;CFManualExpenseNo@1001 : Code[20];NewDimValue@1000 : Code[20]);
    VAR
      CFManualExpense@1004 : Record 850;
    BEGIN
      IF CFManualExpense.GET(CFManualExpenseNo) THEN BEGIN
        CASE GlobalDimCodeNo OF
          1:
            CFManualExpense."Global Dimension 1 Code" := NewDimValue;
          2:
            CFManualExpense."Global Dimension 2 Code" := NewDimValue;
        END;
        CFManualExpense.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE UpdateNeutrRevGlobalDimCode@43(GlobalDimCodeNo@1003 : Integer;CFManualRevenueNo@1001 : Code[20];NewDimValue@1000 : Code[20]);
    VAR
      CFManualRevenue@1004 : Record 849;
    BEGIN
      IF CFManualRevenue.GET(CFManualRevenueNo) THEN BEGIN
        CASE GlobalDimCodeNo OF
          1:
            CFManualRevenue."Global Dimension 1 Code" := NewDimValue;
          2:
            CFManualRevenue."Global Dimension 2 Code" := NewDimValue;
        END;
        CFManualRevenue.MODIFY(TRUE);
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterUpdateGlobalDimCode@1(GlobalDimCodeNo@1003 : Integer;TableID@1002 : Integer;AccNo@1001 : Code[20];NewDimValue@1000 : Code[20]);
    BEGIN
    END;

    LOCAL PROCEDURE CheckDimension@6(DimensionCode@1000 : Code[20]);
    BEGIN
      DimMgt.SetCompany(Rec.CURRENTCOMPANY); //**4PS.n
      IF NOT DimMgt.CheckDim(DimensionCode) THEN
        ERROR(DimMgt.GetDimErr);
    END;

    LOCAL PROCEDURE CheckDimensionValue@8(DimensionCode@1000 : Code[20];DimensionValueCode@1001 : Code[20]);
    BEGIN
      DimMgt.SetCompany(Rec.CURRENTCOMPANY); //**4PS.n
      IF NOT DimMgt.CheckDimValue(DimensionCode,DimensionValueCode) THEN
        ERROR(DimMgt.GetDimErr);
      IF "Value Posting" = "Value Posting"::"No Code" THEN
        TESTFIELD("Dimension Value Code",'');
    END;

    LOCAL PROCEDURE SetRangeToLastFieldInPrimaryKey@7(RecRef@1000 : RecordRef;Value@1001 : Code[20]);
    VAR
      FieldRef@1002 : FieldRef;
      KeyRef@1003 : KeyRef;
    BEGIN
      KeyRef := RecRef.KEYINDEX(1);
      FieldRef := KeyRef.FIELDINDEX(KeyRef.FIELDCOUNT);
      FieldRef.SETRANGE(Value);
    END;

    LOCAL PROCEDURE UpdateTableIdAndNo@11(Id@1002 : GUID);
    VAR
      IntegrationRecord@1001 : Record 5151;
      Customer@1006 : Record 18;
      Item@1005 : Record 27;
      Vendor@1004 : Record 23;
      Employee@1003 : Record 5200;
    BEGIN
      IF NOT IntegrationRecord.GET(Id) THEN
        ERROR(ParentIdDoesNotMatchAnIntegrationRecordErr);

      CASE IntegrationRecord."Table ID" OF
        DATABASE::Item:
          BEGIN
            Item.GET(IntegrationRecord."Record ID");
            "No." := Item."No.";
          END;
        DATABASE::Customer:
          BEGIN
            Customer.GET(IntegrationRecord."Record ID");
            "No." := Customer."No.";
          END;
        DATABASE::Vendor:
          BEGIN
            Vendor.GET(IntegrationRecord."Record ID");
            "No." := Vendor."No.";
          END;
        DATABASE::Employee:
          BEGIN
            Employee.GET(IntegrationRecord."Record ID");
            "No." := Employee."No.";
          END;
        ELSE
          ThrowEntityNotSupportedError(IntegrationRecord);
      END;

      "Table ID" := IntegrationRecord."Table ID";
    END;

    LOCAL PROCEDURE UpdateParentId@4();
    VAR
      Customer@1006 : Record 18;
      Item@1005 : Record 27;
      Vendor@1004 : Record 23;
      Employee@1003 : Record 5200;
    BEGIN
      CASE "Table ID" OF
        DATABASE::Item:
          BEGIN
            IF Item.GET("No.") THEN
              ParentId := Item.Id;
          END;
        DATABASE::Customer:
          BEGIN
            IF Customer.GET("No.") THEN
              ParentId := Customer.Id;
          END;
        DATABASE::Vendor:
          BEGIN
            IF Vendor.GET("No.") THEN
              ParentId := Vendor.Id;
          END;
        DATABASE::Employee:
          BEGIN
            IF Employee.GET("No.") THEN
              ParentId := Employee.Id;
          END;
      END;
    END;

    LOCAL PROCEDURE UpdateDimensionId@5();
    VAR
      Dimension@1000 : Record 348;
    BEGIN
      IF Dimension.GET("Dimension Code") THEN
        DimensionId := Dimension.Id;
    END;

    LOCAL PROCEDURE UpdateDimensionValueId@9();
    VAR
      DimensionValue@1000 : Record 349;
    BEGIN
      IF DimensionValue.GET("Dimension Code","Dimension Value Code") THEN
        DimensionValueId := DimensionValue.Id;
      IF "Dimension Value Code" = '' THEN
        CLEAR(DimensionValueId);
    END;

    [External]
    PROCEDURE UpdateReferencedIds@13();
    BEGIN
      UpdateParentId;
      UpdateDimensionId;
      UpdateDimensionValueId;
      MODIFY;
    END;

    LOCAL PROCEDURE ThrowEntityNotSupportedError@2(VAR IntegrationRecord@1007 : Record 5151);
    VAR
      AllObjWithCaption@1006 : Record 2000000058;
    BEGIN
      AllObjWithCaption.SETRANGE("Object Type",AllObjWithCaption."Object Type"::Table);
      AllObjWithCaption.SETRANGE("Object ID",IntegrationRecord."Table ID");
      IF AllObjWithCaption.FINDFIRST THEN ;
      ERROR(STRSUBSTNO(RequestedRecordIsNotSupportedErr,AllObjWithCaption."Object Caption"));
    END;

    LOCAL PROCEDURE CheckDimensionAuthorisation@1100525000(GlobalDim@1100525001 : Integer;TableID@1100525002 : Integer) : Boolean;
    BEGIN
      //**4PS
      EXIT(DimMgt.CheckDimensionAuthorisation(GlobalDim, TableID));
    END;

    LOCAL PROCEDURE UpdateVendorTemplateGLobalDimCode@1100525001(GlobalDimCodeNo@1100525003 : Integer;VendTemplateNo@1100525002 : Code[20];NewDimValue@1100525001 : Code[20]);
    VAR
      VendorTemplate@1100525000 : Record 11020240;
    BEGIN
      //**4PS
      IF VendorTemplate.GET(VendTemplateNo) THEN BEGIN
        CASE GlobalDimCodeNo OF
          1:
            VendorTemplate."Global Dimension 1 Code" := NewDimValue;
          2:
            VendorTemplate."Global Dimension 2 Code" := NewDimValue;
        END;
        VendorTemplate.MODIFY(TRUE);
      END;
    END;

    PROCEDURE UpdatePlantTypeGLobalDimCode@1100525002(GlobalDimCodeNo@1100525002 : Integer;PlantTypeCode@1100525001 : Code[20];NewDimValue@1100525000 : Code[20]);
    VAR
      PlantType@1100525003 : Record 11012551;
    BEGIN
      //**4PS
      IF PlantType.GET(PlantTypeCode) THEN BEGIN
        CASE GlobalDimCodeNo OF
          1:
            PlantType."Department Code" := NewDimValue;
          2:
            PlantType."Cost Object" := NewDimValue;
        END;
        PlantType.MODIFY(TRUE);
      END;
    END;

    PROCEDURE UpdatePlantNoGLobalDimCode@1100525003(GlobalDimCodeNo@1100525002 : Integer;PlantTypePlantNo@1100525001 : Code[20];NewDimValue@1100525000 : Code[20]);
    VAR
      PlantNumber@1100525003 : Record 11012552;
    BEGIN
      //**4PS
      DimMgt.AnalyseCombinedPlantNo(PlantTypePlantNo, PlantNumber."Plant Type", PlantNumber."No.");
      IF PlantNumber.GET(PlantNumber."Plant Type", PlantNumber."No.") THEN BEGIN
        CASE GlobalDimCodeNo OF
          1:
            PlantNumber."Department Code" := NewDimValue;
        END;
        PlantNumber.MODIFY(TRUE);
      END;
    END;

    PROCEDURE UpdateServOrderGLobalDimCode@1100525004(GlobalDimCodeNo@1100525002 : Integer;ServiceOrderNo@1100525001 : Code[20];NewDimValue@1100525000 : Code[20]);
    VAR
      ServiceOrder@1100525003 : Record 11012823;
    BEGIN
      //**4PS
      IF ServiceOrder.GET(ServiceOrderNo) THEN BEGIN
        CASE GlobalDimCodeNo OF
          1:
            ERROR(Text11012001, FIELDCAPTION("Dimension Code"), ServiceOrder.TABLECAPTION);
        END;
        ServiceOrder.MODIFY(TRUE);
      END;
    END;

    PROCEDURE UpdateServContractrGLobalDimCode@1100525005(GlobalDimCodeNo@1100525002 : Integer;ServiceContractNo@1100525001 : Code[20];NewDimValue@1100525000 : Code[20]);
    VAR
      ServiceContract@1100525003 : Record 11012812;
    BEGIN
      //**4PS
      IF ServiceContract.GET(ServiceContractNo) THEN BEGIN
        CASE GlobalDimCodeNo OF
          1: ERROR(Text11012001, FIELDCAPTION("Dimension Code"), ServiceContract.TABLECAPTION);
        END;
        ServiceContract.MODIFY(TRUE);
      END;
    END;

    PROCEDURE UpdateLoanHeaderGLobalDimCode@1100528500(GlobalDimCodeNo@1100525002 : Integer;LoanCode@1100525001 : Code[20];NewDimValue@1100525000 : Code[20]);
    VAR
      LoanHeader@1100525003 : Record 11020587;
    BEGIN
      //**4PS
      IF LoanHeader.GET(LoanCode) THEN BEGIN
        CASE GlobalDimCodeNo OF
          1:
            LoanHeader."Global Dimension 1 Code" := NewDimValue;
          2:
            LoanHeader."Global Dimension 2 Code" := NewDimValue;
        END;
        LoanHeader.MODIFY(TRUE);
      END;
    END;

    BEGIN
    {
      4PS 20100420: Field authorisation
    }
    END.
  }
}

