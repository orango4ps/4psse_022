OBJECT Table 11124992 App Setup Group GAP
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnInsert=BEGIN
               InsertProductSetup;
             END;

    OnModify=BEGIN
               InsertProductSetup;
             END;

    OnDelete=VAR
               GAPUserByAppSetupGroup@1100528550 : Record 11124980;
               RecordRef@1100528000 : RecordRef;
               FieldRefGroupName@1100528001 : FieldRef;
             BEGIN
               IF "Product Code" = '' THEN EXIT;

               GAPUserByAppSetupGroup.SETRANGE("Product Code", "Product Code");
               GAPUserByAppSetupGroup.SETRANGE("Group Name", Name);

               IF GAPUserByAppSetupGroup.FINDSET THEN
                 IF DIALOG.CONFIRM(STRSUBSTNO(TEXT000,Name,GAPUserByAppSetupGroup.COUNT)) THEN
                   GAPUserByAppSetupGroup.DELETEALL(TRUE)
                 ELSE
                   ERROR('');

               IF NOT GAPProducts.GET("Product Code") THEN
                 ERROR(TEXT001,"Product Code");


               RecordRef.OPEN(GAPProducts."User Setup Table Id");
               FieldRefGroupName := RecordRef.FIELD(10);
               FieldRefGroupName.SETFILTER(Name);

               IF RecordRef.FINDFIRST THEN
                 RecordRef.DELETEALL(TRUE);
             END;

    CaptionML=ENU=App Setup Group;
    LookupPageID=Page11129144;
  }
  FIELDS
  {
    { 10  ;   ;Name                ;Code10        ;DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Name;
                                                              SVE=Namn] }
    { 20  ;   ;Product Code        ;Code10        ;TableRelation="Products GAP".Code;
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Product Code;
                                                              SVE=Produktkod];
                                                   NotBlank=Yes }
    { 30  ;   ;Description         ;Text100       ;DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Description;
                                                              SVE=Beskrivning] }
  }
  KEYS
  {
    {    ;Name                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      TEXT000@1100528550 : TextConst 'ENU=Group %1 has %2 users. These users and their app setup must be deleted before the app group can be deleted. Do you want to delete those users and their setup?';
      GAPProducts@1100528000 : Record 11124990;
      TEXT001@1100528001 : TextConst 'ENU=Product %1 is not implemented';

    PROCEDURE ActiveUsersCount@1100528550(OnlyActiveUsers@1100528000 : Boolean) : Integer;
    VAR
      RecordRef@1100528003 : RecordRef;
      FieldRefGroupName@1100528002 : FieldRef;
      FieldRefUsername@1100528001 : FieldRef;
      FieldRefActive@1100528004 : FieldRef;
    BEGIN
      IF "Product Code" = '' THEN EXIT;

      IF NOT GAPProducts.GET("Product Code") THEN
        ERROR(TEXT001,"Product Code");

      RecordRef.OPEN(GAPProducts."User Setup Table Id");
      FieldRefGroupName := RecordRef.FIELD(10);
      FieldRefGroupName.SETFILTER(Name);

      FieldRefUsername := RecordRef.FIELD(20);
      FieldRefUsername.SETFILTER('<>%1','');

      IF OnlyActiveUsers THEN BEGIN
        FieldRefActive := RecordRef.FIELD(40);
        FieldRefActive.SETFILTER('Yes');
      END;

      EXIT(RecordRef.COUNT);
    END;

    PROCEDURE UsersCountByProduct@1100528553(OnlyActiveUsers@1100528550 : Boolean) : Integer;
    VAR
      RecordRef@1100528003 : RecordRef;
      FieldRefProductCode@1100528002 : FieldRef;
      FieldRefUsername@1100528001 : FieldRef;
      FieldRefActive@1100528000 : FieldRef;
    BEGIN
      IF "Product Code" = '' THEN EXIT;

      IF NOT GAPProducts.GET("Product Code") THEN
        ERROR(TEXT001,"Product Code");

      RecordRef.OPEN(GAPProducts."User Setup Table Id");
      FieldRefProductCode := RecordRef.FIELD(30);
      FieldRefProductCode.SETFILTER("Product Code");

      FieldRefUsername := RecordRef.FIELD(20);
      FieldRefUsername.SETFILTER('<>%1','');

      IF OnlyActiveUsers THEN BEGIN
        FieldRefActive := RecordRef.FIELD(40);
        FieldRefActive.SETFILTER('Yes');
      END;

      EXIT(RecordRef.COUNT);
    END;

    LOCAL PROCEDURE InsertProductSetup@1100528000();
    VAR
      RecordRef@1100528002 : RecordRef;
      FieldRefGroupName@1100528001 : FieldRef;
      FieldRefUsername@1100528000 : FieldRef;
    BEGIN
      IF "Product Code" = '' THEN EXIT;

      IF NOT GAPProducts.GET("Product Code") THEN
        ERROR(TEXT001,"Product Code");

      RecordRef.OPEN(GAPProducts."User Setup Table Id");
      FieldRefGroupName := RecordRef.FIELD(10);
      FieldRefGroupName.VALUE := Name;
      FieldRefUsername := RecordRef.FIELD(20);
      FieldRefUsername.VALUE := '';

      IF NOT RecordRef.INSERT(TRUE) THEN
        RecordRef.MODIFY(TRUE);
    END;

    BEGIN
    END.
  }
}

