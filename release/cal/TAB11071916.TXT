OBJECT Table 11071916 Std. Surv. Sales Surch. Vers.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Index Date");
             END;

    OnDelete=VAR
               StdSurvSalesSurchLine@1100528600 : Record 11071917;
             BEGIN
               StdSurvSalesSurchLine.SETRANGE(Type, Type);
               StdSurvSalesSurchLine.SETRANGE(Code, Code);
               StdSurvSalesSurchLine.SETRANGE("Index Date", "Index Date");
               StdSurvSalesSurchLine.SETRANGE("Department Code", "Department Code");
               StdSurvSalesSurchLine.DELETEALL(TRUE);
             END;

    OnRename=VAR
               StdSurvSalesSurchLine@1100528600 : Record 11071917;
               ChangedStdSurvSalesSurchLine@1100528601 : Record 11071917;
             BEGIN
               IF (("Index Date" <> 0D) AND ("Index Date" <> xRec."Index Date")) OR
                  ("Department Code" <> xRec."Department Code")
               THEN BEGIN
                 StdSurvSalesSurchLine.SETRANGE(Type, xRec.Type);
                 StdSurvSalesSurchLine.SETRANGE(Code, xRec.Code);
                 StdSurvSalesSurchLine.SETRANGE("Index Date", xRec."Index Date");
                 StdSurvSalesSurchLine.SETRANGE("Department Code", xRec."Department Code");
                 IF StdSurvSalesSurchLine.FINDSET THEN
                   REPEAT
                     ChangedStdSurvSalesSurchLine.COPY(StdSurvSalesSurchLine);
                     ChangedStdSurvSalesSurchLine.Type := Type;
                     ChangedStdSurvSalesSurchLine.Code := Code;
                     ChangedStdSurvSalesSurchLine."Index Date" := "Index Date";
                     ChangedStdSurvSalesSurchLine."Department Code" := "Department Code";
                     ChangedStdSurvSalesSurchLine.INSERT;
                     StdSurvSalesSurchLine.DELETE;
                   UNTIL StdSurvSalesSurchLine.NEXT = 0;
               END;
             END;

    CaptionML=[ENU=Standard Survey Sales Surcharge Version;
               NOR=Versjon av standard kartlegging, tilleggsgebyr for salg;
               SVE=Ver. std.kartl. f”rs.till„gg];
    LookupPageID=Page11126276;
    DrillDownPageID=Page11126276;
  }
  FIELDS
  {
    { 10  ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Type;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=[ENU=Company,Customer,Customer Template;
                                                                    NOR=Selskap,Kunde;
                                                                    SVE=F”retag,Kund,Kundmall];
                                                   OptionString=Company,Customer,Customer Template }
    { 15  ;   ;Code                ;Code20        ;TableRelation=IF (Type=CONST(Customer)) Customer
                                                                 ELSE IF (Type=CONST(Customer Template)) "Customer Template";
                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod] }
    { 16  ;   ;Department Code     ;Code20        ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   CaptionML=[ENU=Department Code;
                                                              NOR=Avdelingskode;
                                                              SVE=Mottagande avdelningskod];
                                                   CaptionClass='1,2,1' }
    { 17  ;   ;Index Date          ;Date          ;CaptionML=[ENU=Index Date;
                                                              NOR=Indeksdato;
                                                              SVE=Indexdatum] }
    { 20  ;   ;Expired             ;Boolean       ;CaptionML=[ENU=Expired;
                                                              NOR=Utg†tt;
                                                              SVE=Utg†ngna] }
  }
  KEYS
  {
    {    ;Type,Code,Department Code,Index Date    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE CopyFromCompanyLevel@1100528600();
    VAR
      StdSurvSalesSurchVers@1100528603 : Record 11071916;
      StdSurvSalesSurchLine@1100528602 : Record 11071917;
      NewStdSurvSalesSurchLine@1100528604 : Record 11071917;
    BEGIN
      TESTFIELD(Type, Type::Customer);
      StdSurvSalesSurchVers.SETRANGE(Type, Type::Company);
      StdSurvSalesSurchVers.SETRANGE(Code, '');
      StdSurvSalesSurchVers.SETRANGE(Expired, FALSE);
      StdSurvSalesSurchVers.SETRANGE("Department Code", "Department Code");
      StdSurvSalesSurchVers.SETFILTER("Index Date", '<=%1', "Index Date");
      IF StdSurvSalesSurchVers.FINDLAST THEN BEGIN
        StdSurvSalesSurchLine.SETRANGE(Type, StdSurvSalesSurchVers.Type);
        StdSurvSalesSurchLine.SETRANGE(Code, StdSurvSalesSurchVers.Code);
        StdSurvSalesSurchLine.SETRANGE("Index Date", StdSurvSalesSurchVers."Index Date");
        IF StdSurvSalesSurchLine.FINDSET THEN
          REPEAT
            NewStdSurvSalesSurchLine := StdSurvSalesSurchLine;
            NewStdSurvSalesSurchLine.Type := NewStdSurvSalesSurchLine.Type::Customer;
            NewStdSurvSalesSurchLine.Code := Code;
            NewStdSurvSalesSurchLine."Index Date" := "Index Date";
            NewStdSurvSalesSurchLine.INSERT(TRUE);
          UNTIL StdSurvSalesSurchLine.NEXT = 0;
      END;
    END;

    PROCEDURE NewerVersionExists@1100528601() : Boolean;
    VAR
      StdSurvSalesSurchVers@1100528600 : Record 11071916;
    BEGIN
      StdSurvSalesSurchVers := Rec;
      StdSurvSalesSurchVers.SETRECFILTER;
      StdSurvSalesSurchVers.SETFILTER("Index Date", '>%1', "Index Date");
      StdSurvSalesSurchVers.SETRANGE(Expired, FALSE);
      EXIT(NOT StdSurvSalesSurchVers.ISEMPTY);
    END;

    BEGIN
    END.
  }
}

