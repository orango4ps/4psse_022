OBJECT Report 11012777 Transfer Service to Project
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 11012819=imd,
                TableData 11072005=imd;
    CaptionML=[ENU=Transfer Service to Project;
               NOR=Overf›r service til prosjekt];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   ServiceSetup.GET;
                   ServiceSetup.TESTFIELD("Service to Project", ServiceSetup."Service to Project"::"Transfer Cost and Revenue");
                   PostingDate := WORKDATE;
                   ServiceSetup.TESTFIELD("Intern Charge Journal Template");
                   ServiceSetup.TESTFIELD("Intern Charge Journal Batch");
                   GenJournalTemplate.GET(ServiceSetup."Intern Charge Journal Template");
                   GenJournalBatch.GET(GenJournalTemplate.Name, ServiceSetup."Intern Charge Journal Batch");
                   GenJournalBatch.TESTFIELD("No. Series");
                 END;

    OnPostReport=BEGIN
                   IF LedgerEntryCount = 0 THEN
                     MESSAGE(Text000)
                   ELSE
                     MESSAGE(Text001, LedgerEntryCount);
                 END;

  }
  DATASET
  {
    { 1373;    ;DataItem;                    ;
               DataItemTable=Table11012823;
               DataItemTableView=SORTING(No.)
                                 WHERE(Status=FILTER(<Finished));
               OnAfterGetRecord=BEGIN
                                  ServiceOrderExtension.GetServOrderExtension("No.");
                                  IF ServiceOrderExtension."Master Project" = '' THEN
                                    CurrReport.SKIP;
                                  IF (ProjectFilter <> '') AND (ServiceOrderExtension."Master Project" <> ProjectFilter) THEN
                                    CurrReport.SKIP;

                                  DocumentNo := '';
                                END;

               ReqFilterFields=No.,Customer No. }

    { 4723;1   ;DataItem;                    ;
               DataItemTable=Table11012819;
               DataItemTableView=SORTING(Service Order No.,Cost Plus Line No.)
                                 WHERE(Surcharge=CONST(No),
                                       Transfer to Project=CONST(" "));
               OnAfterGetRecord=VAR
                                  GenJournalLine@1100528600 : Record 81;
                                  Job@1100528610 : Record 11072003;
                                  JobsSetup@1100528611 : Record 315;
                                  ProjectElement@1100528612 : Record 11012010;
                                  NoSeriesManagement@1100528605 : Codeunit 396;
                                BEGIN
                                  IF UseOriginalPostingDate THEN
                                    PostingDate := "Posting Date";

                                  IF DocumentNo = '' THEN BEGIN
                                    DocumentNo := NoSeriesManagement.GetNextNo(GenJournalBatch."No. Series", PostingDate, FALSE);

                                    GenJournalLine.LOCKTABLE;
                                    GenJournalLine.SETRANGE("Journal Template Name", GenJournalTemplate.Name);
                                    GenJournalLine.SETRANGE("Journal Batch Name", GenJournalBatch.Name);
                                    IF GenJournalLine.FINDLAST THEN
                                      NextLineNo := GenJournalLine."Line No."
                                    ELSE
                                      NextLineNo := 0;
                                    GenJournalLine.SETRANGE("Document No.", DocumentNo);
                                    IF NOT GenJournalLine.ISEMPTY THEN
                                      ERROR(Text002, GenJournalBatch.Name);
                                  END;

                                  NextLineNo += 1;
                                  GenJournalLine.INIT; //mg, 06-01-2013: T004129
                                  GenJournalLine."Line No." := NextLineNo;
                                  GenJournalLine."Account Type" := GenJournalLine."Account Type"::"G/L Account";
                                  GenJournalLine.VALIDATE("Journal Template Name", ServiceSetup."Intern Charge Journal Template");
                                  GenJournalLine.VALIDATE("Journal Batch Name", ServiceSetup."Intern Charge Journal Batch");
                                  GenJournalLine.VALIDATE("Service Order No.", "Service Order No.");
                                  GenJournalLine.VALIDATE("Shortcut Dimension 2 Code", "Global Dimension 2 Code");
                                  GenJournalLine."Origin Type" := GenJournalLine."Origin Type"::Service;
                                  GenJournalLine."Source Code" := GenJournalTemplate."Source Code";
                                  GenJournalLine."Document No." := DocumentNo;
                                  GenJournalLine.Description := Description;
                                  GenJournalLine."Description 2" := "Description 2";
                                  GenJournalLine."Employee No." := "Employee No."; //C024798.n

                                  GenJournalLine."System-Created Entry" := TRUE;
                                  GenJournalLine."Posting Date" := PostingDate;
                                  IF GenJournalBatch."Bal. Account No." <> '' THEN BEGIN
                                    GenJournalLine."Bal. Account Type" := GenJournalLine."Bal. Account Type"::"G/L Account";
                                    GenJournalLine."Bal. Account No." := GenJournalBatch."Bal. Account No.";
                                  END;
                                  GenJournalLine."Gen. Prod. Posting Group" := '';
                                  GenJournalLine."Gen. Bus. Posting Group" := '';
                                  GenJournalLine."VAT Prod. Posting Group" :='';
                                  GenJournalLine."VAT Bus. Posting Group" := '';
                                  GenJournalLine."Gen. Posting Type" := 0;

                                  GenJournalLine."Interest Date" := PostingDate;
                                  IF "Cost Type" = "Cost Type"::Revenues THEN BEGIN
                                    GenJournalLine.Quantity := Quantity;
                                    GenJournalLine.VALIDATE(Amount, "Total Revenue");
                                  END ELSE BEGIN
                                    GenJournalLine.Quantity := -Quantity;
                                    GenJournalLine.VALIDATE(Amount, -"Total Cost");
                                  END;
                                  GenJournalLine."Unit of Measure Code" := "Unit of Measure Code";
                                  GenJournalLine.INSERT(TRUE);

                                  Job.CHANGECOMPANY(ServiceOrderExtension."Master Company");
                                  JobsSetup.CHANGECOMPANY(ServiceOrderExtension."Master Company");
                                  ProjectElement.CHANGECOMPANY(ServiceOrderExtension."Master Company");

                                  Job.GET(ServiceOrderExtension."Master Project");
                                  JobsSetup.GET;
                                  IF JobsSetup."Posting Element Mandatory" THEN
                                    ProjectElement.GET(ServiceOrderExtension."Master Project", ServiceOrderExtension."Master Element"); //db, 10-09-13: C009364

                                  NextLineNo += 1;
                                  GenJournalLine.INIT; //mg, 06-01-2013: T004129
                                  GenJournalLine."Line No." := NextLineNo;
                                  GenJournalLine."Account Type" := GenJournalLine."Account Type"::"G/L Account";
                                  GenJournalLine.VALIDATE("Journal Template Name", ServiceSetup."Intern Charge Journal Template");
                                  GenJournalLine.VALIDATE("Journal Batch Name", ServiceSetup."Intern Charge Journal Batch");
                                  GenJournalLine."Service Order No." := '';
                                  IF (ServiceOrderExtension."Master Company" <> '') AND (ServiceOrderExtension."Master Company" <> COMPANYNAME) THEN
                                    GenJournalLine.VALIDATE("Receiving Company", ServiceOrderExtension."Master Company");
                                  GenJournalLine.VALIDATE("Job No.", ServiceOrderExtension."Master Project");
                                  GenJournalLine.Element := ServiceOrderExtension."Master Element";
                                  GenJournalLine."Extension Contract" := ServiceOrderExtension."Master Extension Contract";
                                  IF (ServiceSetup."Orig.Cost Object Intern Charge") AND (GenJournalLine."Receiving Company" = '') THEN
                                    GenJournalLine.VALIDATE("Shortcut Dimension 2 Code", "Global Dimension 2 Code")
                                  ELSE BEGIN
                                    IF "Cost Type" = "Cost Type"::Revenues THEN BEGIN
                                      Job.TESTFIELD("Cost Object Inter Project Rev");
                                      GenJournalLine.VALIDATE("Shortcut Dimension 2 Code", Job."Cost Object Inter Project Rev")
                                    END ELSE BEGIN
                                      Job.TESTFIELD("Cost Object Inter Project Cost");
                                      GenJournalLine.VALIDATE("Shortcut Dimension 2 Code", Job."Cost Object Inter Project Cost");
                                    END;
                                  END;
                                  GenJournalLine."Origin Type" := GenJournalLine."Origin Type"::Project;
                                  GenJournalLine."Source Code" := GenJournalTemplate."Source Code";
                                  GenJournalLine."Document No." := DocumentNo;
                                  GenJournalLine.Description := Description;
                                  GenJournalLine."Description 2" := "Description 2";
                                  GenJournalLine."Employee No." := "Employee No."; //C024798.n

                                  GenJournalLine."System-Created Entry" := TRUE;
                                  GenJournalLine."Posting Date" := PostingDate;
                                  IF GenJournalBatch."Bal. Account No." <> '' THEN BEGIN
                                    GenJournalLine."Bal. Account Type" := GenJournalLine."Bal. Account Type"::"G/L Account";
                                    GenJournalLine."Bal. Account No." := GenJournalBatch."Bal. Account No.";
                                  END;
                                  GenJournalLine."Gen. Prod. Posting Group" := '';
                                  GenJournalLine."Gen. Bus. Posting Group" := '';
                                  GenJournalLine."VAT Prod. Posting Group" :='';
                                  GenJournalLine."VAT Bus. Posting Group" := '';
                                  GenJournalLine."Gen. Posting Type" := 0;

                                  GenJournalLine."Interest Date" := PostingDate;
                                  IF "Cost Type" = "Cost Type"::Revenues THEN BEGIN
                                    GenJournalLine.Quantity := -Quantity;
                                    GenJournalLine.VALIDATE(Amount, -"Total Revenue");
                                  END ELSE BEGIN
                                    GenJournalLine.Quantity := Quantity;
                                    GenJournalLine.VALIDATE(Amount, "Total Cost");
                                  END;
                                  GenJournalLine."Unit of Measure Code" := "Unit of Measure Code";
                                  GenJournalLine.INSERT(TRUE);

                                  "Transfer to Project" := "Transfer to Project"::Processed;
                                  MODIFY;

                                  LedgerEntryCount += 1;
                                END;

               OnPostDataItem=VAR
                                GenJournalLine@1100528600 : Record 81;
                                ServiceLedgerEntry@1100528601 : Record 11012819;
                                JobLedgerEntry@1100527300 : Record 11072005;
                              BEGIN
                                GenJournalLine.SETRANGE("Journal Template Name", ServiceSetup."Intern Charge Journal Template");
                                GenJournalLine.SETRANGE("Journal Batch Name", ServiceSetup."Intern Charge Journal Batch");
                                GenJournalLine.SETRANGE("Document No.", DocumentNo);
                                IF GenJournalLine.FINDSET THEN BEGIN
                                  CODEUNIT.RUN(CODEUNIT::"Gen. Jnl.-Post Batch", GenJournalLine);

                                  ServiceLedgerEntry.SETCURRENTKEY("Service Order No.");
                                  ServiceLedgerEntry.SETRANGE("Service Order No.", "Service Order No.");
                                  ServiceLedgerEntry.SETRANGE("Document No.", DocumentNo);
                                  ServiceLedgerEntry.SETRANGE("Transfer to Project", ServiceLedgerEntry."Transfer to Project"::" ");
                                  ServiceLedgerEntry.MODIFYALL("Transfer to Project", ServiceLedgerEntry."Transfer to Project"::Correction);

                                  JobLedgerEntry.RESET;
                                  JobLedgerEntry.SETRANGE("Job No.", ServiceOrderExtension."Master Project");
                                  JobLedgerEntry.SETRANGE("Document No.", DocumentNo);
                                  IF JobLedgerEntry.FINDSET THEN BEGIN
                                    JobLedgerEntry.MODIFYALL("Service Order No.", "Service Order No.");
                                    JobLedgerEntry.MODIFYALL("Service Contract No.", "Service Contract No.");
                                  END;
                                END;
                              END;

               DataItemLink=Service Order No.=FIELD(No.) }

    { 1100528600;1;DataItem;ServiceLedgerEntrySurcharges;
               DataItemTable=Table11012819;
               DataItemTableView=SORTING(Service Order No.,Cost Plus Line No.)
                                 WHERE(Surcharge=CONST(Yes),
                                       Transfer to Project=CONST(" "));
               OnAfterGetRecord=BEGIN
                                  "Transfer to Project" := "Transfer to Project"::Processed;
                                  MODIFY;
                                END;

               DataItemLink=Service Order No.=FIELD(No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   PostingDateEditable := NOT UseOriginalPostingDate;
                 END;

    }
    CONTROLS
    {
      { 1100528600;;Container;
                  CaptionML=[ENU=Options;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  ContainerType=ContentArea }

      { 1100528603;1;Field  ;
                  CaptionML=ENU=Use Original Posting Date;
                  SourceExpr=UseOriginalPostingDate;
                  OnValidate=BEGIN
                               PostingDateEditable := NOT UseOriginalPostingDate;
                             END;
                              }

      { 1100528601;1;Field  ;
                  CaptionML=[ENU=Posting Date;
                             NOR=Bokf›ringsdato;
                             SVE=Bokf”ringsdatum];
                  SourceExpr=PostingDate;
                  Editable=PostingDateEditable }

      { 1100528200;1;Field  ;
                  CaptionML=[ENU=Company Filter;
                             NOR=Selskapsfilter;
                             SVE=F”retagsfilter];
                  SourceExpr=Companyfilter;
                  TableRelation=Company;
                  OnValidate=VAR
                               CheckLicenceIC@1100528200 : Codeunit 11012780;
                             BEGIN
                               IF Companyfilter <> '' THEN
                                 CheckLicenceIC.CheckICEntry(Companyfilter);
                             END;
                              }

      { 1100528602;1;Field  ;
                  CaptionML=[ENU=Project Filter;
                             NOR=Prosjektfilter;
                             SVE=Projektfilter];
                  SourceExpr=ProjectFilter;
                  TableRelation=Job;
                  OnLookup=VAR
                             Job@1100528200 : Record 11072003;
                             PageNo@1100528201 : Integer;
                           BEGIN
                             Job.SETRANGE("Internal Project", FALSE);
                             Job.CHANGECOMPANY(Companyfilter);
                             IF ProjectFilter <> '' THEN
                               IF Job.GET(ProjectFilter) THEN;
                             IF Companyfilter <> '' THEN
                               PageNo := PAGE::"Job List IC";
                             IF PAGE.RUNMODAL(PageNo, Job) = ACTION::LookupOK THEN BEGIN
                               ProjectFilter := Job."No.";

                             END;
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ServiceSetup@1100528601 : Record 11012800;
      ServiceOrderExtension@1100528600 : Record 11071727;
      DocumentNo@1100528602 : Text[30];
      NextLineNo@1100528603 : Integer;
      LedgerEntryCount@1100528604 : Integer;
      Text000@1100528605 : TextConst 'ENU=No Ledger Entries transfered to Project;NOR=Ingen hovedbokoppf›ringer overf›rt til prosjekt';
      Text001@1100528606 : TextConst 'ENU=%1 Ledger Entries transfered to Project;NOR=%1 hovedboktransaksjoner overf›rt til prosjekt';
      Text002@1100528607 : TextConst 'ENU=Entries Exist in Batch %1;NOR=Transaksjoner finnes i batch %1';
      PostingDate@1100528608 : Date;
      ProjectFilter@1100528609 : Code[20];
      UseOriginalPostingDate@1100528610 : Boolean;
      PostingDateEditable@1100528611 : Boolean INDATASET;
      Companyfilter@1100528200 : Text[50];
      GenJournalTemplate@1000 : Record 80;
      GenJournalBatch@1001 : Record 232;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

