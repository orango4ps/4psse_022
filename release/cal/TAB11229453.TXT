OBJECT Table 11229453 Mail Template Table
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CheckRecord;
               AddTableCaptionElementName;
             END;

    OnModify=BEGIN
               CheckRecord;
             END;

    OnDelete=BEGIN
               CALCFIELDS("Table Caption", "Table Name");
               IF (Level = 1) AND ("Parent Table No." = "Table No.") AND ("Table No." <> 0) THEN
                 ERROR(DeleteErrorTxt, FIELDCAPTION("Table No."), "Table Name");
               DeleteDetails;
             END;

    OnRename=BEGIN
               IF (Level = 1) AND ("Parent Table No." = "Table No.") THEN
                 ERROR(RenameErrorTxt, FIELDCAPTION("Table No."), "Table Name");
               xRec.DeleteDetails;
               "Parent Table No." := 0;
               CALCFIELDS("Parent Table Caption", "Parent Table Name");
               "Parent Level" := Level - 1;
             END;

    CaptionML=ENU=Mail Template Table;
    LookupPageID=Page11129764;
  }
  FIELDS
  {
    { 10  ;   ;Mail Template No.   ;Integer       ;TableRelation="Mail Template".No.;
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Mail Template No.;
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 11  ;   ;Mail Template Description;Text100  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Mail Template".Description WHERE (No.=FIELD(Mail Template No.)));
                                                   CaptionML=ENU=Mail Template Description;
                                                   Editable=No }
    { 20  ;   ;Level               ;Integer       ;OnValidate=VAR
                                                                ParentTab@1100528300 : Integer;
                                                              BEGIN
                                                                IF (Level > 1) THEN
                                                                  "Parent Level" := Level - 1;
                                                              END;

                                                   CaptionML=[ENU=Level;
                                                              SVE=Niv†];
                                                   MinValue=1;
                                                   NotBlank=Yes }
    { 30  ;   ;Table No.           ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Table));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Table Name", "Table Caption");
                                                                IF ("Element Prefix" = '') THEN
                                                                  VALIDATE("Element Prefix", "Table Caption");
                                                              END;

                                                   CaptionML=[ENU=Table No.;
                                                              SVE=Tabellnr];
                                                   NotBlank=Yes }
    { 31  ;   ;Table Name          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Name" WHERE (Object Type=CONST(Table),
                                                                                                             Object ID=FIELD(Table No.)));
                                                   CaptionML=[ENU=Table Name;
                                                              SVE=Tabellnamn];
                                                   Editable=No }
    { 32  ;   ;Table Caption       ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table No.)));
                                                   CaptionML=[ENU=Table Caption;
                                                              SVE=Tabellrubrik];
                                                   Editable=No }
    { 40  ;   ;Seq. No.            ;Integer       ;DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Seq. No.;
                                                              SVE=Sekv. nr];
                                                   Editable=No }
    { 100 ;   ;Parent Table No.    ;Integer       ;TableRelation="Mail Template Table"."Table No." WHERE (Mail Template No.=FIELD(Mail Template No.),
                                                                                                          Level=FIELD(Parent Level));
                                                   OnValidate=BEGIN
                                                                IF ("Parent Table No." = "Table No.") THEN
                                                                  FIELDERROR("Parent Table No.");
                                                                CALCFIELDS("Parent Table Name", "Parent Table Caption");
                                                              END;

                                                   OnLookup=VAR
                                                              MailTemplateTable@1100528300 : Record 11229453;
                                                            BEGIN
                                                              MailTemplateTable.SETRANGE("Mail Template No.", "Mail Template No.");
                                                              MailTemplateTable.SETRANGE(Level, "Parent Level");
                                                              IF (PAGE.RUNMODAL(0, MailTemplateTable) = ACTION::LookupOK) THEN  BEGIN
                                                                "Parent Table No." := MailTemplateTable."Table No.";
                                                                "Parent Seq. No." := MailTemplateTable."Seq. No.";
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Parent Table No.;
                                                              SVE=Nr f”r ”verordnad tabell] }
    { 101 ;   ;Parent Table Name   ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Name" WHERE (Object Type=CONST(Table),
                                                                                                             Object ID=FIELD(Parent Table No.)));
                                                   CaptionML=ENU=Parent Table Name;
                                                   Editable=No }
    { 102 ;   ;Parent Table Caption;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Parent Table No.)));
                                                   CaptionML=ENU=Parent Table Caption;
                                                   Editable=No }
    { 103 ;   ;Parent Element Prefix;Text80       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Mail Template Table"."Element Prefix" WHERE (Mail Template No.=FIELD(Mail Template No.),
                                                                                                                    Level=FIELD(Parent Level),
                                                                                                                    Table No.=FIELD(Parent Table No.),
                                                                                                                    Seq. No.=FIELD(Parent Seq. No.)));
                                                   CaptionML=ENU=Parent Element Prefix;
                                                   Editable=No }
    { 110 ;   ;Parent Level        ;Integer       ;DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Parent Level;
                                                   Editable=No }
    { 120 ;   ;Element Prefix      ;Text80        ;OnValidate=BEGIN
                                                                "Element Prefix" := DELCHR("Element Prefix", '<=>', ' -.,()%&');
                                                                UpdateMailTemplateTableFieldElementName("Element Prefix");
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Element Prefix }
    { 130 ;   ;Parent Seq. No.     ;Integer       ;DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Parent Seq. No.;
                                                   Editable=No }
    { 131 ;   ;Links               ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Mail Template Table Link" WHERE (Mail Template No.=FIELD(Mail Template No.),
                                                                                                       Level=FIELD(Level),
                                                                                                       Table No.=FIELD(Table No.),
                                                                                                       Table Seq. No.=FIELD(Seq. No.)));
                                                   CaptionML=[ENU=Links;
                                                              SVE=L„nkar];
                                                   Editable=No }
    { 132 ;   ;Filters             ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Mail Template Table Filter" WHERE (Mail Template No.=FIELD(Mail Template No.),
                                                                                                         Level=FIELD(Level),
                                                                                                         Table No.=FIELD(Table No.),
                                                                                                         Table Seq. No.=FIELD(Seq. No.)));
                                                   CaptionML=[ENU=Filters;
                                                              SVE=Filter];
                                                   Editable=No }
    { 133 ;   ;Fields              ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Mail Template Table Field" WHERE (Mail Template No.=FIELD(Mail Template No.),
                                                                                                        Level=FIELD(Level),
                                                                                                        Table No.=FIELD(Table No.),
                                                                                                        Table Seq. No.=FIELD(Seq. No.),
                                                                                                        Type=CONST(Value)));
                                                   CaptionML=[ENU=Fields;
                                                              SVE=F„lt];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Mail Template No.,Level,Table No.,Seq. No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Table No.,Table Caption,Element Prefix   }
  }
  CODE
  {
    VAR
      MailBodyManagement@1000000004 : Codeunit 11012622;
      AlreadyUserTxt@1100528302 : TextConst 'ENU=%1 %2 is already used in %3.';
      DeleteErrorTxt@1100528300 : TextConst 'ENU=%1 %2 can''t be deleted.';
      RenameErrorTxt@1100528301 : TextConst 'ENU=%1 %2 can''t be renamed.';

    PROCEDURE AddTableCaptionElementName@1100528303();
    VAR
      MailTemplateTableField@1100528300 : Record 11229455;
    BEGIN
      IF (NOT MailTemplateTableField.GET("Mail Template No.", Level, "Table No.", "Seq. No.", 0)) THEN BEGIN
        MailTemplateTableField.INIT;
        MailTemplateTableField."Mail Template No." := "Mail Template No.";
        MailTemplateTableField.Level := Level;
        MailTemplateTableField."Table No." := "Table No.";
        MailTemplateTableField."Table Seq. No." := "Seq. No.";
        MailTemplateTableField."Seq. No." := 0;
        MailTemplateTableField.INSERT;
      END;
      MailTemplateTableField.Type := MailTemplateTableField.Type::TableCaption;
      MailTemplateTableField.VALIDATE("Element Name", STRSUBSTNO('%1_%2', "Element Prefix", MailTemplateTableField.Type));
      MailTemplateTableField.MODIFY;
    END;

    PROCEDURE DeleteDetails@1000000000();
    VAR
      MailTemplateTableLink@1100528302 : Record 11229454;
      MailTemplateTableFilter@1100528301 : Record 11229456;
      MailTemplateTableField@1100528300 : Record 11229455;
    BEGIN
      MailTemplateTableLink.SETRANGE("Mail Template No.", "Mail Template No.");
      MailTemplateTableLink.SETRANGE(Level, Level);
      MailTemplateTableLink.SETRANGE("Table No.", "Table No.");
      MailTemplateTableLink.DELETEALL;

      MailTemplateTableFilter.SETRANGE("Mail Template No.", "Mail Template No.");
      MailTemplateTableFilter.SETRANGE(Level, Level);
      MailTemplateTableFilter.SETRANGE("Table No.", "Table No.");
      MailTemplateTableFilter.DELETEALL;

      MailTemplateTableField.SETRANGE("Mail Template No.", "Mail Template No.");
      MailTemplateTableField.SETRANGE(Level, Level);
      MailTemplateTableField.SETRANGE("Table No.", "Table No.");
      MailTemplateTableField.DELETEALL;
    END;

    PROCEDURE GetFirstParentTable@1100528306(VAR MailTemplateTable@1100528300 : Record 11229453);
    BEGIN
      MailTemplateTable.SETRANGE("Mail Template No.", "Mail Template No.");
      MailTemplateTable.SETRANGE(Level, "Parent Level");
      IF (NOT MailTemplateTable.FINDFIRST) THEN
        MailTemplateTable.INIT;
    END;

    PROCEDURE GetLastSeqNo@1100528301() : Integer;
    VAR
      MailTemplateTable@1100528300 : Record 11229453;
    BEGIN
      MailTemplateTable.SETRANGE("Mail Template No.", "Mail Template No.");
      MailTemplateTable.SETRANGE(Level, Level);
      MailTemplateTable.SETRANGE("Table No.", "Table No.");
      IF (MailTemplateTable.FINDLAST) THEN
        EXIT(MailTemplateTable."Seq. No.");
      EXIT(0);
    END;

    LOCAL PROCEDURE "---Locals---"@1100528304();
    BEGIN
    END;

    LOCAL PROCEDURE CheckRecord@1100528305();
    VAR
      MailTemplateTable@1100528300 : Record 11229453;
    BEGIN
      IF (Level >= 1) THEN
        "Parent Level" := Level - 1;
      IF ("Parent Table No." = 0) AND (Level <> 1) THEN BEGIN
        MailTemplateTable.COPY(Rec);
        MailTemplateTable.GetFirstParentTable(MailTemplateTable);
        "Parent Table No." :=  MailTemplateTable."Table No.";
        "Parent Seq. No." := MailTemplateTable."Seq. No.";
      END;
    END;

    LOCAL PROCEDURE UpdateMailTemplateTableFieldElementName@1100528302(ElementPrefix@1100528301 : Text);
    VAR
      MailTemplateTableField@1100528300 : Record 11229455;
    BEGIN
      MailTemplateTableField.SETRANGE("Mail Template No.", "Mail Template No.");
      MailTemplateTableField.SETRANGE(Level, Level);
      MailTemplateTableField.SETRANGE("Table No.", "Table No.");
      MailTemplateTableField.SETRANGE("Table Seq. No.", "Seq. No.");
      MailTemplateTableField.SETRANGE(Type, MailTemplateTableField.Type::Value);
      IF (MailTemplateTableField.FINDSET(TRUE)) THEN BEGIN
        REPEAT
          MailTemplateTableField.VALIDATE("Element Name");
          MailTemplateTableField.MODIFY;
        UNTIL (MailTemplateTableField.NEXT = 0);
      END;

      MailTemplateTableField.SETFILTER(Type, '<>%1', MailTemplateTableField.Type::Value);
      MailTemplateTableField.DELETEALL;
      AddTableCaptionElementName;

      MailTemplateTableField.SETRANGE(Type, MailTemplateTableField.Type::Value);
      IF (MailTemplateTableField.FINDSET(TRUE)) THEN BEGIN
        REPEAT
          MailTemplateTableField.AddOrUpdateFieldCaptionElementName;
        UNTIL (MailTemplateTableField.NEXT = 0);
      END;
    END;

    BEGIN
    END.
  }
}

