OBJECT Page 11072482 Comment Sheet Subform
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Bemerkungen;
               ENU=Comment Sheet;
               NLD=Opmerkingen;
               NOR=Merknadsark;
               SVE=Kommentarsark];
    MultipleNewLines=Yes;
    SourceTable=Table97;
    DelayedInsert=Yes;
    DataCaptionFields=No.;
    PageType=ListPart;
    AutoSplitKey=Yes;
    OnOpenPage=BEGIN
                 CurrPage.EDITABLE(CommentIsEditable);//**4PS.n
               END;

    OnClosePage=BEGIN
                  //**4PS.sn
                  IF UpdateMain THEN BEGIN
                    CASE "Table Name" OF
                      "Table Name"::Item:
                        BEGIN
                          ItemRec.GET("No.");
                          ItemRec.MODIFY(TRUE);
                        END;
                      "Table Name"::Customer:
                        BEGIN
                          CustRec.GET("No.");
                          CustRec.MODIFY(TRUE);
                        END;
                      "Table Name"::Vendor:
                        BEGIN
                          VendorRec.GET("No.");
                          VendorRec.MODIFY(TRUE);
                        END;
                    END;
                  END;
                  //**4PS.en
                END;

    OnNewRecord=BEGIN
                  SetUpNewLine;
                END;

    OnInsertRecord=BEGIN
                     //**4PS.sn
                     IF NOT CommentIsEditable THEN
                       EXIT(FALSE);
                     UpdateMain := TRUE;
                     //**4PS.en
                   END;

    OnModifyRecord=BEGIN
                     //**4PS.sn
                     IF NOT CommentIsEditable THEN
                       EXIT(FALSE);
                     UpdateMain := TRUE;
                     //**4PS.en
                   END;

    OnDeleteRecord=BEGIN
                     //**4PS.sn
                     IF NOT CommentIsEditable THEN
                       EXIT(FALSE);
                     UpdateMain := TRUE;
                     //**4PS.en
                   END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                SourceExpr=Date }

    { 1100525000;2;Field  ;
                SourceExpr="Time Created";
                Visible=FALSE }

    { 1100485090;2;Field  ;
                SourceExpr="Comment Code" }

    { 6   ;2   ;Field     ;
                SourceExpr=Code;
                OnValidate=BEGIN
                             CodeOnAfterValidate;
                           END;

                OnLookup=VAR
                           TxtRec@1100485000 : Record 7;
                         BEGIN
                           //**4PS
                           TxtRec.Code := Code;
                           IF PAGE.RUNMODAL(PAGE::"Standard Text Codes", TxtRec) = ACTION::LookupOK THEN BEGIN
                             IF  (TxtRec.Code <> Code) AND (TxtRec.Code <> '') THEN BEGIN
                               Code := TxtRec.Code;
                               GetStandardText;
                             END;
                           END;
                         END;
                          }

    { 4   ;2   ;Field     ;
                SourceExpr=Comment }

    { 1100485001;2;Field  ;
                SourceExpr="Created by";
                Visible=FALSE }

  }
  CODE
  {
    VAR
      ItemRec@1100485006 : Record 27;
      CustRec@1100485011 : Record 18;
      VendorRec@1100485012 : Record 23;
      UpdateMain@1100485000 : Boolean;

    PROCEDURE GetStandardText@1210190000();
    VAR
      TxtRec@1210190003 : Record 7;
      LineTxtRec@1210190002 : Record 280;
      CommLineRec@1210190001 : Record 97;
      CurrLino@1210190000 : Integer;
      lvText11012000@1100485001 : TextConst 'DEU="Nicht genÅgend Raum zum ZwischenfÅgen von Textzeilen; nicht alle Textzeilen wurden hinzugefÅgt.";ENU=Not enough space to insert text lines, not all text lines are added.;NLD=Niet voldoende ruimte om tekstregels tussen te voegen, niet alle tekstregels zijn toegevoegd.;NOR=Det finnes ikke tilstrekkelig med plass for Ü sette inn tekstrader. ikke alle tekstrader legges til.;SVE=Det finns inte tillrÑckligt med utrymme fîr att infoga textrader. Inte alla textrader lÑggs till.';
    BEGIN
      //**4PS
      IF Code = '' THEN
        EXIT;

      CurrPage.SAVERECORD;
      CurrLino := "Line No.";
      IF TxtRec.GET(Code) THEN BEGIN
        IF (TxtRec.Description <> '') THEN BEGIN
          Comment := COPYSTR(TxtRec.Description, 1, MAXSTRLEN(Comment));
          MODIFY;
        END;
        LineTxtRec.SETRANGE("Table Name", LineTxtRec."Table Name"::"Standard Text");
        LineTxtRec.SETRANGE("No.", Code);
        IF LineTxtRec.FIND('-') THEN BEGIN
          REPEAT
            CommLineRec := Rec;
            CommLineRec.Comment := COPYSTR(LineTxtRec.Text, 1, MAXSTRLEN(CommLineRec.Comment));
            CommLineRec."Line No." := CurrLino + 1;
            CommLineRec.Date := 0D;
            CommLineRec.Code := '';
            IF NOT CommLineRec.INSERT THEN BEGIN
              MESSAGE(lvText11012000);
              EXIT;
            END;
            CurrLino := CommLineRec."Line No.";
          UNTIL LineTxtRec.NEXT = 0;
        END;
      END;
      CurrPage.UPDATE(TRUE);
    END;

    PROCEDURE CreateCommentsWithTextEditor@1199197400();
    VAR
      TextEditor@1199197400 : Codeunit 11012001;
      CommentLine@1199197401 : Record 97;
    BEGIN
      CommentLine := Rec;
      CommentLine.COPYFILTERS(Rec);
      TextEditor.CommentLine(CommentLine);
    END;

    PROCEDURE SetDocument@1199197401(DocumentNo@1199197400 : Code[20]);
    VAR
      DocumentProperties@1199197401 : Record 11012746;
    BEGIN
      IF DocumentProperties.GET(DocumentNo) THEN BEGIN
        SETRANGE("No.", DocumentNo);
        CurrPage.EDITABLE(TRUE);
        CurrPage.UPDATE(FALSE);
      END ELSE BEGIN
        SETRANGE("No.", '');
        CurrPage.EDITABLE(FALSE);
      END;
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE CommentIsEditable@1100528400() : Boolean;
    VAR
      CentralDataMgt@1100528400 : Codeunit 11012010;
      NavBln@1100528403 : Boolean;
      DiscBln@1100528402 : Boolean;
      DiscGrpBln@1100528300 : Boolean;
      RcpBln@1100528401 : Boolean;
      CompanyRelationsCentralMgt@1100528404 : Record 11012059;
      FPSLicenseManagement@1100528500 : Codeunit 11229289;
    BEGIN
      //**4PS.sn
      IF NOT FPSLicenseManagement.LicenseAndReadPermissionForTable(CompanyRelationsCentralMgt) THEN
        EXIT(FALSE);

      CASE "Table Name" OF
        "Table Name"::Customer:
          EXIT(CentralDataMgt.CustCommentIsEditable("No."));
        "Table Name"::Vendor:
          EXIT(CentralDataMgt.VendorCommentIsEditable("No."));
        "Table Name"::Item:
          BEGIN
            CentralDataMgt.ItemFormEditable(NavBln,DiscBln,DiscGrpBln,RcpBln);
            EXIT(NOT NavBln);
          END;
        ELSE
          EXIT(TRUE);
      END;
      //**4PS.en
    END;

    LOCAL PROCEDURE CodeOnAfterValidate@19050293();
    BEGIN
      //**4PS
      IF Code <> xRec.Code THEN
        GetStandardText;
    END;

    BEGIN
    END.
  }
}

