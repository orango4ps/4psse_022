OBJECT Table 11125823 BIM Model
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 BimSetup.GET;
                 BimSetup.TESTFIELD("BIM Model Nos.");
                 NoSeriesMgt.InitSeries(BimSetup."BIM Model Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;
             END;

    OnDelete=VAR
               BimModelLink@1100525000 : Record 11125824;
               BimModelUserDefaults@1100525001 : Record 11125825;
               BimModelIfcFile@1100529000 : Record 11125827;
             BEGIN
               BimModelLink.SETRANGE("BIM Model No.", "No.");
               BimModelLink.DELETEALL;

               BimModelUserDefaults.SETRANGE("BIM Model No.", "No.");
               BimModelUserDefaults.DELETEALL;

               BimModelIfcFile.SETRANGE("BIM Model No.", "No.");
               BimModelIfcFile.DELETEALL;
             END;

    CaptionML=ENU=BIM Model;
    LookupPageID=Page11130582;
  }
  FIELDS
  {
    { 10  ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  BimSetup.GET;
                                                                  NoSeriesMgt.TestManual(BimSetup."BIM Model Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              NOR=Nr;
                                                              SVE=Nr] }
    { 20  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 30  ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=ENU=Development,Realization,Finished;
                                                   OptionString=Development,Realization,Finished }
    { 50  ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              NOR=Nr-serie;
                                                              SVE=Nr-serie];
                                                   Editable=No }
    { 60  ;   ;Service Location Filter;Code20     ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Service Location Filter;
                                                              NOR=Servicelokasjonsfilter;
                                                              SVE=Serviceplatsfilter];
                                                   Editable=No }
    { 70  ;   ;Project Filter      ;Code20        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Project Filter;
                                                              NOR=Prosjektfilter;
                                                              SVE=Projektfilter];
                                                   Editable=No }
    { 80  ;   ;Plant Location Filter;Code20       ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Plant Location Filter;
                                                              SVE=Maskinplatsfilter];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      BimSetup@1100525000 : Record 11125822;
      NoSeriesMgt@1100525001 : Codeunit 396;

    PROCEDURE AssistEdit@2(OldBimModel@11012000 : Record 11125823) : Boolean;
    VAR
      BimModel@1100525000 : Record 11125823;
    BEGIN
      WITH BimModel DO BEGIN
        BimModel := Rec;
        BimSetup.GET;
        BimSetup.TESTFIELD("BIM Model Nos.");
        IF NoSeriesMgt.SelectSeries(BimSetup."BIM Model Nos.",OldBimModel."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("No.");
          Rec := BimModel;
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE RunViewer@1100525001();
    VAR
      BimModel@1100525000 : Record 11125823;
      BimModelIfcFileFilters@1100529001 : Record 11125827;
      BimViewer@1100529000 : Page 11130583;
    BEGIN
      TESTFIELD("No.");

      BimModel.FILTERGROUP(9);
      BimModel.SETRANGE("No.", "No.");
      BimModel.FILTERGROUP(0);
      BimModel := Rec;

      BimModelIfcFileFilters.SETRANGE("BIM Model No.", "No.");
      BimModelIfcFileFilters.SETRANGE(Status, BimModelIfcFileFilters.Status::Active);
      BimModelIfcFileFilters.SETRANGE(Load, TRUE);
      BimModelIfcFileFilters.FINDFIRST;

      BimViewer.SETTABLEVIEW(BimModel);
      BimViewer.SETRECORD(BimModel);
      BimViewer.SetIfcFileSelection(BimModelIfcFileFilters, DATABASE::"BIM Model");
      BimViewer.RUNMODAL;
    END;

    PROCEDURE BimModelIfcFileNames@1100529001() FileNames : Text;
    VAR
      BimModelIfcFile@1100525000 : Record 11125827;
      FileMgt@1100529001 : Codeunit 419;
      Counter1@1100525001 : Integer;
      Counter2@1100529000 : Integer;
      Directory@1100529002 : Text;
      Filename@1100529003 : Text;
    BEGIN
      IF "No." <> '' THEN BEGIN
        BimModelIfcFile.SETRANGE("BIM Model No.", "No.");
        IF NOT BimModelIfcFile.ISEMPTY THEN BEGIN
          BimModelIfcFile.SETRANGE(Status, BimModelIfcFile.Status::Active);
          Counter1 := BimModelIfcFile.COUNT;
          BimModelIfcFile.SETRANGE(Load, TRUE);
          Counter2 := BimModelIfcFile.COUNT;
          IF Counter2 > 0 THEN BEGIN
            BimModelIfcFile.FINDSET;
            REPEAT
              IF FileNames <> '' THEN
                FileNames := FileNames + ', ';
              FileMgt.SplitFileAbsPathName(BimModelIfcFile."IFC File", Directory, Filename);
              FileNames := FileNames + Filename;
            UNTIL BimModelIfcFile.NEXT = 0;
          END;
          IF Counter1 > Counter2 THEN
            FileNames := STRSUBSTNO('[%1-%2]  ', Counter2, Counter1) + FileNames;
        END;
      END;
    END;

    PROCEDURE ShowBimModelIfcFiles@1100529000(EditMode@1100525002 : Boolean);
    VAR
      BimModelIfcFile@1100525000 : Record 11125827;
      BimModelIfcFiles@1100525001 : Page 11130592;
    BEGIN
      TESTFIELD("No.");
      BimModelIfcFile.FILTERGROUP(9);
      BimModelIfcFile.SETRANGE("BIM Model No.", "No.");
      BimModelIfcFile.FILTERGROUP(0);
      BimModelIfcFiles.EDITABLE(EditMode);
      BimModelIfcFiles.SETTABLEVIEW(BimModelIfcFile);
      BimModelIfcFiles.RUNMODAL;
    END;

    PROCEDURE BimModelLinkTypeOptionCaption@1100525003() : Text;
    VAR
      BimModelLink@1100525000 : Record 11125824;
    BEGIN
      IF "No." <> '' THEN BEGIN
        BimModelLink.SETRANGE("BIM Model No.", "No.");
        IF BimModelLink.FINDFIRST THEN BEGIN
          BimModelLink.SETFILTER(Type, '<>%1', BimModelLink.Type);
          IF BimModelLink.ISEMPTY THEN
            EXIT(FORMAT(BimModelLink.Type));
        END;
      END;
    END;

    PROCEDURE BimModelLinkNo@1100525002() : Text;
    VAR
      BimModelLink@1100525000 : Record 11125824;
      Counter@1100525001 : Integer;
    BEGIN
      IF "No." <> '' THEN BEGIN
        BimModelLink.SETRANGE("BIM Model No.", "No.");
        IF BimModelLink.FINDFIRST THEN BEGIN
          Counter := BimModelLink.COUNT;
          IF Counter = 1 THEN
            EXIT(BimModelLink."No.");
          IF Counter > 1 THEN
            EXIT(STRSUBSTNO('[%1]', Counter));
        END;
      END;
    END;

    PROCEDURE ShowBimModelLinks@1100525004(EditMode@1100525002 : Boolean);
    VAR
      BimModelLink@1100525000 : Record 11125824;
      BimModelLinks@1100525001 : Page 11130584;
    BEGIN
      TESTFIELD("No.");
      BimModelLink.FILTERGROUP(9);
      BimModelLink.SETRANGE("BIM Model No.", "No.");
      BimModelLink.FILTERGROUP(0);
      BimModelLinks.EDITABLE(EditMode);
      BimModelLinks.SETTABLEVIEW(BimModelLink);
      BimModelLinks.RUNMODAL;
    END;

    PROCEDURE ShowDocuments@1100529400();
    VAR
      RecRef@1100525000 : RecordRef;
      DocumentFilters@1100525001 : Codeunit 11012432;
    BEGIN
      TESTFIELD("No.");
      RecRef.GETTABLE(Rec);
      DocumentFilters.CreateTempFilterAndExecute1(RecRef, '', FALSE, TRUE);
    END;

    PROCEDURE NumberOfDocumentLinks@1100525006() : Integer;
    VAR
      RecRef@1100529400 : RecordRef;
      DocumentLinkMgt@1100525000 : Codeunit 11012401;
    BEGIN
      RecRef.GETTABLE(Rec);
      EXIT(DocumentLinkMgt.EntityNumberOfDocumentLinks(RecRef));
    END;

    BEGIN
    END.
  }
}

