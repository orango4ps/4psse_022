OBJECT Report 11012316 Check GTIN Code
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Produktbeziehung aufgrund GTIN-Code ÅberprÅfen;
               ENU=Check Product Relation by GTIN Code;
               NLD=Productrelatie op basis van GTIN-code controleren;
               NOR=Kontroller produktrelasjon etter GTIN-kod;
               SVE=Kontrollera produktrelation efter GTIN-kod];
    ProcessingOnly=Yes;
    UsageCategory=Tasks;
  }
  DATASET
  {
    { 8148;    ;DataItem;                    ;
               DataItemTable=Table11012316;
               OnPreDataItem=BEGIN
                               IF CheckBasicItem = FALSE THEN CurrReport.BREAK;
                               status.OPEN(Text002);
                               NoOfLines := COUNT;
                               StatusCounter := 0;
                             END;

               OnAfterGetRecord=BEGIN
                                  StatusCounter := StatusCounter+1;
                                  status.UPDATE(1,ROUND(StatusCounter/NoOfLines*10000,1));

                                  CheckGTINCodeBasicItem("Basic Item");
                                END;

               OnPostDataItem=BEGIN
                                IF CheckBasicItem = TRUE THEN
                                  status.CLOSE;
                              END;

               ReqFilterFields=Manufacturer,Product Code }

    { 4881;    ;DataItem;                    ;
               DataItemTable=Table11012317;
               OnPreDataItem=BEGIN
                               IF CheckTradeItem = FALSE THEN CurrReport.BREAK;
                               status.OPEN(Text001);
                               NoOfLines := COUNT;
                               StatusCounter := 0;
                             END;

               OnAfterGetRecord=BEGIN
                                  StatusCounter := StatusCounter+1;
                                  status.UPDATE(1,ROUND(StatusCounter/NoOfLines*10000,1));

                                  CheckGTINCodeTradeItem("Trade Item");
                                END;

               OnPostDataItem=BEGIN
                                IF CheckTradeItem = TRUE THEN
                                  status.CLOSE;
                              END;

               ReqFilterFields=Vendor,Item Code,Manufacturer,Product Code,Import Date-Time }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1210190000;;Container;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  ContainerType=ContentArea }

      { 1210190001;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1210190002;2;Field  ;
                  CaptionML=[DEU=Filter auf Basisartikel anwenden;
                             ENU=Use Filter Basic Item;
                             NLD=Filter op basisartikel toepassen;
                             NOR=Bruk filter for grunnartikkel;
                             SVE=AnvÑnd filtrering av basartikel];
                  SourceExpr=CheckBasicItem }

      { 1210190003;2;Field  ;
                  CaptionML=[DEU=Filter auf Handelsartikel anwenden;
                             ENU=Use Filter Trade Item;
                             NLD="Filter op handelsartikel toepassen ";
                             NOR="Bruk filter for katalogartikkel ";
                             SVE=AnvÑnd filtrering av skaffningsartikel];
                  SourceExpr=CheckTradeItem }

      { 1210190004;2;Field  ;
                  CaptionML=[DEU=Wildcard anwenden;
                             ENU=Use Wild Card;
                             NLD=Wildcard toepassen;
                             NOR=Bruk jokertegn;
                             SVE=AnvÑnd jokertecken];
                  SourceExpr=UseWildCard }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      status@1100485002 : Dialog;
      NoOfLines@1100485001 : Integer;
      StatusCounter@1100485000 : Integer;
      Text001@1100485003 : TextConst 'DEU=Aktualisieren Handelsartikel @1@@@@@@@@@@@@@@@@@@;ENU=Update Trade Item @1@@@@@@@@@@@@@@@@@@;NLD=Bijwerken handelsartikel @1@@@@@@@@@@@@@@@@@@;NOR=Oppdater katalogartikkel @1@@@@@@@@@@@@@@@@@@;SVE=Uppdatera skaffningsartikel @1@@@@@@@@@@@@@@@@@@';
      UseWildCard@1100485004 : Boolean;
      CheckBasicItem@1100485005 : Boolean;
      CheckTradeItem@1100485006 : Boolean;
      Text002@1100485007 : TextConst 'DEU=Aktualisieren Basisartikel @1@@@@@@@@@@@@@@@@@@;ENU=Update Basic Item @1@@@@@@@@@@@@@@@@@@;NLD=Bijwerken basisartikel @1@@@@@@@@@@@@@@@@@@;NOR=Oppdater grunnartikkel @1@@@@@@@@@@@@@@@@@@;SVE=Uppdatera basartikel @1@@@@@@@@@@@@@@@@@@';
      FilterGTIN@1100485009 : Text[30];
      OrigGTIN@1100485008 : Text[30];

    PROCEDURE CheckGTINCodeBasicItem@1100485000(BasicItemRec@1100485000 : Record 11012316);
    VAR
      BasicItemRec2@1100485005 : Record 11012316;
      TradeItemRec@1100485001 : Record 11012317;
      TradeItemRec2@1100485003 : Record 11012317;
      SameGTIN@1100485006 : Boolean;
    BEGIN
      WITH TradeItemRec DO BEGIN
        OrigGTIN := BasicItemRec."GTIN Code";
        IF STRLEN(OrigGTIN) < 13 THEN EXIT;
        IF UseWildCard THEN
          FilterGTIN := COPYSTR(OrigGTIN, 1, STRLEN(OrigGTIN) - 2) + '*'
        ELSE
          FilterGTIN := OrigGTIN;

        RESET;
        SETCURRENTKEY("GTIN Code (Item)");
        SETFILTER("GTIN Code (Item)", FilterGTIN);
        IF FINDSET(TRUE, FALSE) THEN BEGIN
          REPEAT
            SameGTIN := TRUE;
            IF TradeItemRec."GTIN Code (Item)" <> BasicItemRec."GTIN Code" THEN BEGIN
               BasicItemRec2.SETCURRENTKEY("GTIN Code");
               BasicItemRec2.SETRANGE("GTIN Code", TradeItemRec."GTIN Code (Item)");
               IF BasicItemRec2.FINDFIRST THEN SameGTIN := FALSE;
            END;
            IF SameGTIN = TRUE THEN BEGIN
              IF NOT BasicItemRec2.GET(Manufacturer, "Product Code") THEN BEGIN
                //alleen onbekende/onvolledige verwijzing herschrijven
                TradeItemRec2 := TradeItemRec;
                TradeItemRec2.Manufacturer := BasicItemRec.Manufacturer;
                TradeItemRec2."Product Code" := BasicItemRec."Product Code";
                TradeItemRec2.MODIFY(TRUE);
              END;
            END;
          UNTIL NEXT = 0;
        END;
      END;
    END;

    PROCEDURE CheckGTINCodeTradeItem@1100485001(VAR TradeItemRec@1100485000 : Record 11012317);
    VAR
      BasicItemRec@1100485001 : Record 11012316;
      BasicItemRec2@1100485005 : Record 11012316;
      TradeItemRec2@1100485002 : Record 11012317;
    BEGIN
      WITH TradeItemRec DO BEGIN
        OrigGTIN := "GTIN Code (Item)";
        IF STRLEN(OrigGTIN) < 13 THEN EXIT;
        IF UseWildCard THEN
          FilterGTIN := COPYSTR(OrigGTIN, 1, STRLEN(OrigGTIN) - 2) + '*'
        ELSE
          FilterGTIN := OrigGTIN;

        IF BasicItemRec.GET(Manufacturer, "Product Code") THEN
          EXIT;  //alleen onbekende/onvolledige verwijzing herschrijven

        BasicItemRec2.SETCURRENTKEY("GTIN Code");
        BasicItemRec2.SETRANGE("GTIN Code", OrigGTIN);
        IF BasicItemRec2.FINDFIRST THEN BEGIN
          TradeItemRec2 := TradeItemRec;  //db.sn, 07-03-07
          TradeItemRec2.Manufacturer := BasicItemRec2.Manufacturer;
          TradeItemRec2."Product Code" := BasicItemRec2."Product Code";
          TradeItemRec2.MODIFY(TRUE);     //db.en, 07-03-07
        END ELSE BEGIN
          IF UseWildCard THEN BEGIN
            BasicItemRec2.SETFILTER("GTIN Code", FilterGTIN);
            IF BasicItemRec2.FINDFIRST THEN BEGIN
              TradeItemRec2 := TradeItemRec;  //db.sn, 07-03-07
              TradeItemRec2.Manufacturer := BasicItemRec2.Manufacturer;
              TradeItemRec2."Product Code" := BasicItemRec2."Product Code";
              TradeItemRec2.MODIFY(TRUE);     //db.en, 07-03-07
            END;
          END;
        END;
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

