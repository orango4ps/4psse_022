OBJECT Table 11012059 Company Relations Central Mgt
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnDelete=BEGIN
               //X016.sn
               IF "Company Contact Data" <> '' THEN
                 DeleteLocalSetup(DATABASE::"Marketing Setup", "Company Contact Data");
               IF "Company Item Data" <> '' THEN
                 DeleteLocalSetup(DATABASE::"Inventory Setup", "Company Item Data");
               IF "Company Finance Data" <> '' THEN
                 DeleteLocalSetup(DATABASE::"General Ledger Setup", "Company Finance Data");
               //X016.en
             END;

    CaptionML=[ENU=Company Relations Central Management;
               NOR=Selskapsrelasjoner (sentral ledelse);
               SVE=Fîretagets centrala relationshantering];
    LookupPageID=Page11125007;
    DrillDownPageID=Page11125007;
  }
  FIELDS
  {
    { 10  ;   ;Company             ;Text30        ;TableRelation=Company.Name;
                                                   CaptionML=[ENU=Company;
                                                              NOR=Selskap;
                                                              SVE=Fîretag];
                                                   NotBlank=Yes }
    { 60  ;   ;Company Contact Data;Text30        ;TableRelation=Company.Name;
                                                   CaptionML=[ENU=Company Contact Data;
                                                              NOR=Selskapskontaktdata;
                                                              SVE=Fîretagskontaktdata] }
    { 70  ;   ;Company Item Data   ;Text30        ;TableRelation=Company.Name;
                                                   CaptionML=[ENU=Company Item Data;
                                                              NOR=Selskapsartikkeldata;
                                                              SVE=Fîretagsartikeldata] }
    { 80  ;   ;Company Finance Data;Text30        ;TableRelation=Company.Name;
                                                   CaptionML=[ENU=Company Finance Data;
                                                              NOR=Selskapets ùkonomiske data;
                                                              SVE=Ekonomiska fîretagsdata] }
    { 90  ;   ;Company Service Data;Text30        ;TableRelation=Company.Name;
                                                   CaptionML=[ENU=Company Service Data;
                                                              NOR=Selskapets Servicedata;
                                                              SVE=Fîretagets servicedata] }
  }
  KEYS
  {
    {    ;Company                                 ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1210190000 : TextConst 'ENU=Select Leading Company;NOR=Velg Ledende selskap;SVE=VÑlj ledande fîretag';

    PROCEDURE UpdateCompanyRelation@1100525004(TableNo@1100525000 : Integer;LeadingCompany@1100525001 : Text[30]);
    VAR
      CompRelationRec@1100525002 : Record 11012059;
      CompRelationRec2@1210190001 : Record 11012059;
      CompRec@1210190000 : Record 2000000006;
      NumComp@1210190002 : Integer;
    BEGIN
      //X016
      IF LeadingCompany <> '' THEN BEGIN
        IF NOT CompRelationRec.GET(COMPANYNAME) THEN BEGIN
          CompRelationRec.INIT;
          CompRelationRec.Company := COMPANYNAME;
          CompRelationRec.INSERT;
        END;
        IF NOT CompRec.GET(LeadingCompany) THEN BEGIN
          CompRelationRec2.SETFILTER(Company, '<>%1', COMPANYNAME);
          CASE TableNo OF
            DATABASE::"General Ledger Setup":
              CompRelationRec2.SETFILTER("Company Finance Data", '<>%1', '');
            DATABASE::"Inventory Setup":
              CompRelationRec2.SETFILTER("Company Item Data", '<>%1', '');
            DATABASE::"Marketing Setup":
              CompRelationRec2.SETFILTER("Company Contact Data", '<>%1', '');
            DATABASE::"Service Setup":
              CompRelationRec2.SETFILTER("Company Service Data", '<>%1', '');
          END;
          IF CompRelationRec2.FINDFIRST THEN BEGIN
            REPEAT
              CASE TableNo OF
                DATABASE::"General Ledger Setup":
                  BEGIN
                    IF CompRelationRec2."Company Finance Data" = CompRelationRec2.Company THEN BEGIN
                      LeadingCompany := CompRelationRec2.Company;
                      NumComp := NumComp + 1;
                    END;
                  END;
                DATABASE::"Inventory Setup":
                  BEGIN
                    IF CompRelationRec2."Company Item Data" = CompRelationRec2.Company THEN BEGIN
                      LeadingCompany := CompRelationRec2.Company;
                      NumComp := NumComp + 1;
                    END;
                  END;
                DATABASE::"Marketing Setup":
                  BEGIN
                    IF CompRelationRec2."Company Contact Data" = CompRelationRec2.Company THEN BEGIN
                      LeadingCompany := CompRelationRec2.Company;
                      NumComp := NumComp + 1;
                    END;
                  END;
                DATABASE::"Service Setup":
                  BEGIN
                    IF CompRelationRec2."Company Service Data" = CompRelationRec2.Company THEN BEGIN
                      LeadingCompany := CompRelationRec2.Company;
                      NumComp := NumComp + 1;
                    END;
                  END;
              END;
            UNTIL CompRelationRec2.NEXT = 0;
            IF NumComp <> 1 THEN
              LeadingCompany := Text000;
          END ELSE BEGIN
            LeadingCompany := COMPANYNAME;
          END;
        END;
        CASE TableNo OF
          DATABASE::"General Ledger Setup":
            CompRelationRec."Company Finance Data" := LeadingCompany;
          DATABASE::"Inventory Setup":
            CompRelationRec."Company Item Data" := LeadingCompany;
          DATABASE::"Marketing Setup":
            CompRelationRec."Company Contact Data" := LeadingCompany;
          DATABASE::"Service Setup":
            CompRelationRec."Company Service Data" := LeadingCompany;
        END;
        CompRelationRec.MODIFY;
      END ELSE BEGIN
        IF CompRelationRec.GET(COMPANYNAME) THEN BEGIN
          CASE TableNo OF
            DATABASE::"General Ledger Setup":
              CompRelationRec."Company Finance Data" := LeadingCompany;
            DATABASE::"Inventory Setup":
              CompRelationRec."Company Item Data" := LeadingCompany;
            DATABASE::"Marketing Setup":
              CompRelationRec."Company Contact Data" := LeadingCompany;
            DATABASE::"Service Setup":
              CompRelationRec."Company Service Data" := LeadingCompany;
          END;
          IF (CompRelationRec."Company Finance Data" <> '') OR
             (CompRelationRec."Company Item Data" <> '') OR
             (CompRelationRec."Company Contact Data" <> '') OR
             (CompRelationRec."Company Service Data" <> '') THEN
            CompRelationRec.MODIFY
          ELSE
            CompRelationRec.DELETE;
        END;
      END;
    END;

    PROCEDURE GetLeadingCompany@1210190000(TableNo@1210190001 : Integer;CentralMgt@1210190005 : Boolean) : Text[50];
    VAR
      CompRelationRec@1210190000 : Record 11012059;
    BEGIN
      //X016
      IF NOT CompRelationRec.GET(COMPANYNAME) THEN CompRelationRec.INIT;
      CASE TableNo OF
        DATABASE::"Marketing Setup":
          BEGIN
            IF CentralMgt THEN BEGIN
              IF CompRelationRec."Company Contact Data" = '' THEN
                EXIT(Text000)
              ELSE
                EXIT(CompRelationRec."Company Contact Data");
             END;
          END;
        DATABASE::"Inventory Setup":
          BEGIN
            IF CentralMgt THEN BEGIN
              IF CompRelationRec."Company Item Data" = '' THEN
                EXIT(Text000)
              ELSE
                EXIT(CompRelationRec."Company Item Data");
            END;
          END;
        DATABASE::"General Ledger Setup":
          BEGIN
            IF CentralMgt THEN BEGIN
              IF CompRelationRec."Company Finance Data" = '' THEN
                EXIT(Text000)
              ELSE
                EXIT(CompRelationRec."Company Finance Data");
            END;
          END;
        DATABASE::"Service Setup":
          BEGIN
            IF CentralMgt THEN BEGIN
              IF CompRelationRec."Company Service Data" = '' THEN
                EXIT(Text000)
              ELSE
                EXIT(CompRelationRec."Company Service Data");
            END;
          END;

      END;
      EXIT('');
    END;

    PROCEDURE DeleteLocalSetup@1210190001(TableNo@1210190001 : Integer;CompNameOld@1210190003 : Text[50]);
    VAR
      MarketingSetup@1210190006 : Record 5079;
      InventSetup@1210190000 : Record 313;
      GenLedgerSetup@1210190005 : Record 98;
      ServiceSetup@1100528600 : Record 11012800;
      CompRelationRec@1210190004 : Record 11012059;
      CompRelationRec2@1210190002 : Record 11012059;
    BEGIN
      //X016
      CASE TableNo OF
        DATABASE::"Marketing Setup":
          BEGIN
            MarketingSetup.CHANGECOMPANY(Company);
            MarketingSetup.GET;
            MarketingSetup.InitFlagsCentralMgt;
            MarketingSetup.MODIFY;
          END;
        DATABASE::"Inventory Setup":
          BEGIN
            InventSetup.CHANGECOMPANY(Company);
            InventSetup.GET;
            InventSetup.InitFlagsCentralMgt;
            InventSetup.MODIFY;
          END;
        DATABASE::"General Ledger Setup":
          BEGIN
            GenLedgerSetup.CHANGECOMPANY(Company);
            GenLedgerSetup.GET;
            GenLedgerSetup.InitFlagsCentralMgt;
            GenLedgerSetup.MODIFY;
          END;
        DATABASE::"Service Setup":
          BEGIN
            ServiceSetup.CHANGECOMPANY(Company);
            ServiceSetup.GET;
            ServiceSetup.InitFlagsCentralMgt;
            ServiceSetup.MODIFY;
          END;
      END;
      IF Company <> CompNameOld THEN EXIT;

      CompRelationRec.SETFILTER(Company, '<>%1', Company);
      CompRelationRec.SETRANGE("Company Item Data", CompNameOld);
      IF CompRelationRec.FINDSET(TRUE,TRUE) THEN BEGIN
        REPEAT
          CASE TableNo OF
            DATABASE::"Marketing Setup":
              BEGIN
                MarketingSetup.CHANGECOMPANY(CompRelationRec.Company);
                MarketingSetup.GET;
                MarketingSetup.InitFlagsCentralMgt;
                MarketingSetup.MODIFY;

                CompRelationRec2 := CompRelationRec;
                CompRelationRec2."Company Contact Data" := '';
                CompRelationRec2.MODIFY;
              END;
            DATABASE::"Inventory Setup":
              BEGIN
                InventSetup.CHANGECOMPANY(CompRelationRec.Company);
                InventSetup.GET;
                InventSetup.InitFlagsCentralMgt;
                InventSetup.MODIFY;

                CompRelationRec2 := CompRelationRec;
                CompRelationRec2."Company Item Data" := '';
                CompRelationRec2.MODIFY;
              END;
            DATABASE::"General Ledger Setup":
              BEGIN
                GenLedgerSetup.CHANGECOMPANY(CompRelationRec.Company);
                GenLedgerSetup.GET;
                GenLedgerSetup.InitFlagsCentralMgt;
                GenLedgerSetup.MODIFY;

                CompRelationRec2 := CompRelationRec;
                CompRelationRec2."Company Finance Data" := '';
                CompRelationRec2.MODIFY;
              END;
            DATABASE::"Service Setup":
              BEGIN
                ServiceSetup.CHANGECOMPANY(CompRelationRec.Company);
                ServiceSetup.GET;
                ServiceSetup.InitFlagsCentralMgt;
                ServiceSetup.MODIFY;

                CompRelationRec2 := CompRelationRec;
                CompRelationRec2."Company Service Data" := '';
                CompRelationRec2.MODIFY;
              END;
          END;
        UNTIL CompRelationRec.NEXT = 0;
      END;
    END;

    PROCEDURE CopyLocalSetup@1100485000(TableNo@1210190003 : Integer;CompName@1210190001 : Text[50]);
    VAR
      MarketingSetup1@1210190007 : Record 5079;
      MarketingSetup2@1210190008 : Record 5079;
      InventSetup1@1210190000 : Record 313;
      InventSetup2@1210190002 : Record 313;
      GenLedgerSetup1@1210190005 : Record 98;
      GenLedgerSetup2@1210190006 : Record 98;
      ServiceSetup1@1100528600 : Record 11012800;
      ServiceSetup2@1100528601 : Record 11012800;
      CompRelationRec@1210190004 : Record 11012059;
    BEGIN
      //X016
      CompRelationRec.SETRANGE(Company, CompName);
      IF CompRelationRec.FINDSET THEN BEGIN
        REPEAT
          CASE TableNo OF
            DATABASE::"Marketing Setup":
              BEGIN
                MarketingSetup2.CHANGECOMPANY(CompRelationRec."Company Contact Data");
                MarketingSetup2.GET;
                MarketingSetup1.CHANGECOMPANY(CompRelationRec.Company);
                MarketingSetup1.GET;
                IF MarketingSetup1."Central Management" THEN BEGIN
                  MarketingSetup1.CopyFlagsCentralMgt(MarketingSetup1, MarketingSetup2);
                  MarketingSetup1.MODIFY;
                END;
              END;
            DATABASE::"Inventory Setup":
              BEGIN
                InventSetup2.CHANGECOMPANY(CompRelationRec."Company Item Data");
                InventSetup2.GET;
                InventSetup1.CHANGECOMPANY(CompRelationRec.Company);
                InventSetup1.GET;
                IF InventSetup1."Central Management" THEN BEGIN
                  InventSetup1.CopyFlagsCentralMgt(InventSetup1, InventSetup2);
                  InventSetup1.MODIFY;
                END;
              END;
            DATABASE::"General Ledger Setup":
              BEGIN
                GenLedgerSetup2.CHANGECOMPANY(CompRelationRec."Company Finance Data");
                GenLedgerSetup2.GET;
                GenLedgerSetup1.CHANGECOMPANY(CompRelationRec.Company);
                GenLedgerSetup1.GET;
                IF GenLedgerSetup1."Central Management" THEN BEGIN
                  GenLedgerSetup1.CopyFlagsCentralMgt(GenLedgerSetup1, GenLedgerSetup2);
                  GenLedgerSetup1.MODIFY;
                END;
              END;
            DATABASE::"Service Setup":
              BEGIN
                ServiceSetup2.CHANGECOMPANY(CompRelationRec."Company Service Data");
                ServiceSetup2.GET;
                ServiceSetup1.CHANGECOMPANY(CompRelationRec.Company);
                ServiceSetup1.GET;
                IF ServiceSetup1."Central Management" THEN BEGIN
                  ServiceSetup1.CopyFlagsCentralMgt(ServiceSetup1, ServiceSetup2);
                  ServiceSetup1.MODIFY;
                END;
              END;
          END;
        UNTIL CompRelationRec.NEXT = 0;
      END;
    END;

    BEGIN
    {
      X016: Multiple central management companies for 1 operating company (db, 22-08-11)
    }
    END.
  }
}

