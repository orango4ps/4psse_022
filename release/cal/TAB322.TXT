OBJECT Table 322 Tax Detail
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               TaxJurisdiction@1000 : Record 320;
             BEGIN
               TaxJurisdiction.GET("Tax Jurisdiction Code");
               "Calculate Tax on Tax" := TaxJurisdiction."Calculate Tax on Tax";
             END;

    OnRename=VAR
               TaxJurisdiction@1000 : Record 320;
             BEGIN
               TaxJurisdiction.GET("Tax Jurisdiction Code");
               "Calculate Tax on Tax" := TaxJurisdiction."Calculate Tax on Tax";
             END;

    CaptionML=[ENU=Tax Detail;
               NOR=Mva-detalj;
               SVE=Skatteuppgift];
  }
  FIELDS
  {
    { 1   ;   ;Tax Jurisdiction Code;Code10       ;TableRelation="Tax Jurisdiction";
                                                   CaptionML=[ENU=Tax Jurisdiction Code;
                                                              NOR=Mva-jurisdiksjonskode;
                                                              SVE=Skattemyndighetskod];
                                                   NotBlank=Yes }
    { 2   ;   ;Tax Group Code      ;Code20        ;TableRelation="Tax Group";
                                                   CaptionML=[ENU=Tax Group Code;
                                                              NOR=Mva-gruppekode;
                                                              SVE=Skattegruppskod] }
    { 3   ;   ;Tax Type            ;Option        ;CaptionML=[ENU=Tax Type;
                                                              NOR=Mva-type;
                                                              SVE=Skattetyp];
                                                   OptionCaptionML=[ENU=Sales Tax,Excise Tax;
                                                                    NOR=Salgsmva.,Forbrukeravgift;
                                                                    SVE=Oms„ttningsskatt,Accisskatt];
                                                   OptionString=Sales Tax,Excise Tax;
                                                   NotBlank=No }
    { 4   ;   ;Maximum Amount/Qty. ;Decimal       ;CaptionML=[ENU=Maximum Amount/Qty.;
                                                              NOR=Maksimalt bel›p/antall;
                                                              SVE=Maximalt belopp/antal];
                                                   DecimalPlaces=2:2;
                                                   MinValue=0 }
    { 5   ;   ;Tax Below Maximum   ;Decimal       ;CaptionML=[ENU=Tax Below Maximum;
                                                              NOR=Mva. under maksimum;
                                                              SVE=Skatt under maximumgr„ns];
                                                   DecimalPlaces=1:3;
                                                   MinValue=0 }
    { 6   ;   ;Tax Above Maximum   ;Decimal       ;CaptionML=[ENU=Tax Above Maximum;
                                                              NOR=Mva. over maksimum;
                                                              SVE=Skatt ”ver maximumgr„ns];
                                                   DecimalPlaces=1:3;
                                                   MinValue=0 }
    { 7   ;   ;Effective Date      ;Date          ;CaptionML=[ENU=Effective Date;
                                                              NOR=Effektiv dato;
                                                              SVE=G„ller fr.o.m. datum] }
    { 8   ;   ;Calculate Tax on Tax;Boolean       ;CaptionML=[ENU=Calculate Tax on Tax;
                                                              NOR=Beregn mva. p† mva.;
                                                              SVE=Ber„kna skatt p† skatt];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Tax Jurisdiction Code,Tax Group Code,Tax Type,Effective Date;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      SpecifyTaxMsg@1001 : TextConst '@@@="%1=Tax Area Code or Tax Group Code";ENU=Please specify a %1 first.;NOR=Angi %1 f›rst.;SVE=Ange en %1 f”rst.';
      CannotChangeNonTaxableGroupCodeErr@1000 : TextConst 'ENU=You cannot change the rate for the non-taxable group.;NOR=Du kan ikke endre kursen for den ikke-mva-pliktige gruppen.';

    LOCAL PROCEDURE ApplyCommonFilters@5(TaxJurisdictionCode@1003 : Code[20];TaxGroupCode@1002 : Code[20];TaxType@1001 : Option;EffectiveDate@1000 : Date);
    BEGIN
      RESET;
      SETRANGE("Tax Jurisdiction Code",TaxJurisdictionCode);
      SETRANGE("Tax Group Code",TaxGroupCode);
      SETRANGE("Tax Type",TaxType);
      IF EffectiveDate <> 0D THEN
        SETFILTER("Effective Date",'<=%1',EffectiveDate);
    END;

    [External]
    PROCEDURE ValidateTaxSetup@3(TaxAreaCode@1001 : Code[20];TaxGroupCode@1000 : Code[20];EffectiveDate@1002 : Date);
    VAR
      TaxArea@1005 : Record 318;
      TaxAreaLine@1004 : Record 319;
      TaxGroup@1003 : Record 321;
    BEGIN
      TaxArea.GET(TaxAreaCode);
      TaxGroup.GET(TaxGroupCode);
      TaxAreaLine.SETRANGE("Tax Area",TaxArea.Code);
      IF TaxAreaLine.FINDSET THEN
        REPEAT
          ApplyCommonFilters(TaxAreaLine."Tax Jurisdiction Code",TaxGroupCode,"Tax Type"::"Sales Tax",EffectiveDate);
          IF NOT FINDFIRST THEN
            SetNewTaxRate(TaxAreaLine."Tax Jurisdiction Code",TaxGroupCode,"Tax Type"::"Sales Tax",EffectiveDate,0);
        UNTIL TaxAreaLine.NEXT = 0;
    END;

    [External]
    PROCEDURE GetSalesTaxRate@4(TaxAreaCode@1002 : Code[20];TaxGroupCode@1001 : Code[20];EffectiveDate@1003 : Date;TaxLiable@1005 : Boolean) : Decimal;
    VAR
      TaxAreaLine@1004 : Record 319;
      TotalTaxRate@1000 : Decimal;
    BEGIN
      IF NOT TaxLiable THEN
        EXIT(0);
      TaxAreaLine.SETRANGE("Tax Area",TaxAreaCode);
      IF TaxAreaLine.FINDSET THEN
        REPEAT
          TotalTaxRate += GetTaxRate(TaxAreaLine."Tax Jurisdiction Code",TaxGroupCode,"Tax Type"::"Sales Tax",EffectiveDate);
        UNTIL TaxAreaLine.NEXT = 0;
      EXIT(TotalTaxRate);
    END;

    [External]
    PROCEDURE SetSalesTaxRate@6(TaxAreaCode@1001 : Code[20];TaxGroupCode@1000 : Code[20];NewTaxRate@1007 : Decimal;EffectiveDate@1009 : Date);
    VAR
      TaxSetup@1008 : Record 326;
      TaxAreaLine@1006 : Record 319;
      TaxJurisdiction@1004 : Record 320;
      TaxJurisdiction2@1003 : Record 320;
      TotalTaxRate@1002 : Decimal;
    BEGIN
      IF TaxGroupCode = '' THEN
        EXIT;
      IF TaxSetup.GET THEN
        IF TaxSetup."Non-Taxable Tax Group Code" = TaxGroupCode THEN
          ERROR(CannotChangeNonTaxableGroupCodeErr);
      IF NewTaxRate = GetSalesTaxRate(TaxAreaCode,TaxGroupCode,EffectiveDate,TRUE) THEN
        EXIT;
      IF TaxAreaCode = '' THEN BEGIN
        MESSAGE(SpecifyTaxMsg,TaxAreaLine.FIELDCAPTION("Tax Area"));
        EXIT;
      END;
      TotalTaxRate := 0;
      TaxAreaLine.SETRANGE("Tax Area",TaxAreaCode);
      TaxAreaLine.SETFILTER("Tax Jurisdiction Code",'<>%1','');
      IF TaxAreaLine.FINDSET THEN
        REPEAT
          IF TaxJurisdiction.GET(TaxAreaLine."Tax Jurisdiction Code") THEN BEGIN
            TotalTaxRate += GetTaxRate(TaxJurisdiction.Code,TaxGroupCode,"Tax Type"::"Sales Tax",EffectiveDate);
            IF TaxJurisdiction2.Code = '' THEN // the first
              TaxJurisdiction2 := TaxJurisdiction
            ELSE
              IF TaxJurisdiction.Code <> TaxJurisdiction."Report-to Jurisdiction" THEN
                TaxJurisdiction2 := TaxJurisdiction;
          END;
        UNTIL TaxAreaLine.NEXT = 0;
      IF TaxJurisdiction2.Code = '' THEN
        EXIT; // missing setup
      TotalTaxRate -= GetTaxRate(TaxJurisdiction2.Code,TaxGroupCode,"Tax Type"::"Sales Tax",EffectiveDate);
      SetNewTaxRate(TaxJurisdiction2.Code,TaxGroupCode,"Tax Type"::"Sales Tax",EffectiveDate,NewTaxRate - TotalTaxRate);
    END;

    [External]
    PROCEDURE SetSalesTaxRateDetailed@7(TaxAreaCode@1001 : Code[20];TaxGroupCode@1000 : Code[20];NewCityRate@1007 : Decimal;NewCountyRate@1005 : Decimal;NewStateRate@1008 : Decimal;EffectiveDate@1003 : Date);
    VAR
      TaxAreaLine@1006 : Record 319;
      TaxJurisdiction@1004 : Record 320;
      TaxJurisDictionCodes@1009 : ARRAY [3] OF Code[10];
      i@1002 : Integer;
    BEGIN
      IF TaxAreaCode = '' THEN BEGIN
        MESSAGE(SpecifyTaxMsg,TaxAreaLine.FIELDCAPTION("Tax Area"));
        EXIT;
      END;
      TaxAreaLine.SETRANGE("Tax Area",TaxAreaCode);
      TaxAreaLine.SETFILTER("Tax Jurisdiction Code",'<>%1','');
      IF TaxAreaLine.FINDSET THEN
        REPEAT
          IF TaxJurisdiction.GET(TaxAreaLine."Tax Jurisdiction Code") THEN BEGIN
            i += 1;
            IF i <= 3 THEN
              TaxJurisDictionCodes[i] := TaxJurisdiction.Code;
          END;
        UNTIL (TaxAreaLine.NEXT = 0) OR (i = 3);
      IF i = 0 THEN
        EXIT;
      IF i < 3 THEN BEGIN
        NewStateRate += NewCountyRate;
        NewCountyRate := 0;
      END;
      IF i < 2 THEN BEGIN
        NewStateRate += NewCityRate;
        NewCityRate := 0;
      END;
      CASE i OF
        1:
          SetNewTaxRate(TaxJurisDictionCodes[1],TaxGroupCode,"Tax Type"::"Sales Tax",EffectiveDate,NewStateRate);
        2:
          BEGIN
            SetNewTaxRate(TaxJurisDictionCodes[1],TaxGroupCode,"Tax Type"::"Sales Tax",EffectiveDate,NewCityRate);
            SetNewTaxRate(TaxJurisDictionCodes[2],TaxGroupCode,"Tax Type"::"Sales Tax",EffectiveDate,NewStateRate);
          END;
        3:
          BEGIN
            SetNewTaxRate(TaxJurisDictionCodes[1],TaxGroupCode,"Tax Type"::"Sales Tax",EffectiveDate,NewCityRate);
            SetNewTaxRate(TaxJurisDictionCodes[2],TaxGroupCode,"Tax Type"::"Sales Tax",EffectiveDate,NewCountyRate);
            SetNewTaxRate(TaxJurisDictionCodes[3],TaxGroupCode,"Tax Type"::"Sales Tax",EffectiveDate,NewStateRate);
          END;
      END;
    END;

    LOCAL PROCEDURE GetTaxRate@2(TaxJurisdictionCode@1000 : Code[20];TaxGroupCode@1001 : Code[20];TaxType@1002 : Option;EffectiveDate@1003 : Date) : Decimal;
    BEGIN
      ApplyCommonFilters(TaxJurisdictionCode,TaxGroupCode,TaxType,EffectiveDate);
      IF FINDLAST THEN
        EXIT("Tax Below Maximum");
      EXIT(0);
    END;

    LOCAL PROCEDURE SetNewTaxRate@1(TaxJurisdictionCode@1000 : Code[10];TaxGroupCode@1001 : Code[20];TaxType@1002 : Option;EffectiveDate@1003 : Date;NewTaxRate@1004 : Decimal);
    BEGIN
      ApplyCommonFilters(TaxJurisdictionCode,TaxGroupCode,TaxType,EffectiveDate);
      SETRANGE("Effective Date",EffectiveDate);
      LOCKTABLE;
      IF NOT FINDLAST THEN BEGIN
        INIT;
        "Tax Jurisdiction Code" := TaxJurisdictionCode;
        "Tax Group Code" := TaxGroupCode;
        "Tax Type" := TaxType;
        "Effective Date" := EffectiveDate;
        INSERT;
      END;
      "Tax Below Maximum" := NewTaxRate;
      MODIFY;
    END;

    BEGIN
    END.
  }
}

