OBJECT Table 221 Gen. Jnl. Allocation
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03,4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               LOCKTABLE;
               GenJnlLine.GET("Journal Template Name","Journal Batch Name","Journal Line No.");

               ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
               ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");
             END;

    OnDelete=BEGIN
               VALIDATE(Amount,0);
             END;

    CaptionML=[ENU=Gen. Jnl. Allocation;
               NOR=Fordeling;
               SVE=Redovisningsjnl med f”rdelning];
  }
  FIELDS
  {
    { 1   ;   ;Journal Template Name;Code10       ;TableRelation="Gen. Journal Template";
                                                   CaptionML=[ENU=Journal Template Name;
                                                              NOR=Kladdemalnavn;
                                                              SVE=Mallnamn f”r journal] }
    { 2   ;   ;Journal Batch Name  ;Code10        ;TableRelation="Gen. Journal Batch".Name WHERE (Journal Template Name=FIELD(Journal Template Name));
                                                   CaptionML=[ENU=Journal Batch Name;
                                                              NOR=Kladdenavn;
                                                              SVE=Journalnamn] }
    { 3   ;   ;Journal Line No.    ;Integer       ;TableRelation="Gen. Journal Line"."Line No." WHERE (Journal Template Name=FIELD(Journal Template Name),
                                                                                                       Journal Batch Name=FIELD(Journal Batch Name));
                                                   CaptionML=[ENU=Journal Line No.;
                                                              NOR=Kladdelinjenr.;
                                                              SVE=Journalradnr] }
    { 4   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 5   ;   ;Account No.         ;Code20        ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                IF "Account No." = '' THEN BEGIN
                                                                  GLAcc.INIT;
                                                                  CreateDim(DATABASE::"G/L Account","Account No.");
                                                                END ELSE BEGIN
                                                                  GLAcc.GET("Account No.");
                                                                  GLAcc.CheckGLAcc;
                                                                  GLAcc.TESTFIELD("Direct Posting",TRUE);
                                                                END;
                                                                "Account Name" := GLAcc.Name;
                                                                "Gen. Posting Type" := GLAcc."Gen. Posting Type";
                                                                "Gen. Bus. Posting Group" := GLAcc."Gen. Bus. Posting Group";
                                                                "Gen. Prod. Posting Group" := GLAcc."Gen. Prod. Posting Group";
                                                                "VAT Bus. Posting Group" := GLAcc."VAT Bus. Posting Group";
                                                                "VAT Prod. Posting Group" := GLAcc."VAT Prod. Posting Group";
                                                                VALIDATE("VAT Prod. Posting Group");

                                                                CreateDim(DATABASE::"G/L Account","Account No.");
                                                              END;

                                                   CaptionML=[ENU=Account No.;
                                                              NOR=Kontonr.;
                                                              SVE=Nr] }
    { 6   ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Shortcut Dimension 1 Code;
                                                              NOR=Snarveisdimensjon 1 - kode;
                                                              SVE=Genv„g dimension 1 kod];
                                                   CaptionClass='1,2,1' }
    { 7   ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Shortcut Dimension 2 Code;
                                                              NOR=Snarveisdimensjon 2 - kode;
                                                              SVE=Genv„g dimension 2 kod];
                                                   CaptionClass='1,2,2' }
    { 8   ;   ;Allocation Quantity ;Decimal       ;OnValidate=BEGIN
                                                                IF "Allocation Quantity" = 0 THEN BEGIN
                                                                  "Allocation %" := 0;
                                                                  Amount := 0;
                                                                END;
                                                                UpdateAllocations(GenJnlLine);
                                                              END;

                                                   CaptionML=[ENU=Allocation Quantity;
                                                              NOR=Andel i antall;
                                                              SVE=F”rdelningsantal];
                                                   DecimalPlaces=0:5 }
    { 9   ;   ;Allocation %        ;Decimal       ;OnValidate=BEGIN
                                                                "Allocation Quantity" := 0;
                                                                IF "Allocation %" = 0 THEN
                                                                  Amount := 0;
                                                                UpdateAllocations(GenJnlLine);
                                                              END;

                                                   CaptionML=[ENU=Allocation %;
                                                              NOR=Andel i pst.;
                                                              SVE=F”rdelning %];
                                                   DecimalPlaces=2:2 }
    { 10  ;   ;Amount              ;Decimal       ;OnValidate=BEGIN
                                                                IF ("Allocation Quantity" <> 0) OR ("Allocation %" <> 0) THEN BEGIN
                                                                  "Allocation Quantity" := 0;
                                                                  "Allocation %" := 0;
                                                                  UpdateAllocations(GenJnlLine);
                                                                END ELSE BEGIN
                                                                  VALIDATE("VAT Prod. Posting Group");
                                                                  MODIFY;
                                                                  UpdateJnlBalance(GenJnlLine);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Amount;
                                                              NOR=Bel›p;
                                                              SVE=Belopp];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 11  ;   ;Gen. Posting Type   ;Option        ;OnValidate=BEGIN
                                                                VALIDATE("VAT Prod. Posting Group");
                                                              END;

                                                   CaptionML=[ENU=Gen. Posting Type;
                                                              NOR=Bokf›ringstype;
                                                              SVE=Typ av bokf”ring];
                                                   OptionCaptionML=[ENU=" ,Purchase,Sale";
                                                                    NOR=" ,Kj›p,Salg";
                                                                    SVE=" ,Ink”p,F”rs„ljning"];
                                                   OptionString=[ ,Purchase,Sale] }
    { 12  ;   ;Gen. Bus. Posting Group;Code20     ;TableRelation="Gen. Business Posting Group";
                                                   OnValidate=BEGIN
                                                                IF xRec."Gen. Bus. Posting Group" <> "Gen. Bus. Posting Group" THEN
                                                                  IF GenBusPostingGrp.ValidateVatBusPostingGroup(GenBusPostingGrp,"Gen. Bus. Posting Group") THEN
                                                                    VALIDATE("VAT Bus. Posting Group",GenBusPostingGrp."Def. VAT Bus. Posting Group");
                                                              END;

                                                   CaptionML=[ENU=Gen. Bus. Posting Group;
                                                              NOR=Bokf›ringsgruppe - firma;
                                                              SVE=Gen. r”relsebokf”ringsmall] }
    { 13  ;   ;Gen. Prod. Posting Group;Code20    ;TableRelation="Gen. Product Posting Group";
                                                   OnValidate=BEGIN
                                                                IF xRec."Gen. Prod. Posting Group" <> "Gen. Prod. Posting Group" THEN
                                                                  IF GenProdPostingGrp.ValidateVatProdPostingGroup(GenProdPostingGrp,"Gen. Prod. Posting Group") THEN
                                                                    VALIDATE("VAT Prod. Posting Group",GenProdPostingGrp."Def. VAT Prod. Posting Group");
                                                              END;

                                                   CaptionML=[ENU=Gen. Prod. Posting Group;
                                                              NOR=Bokf›ringsgruppe - vare;
                                                              SVE=Produktbokf”ringsmall] }
    { 14  ;   ;VAT Calculation Type;Option        ;CaptionML=[ENU=VAT Calculation Type;
                                                              NOR=Mva-beregningstype;
                                                              SVE=Momsber„kningstyp];
                                                   OptionCaptionML=[ENU=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax;
                                                                    NOR=Normal mva.,Snudd avregning,Full mva.,Salgsmva.;
                                                                    SVE=Normal moms,Omv„nd EG-moms,Enbart moms,Oms„ttningsskatt];
                                                   OptionString=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax;
                                                   Editable=No }
    { 15  ;   ;VAT Amount          ;Decimal       ;CaptionML=[ENU=VAT Amount;
                                                              NOR=Mva-bel›p;
                                                              SVE=Momsbelopp];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 16  ;   ;VAT %               ;Decimal       ;CaptionML=[ENU=VAT %;
                                                              NOR=Mva-%;
                                                              SVE=Momssats %];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100;
                                                   Editable=No }
    { 17  ;   ;Account Name        ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("G/L Account".Name WHERE (No.=FIELD(Account No.)));
                                                   CaptionML=[ENU=Account Name;
                                                              NOR=Kontonavn;
                                                              SVE=Kontonamn];
                                                   Editable=No }
    { 18  ;   ;Tax Area Code       ;Code20        ;TableRelation="Tax Area";
                                                   OnValidate=BEGIN
                                                                VALIDATE("VAT Prod. Posting Group");
                                                              END;

                                                   CaptionML=[ENU=Tax Area Code;
                                                              NOR=Mva-omr†dekode;
                                                              SVE=Skatteomr†deskod] }
    { 19  ;   ;Tax Liable          ;Boolean       ;OnValidate=BEGIN
                                                                VALIDATE("VAT Prod. Posting Group");
                                                              END;

                                                   CaptionML=[ENU=Tax Liable;
                                                              NOR=Skyldig mva.;
                                                              SVE=Skattepliktig] }
    { 20  ;   ;Tax Group Code      ;Code20        ;TableRelation="Tax Group";
                                                   OnValidate=BEGIN
                                                                VALIDATE("VAT Prod. Posting Group");
                                                              END;

                                                   CaptionML=[ENU=Tax Group Code;
                                                              NOR=Mva-gruppekode;
                                                              SVE=Skattegruppskod] }
    { 21  ;   ;Use Tax             ;Boolean       ;OnValidate=BEGIN
                                                                VALIDATE("VAT Prod. Posting Group");
                                                              END;

                                                   CaptionML=[ENU=Use Tax;
                                                              NOR=Amerikansk mva.;
                                                              SVE=Anv„nd skatt] }
    { 22  ;   ;VAT Bus. Posting Group;Code20      ;TableRelation="VAT Business Posting Group";
                                                   OnValidate=BEGIN
                                                                VALIDATE("VAT Prod. Posting Group");
                                                              END;

                                                   CaptionML=[ENU=VAT Bus. Posting Group;
                                                              NOR=Mva-bokf›ringsgruppe - firma;
                                                              SVE=Moms r”relsebokf”ringsmall] }
    { 23  ;   ;VAT Prod. Posting Group;Code20     ;TableRelation="VAT Product Posting Group";
                                                   OnValidate=BEGIN
                                                                GenJnlLine.GET("Journal Template Name","Journal Batch Name","Journal Line No.");
                                                                CheckVAT(GenJnlLine);
                                                                UpdateVAT(GenJnlLine);
                                                              END;

                                                   CaptionML=[ENU=VAT Prod. Posting Group;
                                                              NOR=Mva-bokf›ringsgruppe - vare;
                                                              SVE=Moms produktbokf”ringsmall] }
    { 24  ;   ;Additional-Currency Amount;Decimal ;CaptionML=[ENU=Additional-Currency Amount;
                                                              NOR=Tilleggsvalutabel›p;
                                                              SVE=Alt. valuta belopp];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 480 ;   ;Dimension Set ID    ;Integer       ;TableRelation="Dimension Set Entry";
                                                   OnValidate=BEGIN
                                                                DimMgt.UpdateGlobalDimFromDimSetID("Dimension Set ID","Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              ShowDimensions;
                                                            END;

                                                   CaptionML=[ENU=Dimension Set ID;
                                                              NOR=Dimensjonssett-ID;
                                                              SVE=Dimensionsupps„ttnings-ID];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Journal Template Name,Journal Batch Name,Journal Line No.,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 cannot be used in allocations when they are completed on the general journal line.;NOR=%1 kan ikke brukes i fordelinger n†r de fylles ut p† en finanskladdelinje.;SVE=%1 kan inte anv„ndas i f”rdelningar n„r den „r avslutad i redov.jnlraden';
      GLAcc@1001 : Record 15;
      GenJnlLine@1002 : Record 81;
      GenBusPostingGrp@1003 : Record 250;
      GenProdPostingGrp@1004 : Record 251;
      DimMgt@1005 : Codeunit 408;
      DPA_Management@1100000999 : Codeunit 11012783;

    [External]
    PROCEDURE UpdateAllocations@2(VAR GenJnlLine@1000 : Record 81);
    VAR
      GenJnlAlloc@1001 : Record 221;
      GenJnlAlloc2@1002 : Record 221;
      FromAllocations@1003 : Boolean;
      TotalQty@1004 : Decimal;
      TotalPct@1005 : Decimal;
      TotalPctRnded@1006 : Decimal;
      TotalAmountLCY@1007 : Decimal;
      TotalAmountLCY2@1008 : Decimal;
      TotalAmountLCYRnded@1009 : Decimal;
      TotalAmountLCYRnded2@1010 : Decimal;
      UpdateGenJnlLine@1011 : Boolean;
    BEGIN
      IF "Line No." <> 0 THEN BEGIN
        FromAllocations := TRUE;
        GenJnlAlloc.UpdateVAT(GenJnlLine);
        MODIFY;
        GenJnlLine.GET("Journal Template Name","Journal Batch Name","Journal Line No.");
        CheckVAT(GenJnlLine);
      END;

      GenJnlAlloc.SETRANGE("Journal Template Name",GenJnlLine."Journal Template Name");
      GenJnlAlloc.SETRANGE("Journal Batch Name",GenJnlLine."Journal Batch Name");
      GenJnlAlloc.SETRANGE("Journal Line No.",GenJnlLine."Line No.");
      IF FromAllocations THEN
        UpdateGenJnlLine := TRUE
      ELSE
        IF NOT GenJnlAlloc.ISEMPTY THEN BEGIN
          GenJnlAlloc.LOCKTABLE;
          UpdateGenJnlLine := TRUE;
        END;

      IF GenJnlAlloc.FINDSET THEN
        REPEAT
          IF (GenJnlAlloc."Allocation Quantity" <> 0) OR (GenJnlAlloc."Allocation %" <> 0) THEN BEGIN
            IF NOT FromAllocations THEN
              GenJnlAlloc.CheckVAT(GenJnlLine);
            IF GenJnlAlloc."Allocation Quantity" = 0 THEN BEGIN
              TotalAmountLCY2 := TotalAmountLCY2 - GenJnlLine."Amount (LCY)" * GenJnlAlloc."Allocation %" / 100;
              GenJnlAlloc.Amount := ROUND(TotalAmountLCY2) - TotalAmountLCYRnded2;
              TotalAmountLCYRnded2 := TotalAmountLCYRnded2 + GenJnlAlloc.Amount;
            END ELSE BEGIN
              IF TotalQty = 0 THEN BEGIN
                GenJnlAlloc2.COPY(GenJnlAlloc);
                GenJnlAlloc2.SETFILTER("Allocation Quantity",'<>0');
                GenJnlAlloc2.CALCSUMS("Allocation Quantity");
                TotalQty := GenJnlAlloc2."Allocation Quantity";
                IF TotalQty = 0 THEN
                  TotalQty := 1;
              END;
              TotalPct := TotalPct + GenJnlAlloc."Allocation Quantity" / TotalQty * 100;
              GenJnlAlloc."Allocation %" := ROUND(TotalPct,0.01) - TotalPctRnded;
              TotalPctRnded := TotalPctRnded + GenJnlAlloc."Allocation %";
              TotalAmountLCY := TotalAmountLCY - GenJnlLine."Amount (LCY)" * GenJnlAlloc."Allocation Quantity" / TotalQty;
              GenJnlAlloc.Amount := ROUND(TotalAmountLCY) - TotalAmountLCYRnded;
              TotalAmountLCYRnded := TotalAmountLCYRnded + GenJnlAlloc.Amount;
            END;
            GenJnlAlloc.UpdateVAT(GenJnlLine);
            GenJnlAlloc.MODIFY;
          END;
        UNTIL GenJnlAlloc.NEXT = 0;

      IF UpdateGenJnlLine THEN
        UpdateJnlBalance(GenJnlLine);

      IF FromAllocations THEN
        FIND;
    END;

    [External]
    PROCEDURE UpdateAllocationsAddCurr@6(VAR GenJnlLine@1000 : Record 81;AddCurrAmount@1001 : Decimal);
    VAR
      GenJnlAlloc@1002 : Record 221;
      GenJnlAlloc2@1003 : Record 221;
      GLSetup@1004 : Record 98;
      Currency@1005 : Record 4;
      TotalQty@1006 : Decimal;
      TotalPct@1007 : Decimal;
      TotalPctRnded@1008 : Decimal;
      TotalAmountAddCurr@1009 : Decimal;
      TotalAmountAddCurr2@1010 : Decimal;
      TotalAmountAddCurrRnded@1011 : Decimal;
      TotalAmountAddCurrRnded2@1012 : Decimal;
    BEGIN
      GenJnlAlloc.SETRANGE("Journal Template Name",GenJnlLine."Journal Template Name");
      GenJnlAlloc.SETRANGE("Journal Batch Name",GenJnlLine."Journal Batch Name");
      GenJnlAlloc.SETRANGE("Journal Line No.",GenJnlLine."Line No.");
      GenJnlAlloc.LOCKTABLE;
      IF GenJnlAlloc.FINDSET THEN BEGIN
        GLSetup.GET;
        Currency.GET(GLSetup."Additional Reporting Currency");
        Currency.TESTFIELD("Amount Rounding Precision");
        REPEAT
          IF (GenJnlAlloc."Allocation Quantity" <> 0) OR (GenJnlAlloc."Allocation %" <> 0) THEN BEGIN
            IF GenJnlAlloc."Allocation Quantity" = 0 THEN BEGIN
              TotalAmountAddCurr2 :=
                TotalAmountAddCurr2 - AddCurrAmount * GenJnlAlloc."Allocation %" / 100;
              GenJnlAlloc."Additional-Currency Amount" :=
                ROUND(TotalAmountAddCurr2,Currency."Amount Rounding Precision") -
                TotalAmountAddCurrRnded2;
              TotalAmountAddCurrRnded2 :=
                TotalAmountAddCurrRnded2 + GenJnlAlloc."Additional-Currency Amount";
            END ELSE BEGIN
              IF TotalQty = 0 THEN BEGIN
                GenJnlAlloc2.COPY(GenJnlAlloc);
                GenJnlAlloc2.SETFILTER("Allocation Quantity",'<>0');
                REPEAT
                  TotalQty := TotalQty + GenJnlAlloc2."Allocation Quantity";
                UNTIL GenJnlAlloc2.NEXT = 0;
                IF TotalQty = 0 THEN
                  TotalQty := 1;
              END;
              TotalPct := TotalPct + GenJnlAlloc."Allocation Quantity" / TotalQty * 100;
              GenJnlAlloc."Allocation %" := ROUND(TotalPct,0.01) - TotalPctRnded;
              TotalPctRnded := TotalPctRnded + GenJnlAlloc."Allocation %";
              TotalAmountAddCurr :=
                TotalAmountAddCurr -
                AddCurrAmount * GenJnlAlloc."Allocation Quantity" / TotalQty;
              GenJnlAlloc."Additional-Currency Amount" :=
                ROUND(TotalAmountAddCurr,Currency."Amount Rounding Precision") -
                TotalAmountAddCurrRnded;
              TotalAmountAddCurrRnded :=
                TotalAmountAddCurrRnded + GenJnlAlloc."Additional-Currency Amount";
            END;
            GenJnlAlloc.MODIFY;
          END;
        UNTIL GenJnlAlloc.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE UpdateJnlBalance@4(VAR GenJnlLine@1000 : Record 81);
    BEGIN
      GenJnlLine.CALCFIELDS("Allocated Amt. (LCY)");
      IF GenJnlLine."Bal. Account No." = '' THEN
        GenJnlLine."Balance (LCY)" := GenJnlLine."Amount (LCY)" + GenJnlLine."Allocated Amt. (LCY)"
      ELSE
        GenJnlLine."Balance (LCY)" := GenJnlLine."Allocated Amt. (LCY)";
      GenJnlLine.MODIFY;
    END;

    [External]
    PROCEDURE CheckVAT@1(VAR GenJnlLine@1000 : Record 81);
    BEGIN
      IF ("Gen. Posting Type" <> 0) AND (GenJnlLine."Gen. Posting Type" <> 0) THEN
        ERROR(
          Text000,
          GenJnlLine.FIELDCAPTION("Gen. Posting Type"));
    END;

    [External]
    PROCEDURE UpdateVAT@3(VAR GenJnlLine@1000 : Record 81);
    VAR
      GenJnlLine2@1001 : Record 81;
    BEGIN
      //**4PS.sn
      IF GenJnlLine."VAT Posting" = GenJnlLine."VAT Posting"::"Manual VAT Entry" THEN
        EXIT;
      //**4PS.en

      GenJnlLine2.CopyFromGenJnlAllocation(Rec);
      GenJnlLine2."Posting Date" := GenJnlLine."Posting Date";
      GenJnlLine2.VALIDATE("VAT Prod. Posting Group");
      Amount := GenJnlLine2."Amount (LCY)";
      "VAT Calculation Type" := GenJnlLine2."VAT Calculation Type";
      "VAT Amount" := GenJnlLine2."VAT Amount";
      "VAT %" := GenJnlLine2."VAT %";
    END;

    [External]
    PROCEDURE GetCurrencyCode@5() : Code[10];
    VAR
      GenJnlLine3@1000 : Record 81;
    BEGIN
      GenJnlLine3.SETRANGE("Journal Template Name","Journal Template Name");
      GenJnlLine3.SETRANGE("Journal Batch Name","Journal Batch Name");
      GenJnlLine3.SETRANGE("Line No.","Journal Line No.");
      IF GenJnlLine3.FINDFIRST THEN
        EXIT(GenJnlLine3."Currency Code");

      EXIT('');
    END;

    [External]
    PROCEDURE CreateDim@13(Type1@1000 : Integer;No1@1001 : Code[20]);
    VAR
      TableID@1002 : ARRAY [10] OF Integer;
      No@1003 : ARRAY [10] OF Code[20];
    BEGIN
      TableID[1] := Type1;
      No[1] := No1;
      OnAfterCreateDimTableIDs(Rec,CurrFieldNo,TableID,No);

      //"Shortcut Dimension 1 Code" := ''; //**4PS.o Must be kept for Department Authorization
      "Shortcut Dimension 2 Code" := '';
      "Dimension Set ID" :=
        DimMgt.GetRecDefaultDimID(Rec,CurrFieldNo,TableID,No,'',"Shortcut Dimension 1 Code","Shortcut Dimension 2 Code",0,0);
    END;

    [External]
    PROCEDURE ValidateShortcutDimCode@14(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      //**4PS.sn
      IF FieldNumber = 1 THEN
        DimMgt.CheckDepartmentAuthorization(DATABASE::"Gen. Jnl. Allocation", ShortcutDimCode);
      //**4PS.en
      DimMgt.ValidateShortcutDimValues(FieldNumber,ShortcutDimCode,"Dimension Set ID");
    END;

    [External]
    PROCEDURE LookupShortcutDimCode@18(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.LookupDimValueCode(FieldNumber,ShortcutDimCode);
      DimMgt.ValidateShortcutDimValues(FieldNumber,ShortcutDimCode,"Dimension Set ID");
    END;

    [External]
    PROCEDURE ShowShortcutDimCode@15(VAR ShortcutDimCode@1000 : ARRAY [8] OF Code[20]);
    BEGIN
      DimMgt.GetShortcutDimensions("Dimension Set ID",ShortcutDimCode);
    END;

    [External]
    PROCEDURE ShowDimensions@7();
    BEGIN
      DimMgt.SetTableID(DATABASE::"Gen. Jnl. Allocation"); //**4PS.n
      "Dimension Set ID" :=
        DimMgt.EditDimensionSet2("Dimension Set ID",
          STRSUBSTNO('%1 %2 %3',"Journal Template Name","Journal Batch Name","Journal Line No."),
          "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCreateDimTableIDs@164(VAR GenJnlAllocation@1000 : Record 221;VAR FieldNo@1001 : Integer;VAR TableID@1003 : ARRAY [10] OF Integer;VAR No@1002 : ARRAY [10] OF Code[20]);
    BEGIN
    END;

    PROCEDURE DPA_GetFieldNoDepartmentFilter@1100000993() : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      EXIT(0);
    END;

    PROCEDURE DPA_GetFiltersFltrGrp@1100000997(FilterGroupNo@1100525000 : Integer) FilterText : Text[250];
    BEGIN
      //**4PS Generated Code - Do Not Change
      FILTERGROUP(FilterGroupNo);
      FilterText := COPYSTR(GETFILTERS, 1, MAXSTRLEN(FilterText));
      FILTERGROUP(0);
    END;

    PROCEDURE DPA_SetFilters@1100000998(Context@1100525000 : Text[30]) : Boolean;
    VAR
      RecRef@1100525003 : RecordRef;
      i@1100525004 : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN
        EXIT;

      RecRef.GETTABLE(Rec);
      DPA_Management.SetDPAFilterFromContext(
        RecRef,
        Context,
        DPA_GetFieldNoDepartmentCode,
        DPA_GetFieldNoDepartmentFilter );

      FOR i := 10 DOWNTO 0 DO BEGIN
        RecRef.FILTERGROUP(i);
        FILTERGROUP(i);
        SETVIEW(RecRef.GETVIEW(FALSE));
      END;

      EXIT(DPA_Management.IsHardFilter);
    END;

    LOCAL PROCEDURE DPA_IsActive@1100000999() : Boolean;
    VAR
      DepartmentAuthorizObject@1100526000 : Record 11012466;
      FPSLicenseManagement@1100528500 : Codeunit 11229289;
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT FPSLicenseManagement.LicenseAndReadPermissionForTable(DepartmentAuthorizObject) THEN
        EXIT(FALSE);

      IF NOT DPA_Management.AuthorizationActive THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    PROCEDURE DPA_FindRecWithDPAFilterAppld@1100000989();
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN BEGIN
        FIND;
        EXIT;
      END;

      IF NOT DPA_Management.HasTableHardFilter(221) THEN BEGIN
        FIND;
        EXIT;
      END;

      DPA_SetFilters('Table 221');
      IF NOT FIND THEN
        DPA_Management.RaiseDPAErrorForTable(TABLECAPTION);
    END;

    PROCEDURE DPA_ChkDepartmentCodeAllowed@1100000990(IsHardFilter@1100526000 : Boolean);
    VAR
      DimensionManagement@1100526001 : Codeunit 408;
    BEGIN
      //**4PS Generated Code - Do Not Change
      DimensionManagement.CheckDepartmAuthWithFilterType(
        DATABASE::"Gen. Jnl. Allocation", "Shortcut Dimension 1 Code", IsHardFilter);
    END;

    PROCEDURE DPA_GetFieldNoDepartmentCode@1100000994() : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      EXIT(FIELDNO("Shortcut Dimension 1 Code"));
    END;

    PROCEDURE DPA_SetDefaultDepartment@1100000996();
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN
        EXIT;

      DPA_Management.SetDepartmentCodeUser(TABLENAME, "Shortcut Dimension 1 Code");
    END;

    BEGIN
    END.
  }
}

