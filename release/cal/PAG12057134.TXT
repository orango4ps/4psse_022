OBJECT Page 12057134 ExFlow NAV Search Page
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF512000;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Search Documents;
               SVE=S”k Dokument];
    SaveValues=No;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table12013608;
    PageType=Worksheet;
    UsageCategory=ReportsAndAnalysis;
    SourceTableTemporary=Yes;
    RefreshOnActivate=Yes;
    OnOpenPage=BEGIN
                 DimensionsPage:=STRSUBSTNO(Txt001);
                 TxtSearchCompany:=COMPANYNAME;
                 SetApprover;
               END;

    OnAfterGetRecord=BEGIN
                       IF "Requisition No. Series" = '' THEN
                         FromImportDocJournal:=FALSE
                       ELSE
                         FromImportDocJournal:=TRUE;
                     END;

    OnAfterGetCurrRecord=BEGIN
                           IF "Document No." <> '' THEN
                             CurrPage.ExFlowNAVSearchLinesSub.PAGE.PopulateLines("Document No.","Temp Company Name");
                         END;

    ActionList=ACTIONS
    {
      { 1100285002;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100285003;1 ;Action    ;
                      Name=ApplyFilter;
                      CaptionML=[ENU=Apply Filter;
                                 SVE=Applicera filter];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Apply;
                      OnAction=VAR
                                 WebCompID@1100285000 : Record 12013630;
                               BEGIN
                                 DELETEALL;
                                 TempWebSearchRecord.DELETEALL;
                                 TempWebSearchRecord.RESET;
                                 TempWebSearchRecord.INIT;
                                  IF TxtSearchCompany <>'' THEN
                                   TempWebSearchRecord.CompName:= TxtSearchCompany
                                  ELSE BEGIN
                                   TempWebSearchRecord.CompName:=COMPANYNAME;
                                   SetApprover;
                                  END;
                                 TempWebSearchRecord.docNo:= TxtSearchDocNo;
                                 TempWebSearchRecord.vendorName:=TxtSearchVendor;
                                 TempWebSearchRecord.vendorDocNo:= TxtSearchInvoiceNo;
                                 TempWebSearchRecord.duedate_from:= FORMAT(TxtSearchDueDateFrom,0,'<Year4><Month,2><Day,2>');
                                 TempWebSearchRecord.duedate_to := FORMAT(TxtSearchDueDateTo,0,'<Year4><Month,2><Day,2>');
                                 TempWebSearchRecord.postingdate_from:= FORMAT(TxtSearchPostingDateFrom,0,'<Year4><Month,2><Day,2>');
                                 TempWebSearchRecord.postingdate_to:= FORMAT(TxtSearchPostingDateTo,0,'<Year4><Month,2><Day,2>');
                                 TempWebSearchRecord.createDate_From:=FORMAT(TxtSearchCreateDateFrom,0,'<Year4><Month,2><Day,2>');
                                 TempWebSearchRecord.createDate_To:=FORMAT(TxtSearchCreateDateTo,0,'<Year4><Month,2><Day,2>');
                                 TempWebSearchRecord.minAmount:=TxtSearchMinAmount;
                                 TempWebSearchRecord.maxAmount:=TxtSearchMaxAmount;
                                 TempWebSearchRecord.approver:=TxtSearchApprover;

                                 CASE TxtSearchDocType OF
                                   0:TempWebSearchRecord.docType:= '';
                                   1:TempWebSearchRecord.docType:= 'Invoice';
                                   2:TempWebSearchRecord.docType:= 'Credit';
                                   3:TempWebSearchRecord.docType:= 'Order';
                                   4:TempWebSearchRecord.docType:= 'Requisition';
                                 END;

                                 CASE TxtSearchDocStatus OF
                                   0:TempWebSearchRecord.docStatus:= '';
                                   1:TempWebSearchRecord.docStatus:='Inactive';
                                   2:TempWebSearchRecord.docStatus:='Active';
                                   3:TempWebSearchRecord.docStatus:='Approved';
                                 END;

                                 TempWebSearchRecord.INSERT;

                                 TempExDoc.DELETEALL;

                                 //>>1367
                                 IF TxtSearchCompany <>'' THEN
                                   SearchExFlowDocuments.WebSearchPage(TempExDoc,TempWebSearchRecord,arrDimWebColumnID,arrDimValue,FALSE)
                                 ELSE BEGIN
                                   IF WebCompID.FINDFIRST THEN
                                     REPEAT
                                       TempWebSearchRecord.CompName := WebCompID."Company Name";
                                       TempWebSearchRecord.MODIFY;
                                       SearchExFlowDocuments.WebSearchPage(TempExDoc,TempWebSearchRecord,arrDimWebColumnID,arrDimValue,FALSE);
                                     UNTIL WebCompID.NEXT=0;
                                 END;
                                 //<<1367

                                 IF TempExDoc.FINDSET THEN
                                   REPEAT
                                     INIT;
                                     Rec.TRANSFERFIELDS(TempExDoc);
                                     INSERT;
                                   UNTIL TempExDoc.NEXT = 0;
                               END;
                                }
      { 1100285022;1 ;Action    ;
                      Name=RemoveFilters;
                      CaptionML=[ENU=Remove Filters;
                                 SVE=Ta Bort Filter];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=RemoveFilterLines;
                      OnAction=BEGIN
                                 RemoveFilters;
                               END;
                                }
      { 1100285027;1 ;Action    ;
                      Name=ShowCard;
                      ShortCutKey=Shift+F7;
                      CaptionML=[ENU=&Card;
                                 SVE=&Kort];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=EditLines;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 PurchHeader@1100285002 : Record 38;
                                 PurchInvHeader@1100285001 : Record 122;
                                 PurchCrMemoHeader@1100285000 : Record 124;
                               BEGIN
                                 //>>1367
                                 IF "Temp Company Name" <> COMPANYNAME THEN
                                   ERROR(Txt004,"Temp Company Name");
                                 //<<1367
                                 ShowCard;
                               END;
                                }
      { 1100285010;1 ;Action    ;
                      Name=ExFlowCard;
                      CaptionML=[ENU=ExFlow Card;
                                 SVE=ExFlow Kort];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Card;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ExDoc@1100285000 : Record 12013608;
                                 ExPostedDoc@1100285001 : Record 12013626;
                                 EXDocumentLineApprover@1100285002 : Record 12013610;
                                 EXDocumentLine@1100285003 : Record 12013609;
                               BEGIN
                                 //>>1367
                                 IF "Temp Company Name" <> COMPANYNAME THEN
                                   ERROR(Txt004,"Temp Company Name");
                                 //<<1367
                                 CASE "Document Type" OF
                                   "Document Type"::Invoice , "Document Type"::"Credit Memo", "Document Type"::Order , "Document Type"::Quote , "Document Type"::"Blanket Order" , "Document Type"::"Return Order" , "Document Type"::Requisition :
                                     BEGIN
                                       ExDoc.SETRANGE("Document No.", "Document No.");
                                       ExDoc.SETRANGE("Document Type","Document Type");
                                       ExDoc.FINDFIRST;
                                       PAGE.RUNMODAL(12013653,ExDoc);
                                     END;
                                   "Document Type"::"Posted Invoice" , "Document Type"::"Posted Credit Memo"  :
                                     BEGIN
                                       ExPostedDoc.SETRANGE("Document No.", "Document No.");
                                       ExPostedDoc.SETRANGE("Document Type","Document Type");
                                       ExPostedDoc.FINDFIRST;
                                       PAGE.RUNMODAL(12057118,ExPostedDoc);
                                     END;
                                 END;
                               END;
                                }
      { 1100285040;1 ;Action    ;
                      Name=ImpDocJour;
                      CaptionML=[ENU=Import Document Worksheet;
                                 SVE=Dokument import journal];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=OpenJournal;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ExfJnlManagement@1100285001 : Codeunit 12013587;
                                 ExflowJournalBatch@1100285002 : Record 12013590;
                               BEGIN
                                 //>>1367
                                 IF "Temp Company Name" <> COMPANYNAME THEN
                                   ERROR(Txt004,"Temp Company Name");
                                 //1367

                                 ExflowJournalBatch.SETRANGE(Name,"Requisition No. Series");
                                 IF ExflowJournalBatch.FINDFIRST THEN
                                   ExfJnlManagement.TemplateSelectionFromBatch(ExflowJournalBatch)
                                 ELSE
                                   ERROR(Txt005);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100285000;;Container;
                ContainerType=ContentArea }

    { 1100285004;1;Group  ;
                Name=Filter on:;
                CaptionML=[ENU=Filter on:;
                           SVE=Filtrera p†:];
                GroupType=Group }

    { 1100285009;2;Field  ;
                CaptionML=[ENU=Company;
                           SVE=Bolag];
                SourceExpr=TxtSearchCompany;
                TableRelation=Company;
                OnValidate=BEGIN
                             IF TxtSearchCompany = '' THEN
                               SetApprover;
                           END;
                            }

    { 1100285041;2;Field  ;
                Name=TxtSearchApprover;
                CaptionML=[ENU=Approver;
                           SVE=Attestant];
                SourceExpr=TxtSearchApprover;
                OnLookup=VAR
                           lp@1100285000 : Page 12013625;
                           lr@1100285001 : Record 12013641;
                         BEGIN
                           //lookup ExFlow User Setup
                           lp.LOOKUPMODE:=TRUE;
                           lr.RESET;
                           lp.SETTABLEVIEW(lr);
                           IF lp.RUNMODAL=ACTION::LookupOK THEN
                             BEGIN
                               lp.GETRECORD(lr);
                               TxtSearchApprover:=lr."User ID";
                             END;
                           CurrPage.UPDATE(FALSE);
                         END;
                          }

    { 1100285001;2;Field  ;
                CaptionML=[ENU=Document No;
                           SVE=Dokument Nr];
                SourceExpr=TxtSearchDocNo }

    { 1100285008;2;Field  ;
                CaptionML=[ENU=Document Type;
                           SVE=Dokumenttyp];
                OptionCaptionML=[ENU=" ,Invoice,Credit,Order,Requisition";
                                 SVE=" ,Faktura,Kredit,Order,Rekvisition"];
                SourceExpr=TxtSearchDocType }

    { 1100285014;2;Field  ;
                CaptionML=[ENU=Document Status;
                           SVE=Dokumentstatus];
                OptionCaptionML=[ENU=" ,Inactive,Active,Approved";
                                 SVE=" ,Inaktiv,Aktiv,Attesterad"];
                SourceExpr=TxtSearchDocStatus }

    { 1100285034;2;Field  ;
                Name=TxtSearchVendor;
                CaptionML=[ENU=Vendor Name;
                           SVE=Leverant”rsnamn];
                SourceExpr=TxtSearchVendor;
                OnLookup=VAR
                           lp@1100285000 : Page 27;
                           lr@1100285001 : Record 23;
                         BEGIN
                           //lookup Vendor List
                           lp.LOOKUPMODE:=TRUE;
                           lr.RESET;
                           lp.SETTABLEVIEW(lr);
                           IF lp.RUNMODAL=ACTION::LookupOK THEN
                             BEGIN
                               lp.GETRECORD(lr);
                               TxtSearchVendor:=lr.Name;
                             END;
                           CurrPage.UPDATE(FALSE);
                         END;
                          }

    { 1100285016;2;Field  ;
                CaptionML=[ENU=Vendor Document No;
                           SVE=Leverant”rens dokumentnr];
                SourceExpr=TxtSearchInvoiceNo }

    { 1100285019;2;Group  ;
                GroupType=Group }

    { 1100285020;3;Group  ;
                GroupType=GridLayout;
                Layout=Rows }

    { 1100285021;4;Group  ;
                CaptionML=[ENU="Due Date: ";
                           SVE="F”rfallodatum: "];
                GroupType=Group }

    { 1100285017;5;Field  ;
                CaptionML=[ENU="Due Date: ";
                           SVE="F”rfallodatum: "];
                SourceExpr=TxtSearchDueDateFrom;
                ShowCaption=No }

    { 1100285023;5;Field  ;
                CaptionML=[ENU="   -";
                           SVE="   -"];
                ShowCaption=No }

    { 1100285018;5;Field  ;
                SourceExpr=TxtSearchDueDateTo;
                ShowCaption=No }

    { 1100285026;4;Group  ;
                CaptionML=[ENU="Created Date: ";
                           SVE=Skapad Datum:];
                Visible=false;
                Enabled=false;
                GroupType=Group }

    { 1100285028;5;Field  ;
                CaptionML=[ENU="Created Date: ";
                           SVE="Skapad datum: "];
                SourceExpr=TxtSearchCreateDateFrom;
                ShowCaption=No }

    { 1100285029;5;Field  ;
                CaptionML=[ENU="   -";
                           SVE="   -"];
                ShowCaption=No }

    { 1100285030;5;Field  ;
                SourceExpr=TxtSearchCreateDateTo;
                ShowCaption=No }

    { 1100285033;4;Group  ;
                CaptionML=[ENU="Document Date: ";
                           SVE="Dokumentdatum: "];
                GroupType=Group }

    { 1100285035;5;Field  ;
                CaptionML=[ENU="Document Date: ";
                           SVE="Dokumentdatum: "];
                SourceExpr=TxtSearchPostingDateFrom;
                ShowCaption=No }

    { 1100285036;5;Field  ;
                CaptionML=[ENU="   -";
                           SVE="   -"];
                ShowCaption=No }

    { 1100285037;5;Field  ;
                SourceExpr=TxtSearchPostingDateTo;
                ShowCaption=No }

    { 1100285043;4;Group  ;
                CaptionML=[ENU=Amount;
                           SVE=Belopp];
                GroupType=Group }

    { 1100285044;5;Field  ;
                CaptionML=[ENU=Amount;
                           SVE=Belopp];
                SourceExpr=TxtSearchMinAmount;
                ShowCaption=No }

    { 1100285046;5;Field  ;
                CaptionML=[ENU="   -";
                           SVE="   -"];
                ShowCaption=No }

    { 1100285047;5;Field  ;
                SourceExpr=TxtSearchMaxAmount;
                ShowCaption=No }

    { 1100285042;2;Field  ;
                DrillDown=Yes;
                CaptionML=[ENU=Search Columns;
                           SVE=S”k Kolumner];
                SourceExpr=DimensionsPage;
                Editable=FALSE;
                OnDrillDown=VAR
                              WebCompanyID@1100285002 : Record 12013630;
                              Tmp@1100285004 : Text;
                              TmpDate@1100285003 : Date;
                              TextManagement@1100583000 : Codeunit 41;
                            BEGIN
                               FOR i:= 1 TO ARRAYLEN(arrDimValue) DO
                                 BEGIN
                                  CLEAR(arrDimWebColumnID[i]);
                                  CLEAR(arrDimValue[i]);
                                  DimVisable:=FALSE;   //check this
                                 END;
                              PickedDims:='';

                              IF TxtSearchCompany = '' THEN
                                ERROR(Txt002);

                              ExFlowNavSearchDim.PopulateTable(TxtSearchCompany);

                              CLEAR(ExFlowNavSearchDim);  //1538

                              ExFlowNavSearchDim.SetTempTable(ExCodingRuleLineTmp,TxtSearchCompany);  //1538

                              IF ExFlowNavSearchDim.RUNMODAL <> ACTION::OK THEN
                                EXIT;

                              ExFlowNavSearchDim.GetTempTable(ExCodingRuleLineTmp);  //1538

                              ExCodingRuleLineTmp.SETFILTER("Text Range",'<>%1','');

                              IF NOT ExCodingRuleLineTmp.FINDSET THEN
                                EXIT;

                              i := 0;
                              PickedDims:='';
                              WebCompanyID.GET(TxtSearchCompany);
                              REPEAT
                                i += 1;
                                arrDimWebColumnID[i] := FORMAT(ExCodingRuleLineTmp.ID + WebCompanyID."Web Company ID");
                                arrDimValue[i] := ExCodingRuleLineTmp."Text Range";
                                PickedDims+= ExCodingRuleLineTmp."Column Code" + ': ';
                                PickedDims+=arrDimValue[i];
                                PickedDims+=',';
                                IF ExCodingRuleLineTmp."Column Code"= 'PERDATE' THEN BEGIN
                                  Tmp:= FORMAT(arrDimValue[i]);
                                  TextManagement.MakeDateText(Tmp);
                                  EVALUATE(TmpDate,Tmp);
                                  arrDimValue[i]:= FORMAT(TmpDate,0,9);
                                  //make an error message if not in a right format
                                END;
                              UNTIL ExCodingRuleLineTmp.NEXT = 0;
                              j:=i;

                              PickedDims:=COPYSTR(PickedDims,1,STRLEN(PickedDims)-1);

                              IF j <> 0 THEN
                                DimVisable:=TRUE
                              ELSE
                                DimVisable:=FALSE;
                            END;

                ShowCaption=No }

    { 1100285056;2;Group  ;
                Visible=DimVisable;
                GroupType=Group }

    { 1100285045;3;Field  ;
                DrillDown=Yes;
                CaptionML=[ENU=Picked values:;
                           SVE=Valda v„rden:];
                SourceExpr=PickedDims;
                Editable=FALSE;
                OnDrillDown=VAR
                              ShowPickedDims@1100285000 : Text;
                            BEGIN
                              ShowPickedDims:=PickedDims;
                              MESSAGE(ShowPickedDims);
                            END;

                ShowCaption=Yes }

    { 1100285007;1;Group  ;
                GroupType=Repeater;
                Layout=Columns }

    { 1100285024;2;Field  ;
                Name=Company2;
                CaptionML=[ENU=Company;
                           SVE=Bolag];
                SourceExpr="Temp Company Name" }

    { 1100285005;2;Field  ;
                CaptionML=[ENU=Document No.;
                           SVE=Dokumentnr];
                SourceExpr="Document No.";
                Editable=FALSE }

    { 1100285052;2;Field  ;
                CaptionML=[ENU=From Import Document Journal;
                           SVE="Fr†n dokument import journalen "];
                SourceExpr=FromImportDocJournal }

    { 1100285051;2;Field  ;
                CaptionML=[ENU=Import Journal Batch Name;
                           SVE=Importjournal batch namn];
                SourceExpr="Requisition No. Series" }

    { 1100285006;2;Field  ;
                CaptionML=[ENU=Document Type;
                           SVE=Dokumenttyp];
                SourceExpr="Document Type";
                Editable=FALSE }

    { 1100285011;2;Field  ;
                CaptionML=[ENU=Posting Date;
                           SVE=Bokf”ringsdatum];
                SourceExpr="Posting Date";
                Editable=FALSE }

    { 1100285012;2;Field  ;
                CaptionML=[ENU=Vendor No.;
                           SVE=Leverant”rsnr];
                SourceExpr="Vendor No.";
                Editable=FALSE }

    { 1100285013;2;Field  ;
                CaptionML=[ENU=Vendor Name;
                           SVE=Leverant”rsnamn];
                SourceExpr="Vendor Name";
                Editable=FALSE }

    { 1100285015;2;Field  ;
                SourceExpr="Gross Amount" }

    { 1100285050;2;Field  ;
                SourceExpr="VAT Amount" }

    { 1100285048;2;Field  ;
                SourceExpr="Net Amount" }

    { 1100285038;2;Field  ;
                SourceExpr="Vendor Document No." }

    { 1100285039;2;Field  ;
                SourceExpr="Created Date" }

    { 1100285025;2;Field  ;
                SourceExpr="Document Date" }

    { 1100285031;1;Group  ;
                GroupType=Group }

    { 1100285032;2;Part   ;
                Name=ExFlowNAVSearchLinesSub;
                SubPageView=SORTING(Document Type,Document No.,Line No.);
                SubPageLink=Document No.=FIELD(Document No.),
                            Document Type=FIELD(Document Type);
                PagePartID=Page12057136;
                PartType=Page }

  }
  CODE
  {
    VAR
      TxtSearchCompany@1100285007 : Text;
      TxtSearchDocNo@1100285000 : Text;
      TxtSearchDocType@1100285006 : ' ,Invoice,Credit,Order,Requisition';
      TxtSearchDocStatus@1100285012 : ' ,Inactive,Active,Approved';
      TxtSearchVendor@1100285008 : Text;
      TxtSearchVendorNo@1100285011 : Text;
      TxtSearchApprover@1100285015 : Text;
      TxtSearchApproverStatus@1100285016 : Option;
      TxtSearchCreateDateFrom@1100285017 : Date;
      TxtSearchCreateDateTo@1100285021 : Date;
      TxtSearchDueDateFrom@1100285018 : Date;
      TxtSearchDueDateTo@1100285020 : Date;
      TxtSearchPostingDateFrom@1100285022 : Date;
      TxtSearchPostingDateTo@1100285023 : Date;
      TxtSearchInvoiceNo@1100285019 : Text;
      TxtSearchMinAmount@1100285024 : Text;
      TxtSearchMaxAmount@1100285025 : Text;
      WebServiceSearchRoutine@1100285001 : Codeunit 12013635;
      SearchExFlowDocuments@1100285036 : Codeunit 12057148;
      TempWebSearchRecord@1100285002 : TEMPORARY Record 12057116;
      TempExDoc@1100285003 : TEMPORARY Record 12013608;
      arrDimWebColumnID@1100285005 : ARRAY [10] OF Code[20];
      arrDimValue@1100285004 : ARRAY [10] OF Code[1024];
      getfilters@1100285009 : Text;
      Type@1100285010 : Integer;
      RecDocumentType@1100285013 : Text;
      Company@1100285014 : Text[30];
      DimensionsPage@1100285026 : Text;
      Txt001@1100285028 : TextConst 'ENU=Search Columns;SVE=S”k Kolumner';
      EXUserCompany@1100285027 : Record 12013641;
      Txt002@1100285030 : TextConst 'ENU=Select company to be able to use search columns function;SVE=V„lj bolag f”r att anv„nda s”k kolumn funktionen';
      i@1100285029 : Integer;
      j@1100285032 : Integer;
      DimVisable@1100285031 : Boolean;
      PickedDims@1100285041 : Code[200];
      ShowPickedDims@1100285033 : Text[200];
      Txt003@1100285034 : TextConst 'ENU=Please enter deferral start date in one of the following formats: "yyyy-mm-dd", "yy-mm-dd", "yyyy/mm/dd", "yy/mm/dd", "yyyy.mm.dd", "yy.mm.dd", "yymmdd", "yyyymmdd";SVE=Skriv periodiseringsstartdatumet i ett av f”ljande format: "yyyy-mm-dd", "yy-mm-dd", "yyyy/mm/dd", "yy/mm/dd", "yyyy.mm.dd", "yy.mm.dd", "yymmdd", "yyyymmdd"';
      Txt004@1100285035 : TextConst 'ENU=This document is from company %1.\To view additional details, please change NAV company to %1.;SVE=Det h„r dokumentet ligger i f”retag %1.\F”r att se mer detaljer byt NAV f”retag till %1.';
      Txt005@1100285037 : TextConst 'ENU=This document is not from the import journal.;SVE=Det h„r dokumentet „r inte fr†n import journalen.';
      FromImportDocJournal@1100285038 : Boolean;
      ExCodingRuleLineTmp@1100285039 : TEMPORARY Record 12013663;
      ExFlowNavSearchDim@1100285040 : Page 12057135;

    LOCAL PROCEDURE RemoveFilters@1100285026();
    BEGIN
      DELETEALL;
      TxtSearchDocNo:='';
      TxtSearchDocType:=0;
      TxtSearchDocStatus:=0;
      TxtSearchVendor:='';
      TxtSearchApproverStatus:=0;
      TxtSearchCreateDateFrom:=0D;
      TxtSearchCreateDateTo:=0D;
      TxtSearchDueDateFrom:=0D;
      TxtSearchDueDateTo:=0D;
      TxtSearchPostingDateFrom:=0D;
      TxtSearchPostingDateTo:=0D;
      TxtSearchInvoiceNo:='';
      TxtSearchMinAmount:='';
      TxtSearchMaxAmount:='';
      FOR i:= 1 TO j DO
        BEGIN
          CLEAR(arrDimWebColumnID[i]);
          CLEAR(arrDimValue[i]);
        END;
      DimVisable:=FALSE;

      ExFlowNavSearchDim.ResetValues(ExCodingRuleLineTmp);
    END;

    LOCAL PROCEDURE SetApprover@1100285030();
    VAR
      ExFlowSetup@1100285000 : Record 12013601;
    BEGIN
      IF ExFlowSetup.FINDFIRST THEN BEGIN
        IF ExFlowSetup."ExFlow User Domain Validation" = ExFlowSetup."ExFlow User Domain Validation"::"With Domain" THEN
          TxtSearchApprover:=USERID;
        IF ExFlowSetup."ExFlow User Domain Validation" = ExFlowSetup."ExFlow User Domain Validation"::"Without Domain" THEN
          TxtSearchApprover:=COPYSTR(USERID,STRPOS(USERID,'\')+1, STRLEN(USERID));
      END;
    END;

    BEGIN
    END.
  }
}

