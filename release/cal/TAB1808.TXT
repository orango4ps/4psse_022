OBJECT Table 1808 Aggregated Assisted Setup
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Aggregated Assisted Setup;
               NOR=Aggregert assistert oppsett];
  }
  FIELDS
  {
    { 1   ;   ;Page ID             ;Integer       ;CaptionML=[ENU=Page ID;
                                                              NOR=Side-ID;
                                                              SVE=Sid-ID] }
    { 2   ;   ;Name                ;Text250       ;CaptionML=[ENU=Name;
                                                              NOR=Navn;
                                                              SVE=Namn] }
    { 3   ;   ;Order               ;Integer       ;CaptionML=[ENU=Order;
                                                              NOR=Rekkef›lge;
                                                              SVE=Order] }
    { 4   ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=[ENU="Not Completed,Completed,Not Started,Seen,Watched,Read, ";
                                                                    NOR="Ikke fullf›rt,Fullf›rt,Ikke startet,Vist,Overv†ket,Lest, ";
                                                                    SVE="Inte slutf”rd,Slutf”rd,Ej p†b”rjad,Visat,Bevakat,L„st, "];
                                                   OptionString=[Not Completed,Completed,Not Started,Seen,Watched,Read, ] }
    { 5   ;   ;Visible             ;Boolean       ;CaptionML=[ENU=Visible;
                                                              NOR=Synlig;
                                                              SVE=Synlig] }
    { 8   ;   ;Icon                ;Media         ;CaptionML=[ENU=Icon;
                                                              NOR=Ikon;
                                                              SVE=Ikon] }
    { 9   ;   ;Item Type           ;Option        ;InitValue=Setup and Help;
                                                   CaptionML=[ENU=Item Type;
                                                              NOR=Varetype;
                                                              SVE=Artikeltyp];
                                                   OptionCaptionML=[ENU=" ,Group,Setup and Help";
                                                                    NOR=" ,Gruppe,Oppsett og hjelp";
                                                                    SVE=" ,Grupp,Konfiguration och hj„lp"];
                                                   OptionString=[ ,Group,Setup and Help] }
    { 12  ;   ;Assisted Setup Page ID;Integer     ;CaptionML=[ENU=Assisted Setup Page ID;
                                                              NOR=Side-ID for assistert oppsett;
                                                              SVE=Assisterad konfiguration av sid-ID] }
    { 17  ;   ;External Assisted Setup;Boolean    ;CaptionML=[ENU=External Assisted Setup;
                                                              NOR=Eksternt assistert oppsett] }
    { 18  ;   ;Record ID           ;RecordID      ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Record ID;
                                                              NOR=Post-ID;
                                                              SVE=Post-ID] }
  }
  KEYS
  {
    {    ;Page ID                                 ;Clustered=Yes }
    {    ;External Assisted Setup,Order,Visible    }
  }
  FIELDGROUPS
  {
    { 1   ;Brick               ;Name,Status,Icon                         }
  }
  CODE
  {
    VAR
      RunSetupAgainQst@1001 : TextConst '@@@="%1 = Assisted Setup Name";ENU=You have already completed the %1 assisted setup guide. Do you want to run it again?;NOR=Du har allerede fullf›rt den assisterte oppsettsveiledningen for %1. Vil du kj›re den p† nytt?;SVE=Du har redan slutf”rt guiden f”r assisterad konfiguration %1. Vill du k”ra den igen?';

    [External]
    PROCEDURE RunAssistedSetup@4();
    VAR
      AssistedSetup@1000 : Record 1803;
    BEGIN
      IF "Item Type" <> "Item Type"::"Setup and Help" THEN
        EXIT;

      IF Status = Status::Completed THEN
        CASE "Page ID" OF
          PAGE::"Assisted Company Setup Wizard":
            AssistedSetup.HandleOpenCompletedAssistedCompanySetupWizard;
          ELSE
            IF NOT CONFIRM(RunSetupAgainQst,FALSE,Name) THEN
              EXIT;
        END;

      COMMIT;
      PAGE.RUNMODAL("Assisted Setup Page ID");
      OnUpdateAssistedSetupStatus(Rec);
    END;

    [External]
    PROCEDURE AddExtensionAssistedSetup@24(PageID@1007 : Integer;AssistantName@1006 : Text[250];AssistantVisible@1004 : Boolean;AssistedSetupRecordID@1001 : RecordID;AssistedSetupStatus@1000 : Option;AssistedSetupIconCode@1002 : Code[50]);
    VAR
      AssistedSetupIcons@1003 : Record 1810;
    BEGIN
      IF NOT GET(PageID) THEN BEGIN
        CLEAR(Rec);
        "Page ID" := PageID;
        Visible := AssistantVisible;
        INSERT(TRUE);
      END;

      "Page ID" := PageID;
      Name := AssistantName;
      Order := GetNextSortingOrder;
      "Item Type" := "Item Type"::"Setup and Help";

      "Assisted Setup Page ID" := PageID;
      "External Assisted Setup" := TRUE;
      "Record ID" := AssistedSetupRecordID;
      Status := AssistedSetupStatus;

      IF AssistedSetupIcons.GET(AssistedSetupIconCode) THEN
        Icon := AssistedSetupIcons.Image;

      MODIFY(TRUE);
    END;

    [External]
    PROCEDURE InsertAssistedSetupIcon@5(AssistedSetupIconCode@1003 : Code[50];IconInStream@1000 : InStream);
    VAR
      AssistedSetupIcons@1002 : Record 1810;
    BEGIN
      IF NOT AssistedSetupIcons.GET(AssistedSetupIconCode) THEN BEGIN
        AssistedSetupIcons.INIT;
        AssistedSetupIcons."No." := AssistedSetupIconCode;
        AssistedSetupIcons.Image.IMPORTSTREAM(IconInStream,AssistedSetupIconCode);
        AssistedSetupIcons.INSERT(TRUE);
      END ELSE BEGIN
        AssistedSetupIcons.Image.IMPORTSTREAM(IconInStream,AssistedSetupIconCode);
        AssistedSetupIcons.MODIFY(TRUE);
      END;
    END;

    [Integration]
    [External]
    PROCEDURE OnRegisterAssistedSetup@13(VAR TempAggregatedAssistedSetup@1000 : TEMPORARY Record 1808);
    BEGIN
    END;

    [Integration]
    [External]
    PROCEDURE OnUpdateAssistedSetupStatus@8(VAR TempAggregatedAssistedSetup@1000 : TEMPORARY Record 1808);
    BEGIN
    END;

    LOCAL PROCEDURE GetNextSortingOrder@35() SortingOrder : Integer;
    VAR
      AssistedSetup@1000 : Record 1803;
    BEGIN
      SortingOrder := 1;

      AssistedSetup.SETCURRENTKEY(Order);
      IF AssistedSetup.FINDLAST THEN
        SortingOrder := AssistedSetup.Order + 1;
    END;

    [External]
    PROCEDURE SetStatus@3(VAR TempAggregatedAssistedSetup@1002 : TEMPORARY Record 1808;EntryId@1000 : Integer;ItemStatus@1001 : Option);
    BEGIN
      TempAggregatedAssistedSetup.GET(EntryId);
      TempAggregatedAssistedSetup.Status := ItemStatus;
      TempAggregatedAssistedSetup.MODIFY;
    END;

    BEGIN
    END.
  }
}

