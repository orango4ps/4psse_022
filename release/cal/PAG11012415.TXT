OBJECT Page 11012415 Document Filter Card
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Belegartenfilter;
               ENU=Document Filter Card;
               NLD=Documentfilter;
               NOR=Dokumentfilterkort;
               SVE=Dokumentfilterkort];
    SourceTable=Table11012073;
    PageType=ListPlus;
    UsageCategory=Tasks;
    OnInit=BEGIN
             InternalCompanyEditable := TRUE;
           END;

    OnOpenPage=VAR
                 DocumentMgtSetup@1100528700 : Record 11071831;
               BEGIN
                 SETFILTER("User ID",'%1|%2',USERID,'');
                 IF NOT ShowTempFilters THEN BEGIN
                   FILTERGROUP(10);
                   SETRANGE(Temp, FALSE);
                   FILTERGROUP(0);
                 END;

                 DocumentMgtSetup.GET;
                 IF DocumentMgtSetup."Show Only Own Documents" THEN
                   InternalCompanyEditable := FALSE;
               END;

    OnAfterGetRecord=BEGIN
                       PersonalFilter := ("User ID" <> '');
                       OnAfterGetCurrRecord;
                     END;

    OnNewRecord=BEGIN
                  "User ID" := USERID;
                  PersonalFilter := TRUE;

                  InitValueStatusFilter;
                  InitValueFinalPrFilter;
                  InitValueExtDocFilter;
                  "Internal Company" := COMPANYNAME;
                  OnAfterGetCurrRecord;
                END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100528701;1 ;Action    ;
                      CaptionML=[DEU=Belege a&nzeigen;
                                 ENU=S&how Documents;
                                 NLD=Toon documenten;
                                 NOR=V&is dokument;
                                 SVE=V&isa dokument];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=View;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 DocumentFilters@1100528700 : Codeunit 11012432;
                                 DocumentFilter@1100528702 : Record 11012073;
                               BEGIN
                                 IF Temp THEN BEGIN
                                   DocumentFilters.GetCurrentDocumentFilter(DocumentFilter);
                                   IF (DocumentFilter."User ID" = "User ID") AND
                                      (DocumentFilter."Document Filter" = "Document Filter") THEN BEGIN
                                     DocumentFilters.RefreshDocumentFilterResult(Rec);
                                     CurrPage.CLOSE;
                                   END;
                                 END ELSE
                                   DocumentFilters.ExecuteDocumentFilter(Rec, TRUE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1210190000;1;Group  ;
                CaptionML=[DEU=Allgemein;
                           ENU=General;
                           NLD=Algemeen;
                           NOR=Generell;
                           SVE=Allm„nt];
                GroupType=Group }

    { 1210190001;2;Field  ;
                SourceExpr="Document Filter";
                Importance=Promoted }

    { 1100485004;2;Field  ;
                CaptionML=[DEU=Pers”nlicher Filter;
                           ENU=Personal Filter;
                           NLD=Persoonlijk filter;
                           NOR=Personlig filter;
                           SVE=Personligt filter];
                SourceExpr=PersonalFilter;
                OnValidate=BEGIN
                             IF PersonalFilter THEN
                               "User ID" := USERID
                             ELSE
                               "User ID" := '';
                             PersonalFilterOnAfterValidate;
                           END;
                            }

    { 1210190003;2;Field  ;
                SourceExpr=Description;
                Importance=Promoted }

    { 1100528708;2;Field  ;
                SourceExpr=Department;
                Importance=Promoted }

    { 1210190005;2;Field  ;
                CaptionML=[DEU=Status;
                           ENU=Status;
                           NLD=Status;
                           NOR=Status;
                           SVE=Status];
                SourceExpr=StatusFilter;
                OnValidate=BEGIN
                             IF StatusFilter <> '' THEN
                               Status := ReadableOptionsToStatusFilter(StatusFilter)
                             ELSE
                               Status := '';
                           END;
                            }

    { 1210190013;2;Field  ;
                SourceExpr="Contact No.";
                Importance=Promoted }

    { 1210190015;2;Field  ;
                SourceExpr="Contact Person No." }

    { 1100528702;2;Field  ;
                SourceExpr="Document Group" }

    { 1100528704;2;Field  ;
                SourceExpr="Document Type" }

    { 1210190028;2;Field  ;
                SourceExpr="Document Category" }

    { 1210190019;2;Field  ;
                SourceExpr=Sender }

    { 1210190021;2;Field  ;
                SourceExpr=Editor }

    { 1100528706;2;Field  ;
                SourceExpr="Handled by" }

    { 1210190026;2;Field  ;
                CaptionML=[DEU=Definitiv gedruckt;
                           ENU=Final Printed;
                           NLD=Definitief geprint;
                           NOR=Resultat er skrevet ut;
                           SVE=Resultat „r utskrivet];
                SourceExpr=FinalPrintedFilter;
                OnValidate=VAR
                             RecRef@1100528701 : RecordRef;
                             FldRef@1100528702 : FieldRef;
                           BEGIN
                             IF FinalPrintedFilter <> '' THEN BEGIN
                               RecRef.GETTABLE(Rec);
                               FldRef := RecRef.FIELD(FIELDNO("Final Printed"));
                               "Final Printed" := ReadableOptionsToBoolFilter(FinalPrintedFilter, FldRef);
                             END ELSE
                               "Final Printed" := '';
                           END;
                            }

    { 1100528710;2;Field  ;
                CaptionML=[DEU=Externer Beleg;
                           ENU=External Document;
                           NLD=Extern document;
                           NOR=Eksternt dokument;
                           SVE=Externt dokument];
                SourceExpr=ExternalDocumentFilter;
                OnValidate=VAR
                             RecRef@1100528700 : RecordRef;
                             FldRef@1100528701 : FieldRef;
                           BEGIN
                             IF ExternalDocumentFilter <> '' THEN BEGIN
                               RecRef.GETTABLE(Rec);
                               FldRef := RecRef.FIELD(FIELDNO("External Document"));
                               "External Document" := ReadableOptionsToBoolFilter(ExternalDocumentFilter, FldRef);
                             END ELSE
                               "External Document" := '';
                           END;
                            }

    { 1100528200;2;Field  ;
                SourceExpr="Original To" }

    { 1100528703;2;Field  ;
                SourceExpr="Internal Company";
                Editable=InternalCompanyEditable }

    { 1100528700;1;Part   ;
                SubPageLink=User ID=FIELD(User ID),
                            Document Filter=FIELD(Document Filter);
                PagePartID=Page11012418;
                PartType=Page }

  }
  CODE
  {
    VAR
      PersonalFilter@1100485000 : Boolean;
      StatusFilter@1210190001 : Text[100];
      FinalPrintedFilter@1210190000 : Text[100];
      ExternalDocumentFilter@1100528701 : Text[100];
      ShowTempFilters@1100528700 : Boolean;
      InternalCompanyEditable@1100528702 : Boolean INDATASET;

    PROCEDURE SetShowTempFilters@1100528700(NewValue@1100528700 : Boolean);
    BEGIN
      ShowTempFilters := NewValue;
    END;

    LOCAL PROCEDURE PersonalFilterOnAfterValidate@19075822();
    BEGIN
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE OnAfterGetCurrRecord@19077479();
    VAR
      Number@1210190000 : Integer;
    BEGIN
      xRec := Rec;
      IF EVALUATE(Number, COPYSTR(Status, 1, 1)) THEN
        //First character can be converted to integer, this is new-style Statusfilter
        StatusFilter := StatusFilterToReadableOptions(Status)
      ELSE
        //In old days the status field contained language dependent strings.
        //this is changed now (25281) but it is difficult to create an automatic dataconversion
        //Therefore the form must be able to handle the old-style Statusfilter.
        StatusFilter := Status;

      IF EVALUATE(Number, COPYSTR("Final Printed", 1, 1)) THEN
        //First character can be converted to integer, this is new-style Final Printedfilter
        FinalPrintedFilter := FinalPrFilterToReadableOptions("Final Printed")
      ELSE
        //In old days the Final Printed field contained language dependent strings.
        //this is changed now (25281) but it is difficult to create an automatic dataconversion
        //Therefore the form must be able to handle the old-style Final Printedfilter.
        FinalPrintedFilter := "Final Printed";

      ExternalDocumentFilter := FinalPrFilterToReadableOptions("External Document");
    END;

    BEGIN
    END.
  }
}

