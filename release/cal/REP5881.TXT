OBJECT Report 5881 Make Phys. Invt. Recording
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Neue Inventurerfassung erstellen;
               ENU=Make New Phys. Invt. Recording;
               NLD=Nieuwe inventarisatieregistratie maken;
               NOR=Foreta ny registrering for vareopptelling];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  HeaderCount := 0;
                END;

    OnPostReport=BEGIN
                   CASE HeaderCount OF
                     0:
                       MESSAGE(NewOrderNotCreatedMsg);
                     1:
                       MESSAGE(NewOrderCreatedMsg,
                         PhysInvtRecordHeader."Order No.",PhysInvtRecordHeader."Recording No." );
                     ELSE
                       MESSAGE(DifferentOrdersMsg,HeaderCount);
                   END;
                 END;

  }
  DATASET
  {
    { 3530;    ;DataItem;                    ;
               DataItemTable=Table5875;
               DataItemTableView=SORTING(No.);
               OnAfterGetRecord=BEGIN
                                  TESTFIELD(Status,Status::Open);
                                END;

               ReqFilterFields=No. }

    { 8887;1   ;DataItem;                    ;
               DataItemTable=Table5876;
               DataItemTableView=SORTING(Document No.,Line No.);
               OnPreDataItem=BEGIN
                               HeaderInserted := FALSE;
                             END;

               OnAfterGetRecord=BEGIN
                                  IF CheckOrderLine("Phys. Invt. Order Line") THEN BEGIN
                                    IF NOT HeaderInserted THEN BEGIN
                                      InsertRecordingHeader("Phys. Invt. Order Header");
                                      HeaderInserted := TRUE;
                                      NextLineNo := 10000;
                                      HeaderCount := HeaderCount + 1;
                                    END;
                                    InsertRecordingLine("Phys. Invt. Order Line");
                                  END;
                                END;

               ReqFilterFields=Item No.,Location Code,Bin Code,Shelf No.,Inventory Posting Group;
               DataItemLink=Document No.=FIELD(No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 3   ;2   ;Field     ;
                  CaptionML=[DEU=Nur nicht erfasste Zeilen;
                             ENU=Only Lines Not In Recordings;
                             NLD=Alleen regels die zich niet in registraties bevinden;
                             NOR=Bare linjer som ikke finnes i registreringer];
                  ToolTipML=[DEU=Gibt an, dass neue Inventurerfassungszeilen nur erstellt werden, wenn die Daten nicht auf einer anderen Inventurerfassungszeile vorhanden sind. Dies ist nÅtzlich, wenn Sie dafÅr sorgen mîchten, dass unterschiedliche Registrierungen nicht dieselben Artikel enthalten.;
                             ENU=Specifies that a new physical inventory recording lines are only created when the data does not exist on any other physical inventory recording line. This is useful when you want to make sure that different recordings do not contain the same items.;
                             NLD=Hiermee wordt opgegeven dat nieuwe inventarisatieregistratieregels alleen worden gemaakt wanneer de gegevens niet voorkomen op een andere inventarisatieregistratieregel. Dit is handig als u ervoor wilt zorgen dat verschillende registraties niet dezelfde artikelen bevatten.;
                             NOR=Angir at nye vareopptellingsregistreringslinjer bare opprettes nÜr dataene ikke finnes pÜ noen andre vareopptellingsregistreringslinjer. Dette er nyttig nÜr du vil forsikre deg om at ulike registreringer ikke inneholder de samme varene.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=OnlyLinesNotInRecordings }

      { 5   ;2   ;Field     ;
                  CaptionML=[DEU=Registrierung ohne Auftrag zulÑssig;
                             ENU=Allow Recording Without Order;
                             NLD=Registratie zonder order toestaan;
                             NOR=Tillat registrering uten ordre];
                  ToolTipML=[DEU=Gibt an, dass Erfassungszeilen automatisch fÅr Artikel erstellt werden, die nicht im Inventurauftrag vorhanden sind. Dies kann nur passieren, wenn fÅr einen Artikel in der Reihenfolge keiner der Werte in diesen vier Feldern vorhanden ist: Artikelnummer, Variantencode, Standortcode und Lagerortcode.;
                             ENU=Specifies that recording lines are automatically created for items that do not exist on the physical inventory order. This can only happen if none of the values in these four fields exist for an item on the order: Item No., Variant Code, Location Code, and Bin Code.;
                             NLD=Hiermee wordt opgegeven dat er automatisch registratieregels worden gemaakt voor artikelen die niet bestaan in de inventarisatieorder. Dit kan alleen gebeuren als geen van de waarden in deze vier velden bestaan voor een artikel in de order: Artikelnr., Variantcode, Vestigingscode en Opslaglocatiecode.;
                             NOR=Angir at registreringslinjer opprettes automatisk for varer som ikke finnes i vareopptellingsordren. Dette kan bare skje hvis ingen av verdiene i fõlgende fire felt finnes for en vare i ordren: Varenr., Variantkode, Lokasjonskode og Hyllekode.;
                             SVE=Anger att registreringsrader skapas automatiskt fîr artiklar som inte finns pÜ inventeringsordern. Detta kan bara intrÑffa om det inte finns nÜgot vÑrde i dessa fyra fÑlt fîr en artikel pÜ ordern: Artikelnr, Variantkod, LagerstÑllekod och Lagerplatskod.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=AllowRecWithoutOrder }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      NewOrderNotCreatedMsg@1000 : TextConst 'DEU=Es wurde keine Inventurerfassung erstellt, weil es keine gÅltigen Inventurauftragszeilen gibt.;ENU=A physical inventory recording was not created because no valid physical inventory order lines exist.;NLD=Er is geen inventarisatieregistratie gemaakt omdat er geen geldige inventarisatieorderregels zijn.;NOR=En vareopptellingsregistrering ble ikke opprettet fordi det ikke finnes noen gyldige vareopptellingsordrelinjer.';
      NewOrderCreatedMsg@1001 : TextConst '@@@="%1 = Order No. %2 = Recording No.";DEU=Inventurerfassung  %1 %2 wurde erstellt.;ENU=Physical inventory recording %1 %2 has been created.;NLD=Inventarisatieregistratie %1 %2 is gemaakt.;NOR=Vareopptellingsregistrering %1 %2 har blitt opprettet.';
      DifferentOrdersMsg@1002 : TextConst '@@@="%1 = counter";DEU=Es wurden %1 verschiedene AuftrÑge erstellt.;ENU=%1 different orders has been created.;NLD=Er zijn %1 verschillende orders gemaakt.;NOR=%1 forskjellige ordrer er opprettet.';
      PhysInvtRecordHeader@1003 : Record 5877;
      PhysInvtRecordLine@1004 : Record 5878;
      NextLineNo@1005 : Integer;
      HeaderCount@1006 : Integer;
      OnlyLinesNotInRecordings@1007 : Boolean;
      HeaderInserted@1008 : Boolean;
      AllowRecWithoutOrder@1009 : Boolean;

    [External]
    PROCEDURE CheckOrderLine@1(PhysInvtOrderLine@1000 : Record 5876) : Boolean;
    VAR
      PhysInvtRecordLine2@1001 : Record 5878;
    BEGIN
      WITH PhysInvtOrderLine DO BEGIN
        IF EmptyLine THEN
          EXIT(FALSE);
        TESTFIELD("Item No.");
        IF OnlyLinesNotInRecordings THEN BEGIN
          PhysInvtRecordLine2.SETCURRENTKEY(
            "Order No.","Item No.","Variant Code","Location Code","Bin Code");
          PhysInvtRecordLine2.SETRANGE("Order No.","Document No.");
          PhysInvtRecordLine2.SETRANGE("Item No.","Item No.");
          PhysInvtRecordLine2.SETRANGE("Variant Code","Variant Code");
          PhysInvtRecordLine2.SETRANGE("Location Code","Location Code");
          PhysInvtRecordLine2.SETRANGE("Bin Code","Bin Code");
          IF PhysInvtRecordLine2.FINDFIRST THEN
            EXIT(FALSE);
        END;
      END;
      EXIT(TRUE);
    END;

    [External]
    PROCEDURE InsertRecordingHeader@2(PhysInvtOrderHeader@1000 : Record 5875);
    BEGIN
      WITH PhysInvtRecordHeader DO BEGIN
        INIT;
        "Order No." := PhysInvtOrderHeader."No.";
        "Recording No." := 0;
        "Person Responsible" := PhysInvtOrderHeader."Person Responsible";
        "Location Code" := PhysInvtOrderHeader."Location Code";
        "Bin Code" := PhysInvtOrderHeader."Bin Code";
        "Allow Recording Without Order" := AllowRecWithoutOrder;
        INSERT(TRUE);
      END;
    END;

    [External]
    PROCEDURE InsertRecordingLine@3(PhysInvtOrderLine@1000 : Record 5876);
    BEGIN
      WITH PhysInvtRecordLine DO BEGIN
        INIT;
        "Order No." := PhysInvtRecordHeader."Order No.";
        "Recording No." := PhysInvtRecordHeader."Recording No.";
        "Line No." := NextLineNo;
        VALIDATE("Item No.",PhysInvtOrderLine."Item No.");
        VALIDATE("Variant Code",PhysInvtOrderLine."Variant Code");
        VALIDATE("Location Code",PhysInvtOrderLine."Location Code");
        VALIDATE("Bin Code",PhysInvtOrderLine."Bin Code");
        Description := PhysInvtOrderLine.Description;
        "Description 2" := PhysInvtOrderLine."Description 2";
        "Use Item Tracking" := PhysInvtOrderLine."Use Item Tracking";
        VALIDATE("Unit of Measure Code",PhysInvtOrderLine."Base Unit of Measure Code");
        Recorded := FALSE;
        INSERT;
        NextLineNo := NextLineNo + 10000;
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

