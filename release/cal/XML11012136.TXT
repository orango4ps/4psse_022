OBJECT XMLport 11012136 Imp. Service Object
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Imp. Service Object;
               SVE=Imp. serviceobjekt];
    DefaultFieldsValidation=No;
    TextEncoding=WINDOWS;
    OnInitXMLport=BEGIN
                    XmlPortID := 11012136;
                    MainTableID := 11071691;
                    MainTableLookupPageID := 11125145;
                    XMLPortRequestPageID := 11230046
                  END;

    OnPreXMLport=VAR
                   ConversionXMLPort4PS@1100527700 : Record 11020640;
                 BEGIN
                   IF NOT currXMLport.IMPORTFILE THEN BEGIN
                     AddConversionFileFormatLine;
                     ConversionXMLPort4PS.UpdateFromXMLPort(XmlPortID,MainTableID,MainTableLookupPageID,XMLPortRequestPageID,FALSE,0);
                     COMMIT;
                     ERROR('');
                   END;


                   ConvMgt.CheckComCde(CreateNewNosBln,MainTableID,CommentCde);
                   ConvMgt.CheckComCde(UseOldLocNoBln,11012801,LocCommentCde);
                   ConvMgt.CheckComCde(UseOldStdObjNoBln,11020334,StdObjCommentCde);

                   ConvMgt.DeleteConComment(XmlPortID);
                   ConvMgt.CreateConComment(XmlPortID,STRSUBSTNO('Import File: %1.',currXMLport.FILENAME));
                   ConvMgt.CreateConComment(XmlPortID,'Start');
                 END;

    OnPostXMLport=BEGIN
                    ConvMgt.CreateConComment(XmlPortID,'Stop');
                    ConvMgt.CreateConComment(XmlPortID,ConvMgt.CreateNoOfLinesCommentTxt(MainTableID,ImpLineCounter));
                  END;

    Format=Variable Text;
    FieldSeparator=[;];
  }
  ELEMENTS
  {
    { [{755F5B7F-F062-4834-8364-DC4CD51C5A62}];  ;Root                ;Element ;Text     }

    { [{A7DBD164-8D3E-4F8D-B3A0-89F91BC12205}];1 ;ServiceObject       ;Element ;Table   ;
                                                  VariableName=Service-Object;
                                                  SourceTable=Table11071691;
                                                  SourceTableView=SORTING(Field10);
                                                  Import::OnAfterInitRecord=BEGIN
                                                                              ClearFields;
                                                                            END;

                                                  Import::OnBeforeInsertRecord=VAR
                                                                                 ResourceNo@1100529000 : Code[20];
                                                                               BEGIN
                                                                                 StripData;
                                                                                 ImpLineCounter := ImpLineCounter + 1;
                                                                                 CLEAR("Service-Object");

                                                                                 WITH "Service-Object" DO BEGIN
                                                                                   StdObjCde := ConvMgt.DetermineNewCode(UseOldStdObjNoBln,CommentRec."Table Name"::"Standard Object",StdObjCommentCde,B);
                                                                                   LocCde := ConvMgt.DetermineNewCode(UseOldLocNoBln,CommentRec."Table Name"::"Service Location",LocCommentCde,D);

                                                                                   IF ConvMgt.IsStandardObjectPresent(StdObjCde) THEN BEGIN
                                                                                     IF CreateNewNosBln THEN
                                                                                       "No." := ''
                                                                                     ELSE
                                                                                       "No." := A;

                                                                                     INSERT(TRUE);
                                                                                     IF CreateNewNosBln THEN
                                                                                       ConvMgt.FillComment(CommentRec."Table Name"::ServObject,"No.",CommentCde,A);

                                                                                     VALIDATE("Standard Object No.",StdObjCde);

                                                                                     IF C <> '' THEN
                                                                                       VALIDATE(Description,C);

                                                                                     IF (LocCde <> '') AND ConvMgt.IsServiceLocationPresent(LocCde) THEN
                                                                                       VALIDATE("Service Location No.",LocCde)
                                                                                     ELSE
                                                                                       IF D <> '' THEN
                                                                                         ConvMgt.CreateConComment(XmlPortID,ConvMgt.CreateFieldNotFilledCommentTxt(
                                                                                         11012801,
                                                                                         D,
                                                                                         FIELDCAPTION("Service Location No."),
                                                                                         MainTableID,
                                                                                         "No."));

                                                                                     IF E <> '' THEN
                                                                                       VALIDATE("Customer Feature",E);

                                                                                     IF F <> '' THEN BEGIN
                                                                                       IF ConvMgt.IsEmployeePresent(F) THEN BEGIN
                                                                                         //VALIDATE("Preferred Employee",F);  //DP00465.o
                                                                                         IF ConvMgt.IsEmployeeResource(F, ResourceNo) THEN  //DP00465
                                                                                           VALIDATE("Preferred Employee OBS",ResourceNo)
                                                                                         ELSE BEGIN
                                                                                           ConvMgt.CreateConComment(XmlPortID,ConvMgt.CreateFieldNotFilledCommentTxt(
                                                                                           11229278,
                                                                                           F,
                                                                                           FIELDCAPTION("Preferred Employee OBS"),
                                                                                           MainTableID,
                                                                                           "No."));
                                                                                         END;
                                                                                       END ELSE BEGIN
                                                                                         ConvMgt.CreateConComment(XmlPortID,ConvMgt.CreateFieldNotFilledCommentTxt(
                                                                                           5200,
                                                                                           F,
                                                                                           FIELDCAPTION("Preferred Employee OBS"),
                                                                                           MainTableID,
                                                                                           "No."));
                                                                                       END;
                                                                                     END;

                                                                                     VALIDATE(Building,G);
                                                                                     VALIDATE(Floor,H);
                                                                                     VALIDATE(Room,I);

                                                                                     IF JDate <> 0D THEN
                                                                                       VALIDATE("In use since",JDate);
                                                                                     IF K <> '' THEN
                                                                                       VALIDATE("Object Customer",K);

                                                                                     IF LDec <> 0 THEN
                                                                                       VALIDATE(Quantity,LDec);
                                                                                     IF MInt = 1 THEN
                                                                                       VALIDATE(Blocked,TRUE);

                                                                                     IF N <> '' THEN
                                                                                       VALIDATE("Warranty Code Customer",N);
                                                                                     IF ODate <> 0D THEN
                                                                                       VALIDATE("Warranty Starting Date Cust.",ODate);
                                                                                     IF P <> '' THEN
                                                                                       VALIDATE(Text,P);

                                                                                     IF QDate <> 0D THEN
                                                                                       VALIDATE("Replace from",QDate);

                                                                                     MODIFY(TRUE);
                                                                                   END ELSE
                                                                                     ConvMgt.CreateConComment(XmlPortID,ConvMgt.CreateNoRecAddedCommentTxt(
                                                                                      11020334,
                                                                                      B,
                                                                                      MainTableID,
                                                                                      STRSUBSTNO('%1-%2',A,B)));

                                                                                 END;

                                                                                 ClearFields;
                                                                                 currXMLport.SKIP;
                                                                               END;
                                                                                }

    { [{5C900A9C-2240-4551-8D3B-A67A2D629F53}];2 ;A                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{5E5EE9AF-296C-4B48-B3A4-71FD1D3BDBB8}];2 ;B                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{81EF86E1-A401-49BA-998E-C96326459340}];2 ;C                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{A4D141C2-7800-4941-8428-EBDFE695445E}];2 ;D                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{A94DDF8B-AF48-4B38-93FE-471C5E7FDE8B}];2 ;E                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{7009774A-5D5B-4893-B574-E0BA7086A95E}];2 ;F                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{61A54020-5A22-4777-8499-A876D2E08C7F}];2 ;G                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{C3892DEF-6174-4845-A9BD-FA81350F4D61}];2 ;H                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{3B150350-762E-4D06-90AD-0B0A47DC3FEC}];2 ;I                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{079B7881-C2A9-4D33-BDD1-9AC71DDCA4F1}];2 ;J                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  IF J <> '' THEN
                                                                                    EVALUATE(JDate,J);
                                                                                END;
                                                                                 }

    { [{07C9B57E-65F8-4CAD-BA26-FC1EF7C6A2F3}];2 ;K                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{8161E34D-3A46-41B0-8FDD-5E185F9C463E}];2 ;L                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  IF L <> '' THEN
                                                                                    EVALUATE(LDec,L);
                                                                                END;
                                                                                 }

    { [{DB52902E-AEA2-49AF-B81D-A7BFE2B4BCC0}];2 ;M                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  IF M <> '' THEN
                                                                                    EVALUATE(MInt,M);
                                                                                END;
                                                                                 }

    { [{67E04C3C-8FA3-4B7D-90B0-B4F78E627497}];2 ;N                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{02596068-D144-4C72-A2E7-1BFC838E7413}];2 ;O                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  IF O <> '' THEN
                                                                                    EVALUATE(ODate,O);
                                                                                END;
                                                                                 }

    { [{C572BE13-0ECC-4054-926C-D162EC84BA43}];2 ;P                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{63E88C6F-614C-4576-84AC-E6ED90488EA3}];2 ;Q                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  IF Q <> '' THEN
                                                                                    EVALUATE(QDate,Q);
                                                                                END;
                                                                                 }

    { [{4DE894A5-3D25-4067-B986-25835834C5D6}];2 ;R                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{64CC39BC-E41F-4BBE-992C-0F61A1F625B0}];2 ;S                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{A345EA35-9079-4219-A4A9-19ECE6D4D472}];2 ;T                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{80227F89-07D2-4832-82F1-66D5E2AE22F9}];2 ;U                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{A4F20BFC-579F-4D4B-9EF4-0BCEA57D7BB9}];2 ;V                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{CBF4615E-8F0E-43D6-B477-490FB7ECB247}];2 ;W                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{2557FF88-D523-45F8-BFF1-AC27FBFC903D}];2 ;X                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      CaptionML=ENU=Imp. Service Location OBJ;
      OnOpenPage=BEGIN
                   RunIsImportRunBln := TRUE;
                   CreateNewNosBln := TRUE;
                 END;

    }
    CONTROLS
    {
      { 1100409010;;Container;
                  Name=Imp. Service Object;
                  CaptionML=ENU=<Imp. Service Object>;
                  ContainerType=ContentArea }

      { 1100409009;1;Group  ;
                  CaptionML=[ENU=Options;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100525001;2;Field  ;
                  CaptionML=[ENU=Create new Object Nos;
                             SVE=Skapa nya objektnr];
                  SourceExpr=CreateNewNosBln;
                  OnValidate=BEGIN
                               IF NOT CreateNewNosBln THEN
                                 CommentCde := '';
                             END;
                              }

      { 1100525002;2;Field  ;
                  Name=ComCde;
                  CaptionML=[ENU=Comment Code for storing old Object Nos.;
                             SVE=Kommentarskod f”r lagring av gamla objektnr];
                  SourceExpr=CommentCde;
                  TableRelation="Comment Code" }

      { 1100525003;2;Field  ;
                  CaptionML=[ENU=Lookup new Standard Object based on old No.;
                             SVE=Val av nytt standardobjekt baserat p† gammalt nr];
                  SourceExpr=UseOldStdObjNoBln;
                  OnValidate=BEGIN
                               IF NOT UseOldStdObjNoBln THEN
                                 StdObjCommentCde := '';
                             END;
                              }

      { 1100525004;2;Field  ;
                  Name=StdObjCde;
                  CaptionML=[ENU=Comment Code for searching Standard Object No.;
                             SVE=Kommentarskod f”r s”kning efter standardobjektnr];
                  SourceExpr=StdObjCommentCde;
                  TableRelation="Comment Code" }

      { 1100525000;2;Field  ;
                  CaptionML=[ENU=Lookup new Service Location No. based on old Location No.;
                             SVE=Val av nytt serviceplatsnr baserat p† gammalt lagerst„llenr];
                  SourceExpr=UseOldLocNoBln;
                  OnValidate=BEGIN
                               IF NOT UseOldLocNoBln THEN
                                 LocCommentCde := '';
                             END;
                              }

      { 1100525005;2;Field  ;
                  CaptionML=[ENU=Comment Code for searching Service Location No.;
                             SVE=Kommentarskod f”r s”kning efter serviceplatsnr];
                  SourceExpr=LocCommentCde;
                  TableRelation="Comment Code" }

    }
  }
  CODE
  {
    VAR
      CommentRec@1100525036 : Record 97;
      ConvMgt@1100525035 : Codeunit 11020212;
      XmlPortID@1100525032 : Integer;
      MainTableID@1100525031 : Integer;
      MainTableLookupPageID@1100527701 : Integer;
      XMLPortRequestPageID@1100527700 : Integer;
      CreateNewNosBln@1100525030 : Boolean;
      ImpLineCounter@1100525029 : Integer;
      CommentCde@1100525028 : Code[10];
      ConversionFileFormat@1100525027 : Record 11020691;
      RunIsImportRunBln@1100525026 : Boolean;
      UseOldStdObjNoBln@1100525022 : Boolean;
      UseOldLocNoBln@1100525021 : Boolean;
      StdObjCommentCde@1100525020 : Code[20];
      LocCommentCde@1100525019 : Code[20];
      StdObjCde@1100525018 : Code[20];
      LocCde@1100525017 : Code[20];
      JDate@1100525007 : Date;
      LDec@1100525005 : Decimal;
      MInt@1100525004 : Integer;
      ODate@1100525002 : Date;
      QDate@1100525000 : Date;

    PROCEDURE StripData@1100485001();
    BEGIN
      A := DELCHR(A,'<>',' ');
      B := DELCHR(B,'<>',' ');
      C := DELCHR(C,'<>',' ');
      H := DELCHR(H,'<>',' ');
      I := DELCHR(I,'<>',' ');
      K := DELCHR(K,'<>',' ');
      P := DELCHR(P,'<>',' ');
    END;

    PROCEDURE AddConversionFileFormatLine@1100529901();
    BEGIN
      // This function stores the file format into table [Conversion File Format].
      // There is no other standard documentation; so please maintain this function in case of customization.
      //
      // Parameters: 1: [Dataport No.],
      //             2: [csv-Column],
      //             3: [NAV Table ID],
      //             4: [NAV Field Name],
      //             5: [Data Type of dataport field],
      //             6: [extra Comment].
      //
      WITH ConversionFileFormat DO BEGIN
        ConversionFileFormat.DeleteColumnLines(XmlPortID);
        InsertColumnLine(XmlPortID,'A',MainTableID,"Service-Object".FIELDNAME("No."),'Code20','OldOrNewTxt');
        InsertColumnLine(XmlPortID,'B',MainTableID,"Service-Object".FIELDNAME("Standard Object No."),'Code20','OldTxt');
        InsertColumnLine(XmlPortID,'C',MainTableID,"Service-Object".FIELDNAME(Description),'Text100','');
        InsertColumnLine(XmlPortID,'D',MainTableID,"Service-Object".FIELDNAME("Service Location No."),'Code20','OldTxt');
        InsertColumnLine(XmlPortID,'E',MainTableID,"Service-Object".FIELDNAME("Customer Feature"),'Text30','');
        InsertColumnLine(XmlPortID,'F',MainTableID,"Service-Object".FIELDNAME("Preferred Employee OBS"),'Code20','');
        InsertColumnLine(XmlPortID,'G',MainTableID,"Service-Object".FIELDNAME(Building),'Text30','');
        InsertColumnLine(XmlPortID,'H',MainTableID,"Service-Object".FIELDNAME(Floor),'Text30','');
        InsertColumnLine(XmlPortID,'I',MainTableID,"Service-Object".FIELDNAME(Room),'Text30','');
        InsertColumnLine(XmlPortID,'J',MainTableID,"Service-Object".FIELDNAME("In use since"),'Date','');
        InsertColumnLine(XmlPortID,'K',MainTableID,"Service-Object".FIELDNAME("Object Customer"),'Code20','');
        InsertColumnLine(XmlPortID,'L',MainTableID,"Service-Object".FIELDNAME(Quantity),'Decimal','');
        InsertColumnLine(XmlPortID,'M',MainTableID,"Service-Object".FIELDNAME(Blocked),'Integer','OneTrueTxt');
        InsertColumnLine(XmlPortID,'N',MainTableID,"Service-Object".FIELDNAME("Warranty Code Customer"),'Code10','');
        InsertColumnLine(XmlPortID,'O',MainTableID,"Service-Object".FIELDNAME("Warranty Starting Date Cust."),'Date','');
        InsertColumnLine(XmlPortID,'P',MainTableID,"Service-Object".FIELDNAME(Text),'Text250','');
        InsertColumnLine(XmlPortID,'Q',MainTableID,"Service-Object".FIELDNAME("Replace from"),'Date','');

      END;
    END;

    PROCEDURE ClearFields@1100529900();
    BEGIN
      CLEAR("Service-Object");
      StdObjCde := '';
      LocCde := '';

      A := '';
      B := '';
      C := '';
      D := '';
      E := '';
      F := '';
      G := '';
      H := '';
      I := '';
      J := '';
      K := '';
      L := '';
      M := '';
      N := '';
      O := '';
      P := '';
      Q := '';
      JDate := 0D;
      LDec := 0;
      MInt := 0;
      ODate := 0D;
      QDate := 0D;
    END;

    PROCEDURE InitXMLport@1100529500(iCreateNewNosBln@1100529505 : Boolean;iCommentCde@1100529504 : Code[10];iUseOldStdObjNoBln@1100529503 : Boolean;iUseOldLocNoBln@1100529502 : Boolean;iStdObjCommentCde@1100529501 : Code[20];iLocCommentCde@1100529500 : Code[20]);
    BEGIN
      CreateNewNosBln := iCreateNewNosBln;
      CommentCde := iCommentCde;
      UseOldStdObjNoBln := iUseOldStdObjNoBln;
      UseOldLocNoBln := iUseOldLocNoBln;
      StdObjCommentCde := iStdObjCommentCde;
      LocCommentCde := iLocCommentCde;
    END;

    BEGIN
    {
      // Old No.'s are stored in Comment Table (97), marked with [CommentCde].
      // Object No is stored in [XmlPortID]. In case of customisation (copy of dataport) change in OnInitDataport;
      //
      // Please use [AddConversionFileFormatLine] in case Import file format is changed.
    }
    END.
  }
}

