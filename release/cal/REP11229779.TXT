OBJECT Report 11229779 Exchange Estimates
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Kalkulationen ersetzen;
               ENU=Exchange Estimates;
               NLD=Calculaties uitwisselen;
               NOR=Utveksle kalkyler;
               SVE=Byt kalkyler];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   DocumentServiceMgt.SetUseDocumentService4PS(TRUE);
                 END;

    OnPreReport=VAR
                  TempBlob@1100528501 : TEMPORARY Record 99008535;
                  ExchangeEstimates@1100529603 : XMLport 11012162;
                  XmlInStream@1100528502 : InStream;
                  XmlOutStream@1100529601 : OutStream;
                  CustXmlFile@1100528500 : File;
                BEGIN
                  IF Exchange = Exchange::Export THEN BEGIN
                    TempBlob.Blob.CREATEOUTSTREAM(XmlOutStream);
                    CLEARLASTERROR;
                    CLEAR(ExchangeEstimates);
                    ExchangeEstimates.SetEstimateFilters(Estimate);
                    ExchangeEstimates.SETDESTINATION(XmlOutStream);
                    IF NOT ExchangeEstimates.EXPORT THEN
                      ERROR(GETLASTERRORTEXT);

                    ExportStream(TempBlob);
                  END ELSE BEGIN
                    CheckImportFile;

                    CustXmlFile.OPEN(ServerFileName);
                    CustXmlFile.CREATEINSTREAM(XmlInStream);

                    CLEARLASTERROR;
                    CLEAR(ExchangeEstimates);
                    ExchangeEstimates.SetEstimateNo(EstimateNo);
                    ExchangeEstimates.SETSOURCE(XmlInStream);
                    IF NOT ExchangeEstimates.IMPORT THEN BEGIN
                      CustXmlFile.CLOSE;
                      ERROR(GETLASTERRORTEXT);
                    END;
                    CustXmlFile.CLOSE;
                  END;
                END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 1100529600;;DataItem;                  ;
               DataItemTable=Table11012151;
               ReqFilterFields=No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=VAR
                   ClientTypeManagement@1100528500 : Codeunit 4;
                 BEGIN
                   ServerFileName := '';
                   ImportFileURL := '';
                   ImportFolder := '';
                   ImportFileName := '';
                   ImportFileAbsPathName := '';
                   ExportFolder := '';
                   ExportFileName := 'Exchange Estimates' + TxtFileExt;
                   ExportFolderURL := '';

                   ImportOptionsVisible := Exchange = Exchange::Import;
                   ExportOptionsVisible := Exchange = Exchange::Export;
                   ExportFileNameVisible := NOT ClientTypeManagement.IsCommonWebClientType;

                   URLVisible := DocumentServiceMgt.CanConnect;
                   FolderVisible := FileMgt.ClientAvailable;
                 END;

    }
    CONTROLS
    {
      { 1100529600;;Container;
                  ContainerType=ContentArea }

      { 1100529604;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1100529603;2;Field  ;
                  Name=Exchange;
                  CaptionML=[DEU=Ersetze;
                             ENU=Exchange;
                             NLD=Uitwisseling;
                             NOR=Bytt ut;
                             SVE=Byt];
                  OptionCaptionML=[DEU=Exportieren,Importieren;
                                   ENU=Export,Import;
                                   NLD=Exporteren,Importeren;
                                   NOR=Eksporter,Importer;
                                   SVE=Exportera,Importera];
                  SourceExpr=Exchange;
                  OnValidate=BEGIN
                               IF Exchange = Exchange::Export THEN BEGIN
                                 EstimateNo := '';
                                 CLEAR(Estimate);
                               END;
                               EstimateNoEditable := Exchange = Exchange::Import;
                               ImportOptionsVisible := Exchange = Exchange::Import;
                               ExportOptionsVisible := Exchange = Exchange::Export;
                             END;
                              }

      { 1100529602;2;Field  ;
                  Name=EstimateNo;
                  CaptionML=[DEU=Kalkulation;
                             ENU=Estimate;
                             NLD=Calculatie;
                             NOR=Kalkyle;
                             SVE=Kalkyl];
                  SourceExpr=EstimateNo;
                  TableRelation=Estimate;
                  Editable=EstimateNoEditable;
                  OnValidate=VAR
                               EstimateLoc@1100529600 : Record 11012151;
                             BEGIN
                               IF NOT EstimateLoc.GET(EstimateNo) THEN
                                 CLEAR(EstimateLoc);
                               EstimateDescription := EstimateLoc.Description;
                             END;
                              }

      { 1100529601;2;Field  ;
                  Name=EstimateDescription;
                  CaptionML=[DEU=Beschreibung Kalkulation;
                             ENU=Estimate Description;
                             NLD=Calculatieomschrijving;
                             NOR=Kalkylebeskrivelse;
                             SVE=Kalkylbeskrivning];
                  SourceExpr=EstimateDescription;
                  Editable=FALSE }

      { 1100529607;2;Group  ;
                  CaptionML=[DEU=Importieren;
                             ENU=Import;
                             NLD=Import;
                             NOR=Importer;
                             SVE=Importera];
                  Visible=ImportOptionsVisible;
                  GroupType=Group }

      { 1100529605;3;Field  ;
                  Name=ImportFileName;
                  CaptionML=[DEU=Dateiname;
                             ENU=File Name;
                             NLD=Bestandsnaam;
                             NOR=Filnavn;
                             SVE=Filnamn];
                  SourceExpr=ImportFileName;
                  Editable=FALSE;
                  OnAssistEdit=BEGIN
                                 UploadClientFile;
                               END;
                                }

      { 1100529610;3;Group  ;
                  Visible=URLVisible;
                  GroupType=Group }

      { 1100529606;4;Field  ;
                  Name=ImportFileURL;
                  CaptionML=[DEU=Datei-URL;
                             ENU=File URL;
                             NLD=Bestand URL];
                  SourceExpr=ImportFileURL;
                  OnValidate=BEGIN
                               IF ImportFileURL <> '' THEN BEGIN
                                 DocumentServiceMgt.SetDoNotShowError(TRUE);
                                 IF DocumentServiceMgt.IsServiceUri(ImportFileURL) THEN BEGIN
                                   ImportFolder := '';
                                   ImportFileName := '';
                                   ImportFileAbsPathName := '';
                                 END ELSE BEGIN
                                   MESSAGE(URLNotValid);
                                   ImportFileURL := '';
                                 END;
                               END;
                             END;

                  OnAssistEdit=VAR
                                 URLLoc@1100529601 : Text;
                               BEGIN
                                 IF ImportFileURL = '' THEN
                                   URLLoc := DocumentServiceMgt.GetDocumentServiceURL
                                 ELSE
                                   URLLoc := ImportFileURL;
                                 HYPERLINK(URLLoc);
                               END;
                                }

      { 1100529608;2;Group  ;
                  CaptionML=[DEU=Exportieren;
                             ENU=Export;
                             NLD=Export;
                             NOR=Eksport;
                             SVE=Exportera];
                  Visible=ExportOptionsVisible;
                  GroupType=Group }

      { 1100529612;3;Group  ;
                  Visible=FolderVisible;
                  GroupType=Group }

      { 1100529609;4;Field  ;
                  Name=ExportFolder;
                  CaptionML=[DEU=Ordnername;
                             ENU=Folder Name;
                             NLD=Mapnaam;
                             NOR=Mappenavn;
                             SVE=Mappnamn];
                  SourceExpr=ExportFolder;
                  Editable=FALSE;
                  OnAssistEdit=BEGIN
                                 IF FileMgt.SelectFolderDialog('', ExportFolder) THEN
                                   ExportFolderURL := '';
                               END;
                                }

      { 1100529611;3;Field  ;
                  Name=ExportFileName;
                  CaptionML=[DEU=Dateiname;
                             ENU=File Name;
                             NLD=Bestandsnaam;
                             NOR=Filnavn;
                             SVE=Filnamn];
                  SourceExpr=ExportFileName;
                  Visible=ExportFileNameVisible;
                  OnValidate=BEGIN
                               ValidateExportFileName;
                             END;
                              }

      { 1100529614;3;Group  ;
                  Visible=URLVisible;
                  GroupType=Group }

      { 1100529613;4;Field  ;
                  Name=ExportFolderURL;
                  CaptionML=[DEU=Ordner-URL;
                             ENU=Folder URL;
                             NLD=Map URL];
                  SourceExpr=ExportFolderURL;
                  OnValidate=BEGIN
                               IF ExportFolderURL <> '' THEN BEGIN
                                 DocumentServiceMgt.SetDoNotShowError(TRUE);
                                 IF DocumentServiceMgt.IsServiceUri(ExportFolderURL) THEN
                                   ExportFolder := ''
                                 ELSE BEGIN
                                   MESSAGE(URLNotValid);
                                   ExportFolderURL := '';
                                 END;
                               END;
                             END;

                  OnAssistEdit=VAR
                                 URLLoc@1100529601 : Text;
                               BEGIN
                                 IF ExportFolderURL = '' THEN
                                   URLLoc := DocumentServiceMgt.GetDocumentServiceURL
                                 ELSE
                                   URLLoc := ExportFolderURL;
                                 HYPERLINK(URLLoc);
                               END;
                                }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      DocumentServiceMgt@1100529606 : Codeunit 9510;
      FileMgt@1100529608 : Codeunit 419;
      EstimateNoEditable@1100529603 : Boolean INDATASET;
      ImportOptionsVisible@1100529611 : Boolean INDATASET;
      ExportOptionsVisible@1100529607 : Boolean INDATASET;
      FolderVisible@1100529614 : Boolean INDATASET;
      URLVisible@1100529613 : Boolean INDATASET;
      ExportFileNameVisible@1100528500 : Boolean INDATASET;
      Exchange@1100529600 : 'Export,Import';
      EstimateNo@1100529601 : Code[20];
      EstimateDescription@1100529604 : Text[50];
      ServerFileName@1100529602 : Text;
      FileNotSelected@1100529610 : TextConst 'DEU=Datei ist nicht ausgewÑhlt.;ENU=File is not selected.;NLD=Bestand is niet geselecteerd.';
      ImportFileURL@1100529615 : Text;
      ImportFolder@1100529621 : Text;
      ImportFileName@1100529620 : Text;
      ImportFileAbsPathName@1100529619 : Text;
      ExportFolderURL@1100529617 : Text;
      ExportFolder@1100529624 : Text;
      URLNotValid@1100529625 : TextConst 'DEU=Die URL ist ungÅltig.;ENU=The URL is not valid.;NLD=De URL is ongeldig.;NOR=URL-adressen er ikke gyldig.;SVE=Webbadressen Ñr ogiltig.';
      ExportFileName@1100529609 : Text;
      TxtFileExt@1100529612 : TextConst 'DEU=.txt;ENU=.txt;NLD=.txt;NOR=.txt;SVE=.txt';

    LOCAL PROCEDURE UploadClientFile@1100529604();
    BEGIN
      IF FileMgt.ClientAvailable THEN BEGIN
        IF FileMgt.SelectFile(TxtFileExt, ImportFolder, ImportFileName, ImportFileAbsPathName) THEN
          ImportFileURL := ''
        ELSE BEGIN
          ImportFileName := '';
          ImportFileAbsPathName := ImportFolder;
        END;
      END ELSE BEGIN
        ServerFileName := '';
        IF UPLOAD('', '', '', '', ServerFileName) THEN BEGIN
          ImportFileName := FileMgt.GetFileName(ServerFileName);
          ImportFileURL := '';
        END ELSE
          ImportFileName := '';
      END;
    END;

    LOCAL PROCEDURE ValidateExportFileName@1100529602();
    BEGIN
      IF ExportFileName = '' THEN
        ExportFileName := 'Exchange Estimates' + TxtFileExt
      ELSE
        IF FileMgt.GetExtension(ExportFileName) <> TxtFileExt THEN
          ExportFileName := FileMgt.GetFileNameWithoutExtension(ExportFileName) + TxtFileExt;
    END;

    LOCAL PROCEDURE CheckImportFile@1100529601();
    BEGIN
      IF ServerFileName <> '' THEN
        IF FileMgt.ServerFileExists(ServerFileName) THEN
          EXIT;

      IF (ImportFileAbsPathName <> '') AND FileMgt.ClientAvailable THEN BEGIN
        ServerFileName := FileMgt.UploadFileSilentToServerPath(ImportFileAbsPathName, '');
        EXIT;
      END;

      IF DocumentServiceMgt.CanConnect THEN BEGIN
        DocumentServiceMgt.SetDoNotShowError(TRUE);
        IF DocumentServiceMgt.IsServiceUri(ImportFileURL) THEN BEGIN
          ServerFileName := DocumentServiceMgt.DownloadFileFromCloudToServerSilent(ImportFileURL);
          EXIT;
        END;
      END;

      ERROR(FileNotSelected);
    END;

    LOCAL PROCEDURE ExportStream@1100529606(VAR TempBlob@1100528501 : TEMPORARY Record 99008535);
    VAR
      ClientTypeManagement@1100528500 : Codeunit 4;
      InStream@1100528503 : InStream;
    BEGIN
      ValidateExportFileName;

      IF NOT ClientTypeManagement.IsCommonWebClientType AND (ExportFolder <> '') THEN
        IF FileMgt.ClientAvailable AND FileMgt.ClientDirectoryExists(ExportFolder) THEN BEGIN
          FileMgt.BLOBExport4PS(TempBlob, ExportFolder + '\' + ExportFileName, FALSE, TRUE);
          EXIT;
        END;

      TempBlob.Blob.CREATEINSTREAM(InStream);
      IF (DocumentServiceMgt.CanConnect) AND (ExportFolderURL <> '') THEN
        IF DocumentServiceMgt.SaveStream(InStream, '', TRUE) <> '' THEN
          EXIT;

      DOWNLOADFROMSTREAM(InStream, '', '', '', ExportFileName);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

