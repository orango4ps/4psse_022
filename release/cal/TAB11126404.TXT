OBJECT Table 11126404 Subcontr.Port. Service Order
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               SubcontrPortLocation@1100528100 : Record 11126403;
               ServiceOrder@1100528101 : Record 11012823;
               SubcontPortVendor@1100528102 : Record 11126400;
             BEGIN
               IF AutomaticUpdateApplies THEN
                 InsertSubcontrPortLocation;
               SubcontrPortLocation.SETRANGE("Vendor No.", "Vendor No."); //mg, 09-01-2014: T004148
               IF NOT SubcontrPortLocation.ISEMPTY AND //kkleermaker.n, C011450, 131210
                  ("Service Location No." <> '')
               THEN BEGIN
                 IF NOT SubcontrPortLocation.GET("Vendor No.", '', "Service Location No.") THEN
                   ERROR(Text001, ServiceOrder.TABLECAPTION, "Service Order No.", SubcontPortVendor.TABLECAPTION, "Vendor No.");
               END;
             END;

    OnDelete=BEGIN
               IF AutomaticUpdateApplies THEN
                 DeleteSubcontrPortLocation;
             END;

    CaptionML=[ENU=Subcontractor Service Order;
               NOR=Underentrepren›r serviceordre;
               SVE=Underentreprenad, serviceorder];
    LookupPageID=Page11126404;
    DrillDownPageID=Page11126404;
  }
  FIELDS
  {
    { 10  ;   ;Vendor No.          ;Code20        ;TableRelation="Subcont.Port. Vendor"."Vendor No.";
                                                   CaptionML=[ENU=Vendor No.;
                                                              NOR=Leverand›rnr.;
                                                              SVE=Lev.nr];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 20  ;   ;Service Order No.   ;Code20        ;TableRelation="Service Order";
                                                   CaptionML=[ENU=Service Order No.;
                                                              NOR=Serviceordrenr.;
                                                              SVE=Serviceordernr];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 30  ;   ;Service Location No.;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Order"."Service Location No." WHERE (No.=FIELD(Service Order No.)));
                                                   CaptionML=[ENU=Service Location No.;
                                                              NOR=Servicelokasjon Nr.;
                                                              SVE=Serviceplatsnr] }
  }
  KEYS
  {
    {    ;Service Order No.                       ;Clustered=Yes }
    {    ;Vendor No.,Service Order No.             }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100528100 : TextConst 'ENU=%1 %2 cannot be published on Subcontractors Portal, because the service location is not visible for %3 %4.;NOR=%1 %2 kan ikke publiserer p† underleverand›rs portal fordi tjenesten er ikke synlig for %3 %4';

    PROCEDURE InsertSubcontrPortLocation@1100528600();
    VAR
      SubcontrPortLocation@1100528601 : Record 11126403;
    BEGIN
      IF NOT AutomaticUpdateApplies THEN
        EXIT;
      IF SubcontrPortLocation.GET("Vendor No.", '', "Service Location No.") THEN
        EXIT;
      SubcontrPortLocation."Vendor No." := "Vendor No.";
      SubcontrPortLocation.Login := '';
      SubcontrPortLocation."Service Location No." := "Service Location No.";
      SubcontrPortLocation.INSERT(TRUE);
    END;

    PROCEDURE DeleteSubcontrPortLocation@1100528603() : Boolean;
    VAR
      SubcontrPortLocation@1100528601 : Record 11126403;
      SubcontrPortServiceOrder@1100409000 : Record 11126404;
    BEGIN
      IF NOT AutomaticUpdateApplies THEN
        EXIT;
      IF NOT SubcontrPortLocation.GET("Vendor No.", '', "Service Location No.") THEN
        EXIT;
      SubcontrPortServiceOrder.SETCURRENTKEY("Vendor No.");
      SubcontrPortServiceOrder.SETRANGE("Vendor No.", "Vendor No.");
      SubcontrPortServiceOrder.SETFILTER("Service Order No.", '<>%1', "Service Order No.");
      SubcontrPortServiceOrder.SETRANGE("Service Location No.", "Service Location No.");
      IF SubcontrPortServiceOrder.ISEMPTY THEN BEGIN
        SubcontrPortLocation.SETRECFILTER;
        SubcontrPortLocation.SETRANGE(Login);
        SubcontrPortLocation.DELETE(FALSE);
      END;
    END;

    PROCEDURE AutomaticUpdateApplies@1100528610() : Boolean;
    VAR
      SubcontPortSetup@1100528600 : Record 11126402;
    BEGIN
      SubcontPortSetup.GET('1');
      IF NOT SubcontPortSetup."Automatically Update Locations" THEN
        EXIT(FALSE);
      CALCFIELDS("Service Location No.");
      IF "Service Location No." = '' THEN
        EXIT(FALSE);
      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

