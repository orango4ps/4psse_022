OBJECT Table 11071807 Serv. Surv. Loc. Obj. Detail
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TestObjectsNotCreated;
               TestCanInsertDetail;
             END;

    OnModify=BEGIN
               TestObjectsNotCreated;
             END;

    OnDelete=VAR
               ServSurvObjectAddAct@1100528600 : Record 11071814;
               SurvObjAddActProperty@1100528601 : Record 11071955;
             BEGIN
               TestObjectsNotCreated;

               ServSurvObjectAddAct.SETRANGE("Survey No.", "Survey No.");
               ServSurvObjectAddAct.SETRANGE("Location Code", "Location Code");
               ServSurvObjectAddAct.SETRANGE("Seq. No.", "Seq. No.");
               ServSurvObjectAddAct.SETRANGE("Maintenance Scheme Code", "Maintenance Scheme Code");
               ServSurvObjectAddAct.DELETEALL(TRUE);

               SurvObjAddActProperty.SETRANGE("Survey No.", "Survey No.");
               SurvObjAddActProperty.SETRANGE("Location Code", "Location Code");
               SurvObjAddActProperty.SETRANGE("Seq. No.", "Seq. No.");
               SurvObjAddActProperty.SETRANGE("Maintenance Scheme Code", "Maintenance Scheme Code");
               SurvObjAddActProperty.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=Service Survey Location Object Detail;
               NOR=Objektdetalj for servicekartlegginglokasjon;
               SVE=Srv.kartl.plats obj.uppg.];
    LookupPageID=Page11126129;
    DrillDownPageID=Page11126129;
  }
  FIELDS
  {
    { 10  ;   ;Survey No.          ;Code20        ;TableRelation="Service Survey";
                                                   CaptionML=[ENU=Survey No.;
                                                              NOR=Kartlegging nr.;
                                                              SVE=Kartl„ggningsnr];
                                                   NotBlank=Yes }
    { 20  ;   ;Location Code       ;Code10        ;TableRelation="Service Survey Location"."Location Code" WHERE (Survey No.=FIELD(Survey No.));
                                                   CaptionML=[ENU=Location Code;
                                                              NOR=Lokasjonskode;
                                                              SVE=Lagerst„llekod];
                                                   NotBlank=Yes }
    { 30  ;   ;Seq. No.            ;Integer       ;TableRelation="Serv. Surv. Location Object"."Seq. No." WHERE (Survey No.=FIELD(Survey No.),
                                                                                                                 Location Code=FIELD(Location Code));
                                                   OnValidate=VAR
                                                                ServSurvLocationObject@1100528600 : Record 11071813;
                                                              BEGIN
                                                                IF xRec."Seq. No." <> "Seq. No." THEN
                                                                  IF ServSurvLocationObject.GET("Survey No.", "Location Code", "Seq. No.") THEN
                                                                    VALIDATE("Service Package", ServSurvLocationObject."Service Package");
                                                              END;

                                                   CaptionML=[ENU=Seq. No.;
                                                              NOR=Sekv. nr;
                                                              SVE=Sekv. nr];
                                                   NotBlank=Yes }
    { 40  ;   ;Standard Object No. ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Serv. Surv. Location Object"."Standard Object No." WHERE (Survey No.=FIELD(Survey No.),
                                                                                                                                 Location Code=FIELD(Location Code),
                                                                                                                                 Seq. No.=FIELD(Seq. No.)));
                                                   OnValidate=BEGIN
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=[ENU=Standard Object No.;
                                                              NOR=Standardobjektnr;
                                                              SVE=Standardobjektsnr];
                                                   Editable=No }
    { 50  ;   ;Object Description  ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Serv. Surv. Location Object".Description WHERE (Survey No.=FIELD(Survey No.),
                                                                                                                       Location Code=FIELD(Location Code),
                                                                                                                       Seq. No.=FIELD(Seq. No.)));
                                                   CaptionML=[ENU=Object Description;
                                                              NOR=Objektbeskrivelse;
                                                              SVE=Objektbeskrivning];
                                                   Editable=No }
    { 55  ;   ;Object Description 2;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Serv. Surv. Location Object"."Description 2" WHERE (Survey No.=FIELD(Survey No.),
                                                                                                                           Location Code=FIELD(Location Code),
                                                                                                                           Seq. No.=FIELD(Seq. No.)));
                                                   CaptionML=ENU=Object Description 2;
                                                   Editable=No }
    { 59  ;   ;Include in Contract ;Boolean       ;OnValidate=BEGIN
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=[ENU=Include in Contract;
                                                              NOR=Inkluder i kontrakt;
                                                              SVE=Inkludera i kontrakt] }
    { 70  ;   ;Service Package     ;Code10        ;CaptionML=[ENU=Service Package;
                                                              NOR=Servicepakke;
                                                              SVE=Servicepaket];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 80  ;   ;Maintenance Scheme Code;Code20     ;TableRelation="Maintenance Scheme";
                                                   OnValidate=VAR
                                                                MaintenanceScheme@1100528600 : Record 11071745;
                                                                ServSurvObjectAddAct@1100528500 : Record 11071814;
                                                              BEGIN
                                                                IF xRec."Maintenance Scheme Code" <> "Maintenance Scheme Code" THEN BEGIN
                                                                  ServSurvObjectAddAct.SETRANGE("Survey No.", "Survey No.");
                                                                  ServSurvObjectAddAct.SETRANGE("Location Code", "Location Code");
                                                                  ServSurvObjectAddAct.SETRANGE("Seq. No.", "Seq. No.");
                                                                  ServSurvObjectAddAct.SETRANGE("Maintenance Scheme Code", xRec."Maintenance Scheme Code");
                                                                  ServSurvObjectAddAct.DELETEALL(TRUE);
                                                                END;
                                                                IF "Maintenance Scheme Code" <> '' THEN BEGIN
                                                                  IF NOT ServSurvLocationObject.GET("Survey No.", "Location Code", "Seq. No.") THEN
                                                                    ServSurvLocationObject.INIT;
                                                                  IF ServSurvLocationObject."Include in Contract" = ServSurvLocationObject."Include in Contract"::"Only Service Order" THEN
                                                                    ServSurvLocationObject.FIELDERROR("Include in Contract");
                                                                  MaintenanceScheme.GET("Maintenance Scheme Code");
                                                                  MaintenanceScheme.TESTFIELD(Expired, FALSE);
                                                                  VALIDATE("Maintenance Interval", MaintenanceScheme."Maintenance Interval");
                                                                  IF MaintenanceScheme."Survey Template" THEN
                                                                    CopyMaintenanceSchemeVersionLinesToHigherLevel;
                                                                END;
                                                                UpdateTotals;
                                                              END;

                                                   OnLookup=VAR
                                                              StdObjectMaintenanceScheme@1100528600 : Record 11071703;
                                                              MaintenanceScheme@1100528601 : Record 11071745;
                                                            BEGIN
                                                              CALCFIELDS("Standard Object No.");
                                                              StdObjectMaintenanceScheme.SETRANGE("Object No.", "Standard Object No.");
                                                              StdObjectMaintenanceScheme.SETRANGE("Service Package", "Service Package");
                                                              StdObjectMaintenanceScheme.SETRANGE(Subcontracting, Subcontracting);
                                                              IF StdObjectMaintenanceScheme.FINDSET THEN
                                                                REPEAT
                                                                  MaintenanceScheme.Code := StdObjectMaintenanceScheme."Maintenance Scheme";
                                                                  MaintenanceScheme.MARK(TRUE);
                                                                UNTIL StdObjectMaintenanceScheme.NEXT = 0;
                                                              MaintenanceScheme.MARKEDONLY(TRUE);
                                                              IF "Maintenance Scheme Code" <> '' THEN
                                                                IF MaintenanceScheme.GET("Maintenance Scheme Code") THEN;
                                                              IF PAGE.RUNMODAL(0, MaintenanceScheme) = ACTION::LookupOK THEN
                                                                VALIDATE("Maintenance Scheme Code", MaintenanceScheme.Code);
                                                            END;

                                                   CaptionML=[ENU=Maintenance Scheme Code;
                                                              NOR=Kode for vedlikeholdsskjema;
                                                              SVE=Kod f”r underh†llsschema];
                                                   NotBlank=Yes }
    { 90  ;   ;Maintenance Scheme Description;Text50;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Maintenance Scheme".Description WHERE (Code=FIELD(Maintenance Scheme Code)));
                                                   CaptionML=[ENU=Maintenance Scheme Description;
                                                              NOR=Beskrivelse av vedlikeholdskjema;
                                                              SVE=Beskrivning av underh†llsschema];
                                                   Editable=No }
    { 100 ;   ;Maintenance Interval;DateFormula   ;OnValidate=BEGIN
                                                                UpdateFrequencyPerYear;
                                                                UpdateTotals;
                                                                UpdateMaintenanceIntervalSurveyObjAddAct;
                                                              END;

                                                   CaptionML=[ENU=Maintenance Interval;
                                                              NOR=Vedlikeholdsintervall;
                                                              SVE=Underh†llsintervall] }
    { 105 ;   ;Frequency per Year  ;Decimal       ;CaptionML=[ENU=Frequency per Year;
                                                              NOR=Frekvens per †r;
                                                              SVE=Frekvens per †r];
                                                   Editable=No }
    { 110 ;   ;Labor per Service   ;Decimal       ;CaptionML=[ENU=Labor per Service;
                                                              NOR=Arbeidskraft per service;
                                                              SVE=Arbetskraft per service];
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 120 ;   ;Hours per Service   ;Decimal       ;CaptionML=[ENU=Hours per Service;
                                                              NOR=Timer per service;
                                                              SVE=Timmar per service];
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 130 ;   ;Material per Service;Decimal       ;CaptionML=[ENU=Material per Service;
                                                              NOR=Meteriale per service;
                                                              SVE=Material per service];
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 140 ;   ;Subcontracting per Service;Decimal ;CaptionML=[ENU=Subcontracting per Service;
                                                              NOR=Underentrepen›r per service;
                                                              SVE=Underentreprenad per service];
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 150 ;   ;Plant per Service   ;Decimal       ;CaptionML=[ENU=Plant per Service;
                                                              NOR=Maskin per service;
                                                              SVE=Maskin per service];
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 160 ;   ;Sundry per Service  ;Decimal       ;CaptionML=[ENU=Sundry per Service;
                                                              NOR=Diverse per service;
                                                              SVE=Diverse per service];
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 170 ;   ;Total Cost per Service;Decimal     ;CaptionML=[ENU=Total Cost per Service;
                                                              NOR=Totalkostnad per service;
                                                              SVE=Totalkostnad per service];
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 180 ;   ;Total Labor         ;Decimal       ;FieldClass=Normal;
                                                   CaptionML=[ENU=Total Labor;
                                                              NOR=Total arbeidskraft;
                                                              SVE=Total arbetskraft];
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 190 ;   ;Total Hours         ;Decimal       ;CaptionML=[ENU=Total Hours;
                                                              NOR=Timer totalt;
                                                              SVE=Timmar totalt];
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 200 ;   ;Total Material      ;Decimal       ;CaptionML=[ENU=Total Material;
                                                              NOR=Totalt materiale;
                                                              SVE=Totalt material];
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 210 ;   ;Total Subcontracting;Decimal       ;CaptionML=[ENU=Total Subcontracting;
                                                              NOR=Underleverand›r Total;
                                                              SVE=Underentreprenad totalt];
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 220 ;   ;Total Plant         ;Decimal       ;CaptionML=[ENU=Total Plant;
                                                              NOR=Maskin total;
                                                              SVE=Maskin totalt];
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 230 ;   ;Total Sundry        ;Decimal       ;CaptionML=[ENU=Total Sundry;
                                                              NOR=Totalt diverse;
                                                              SVE=Totalt diverse];
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 240 ;   ;Total Cost          ;Decimal       ;CaptionML=[ENU=Total Cost;
                                                              NOR=Total kost;
                                                              SVE=Totalkostnad];
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 250 ;   ;Surcharge per Service;Decimal      ;CaptionML=[ENU=Surcharge per Service;
                                                              NOR=Tilleggsavgift per Service;
                                                              SVE=Till„ggsbelopp per service];
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 251 ;   ;Surcharge per Service (Labor);Decimal;
                                                   CaptionML=ENU=Surcharge per Service (Labor);
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 252 ;   ;Surcharge per Service (Mat.);Decimal;
                                                   CaptionML=ENU=Surcharge per Service (Material);
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 253 ;   ;Surcharge per Service (Subc.);Decimal;
                                                   CaptionML=ENU=Surcharge per Service (Subcontracting);
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 254 ;   ;Surcharge per Service (Plant);Decimal;
                                                   CaptionML=ENU=Surcharge per Service (Plant);
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 255 ;   ;Surcharge per Service (Sundry);Decimal;
                                                   CaptionML=ENU=Surcharge per Service (Sundry);
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 260 ;   ;Total Surcharge     ;Decimal       ;CaptionML=[ENU=Total Surcharge;
                                                              NOR=Total tilleggsavgift;
                                                              SVE=Till„ggsavgifter totalt];
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 261 ;   ;Total Surcharge (Labor);Decimal    ;CaptionML=ENU=Total Surcharge (Labor);
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 262 ;   ;Total Surcharge (Material);Decimal ;CaptionML=ENU=Total Surcharge (Material);
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 263 ;   ;Total Surcharge (Subcontr.);Decimal;CaptionML=ENU=Total Surcharge (Subcontracting);
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 264 ;   ;Total Surcharge (Plant);Decimal    ;CaptionML=ENU=Total Surcharge (Plant);
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 265 ;   ;Total Surcharge (Sundry);Decimal   ;CaptionML=ENU=Total Surcharge (Sundry);
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 270 ;   ;Sales Surcharge per Service;Decimal;CaptionML=[ENU=Sales Surcharge per Service;
                                                              NOR=Salgstilleggsavgift per Service;
                                                              SVE=F”rs.till„ggsbel. per serv.];
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 271 ;   ;Sales Surch. per Serv. (Labor);Decimal;
                                                   CaptionML=ENU=Sales Surcharge per Service (Labor);
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 272 ;   ;Sales Surch. per Serv. (Mat.);Decimal;
                                                   CaptionML=ENU=Sales Surcharge per Service (Material);
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 273 ;   ;Sales Surch. per Serv. (Subc.);Decimal;
                                                   CaptionML=ENU=Sales Surcharge per Service (Subcontracting);
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 274 ;   ;Sales Surch. per Serv. (Plant);Decimal;
                                                   CaptionML=ENU=Sales Surcharge per Service (Plant);
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 275 ;   ;Sales Surch. per Serv. (Sun.);Decimal;
                                                   CaptionML=ENU=Sales Surcharge per Service (Sundry);
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 280 ;   ;Total Sales Surcharge;Decimal      ;CaptionML=[ENU=Total Sales Surcharge;
                                                              NOR=Totalt salgstilleggsavgift;
                                                              SVE=Totalt f”rs„ljningstill„gg];
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 281 ;   ;Total Sales Surch. (Labor);Decimal ;CaptionML=ENU=Total Sales Surcharge (Labor);
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 282 ;   ;Total Sales Surch. (Material);Decimal;
                                                   CaptionML=ENU=Total Sales Surcharge (Material);
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 283 ;   ;Total Sales Surch. (Subcontr.);Decimal;
                                                   CaptionML=ENU=Total Sales Surcharge (Subcontracting);
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 284 ;   ;Total Sales Surch. (Plant);Decimal ;CaptionML=ENU=Total Sales Surcharge (Plant);
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 285 ;   ;Total Sales Surch. (Sundry);Decimal;CaptionML=ENU=Total Sales Surcharge (Sundry);
                                                   DecimalPlaces=2:5;
                                                   Editable=No }
    { 290 ;   ;Subcontracting      ;Boolean       ;CaptionML=[ENU=Subcontracting;
                                                              NOR=Underentrepen›r;
                                                              SVE=Underentreprenad] }
    { 300 ;   ;Subcontractor No.   ;Code20        ;TableRelation=Vendor;
                                                   CaptionML=[ENU=Subcontractor No.;
                                                              NOR=Underleverand›rnr.;
                                                              SVE=Underleverant”rsnr] }
    { 310 ;   ;Object Customer     ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Serv. Surv. Location Object"."Object Customer" WHERE (Survey No.=FIELD(Survey No.),
                                                                                                                             Location Code=FIELD(Location Code),
                                                                                                                             Seq. No.=FIELD(Seq. No.)));
                                                   CaptionML=[ENU=Object Customer;
                                                              NOR=Objektkunde;
                                                              SVE=Objektkund];
                                                   Editable=No }
    { 320 ;   ;Customer Feature    ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Serv. Surv. Location Object"."Customer Feature" WHERE (Survey No.=FIELD(Survey No.),
                                                                                                                              Location Code=FIELD(Location Code),
                                                                                                                              Seq. No.=FIELD(Seq. No.)));
                                                   CaptionML=[ENU=Customer Feature;
                                                              NOR=Kundefunksjon;
                                                              SVE=Kundfunktion];
                                                   Editable=No }
    { 330 ;   ;Quantity per Object ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Serv. Surv. Location Object"."Quantity per Object" WHERE (Survey No.=FIELD(Survey No.),
                                                                                                                                 Location Code=FIELD(Location Code),
                                                                                                                                 Seq. No.=FIELD(Seq. No.)));
                                                   CaptionML=[ENU=Quantity per Object;
                                                              SVE=Antal per objekt];
                                                   BlankZero=Yes;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Survey No.,Location Code,Seq. No.,Maintenance Scheme Code;
                                                   SumIndexFields=Total Cost,Total Surcharge,Total Sales Surcharge,Total Hours,Total Material,Total Labor,Total Plant,Total Subcontracting,Total Sundry;
                                                   MaintainSIFTIndex=No;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ServSurvLocationObject@1100528600 : Record 11071813;

    PROCEDURE UpdateTotals@1100528600();
    VAR
      ServiceSurveyManagement@1100528600 : Codeunit 11012852;
    BEGIN
      "Labor per Service" := 0;
      "Hours per Service" := 0;
      "Material per Service" := 0;
      "Subcontracting per Service" := 0;
      "Plant per Service" := 0;
      "Sundry per Service" := 0;
      "Total Cost per Service" := 0;
      CALCFIELDS("Quantity per Object");

      ServiceSurveyManagement.GetCostObjectDetail(Rec, "Labor per Service", "Hours per Service", "Material per Service",
        "Subcontracting per Service", "Plant per Service", "Sundry per Service", "Total Cost per Service");

      "Surcharge per Service" :=
        ServiceSurveyManagement.GetSurveySurchargeAmounts(Rec,
          0, "Surcharge per Service (Labor)", "Surcharge per Service (Mat.)", "Surcharge per Service (Subc.)",
          "Surcharge per Service (Plant)", "Surcharge per Service (Sundry)");

      "Sales Surcharge per Service" :=
        ServiceSurveyManagement.GetSurveySurchargeAmounts(Rec,
          1, "Sales Surch. per Serv. (Labor)", "Sales Surch. per Serv. (Mat.)", "Sales Surch. per Serv. (Subc.)",
          "Sales Surch. per Serv. (Plant)", "Sales Surch. per Serv. (Sun.)");

      "Total Labor" := "Labor per Service" * "Frequency per Year" * "Quantity per Object";
      "Total Hours" := "Hours per Service" * "Frequency per Year" * "Quantity per Object";
      "Total Material" := "Material per Service" * "Frequency per Year" * "Quantity per Object";
      "Total Subcontracting" := "Subcontracting per Service" * "Frequency per Year" * "Quantity per Object";
      "Total Plant" := "Plant per Service" * "Frequency per Year" * "Quantity per Object";
      "Total Sundry" := "Sundry per Service" * "Frequency per Year" * "Quantity per Object";
      "Total Cost" := "Total Cost per Service" * "Frequency per Year" * "Quantity per Object";

      "Total Surcharge" := "Surcharge per Service" * "Frequency per Year" * "Quantity per Object";
      "Total Surcharge (Labor)" := "Surcharge per Service (Labor)" * "Frequency per Year" * "Quantity per Object";
      "Total Surcharge (Material)" := "Surcharge per Service (Mat.)" * "Frequency per Year" * "Quantity per Object";
      "Total Surcharge (Subcontr.)" := "Surcharge per Service (Subc.)" * "Frequency per Year" * "Quantity per Object";
      "Total Surcharge (Plant)" := "Surcharge per Service (Plant)" * "Frequency per Year" * "Quantity per Object";
      "Total Surcharge (Sundry)" := "Surcharge per Service (Sundry)" * "Frequency per Year" * "Quantity per Object";

      "Total Sales Surcharge" := "Sales Surcharge per Service" * "Frequency per Year" * "Quantity per Object";
      "Total Sales Surch. (Labor)" := "Sales Surch. per Serv. (Labor)" * "Frequency per Year" * "Quantity per Object";
      "Total Sales Surch. (Material)" := "Sales Surch. per Serv. (Mat.)" * "Frequency per Year" * "Quantity per Object";
      "Total Sales Surch. (Subcontr.)" := "Sales Surch. per Serv. (Subc.)" * "Frequency per Year" * "Quantity per Object";
      "Total Sales Surch. (Plant)" := "Sales Surch. per Serv. (Plant)" * "Frequency per Year" * "Quantity per Object";
      "Total Sales Surch. (Sundry)" := "Sales Surch. per Serv. (Sun.)" * "Frequency per Year" * "Quantity per Object";
    END;

    PROCEDURE UpdateTotalsObject@1100528601();
    VAR
      ServSurvLocationObject@1100528600 : Record 11071813;
    BEGIN
      IF ServSurvLocationObject.GET("Survey No.", "Location Code", "Seq. No.") THEN BEGIN
        ServSurvLocationObject.UpdateTotals;
        ServSurvLocationObject.MODIFY;
      END;
    END;

    PROCEDURE UpdateFrequencyPerYear@1100485000();
    VAR
      MaintenanceManagement@1100528600 : Codeunit 11012829;
    BEGIN
      VALIDATE("Frequency per Year", MaintenanceManagement.ConvPeriodToTimesPerYear("Maintenance Interval"));
    END;

    PROCEDURE UpdateMaintenanceIntervalSurveyObjAddAct@1100528800();
    VAR
      ServSurvObjectAddAct@1100528800 : Record 11071814;
    BEGIN
      ServSurvObjectAddAct.SETRANGE("Survey No.", "Survey No.");
      ServSurvObjectAddAct.SETRANGE("Location Code", "Location Code");
      ServSurvObjectAddAct.SETRANGE("Seq. No.", "Seq. No.");
      ServSurvObjectAddAct.SETRANGE("Maintenance Scheme Code", "Maintenance Scheme Code");
      IF ServSurvObjectAddAct.FINDSET(TRUE, FALSE) THEN BEGIN
        REPEAT
          IF ServSurvObjectAddAct."Maintenance Interval" <> "Maintenance Interval" THEN BEGIN
            ServSurvObjectAddAct.VALIDATE("Maintenance Interval", "Maintenance Interval");
            ServSurvObjectAddAct.MODIFY(TRUE);
          END;
        UNTIL ServSurvObjectAddAct.NEXT = 0;
      END;
    END;

    PROCEDURE TestObjectsNotCreated@1100528603();
    VAR
      ServSurvLocationObject@1100528600 : Record 11071813;
    BEGIN
      IF ServSurvLocationObject.GET("Survey No.", "Location Code", "Seq. No.") THEN
        IF ServSurvLocationObject."Existing Service Object No." = '' THEN
          ServSurvLocationObject.TESTFIELD("Objects Created", FALSE);
    END;

    LOCAL PROCEDURE TestCanInsertDetail@1100528604();
    VAR
      ServSurvLocationObject@1100528600 : Record 11071813;
    BEGIN
      IF ServSurvLocationObject.GET("Survey No.", "Location Code", "Seq. No.") THEN
        IF "Maintenance Scheme Code" <> '' THEN
          ServSurvLocationObject.TESTFIELD("Include in Contract", ServSurvLocationObject."Include in Contract"::"With Maintenance");
    END;

    PROCEDURE CopyMaintenanceSchemeVersionLinesToHigherLevel@1100528602();
    VAR
      MaintenanceSchemeVersion@1100528601 : Record 11071743;
      MaintSchemeVersionLine@1100528600 : Record 11071744;
      ServSurvObjectAddAct@1100528603 : Record 11071814;
      ServiceSurvey@1100528602 : Record 11071803;
      LineNo@1100528604 : Integer;
    BEGIN
      IF "Maintenance Scheme Code" = '' THEN
        EXIT;
      ServiceSurvey.GET("Survey No.");
      ServiceSurvey.TESTFIELD("Reference Date");

      MaintenanceSchemeVersion.SETRANGE("Line Type", MaintenanceSchemeVersion."Line Type"::"Maintenance Scheme");
      MaintenanceSchemeVersion.SETRANGE(Code, "Maintenance Scheme Code");
      MaintenanceSchemeVersion.SETRANGE("Belongs to Scheme", '');
      MaintenanceSchemeVersion.SETFILTER("Index Date", '<=%1', ServiceSurvey."Reference Date");
      MaintenanceSchemeVersion.SETRANGE(Expired, FALSE);
      IF NOT MaintenanceSchemeVersion.FINDLAST THEN
        EXIT;

      MaintSchemeVersionLine.SETRANGE("Line Type", MaintenanceSchemeVersion."Line Type");
      MaintSchemeVersionLine.SETRANGE("Type Code", MaintenanceSchemeVersion.Code);
      MaintSchemeVersionLine.SETRANGE("Belongs to Scheme", '');
      MaintSchemeVersionLine.SETRANGE("Index Date", MaintenanceSchemeVersion."Index Date");
      IF MaintSchemeVersionLine.FINDSET THEN
        REPEAT
          ServSurvObjectAddAct.INIT;
          ServSurvObjectAddAct."Survey No." := "Survey No.";
          ServSurvObjectAddAct."Location Code" := "Location Code";
          ServSurvObjectAddAct."Seq. No." := "Seq. No.";
          ServSurvObjectAddAct."Maintenance Scheme Code" := "Maintenance Scheme Code";
          ServSurvObjectAddAct."Line No." := LineNo + 10000;
          ServSurvObjectAddAct."Line Type" := ServSurvObjectAddAct."Line Type"::"Survey Object";
          ServSurvObjectAddAct.INSERT(TRUE);

          ServSurvObjectAddAct."Cost Type" := MaintSchemeVersionLine."Cost Type";
          ServSurvObjectAddAct."Unit of Measure" := MaintSchemeVersionLine."Unit of Measure";
          ServSurvObjectAddAct.VALIDATE("Maintenance Interval", "Maintenance Interval");
          IF ServSurvObjectAddAct."Cost Type" = ServSurvObjectAddAct."Cost Type"::Labor THEN BEGIN
            IF MaintSchemeVersionLine.Norm <> 0 THEN
              ServSurvObjectAddAct.VALIDATE(Norm, MaintSchemeVersionLine.Norm);
            IF MaintSchemeVersionLine."Rate Code" <> '' THEN
              ServSurvObjectAddAct.VALIDATE("Rate Code", MaintSchemeVersionLine."Rate Code")
            ELSE
              ServSurvObjectAddAct.VALIDATE(Rate, MaintSchemeVersionLine.Rate);
          END;
          ServSurvObjectAddAct.VALIDATE("Cost Object", MaintSchemeVersionLine."Cost Object");
          ServSurvObjectAddAct.VALIDATE("Cost Component", MaintSchemeVersionLine."Cost Component");
          ServSurvObjectAddAct.VALIDATE("Item No.", MaintSchemeVersionLine."Item No.");
          ServSurvObjectAddAct.VALIDATE(Manufacturer, MaintSchemeVersionLine.Manufacturer);
          ServSurvObjectAddAct.VALIDATE("Basic Item", MaintSchemeVersionLine."Basic Item");
          ServSurvObjectAddAct.VALIDATE("Vendor (Trade Item)", MaintSchemeVersionLine."Vendor (Trade Item)");
          ServSurvObjectAddAct.VALIDATE("Trade Item", MaintSchemeVersionLine."Trade Item");
          ServSurvObjectAddAct.VALIDATE(Vendor, MaintSchemeVersionLine.Vendor);
          IF (ServSurvObjectAddAct."Cost Type" <> ServSurvObjectAddAct."Cost Type"::Labor) AND
             (((MaintSchemeVersionLine."Basic Item" = '') AND (MaintSchemeVersionLine."Trade Item" = '') AND (MaintSchemeVersionLine."Item No." = '')) OR
              MaintSchemeVersionLine."Price Agreement")
          THEN BEGIN
            ServSurvObjectAddAct.VALIDATE("Gross Price", MaintSchemeVersionLine."Gross Price");
            ServSurvObjectAddAct.VALIDATE("Purchase Discount %", MaintSchemeVersionLine."Purchase Discount %");
            ServSurvObjectAddAct.VALIDATE("Cost Price", MaintSchemeVersionLine."Cost Price");
          END;
          ServSurvObjectAddAct.Description := MaintSchemeVersionLine.Description;
          IF MaintSchemeVersionLine."Quantity (Survey)" <> 0 THEN
            ServSurvObjectAddAct.VALIDATE(Quantity, MaintSchemeVersionLine."Quantity (Survey)");
          ServSurvObjectAddAct.MODIFY(TRUE);
          LineNo += 10000;
        UNTIL MaintSchemeVersionLine.NEXT = 0;
    END;

    PROCEDURE InitRecord@1100528605();
    VAR
      ServiceSetup@1100528601 : Record 11012800;
      ServSurvLocationObject@1100528600 : Record 11071813;
      ServiceSurvey@1100527350 : Record 11071803;
    BEGIN
      IF ServSurvLocationObject.GET("Survey No.", "Location Code", "Seq. No.") THEN
        "Service Package" := ServSurvLocationObject."Service Package";
      ServiceSetup.GET;
      "Include in Contract" := ServiceSetup."Include in Contract (Scheme)";
      IF ServiceSurvey.GET("Survey No.") THEN
        IF ServiceSurvey."Existing Contract No." <> '' THEN
          "Include in Contract" := TRUE;
    END;

    BEGIN
    END.
  }
}

