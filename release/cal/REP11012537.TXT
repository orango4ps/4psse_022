OBJECT Report 11012537 Update Plot Options
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Aktualisieren Baunummeroptionen;
               ENU=Update Plot Options;
               NLD=Bijwerken bouwnummer opties;
               NOR=Oppdater tegningsalternativer;
               SVE=Uppdatera Ritningsalternativ];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 8405;    ;DataItem;ModelOption         ;
               DataItemTable=Table11012502;
               DataItemTableView=SORTING(Project No.,Plot No.,House Model,Main Group,Group,Sub Group,Option);
               OnAfterGetRecord=VAR
                                  Plot@1100530001 : Record 11012500;
                                  PlotOption@1100530000 : Record 11012502;
                                  OptionBudget@1100530004 : Record 11012503;
                                  OptionSurchRec@1100530005 : Record 11012512;
                                  BudgetSummarySheet@1100530006 : Record 11012064;
                                  BuyersManagementCU@1100530003 : Codeunit 11012500;
                                BEGIN
                                  Plot.SETRANGE("Project No.", "Project No.");
                                  Plot.SETFILTER("Plot No.", PlotFilter);
                                  IF Plot.FINDSET THEN
                                    REPEAT
                                      IF PlotOption.GET("Project No.", Plot."Plot No.", "House Model", "Main Group", Group, "Sub Group", Option) THEN BEGIN
                                        IF PlotOption.Status < PlotOption.Status::Order THEN BEGIN
                                          IF UpdateCostPrice THEN BEGIN
                                            PlotOption.UpdateWorkBudgetOnChanges(PlotOption, TRUE, FALSE);

                                            OptionBudget.SETRANGE("Project No.", "Project No.");
                                            OptionBudget.SETRANGE("Plot No.", Plot."Plot No.");
                                            OptionBudget.SETRANGE("House Model", "House Model");
                                            OptionBudget.SETRANGE("Main Group", "Main Group");
                                            OptionBudget.SETRANGE(Group, Group);
                                            OptionBudget.SETRANGE("Sub Group", "Sub Group");
                                            OptionBudget.SETRANGE(Option, Option);
                                            OptionBudget.DELETEALL;

                                            OptionSurchRec.SETRANGE("Project No.", "Project No.");
                                            OptionSurchRec.SETRANGE("Plot No.", Plot."Plot No.");
                                            OptionSurchRec.SETRANGE("House Model", "House Model");
                                            OptionSurchRec.SETRANGE("Main Group", "Main Group");
                                            OptionSurchRec.SETRANGE(Group, Group);
                                            OptionSurchRec.SETRANGE("Sub Group", "Sub Group");
                                            OptionSurchRec.SETRANGE(Option, Option);
                                            OptionSurchRec.DELETEALL;

                                            BudgetSummarySheet.SETRANGE("Project No.", "Project No.");
                                            BudgetSummarySheet.SETRANGE("Contract No.", '');
                                            BudgetSummarySheet.SETRANGE("Plot No.", Plot."Plot No.");
                                            BudgetSummarySheet.SETRANGE("House Model", "House Model");
                                            BudgetSummarySheet.SETRANGE("Main Group", "Main Group");
                                            BudgetSummarySheet.SETRANGE(Group, Group);
                                            BudgetSummarySheet.SETRANGE("Sub Group", "Sub Group");
                                            BudgetSummarySheet.SETRANGE(Option, Option);
                                            BudgetSummarySheet.DELETEALL;

                                            BuyersManagementCU.BmCopyOptionBudget(ModelOption, "Project No.", Plot."Plot No.", "House Model",
                                              "Main Group", Group, "Sub Group", Option);
                                            BuyersManagementCU.BmCopyOptionSummarySheet(ModelOption, "Project No.", Plot."Plot No.", "House Model",
                                              "Main Group", Group, "Sub Group", Option);
                                            BuyersManagementCU.BmCopyOptionSurcharges(ModelOption, "Project No.", Plot."Plot No.", "House Model",
                                              "Main Group", Group, "Sub Group", Option);

                                            BuyersManagementCU.BmCalcSurchargesUpdateOption(
                                              "Project No.", Plot."Plot No.", "House Model", "Main Group", Group, "Sub Group", Option, FALSE, OptionSurchRec);
                                            BuyersManagementCU.BmCalculateSalesPrice(PlotOption);

                                            PlotOption.UpdateWorkBudgetOnChanges(PlotOption, FALSE, FALSE);
                                            PlotOption.GET("Project No.", Plot."Plot No.", "House Model", "Main Group", Group, "Sub Group", Option);
                                          END;
                                          IF UpdateExternalOptionNo THEN
                                            PlotOption."Option No. External" := "Option No. External";
                                          IF UpdateDescription THEN
                                            PlotOption.Description := Description;
                                          IF UpdateSalesPrice THEN BEGIN
                                            PlotOption."Sales Price incl. VAT" := "Sales Price incl. VAT";
                                            PlotOption.DetermineTotal(FALSE);
                                          END;
                                          PlotOption.MODIFY(TRUE);
                                        END;
                                      END;
                                    UNTIL Plot.NEXT = 0;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1100530008;2;Field  ;
                  CaptionML=[DEU=Baunummerfilter;
                             ENU=Plot Filter;
                             NLD=Bouwnummer filter;
                             NOR=Tegningfilter;
                             SVE=Ritnings filter];
                  SourceExpr=PlotFilter }

      { 1100530000;2;Field  ;
                  CaptionML=[DEU=Aktualisieren Beschreibung;
                             ENU=Update Description;
                             NLD=Bijwerken omschrijving;
                             NOR=Oppdater Beskrivelse;
                             SVE=Uppdatera beskrivning];
                  SourceExpr=UpdateDescription }

      { 1100530003;2;Field  ;
                  CaptionML=[DEU=Aktualisieren externe Optionsnummer;
                             ENU=Update External Option No.;
                             NLD=Bijwerken extern optienummer;
                             NOR=Oppdater eksternt alternativnr;
                             SVE=Uppdatera externt alternativnr];
                  SourceExpr=UpdateExternalOptionNo }

      { 1100530007;2;Field  ;
                  CaptionML=[DEU=Aktualisieren Einstandspreis;
                             ENU=Update Cost Price;
                             NLD=Bijwerken kostprijs;
                             NOR=Oppdater selvkost;
                             SVE=Uppdatera sj„lvkostnadspris];
                  SourceExpr=UpdateCostPrice }

      { 1100530005;2;Field  ;
                  CaptionML=[DEU=Aktualisieren VK-Preis;
                             ENU=Update Sales Price;
                             NLD=Bijwerken verkoopprijs;
                             NOR=Oppdater salgspris;
                             SVE=Uppdatera f”rs„ljningspris];
                  SourceExpr=UpdateSalesPrice }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      PlotFilter@1100530000 : Text[250];
      UpdateExternalOptionNo@1100530001 : Boolean;
      UpdateDescription@1100530002 : Boolean;
      UpdateSalesPrice@1100530003 : Boolean;
      UpdateCostPrice@1100530004 : Boolean;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

