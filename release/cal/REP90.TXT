OBJECT Report 90 Import Consolidation from DB
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.01;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Konsolidierungsbericht;
               ENU=Consolidation Report;
               NLD=Consolidatierapport;
               NOR=Konsolideringsrapport];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  DimSelectionBuf.CompareDimText(
                    3,REPORT::"Import Consolidation from DB",'',ColumnDim,Text020);
                END;

    OnPostReport=BEGIN
                   COMMIT;
                   REPORT.RUN(REPORT::"Consolidated Trial Balance");
                 END;

  }
  DATASET
  {
    { 9370;    ;DataItem;                    ;
               DataItemTable=Table220;
               DataItemTableView=SORTING(Code)
                                 WHERE(Consolidate=CONST(Yes));
               OnPreDataItem=BEGIN
                               CheckConsolidDates(ConsolidStartDate,ConsolidEndDate);

                               IF GLDocNo = '' THEN
                                 ERROR(Text000);

                               Window.OPEN(
                                 Text001 +
                                 Text002 +
                                 Text003 +
                                 Text004);
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(1,Code);
                                  Window.UPDATE(2,'');

                                  CLEAR(BusUnitConsolidate);
                                  BusUnitConsolidate.SetDocNo(GLDocNo);

                                  TESTFIELD("Company Name");
                                  "G/L Entry".CHANGECOMPANY("Company Name");
                                  "Dimension Set Entry".CHANGECOMPANY("Company Name");
                                  "G/L Account".CHANGECOMPANY("Company Name");
                                  "Currency Exchange Rate".CHANGECOMPANY("Company Name");
                                  Dim.CHANGECOMPANY("Company Name");
                                  DimVal.CHANGECOMPANY("Company Name");

                                  SelectedDim.SETRANGE("User ID",USERID);
                                  SelectedDim.SETRANGE("Object Type",3);
                                  SelectedDim.SETRANGE("Object ID",REPORT::"Import Consolidation from DB");
                                  BusUnitConsolidate.SetSelectedDim(SelectedDim);

                                  TempDim.RESET;
                                  TempDim.DELETEALL;
                                  IF Dim.FIND('-') THEN BEGIN
                                    REPEAT
                                      TempDim.INIT;
                                      TempDim := Dim;
                                      TempDim.INSERT;
                                    UNTIL Dim.NEXT = 0;
                                  END;
                                  TempDim.RESET;
                                  TempDimVal.RESET;
                                  TempDimVal.DELETEALL;
                                  IF DimVal.FIND('-') THEN BEGIN
                                    REPEAT
                                      TempDimVal.INIT;
                                      TempDimVal := DimVal;
                                      TempDimVal.INSERT;
                                    UNTIL DimVal.NEXT = 0;
                                  END;

                                  AdditionalCurrencyCode := '';
                                  SubsidCurrencyCode := '';
                                END;

               ReqFilterFields=Code }

    { 6710;1   ;DataItem;                    ;
               DataItemTable=Table15;
               DataItemTableView=SORTING(No.)
                                 WHERE(Account Type=CONST(Posting));
               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(2,"No.");
                                  Window.UPDATE(3,'');

                                  BusUnitConsolidate.InsertGLAccount("G/L Account");
                                END;
                                 }

    { 7069;2   ;DataItem;                    ;
               DataItemTable=Table17;
               DataItemTableView=SORTING(G/L Account No.,Posting Date);
               OnPreDataItem=BEGIN
                               SETRANGE("Posting Date",ConsolidStartDate,ConsolidEndDate);

                               IF GETRANGEMIN("Posting Date") = NORMALDATE(GETRANGEMIN("Posting Date")) THEN
                                 CheckClosingPostings("G/L Account"."No.",GETRANGEMIN("Posting Date"),GETRANGEMAX("Posting Date"));
                             END;

               OnAfterGetRecord=BEGIN
                                  GLEntryNo := BusUnitConsolidate.InsertGLEntry("G/L Entry");
                                END;

               DataItemLink=G/L Account No.=FIELD(No.) }

    { 4925;3   ;DataItem;                    ;
               DataItemTable=Table480;
               DataItemTableView=SORTING(Dimension Set ID,Dimension Code);
               OnPreDataItem=VAR
                               BusUnitDim@1002 : Record 348;
                               DimMgt@1000 : Codeunit 408;
                               ColumnDimFilter@1001 : Text;
                             BEGIN
                               IF ColumnDim <> '' THEN BEGIN
                                 ColumnDimFilter := CONVERTSTR(ColumnDim,';','|');
                                 BusUnitDim.CHANGECOMPANY("Business Unit"."Company Name");
                                 SETFILTER("Dimension Code",DimMgt.GetConsolidatedDimFilterByDimFilter(BusUnitDim,ColumnDimFilter));
                               END;
                             END;

               OnAfterGetRecord=VAR
                                  TempDimBuf@1000 : TEMPORARY Record 360;
                                BEGIN
                                  TempDimBuf.INIT;
                                  TempDimBuf."Table ID" := DATABASE::"G/L Entry";
                                  TempDimBuf."Entry No." := GLEntryNo;
                                  IF TempDim.GET("Dimension Code") AND
                                     (TempDim."Consolidation Code" <> '')
                                  THEN
                                    TempDimBuf."Dimension Code" := TempDim."Consolidation Code"
                                  ELSE
                                    TempDimBuf."Dimension Code" := "Dimension Code";
                                  IF TempDimVal.GET("Dimension Code","Dimension Value Code") AND
                                     (TempDimVal."Consolidation Code" <> '')
                                  THEN
                                    TempDimBuf."Dimension Value Code" := TempDimVal."Consolidation Code"
                                  ELSE
                                    TempDimBuf."Dimension Value Code" := "Dimension Value Code";
                                  BusUnitConsolidate.InsertEntryDim(TempDimBuf,TempDimBuf."Entry No.");
                                END;

               DataItemLink=Dimension Set ID=FIELD(Dimension Set ID) }

    { 8685;1   ;DataItem;                    ;
               DataItemTable=Table330;
               DataItemTableView=SORTING(Currency Code,Starting Date);
               OnPreDataItem=VAR
                               SubsidGLSetup@1000 : Record 98;
                             BEGIN
                               IF "Business Unit"."Currency Code" = '' THEN
                                 CurrReport.BREAK;

                               SubsidGLSetup.CHANGECOMPANY("Business Unit"."Company Name");
                               SubsidGLSetup.GET;
                               AdditionalCurrencyCode := SubsidGLSetup."Additional Reporting Currency";
                               IF SubsidGLSetup."LCY Code" <> '' THEN
                                 SubsidCurrencyCode := SubsidGLSetup."LCY Code"
                               ELSE
                                 SubsidCurrencyCode := "Business Unit"."Currency Code";

                               IF (ParentCurrencyCode = '') AND (AdditionalCurrencyCode = '') THEN
                                 CurrReport.BREAK;

                               SETFILTER("Currency Code",'%1|%2',ParentCurrencyCode,AdditionalCurrencyCode);
                               SETRANGE("Starting Date",0D,ConsolidEndDate);
                             END;

               OnAfterGetRecord=BEGIN
                                  BusUnitConsolidate.InsertExchRate("Currency Exchange Rate");
                                END;
                                 }

    { 4528;1   ;DataItem;DoTheConsolidation  ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number)
                                 WHERE(Number=CONST(1));
               OnAfterGetRecord=BEGIN
                                  BusUnitConsolidate.SetGlobals(
                                    '','',"Business Unit"."Company Name",
                                    SubsidCurrencyCode,AdditionalCurrencyCode,ParentCurrencyCode,
                                    0,ConsolidStartDate,ConsolidEndDate);
                                  BusUnitConsolidate.UpdateGLEntryDimSetID;
                                  BusUnitConsolidate.RUN("Business Unit");
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF ConsolidStartDate = 0D THEN
                     ConsolidStartDate := WORKDATE;
                   IF ConsolidEndDate = 0D THEN
                     ConsolidEndDate := WORKDATE;

                   IF ParentCurrencyCode = '' THEN BEGIN
                     GLSetup.GET;
                     ParentCurrencyCode := GLSetup."LCY Code";
                   END;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 21  ;2   ;Group     ;
                  CaptionML=[DEU=Konsolidierungsperiode;
                             ENU=Consolidation Period;
                             NLD=Te consolideren periode;
                             NOR=Konsolideringsperiode] }

      { 1   ;3   ;Field     ;
                  Name=StartingDate;
                  CaptionML=[DEU=Startdatum;
                             ENU=Starting Date;
                             NLD=Begindatum;
                             NOR=Startdato;
                             SVE=Startdatum];
                  ToolTipML=[DEU=Gibt das Startdatum an.;
                             ENU=Specifies the starting date.;
                             NLD=Hiermee wordt de begindatum opgegeven.;
                             NOR=Angir startdatoen.];
                  ApplicationArea=#Suite;
                  ClosingDates=Yes;
                  SourceExpr=ConsolidStartDate }

      { 2   ;3   ;Field     ;
                  Name=EndingDate;
                  CaptionML=[DEU=Enddatum;
                             ENU=Ending Date;
                             NLD=Einddatum;
                             NOR=Sluttdato;
                             SVE=Slutdatum];
                  ToolTipML=[DEU=Gibt das Enddatum an.;
                             ENU=Specifies the ending date.;
                             NLD=Hiermee wordt de einddatum opgegeven.;
                             NOR=Angir sluttdatoen.];
                  ApplicationArea=#Suite;
                  ClosingDates=Yes;
                  SourceExpr=ConsolidEndDate }

      { 4   ;2   ;Group     ;
                  CaptionML=[DEU=Feldinhalt kopieren;
                             ENU=Copy Field Contents;
                             NLD=Kopiâren per;
                             NOR=Overfõr feltinnhold] }

      { 7   ;3   ;Field     ;
                  CaptionML=[DEU=Dimensionen kopieren;
                             ENU=Copy Dimensions;
                             NLD=Dimensies kopiâren;
                             NOR=Kopier dimensjoner;
                             SVE=Kopierar dimensioner];
                  ToolTipML=[DEU=Gibt an, ob die Posten beim Transfer nach Dimensionen klassifiziert werden sollen.;
                             ENU=Specifies if you want the entries to be classified by dimensions when they are transferred.;
                             NLD=Hiermee wordt opgegeven of u wilt dat de posten op dimensie worden geclassificeerd wanneer deze worden overgedragen.;
                             NOR=Angir om du vil klassifisere postene etter dimensjoner nÜr de overfõres.];
                  ApplicationArea=#Dimensions;
                  SourceExpr=ColumnDim;
                  Editable=FALSE;
                  OnAssistEdit=BEGIN
                                 DimSelectionBuf.SetDimSelectionMultiple(3,REPORT::"Import Consolidation from DB",ColumnDim);
                               END;
                                }

      { 6   ;2   ;Field     ;
                  Name=DocumentNo;
                  CaptionML=[DEU=Belegnr.;
                             ENU=Document No.;
                             NLD=Documentnr.;
                             NOR=Bilagsnr.;
                             SVE=Dokumentnr];
                  ToolTipML=[DEU=Gibt die FIBU-Belegnummer an.;
                             ENU=Specifies the G/L document number.;
                             NLD=Hiermee wordt het grootboekdocumentnummer opgegeven.;
                             NOR=Angir finansdokumentnummeret.];
                  ApplicationArea=#Suite;
                  SourceExpr=GLDocNo }

      { 3   ;2   ;Field     ;
                  CaptionML=[DEU=öbergeordneter WÑhrungscode;
                             ENU=Parent Currency Code;
                             NLD=Hoofdvalutacode;
                             NOR=Overordnet valutakode];
                  ToolTipML=[DEU=Gibt den Åbergeordneten WÑhrungscode an.;
                             ENU=Specifies the parent currency code.;
                             NLD=Hiermee wordt de bovenliggende valutacode opgegeven.;
                             NOR=Angir den overordnede valutakoden.];
                  ApplicationArea=#Suite;
                  SourceExpr=ParentCurrencyCode }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Geben Sie eine Belegnummer ein.;ENU=Enter a document number.;NLD=Voer een documentnummer in.;NOR=Angi et bilagsnummer.;SVE=Ange ett dokumentnr.';
      Text001@1001 : TextConst 'DEU=Niederlassungsdaten werden importiert...\\;ENU=Importing Subsidiary Data...\\;NLD=Gegevens dochteronderneming worden geãmporteerd...\\;NOR=Importerer underordnede data...\\';
      Text002@1002 : TextConst 'DEU=Konzernmandant       #1##########\;ENU=Business Unit Code   #1##########\;NLD=Bedrijfsunit         #1##########\;NOR=Konsernkode          #1##########\';
      Text003@1003 : TextConst 'DEU=Sachkontonr.         #2##########\;ENU=G/L Account No.      #2##########\;NLD=Grootboekrekening    #2##########\;NOR=Finanskontonr.       #2##########\';
      Text004@1004 : TextConst 'DEU=Datum                #3######;ENU=Date                 #3######;NLD=Datum                #3######;NOR=Dato                 #3######';
      Text006@1005 : TextConst 'DEU=Geben Sie das Startdatum fÅr die Konsolidierungsperiode ein.;ENU=Enter the starting date for the consolidation period.;NLD=Vul de begindatum van de te consolideren periode in.;NOR=Angi startdato for konsolideringsperioden.';
      Text007@1006 : TextConst 'DEU=Geben Sie das Enddatum fÅr die Konsolidierungsperiode ein.;ENU=Enter the ending date for the consolidation period.;NLD=Vul de einddatum van de te consolideren periode in.;NOR=Angi sluttdato for konsolideringsperioden.';
      Text020@1018 : TextConst 'DEU=Dimensionen kopieren;ENU=Copy Dimensions;NLD=Dimensies kopiâren;NOR=Kopier dimensjoner;SVE=Kopierar dimensioner';
      Text022@1020 : TextConst 'DEU=Ein %1 mit einem %2 an einem Ultimodatum (%3) wurde bei der Konsolidierung von Nicht-Ultimoposten gefunden (%4 %5).;ENU=A %1 with %2 on a closing date (%3) was found while consolidating nonclosing entries (%4 %5).;NLD=Er werd een %1 met %2 op een ultimodatum (%3) gevonden bij het consolideren van niet-afsluitingsposten (%4 %5).;NOR=Det ble funnet en %1 med %2 pÜ en avslutningsdato (%3) i forbindelse med konsolidering av ikke-avsluttede poster (%4 %5).';
      Text023@1021 : TextConst 'DEU=Mîchten Sie die Periode vom %1 bis zum %2 konsolidieren?;ENU=Do you want to consolidate in the period from %1 to %2?;NLD=Wilt u de periode van %1 tot %2 consolideren?;NOR=Vil du konsolidere i perioden fra %1 til %2?';
      Text024@1022 : TextConst 'DEU=Es ist kein %1 fÅr die Konsolidierung vorhanden.;ENU=There is no %1 to consolidate.;NLD=Geen %1 te consolideren.;NOR=Det finnes ingen %1 Ü konsolidere.';
      Text028@1025 : TextConst 'DEU=Sie mÅssen in dem konsolidierten Mandanten ein neues GeschÑftsjahr einrichten.;ENU=You must create a new fiscal year in the consolidated company.;NLD=U moet een nieuw boekjaar in het geconsolideerde bedrijf maken.;NOR=Du mÜ opprette et nytt regnskapsÜr i det konsoliderte selskapet.';
      Text030@1027 : TextConst 'DEU=Bei der Verwendung von Ultimodaten mÅssen Start- und Enddatum gleich sein.;ENU=When using closing dates, the starting and ending dates must be the same.;NLD=Als u ultimodatums gebruikt, moeten de begin- en einddatum gelijk zijn.;NOR=NÜr du bruker avslutningsdatoer, mÜ start- og sluttdatoene vëre de samme.;SVE=NÑr avslutsdatum anvÑnds mÜste start- och slutdatum vara lika.';
      SelectedDim@1034 : Record 369;
      Dim@1036 : Record 348;
      DimVal@1037 : Record 349;
      TempDim@1038 : TEMPORARY Record 348;
      TempDimVal@1039 : TEMPORARY Record 349;
      GLSetup@1043 : Record 98;
      DimSelectionBuf@1047 : Record 368;
      BusUnitConsolidate@1073 : Codeunit 432;
      Window@1050 : Dialog;
      ConsolidStartDate@1051 : Date;
      ConsolidEndDate@1052 : Date;
      GLDocNo@1055 : Code[20];
      ColumnDim@1068 : Text[250];
      ParentCurrencyCode@1072 : Code[10];
      SubsidCurrencyCode@1035 : Code[10];
      AdditionalCurrencyCode@1040 : Code[10];
      Text032@1007 : TextConst 'DEU=%1 liegt nach %2 in Mandant %3.;ENU=The %1 is later than the %2 in company %3.;NLD=%1 is later dan %2 in bedrijf %3.;NOR=%1 er senere enn %2 i selskapet %3.;SVE=%1 Ñr senare Ñn %2 i fîretag %3.';
      GLEntryNo@1008 : Integer;
      ConsPeriodSubsidiaryQst@1009 : TextConst '@@@=%1 and %2 - request page values;DEU=Die zu konsolidierende Periode %1 .. %2 liegt nicht innerhalb des GeschÑftsjahres %3 .. %4 einer oder mehrerer Tochterunternehmen.\Mîchten Sie mit der Konsolidierung fortfahren?;ENU=The consolidation period %1 .. %2 is not within the fiscal year of one or more of the subsidiaries.\Do you want to proceed with the consolidation?;NLD=De te consolideren periode %1 .. %2 ligt niet binnen het boekjaar van een of meer dochterbedrijven.\Wilt u doorgaan met de consolidatie?;NOR=Konsolideringsperioden %1 .. %2 er ikke innenfor regnskapsÜret til ett eller flere av datterselskapene.\Vil du fortsette med konsolideringen?';
      ConsPeriodCompanyQst@1010 : TextConst '@@@=%1, %2, %3, %4 - request page values, %5 - company name;DEU=Die zu konsolidierende Periode %1 .. %2 liegt nicht innerhalb des GeschÑftsjahres %3 .. %4 des konsolidierten Unternehmens %5.\\Mîchten Sie mit der Konsolidierung fortfahren?;ENU=The consolidation period %1 .. %2 is not within the fiscal year %3 .. %4 of the consolidated company %5.\Do you want to proceed with the consolidation?;NLD=De te consolideren periode %1 .. %2 ligt niet binnen het boekjaar %3 .. %4 van het geconsolideerde bedrijf %5.\Wilt u doorgaan met de consolidatie?;NOR=Konsolideringsperioden %1 .. %2 er ikke innenfor regnskapsÜret %3 .. %4 til det konsoliderte selskapet %5.\Vil du fortsette med konsolideringen?';

    LOCAL PROCEDURE CheckClosingPostings@5(GLAccNo@1000 : Code[20];StartDate@1001 : Date;EndDate@1002 : Date);
    VAR
      GLEntry@1003 : Record 17;
      AccountingPeriod@1004 : Record 50;
    BEGIN
      AccountingPeriod.CHANGECOMPANY("Business Unit"."Company Name");
      AccountingPeriod.SETCURRENTKEY("New Fiscal Year","Date Locked");
      AccountingPeriod.SETRANGE("New Fiscal Year",TRUE);
      AccountingPeriod.SETRANGE("Date Locked",TRUE);
      AccountingPeriod.SETRANGE("Starting Date",StartDate + 1,EndDate);
      IF AccountingPeriod.FIND('-') THEN BEGIN
        GLEntry.CHANGECOMPANY("Business Unit"."Company Name");
        GLEntry.SETRANGE("G/L Account No.",GLAccNo);
        REPEAT
          GLEntry.SETRANGE("Posting Date",CLOSINGDATE(AccountingPeriod."Starting Date" - 1));
          IF NOT GLEntry.ISEMPTY THEN
            ERROR(
              Text022,
              GLEntry.TABLECAPTION,
              GLEntry.FIELDCAPTION("Posting Date"),
              GLEntry.GETFILTER("Posting Date"),
              GLEntry.FIELDCAPTION("G/L Account No."),
              GLAccNo);
        UNTIL AccountingPeriod.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE CheckConsolidDates@9(StartDate@1000 : Date;EndDate@1001 : Date);
    VAR
      BusUnit@1002 : Record 220;
      ConfirmManagement@1004 : Codeunit 27;
      ConsolPeriodInclInFiscalYears@1003 : Boolean;
    BEGIN
      IF StartDate = 0D THEN
        ERROR(Text006);
      IF EndDate = 0D THEN
        ERROR(Text007);

      IF NOT ConfirmManagement.ConfirmProcess(STRSUBSTNO(Text023,StartDate,EndDate),TRUE) THEN
        CurrReport.BREAK;

      CheckClosingDates(StartDate,EndDate);

      BusUnit.COPYFILTERS("Business Unit");
      BusUnit.SETRANGE(Consolidate,TRUE);
      IF NOT BusUnit.FIND('-') THEN
        ERROR(Text024,BusUnit.TABLECAPTION);

      ConsolPeriodInclInFiscalYears := TRUE;
      REPEAT
        IF (StartDate = NORMALDATE(StartDate)) OR (EndDate = NORMALDATE(EndDate)) THEN
          IF (BusUnit."Starting Date" <> 0D) OR (BusUnit."Ending Date" <> 0D) THEN BEGIN
            CheckBusUnitsDatesToFiscalYear(BusUnit);
            ConsolPeriodInclInFiscalYears :=
              ConsolPeriodInclInFiscalYears AND CheckDatesToBusUnitDates(StartDate,EndDate,BusUnit);
          END;
      UNTIL BusUnit.NEXT = 0;

      IF NOT ConsolPeriodInclInFiscalYears THEN
        IF NOT ConfirmManagement.ConfirmProcess(
             STRSUBSTNO(ConsPeriodSubsidiaryQst,StartDate,EndDate),TRUE)
        THEN
          CurrReport.BREAK;

      CheckDatesToFiscalYear(StartDate,EndDate);
    END;

    LOCAL PROCEDURE CheckDatesToFiscalYear@17(StartDate@1000 : Date;EndDate@1001 : Date);
    VAR
      AccountingPeriod@1002 : Record 50;
      ConfirmManagement@1006 : Codeunit 27;
      FiscalYearStartDate@1003 : Date;
      FiscalYearEndDate@1004 : Date;
      ConsolPeriodInclInFiscalYear@1005 : Boolean;
    BEGIN
      ConsolPeriodInclInFiscalYear := TRUE;

      AccountingPeriod.RESET;
      AccountingPeriod.SETRANGE(Closed,FALSE);
      AccountingPeriod.SETRANGE("New Fiscal Year",TRUE);
      IF AccountingPeriod.FIND('-') THEN BEGIN
        FiscalYearStartDate := AccountingPeriod."Starting Date";
        IF AccountingPeriod.FIND('>') THEN
          FiscalYearEndDate := CALCDATE('<-1D>',AccountingPeriod."Starting Date")
        ELSE
          ERROR(Text028);

        ConsolPeriodInclInFiscalYear := (StartDate >= FiscalYearStartDate) AND (EndDate <= FiscalYearEndDate);

        IF NOT ConsolPeriodInclInFiscalYear THEN
          IF NOT ConfirmManagement.ConfirmProcess(
               STRSUBSTNO(
                 ConsPeriodCompanyQst,StartDate,EndDate,FiscalYearStartDate,
                 FiscalYearEndDate,COMPANYNAME),TRUE)
          THEN
            CurrReport.BREAK;
      END;
    END;

    LOCAL PROCEDURE CheckDatesToBusUnitDates@13(StartDate@1000 : Date;EndDate@1001 : Date;BusUnit@1002 : Record 220) : Boolean;
    VAR
      ConsolPeriodInclInFiscalYear@1003 : Boolean;
    BEGIN
      ConsolPeriodInclInFiscalYear := (StartDate >= BusUnit."Starting Date") AND (EndDate <= BusUnit."Ending Date");
      EXIT(ConsolPeriodInclInFiscalYear);
    END;

    LOCAL PROCEDURE CheckClosingDates@20(StartDate@1000 : Date;EndDate@1001 : Date);
    BEGIN
      IF (StartDate = CLOSINGDATE(StartDate)) OR
         (EndDate = CLOSINGDATE(EndDate))
      THEN BEGIN
        IF StartDate <> EndDate THEN
          ERROR(Text030);
      END;
    END;

    LOCAL PROCEDURE CheckBusUnitsDatesToFiscalYear@8(VAR BusUnit@1000 : Record 220);
    BEGIN
      WITH BusUnit DO
        IF ("Starting Date" <> 0D) OR ("Ending Date" <> 0D) THEN BEGIN
          TESTFIELD("Starting Date");
          TESTFIELD("Ending Date");
          IF "Starting Date" > "Ending Date" THEN
            ERROR(
              Text032,FIELDCAPTION("Starting Date"),
              FIELDCAPTION("Ending Date"),"Company Name");
        END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

