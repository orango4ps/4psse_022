OBJECT Codeunit 5981 Service-Post (Yes/No)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03;
  }
  PROPERTIES
  {
    TableNo=5902;
    EventSubscriberInstance=Manual;
    OnRun=BEGIN
            Code(Rec,ServiceHeaderPreviewContext);
          END;

  }
  CODE
  {
    VAR
      ShipInvoiceConsumeQst@1000 : TextConst 'ENU=&Ship,&Invoice,Ship &and Invoice,Ship and &Consume;NOR=&Lever,&Fakturer,Lever &og fakturer,L&ever og forbruk;SVE=&Leverera,&Fakturera,Leverera &och fakturera,Leverera och f&”rbruka';
      PostConfirmQst@1001 : TextConst '@@@="%1 = Document Type";ENU=Do you want to post the %1?;NOR=Vil du bokf›re %1?;SVE=Vill du bokf”ra %1?';
      ServiceHeaderPreviewContext@1003 : Record 5900;
      Selection@1005 : Integer;
      PreviewMode@1002 : Boolean;
      CancelErr@1006 : TextConst 'ENU=The preview has been canceled.;NOR=Forh†ndsvisningen er avbrutt.;SVE=F”rhandsgranskningen har avbrutits.';
      NothingToPostErr@1007 : TextConst 'ENU=There is nothing to post.;NOR=Det finnes ingenting † bokf›re.;SVE=Det finns inget att bokf”ra.';

    LOCAL PROCEDURE Code@1(VAR PassedServLine@1003 : Record 5902;VAR PassedServiceHeader@1005 : Record 5900);
    VAR
      ServicePost@1004 : Codeunit 5980;
      ConfirmManagement@1008 : Codeunit 27;
      Ship@1000 : Boolean;
      Consume@1001 : Boolean;
      Invoice@1002 : Boolean;
      HideDialog@1006 : Boolean;
      IsHandled@1007 : Boolean;
    BEGIN
      IF NOT PassedServiceHeader.FIND THEN
        ERROR(NothingToPostErr);

      HideDialog := FALSE;
      IsHandled := FALSE;
      OnBeforeConfirmServPost(PassedServiceHeader,HideDialog,Ship,Consume,Invoice,IsHandled,PreviewMode,PassedServLine);
      IF IsHandled THEN
        EXIT;

      WITH PassedServiceHeader DO BEGIN
        IF NOT HideDialog THEN
          CASE "Document Type" OF
            "Document Type"::Order:
              BEGIN
                Selection := STRMENU(ShipInvoiceConsumeQst,3);
                IF Selection = 0 THEN BEGIN
                  IF PreviewMode THEN
                    ERROR(CancelErr);
                  EXIT;
                END;
                Ship := Selection IN [1,3,4];
                Consume := Selection IN [4];
                Invoice := Selection IN [2,3];
              END
            ELSE
              IF NOT PreviewMode THEN
                IF NOT ConfirmManagement.ConfirmProcess(
                     STRSUBSTNO(PostConfirmQst,"Document Type"),TRUE)
                THEN
                  EXIT;
          END;

        OnAfterConfirmPost(PassedServiceHeader,Ship,Consume,Invoice);

        ServicePost.SetPreviewMode(PreviewMode);
        ServicePost.PostWithLines(PassedServiceHeader,PassedServLine,Ship,Consume,Invoice);

        IF NOT PreviewMode THEN
          COMMIT;
      END;

      OnAfterPost(PassedServiceHeader);
    END;

    [External]
    PROCEDURE PostDocument@2(VAR ServiceHeaderSource@1000 : Record 5900);
    VAR
      DummyServLine@1001 : TEMPORARY Record 5902;
    BEGIN
      PostDocumentWithLines(ServiceHeaderSource,DummyServLine);
    END;

    [External]
    PROCEDURE PostDocumentWithLines@3(VAR ServiceHeaderSource@1000 : Record 5900;VAR PassedServLine@1001 : Record 5902);
    VAR
      ServiceHeader@1002 : Record 5900;
    BEGIN
      ServiceHeader.COPY(ServiceHeaderSource);
      Code(PassedServLine,ServiceHeader);
      ServiceHeaderSource := ServiceHeader;
    END;

    [External]
    PROCEDURE PreviewDocument@4(VAR ServHeader@1000 : Record 5900);
    VAR
      TempServLine@1001 : TEMPORARY Record 5902;
      GenJnlPostPreview@1003 : Codeunit 19;
      ServicePostYesNo@1002 : Codeunit 5981;
    BEGIN
      BINDSUBSCRIPTION(ServicePostYesNo);
      ServicePostYesNo.SetPreviewContext(ServHeader);
      GenJnlPostPreview.Preview(ServicePostYesNo,TempServLine);
    END;

    [External]
    PROCEDURE PreviewDocumentWithLines@5(VAR ServHeader@1000 : Record 5900;VAR PassedServLine@1004 : Record 5902);
    VAR
      GenJnlPostPreview@1003 : Codeunit 19;
      ServicePostYesNo@1001 : Codeunit 5981;
    BEGIN
      BINDSUBSCRIPTION(ServicePostYesNo);
      ServicePostYesNo.SetPreviewContext(ServHeader);
      GenJnlPostPreview.Preview(ServicePostYesNo,PassedServLine);
    END;

    [External]
    PROCEDURE SetPreviewContext@7(VAR ServiceHeader@1000 : Record 5900);
    BEGIN
      ServiceHeaderPreviewContext.COPY(ServiceHeader);
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterConfirmPost@9(ServiceHeader@1000 : Record 5900;Ship@1001 : Boolean;Consume@1002 : Boolean;Invoice@1003 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPost@10(VAR PassedServiceHeader@1000 : Record 5900);
    BEGIN
    END;

    [EventSubscriber(Codeunit,19,OnRunPreview)]
    LOCAL PROCEDURE OnRunPreview@6(VAR Result@1000 : Boolean;Subscriber@1001 : Variant;RecVar@1002 : Variant);
    VAR
      ServicePostYesNo@1003 : Codeunit 5981;
    BEGIN
      ServicePostYesNo := Subscriber;
      PreviewMode := TRUE;
      Result := ServicePostYesNo.RUN(RecVar);
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeConfirmServPost@8(VAR ServiceHeader@1000 : Record 5900;VAR HideDialog@1001 : Boolean;VAR Ship@1004 : Boolean;VAR Consume@1003 : Boolean;VAR Invoice@1002 : Boolean;VAR IsHandled@1005 : Boolean;PreviewMode@1006 : Boolean;VAR ServiceLine@1007 : Record 5902);
    BEGIN
    END;

    BEGIN
    END.
  }
}

