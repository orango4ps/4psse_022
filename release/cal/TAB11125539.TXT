OBJECT Table 11125539 Totals (SMF)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Totals (Site Manager Form);
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr];
                                                   Editable=No }
    { 20  ;   ;Working Date Filter ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Working Date Filter;
                                                   Description=Flowfilter: Must be a filter on one specific date }
    { 30  ;   ;Shift Code Filter   ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation="Shift (SMF)";
                                                   CaptionML=[ENU=Shift Code Filter;
                                                              NOR=Skiftkodefilter;
                                                              SVE=Skiftkodsfilter];
                                                   Description=Flowfilter: Must be a filter on one specific shift (or on shift is empty) }
    { 100 ;   ;Text Lines          ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Text Line (SMF)" WHERE (Type=CONST(Text),
                                                                                              Project No.=FIELD(Project No.)));
                                                   CaptionML=[ENU=Text Lines;
                                                              NOR=Tekstrader;
                                                              SVE=Textrader];
                                                   Editable=No }
    { 101 ;   ;Text Lines (Date)   ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Text Line (SMF)" WHERE (Type=CONST(Text),
                                                                                              Project No.=FIELD(Project No.),
                                                                                              Date=FIELD(Working Date Filter)));
                                                   CaptionML=ENU=Text Lines (Date);
                                                   Editable=No }
    { 110 ;   ;Weather Conditions  ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Text Line (SMF)" WHERE (Type=CONST(Weather),
                                                                                              Project No.=FIELD(Project No.)));
                                                   CaptionML=[ENU=Wheather Conditions;
                                                              NOR=V‘rforhold;
                                                              SVE=V„derf”rh†llanden];
                                                   Editable=No }
    { 111 ;   ;Weather Conditions (Date);Integer  ;FieldClass=FlowField;
                                                   CalcFormula=Count("Text Line (SMF)" WHERE (Type=CONST(Weather),
                                                                                              Project No.=FIELD(Project No.),
                                                                                              Date=FIELD(Working Date Filter)));
                                                   CaptionML=ENU=Weather Conditions (Date);
                                                   Editable=No }
    { 120 ;   ;Work Hours Exceptions;Integer      ;FieldClass=FlowField;
                                                   CalcFormula=Count("Working Hours (SMF)" WHERE (Project No.=FIELD(Project No.),
                                                                                                  Shift Code=FIELD(Shift Code Filter)));
                                                   CaptionML=ENU=Work Hours Exceptions;
                                                   Editable=No }
    { 121 ;   ;Work Hours Exceptions (Date);Integer;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Count("Working Hours (SMF)" WHERE (Project No.=FIELD(Project No.),
                                                                                                  Shift Code=FIELD(Shift Code Filter),
                                                                                                  Date=FIELD(Working Date Filter)));
                                                   CaptionML=ENU=Work Hours Exceptions (Date);
                                                   Editable=No }
    { 130 ;   ;Inspections         ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Inspection SMF" WHERE (Project No.=FIELD(Project No.)));
                                                   CaptionML=[ENU=Inspections;
                                                              NOR=Inspeksjoner;
                                                              SVE=Inspektioner];
                                                   Editable=No }
    { 131 ;   ;Inspections (Date)  ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Inspection SMF" WHERE (Project No.=FIELD(Project No.),
                                                                                             Date=FIELD(Working Date Filter)));
                                                   CaptionML=ENU=Inspections (Date);
                                                   Editable=No }
    { 140 ;   ;Deviations          ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("QA Deviation" WHERE (Source Type=CONST(Project),
                                                                                           Source No.=FIELD(Project No.)));
                                                   CaptionML=[ENU=Deviations;
                                                              NOR=Avvik;
                                                              SVE=Avvikelser];
                                                   Editable=No }
    { 150 ;   ;Incidents/Accidents ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Accident QA" WHERE (Source Type=CONST(Project),
                                                                                          Source No.=FIELD(Project No.)));
                                                   CaptionML=ENU=Incidents/Accidents;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Project No.                             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100525000 : TextConst 'ENU=Insert not allowed in table ''%1''';

    PROCEDURE DrillDownSMFTextLines@1100525503(ProjectNo@1100525500 : Code[20]);
    VAR
      TextLineSMF@1100525505 : Record 11125526;
    BEGIN
      DrillDownSMFText(ProjectNo, TextLineSMF.Type::Text);
    END;

    PROCEDURE DrillDownSMFWeatherConditions@1100525000(ProjectNo@1100525500 : Code[20]);
    VAR
      TextLineSMF@1100525505 : Record 11125526;
    BEGIN
      DrillDownSMFText(ProjectNo, TextLineSMF.Type::Weather);
    END;

    LOCAL PROCEDURE DrillDownSMFText@1100525001(ProjectNo@1100525500 : Code[20];LineType@1100525000 : Option);
    VAR
      TextLineSMF@1100525505 : Record 11125526;
    BEGIN
      IF ProjectNo = '' THEN
        EXIT;
      TextLineSMF.FILTERGROUP(2);
      TextLineSMF.SETRANGE("Project No.", ProjectNo);
      TextLineSMF.SETRANGE(Type, LineType);
      TextLineSMF.FILTERGROUP(0);
      PAGE.RUNMODAL(PAGE::"Site Manager Form Text", TextLineSMF);
    END;

    PROCEDURE DrillDownSMFWorkHourExceptions@1100525510(ProjectNo@1100525500 : Code[20];ShiftCode@1100525001 : Code[10]);
    VAR
      ProjShiftSMF@1100525002 : Record 11125538;
      WorkingHoursSMF@1100525000 : Record 11125528;
    BEGIN
      IF ProjectNo = '' THEN
        EXIT;
      ProjShiftSMF.SETRANGE("Project No.", ProjectNo);
      ProjShiftSMF.SETFILTER("Shift Code", '<>%1', '');
      IF ProjShiftSMF.ISEMPTY THEN BEGIN
        IF ShiftCode <> '' THEN
          ShiftCode := '';
      END ELSE BEGIN
        IF ShiftCode = '' THEN
          EXIT;
      END;

      WorkingHoursSMF.FILTERGROUP(2);
      WorkingHoursSMF.SETRANGE("Project No.", ProjectNo);
      WorkingHoursSMF.SETRANGE("Shift Code", ShiftCode);
      WorkingHoursSMF.FILTERGROUP(0);
      PAGE.RUNMODAL(PAGE::"Site Man. Form Working Hours", WorkingHoursSMF);
    END;

    PROCEDURE DrillDownSMFInspectionLines@1210190001(ProjectNo@1100525500 : Code[20]);
    VAR
      InspectionSMF@1100525505 : Record 11125535;
    BEGIN
      IF ProjectNo = '' THEN
        EXIT;
      InspectionSMF.FILTERGROUP(2);
      InspectionSMF.SETRANGE("Project No.", ProjectNo);
      InspectionSMF.FILTERGROUP(0);
      PAGE.RUNMODAL(PAGE::"Site Man. Form Inspections", InspectionSMF);
    END;

    PROCEDURE DrillDownSMFDeviationLines@1210190003(ProjectNo@1100525500 : Code[20]);
    VAR
      QADeviation@1100525505 : Record 11125986;
    BEGIN
      IF ProjectNo = '' THEN
        EXIT;
      QADeviation.FILTERGROUP(2);
      QADeviation.SETRANGE("Source Type", QADeviation."Source Type"::Project);
      QADeviation.SETRANGE("Source No.", ProjectNo);
      QADeviation.FILTERGROUP(0);
      PAGE.RUNMODAL(PAGE::"Deviations (SMF)", QADeviation);
    END;

    PROCEDURE DrillDownSMFAccidentLines@1210190004(ProjectNo@1100525500 : Code[20]);
    VAR
      AccidentQA@1100525505 : Record 11126005;
    BEGIN
      IF ProjectNo = '' THEN
        EXIT;
      AccidentQA.FILTERGROUP(2);
      AccidentQA.SETRANGE("Source Type", AccidentQA."Source Type"::Project);
      AccidentQA.SETRANGE("Source No.", ProjectNo);
      AccidentQA.FILTERGROUP(0);
      PAGE.RUNMODAL(PAGE::"Accidents (SMF)", AccidentQA);
    END;

    PROCEDURE GetStartEndTimes@1100525002(ProjectNo@1100525000 : Code[20];ShiftCode@1100525001 : Code[10];WorkingDate@1100525002 : Date;VAR StartingTime@1100525003 : Time;VAR EndingTime@1100525004 : Time) : Boolean;
    VAR
      ProjShiftSMF@1100525005 : Record 11125538;
      WorkingHoursSMF@1100525006 : Record 11125528;
    BEGIN
      StartingTime := 0T;
      EndingTime := 0T;
      IF ProjectNo = '' THEN
        EXIT(FALSE);

      ProjShiftSMF.SETRANGE("Project No.", ProjectNo);
      ProjShiftSMF.SETFILTER("Shift Code", '<>%1', '');
      IF ProjShiftSMF.ISEMPTY THEN BEGIN
        IF ShiftCode <> '' THEN
          ShiftCode := '';
      END ELSE BEGIN
        IF ShiftCode = '' THEN
          EXIT(FALSE);
      END;

      IF WorkingDate <> 0D THEN BEGIN
        IF WorkingHoursSMF.GET(ProjectNo, ShiftCode, WorkingDate) THEN BEGIN
          StartingTime := WorkingHoursSMF."Starting Time";
          EndingTime := WorkingHoursSMF."Ending Time";
          EXIT(TRUE);
        END;
      END;
      IF ProjShiftSMF.GET(ProjectNo, ShiftCode) THEN BEGIN
        StartingTime := ProjShiftSMF."Starting Time";
        EndingTime := ProjShiftSMF."Ending Time";
        EXIT(TRUE);
      END;
      EXIT(FALSE);
    END;

    BEGIN
    END.
  }
}

