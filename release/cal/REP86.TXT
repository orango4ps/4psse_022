OBJECT Report 86 Adjust Add. Reporting Currency
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00,4PSSE;
  }
  PROPERTIES
  {
    Permissions=TableData 17=im,
                TableData 32=m,
                TableData 45=im,
                TableData 11072005=m,
                TableData 254=m,
                TableData 355=imd,
                TableData 5802=m;
    CaptionML=[ENU=Adjust Add. Reporting Currency;
               NOR=Juster tilleggsrapp.valuta;
               SVE=Just. alt. rapporteringsvaluta];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   IF GLSetup."Additional Reporting Currency" = '' THEN
                     GLSetup.GET;
                 END;

    OnPreReport=VAR
                  GenJnlLine@1000 : Record 81;
                BEGIN
                  Currency.GET(GLSetup."Additional Reporting Currency");
                  Currency.TESTFIELD("Amount Rounding Precision");
                  Currency.TESTFIELD("Unit-Amount Rounding Precision");
                  Currency.TESTFIELD("Residual Gains Account");
                  ResidualGLAcc.GET(Currency."Residual Gains Account");
                  ResidualGLAcc.TESTFIELD(Blocked,FALSE);
                  ResidualGLAcc.TESTFIELD("Account Type",ResidualGLAcc."Account Type"::Posting);
                  Currency.TESTFIELD("Residual Losses Account");
                  ResidualGLAcc.GET(Currency."Residual Losses Account");
                  ResidualGLAcc.TESTFIELD(Blocked,FALSE);
                  ResidualGLAcc.TESTFIELD("Account Type",ResidualGLAcc."Account Type"::Posting);
                  SourceCodeSetup.GET;
                  SourceCodeSetup.TESTFIELD("Adjust Add. Reporting Currency");

                  IF DocumentNo = '' THEN
                    ERROR(
                      Text000,GenJnlLine.FIELDCAPTION("Document No."));
                  IF RetainedEarningsGLAcc."No." = '' THEN
                    ERROR(Text001);
                END;

    OnPostReport=BEGIN
                   ReportIsExecuted := TRUE;
                 END;

  }
  DATASET
  {
    { 7612;    ;DataItem;                    ;
               DataItemTable=Table254;
               DataItemTableView=SORTING(Type,Closed)
                                 WHERE(Type=FILTER(Purchase..Sale));
               OnPreDataItem=VAR
                               GLSetup@1000 : Record 98;
                             BEGIN
                               Window.OPEN(
                                 Text002);
                               IF COUNT > 0 THEN
                                 VATEntryStep := 10000 * 100000 DIV COUNT;

                               GLSetup.GET;
                               IF NOT GLSetup."Unrealized VAT" THEN
                                 SETRANGE(Closed,FALSE);
                             END;

               OnAfterGetRecord=BEGIN
                                  VATEntryCount := VATEntryCount + VATEntryStep;
                                  IF VATEntryOldCount DIV 100000 <> VATEntryCount DIV 100000 THEN BEGIN
                                    Window.UPDATE(1,VATEntryCount DIV 100000);
                                    VATEntryOldCount := VATEntryCount;
                                  END;

                                  "Additional-Currency Amount" := ExchangeAmtLCYToFCY("Posting Date",Amount,FALSE);
                                  "Additional-Currency Base" := ExchangeAmtLCYToFCY("Posting Date",Base,FALSE);
                                  "Add.-Currency Unrealized Amt." := ExchangeAmtLCYToFCY("Posting Date","Unrealized Amount",FALSE);
                                  "Add.-Currency Unrealized Base" := ExchangeAmtLCYToFCY("Posting Date","Unrealized Base",FALSE);
                                  "Add.-Curr. Rem. Unreal. Amount" := ExchangeAmtLCYToFCY("Posting Date","Remaining Unrealized Amount",FALSE);
                                  "Add.-Curr. Rem. Unreal. Base" := ExchangeAmtLCYToFCY("Posting Date","Remaining Unrealized Base",FALSE);
                                  MODIFY;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;
                              END;
                               }

    { 7069;    ;DataItem;                    ;
               DataItemTable=Table17;
               DataItemTableView=SORTING(Entry No.);
               OnPreDataItem=BEGIN
                               Window.OPEN(
                                 Text003 +
                                 Text004);
                             END;

               OnAfterGetRecord=VAR
                                  GLAccNo@1000 : Code[20];
                                BEGIN
                                  IF OldGLEntry."Posting Date" < "Posting Date" THEN BEGIN
                                    Window.UPDATE(1,"Posting Date");
                                    OldGLEntry := "G/L Entry";
                                  END;
                                  "Additional-Currency Amount" := ExchangeAmtLCYToFCY("Posting Date",Amount,FALSE);
                                  IF "Debit Amount" <> 0 THEN BEGIN
                                    "Add.-Currency Debit Amount" := "Additional-Currency Amount";
                                    "Add.-Currency Credit Amount" := 0;
                                  END ELSE BEGIN
                                    "Add.-Currency Debit Amount" := 0;
                                    "Add.-Currency Credit Amount" := -"Additional-Currency Amount";
                                  END;
                                  MODIFY;

                                  TotalAddCurrAmount := TotalAddCurrAmount + "Additional-Currency Amount";
                                  TotalAmount := TotalAmount + Amount;
                                  IF TotalAmount = 0 THEN
                                    IF TotalAddCurrAmount <> 0 THEN BEGIN
                                      IF TotalAddCurrAmount < 0 THEN
                                        GLAccNo := Currency."Residual Losses Account"
                                      ELSE
                                        GLAccNo := Currency."Residual Gains Account";
                                      InsertGLEntry(
                                        "Posting Date","Document Date","Document Type","Document No.",GLAccNo,
                                        "Journal Batch Name","Reason Code",-TotalAddCurrAmount);
                                      TotalAddCurrAmount := 0;
                                    END;

                                  IF "Entry No." = LastEntryNo THEN
                                    CurrReport.BREAK;

                                  IF "Posting Date" = CLOSINGDATE("Posting Date") THEN BEGIN
                                    TmpCloseIncomeStatementBuffer."Closing Date" := "Posting Date";
                                    TmpCloseIncomeStatementBuffer."G/L Account No." := "G/L Account No.";
                                    IF TmpCloseIncomeStatementBuffer.INSERT THEN;
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                IF TmpCloseIncomeStatementBuffer.FIND('-') THEN
                                  REPEAT
                                    IF IsAccountingPeriodClosingDate(
                                         TmpCloseIncomeStatementBuffer."Closing Date")
                                    THEN BEGIN
                                      CheckCombination(TmpCloseIncomeStatementBuffer);

                                      CLEAR(TmpCloseIncomeStatementBuffer3);
                                      TmpCloseIncomeStatementBuffer3."Closing Date" := "Posting Date";
                                      TmpCloseIncomeStatementBuffer3."G/L Account No." := Currency."Residual Gains Account";
                                      IF TmpCloseIncomeStatementBuffer3.INSERT THEN;
                                      TmpCloseIncomeStatementBuffer3."Closing Date" := "Posting Date";
                                      TmpCloseIncomeStatementBuffer3."G/L Account No." := Currency."Residual Losses Account";
                                      IF TmpCloseIncomeStatementBuffer3.INSERT THEN;
                                    END;
                                  UNTIL TmpCloseIncomeStatementBuffer.NEXT = 0;

                                IF TmpCloseIncomeStatementBuffer3.FIND('-') THEN
                                  REPEAT
                                    CheckCombination(TmpCloseIncomeStatementBuffer3);
                                  UNTIL TmpCloseIncomeStatementBuffer3.NEXT = 0;

                                IF GLReg."To Entry No." <> 0 THEN
                                  GLReg.INSERT;
                                Window.CLOSE;
                              END;
                               }

    { 8894;    ;DataItem;                    ;
               DataItemTable=Table5802;
               DataItemTableView=SORTING(Item No.);
               OnPreDataItem=BEGIN
                               Window.OPEN(
                                 Text011 +
                                 Text006);
                             END;

               OnAfterGetRecord=VAR
                                  ItemLedgerEntry@1000 : Record 32;
                                  PostingDate@1001 : Date;
                                BEGIN
                                  IF OldValueEntry."Item No." <> "Item No." THEN BEGIN
                                    Window.UPDATE(1,"Item No.");
                                    OldValueEntry := "Value Entry";
                                  END;

                                  IF "Entry Type" = "Entry Type"::Revaluation THEN BEGIN
                                    ItemLedgerEntry.GET("Item Ledger Entry No.");
                                    PostingDate := ItemLedgerEntry."Posting Date";
                                  END ELSE
                                    PostingDate := "Posting Date";

                                  "Cost per Unit (ACY)" := ExchangeAmtLCYToFCY(PostingDate,"Cost per Unit",TRUE);
                                  "Cost Amount (Actual) (ACY)" := ExchangeAmtLCYToFCY(PostingDate,"Cost Amount (Actual)",FALSE);
                                  "Cost Amount (Expected) (ACY)" := ExchangeAmtLCYToFCY(PostingDate,"Cost Amount (Expected)",FALSE);
                                  "Cost Amount (Non-Invtbl.)(ACY)" := ExchangeAmtLCYToFCY(PostingDate,"Cost Amount (Non-Invtbl.)",FALSE);
                                  "Cost Posted to G/L (ACY)" := ExchangeAmtLCYToFCY(PostingDate,"Cost Posted to G/L",FALSE);

                                  MODIFY;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;
                              END;
                               }

    { 5612;    ;DataItem;                    ;
               DataItemTable=Table11072005;
               DataItemTableView=SORTING(Job No.,Posting Date);
               OnPreDataItem=BEGIN
                               Window.OPEN(
                                 Text007 +
                                 Text008);
                             END;

               OnAfterGetRecord=BEGIN
                                  IF OldJobLedgEntry."Job No." <> "Job No." THEN BEGIN
                                    Window.UPDATE(1,"Job No.");
                                    OldJobLedgEntry := "Job Ledger Entry";
                                  END;

                                  "Additional-Currency Total Cost" := ExchangeAmtLCYToFCY("Posting Date","Total Cost (LCY)",FALSE);
                                  "Add.-Currency Total Price" := ExchangeAmtLCYToFCY("Posting Date","Total Price (LCY)",FALSE);
                                  MODIFY;
                                END;
                                 }

    { 3581;    ;DataItem;                    ;
               DataItemTable=Table5406;
               DataItemTableView=SORTING(Status,Prod. Order No.,Line No.);
               OnPreDataItem=BEGIN
                               Window.OPEN(
                                 Text99000004 +
                                 Text99000002);
                             END;

               OnAfterGetRecord=BEGIN
                                  IF OldProdOrderLine."Prod. Order No." <> "Prod. Order No." THEN BEGIN
                                    Window.UPDATE(1,"Prod. Order No.");
                                    OldProdOrderLine := "Prod. Order Line";
                                  END;

                                  "Cost Amount (ACY)" := ExchangeAmtLCYToFCY(WORKDATE,"Cost Amount",FALSE);
                                  "Unit Cost (ACY)" := ExchangeAmtLCYToFCY(WORKDATE,"Unit Cost",TRUE);
                                  MODIFY;
                                END;
                                 }

    { 5276;    ;DataItem;                    ;
               DataItemTable=Table1104;
               OnPreDataItem=BEGIN
                               Window.OPEN(
                                 Text012 +
                                 Text004);
                             END;

               OnAfterGetRecord=BEGIN
                                  IF OldCostEntry."Posting Date" < "Posting Date" THEN BEGIN
                                    Window.UPDATE(1,"Posting Date");
                                    OldCostEntry := "Cost Entry";
                                  END;
                                  "Additional-Currency Amount" := ExchangeAmtLCYToFCY("Posting Date",Amount,FALSE);
                                  IF "Debit Amount" <> 0 THEN BEGIN
                                    "Add.-Currency Debit Amount" := "Additional-Currency Amount";
                                    "Add.-Currency Credit Amount" := 0;
                                  END ELSE BEGIN
                                    "Add.-Currency Debit Amount" := 0;
                                    "Add.-Currency Credit Amount" := -"Additional-Currency Amount";
                                  END;
                                  MODIFY;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   GLSetup.TESTFIELD("Additional Reporting Currency");
                   GLSetup2 := GLSetup;
                   //CurrencyFactor := CurrExchRate.ExchangeRate(WORKDATE,GLSetup."Additional Reporting Currency"); //**4PS.o
                   CurrencyFactor := CurrExchRate.ExchangeRate(0, '',WORKDATE,GLSetup."Additional Reporting Currency",TRUE); //**4PS.n
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 7   ;2   ;Field     ;
                  CaptionML=[ENU=Additional Reporting Currency;
                             NOR=Tilleggsrapporteringsvaluta;
                             SVE=Alternativ rapporteringsvaluta];
                  ToolTipML=[ENU=Specifies the currency code from the Additional Reporting Currency field in the General Ledger Setup window.;
                             NOR=Angir valutakoden fra feltet Tilleggsrapporteringsvaluta i vinduet Finansoppsett.;
                             SVE=Anger valutakoden fr†n f„ltet Alternativ rapporteringsvaluta i f”nstret Redovisningsinst„llningar.];
                  ApplicationArea=#Suite;
                  SourceExpr=GLSetup."Additional Reporting Currency";
                  Editable=TRUE;
                  OnValidate=BEGIN
                               GLSetup := GLSetup2;
                             END;

                  OnAssistEdit=BEGIN
                                 ChangeExchangeRate.SetParameter(GLSetup."Additional Reporting Currency",CurrencyFactor,WORKDATE);
                                 IF ChangeExchangeRate.RUNMODAL = ACTION::OK THEN
                                   CurrencyFactor := ChangeExchangeRate.GetParameter;
                                 CLEAR(ChangeExchangeRate);
                               END;
                                }

      { 1   ;2   ;Field     ;
                  Name=DocumentNo;
                  CaptionML=[ENU=Document No.;
                             NOR=Bilagsnr.;
                             SVE=Dokumentnr];
                  ToolTipML=[ENU=Specifies a document number that will be copied to rounding entries if another document number does not apply. The batch job creates these rounding entries.;
                             NOR=Angir et dokumentnummer som skal kopieres til avrundingsposter hvis et annet dokumentnummer ikke passer. Disse avrundingspostene opprettes i kj›rselen.;
                             SVE=Anger ett dokumentnummer som kopieras till avrundningstransaktioner om inget annat dokumentnummer „r till„mpligt. Avrundningstransaktionerna skapas med hj„lp av buntjobbet.];
                  ApplicationArea=#Suite;
                  SourceExpr=DocumentNo }

      { 4   ;2   ;Field     ;
                  Name=RetainedEarningsAcc;
                  CaptionML=[ENU=Retained Earnings Acc.;
                             NOR=Konto for fri egenkapital;
                             SVE=Balanserad vinst eller f”rlust];
                  ToolTipML=[ENU=Specifies the retained earnings account that the batch job posts to. This account should be the same as the account that is used by the Close Income Statement batch job.;
                             NOR=Angir kontoen for fri egenkapital som kj›rselen bokf›res til. Denne kontoen m† v‘re den samme som kontoen som brukes av kj›rselen Lukk resultatregnskapet.;
                             SVE=Anger det konto f”r balanserad vinst eller f”rlust som bokf”ringen ska g”ras till n„r buntjobbet k”rs. Detta konto b”r vara detsamma som det som anv„nds i buntjobbet Avslut av resultatkonton.];
                  ApplicationArea=#Suite;
                  SourceExpr=RetainedEarningsGLAcc."No.";
                  TableRelation="G/L Account";
                  OnValidate=BEGIN
                               IF RetainedEarningsGLAcc."No." <> '' THEN BEGIN
                                 RetainedEarningsGLAcc.FIND;
                                 RetainedEarningsGLAcc.CheckGLAcc;
                               END;
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Enter a %1.;NOR=Angi %1.;SVE=Ange %1.';
      Text001@1001 : TextConst 'ENU=Enter Retained Earnings Account No.;NOR=Angi nummer for kontoen for fri egenkapital;SVE=Skriv in kontot f”r balanserad vinst eller f”rlust';
      Text002@1002 : TextConst 'ENU=Processing VAT Entries @1@@@@@@@@@@\;NOR=Behandler mva-poster   @1@@@@@@@@@@\;SVE=Behandlar momstrans.   @1@@@@@@@@@@\';
      Text003@1003 : TextConst 'ENU=Processing G/L Entries...\\;NOR=Behandler finansposter...\\;SVE=Behandlar redov.transaktioner...\\';
      Text004@1004 : TextConst 'ENU=Posting Date #1##########\;NOR=Bokf. dato             #1##########\;SVE=Bokf.datum             #1##########\';
      Text006@1005 : TextConst 'ENU=Item No. #1##########\;NOR=Varenr.                #1##########\;SVE=Art.nr                 #1##########\';
      Text007@1006 : TextConst 'ENU=Processing Job Ledger Entries...\\;NOR=Behandler prosjektposter...\\;SVE=Behandlar projektreskontratransaktioner...\\';
      Text008@1007 : TextConst 'ENU=Job No. #1##########\;NOR=Prosj.nr               #1##########\;SVE=Proj.nr                #1##########\';
      Text010@1008 : TextConst 'ENU=Residual caused by rounding of %1;NOR=Rest er for†rsaket av avrunding av %1;SVE=Rest p† grund av avrundning av %1';
      Text011@1009 : TextConst 'ENU=Processing Value Entries...\\;NOR=Behandler verdiposter...\\;SVE=Behandlar v„rdetransaktioner...\\';
      Text012@1012 : TextConst 'ENU=Processing Cost Entries...\\;NOR=Behandler kostposter ...\\;SVE=Behandlar kostnadstransaktioner...\\';
      Text99000002@1011 : TextConst 'ENU=Prod. Order No. #1##########\;NOR=Prod.ordrenr.          #1##########\;SVE=Prod.ordernr           #1##########\';
      Text99000004@1013 : TextConst 'ENU=Processing Finished Prod. Order Lines...\\;NOR=Behandler linjer for ferdig prod.ordre...\\;SVE=Behandlar f„rdiga prod.orderrader...\\';
      GLSetup@1014 : Record 98;
      GLSetup2@1015 : Record 98;
      SourceCodeSetup@1016 : Record 242;
      OldGLEntry@1017 : Record 17;
      OldValueEntry@1018 : Record 5802;
      OldJobLedgEntry@1019 : Record 11072005;
      OldProdOrderLine@1022 : Record 5406;
      OldCostEntry@1010 : Record 1104;
      CurrExchRate@1023 : Record 330;
      Currency@1024 : Record 4;
      GLEntry2@1025 : Record 17;
      GLReg@1026 : Record 45;
      TmpCloseIncomeStatementBuffer@1028 : TEMPORARY Record 347;
      TmpCloseIncomeStatementBuffer3@1029 : TEMPORARY Record 347;
      GLEntry3@1030 : Record 17;
      RetainedEarningsGLAcc@1031 : Record 15;
      ResidualGLAcc@1020 : Record 15;
      ChangeExchangeRate@1032 : Page 511;
      Window@1021 : Dialog;
      CurrencyFactor@1033 : Decimal;
      TotalAddCurrAmount@1035 : Decimal;
      TotalAmount@1036 : Decimal;
      NextEntryNo@1037 : Integer;
      LastEntryNo@1038 : Integer;
      NextTransactionNo@1039 : Integer;
      FiscalYearStartDate@1040 : Date;
      ReportIsExecuted@1041 : Boolean;
      VATEntryCount@1042 : Integer;
      VATEntryOldCount@1043 : Integer;
      VATEntryStep@1044 : Integer;
      FiscalYearStartDate2@1045 : Date;
      FiscalYearEndDate2@1046 : Date;
      LastDateChecked@1047 : Date;
      LastFiscalYearStartDate@1048 : Date;
      LastFiscalYearEndDate@1049 : Date;
      LastIsAccPeriodClosingDate@1050 : Boolean;
      DocumentNo@1051 : Code[20];

    [External]
    PROCEDURE SetAddCurr@2(AddCurr@1000 : Code[10]);
    BEGIN
      GLSetup."Additional Reporting Currency" := AddCurr;
    END;

    [External]
    PROCEDURE IsExecuted@3() : Boolean;
    BEGIN
      EXIT(ReportIsExecuted);
    END;

    LOCAL PROCEDURE ExchangeAmtLCYToFCY@1(PostingDate@1003 : Date;Amount@1000 : Decimal;IsUnitAmount@1001 : Boolean) : Decimal;
    VAR
      AmtRndgPrec@1002 : Decimal;
    BEGIN
      IF IsUnitAmount THEN
        AmtRndgPrec := Currency."Unit-Amount Rounding Precision"
      ELSE
        AmtRndgPrec := Currency."Amount Rounding Precision";
      EXIT(
        ROUND(
          CurrExchRate.ExchangeAmtLCYToFCY(
            0, '', //**4PS.n
            PostingDate,GLSetup."Additional Reporting Currency",Amount,
      //    CurrExchRate.ExchangeRate(PostingDate,GLSetup."Additional Reporting Currency")), //**4PS.o
            CurrExchRate.ExchangeRate(0, '',PostingDate,GLSetup."Additional Reporting Currency",TRUE),TRUE), //**4PS.n
          AmtRndgPrec));
    END;

    [External]
    PROCEDURE IsAccountingPeriodClosingDate@5(Date@1000 : Date) : Boolean;
    VAR
      AccountingPeriod@1001 : Record 50;
      OK@1002 : Boolean;
    BEGIN
      IF AccountingPeriod.ISEMPTY THEN
        EXIT(FALSE);

      IF Date <> LastDateChecked THEN BEGIN
        OK := AccountingPeriod.GET(NORMALDATE(Date) + 1);
        IF OK THEN
          OK := AccountingPeriod."New Fiscal Year";
        IF OK THEN BEGIN
          LastFiscalYearEndDate := CLOSINGDATE(Date);
          AccountingPeriod.SETRANGE("New Fiscal Year",TRUE);
          OK := AccountingPeriod.FIND('<');
          LastFiscalYearStartDate := AccountingPeriod."Starting Date";
          LastIsAccPeriodClosingDate := TRUE;
        END ELSE BEGIN
          LastIsAccPeriodClosingDate := FALSE;
          LastFiscalYearStartDate := 0D;
          LastFiscalYearEndDate := 0D;
        END;
      END;
      FiscalYearStartDate2 := LastFiscalYearStartDate;
      FiscalYearEndDate2 := LastFiscalYearEndDate;
      EXIT(LastIsAccPeriodClosingDate);
    END;

    [External]
    PROCEDURE CheckCombination@7(CloseIncomeStmtBuffer2@1000 : Record 347);
    BEGIN
      CLEAR(GLEntry3);
      GLEntry3.SETCURRENTKEY("G/L Account No.","Posting Date");
      GLEntry3.SETRANGE("G/L Account No.",CloseIncomeStmtBuffer2."G/L Account No.");
      GLEntry3.SETRANGE("Posting Date",FiscalYearStartDate2,FiscalYearEndDate2);

      GLEntry3.CALCSUMS(Amount);
      IF GLEntry3.Amount = 0 THEN BEGIN
        GLEntry3.CALCSUMS("Additional-Currency Amount");
        IF GLEntry3."Additional-Currency Amount" <> 0 THEN BEGIN
          InsertGLEntry(
            FiscalYearEndDate2,FiscalYearEndDate2,GLEntry3."Document Type"::" ",DocumentNo,
            CloseIncomeStmtBuffer2."G/L Account No.",
            '','',-GLEntry3."Additional-Currency Amount");

          InsertGLEntry(
            FiscalYearEndDate2,FiscalYearEndDate2,GLEntry3."Document Type"::" ",DocumentNo,
            RetainedEarningsGLAcc."No.",
            '','',GLEntry3."Additional-Currency Amount");
        END;
      END;
    END;

    [External]
    PROCEDURE InsertGLEntry@10(PostingDate@1000 : Date;DocumentDate@1001 : Date;DocumentType@1002 : Integer;DocumentNo@1003 : Code[20];GLAccountNo@1004 : Code[20];JnlBatchName@1005 : Code[10];ReasonCode@1006 : Code[10];AddCurrAmount@1007 : Decimal);
    VAR
      AccountingPeriodMgt@1008 : Codeunit 360;
    BEGIN
      IF NextEntryNo = 0 THEN BEGIN
        IF GLEntry2.FINDLAST THEN BEGIN
          LastEntryNo := GLEntry2."Entry No.";
          NextEntryNo := GLEntry2."Entry No." + 1;
          NextTransactionNo := GLEntry2."Transaction No." + 1;
        END;

        FiscalYearStartDate := AccountingPeriodMgt.GetPeriodStartingDate;

        GLReg.LOCKTABLE;
        IF GLReg.FINDLAST THEN;
        GLReg.Initialize(GLReg."No." + 1,0,0,SourceCodeSetup."Adjust Add. Reporting Currency",JnlBatchName,'');
      END ELSE
        NextEntryNo := NextEntryNo + 1;

      GLEntry2.INIT;
      GLEntry2."Posting Date" := PostingDate;
      GLEntry2."Document Date" := DocumentDate;
      GLEntry2."Document Type" := DocumentType;
      GLEntry2."Document No." := DocumentNo;
      GLEntry2."External Document No." := '';
      GLEntry2.Description :=
        COPYSTR(
          STRSUBSTNO(
            Text010,
            GLEntry2.FIELDCAPTION("Additional-Currency Amount")),
          1,MAXSTRLEN(GLEntry2.Description));
      GLEntry2."Source Code" := SourceCodeSetup."Adjust Add. Reporting Currency";
      GLEntry2."Source Type" := 0;
      GLEntry2."Source No." := '';
      GLEntry2."Job No." := '';
      GLEntry2.Quantity := 0;
      GLEntry2."Journal Batch Name" := JnlBatchName;
      GLEntry2."Reason Code" := ReasonCode;
      GLEntry2."Entry No." := NextEntryNo;
      GLEntry2."Transaction No." := NextTransactionNo;
      GLEntry2."G/L Account No." := GLAccountNo;
      GLEntry2.Amount := 0;
      GLEntry2."User ID" := USERID;
      GLEntry2."No. Series" := '';
      GLEntry2."System-Created Entry" := TRUE;
      GLEntry2."Prior-Year Entry" := GLEntry2."Posting Date" < FiscalYearStartDate;
      GLEntry2."Additional-Currency Amount" := AddCurrAmount;
      IF GLEntry2."Additional-Currency Amount" > 0 THEN BEGIN
        GLEntry2."Add.-Currency Debit Amount" := GLEntry2."Additional-Currency Amount";
        GLEntry2."Add.-Currency Credit Amount" := 0;
      END ELSE BEGIN
        GLEntry2."Add.-Currency Debit Amount" := 0;
        GLEntry2."Add.-Currency Credit Amount" := -GLEntry2."Additional-Currency Amount";
      END;
      //>>4PSSE.I009
      //GLEntry2.INSERT;
      GLEntry2.INSERT(TRUE);
      //<<4PSSE

      GLReg."To Entry No." := GLEntry2."Entry No.";
    END;

    [External]
    PROCEDURE InitializeRequest@4(NewDocumentNo@1000 : Code[20];NewRetainedEarningsGLAccNo@1001 : Code[20]);
    BEGIN
      DocumentNo := NewDocumentNo;
      RetainedEarningsGLAcc."No." := NewRetainedEarningsGLAccNo;
      GLSetup2 := GLSetup;
      //CurrencyFactor := CurrExchRate.ExchangeRate(WORKDATE,GLSetup."Additional Reporting Currency"); //**4PS.o
      CurrencyFactor := CurrExchRate.ExchangeRate(0, '',WORKDATE,GLSetup."Additional Reporting Currency",TRUE); //**4PS.n
    END;

    BEGIN
    {
      4PS, Objects renumbered
    }
    END.
  }
  RDLDATA
  {
  }
}

