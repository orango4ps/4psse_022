OBJECT XMLport 81624 Imp. Preclosure Result IMSE
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS7.00Toolsv1.MC;
  }
  PROPERTIES
  {
    CaptionML=ENU=Imp. Project Preclosure Result IMSE;
    DefaultFieldsValidation=No;
    Format/Evaluate=C/SIDE Format/Evaluate;
    PreserveWhiteSpace=Yes;
    OnInitXMLport=BEGIN
                    XMLNo := 81624;
                    MainTableID := 11012093;
                  END;

    OnPreXMLport=VAR
                   FileManagement@1100525001 : Codeunit 419;
                 BEGIN
                   IF NOT currXMLport.IMPORTFILE THEN BEGIN
                     AddConversionFileFormatLine;
                     FillConversionXMLPortFields;
                     COMMIT;
                     ERROR('');
                   END;

                   ConversionXMLPort.GET(XMLNo);

                   ConvMgt.CheckComCde(UseOldProjNoBln,11072003,ProjCommentCde);

                   ConvMgt.DeleteConComment(XMLNo);
                   ConvMgt.CreateConComment(XMLNo,STRSUBSTNO('Import File: %1.',currXMLport.FILENAME));
                   ConvMgt.CreateConComment(XMLNo,'Start');
                 END;

    OnPostXMLport=BEGIN
                    ConvMgt.CreateConComment(XMLNo,'Stop');
                    ConvMgt.CreateConComment(XMLNo,ConvMgt.CreateNoOfLinesCommentTxt(MainTableID,ImpLineCounter));
                  END;

    Format=Variable Text;
    FieldSeparator=[;];
  }
  ELEMENTS
  {
    { [{7B539476-C20A-46BC-A552-62FED55766AD}];  ;Root                ;Element ;Text     }

    { [{A667E51D-CE85-4A0D-946E-DE2CCC52ED51}];1 ;ProjectPreclosureResult;Element;Table ;
                                                  SourceTable=Table11012093;
                                                  SourceTableView=SORTING(Field10,Field20,Field25);
                                                  Import::OnBeforeInsertRecord=VAR
                                                                                 lvDate@1100285000 : Date;
                                                                                 lvLineNo@1100285001 : Integer;
                                                                                 lvDec@1100285002 : Decimal;
                                                                               BEGIN
                                                                                 IF A = '' THEN
                                                                                   currXMLport.SKIP;

                                                                                 ImpLineCounter := ImpLineCounter + 1;
                                                                                 CLEAR("Project Preclosure Result");

                                                                                 ProjCde := ConvMgt.DetermineNewCode(
                                                                                   UseOldProjNoBln,
                                                                                   CommentRec."Table Name"::Job,
                                                                                   ProjCommentCde,
                                                                                   A);

                                                                                 WITH "Project Preclosure Result" DO BEGIN
                                                                                   RESET;
                                                                                   SETRANGE("Project No.", ProjCde);
                                                                                   IF B <> '' THEN BEGIN
                                                                                     lvDate := 0D;
                                                                                     EVALUATE(lvDate, B);
                                                                                     SETRANGE("Posting Date", lvDate);
                                                                                   END;
                                                                                   IF FINDLAST THEN
                                                                                     lvLineNo := "Line No." + 10000
                                                                                   ELSE
                                                                                     lvLineNo := 10000;
                                                                                   VALIDATE("Project No.", ProjCde);
                                                                                   IF lvDate <> 0D THEN
                                                                                     VALIDATE("Posting Date", lvDate);

                                                                                   VALIDATE("Line No.",lvLineNo);

                                                                                   IF C <> '' THEN BEGIN
                                                                                     CLEAR(lvDec);
                                                                                     EVALUATE(lvDec, C);
                                                                                     VALIDATE("Actual Preclosure Result", lvDec);
                                                                                   END;
                                                                                   INSERT(TRUE);
                                                                                 END;

                                                                                 ClearFields;
                                                                                 currXMLport.SKIP;
                                                                               END;
                                                                                }

    { [{3AC77EC7-9326-4B76-BA5F-EA1F70D4DA67}];2 ;A                   ;Element ;Text    ;
                                                  Width=20 }

    { [{1B889F9B-B9E2-4749-96B0-D22BA1117049}];2 ;B                   ;Element ;Text    ;
                                                  Width=50 }

    { [{D76B46B3-243B-4FF7-8D1D-C7B64CCFA492}];2 ;C                   ;Element ;Text    ;
                                                  Width=50 }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   RunIsImportRunBln := TRUE;
                   CreateNewNosBln := TRUE;
                 END;

    }
    CONTROLS
    {
      { 1100409000;;Container;
                  Name=Imp. Fixed Assets;
                  ContainerType=ContentArea }

      { 1100409001;1;Group  ;
                  CaptionML=[ENU=Options;
                             NLD=Opties;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100285001;2;Field  ;
                  CaptionML=[ENU=Lookup Project No. based on old Project No.;
                             SVE=Val av projektnr baserat p† gammalt projektnr];
                  SourceExpr=UseOldProjNoBln;
                  OnValidate=BEGIN
                               IF NOT UseOldProjNoBln THEN
                                 ProjCommentCde := '';
                             END;
                              }

      { 1100285000;2;Field  ;
                  Name=ProjComCde;
                  CaptionML=[ENU=Comment Code for searching Project No.;
                             SVE=Kommentarskod f”r s”kning efter projektnr];
                  SourceExpr=ProjCommentCde;
                  TableRelation="Comment Code" }

    }
  }
  CODE
  {
    VAR
      "// GENERAL"@1100525034 : Integer;
      CommentRec@1100525033 : Record 97;
      ConvMgt@1100525032 : Codeunit 11020212;
      NoSerieMgt@1100525031 : Codeunit 396;
      XMLNo@1100525028 : Integer;
      MainTableID@1100525027 : Integer;
      CreateNewNosBln@1100525026 : Boolean;
      ImpLineCounter@1100525025 : Integer;
      CommentCde@1100525024 : Code[10];
      ConversionFileFormat@1100525023 : Record 11020691;
      RunIsImportRunBln@1100525022 : Boolean;
      ConversionXMLPort@1100525002 : Record 11020640;
      PostingDateFromImportFile@1100525001 : Boolean;
      "//"@1100525020 : Integer;
      "// THIS DATAPORT"@1100525019 : Integer;
      Text001@1100525036 : TextConst 'ENU=OPENING;NLD=BEGINSTAND;SVE=™PPNAR';
      Text002@1100525035 : TextConst 'ENU=Acquisition Cost;NLD=Aanschafwaarde;SVE=Redov.integration-anskaff.kost';
      Text003@1100525030 : TextConst 'ENU=Cumulative Depreciation;NLD=Cumulatieve afschrijving;SVE=Ackumulerad avskrivning';
      UseOldProjNoBln@1100285001 : Boolean;
      ProjCommentCde@1100285000 : Code[20];
      ProjCde@1100285002 : Code[20];

    PROCEDURE AddConversionFileFormatLine@1100529901();
    VAR
      FADepriationBook@1100529900 : Record 5612;
      GenJnlLine@1100529901 : Record 81;
    BEGIN
      // This function stores the file format into table [Conversion File Format].
      // There is no other standard documentation; so please maintain this function in case of customization.
      //
      // Parameters: 1: [Dataport No.],
      //             2: [csv-Column],
      //             3: [NAV Table ID],
      //             4: [NAV Field Name],
      //             5: [Data Type of dataport field],
      //             6: [extra Comment].
      //
      WITH ConversionFileFormat DO BEGIN
        ConversionFileFormat.DeleteColumnLines(XMLNo);
        InsertColumnLine(XMLNo,'A',MainTableID,"Project Preclosure Result".FIELDNAME("Project No."),'Code20','OldTxt');
        InsertColumnLine(XMLNo,'B',MainTableID,"Project Preclosure Result".FIELDNAME("Posting Date"),'Date','');
        InsertColumnLine(XMLNo,'C',MainTableID,"Project Preclosure Result".FIELDNAME("Actual Preclosure Result"),'Decimal','');
      END;
    END;

    PROCEDURE ClearFields@1100529902();
    BEGIN
      CLEAR("Project Preclosure Result");
      A := '';
      B := '';
      C := '';
      CLEAR(ProjCde);
    END;

    PROCEDURE FillConversionXMLPortFields@1100409004();
    VAR
      ConversionXMLPort@1100409000 : Record 11020640;
    BEGIN
      ConversionXMLPort.GET(XMLNo);
      ConversionXMLPort."Main Table ID" := MainTableID;
      ConversionXMLPort.MODIFY;
    END;

    BEGIN
    END.
  }
}

