OBJECT Codeunit 741 VAT Report Release/Reopen
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [External]
    PROCEDURE Release@1(VAR VATReportHeader@1000 : Record 740);
    VAR
      VATReportsConfiguration@1040 : Record 746;
      ErrorMessage@1041 : Record 700;
      IsValidated@1001 : Boolean;
    BEGIN
      VATReportHeader.CheckIfCanBeReleased(VATReportHeader);

      ErrorMessage.SetContext(VATReportHeader);
      ErrorMessage.ClearLog;

      IsValidated := FALSE;
      OnBeforeValidate(VATReportHeader,IsValidated);
      IF NOT IsValidated THEN BEGIN
        VATReportsConfiguration.SETRANGE("VAT Report Type",VATReportHeader."VAT Report Config. Code");
        IF VATReportHeader."VAT Report Version" <> '' THEN
          VATReportsConfiguration.SETRANGE("VAT Report Version",VATReportHeader."VAT Report Version");
        IF VATReportsConfiguration.FINDFIRST AND (VATReportsConfiguration."Validate Codeunit ID" <> 0) THEN
          CODEUNIT.RUN(VATReportsConfiguration."Validate Codeunit ID",VATReportHeader)
        ELSE
          CODEUNIT.RUN(CODEUNIT::"VAT Report Validate",VATReportHeader);
      END;

      IF ErrorMessage.HasErrors(FALSE) THEN
        EXIT;

      VATReportHeader.Status := VATReportHeader.Status::Released;
      VATReportHeader.MODIFY;
    END;

    [External]
    PROCEDURE Reopen@2(VAR VATReportHeader@1000 : Record 740);
    BEGIN
      VATReportHeader.CheckIfCanBeReopened(VATReportHeader);

      VATReportHeader.Status := VATReportHeader.Status::Open;
      VATReportHeader.MODIFY;
    END;

    [External]
    PROCEDURE Submit@5(VAR VATReportHeader@1000 : Record 740);
    BEGIN
      VATReportHeader.CheckIfCanBeSubmitted;

      VATReportHeader.Status := VATReportHeader.Status::Submitted;
      VATReportHeader.MODIFY;
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeValidate@3(VAR VATReportHeader@1000 : Record 740;VAR IsValidated@1001 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

