OBJECT Table 11072332 DPT Resp Pers Perm Project
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnModify=BEGIN
               CALCFIELDS("Subsite Structure");
               IF ("Subsite Structure" < "Subsite Structure"::PermissionPerDocType) AND (Type = Type::Access) THEN
                 "Document Type" := '';
               CheckPermissions;
             END;

    OnDelete=BEGIN
               CheckRecordChangeAllowed;
             END;

    OnRename=BEGIN
               CheckRecordChangeAllowed;
             END;

    CaptionML=ENU=DPT Responsible Person Permission (Project);
  }
  FIELDS
  {
    { 10  ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Typ];
                                                   OptionCaptionML=ENU=,Access;
                                                   OptionString=,Access;
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 20  ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 30  ;   ;Line No.            ;Integer       ;TableRelation="Project Responsible Person"."Line No." WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr];
                                                   NotBlank=Yes }
    { 31  ;   ;Responsibility      ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Responsible Person".Responsibility WHERE (Project No.=FIELD(Project No.),
                                                                                                                         Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=Responsibility;
                                                              NOR=Ansvar;
                                                              SVE=Ansvar];
                                                   Editable=No }
    { 32  ;   ;Responsibility Description;Text50  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Responsibility.Description WHERE (Code=FIELD(Responsibility)));
                                                   CaptionML=[ENU=Responsibility Description;
                                                              NOR=Ansvar, Beskrivelse;
                                                              SVE=Ansvar, beskrivning];
                                                   Editable=No }
    { 33  ;   ;Employee No.        ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Responsible Person"."Employee No." WHERE (Project No.=FIELD(Project No.),
                                                                                                                         Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=Employee No.;
                                                              NOR=Ansattnr;
                                                              SVE=Anst„llningsnr];
                                                   Editable=No }
    { 34  ;   ;Employee Name       ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Full Name" WHERE (No.=FIELD(Employee No.)));
                                                   CaptionML=[ENU=Employee Name;
                                                              NOR=Navn p† ansatt;
                                                              SVE=Namn p† anst„lld];
                                                   Editable=No }
    { 40  ;   ;Subsite Code        ;Code20        ;TableRelation="DPT Subsite Project".Code WHERE (Project No.=FIELD(Project No.),
                                                                                                   Quote Request Subsite=CONST(No));
                                                   CaptionML=ENU=Subsite Code }
    { 41  ;   ;Subsite Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Subsite Project".Description WHERE (Project No.=FIELD(Project No.),
                                                                                                               Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Subsite Description;
                                                   Editable=No }
    { 42  ;   ;Subsite Structure   ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Subsite Project"."Subsite Structure" WHERE (Project No.=FIELD(Project No.),
                                                                                                                       Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Subsite Structure;
                                                   OptionCaptionML=ENU=1 Document Library,Document Library per Document Type,Permission per Document Type;
                                                   OptionString=OneDocLibrary,DocLibraryPerDocType,PermissionPerDocType;
                                                   Editable=No }
    { 43  ;   ;Quote Request Subsite;Boolean      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Subsite Project"."Quote Request Subsite" WHERE (Project No.=FIELD(Project No.),
                                                                                                                           Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Quote Request Subsite;
                                                   Editable=No }
    { 50  ;   ;Document Type       ;Code20        ;TableRelation=IF (Type=CONST(Access),
                                                                     Subsite Structure=FILTER(>OneDocLibrary)) "DPT Subsite Doc Type Project"."Document Type" WHERE (Project No.=FIELD(Project No.),
                                                                                                                                                                     Subsite Code=FIELD(Subsite Code));
                                                   CaptionML=[ENU=Document Type;
                                                              NOR=Dokumenttype;
                                                              SVE=Dokumenttyp] }
    { 51  ;   ;Document Type Description;Text50   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Document Type".Description WHERE (Type=FIELD(Document Type)));
                                                   CaptionML=[ENU=Document Type Description;
                                                              NOR=Dokumenttype, Beskrivelse;
                                                              SVE=Dokumenttyp, beskrivning];
                                                   Editable=No }
    { 60  ;   ;Project Element     ;Code20        ;TableRelation="Project Element".Element WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=[ENU=Project Element;
                                                              NOR=Prosjektelement;
                                                              SVE=Projektelement] }
    { 61  ;   ;Project Element Description;Text50 ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Element".Description WHERE (Project No.=FIELD(Project No.),
                                                                                                           Element=FIELD(Project Element)));
                                                   CaptionML=ENU=Project Element Description;
                                                   Editable=No }
    { 70  ;   ;Permission Level    ;Option        ;OnValidate=BEGIN
                                                                IF ("Permission Level" < "Private Permission Level") THEN
                                                                  "Private Permission Level" := "Permission Level";
                                                                IF ("Permission Level" = "Permission Level"::Owner) AND ("Subsite Code" <> '') AND ("Document Type" = '') THEN
                                                                  "Private Permission Level" := "Permission Level";
                                                              END;

                                                   CaptionML=ENU=Permission Level;
                                                   OptionCaptionML=ENU=None,Visitor,Member,Owner;
                                                   OptionString=None,Visitor,Member,Owner }
    { 80  ;   ;Show in View OLD    ;Boolean       ;CaptionML=;
                                                   Editable=No }
    { 90  ;   ;Subsite Synced On   ;DateTime      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Subsite Project"."Synchronized On" WHERE (Project No.=FIELD(Project No.),
                                                                                                                     Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Subsite Synchronized On;
                                                   Editable=No }
    { 100 ;   ;Subsite Doc Type Synced On;DateTime;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Subsite Doc Type Project"."Synchronized On" WHERE (Project No.=FIELD(Project No.),
                                                                                                                              Subsite Code=FIELD(Subsite Code),
                                                                                                                              Document Type=FIELD(Document Type)));
                                                   CaptionML=ENU=Subsite Document Type Synchronized On;
                                                   Editable=No }
    { 110 ;   ;Private Permission Level;Option    ;OnValidate=BEGIN
                                                                TESTFIELD("Subsite Code");
                                                                TESTFIELD("Document Type", '');
                                                                IF ("Permission Level" = "Permission Level"::Owner) THEN
                                                                  "Private Permission Level" := "Permission Level";
                                                                IF ("Permission Level" < "Private Permission Level") THEN
                                                                  FIELDERROR("Private Permission Level");
                                                              END;

                                                   CaptionML=ENU=Private Permission Level;
                                                   OptionCaptionML=ENU=None,Visitor,Member,Owner;
                                                   OptionString=None,Visitor,Member,Owner }
  }
  KEYS
  {
    {    ;Type,Project No.,Line No.,Subsite Code,Document Type,Project Element;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100409000 : TextConst 'ENU=You have no permission to change Portal Permission Records.';
      Text001@1100528300 : TextConst 'ENU=%1 Permission Records added and %2 Permission Records modified.';
      AddCounter@1100528302 : Integer;
      ModifyCounter@1100528303 : Integer;

    PROCEDURE CheckPermissions@1100409007();
    BEGIN
      CheckModifyPermission;
      CheckQuoteSitePermissionLevel;
      CheckPermissionLevel;
    END;

    LOCAL PROCEDURE CheckPermissionLevel@1100528303();
    VAR
      DPTRespPersPermProject@1100528300 : Record 11072332;
    BEGIN
      IF (Type <> Type::Access) THEN
        EXIT;
      IF (("Subsite Code" = '') AND ("Document Type" = '') AND ("Permission Level" = "Permission Level"::None)) THEN BEGIN
        DPTRespPersPermProject.SETRANGE(Type , DPTRespPersPermProject.Type::Access);
        DPTRespPersPermProject.SETRANGE("Project No.", "Project No.");
        DPTRespPersPermProject.SETRANGE("Line No.", "Line No.");
        DPTRespPersPermProject.SETFILTER("Subsite Code", '<>%1', "Subsite Code");
        DPTRespPersPermProject.MODIFYALL("Permission Level", "Permission Level"::None);
        EXIT;
      END;
      CALCFIELDS("Subsite Structure");
      IF ("Subsite Structure" < "Subsite Structure"::PermissionPerDocType) THEN
        EXIT;

      IF ("Document Type" = '') THEN BEGIN
        DPTRespPersPermProject.SETRANGE(Type, Type);
        DPTRespPersPermProject.SETRANGE("Project No.", "Project No.");
        DPTRespPersPermProject.SETRANGE("Line No.", "Line No.");
        DPTRespPersPermProject.SETRANGE("Subsite Code", "Subsite Code");
        DPTRespPersPermProject.SETFILTER("Document Type", '<>%1', "Document Type");
        DPTRespPersPermProject.MODIFYALL("Permission Level", "Permission Level");
      END ELSE BEGIN
        DPTRespPersPermProject.GET(Type, "Project No.", "Line No.", "Subsite Code", '');
        IF (DPTRespPersPermProject."Permission Level" = DPTRespPersPermProject."Permission Level"::None) THEN BEGIN
          "Permission Level" := "Permission Level"::Visitor;
        END;
        IF (DPTRespPersPermProject."Permission Level" < "Permission Level") THEN BEGIN
          DPTRespPersPermProject."Permission Level" := "Permission Level";
          DPTRespPersPermProject.MODIFY;
        END;
      END;
    END;

    PROCEDURE CheckModifyPermission@1100409000();
    VAR
      UserSetup@1100409000 : Record 91;
      ProjectResponsiblePerson@1100409001 : Record 11012040;
      DPTRespPersPermProject@1100409002 : Record 11072332;
    BEGIN
      IF (NOT UserSetup.GET(USERID)) THEN
        ERROR(Text000);
      UserSetup.TESTFIELD("Employee No.");
      ProjectResponsiblePerson.SETRANGE("Project No.", "Project No.");
      ProjectResponsiblePerson.SETRANGE("Employee No.", UserSetup."Employee No.");
      IF (NOT ProjectResponsiblePerson.FINDSET) THEN
        ERROR(Text000);
      REPEAT
        DPTRespPersPermProject.SETRANGE(Type, DPTRespPersPermProject.Type::Access);
        DPTRespPersPermProject.SETRANGE("Project No.", ProjectResponsiblePerson."Project No.");
        DPTRespPersPermProject.SETRANGE("Line No.", ProjectResponsiblePerson."Line No.");
        DPTRespPersPermProject.SETFILTER("Employee No.", '<>%1', ProjectResponsiblePerson."Employee No.");
        DPTRespPersPermProject.SETFILTER("Permission Level", '<>%1', DPTRespPersPermProject."Permission Level"::Owner);
        IF (DPTRespPersPermProject.FINDFIRST) THEN
          ERROR(Text000);
      UNTIL (ProjectResponsiblePerson.NEXT = 0);
    END;

    LOCAL PROCEDURE CheckQuoteSitePermissionLevel@1100409001();
    BEGIN
      CALCFIELDS("Quote Request Subsite");
      IF (NOT "Quote Request Subsite") THEN
        EXIT;
      IF ("Permission Level" = "Permission Level"::Member) THEN
        FIELDERROR("Permission Level");
    END;

    PROCEDURE AddPortalPermissions@1100528300(ProjectNo@1100528300 : Code[20];LineNoFilter@1100528307 : Integer;SubsiteCodeFilter@1100528306 : Code[20];ShowMessage@1100528308 : Boolean);
    VAR
      ProjectResponsiblePerson@1100528301 : Record 11012040;
      Job@1100528304 : Record 11072003;
    BEGIN
      AddCounter := 0;
      ModifyCounter := 0;
      Job.GET(ProjectNo);

      ProjectResponsiblePerson.SETRANGE("Project No.", ProjectNo);
      IF (LineNoFilter <> 0) THEN
        ProjectResponsiblePerson.SETRANGE("Line No.", LineNoFilter);
      IF (NOT ProjectResponsiblePerson.FINDSET) THEN
        EXIT;

      REPEAT
        AddPortalPermission(ProjectResponsiblePerson, SubsiteCodeFilter);
      UNTIL (ProjectResponsiblePerson.NEXT = 0);

      IF (ShowMessage) THEN
        MESSAGE(Text001, AddCounter, ModifyCounter - AddCounter);
    END;

    PROCEDURE AddPortalPermission@1100528500(ProjectResponsiblePerson@1100528500 : Record 11012040;SubsiteCodeFilter@1100528501 : Code[20]);
    VAR
      DPTSubsiteProject@1100528303 : Record 11072330;
      DPTSubsiteDocTypeProject@1100528302 : Record 11072331;
    BEGIN
        InsertRecord(ProjectResponsiblePerson, 1, '', '');
        DPTSubsiteProject.SETRANGE("Project No.", ProjectResponsiblePerson."Project No.");
        IF (SubsiteCodeFilter <> '') THEN
          DPTSubsiteProject.SETRANGE(Code, SubsiteCodeFilter);
        DPTSubsiteProject.SETRANGE("Subsite Structure", DPTSubsiteProject."Subsite Structure"::PermissionPerDocType);
        IF (DPTSubsiteProject.FINDSET) THEN BEGIN
          REPEAT
            DPTSubsiteDocTypeProject.SETRANGE("Project No.", ProjectResponsiblePerson."Project No.");
            DPTSubsiteDocTypeProject.SETRANGE("Subsite Code", DPTSubsiteProject.Code);
            IF (DPTSubsiteDocTypeProject.FINDSET) THEN BEGIN
              InsertRecord(ProjectResponsiblePerson, 1, DPTSubsiteProject.Code, ''); //1=Access
              REPEAT
                InsertRecord(ProjectResponsiblePerson, 1, DPTSubsiteProject.Code, DPTSubsiteDocTypeProject."Document Type");
              UNTIL(DPTSubsiteDocTypeProject.NEXT = 0);
            END;
          UNTIL (DPTSubsiteProject.NEXT = 0);
        END;

        DPTSubsiteProject.SETFILTER("Subsite Structure", '<%1', DPTSubsiteProject."Subsite Structure"::PermissionPerDocType);
        IF (DPTSubsiteProject.FINDSET) THEN BEGIN
          REPEAT
            InsertRecord(ProjectResponsiblePerson, 1, DPTSubsiteProject.Code, ''); //1=Access
          UNTIL (DPTSubsiteProject.NEXT = 0);
        END;
    END;

    LOCAL PROCEDURE InsertRecord@1100528302(ProjectResponsiblePerson@1100528306 : Record 11012040;PermissionType@1100528300 : Option;SubsiteCode@1100528304 : Code[20];DocumentType@1100528305 : Code[20]);
    VAR
      DPTRespPersPermProject@1100528301 : Record 11072332;
      DPTDefRespPersPermProject@1100528303 : Record 11072333;
      Job@1100528307 : Record 11072003;
    BEGIN
      Job.GET(ProjectResponsiblePerson."Project No.");
      IF (NOT DPTDefRespPersPermProject.GET(PermissionType, ProjectResponsiblePerson.Responsibility,
        SubsiteCode, DocumentType))
      THEN BEGIN
        DPTDefRespPersPermProject.INIT;
      END;

      IF (NOT DPTRespPersPermProject.GET(PermissionType, ProjectResponsiblePerson."Project No.",
          ProjectResponsiblePerson."Line No.", SubsiteCode, DocumentType))
      THEN BEGIN
        DPTRespPersPermProject.INIT;
        DPTRespPersPermProject.Type := PermissionType;
        DPTRespPersPermProject."Project No." := ProjectResponsiblePerson."Project No.";
        DPTRespPersPermProject."Line No." := ProjectResponsiblePerson."Line No.";
        DPTRespPersPermProject."Subsite Code" := SubsiteCode;
        DPTRespPersPermProject."Document Type" := DocumentType;
        DPTRespPersPermProject.INSERT;
        AddCounter += 1;
      END;
      DPTRespPersPermProject."Permission Level" := DPTDefRespPersPermProject."Permission Level";
      DPTRespPersPermProject."Private Permission Level" := DPTDefRespPersPermProject."Private Permission Level";
      DPTRespPersPermProject.MODIFY;
      ModifyCounter += 1;
    END;

    LOCAL PROCEDURE CheckRecordChangeAllowed@1100528307();
    VAR
      Job@1100528300 : Record 11072003;
    BEGIN
      IF (Type <> Type::Access) OR ("Project Element" <> '') OR (NOT Job.GET("Project No.")) THEN
        EXIT;
    END;

    PROCEDURE MakePrivatePermEqualToPerm@1100529900();
    VAR
      DPTRespPersPermProject@1100529900 : Record 11072332;
    BEGIN
      DPTRespPersPermProject.SETRANGE(Type, Type::Access);
      DPTRespPersPermProject.SETRANGE("Project No.", "Project No.");
      DPTRespPersPermProject.SETFILTER("Subsite Code", '%1', '');
      DPTRespPersPermProject.MODIFYALL("Private Permission Level", "Private Permission Level"::None, FALSE);


      DPTRespPersPermProject.SETFILTER("Subsite Code", '<>%1', '');
      DPTRespPersPermProject.SETFILTER("Document Type", '<>%1', '');
      DPTRespPersPermProject.MODIFYALL("Private Permission Level", "Private Permission Level"::None, FALSE);

      DPTRespPersPermProject.SETFILTER("Document Type", '%1', '');
      IF (NOT DPTRespPersPermProject.FINDSET) THEN
        EXIT;
      REPEAT
        DPTRespPersPermProject."Private Permission Level" := DPTRespPersPermProject."Permission Level";
        DPTRespPersPermProject.MODIFY;
      UNTIL (DPTRespPersPermProject.NEXT = 0);
    END;

    BEGIN
    END.
  }
}

