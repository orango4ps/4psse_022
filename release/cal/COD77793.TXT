OBJECT Codeunit 77793 Export DotNet Translation File
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=TTool;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      TTNetProduct@1008 : Record 77791;
      TTNetCaption@1000 : Record 77793;
      TTNetFile@1004 : Record 77794;
      XMLDOMManagement4PS@1007 : Codeunit 11020220;
      Window@1210190000 : Dialog;
      Text000@1210190001 : TextConst 'ENU=Table:      #1########  #2################################\;SVE=Tabell:     #1########  #2################################\';
      Text001@1210190002 : TextConst 'ENU=Progress: @3@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@;SVE=F”rlopp:  @3@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@';
      Text002@1001 : TextConst 'ENU=Product Code does not exist.';
      Text003@1002 : TextConst 'ENU=Are you sure you want to export all translation files for this product?';
      InStr@1003 : InStream;
      XmlDoc@1005 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      XMLNodeRoot@1006 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";

    PROCEDURE ExportTranslationData@1100525000(NetProductCode@1003 : Code[20];NetFileCode@1002 : Code[20];ExportLanguageCode@1001 : Code[10];FileName@1100525000 : Text);
    VAR
      XmlElement@1100409007 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlElement";
      XmlElementTable@1004 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlElement";
      lvValueText@1000 : Text;
      lvFileName@1006 : Text;
    BEGIN
      TTNetFile.GET(NetProductCode,NetFileCode);
      TTNetFile.CALCFIELDS("File Content");

      XmlDoc := XmlDoc.XmlDocument;
      TTNetFile."File Content".CREATEINSTREAM(InStr);
      XmlDoc.Load(InStr);

      XMLNodeRoot := XmlDoc.DocumentElement; // Returns the root node of the document

      TTNetCaption.SETCURRENTKEY("Entry No.");
      TTNetCaption.SETRANGE("Product Code",NetProductCode);
      TTNetCaption.SETRANGE("File Code",NetFileCode);
      TTNetCaption.SETRANGE("Language ID",ExportLanguageCode);

      WindowOpen;
      IF TTNetCaption.FINDSET THEN
        REPEAT
          WindowUpdate(TTNetCaption."File Code", TTNetCaption."Data Name ID", 0, 0);
          lvValueText := TTNetCaption.GetCaption;
          IF lvValueText <> '' THEN
            SearchDataTag(TTNetCaption."Product Code", TTNetCaption."File Code", TTNetCaption."Data Name ID", lvValueText, XMLNodeRoot);
        UNTIL TTNetCaption.NEXT = 0;
      WindowClose;

      XMLDOMManagement4PS.SaveXMLToClientFile(XmlDoc, FileName);
    END;

    LOCAL PROCEDURE SearchDataTag@4(ProductCode@1100525000 : Code[20];FileCode@1100525008 : Code[20];DataNameID@1100525004 : Code[100];ValueText@1001 : Text;XMLNodeRoot@1004 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode");
    VAR
      TTNetDataName@1000 : Record 77792;
      XMLNodeData@1005 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      XmlElementValue@1003 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlElement";
      NameText@1002 : Text;
      SearchTagText@1006 : Text;
    BEGIN
      IF TTNetDataName.GET(ProductCode,FileCode,DataNameID) THEN
        NameText := TTNetDataName."Text ID";

      IF XMLDOMManagement4PS.FindNode(XMLNodeRoot,STRSUBSTNO('data[@name="%1"]', NameText),XMLNodeData) THEN BEGIN
        XMLDOMManagement4PS.FindNode(XMLNodeData,'value', XmlElementValue);
        XmlElementValue.InnerText:=ValueText;
      END;
    END;

    PROCEDURE WindowOpen@1210190000();
    BEGIN
      Window.OPEN(Text000 + Text001);
    END;

    PROCEDURE WindowClose@1210190001();
    BEGIN
      Window.CLOSE;
    END;

    PROCEDURE WindowUpdate@1210190002(FileCode@1210190000 : Code[20];DataNameID@1210190001 : Code[100];Counter@1210190002 : Integer;MaxCounter@1210190003 : Integer);
    BEGIN
      Window.UPDATE(1, FileCode);
      Window.UPDATE(2, DataNameID);
    END;

    PROCEDURE ExportAllFilesOfProduct@1(NetProductCode@1000 : Code[20];ExportLanguageCode@1004 : Code[10]);
    VAR
      TTNetFile@1002 : Record 77794;
      ExportFileName@1003 : Text;
    BEGIN
      IF NOT TTNetProduct.GET(NetProductCode) THEN
        ERROR(Text002);

      IF NOT CONFIRM(Text003) THEN
        EXIT;

      TTNetFile.SETRANGE("Product Code",NetProductCode);
      IF TTNetFile.FINDSET THEN
        REPEAT
          ExportFileName := DetermineExportFileName(TTNetFile."Import File Name",TTNetProduct."File Name Addition",ExportLanguageCode);
          ExportTranslationData(NetProductCode,TTNetFile.Code,ExportLanguageCode,ExportFileName);
        UNTIL TTNetFile.NEXT = 0;
    END;

    PROCEDURE DetermineExportFileName@2(FileName@1000 : Text;FileNameAddition@1001 : Text[11];ExportLanguageCode@1002 : Code[10]) : Text;
    VAR
      Position@1004 : Integer;
    BEGIN
      IF ExportLanguageCode = TTNetProduct."Source Language ID" THEN
        EXIT(FileName)
      ELSE BEGIN
        FileNameAddition := '.' + FileNameAddition + '.resx';
        Position := STRPOS(FileName,'.resx');
        IF Position > 0 THEN BEGIN
          FileName := COPYSTR(FileName,1,Position-1);
          EXIT(FileName + FileNameAddition);
        END;
      END;
    END;

    EVENT XmlDoc@1005::NodeInserting@93(sender@1001 : Variant;e@1000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT XmlDoc@1005::NodeInserted@94(sender@1001 : Variant;e@1000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT XmlDoc@1005::NodeRemoving@95(sender@1001 : Variant;e@1000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT XmlDoc@1005::NodeRemoved@96(sender@1001 : Variant;e@1000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT XmlDoc@1005::NodeChanging@97(sender@1001 : Variant;e@1000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT XmlDoc@1005::NodeChanged@98(sender@1001 : Variant;e@1000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    BEGIN
    END.
  }
}

