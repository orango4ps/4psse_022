OBJECT Table 6086321 Expense Allocation
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.3.00.01,4PS14.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Entry No.,Continia User ID,Expense Type,Description;
    OnInsert=VAR
               Expense@1160040001 : Record 6086320;
               SplitAndAllocationMgt@1160040000 : Codeunit 6086301;
             BEGIN
               "Entry No." := GetLastEntryNo + 1;

               TestNotPosted;

               Expense.GET("Expense Entry No.");
               SplitAndAllocationMgt.CopyExpDimToAllocationDim(Expense,Rec);
               AddDefaultDim(0);

               IF NOT ("Amount %" IN [0,100]) THEN
                 Modified := TRUE;

               SplitAndAllocationMgt.InsertAllocationComment("Expense Entry No.");

               IF Modified THEN
                 SendToExpenseUser(FALSE);
             END;

    OnModify=BEGIN
               TestNotPosted;
               Modified := TRUE;

               SendToExpenseUser(FALSE);
             END;

    OnDelete=VAR
               EMAttendee@1160040003 : Record 6086308;
               ExpenseAllocation@1160040001 : Record 6086321;
               ExpAllocationDim@1160040000 : Record 6086357;
               SplitAndAllocationMgt@1160040002 : Codeunit 6086301;
             BEGIN
               TestNotPosted;

               ExpAllocationDim.SETRANGE("Expense Allocation Entry No.","Entry No.");
               ExpAllocationDim.DELETEALL;

               ExpenseAllocation.SETCURRENTKEY("Expense Entry No.");
               ExpenseAllocation.SETRANGE("Expense Entry No.","Expense Entry No.");
               ExpenseAllocation.SETFILTER("Entry No.",'<>%1',"Entry No.");
               IF ExpenseAllocation.ISEMPTY THEN
                 SplitAndAllocationMgt.RemoveAllocationComment("Expense Entry No.");

               EMAttendee.SETRANGE("Table ID",DATABASE::"Expense Allocation");
               EMAttendee.SETRANGE("Doc. Ref. No.","Entry No.");
               EMAttendee.DELETEALL;

               IF Modified AND (NOT SkipSendToExpUser) THEN
                 SendToExpenseUser(TRUE);
             END;

    CaptionML=[DEU=Ausgabenzuordnung;
               ENU=Expense Allocation;
               NLD=Uitgaveallocatie];
    LookupPageID=Page6086320;
    DrillDownPageID=Page6086320;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=[DEU=Lfd. Nr.;
                                                              ENU=Entry No.;
                                                              NLD=Volgnr.;
                                                              NOR=Lõpenummer;
                                                              SVE=Lîpnr] }
    { 2   ;   ;Continia User ID    ;Code50        ;TableRelation="Continia User Setup";
                                                   OnValidate=VAR
                                                                Expense@1160040000 : Record 6086320;
                                                              BEGIN
                                                                Expense.GET("Expense Entry No.");
                                                                Expense.TESTFIELD(Posted,FALSE);
                                                                IF "Continia User ID" <> xRec."Continia User ID" THEN
                                                                  IF "Continia User ID" <> Expense."Continia User ID" THEN
                                                                    Expense.TESTFIELD("Matched to Bank Transaction",FALSE);
                                                                Expense.TESTFIELD(Status,Expense.Status::Open);

                                                                VALIDATE("Expense Type");
                                                              END;

                                                   CaptionML=[DEU=Mitarbeiternr.;
                                                              ENU=Continia User ID;
                                                              NLD=Continia gebruikers-id;
                                                              NOR=Ansattnr;
                                                              SVE=AnstÑllningsnr] }
    { 4   ;   ;Description         ;Text50        ;CaptionML=[DEU=Beschreibung;
                                                              ENU=Description;
                                                              NLD=Omschrijving;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 5   ;   ;Description 2       ;Text50        ;CaptionML=[DEU=Beschreibung 2;
                                                              ENU=Description 2;
                                                              NLD=Omschrijving 2;
                                                              NOR=Beskrivelse 2;
                                                              SVE=Beskrivning 2] }
    { 6   ;   ;Document Date       ;Date          ;CaptionML=[DEU=Belegdatum;
                                                              ENU=Document Date;
                                                              NLD=Documentdatum;
                                                              NOR=Bilagsdato;
                                                              SVE=Dokumentdatum] }
    { 7   ;   ;Date Created        ;Date          ;CaptionML=[DEU=Erstellt am;
                                                              ENU=Date Created;
                                                              NLD=Datum aangemaakt;
                                                              NOR=Opprettet;
                                                              SVE=Skapat datum] }
    { 8   ;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   OnValidate=BEGIN
                                                                VALIDATE("Expense Type");
                                                              END;

                                                   CaptionML=[DEU=LÑnder-/Regionscode;
                                                              ENU=Country/Region Code;
                                                              NLD=Land-/regiocode;
                                                              NOR=Lands-/regionkode;
                                                              SVE=Lands-/regionkod];
                                                   NotBlank=Yes }
    { 9   ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=[DEU=WÑhrungscode;
                                                              ENU=Currency Code;
                                                              NLD=Valutacode;
                                                              NOR=Valutakode;
                                                              SVE=Valutakod] }
    { 10  ;   ;No Refund           ;Boolean       ;CaptionML=[DEU=Keine Erstattung;
                                                              ENU=No Refund;
                                                              NLD=Geen restitutie] }
    { 11  ;   ;Amount              ;Decimal       ;OnValidate=BEGIN
                                                                UpdateAmount(FIELDNO(Amount));
                                                              END;

                                                   CaptionML=[DEU=Betrag;
                                                              ENU=Amount;
                                                              NLD=Bedrag;
                                                              NOR=Amount;
                                                              SVE=Belopp];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 12  ;   ;Amount (LCY)        ;Decimal       ;CaptionML=[DEU=Betrag (MW);
                                                              ENU=Amount (LCY);
                                                              NLD=Bedrag (LV);
                                                              NOR=Belõp (NOK);
                                                              SVE=Belopp (BVA)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 18  ;   ;Bank-Currency Amount;Decimal       ;CaptionML=[DEU=Betrag in BankkontowÑhrung;
                                                              ENU=Bank-Currency Amount;
                                                              NLD=Bankvalutabedrag;
                                                              SVE=Belopp (BVA)];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Bank Currency Code" }
    { 19  ;   ;Bank Currency Code  ;Code10        ;TableRelation=Currency;
                                                   CaptionML=[DEU=Bankto.-WÑhrungscode;
                                                              ENU=Bank Currency Code;
                                                              NLD=Bankvalutacode];
                                                   Editable=No }
    { 21  ;   ;Admin Comment       ;Text250       ;CaptionML=[DEU=Admin Bemerkung;
                                                              ENU=Admin Comment;
                                                              NLD=Admin Opmerking] }
    { 23  ;   ;Global Dimension 1 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=VAR
                                                                GLSetup@1160040001 : Record 98;
                                                                SplitAndAllocationMgt@1160040002 : Codeunit 6086301;
                                                              BEGIN
                                                                GLSetup.GET;
                                                                IF GLSetup."Global Dimension 1 Code" <> '' THEN
                                                                  SplitAndAllocationMgt.UpdateExpAllocDim("Entry No.",GLSetup."Global Dimension 1 Code","Global Dimension 1 Code",'','');
                                                              END;

                                                   CaptionML=[DEU=Globaler Dimensionscode 1;
                                                              ENU=Global Dimension 1 Code;
                                                              NLD=Globale dimensiecode 1;
                                                              NOR=Global dimensjon 1 - kode;
                                                              SVE=Global dimension 1 kod];
                                                   CaptionClass='1,1,1' }
    { 24  ;   ;Global Dimension 2 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=VAR
                                                                GLSetup@1160040001 : Record 98;
                                                                SplitAndAllocationMgt@1160040002 : Codeunit 6086301;
                                                              BEGIN
                                                                GLSetup.GET;
                                                                IF GLSetup."Global Dimension 2 Code" <> '' THEN
                                                                  SplitAndAllocationMgt.UpdateExpAllocDim("Entry No.",GLSetup."Global Dimension 2 Code","Global Dimension 2 Code",'','');
                                                              END;

                                                   CaptionML=[DEU=Globaler Dimensionscode 2;
                                                              ENU=Global Dimension 2 Code;
                                                              NLD=Globale dimensiecode 2;
                                                              NOR=Global Dimensjon 2-kode;
                                                              SVE=Global dimension 2 kod];
                                                   CaptionClass='1,1,2' }
    { 50  ;   ;Job No.             ;Code20        ;TableRelation=Job;
                                                   OnValidate=BEGIN
                                                                IF "Job No." = '' THEN
                                                                  VALIDATE("Job Task No.",'');
                                                                VALIDATE(Billable,"Job No." <> '');
                                                                AddDefaultDim(CurrFieldNo);
                                                              END;

                                                   CaptionML=[DEU=Projektnr.;
                                                              ENU=Job No.;
                                                              NLD=Projectnr.;
                                                              NOR=Prosjektnr.;
                                                              SVE=Projektnr] }
    { 51  ;   ;Job Task No.        ;Code20        ;TableRelation="Job Task"."Job Task No." WHERE (Job No.=FIELD(Job No.));
                                                   OnValidate=BEGIN
                                                                VALIDATE(Billable,"Job No." <> '');
                                                              END;

                                                   CaptionML=[DEU=Projektaufgabennr.;
                                                              ENU=Job Task No.;
                                                              NLD=Projecttaaknr.;
                                                              NOR=Prosjektaktivitetsnr.;
                                                              SVE=Projektaktivitetsnr] }
    { 52  ;   ;Billable            ;Boolean       ;OnValidate=BEGIN
                                                                IF Billable THEN
                                                                  "Job Line Type" := "Job Line Type"::Contract
                                                                ELSE
                                                                  "Job Line Type" := "Job Line Type"::" ";
                                                              END;

                                                   CaptionML=[DEU=Fakturierbar;
                                                              ENU=Billable;
                                                              NLD=Facturabel;
                                                              SVE=Fakturerbart] }
    { 53  ;   ;Job Line Type       ;Option        ;CaptionML=[DEU=Projektzeilenart;
                                                              ENU=Job Line Type;
                                                              NLD=Regelsoort project;
                                                              SVE=Projektradtyp];
                                                   OptionCaptionML=[DEU=" ,Budget,Fakturierbar,Budget und Fakturierbar";
                                                                    ENU=" ,Budget,Billable,Both Budget and Billable";
                                                                    NLD=" ,Budget,Factureerbaar,Budget en factureerbaar";
                                                                    SVE=" ,Budget,Fakturerbart,BÜde budget och fakturerbart"];
                                                   OptionString=[ ,Schedule,Contract,Both Schedule and Contract] }
    { 60  ;   ;Cash/Private Card   ;Boolean       ;CaptionML=[DEU=Cash/Privat-Karte;
                                                              ENU=Cash/Private Card;
                                                              NLD=Cash/privÇkaart];
                                                   Editable=No }
    { 73  ;   ;Original Expense Entry No.;Integer ;TableRelation=Expense;
                                                   CaptionML=[DEU=Urspr. Ausgabenpostennr.;
                                                              ENU=Original Expense Entry No.;
                                                              NLD=Oorspronkelijk uitgavevolgnr.] }
    { 109 ;   ;Expense Account Type;Option        ;OnValidate=BEGIN
                                                                IF xRec."Expense Account Type" <> "Expense Account Type" THEN
                                                                  CLEAR("Expense Account");
                                                              END;

                                                   CaptionML=[DEU=Ausgabenverrechnugsart;
                                                              ENU=Expense Account Type;
                                                              NLD=Uitgaverekeningtype];
                                                   OptionCaptionML=[DEU=" ,Sachkonto,,,Artikel";
                                                                    ENU=" ,G/L Account,,,Item";
                                                                    NLD=" ,Grootboekrekening,,,Artikel"];
                                                   OptionString=[ ,G/L Account,,,Item] }
    { 110 ;   ;Expense Account     ;Code20        ;OnValidate=VAR
                                                                GLAcc@1160040000 : Record 15;
                                                              BEGIN
                                                                CASE "Expense Account Type" OF
                                                                  "Expense Account Type"::"G/L Account":
                                                                    BEGIN
                                                                      GLAcc.GET("Expense Account");
                                                                      GLAcc.CheckGLAcc;
                                                                    END;
                                                                END;

                                                                "Exp. Account Manually Changed" := CurrFieldNo = FIELDNO("Expense Account");
                                                                AddDefaultDim(CurrFieldNo);
                                                              END;

                                                   CaptionML=[DEU=Ausgabenkonto;
                                                              ENU=Expense Account;
                                                              NLD=Uitgaverekening] }
    { 111 ;   ;Exp. Account Manually Changed;Boolean;
                                                   CaptionML=[DEU=Ausgabenkonto manuell geÑndert;
                                                              ENU=Expense Account Manually Changed;
                                                              NLD=Uitgaverekening handmatig gewijzigd] }
    { 112 ;   ;Gen. Prod. Posting Group;Code20    ;TableRelation="Gen. Product Posting Group";
                                                   CaptionML=[DEU=Produktbuchungsgruppe;
                                                              ENU=Gen. Prod. Posting Group;
                                                              NLD=Algemene productboekingsgroep;
                                                              NOR=Bokfõringsgruppe - vare;
                                                              SVE=Produktbokfîringsmall] }
    { 113 ;   ;VAT Prod. Posting Group;Code20     ;TableRelation="VAT Product Posting Group";
                                                   CaptionML=[DEU=MwSt.-Produktbuchungsgruppe;
                                                              ENU=VAT Prod. Posting Group;
                                                              NLD=Btw-productboekingsgroep;
                                                              NOR=Mva-bokfõringsgruppe - vare;
                                                              SVE=Moms produktbokfîringsmall] }
    { 114 ;   ;Gen. Bus. Posting Group;Code20     ;TableRelation="Gen. Business Posting Group";
                                                   CaptionML=[DEU=GeschÑftsbuchungsgruppe;
                                                              ENU=Gen. Bus. Posting Group;
                                                              NLD=Bedrijfsboekingsgroep;
                                                              SVE=Gen. rîrelsebokfîringsmall] }
    { 115 ;   ;VAT Bus. Posting Group;Code20      ;TableRelation="VAT Business Posting Group";
                                                   CaptionML=[DEU=MwSt.-GeschÑftsbuchungsgruppe;
                                                              ENU=VAT Bus. Posting Group;
                                                              NLD=Btw-bedrijfsboekingsgroep;
                                                              SVE=Moms rîrelsebokfîringsmall] }
    { 180 ;   ;Expense Type        ;Code20        ;TableRelation="Expense Type";
                                                   OnValidate=VAR
                                                                ContiniaUserSetup@1160040001 : Record 6086002;
                                                                ExpenseType@1160040000 : Record 6086307;
                                                                ExpPostingSetup@1160040002 : Record 6086309;
                                                              BEGIN
                                                                IF NOT ExpenseType.GET("Expense Type") THEN
                                                                  CLEAR(ExpenseType);

                                                                VALIDATE("No Refund",ExpenseType."No Refund");

                                                                IF (ExpenseType.Code <> '') AND ExpPostingSetup.FindPostingSetup(
                                                                   "Expense Type","Country/Region Code","Continia User ID", ContiniaUserSetup."Expense User Group")
                                                                THEN BEGIN
                                                                  VALIDATE("Expense Account Type",ExpPostingSetup."Posting Account Type");
                                                                  "Expense Account" := ExpPostingSetup."Posting Account No.";
                                                                  "Gen. Prod. Posting Group" := ExpPostingSetup."Gen. Prod. Posting Group";
                                                                  "Gen. Bus. Posting Group" := ExpPostingSetup."Gen. Bus. Posting Group";
                                                                  "VAT Prod. Posting Group" := ExpPostingSetup."VAT Prod. Posting Group";
                                                                  "VAT Bus. Posting Group" := ExpPostingSetup."VAT Bus. Posting Group";
                                                                END ELSE BEGIN
                                                                  CLEAR("Expense Account Type");
                                                                  CLEAR("Expense Account");
                                                                  "Gen. Prod. Posting Group" := '';
                                                                  "Gen. Bus. Posting Group" := '';
                                                                  "VAT Prod. Posting Group" := '';
                                                                  "VAT Bus. Posting Group" := '';
                                                                END;

                                                                "Exp. Account Manually Changed" := FALSE;
                                                                AddDefaultDim(CurrFieldNo);
                                                              END;

                                                   CaptionML=[DEU=Ausgabenart;
                                                              ENU=Expense Type;
                                                              NLD=Uitgavetype] }
    { 270 ;   ;No. of Attendees    ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("EM Attendee" WHERE (Table ID=CONST(6086321),
                                                                                          Doc. Ref. No.=FIELD(Entry No.)));
                                                   CaptionML=[DEU=Anzahl Teilnehmer;
                                                              ENU=No. of Attendees;
                                                              NLD=Aantal deelnemers;
                                                              SVE=Antal deltagare];
                                                   Editable=No }
    { 300 ;   ;Expense Entry No.   ;Integer       ;TableRelation=Expense;
                                                   CaptionML=[DEU=Ausgabenpostennr.;
                                                              ENU=Expense Entry No.;
                                                              NLD=Uitgavevolgnr.];
                                                   NotBlank=Yes }
    { 301 ;   ;Modified            ;Boolean       ;CaptionML=[DEU=GeÑndert;
                                                              ENU=Modified;
                                                              NLD=Gewijzigd;
                                                              NOR=Endret;
                                                              SVE=éndrad] }
    { 304 ;   ;Amount %            ;Decimal       ;OnValidate=BEGIN
                                                                UpdateAmount(FIELDNO("Amount %"));
                                                              END;

                                                   CaptionML=[DEU=Betrag %;
                                                              ENU=Amount %;
                                                              NLD=Bedrag %];
                                                   DecimalPlaces=0:5 }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Expense Entry No.                       ;SumIndexFields=Amount,Amount (LCY),Bank-Currency Amount }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      SkipSendToExpUser@1160040003 : Boolean;
      ConfRemAmount@6086301 : TextConst 'DEU=Der Betrag in der Ausgabe (%1 %2) und der zugewiesene Gesamtbetrag (%1 %3) stimmen nicht Åberein. Mîchten Sie die Seite dennoch schlie·en?;ENU=The amount on the Expense (%1 %2) and the total allocated Amount (%1 %3) do not match. Do you want to close the page anyway?;NLD=Het bedrag op de uitgave (%1 %2) en het totaal toegewezen bedrag (%1 %3) komen niet overeen. Wilt u de pagina toch sluiten?';
      ConfZeroLines@6086300 : TextConst 'DEU=Es liegen Zuordnungen mit dem Betrag 0 vor. Mîchten Sie diese wirklich behalten?;ENU=Allocations with 0 amount exist. Are you sure you want to keep these?;NLD=Allocaties met bedrag 0 bestaan. Weet u zeker dat u deze wilt behouden?';
      EmployeeModified@6086302 : TextConst 'DEU=Beim éndern von %1 mÅssen Sie eine neue Ausgabe erstellen.;ENU=When changing %1 you need to create a new expense.;NLD=Wanneer u %1 wijzigt dient u een nieuwe uitgave aan te maken.';
      ExpTypeAttNotReq@1160040002 : TextConst 'DEU=Der %1 %2 erfordert keine Teilnehmer.;ENU=The %1 %2 does not require attendees.;NLD=De %1 %2 vereist geen deelnemers.';
      RemAmountMissmatchErr@6086304 : TextConst 'DEU=Der Betrag auf der Ausgabe (%1 %2) und der zugewiesene Gesamtbetrag (%1 %3) stimmen nicht Åberein.;ENU=The amount on the Expense (%1 %2) and the total allocated Amount (%1 %3) do not match.;NLD=Het bedrag op de uitgave (%1 %2) en het totaal toegewezen bedrag (%1 %3) komen niet overeen.';
      Text002@1160040001 : TextConst 'DEU=%1 muss eingegeben werden, bevor Sie Dimensionen anzeigen kînnen.;ENU=The %1 must be inserted before you can show dimensions.;NLD=De %1 moet aangemaakt voordat u dimensies kunt tonen.';
      ZeroLinesErr@6086303 : TextConst 'DEU="Zuweisungen mit Betrag 0 sind nicht zulÑssig. ";ENU=Allocations with amount 0 are not allowed.;NLD=Toewijzingen met het bedrag 0 zijn niet toegestaan.';

    LOCAL PROCEDURE UpdateAmount@1160040003(CalledByFieldNo@1160040002 : Integer);
    VAR
      BankAccCurrency@1160040003 : Record 4;
      Currency@1160040008 : Record 4;
      GLSetup@1160040005 : Record 98;
      Expense@1160040000 : Record 6086320;
      CurrencyDate@1160040001 : Date;
      CurrencyFactor@1160040009 : Decimal;
    BEGIN
      GLSetup.GET;
      Expense.GET("Expense Entry No.");
      IF Expense."Currency Code" <> '' THEN BEGIN
        IF "Document Date" = 0D THEN
          CurrencyDate := WORKDATE
        ELSE
          CurrencyDate := "Document Date";

        IF Expense.Amount <> 0 THEN
          CurrencyFactor := Expense."Amount (LCY)" / Expense.Amount
        ELSE
          CurrencyFactor := 1;

        Currency.GET(Expense."Currency Code");
        Currency.CheckAmountRoundingPrecision;
      END ELSE BEGIN
        Currency.InitRoundingPrecision;
        CurrencyFactor := 1;
      END;

      IF "Bank Currency Code" <> '' THEN BEGIN
        BankAccCurrency.GET("Bank Currency Code");
        BankAccCurrency.CheckAmountRoundingPrecision;
      END ELSE
        BankAccCurrency.InitRoundingPrecision;

      CASE CalledByFieldNo OF

        FIELDNO(Amount):
          IF Expense.Amount = 0 THEN BEGIN
            "Amount (LCY)" := 0;
            "Amount %" := 100;
            "Bank-Currency Amount" := 0;
          END ELSE BEGIN
            "Amount (LCY)" := ROUND(Amount * CurrencyFactor);
            "Amount %" := Amount / Expense.Amount * 100;
            "Bank-Currency Amount" :=
              ROUND("Amount %" / 100 *
               Expense."Bank-Currency Amount",BankAccCurrency."Amount Rounding Precision");;
          END;

        FIELDNO("Amount %"):
          BEGIN
            Amount := ROUND("Amount %" / 100 * Expense.Amount,Currency."Amount Rounding Precision");
            "Amount (LCY)" := ROUND("Amount %" / 100 * Expense."Amount (LCY)");
            "Bank-Currency Amount" :=
              ROUND("Amount %" / 100 *
                Expense."Bank-Currency Amount",BankAccCurrency."Amount Rounding Precision");
          END;

      END;
    END;

    PROCEDURE ShowDimensions@1160040000(ReadOnly@1160040001 : Boolean);
    BEGIN
      IF "Entry No." = 0 THEN
        ERROR(Text002,TABLECAPTION);

      DrillDownDimensions(NOT ReadOnly);
    END;

    PROCEDURE GetLastEntryNo@1160040004() : Integer;
    VAR
      ExpenseAllocation@1160040000 : Record 6086321;
    BEGIN
      IF ExpenseAllocation.FINDLAST THEN
        EXIT(ExpenseAllocation."Entry No.")
      ELSE
        EXIT(1);
    END;

    LOCAL PROCEDURE TestNotPosted@1160040006();
    VAR
      Expense@1160040000 : Record 6086320;
    BEGIN
      Expense.GET("Expense Entry No.");
      Expense.TESTFIELD(Posted,FALSE);
    END;

    LOCAL PROCEDURE DrillDownDimensions@1160040014(Editable@1160040006 : Boolean);
    VAR
      Expense@1160040007 : Record 6086320;
      ExpAllocDim@1160040000 : Record 6086357;
      TempExpAllocDim@1160040002 : TEMPORARY Record 6086357;
      ExpAllocDimForm@1160040001 : Page 6086368;
      SendToExpUser@1160040003 : Codeunit 6086324;
    BEGIN
      ExpAllocDim.SETRANGE("Expense Allocation Entry No.","Entry No.");

      Expense.GET("Expense Entry No.");

      TempExpAllocDim.DELETEALL;
      IF (NOT Expense.Posted) AND Editable THEN BEGIN
        IF ExpAllocDim.FINDSET THEN
          REPEAT
            TempExpAllocDim := ExpAllocDim;
            TempExpAllocDim.INSERT;
          UNTIL ExpAllocDim.NEXT = 0;

        TempExpAllocDim.SETRANGE("Expense Allocation Entry No.","Entry No.");
        PAGE.RUNMODAL(PAGE::"Expense Allocation Dimensions",TempExpAllocDim);

        IF ExpAllocDim.EMDimUpdated(TempExpAllocDim,"Entry No.") THEN BEGIN
          ExpAllocDim.DELETEALL(TRUE);

          IF TempExpAllocDim.FINDSET THEN
            REPEAT
              ExpAllocDim := TempExpAllocDim;
              ExpAllocDim.INSERT(TRUE);
            UNTIL TempExpAllocDim.NEXT = 0;

          Expense.GET("Expense Entry No.");
          IF Expense.Status = Expense.Status::"Pending Expense User" THEN
            SendToExpUser.UpdateWithoutFiles(Expense);

          CODEUNIT.RUN(CODEUNIT::"Expense-Validate",Expense);
        END;
      END ELSE BEGIN
        ExpAllocDimForm.SETTABLEVIEW(ExpAllocDim);
        ExpAllocDimForm.SetReadOnly;
        ExpAllocDimForm.RUNMODAL;
      END;
    END;

    PROCEDURE LookupPostingAccount@1000000000(VAR Text@1000000000 : Text[1024]) : Boolean;
    VAR
      GLAcc@1000000003 : Record 15;
    BEGIN
      CASE "Expense Account Type" OF
        "Expense Account Type"::"G/L Account":
          BEGIN
            IF GLAcc.GET(Text) THEN;
            IF PAGE.RUNMODAL(0,GLAcc) = ACTION::LookupOK THEN BEGIN
              Text := GLAcc."No.";
              EXIT(TRUE);
            END;
          END;
      END;
    END;

    PROCEDURE SendToExpenseUser@1160040001(ShouldDelete@1160040004 : Boolean);
    VAR
      Expense@1160040000 : Record 6086320;
      EMAllocation@1160040002 : Record 6086321;
      EMAllocationTemp@1160040003 : TEMPORARY Record 6086321;
      SendToExpUser@1160040001 : Codeunit 6086324;
    BEGIN
      Expense.GET("Expense Entry No.");

      IF Expense.Status = Expense.Status::"Pending Expense User" THEN BEGIN
        EMAllocation.SETCURRENTKEY("Expense Entry No.");
        EMAllocation.SETRANGE("Expense Entry No.",Expense."Entry No.");
        IF EMAllocation.FINDSET THEN
          REPEAT
            EMAllocationTemp.TRANSFERFIELDS(EMAllocation);
            EMAllocationTemp.INSERT;
          UNTIL EMAllocation.NEXT = 0;

        IF ShouldDelete THEN BEGIN
          IF EMAllocationTemp.GET("Entry No.") THEN
            EMAllocationTemp.DELETE;
        END ELSE
          IF EMAllocationTemp.GET("Entry No.") THEN BEGIN
            EMAllocationTemp := Rec;
            EMAllocationTemp.MODIFY;
          END ELSE BEGIN
            EMAllocationTemp := Rec;
            EMAllocationTemp."Entry No." += 1;
            EMAllocationTemp.INSERT;
          END;

        SendToExpUser.SetAllocation(EMAllocationTemp);
        SendToExpUser.UpdateWithoutFiles(Expense);
      END;
    END;

    PROCEDURE DrillDownAttendees@1160040011();
    VAR
      ExpenseType@1160040004 : Record 6086307;
      ExpAttendee@1160040000 : Record 6086308;
      TempExpAttendee@1160040002 : TEMPORARY Record 6086308;
      Expense@1160040005 : Record 6086320;
      ExpAttendees@1160040001 : Page 6086325;
    BEGIN
      ExpAttendee.SETRANGE("Table ID",DATABASE::"Expense Allocation");
      ExpAttendee.SETRANGE("Doc. Ref. No.","Entry No.");

      TESTFIELD("Expense Type");
      ExpenseType.GET("Expense Type");
      IF NOT ExpenseType."Attendees Required" THEN
        ERROR(ExpTypeAttNotReq,ExpenseType.TABLECAPTION,ExpenseType.Code);

      Expense.GET("Expense Entry No.");
      IF NOT Expense.Posted THEN BEGIN
        IF ExpAttendee.FINDSET THEN
          REPEAT
            TempExpAttendee := ExpAttendee;
            TempExpAttendee.INSERT;
          UNTIL ExpAttendee.NEXT = 0;

        TempExpAttendee.SETRANGE("Table ID",DATABASE::"Expense Allocation");
        TempExpAttendee.SETRANGE("Doc. Ref. No.","Entry No.");
        PAGE.RUNMODAL(0,TempExpAttendee);
        IF TempExpAttendee.AttendeesUpdated(TempExpAttendee,"Entry No.",DATABASE::"Expense Allocation") THEN BEGIN
          ExpAttendee.DELETEALL;

          IF TempExpAttendee.FINDSET THEN
            REPEAT
              ExpAttendee := TempExpAttendee;
              ExpAttendee.INSERT;
            UNTIL TempExpAttendee.NEXT = 0;

          Expense.SendToExpenseUser;
          CODEUNIT.RUN(CODEUNIT::"Expense-Validate",Expense);
        END;
      END ELSE BEGIN
        ExpAttendees.SETTABLEVIEW(ExpAttendee);
        ExpAttendees.EDITABLE := FALSE;
        ExpAttendees.RUNMODAL;
      END;
    END;

    PROCEDURE GetAttendeesForDisplay@1160040002() DisplayTxt : Text[150];
    VAR
      ExpAttendee@1160040000 : Record 6086308;
    BEGIN
      EXIT(ExpAttendee.GetAttendeesForDisplay(DATABASE::"Expense Allocation","Entry No."));
    END;

    PROCEDURE SetSkipSendToUser@1160040007(Send@1160040000 : Boolean);
    BEGIN
      SkipSendToExpUser := Send;
    END;

    PROCEDURE AdjustAmountsToDecimals@6086300(RemainingAmount@6086300 : Decimal;RemainingAmountLCY@6086301 : Decimal;RemainingAmtBankLCY@6086302 : Decimal);
    VAR
      ExpenseAllocation@6086303 : Record 6086321;
    BEGIN
      IF (RemainingAmount = 0) AND (RemainingAmountLCY <> 0) THEN BEGIN
        ExpenseAllocation.SETCURRENTKEY("Expense Entry No.");
        ExpenseAllocation.SETRANGE("Expense Entry No.",Rec."Expense Entry No.");
        IF ExpenseAllocation.FINDLAST THEN BEGIN
          IF RemainingAmountLCY < 0 THEN
            ExpenseAllocation."Amount (LCY)" := ExpenseAllocation."Amount (LCY)" + RemainingAmountLCY
          ELSE
            ExpenseAllocation."Amount (LCY)" := ExpenseAllocation."Amount (LCY)" - RemainingAmountLCY;
          ExpenseAllocation.MODIFY;
        END;
      END;

      IF (RemainingAmount <> 0) AND (RemainingAmountLCY = 0) THEN BEGIN
        ExpenseAllocation.SETCURRENTKEY("Expense Entry No.");
        ExpenseAllocation.SETRANGE("Expense Entry No.",Rec."Expense Entry No.");
        IF ExpenseAllocation.FINDLAST THEN BEGIN
          IF RemainingAmount < 0 THEN
            ExpenseAllocation.Amount := ExpenseAllocation.Amount + RemainingAmount
          ELSE
            ExpenseAllocation.Amount := ExpenseAllocation.Amount - RemainingAmount;
          ExpenseAllocation.MODIFY;
        END;
      END;

      IF (RemainingAmount = 0) AND (RemainingAmtBankLCY <> 0) THEN BEGIN
        ExpenseAllocation.SETCURRENTKEY("Expense Entry No.");
        ExpenseAllocation.SETRANGE("Expense Entry No.",Rec."Expense Entry No.");
        IF ExpenseAllocation.FINDLAST THEN BEGIN
          IF RemainingAmtBankLCY < 0 THEN
            ExpenseAllocation."Bank-Currency Amount" := ExpenseAllocation."Bank-Currency Amount" + RemainingAmtBankLCY
          ELSE
            ExpenseAllocation."Bank-Currency Amount" := ExpenseAllocation."Bank-Currency Amount" - RemainingAmtBankLCY;
          ExpenseAllocation.MODIFY;
        END;
      END;
    END;

    PROCEDURE ValidateAllocationConsistency@6086301(ShowDialog@6086300 : Boolean) : Boolean;
    VAR
      Expense@6086302 : Record 6086320;
      ExpenseAllocation@6086301 : Record 6086321;
    BEGIN
      Expense.GET(Rec."Expense Entry No.");

      ExpenseAllocation.RESET;
      ExpenseAllocation.SETCURRENTKEY("Expense Entry No.");
      ExpenseAllocation.SETRANGE("Expense Entry No.",Expense."Entry No.");
      ExpenseAllocation.SETFILTER("Continia User ID",'<>%1',Expense."Continia User ID");
      IF NOT ExpenseAllocation.ISEMPTY THEN
        ERROR(EmployeeModified,ExpenseAllocation.FIELDCAPTION("Continia User ID"));

      CLEAR(ExpenseAllocation);
      ExpenseAllocation.SETCURRENTKEY("Expense Entry No.");
      ExpenseAllocation.SETRANGE("Expense Entry No.",Rec."Expense Entry No.");
      ExpenseAllocation.CALCSUMS(Amount);
      IF (NOT ExpenseAllocation.ISEMPTY) AND (ExpenseAllocation.Amount <> Expense.Amount) THEN
        IF ShowDialog THEN
          EXIT(CONFIRM(ConfRemAmount,FALSE,Expense."Currency Code",Expense.Amount,Expense.Amount-ExpenseAllocation.Amount))
        ELSE
          ERROR(RemAmountMissmatchErr,Expense."Currency Code",Expense.Amount,Expense.Amount-ExpenseAllocation.Amount);

      IF ZeroAmountLines THEN
        IF ShowDialog THEN
          EXIT(CONFIRM(ConfZeroLines,FALSE))
        ELSE
          ERROR(ZeroLinesErr);

      EXIT(TRUE);
    END;

    PROCEDURE DeleteAutoGeneratedEntries@6086302() : Boolean;
    VAR
      ExpenseAllocation@1160040000 : Record 6086321;
    BEGIN
      ExpenseAllocation.SETCURRENTKEY("Expense Entry No.");
      ExpenseAllocation.SETRANGE("Expense Entry No.",Rec."Expense Entry No.");
      ExpenseAllocation.SETRANGE(Modified,FALSE);
      ExpenseAllocation.DELETEALL(TRUE);
    END;

    LOCAL PROCEDURE ZeroAmountLines@6086303() : Boolean;
    VAR
      ExpenseAllocation@1160040000 : Record 6086321;
    BEGIN
      ExpenseAllocation.SETCURRENTKEY("Expense Entry No.");
      ExpenseAllocation.SETRANGE("Expense Entry No.",Rec."Expense Entry No.");
      ExpenseAllocation.SETRANGE(Amount,0);
      EXIT(NOT ExpenseAllocation.ISEMPTY);
    END;

    PROCEDURE AddDefaultDim@1160040024(ValidatedFieldNo@1160040002 : Integer);
    VAR
      ContiniaUser@1160040001 : Record 6086002;
      EMDimMgt@1160040000 : Codeunit 6086318;
    BEGIN
      IF "Entry No." = 0 THEN
        EXIT;

      DeleteOldDefaultDim;

      IF ContiniaUser.GET("Continia User ID") THEN BEGIN
        IF ContiniaUser.GetSalesPurchCode <> '' THEN
          EMDimMgt.InsertDefaultDimAllocation(DATABASE::"Salesperson/Purchaser",ContiniaUser.GetSalesPurchCode,Rec);

        IF ContiniaUser."Vendor No." <> '' THEN
          EMDimMgt.InsertDefaultDimAllocation(DATABASE::Vendor,ContiniaUser."Vendor No.",Rec);
      END;

      IF "Expense Account" <> '' THEN
        EMDimMgt.InsertDefaultDimAllocation(DATABASE::"G/L Account","Expense Account",Rec);

      IF "Expense Type" <> '' THEN
        EMDimMgt.InsertDefaultDimAllocation(DATABASE::"Expense Type","Expense Type",Rec);

      IF "Job No." <> '' THEN
        EMDimMgt.InsertDefaultDimAllocation(DATABASE::Job,"Job No.",Rec);

      CASE ValidatedFieldNo OF
        FIELDNO("Continia User ID"):
          IF ContiniaUser.GET("Continia User ID") THEN BEGIN
            IF ContiniaUser.GetSalesPurchCode <> '' THEN
              EMDimMgt.InsertDefaultDimAllocation(DATABASE::"Salesperson/Purchaser",ContiniaUser.GetSalesPurchCode,Rec);

            IF ContiniaUser."Vendor No." <> '' THEN
              EMDimMgt.InsertDefaultDimAllocation(DATABASE::Vendor,ContiniaUser."Vendor No.",Rec);
          END;

        FIELDNO("Expense Account"):
          IF "Expense Account" <> '' THEN
            EMDimMgt.InsertDefaultDimAllocation(DATABASE::"G/L Account","Expense Account",Rec);

        FIELDNO("Expense Type"):
          IF "Expense Type" <> '' THEN
            EMDimMgt.InsertDefaultDimAllocation(DATABASE::"Expense Type","Expense Type",Rec);

        FIELDNO("Job No."):
          IF "Job No." <> '' THEN
            EMDimMgt.InsertDefaultDimAllocation(DATABASE::Job,"Job No.",Rec);
      END;
    END;

    LOCAL PROCEDURE DeleteOldDefaultDim@1160040026();
    VAR
      ContiniaUser@1160040001 : Record 6086002;
      EMDimMgt@1160040000 : Codeunit 6086318;
    BEGIN
      IF ContiniaUser.GET(xRec."Continia User ID") THEN BEGIN
        IF ContiniaUser.GetSalesPurchCode <> '' THEN
          EMDimMgt.DeleteDefaultDimAllocation(DATABASE::"Salesperson/Purchaser",ContiniaUser.GetSalesPurchCode,Rec);

        IF ContiniaUser."Vendor No." <> '' THEN
          EMDimMgt.DeleteDefaultDimAllocation(DATABASE::Vendor,ContiniaUser."Vendor No.",Rec);
      END;

      IF xRec."Expense Type" <> '' THEN
        EMDimMgt.DeleteDefaultDimAllocation(DATABASE::"Expense Type",xRec."Expense Type",Rec);

      IF xRec."Expense Account" <> '' THEN
        EMDimMgt.DeleteDefaultDimAllocation(DATABASE::"G/L Account",xRec."Expense Account",Rec);

      IF xRec."Job No." <> '' THEN
        EMDimMgt.DeleteDefaultDimAllocation(DATABASE::Job,xRec."Job No.",Rec);
    END;

    BEGIN
    {
      4PS, Objects renumbered
    }
    END.
  }
}

