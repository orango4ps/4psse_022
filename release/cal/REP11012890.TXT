OBJECT Report 11012890 Date Compress Service Ledger
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 87=rimd,
                TableData 11012819=rimd,
                TableData 11012821=rimd;
    CaptionML=[DEU=ServiceauftrÑge komprimieren;
               ENU=Date Compress Service Ledger;
               NLD=Serviceposten comprimeren;
               NOR=Datokomprimer servicereskontro;
               SVE=Datumkomprimera servicereskontra];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  DimSelectionBuf.CompareDimText(
                    3,REPORT::"Date Compress Service Ledger",'',RetainDimText,Text010);
                  ServLedgEntryFilter := COPYSTR("Service-Ledger Entry".GETFILTERS,1,MAXSTRLEN(DateComprReg.Filter));
                END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 4723;    ;DataItem;                    ;
               DataItemTable=Table11012819;
               DataItemTableView=SORTING(Service Contract No.,Posting Date);
               OnPreDataItem=VAR
                               GLSetup@1000 : Record 98;
                             BEGIN
                               IF NOT CONFIRM(Text000,FALSE) THEN
                                 CurrReport.BREAK;

                               IF EntrdDateComprReg."Ending Date" = 0D THEN
                                 ERROR(STRSUBSTNO(Text003,EntrdDateComprReg.FIELDCAPTION("Ending Date")));

                               Window.OPEN(
                                 Text004 +
                                 Text005 +
                                 Text005a +
                                 Text006 +
                                 Text007 +
                                 Text008);

                               SourceCodeSetup.GET;
                               SourceCodeSetup.TESTFIELD("Compress Service Ledger");

                               SelectedDim.GetSelectedDim(
                                 USERID,3,REPORT::"Date Compress Service Ledger",'',TempSelectedDim);
                               GLSetup.GET;
                               Retain[7] :=
                                 TempSelectedDim.GET(
                                   USERID,3,REPORT::"Date Compress Service Ledger",'',GLSetup."Global Dimension 1 Code");
                               Retain[8] :=
                                 TempSelectedDim.GET(
                                   USERID,3,REPORT::"Date Compress Service Ledger",'',GLSetup."Global Dimension 2 Code");

                               NewServLedgEntry.LOCKTABLE;
                               ServReg.LOCKTABLE;
                               DateComprReg.LOCKTABLE;

                               IF ServLedgEntry2.FINDLAST THEN;
                               LastEntryNo := ServLedgEntry2."Entry No.";
                               SETRANGE("Entry No.",0,LastEntryNo);
                               SETRANGE("Posting Date",EntrdDateComprReg."Starting Date",EntrdDateComprReg."Ending Date");

                               InitRegisters;
                             END;

               OnAfterGetRecord=VAR
                                  ServOrder@1210190000 : Record 11012823;
                                BEGIN
                                  IF ServOrder."No." <> "Service Order No." THEN
                                    IF NOT ServOrder.GET("Service Order No.") THEN
                                      CurrReport.SKIP;
                                  IF ServOrder.Status < ServOrder.Status::Finished THEN
                                    CurrReport.SKIP;

                                  IF "Service Order No." = '' THEN BEGIN
                                    IF "Service Contract WIP processed" = FALSE THEN
                                      CurrReport.SKIP;
                                  END ELSE BEGIN
                                    IF "Service Order WIP processed" = FALSE THEN
                                      CurrReport.SKIP;
                                  END;

                                  ServLedgEntry2 := "Service-Ledger Entry";
                                  WITH ServLedgEntry2 DO BEGIN
                                    SETCURRENTKEY("Service Order No.");
                                    COPYFILTERS("Service-Ledger Entry");
                                    SETRANGE("Service Order No.","Service Order No.");
                                    SETFILTER("Posting Date",DateComprMgt.GetDateFilter("Posting Date",EntrdDateComprReg,TRUE));
                                    SETRANGE("Cost Type","Cost Type");

                                    IF RetainNo(FIELDNO("Document No.")) THEN
                                      SETRANGE("Document No.","Document No.");
                                    IF RetainNo(FIELDNO("G/L Account")) THEN
                                      SETRANGE("G/L Account","G/L Account");
                                    IF RetainNo(FIELDNO("Location Code")) THEN
                                      SETRANGE("Location Code","Location Code");
                                    IF RetainNo(FIELDNO("Global Dimension 1 Code")) THEN
                                      SETRANGE("Global Dimension 1 Code","Global Dimension 1 Code");
                                    IF RetainNo(FIELDNO("Global Dimension 2 Code")) THEN
                                      SETRANGE("Global Dimension 2 Code","Global Dimension 2 Code");
                                    IF RetainNo(FIELDNO("Employee No.")) THEN
                                      SETRANGE("Employee No.","Employee No.");
                                    IF RetainNo(FIELDNO("Wage Component")) THEN
                                      SETRANGE("Wage Component","Wage Component");
                                    IF RetainNo(FIELDNO("Item No.")) THEN
                                      SETRANGE("Item No.","Item No.");
                                    IF RetainNo(FIELDNO("Customer No.")) THEN
                                      SETRANGE("Customer No.","Customer No.");
                                    IF RetainNo(FIELDNO("Vendor No.")) THEN
                                      SETRANGE("Vendor No.","Vendor No.");
                                    IF RetainNo(FIELDNO("Service Location No.")) THEN
                                      SETRANGE("Service Location No.","Service Location No.");
                                    IF RetainNo(FIELDNO("Source Type")) THEN
                                      SETRANGE("Source Type","Source Type");

                                    IF Quantity >= 0 THEN
                                      SETFILTER(Quantity,'>=0')
                                    ELSE
                                      SETFILTER(Quantity,'<0');

                                    InitNewEntry(NewServLedgEntry);

                                    DimBufMgt.CollectDimEntryNo(
                                      TempSelectedDim,"Dimension Set ID","Entry No.",
                                      0,FALSE,DimEntryNo);
                                    ComprDimEntryNo := DimEntryNo;
                                    SummarizeEntry(NewServLedgEntry,ServLedgEntry2);
                                    WHILE NEXT <> 0 DO BEGIN
                                      DimBufMgt.CollectDimEntryNo(
                                        TempSelectedDim,"Dimension Set ID","Entry No.",
                                        ComprDimEntryNo,TRUE,DimEntryNo);
                                      IF DimEntryNo = ComprDimEntryNo THEN
                                        SummarizeEntry(NewServLedgEntry,ServLedgEntry2);
                                    END;

                                    InsertNewEntry(NewServLedgEntry,ComprDimEntryNo);

                                    ComprCollectedEntries;
                                  END;

                                  IF DateComprReg."No. Records Deleted" >= NoOfDeleted + 10 THEN BEGIN
                                    NoOfDeleted := DateComprReg."No. Records Deleted";
                                    InsertRegisters(ServReg,DateComprReg);
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                IF DateComprReg."No. Records Deleted" > NoOfDeleted THEN
                                  InsertRegisters(ServReg,DateComprReg);
                              END;

               ReqFilterFields=Service Contract No.,Service Location No.,Service Order No.,Source Type,Cost Type }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   IF EntrdDateComprReg."Ending Date" = 0D THEN
                     EntrdDateComprReg."Ending Date" := TODAY;
                   IF EntrdServLedgEntry.Description = '' THEN
                     EntrdServLedgEntry.Description := Text009;

                   WITH "Service-Ledger Entry" DO BEGIN
                     InsertField(FIELDNO("Document No."),FIELDCAPTION("Document No."));
                     InsertField(FIELDNO("G/L Account"),FIELDCAPTION("G/L Account"));
                     InsertField(FIELDNO("Employee No."),FIELDCAPTION("Employee No."));
                     InsertField(FIELDNO("Location Code"),FIELDCAPTION("Location Code"));
                     InsertField(FIELDNO("Wage Component"),FIELDCAPTION("Wage Component"));
                     InsertField(FIELDNO("Global Dimension 1 Code"),FIELDCAPTION("Global Dimension 1 Code"));
                     InsertField(FIELDNO("Global Dimension 2 Code"),FIELDCAPTION("Global Dimension 2 Code"));
                     InsertField(FIELDNO("Item No."),FIELDCAPTION("Item No."));
                     InsertField(FIELDNO("Customer No."),FIELDCAPTION("Customer No."));
                     InsertField(FIELDNO("Vendor No."),FIELDCAPTION("Vendor No."));
                     InsertField(FIELDNO("Service Location No."),FIELDCAPTION("Service Location No."));
                     InsertField(FIELDNO("Source Type"),FIELDCAPTION("Source Type"));
                   END;

                   RetainDimText := DimSelectionBuf.GetDimSelectionText(3,REPORT::"Date Compress Service Ledger",'');
                 END;

    }
    CONTROLS
    {
      { 1100525017;0;Container;
                  ContainerType=ContentArea }

      { 1100525016;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1100525015;2;Field  ;
                  CaptionML=[DEU=Startdatum;
                             ENU=Starting Date;
                             NLD=Begindatum;
                             NOR=Startdato;
                             SVE=Startdatum];
                  SourceExpr=EntrdDateComprReg."Starting Date" }

      { 1100525014;2;Field  ;
                  CaptionML=[DEU=Enddatum;
                             ENU=Ending Date;
                             NLD=Einddatum;
                             NOR=Sluttdato;
                             SVE=Slutdatum];
                  SourceExpr=EntrdDateComprReg."Ending Date" }

      { 1100525013;2;Field  ;
                  CaptionML=[DEU=PeriodenlÑnge;
                             ENU=Period Length;
                             NLD=Periodelengte;
                             NOR=Periodelengde;
                             SVE=PeriodlÑngd];
                  OptionCaptionML=[DEU=Tag,Woche,Monat,Quartal,Jahr,Periode;
                                   ENU=Day,Week,Month,Quarter,Year,Period;
                                   NLD=Dag,Week,Maand,Kwartaal,Jaar,Periode;
                                   NOR=Dag,Uke,MÜned,Kvartal,èr,Periode;
                                   SVE=Dag,Vecka,MÜnad,Kvartal,èr,Period];
                  SourceExpr=EntrdDateComprReg."Period Length" }

      { 1100525012;2;Field  ;
                  CaptionML=[DEU=Buchungsbeschreibung;
                             ENU=Posting Description;
                             NLD=Omschrijving;
                             NOR=Bokfõringsbeskrivelse;
                             SVE=Bokfîringsbeskrivning];
                  SourceExpr=EntrdServLedgEntry.Description }

      { 1100525011;2;Group  ;
                  CaptionML=[DEU=Feldinhalt behalten;
                             ENU=Retain Field Contents;
                             NLD=Te bewaren velden;
                             NOR=Behold felt;
                             SVE=BibehÜll fÑlt] }

      { 1100525010;3;Field  ;
                  CaptionML=[DEU=Belegnr.;
                             ENU=Document No.;
                             NLD=Documentnr.;
                             NOR=Dokumentnr.;
                             SVE=Dokumentnr];
                  SourceExpr=Retain[1] }

      { 1100525009;3;Field  ;
                  CaptionML=[DEU=Sachkonto;
                             ENU=G/L Account;
                             NLD=Grootboekrekening;
                             NOR=Finanskonto;
                             SVE=Redovisningskonto];
                  SourceExpr=Retain[2] }

      { 1100525008;3;Field  ;
                  CaptionML=[DEU=Element;
                             ENU=Element;
                             NLD=Element;
                             NOR=Element;
                             SVE=Element];
                  SourceExpr=Retain[3] }

      { 1100525007;3;Field  ;
                  CaptionML=[DEU=Mitarbeiternr.;
                             ENU=Employee No.;
                             NLD=Werknemernr.;
                             NOR=Ansattnr;
                             SVE=AnstÑllningsnr];
                  SourceExpr=Retain[4] }

      { 1100525006;3;Field  ;
                  CaptionML=[DEU=Lagerortcode;
                             ENU=Location Code;
                             NLD=Vestiging;
                             NOR=Plasseringkode;
                             SVE=LagerstÑllekod];
                  SourceExpr=Retain[5] }

      { 1100525005;3;Field  ;
                  CaptionML=[DEU=Lohnkomponente;
                             ENU=Wage Component;
                             NLD=Looncomponent;
                             NOR=Lõnnsart;
                             SVE=Lîneart];
                  SourceExpr=Retain[6] }

      { 1100525004;3;Field  ;
                  CaptionML=[DEU=Bearbeitungsjahr;
                             ENU=Service Control Year;
                             NLD=Uitvoeringsjaar;
                             NOR=ServicekontrollÜr;
                             SVE=ServicekontrollÜr];
                  SourceExpr=Retain[9] }

      { 1100525003;3;Field  ;
                  CaptionML=[DEU=Artikelnr.;
                             ENU=Item No.;
                             NLD=Artikelnr.;
                             NOR=Varenr.;
                             SVE=Artikelnr];
                  SourceExpr=Retain[10] }

      { 1100525002;3;Field  ;
                  CaptionML=[DEU=Debitorennr.;
                             ENU=Customer No.;
                             NLD=Klant;
                             NOR=Kundenr;
                             SVE=Kundnr];
                  SourceExpr=Retain[11] }

      { 1100525001;3;Field  ;
                  CaptionML=[DEU=Kreditorennr.;
                             ENU=Vendor No.;
                             NLD=Leveranciernr.;
                             NOR=Leverandõrnr.;
                             SVE=Leverantîrsnr];
                  SourceExpr=Retain[12] }

      { 1100525018;3;Field  ;
                  CaptionML=[DEU=Servicestandort;
                             ENU=Service Location No.;
                             NLD=Servicelocatie;
                             NOR=Servicelokasjon Nr.;
                             SVE=Serviceplatsnr];
                  SourceExpr=Retain[13] }

      { 1100525019;3;Field  ;
                  CaptionML=[DEU=Herkunftsart;
                             ENU=Source Type;
                             NLD=Bron;
                             NOR=Kildetype;
                             SVE=Ursprungstyp];
                  SourceExpr=Retain[14] }

      { 1100525000;2;Field  ;
                  CaptionML=[DEU=Dimensionen behalten;
                             ENU=Retain Dimensions;
                             NLD=Te bewaren dimensies;
                             NOR=Behold dimensjoner;
                             SVE=BibehÜll dimensioner];
                  SourceExpr=RetainDimText;
                  Editable=FALSE;
                  OnAssistEdit=BEGIN
                                 DimSelectionBuf.SetDimSelectionMultiple(3,REPORT::"Date Compress Service Ledger",RetainDimText);
                               END;
                                }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Durch diesen Batchauftrag werden Posten gelîscht. Daher muss vor dem AusfÅhren des Batchauftrags eine Datenbanksicherung erstellt werden.\\Mîchten Sie die Posten datumskomprimieren?;ENU=This batch job deletes entries. Therefore, it is important that you make a backup of the database before you run the batch job.\\Do you want to date compress the entries?;NLD=Met deze batchverwerking worden posten verwijderd. Maak daarom eerst een reservekopie van de database voor u de batchverwerking uitvoert.\\Wilt u de posten comprimeren?;NOR=Denne Batchjobben fjerner transaksjoner. Det er derfor viktig at du tar en sikkerhetskopi pÜ databasen fõr du kjõrer Batchjobbent.\\Vil du datokomprimere transaksjonene?;SVE=Detta batchjobb tar bort transaktioner. Det Ñr dÑrfîr viktigt att du tar en sÑkerhetskopia pÜ databasen innan du kîr batchjobbet.\\Vill du datumkomprimera transaktionerna?';
      Text003@1003 : TextConst 'DEU=%1 muss angegeben werden.;ENU=%1 must be specified.;NLD=%1 moet opgegeven worden.;NOR=%1 mÜ spesifiseres.;SVE=%1 mÜste specificeras.';
      Text004@1004 : TextConst 'DEU=Komprimieren Projektposten...\\;ENU=Date compressing job ledger entries...\\;NLD=Comprimeren projectposten...\\;NOR=Datokomprimerer jobbreskontrotransaksjonerˇ...\\;SVE=Datumkomprimerar jobbreskontratransaktionerˇ...\\';
      Text005@1005 : TextConst 'DEU=Servicevertragsnr.          #1##########\;ENU=Service Contract No.             #1##########\;NLD=Servicecontractnr.          #1##########\;NOR=Servicekontrakt nr. #1##########\;SVE=Servicekontraktnr             #1##########\';
      Text005a@1210190000 : TextConst 'DEU=Serviceauftragsnr.          #5##########\;ENU=Service Order No.             #5##########\;NLD=Serviceordernr.          #5##########\;NOR=Serviceordre nr. #5##########\;SVE=Serviceordernr             #5##########\';
      Text006@1006 : TextConst 'DEU=Datum                #2######\\;ENU=Date                    #2######\\;NLD=Datum                   #2######\\;NOR=Dato                #2######\\;SVE=Datum                   #2######\\';
      Text007@1007 : TextConst 'DEU=Anzahl neue Posten   #3######\;ENU=No. of new entries      #3######\;NLD=Nieuwe posten           #3######\;NOR=Antall nye transaksjoner     #3######\;SVE=Antal nya trans.     #3######\';
      Text008@1008 : TextConst 'DEU=Anz. gelîsch. Posten #4######;ENU=No. of entries deleted  #4######;NLD=Verwijderde posten      #4######;NOR=Antall fjernede transaksjoner    #4######;SVE=Antal bortt. trans.    #4######';
      Text009@1009 : TextConst 'DEU=Datumskomprimiert;ENU=Date Compressed;NLD=Gecomprimeerde post;NOR=Dato komprimert;SVE=Datumkomprimerad';
      Text010@1010 : TextConst 'DEU=Dimensionen behalten;ENU=Retain Dimensions;NLD=Te bewaren dimensies;NOR=Behold dimensjoner;SVE=BibehÜll dimensioner';
      SourceCodeSetup@1011 : Record 242;
      DateComprReg@1012 : Record 87;
      EntrdDateComprReg@1013 : Record 87;
      ServReg@1014 : Record 11012821;
      EntrdServLedgEntry@1015 : Record 11012819;
      NewServLedgEntry@1016 : Record 11012819;
      ServLedgEntry2@1017 : Record 11012819;
      SelectedDim@1018 : Record 369;
      TempSelectedDim@1019 : TEMPORARY Record 369;
      DimSelectionBuf@1020 : Record 368;
      DateComprMgt@1021 : Codeunit 356;
      DimBufMgt@1022 : Codeunit 411;
      DimMgt@1023 : Codeunit 408;
      Window@1024 : Dialog;
      ServLedgEntryFilter@1025 : Text[250];
      NoOfFields@1026 : Integer;
      Retain@1027 : ARRAY [14] OF Boolean;
      FieldNumber@1028 : ARRAY [14] OF Integer;
      FieldNameArray@1029 : ARRAY [14] OF Text[100];
      LastEntryNo@1030 : Integer;
      NoOfDeleted@1031 : Integer;
      ServRegExists@1032 : Boolean;
      i@1033 : Integer;
      ComprDimEntryNo@1034 : Integer;
      DimEntryNo@1035 : Integer;
      RetainDimText@1036 : Text[250];

    LOCAL PROCEDURE InitRegisters@4();
    BEGIN
      IF ServReg.FINDLAST THEN;
      ServReg.INIT;
      ServReg."No." := ServReg."No." + 1;
      ServReg."Creation Date" := TODAY;
      ServReg."Source Code" := SourceCodeSetup."Compress Service Ledger";
      ServReg."User ID" := USERID;
      ServReg."From Entry No." := LastEntryNo + 1;

      IF DateComprReg.FINDLAST THEN;
      DateComprReg.INIT;
      DateComprReg."No." := DateComprReg."No." + 1;
      DateComprReg."Table ID" := DATABASE::"Service-Ledger Entry";
      DateComprReg."Creation Date" := TODAY;
      DateComprReg."Starting Date" := EntrdDateComprReg."Starting Date";
      DateComprReg."Ending Date" := EntrdDateComprReg."Ending Date";
      DateComprReg."Period Length" := EntrdDateComprReg."Period Length";
      FOR i := 1 TO NoOfFields DO
        IF Retain[i] THEN
          DateComprReg."Retain Field Contents" :=
            COPYSTR(
              DateComprReg."Retain Field Contents" + ',' + FieldNameArray[i],1,
              MAXSTRLEN(DateComprReg."Retain Field Contents"));
      DateComprReg."Retain Field Contents" := COPYSTR(DateComprReg."Retain Field Contents",2);
      DateComprReg.Filter := ServLedgEntryFilter;
      DateComprReg."Register No." := ServReg."No.";
      DateComprReg."Source Code" := SourceCodeSetup."Compress Service Ledger";
      DateComprReg."User ID" := USERID;

      ServRegExists := FALSE;
      NoOfDeleted := 0;
    END;

    LOCAL PROCEDURE InsertRegisters@1210190000(VAR ServReg@1000 : Record 11012821;VAR DateComprReg@1001 : Record 87);
    BEGIN
      ServReg."To Entry No." := NewServLedgEntry."Entry No.";

      IF ServRegExists THEN BEGIN
        ServReg.MODIFY;
        DateComprReg.MODIFY;
      END ELSE BEGIN
        ServReg.INSERT;
        DateComprReg.INSERT;
        ServRegExists := TRUE;
      END;
      COMMIT;

      NewServLedgEntry.LOCKTABLE;
      ServReg.LOCKTABLE;
      DateComprReg.LOCKTABLE;

      ServLedgEntry2.RESET;
      IF ServLedgEntry2.FINDLAST THEN;
      IF LastEntryNo <> ServLedgEntry2."Entry No." THEN BEGIN
        LastEntryNo := ServLedgEntry2."Entry No.";
        InitRegisters;
      END;
    END;

    LOCAL PROCEDURE InsertField@2(Number@1000 : Integer;Name@1001 : Text[100]);
    BEGIN
      NoOfFields := NoOfFields + 1;
      FieldNumber[NoOfFields] := Number;
      FieldNameArray[NoOfFields] := Name;
    END;

    LOCAL PROCEDURE RetainNo@3(Number@1000 : Integer) : Boolean;
    BEGIN
      EXIT(Retain[Index(Number)]);
    END;

    LOCAL PROCEDURE Index@6(Number@1000 : Integer) : Integer;
    BEGIN
      FOR i := 1 TO NoOfFields DO
        IF Number = FieldNumber[i] THEN
          EXIT(i);
    END;

    PROCEDURE SummarizeEntry@5(VAR NewServLedgEntry@1000 : Record 11012819;ServLedgEntry@1001 : Record 11012819);
    BEGIN
      WITH ServLedgEntry DO BEGIN
        NewServLedgEntry.Quantity := NewServLedgEntry.Quantity + Quantity;
        NewServLedgEntry."Total Cost (LCY)" := NewServLedgEntry."Total Cost (LCY)" + "Total Cost (LCY)";
        NewServLedgEntry."Total Revenue (LCY)" := NewServLedgEntry."Total Revenue (LCY)" + "Total Revenue (LCY)";
        // dp00116.sn
        NewServLedgEntry."Total Cost" := NewServLedgEntry."Total Cost" + "Total Cost";
        NewServLedgEntry."Total Revenue" := NewServLedgEntry."Total Revenue" + "Total Revenue";
        // dp00116.en
        //NewServLedgEntry."Amt. to Post to G/L" := NewServLedgEntry."Amt. to Post to G/L" + "Amt. to Post to G/L";
        //NewServLedgEntry."Amt. Posted to G/L" := NewServLedgEntry."Amt. Posted to G/L" + "Amt. Posted to G/L";
        //NewServLedgEntry."Amt. to Recognize" := NewServLedgEntry."Amt. to Recognize" + "Amt. to Recognize";
        //NewServLedgEntry."Amt. Recognized" := NewServLedgEntry."Amt. Recognized" + "Amt. Recognized";

        //NewServLedgEntry."Line Amount (LCY)" := NewServLedgEntry."Line Amount (LCY)" + "Line Amount (LCY)";
        //NewServLedgEntry."Line Discount Amount (LCY)" := NewServLedgEntry."Line Discount Amount (LCY)" + "Line Discount Amount (LCY)";
        //NewServLedgEntry."Original Total Cost (LCY)" := NewServLedgEntry."Original Total Cost (LCY)" + "Original Total Cost (LCY)";
        NewServLedgEntry."Closed Costs" := NewServLedgEntry."Closed Costs" + "Closed Costs";
        NewServLedgEntry."Closed Revenues" := NewServLedgEntry."Closed Revenues" + "Closed Revenues";

        DELETE;
        DateComprReg."No. Records Deleted" := DateComprReg."No. Records Deleted" + 1;
        Window.UPDATE(4,DateComprReg."No. Records Deleted");
      END;
    END;

    PROCEDURE ComprCollectedEntries@12();
    VAR
      ServLedgEntry@1000 : Record 11012819;
      OldDimEntryNo@1001 : Integer;
      Found@1002 : Boolean;
      ServLedgEntryNo@1003 : Integer;
    BEGIN
      OldDimEntryNo := 0;
      IF DimBufMgt.FindFirstDimEntryNo(DimEntryNo,ServLedgEntryNo) THEN BEGIN
        InitNewEntry(NewServLedgEntry);
        REPEAT
          ServLedgEntry.GET(ServLedgEntryNo);
          SummarizeEntry(NewServLedgEntry,ServLedgEntry);
          OldDimEntryNo := DimEntryNo;
          Found := DimBufMgt.NextDimEntryNo(DimEntryNo,ServLedgEntryNo);
          IF (OldDimEntryNo <> DimEntryNo) OR NOT Found THEN BEGIN
            InsertNewEntry(NewServLedgEntry,OldDimEntryNo);
            IF Found THEN
              InitNewEntry(NewServLedgEntry);
          END;
          OldDimEntryNo := DimEntryNo;
        UNTIL NOT Found;
      END;
      DimBufMgt.DeleteAllDimEntryNo;
    END;

    PROCEDURE InitNewEntry@7(VAR NewServLedgEntry@1000 : Record 11012819);
    BEGIN
      LastEntryNo := LastEntryNo + 1;

      WITH ServLedgEntry2 DO BEGIN
        NewServLedgEntry.INIT;
        NewServLedgEntry."Entry No." := LastEntryNo;
        NewServLedgEntry."Service Contract No." := "Service Contract No.";
        NewServLedgEntry."Service Order No." := "Service Order No.";
        NewServLedgEntry."Posting Date" := GETRANGEMIN("Posting Date");
        NewServLedgEntry.Description := EntrdServLedgEntry.Description;
        NewServLedgEntry."Source Code" := SourceCodeSetup."Compress Service Ledger";
        NewServLedgEntry."User ID" := USERID;
        NewServLedgEntry."Cost Type" := "Cost Type";

        IF RetainNo(FIELDNO("Document No.")) THEN
          NewServLedgEntry."Document No." := "Document No.";
        IF RetainNo(FIELDNO("G/L Account")) THEN
          NewServLedgEntry."G/L Account" := "G/L Account";
        IF RetainNo(FIELDNO("Location Code")) THEN
          NewServLedgEntry."Location Code" := "Location Code";
        IF RetainNo(FIELDNO("Global Dimension 1 Code")) THEN
          NewServLedgEntry."Global Dimension 1 Code" := "Global Dimension 1 Code";
        IF RetainNo(FIELDNO("Global Dimension 2 Code")) THEN
          NewServLedgEntry."Global Dimension 2 Code" := "Global Dimension 2 Code";
        IF RetainNo(FIELDNO("Employee No.")) THEN
          NewServLedgEntry."Employee No." := "Employee No.";
        IF RetainNo(FIELDNO("Wage Component")) THEN
          NewServLedgEntry."Wage Component" := "Wage Component";
        IF RetainNo(FIELDNO("Item No.")) THEN
          NewServLedgEntry."Item No." := "Item No.";
        IF RetainNo(FIELDNO("Customer No.")) THEN
          NewServLedgEntry."Customer No." := "Customer No.";
        IF RetainNo(FIELDNO("Vendor No.")) THEN
          NewServLedgEntry."Vendor No." := "Vendor No.";
        IF RetainNo(FIELDNO("Service Location No.")) THEN
          NewServLedgEntry."Service Location No." := "Service Location No.";
        IF RetainNo(FIELDNO("Source Type")) THEN
          NewServLedgEntry."Source Type" := "Source Type";

        Window.UPDATE(1,NewServLedgEntry."Service Contract No.");
        Window.UPDATE(5,NewServLedgEntry."Service Order No.");
        Window.UPDATE(2,NewServLedgEntry."Posting Date");
        DateComprReg."No. of New Records" := DateComprReg."No. of New Records" + 1;
        Window.UPDATE(3,DateComprReg."No. of New Records");
      END;
    END;

    PROCEDURE InsertNewEntry@23(VAR NewServLedgEntry@1000 : Record 11012819;DimEntryNo@1001 : Integer);
    VAR
      TempDimBuf@1002 : TEMPORARY Record 360;
      TempDimSetEntry@1100525001 : TEMPORARY Record 480;
      DimVal@1100525000 : Record 349;
    BEGIN
      TempDimBuf.DELETEALL;
      DimBufMgt.GetDimensions(DimEntryNo,TempDimBuf);
      IF TempDimBuf.FINDSET THEN
        REPEAT
          DimVal.GET(TempDimBuf."Dimension Code",TempDimBuf."Dimension Value Code");
          TempDimSetEntry."Dimension Code" := TempDimBuf."Dimension Code";
          TempDimSetEntry."Dimension Value Code" := TempDimBuf."Dimension Value Code";
          TempDimSetEntry."Dimension Value ID" := DimVal."Dimension Value ID";
          TempDimSetEntry.INSERT;
        UNTIL TempDimBuf.NEXT = 0;
      NewServLedgEntry."Dimension Set ID" := DimMgt.GetDimensionSetID(TempDimSetEntry);
      NewServLedgEntry.INSERT;
    END;

    BEGIN
    {
      Retain (array=14):
      01:  "Document No."             (optional)
      02:  "G/L Account"              (optional)
      04:  "Employee No."             (optional)
      05:  "Location Code"            (optional)
      06:  "Wage Component"           (optional)
      07:  "Global Dimension 1 Code"  (predefined)
      08:  "Global Dimension 2 Code"  (predefined)
      09:  "Service Control Year"     (optional)
      10:  "Item No."                 (optional)
      11:  "Customer No."             (optional)
      12:  "Vendor No."               (optional)
      13:  "Service Location No."     (optional)
      14:  "Source Type"              (optional)
    }
    END.
  }
  RDLDATA
  {
  }
}

