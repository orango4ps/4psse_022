OBJECT Report 7323 Create Invt Put-away/Pick/Mvmt
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    AccessByPermission=TableData 14=R;
    CaptionML=[DEU=Bestandseinlagerung/Kommissionierung/Umlagerung erstellen;
               ENU=Create Inventory Put-away/Pick/Movement;
               NLD=Voorraadopslag/-pick/-verplaatsing maken;
               NOR=Opprett lagerplassering/-plukk/-flytting];
    ApplicationArea=#Warehouse;
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF NOT CreatePutAway AND NOT (CreatePick OR CreateMovement)THEN
                    ERROR(Text008);

                  CreateInvtPickMovement.SetInvtMovement(CreateMovement);
                END;

    OnPostReport=BEGIN
                   TempWhseActivHdr.DELETEALL;
                 END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 9356;    ;DataItem;                    ;
               DataItemTable=Table5765;
               DataItemTableView=SORTING(Source Document,Source No.);
               OnPreDataItem=BEGIN
                               IF CreatePutAway AND NOT (CreatePick OR CreateMovement) THEN
                                 SETRANGE(Type,Type::Inbound);
                               IF NOT CreatePutAway AND (CreatePick OR CreateMovement) THEN
                                 SETRANGE(Type,Type::Outbound);

                               Window.OPEN(
                                 Text001 +
                                 Text002 +
                                 Text003);

                               DocumentCreated := FALSE;

                               IF CreatePick OR CreateMovement THEN
                                 CreateInvtPickMovement.SetReportGlobals(PrintDocument,ShowError);
                             END;

               OnAfterGetRecord=VAR
                                  ATOMvmntCreated@1001 : Integer;
                                  TotalATOMvmtToBeCreated@1002 : Integer;
                                BEGIN
                                  Window.UPDATE(1,"Source Document");
                                  Window.UPDATE(2,"Source No.");

                                  CASE Type OF
                                    Type::Inbound:
                                      TotalPutAwayCounter += 1;
                                    Type::Outbound:
                                      IF CreatePick THEN
                                        TotalPickCounter += 1
                                      ELSE
                                        TotalMovementCounter += 1;
                                  END;

                                  IF CheckWhseRequest("Warehouse Request") THEN
                                    CurrReport.SKIP;

                                  IF ((Type = Type::Inbound) AND (WhseActivHeader.Type <> WhseActivHeader.Type::"Invt. Put-away")) OR
                                     ((Type = Type::Outbound) AND ((WhseActivHeader.Type <> WhseActivHeader.Type::"Invt. Pick") AND
                                                                   (WhseActivHeader.Type <> WhseActivHeader.Type::"Invt. Movement"))) OR
                                     ("Source Type" <> WhseActivHeader."Source Type") OR
                                     ("Source Subtype" <> WhseActivHeader."Source Subtype") OR
                                     ("Source No." <> WhseActivHeader."Source No.") OR
                                     ("Location Code" <> WhseActivHeader."Location Code")
                                  THEN BEGIN
                                    CASE Type OF
                                      Type::Inbound:
                                        IF NOT CreateInvtPutAway.CheckSourceDoc("Warehouse Request") THEN
                                          CurrReport.SKIP;
                                      Type::Outbound:
                                        IF NOT CreateInvtPickMovement.CheckSourceDoc("Warehouse Request") THEN
                                          CurrReport.SKIP;
                                    END;
                                    InitWhseActivHeader;
                                  END;

                                  CASE Type OF
                                    Type::Inbound:
                                      BEGIN
                                        CreateInvtPutAway.SetWhseRequest("Warehouse Request",TRUE);
                                        CreateInvtPutAway.AutoCreatePutAway(WhseActivHeader);
                                      END;
                                    Type::Outbound:
                                      BEGIN
                                        CreateInvtPickMovement.SetWhseRequest("Warehouse Request",TRUE);
                                        CreateInvtPickMovement.AutoCreatePickOrMove(WhseActivHeader);
                                      END;
                                  END;

                                  IF WhseActivHeader."No." <> '' THEN BEGIN
                                    DocumentCreated := TRUE;
                                    CASE Type OF
                                      Type::Inbound:
                                        PutAwayCounter := PutAwayCounter + 1;
                                      Type::Outbound:
                                        IF CreatePick THEN BEGIN
                                          PickCounter := PickCounter + 1;

                                          CreateInvtPickMovement.GetATOMovementsCounters(ATOMvmntCreated,TotalATOMvmtToBeCreated);
                                          MovementCounter += ATOMvmntCreated;
                                          TotalMovementCounter += TotalATOMvmtToBeCreated;
                                        END ELSE
                                          MovementCounter += 1;
                                    END;
                                    IF PrintDocument THEN
                                      InsertTempWhseActivHdr;
                                    COMMIT;
                                  END;
                                END;

               OnPostDataItem=VAR
                                ExpiredItemMessageText@1000 : Text[100];
                                Msg@1002 : Text;
                              BEGIN
                                ExpiredItemMessageText := CreateInvtPickMovement.GetExpiredItemMessage;
                                IF TempWhseActivHdr.FIND('-') THEN
                                  PrintNewDocuments;

                                Window.CLOSE;
                                IF NOT SuppressMessagesState THEN
                                  IF DocumentCreated THEN BEGIN
                                    IF PutAwayCounter > 0 THEN
                                      AddToText(Msg,STRSUBSTNO(Text005,WhseActivHeader.Type::"Invt. Put-away",PutAwayCounter,TotalPutAwayCounter));
                                    IF PickCounter > 0 THEN
                                      AddToText(Msg,STRSUBSTNO(Text005,WhseActivHeader.Type::"Invt. Pick",PickCounter,TotalPickCounter));
                                    IF MovementCounter > 0 THEN
                                      AddToText(Msg,STRSUBSTNO(Text005,WhseActivHeader.Type::"Invt. Movement",MovementCounter,TotalMovementCounter));

                                    IF CreatePutAway OR CreatePick THEN
                                      Msg += ExpiredItemMessageText;

                                    MESSAGE(Msg);
                                  END ELSE BEGIN
                                    Msg := Text004 + ' ' + ExpiredItemMessageText;
                                    MESSAGE(Msg);
                                  END;
                              END;

               ReqFilterFields=Source Document,Source No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnInit=BEGIN
               CreatePickEditable := TRUE;
               CreateMovementEditable := TRUE;
             END;

      OnOpenPage=BEGIN
                   OnBeforeOpenPage;

                   EnableFieldsInPage;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1   ;2   ;Field     ;
                  Name=CreateInventorytPutAway;
                  CaptionML=[DEU=Lagereinlag. erst.;
                             ENU=Create Invt. Put-Away;
                             NLD=Voorraadopslag maken;
                             NOR=Opprett lagerplassering];
                  ToolTipML=[DEU=Gibt an, ob Sie Lagereinlagerungsbelege fÅr alle Herkunftsbelege erzeugen mîchten, die vom Filter abgedeckt werden und fÅr die ein Einlagerungsbeleg geeignet ist.;
                             ENU=Specifies if you want to create inventory put-away documents for all source documents that are included in the filter and for which a put-away document is appropriate.;
                             NLD=Hiermee wordt opgegeven of u voorraadopslagdocumenten wilt maken voor alle brondocumenten die zijn opgenomen in het filter en waarvoor een opslagdocument geschikt is.;
                             NOR=Angir om du vil opprette lagerplasseringsdokumenter for alle kildedokumenter som er inkludert i filteret, og som et plasseringsdokument passer for.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=CreatePutAway }

      { 3   ;2   ;Field     ;
                  Name=CInvtPick;
                  CaptionML=[DEU=Lagerkomm. erst.;
                             ENU=Create Invt. Pick;
                             NLD=Voorraadpick maken;
                             NOR=Opprett lagerplukking];
                  ToolTipML=[DEU=Gibt an, ob Sie Lagerkommissionierdokumente fÅr alle Herkunftsbelege erzeugen mîchten, die vom Filter abgedeckt werden und fÅr die ein Lagerkommissionierdokument geeignet ist.;
                             ENU=Specifies if you want to create inventory pick documents for all source documents that are included in the filter and for which a pick document is appropriate.;
                             NLD=Hiermee wordt opgegeven of u voorraadpickdocumenten wilt maken voor alle brondocumenten die zijn opgenomen in het filter en waarvoor een pickdocument geschikt is.;
                             NOR=Angir om du vil opprette lagerplukkdokumenter for alle kildedokumenter som er inkludert i filteret, og som et plukkdokument passer for.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=CreatePick;
                  Enabled=CreatePickEditable;
                  Editable=CreatePickEditable;
                  OnValidate=BEGIN
                               IF CreatePick AND CreateMovement THEN
                                 ERROR(Text009);
                               EnableFieldsInPage;
                             END;
                              }

      { 10  ;2   ;Field     ;
                  Name=CInvtMvmt;
                  CaptionML=[DEU=Lagerbestandsumlagerung erstellen;
                             ENU=Create Invt. Movement;
                             NLD=Voorraadverplaatsing maken;
                             NOR=Opprett lagerflytting];
                  ToolTipML=[DEU=Gibt an, ob Sie Lagerbestandsumlagerungsdokumente fÅr alle Herkunftsbelege erzeugen mîchten, die vom Filter abgedeckt werden und fÅr die ein Bestandsumlagerungsdokument geeignet ist.;
                             ENU=Specifies if you want to create inventory movement documents for all source documents that are included in the filter and for which a movement document is appropriate.;
                             NLD=Hiermee wordt opgegeven of u voorraadverplaatsingsdocumenten wilt maken voor alle brondocumenten die zijn opgenomen in het filter en waarvoor een verplaatsingsdocument geschikt is.;
                             NOR=Angir om du vil opprette lagerflyttingsdokumenter for alle kildedokumenter som er inkludert i filteret, og som et flyttedokument passer for.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=CreateMovement;
                  Enabled=CreateMovementEditable;
                  Editable=CreateMovementEditable;
                  OnValidate=BEGIN
                               IF CreatePick AND CreateMovement THEN
                                 ERROR(Text009);
                               EnableFieldsInPage;
                             END;
                              }

      { 6   ;2   ;Field     ;
                  CaptionML=[DEU=Beleg drucken;
                             ENU=Print Document;
                             NLD=Document afdrukken;
                             NOR=Skriv ut dokument];
                  ToolTipML=[DEU=Gibt an, ob das Dokument gedruckt werden soll.;
                             ENU=Specifies if you want the document to be printed.;
                             NLD=Hiermee wordt opgegeven of het document moet worden afgedrukt.;
                             NOR=Angir om du vil at dokumentet skal skrives ut.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=PrintDocument }

      { 7   ;2   ;Field     ;
                  CaptionML=[DEU=Fehler anzeigen;
                             ENU=Show Error;
                             NLD=Fout weergeven;
                             NOR=Vis feil;
                             SVE=Visa fel];
                  ToolTipML=[DEU=Gibt an, ob im Bericht Fehlerinformationen angezeigt werden.;
                             ENU=Specifies if the report shows error information.;
                             NLD=Hiermee wordt opgegeven of er informatie over fouten in het rapport wordt weergegeven.;
                             NOR=Angir om rapporten viser informasjon om feil.;
                             SVE=Anger om rapporten innehÜller felinformation.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=ShowError }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      WhseActivHeader@1002 : Record 5766;
      TempWhseActivHdr@1013 : TEMPORARY Record 5766;
      CreateInvtPutAway@1003 : Codeunit 7321;
      CreateInvtPickMovement@1004 : Codeunit 7322;
      WhseDocPrint@1012 : Codeunit 5776;
      Window@1005 : Dialog;
      CreatePutAway@1000 : Boolean;
      CreatePick@1001 : Boolean;
      Text001@1008 : TextConst 'DEU=LageraktivitÑten werden erstellt...\\;ENU=Creating Inventory Activities...\\;NLD=Voorraadactiviteiten maken...\\;NOR=Opprette lageraktiviteter ...\\';
      Text002@1007 : TextConst 'DEU=Herkunftsart    #1##########\;ENU=Source Type     #1##########\;NLD=Bronsoort       #1##########\;NOR=Kildetype       #1##########\';
      Text003@1006 : TextConst 'DEU=Herkunftsnr.    #2##########;ENU=Source No.      #2##########;NLD=Bronnr.         #2##########;NOR=Kildenr.        #2##########';
      CreateMovement@1023 : Boolean;
      DocumentCreated@1010 : Boolean;
      PrintDocument@1011 : Boolean;
      PutAwayCounter@1014 : Integer;
      PickCounter@1015 : Integer;
      MovementCounter@1027 : Integer;
      Text004@1009 : TextConst 'DEU=Es gibt nichts zu erstellen.;ENU=There is nothing to create.;NLD=Niets om te maken.;NOR=Det finnes ingenting Ü opprette.;SVE=Det finns inget att skapa.';
      Text005@1016 : TextConst 'DEU=Anzahl der erstellten AktivitÑten vom Typ ''%1'': %2 von insgesamt %3.;ENU=Number of %1 activities created: %2 out of a total of %3.;NLD=Aantal gemaakte %1-activiteiten: %2 van %3.;NOR=Antall %1-aktiviteter opprettet: %2 av totalt %3.';
      Text006@1017 : TextConst '@@@={Locked};DEU=%1\\%2;ENU=%1\\%2;NLD=%1\\%2;NOR=%1\\%2';
      Text008@1019 : TextConst 'DEU=WÑhlen Sie ''Lagereinlag. erst.'', ''Lagerkomm. erst.'' oder ''Lagerbestandsumlagerung erstellen'' aus.;ENU=You must select Create Invt. Put-away, Create Invt. Pick, or Create Invt. Movement.;NLD=U moet Voorraadopslag maken, Voorraadpick maken of Voorraadverplaatsing maken selecteren.;NOR=Du mÜ velge Opprett lagerplassering, Opprett lagerplukking eller Opprett lagerflytting.';
      TotalPutAwayCounter@1020 : Integer;
      TotalPickCounter@1021 : Integer;
      TotalMovementCounter@1025 : Integer;
      ShowError@1022 : Boolean;
      Text009@1024 : TextConst 'DEU=Sie kînnen entweder ''Lagerkomm. erst.'' oder ''Lagerbestandsumlagerung erstellen'' auswÑhlen.;ENU=You can select either Create Invt. Pick or Create Invt. Movement.;NLD=U kunt Voorraadpick maken of Voorraadverplaatsing maken selecteren.;NOR=Du kan velge enten Opprett lagerplukking eller Opprett lagerflytting.';
      CreatePickEditable@1026 : Boolean INDATASET;
      CreateMovementEditable@1030 : Boolean INDATASET;
      SuppressMessagesState@1031 : Boolean;

    LOCAL PROCEDURE InitWhseActivHeader@7();
    BEGIN
      WITH WhseActivHeader DO BEGIN
        INIT;
        CASE "Warehouse Request".Type OF
          "Warehouse Request".Type::Inbound:
            Type := Type::"Invt. Put-away";
          "Warehouse Request".Type::Outbound:
            IF CreatePick THEN
              Type := Type::"Invt. Pick"
            ELSE
              Type := Type::"Invt. Movement";
        END;
        "No." := '';
        "Location Code" := "Warehouse Request"."Location Code";
      END;
    END;

    LOCAL PROCEDURE InsertTempWhseActivHdr@1();
    BEGIN
      TempWhseActivHdr.INIT;
      TempWhseActivHdr := WhseActivHeader;
      TempWhseActivHdr.INSERT;
    END;

    LOCAL PROCEDURE PrintNewDocuments@2();
    BEGIN
      WITH TempWhseActivHdr DO BEGIN
        REPEAT
          CASE Type OF
            Type::"Invt. Put-away":
              WhseDocPrint.PrintInvtPutAwayHeader(TempWhseActivHdr,TRUE);
            Type::"Invt. Pick":
              IF CreatePick THEN
                WhseDocPrint.PrintInvtPickHeader(TempWhseActivHdr,TRUE)
              ELSE
                WhseDocPrint.PrintInvtMovementHeader(TempWhseActivHdr,TRUE);
          END;
        UNTIL NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE CheckWhseRequest@3(WhseRequest@1000 : Record 5765) : Boolean;
    VAR
      SalesHeader@1001 : Record 36;
      TransferHeader@1002 : Record 5740;
      GetSrcDocOutbound@1003 : Codeunit 5752;
    BEGIN
      IF WhseRequest."Document Status" <> WhseRequest."Document Status"::Released THEN
        EXIT(TRUE);
      IF (WhseRequest.Type = WhseRequest.Type::Outbound) AND
         (WhseRequest."Shipping Advice" = WhseRequest."Shipping Advice"::Complete)
      THEN
        CASE WhseRequest."Source Type" OF
          DATABASE::"Sales Line":
            IF WhseRequest."Source Subtype" = WhseRequest."Source Subtype"::"1" THEN BEGIN
              SalesHeader.GET(SalesHeader."Document Type"::Order,WhseRequest."Source No.");
              EXIT(GetSrcDocOutbound.CheckSalesHeader(SalesHeader,ShowError));
            END;
          DATABASE::"Transfer Line":
            BEGIN
              TransferHeader.GET(WhseRequest."Source No.");
              EXIT(GetSrcDocOutbound.CheckTransferHeader(TransferHeader,ShowError));
            END;
        END;
    END;

    [External]
    PROCEDURE InitializeRequest@4(NewCreateInvtPutAway@1000 : Boolean;NewCreateInvtPick@1001 : Boolean;NewCreateInvtMovement@1004 : Boolean;NewPrintDocument@1002 : Boolean;NewShowError@1003 : Boolean);
    BEGIN
      CreatePutAway := NewCreateInvtPutAway;
      CreatePick := NewCreateInvtPick;
      CreateMovement := NewCreateInvtMovement;
      PrintDocument := NewPrintDocument;
      ShowError := NewShowError;
    END;

    LOCAL PROCEDURE EnableFieldsInPage@5();
    BEGIN
      CreatePickEditable := NOT CreateMovement;
      CreateMovementEditable := NOT CreatePick;
    END;

    [External]
    PROCEDURE SuppressMessages@6(NewState@1000 : Boolean);
    BEGIN
      SuppressMessagesState := NewState;
    END;

    LOCAL PROCEDURE AddToText@9(VAR OrigText@1000 : Text;Addendum@1001 : Text);
    BEGIN
      IF OrigText = '' THEN
        OrigText := Addendum
      ELSE
        OrigText := STRSUBSTNO(Text006,OrigText,Addendum);
    END;

    [External]
    PROCEDURE GetMovementCounters@16(VAR MovementsCreated@1000 : Integer;VAR TotalMovementsToBeCreated@1001 : Integer);
    BEGIN
      MovementsCreated := MovementCounter;
      TotalMovementsToBeCreated := TotalMovementCounter;
    END;

    [Integration(DEFAULT,TRUE)]
    LOCAL PROCEDURE OnBeforeOpenPage@8();
    BEGIN
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

