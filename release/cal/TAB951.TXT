OBJECT Table 951 Time Sheet Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03,4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 5200=r;
    OnInsert=VAR
               Resource@1000 : Record 156;
             BEGIN
               GetTimeSheetResource(Resource);
               CheckResourcePrivacyBlocked(Resource);
               Resource.TESTFIELD(Blocked,FALSE);

               UpdateApproverID;
               "Time Sheet Starting Date" := TimeSheetHeader."Starting Date";
             END;

    OnModify=VAR
               Resource@1000 : Record 156;
             BEGIN
               GetTimeSheetResource(Resource);
               CheckResourcePrivacyBlocked(Resource);
               Resource.TESTFIELD(Blocked,FALSE);

               UpdateDetails;
             END;

    OnDelete=VAR
               TimeSheetCommentLine@1000 : Record 953;
               Resource@1001 : Record 156;
             BEGIN
               TestStatus;

               GetTimeSheetResource(Resource);
               CheckResourcePrivacyBlocked(Resource);
               Resource.TESTFIELD(Blocked,FALSE);

               TimeSheetDetail.SETRANGE("Time Sheet No.","Time Sheet No.");
               TimeSheetDetail.SETRANGE("Time Sheet Line No.","Line No.");
               TimeSheetDetail.DELETEALL;

               TimeSheetCommentLine.SETRANGE("No.","Time Sheet No.");
               TimeSheetCommentLine.SETRANGE("Time Sheet Line No.","Line No.");
               TimeSheetCommentLine.DELETEALL;
             END;

    CaptionML=[ENU=Time Sheet Line;
               NOR=Timelistelinje;
               SVE=Rad pÜ tidrapport];
  }
  FIELDS
  {
    { 1   ;   ;Time Sheet No.      ;Code20        ;TableRelation="Time Sheet Header";
                                                   CaptionML=[ENU=Time Sheet No.;
                                                              NOR=Timelistenummer;
                                                              SVE=Tidrapportsnr] }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 3   ;   ;Time Sheet Starting Date;Date      ;CaptionML=[ENU=Time Sheet Starting Date;
                                                              NOR=Startdato for timeliste;
                                                              SVE=Startdatum fîr tidrapport];
                                                   Editable=No }
    { 5   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                TestStatus;
                                                                IF Type = Type::"Assembly Order" THEN
                                                                  FIELDERROR(Type);
                                                                IF Type <> xRec.Type THEN BEGIN
                                                                  TimeSheetDetail.SETRANGE("Time Sheet No.","Time Sheet No.");
                                                                  TimeSheetDetail.SETRANGE("Time Sheet Line No.","Line No.");
                                                                  IF NOT TimeSheetDetail.ISEMPTY THEN
                                                                    TimeSheetDetail.DELETEALL;
                                                                  "Job No." := '';
                                                                  "Job Task No." := '';
                                                                  "Service Order No." := '';
                                                                  "Service Order Line No." := 0;
                                                                  "Cause of Absence Code" := '';
                                                                  Description := '';
                                                                  "Assembly Order No." := '';
                                                                  "Assembly Order Line No." := 0;
                                                                  UpdateApproverID;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              NOR=Type;
                                                              SVE=Typ];
                                                   OptionCaptionML=[ENU=" ,Resource,Job,Service,Absence,Assembly Order";
                                                                    NOR=" ,Ressurs,Jobb,Service,Fravër,Monteringsordre";
                                                                    SVE=" ,Resurs,Projekt,Service,FrÜnvaro,Monteringsorder"];
                                                   OptionString=[ ,Resource,Job,Service,Absence,Assembly Order] }
    { 6   ;   ;Job No.             ;Code20        ;TableRelation=Job;
                                                   OnValidate=BEGIN
                                                                IF "Job No." <> '' THEN BEGIN
                                                                  TESTFIELD(Type,Type::Job);
                                                                  Job.GET("Job No.");
                                                                  IF Job.Blocked = Job.Blocked::All THEN
                                                                    Job.TestBlocked;
                                                                END;
                                                                VALIDATE("Job Task No.",'');
                                                                UpdateApproverID;
                                                                UpdateJobId;
                                                              END;

                                                   CaptionML=[ENU=Job No.;
                                                              NOR=Prosjektnr.;
                                                              SVE=Projektnr] }
    { 7   ;   ;Job Task No.        ;Code20        ;TableRelation="Job Task"."Job Task No." WHERE (Job No.=FIELD(Job No.));
                                                   OnValidate=BEGIN
                                                                IF "Job Task No." <> '' THEN BEGIN
                                                                  TESTFIELD(Type,Type::Job);
                                                                  JobTask.GET("Job No.","Job Task No.");
                                                                  JobTask.TESTFIELD("Job Task Type",JobTask."Job Task Type"::Posting);
                                                                  Description := JobTask.Description;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Job Task No.;
                                                              NOR=Prosjektoppgavenr.;
                                                              SVE=Projektaktivitetsnr] }
    { 9   ;   ;Cause of Absence Code;Code10       ;TableRelation="Cause of Absence";
                                                   OnValidate=VAR
                                                                Resource@1002 : Record 156;
                                                                Employee@1001 : Record 5200;
                                                                CauseOfAbsence@1000 : Record 5206;
                                                              BEGIN
                                                                IF "Cause of Absence Code" <> '' THEN BEGIN
                                                                  TESTFIELD(Type,Type::Absence);
                                                                  CauseOfAbsence.GET("Cause of Absence Code");
                                                                  Description := CauseOfAbsence.Description;
                                                                  TimeSheetHeader.GET("Time Sheet No.");
                                                                  Resource.GET(TimeSheetHeader."Resource No.");
                                                                  Resource.TESTFIELD("Base Unit of Measure");
                                                                  Resource.TESTFIELD(Type,Resource.Type::Person);
                                                                  Employee.RESET;
                                                                  Employee.SETRANGE("Resource No.",TimeSheetHeader."Resource No.");
                                                                  IF Employee.ISEMPTY THEN
                                                                    ERROR(Text001,TimeSheetHeader."Resource No.");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Cause of Absence Code;
                                                              NOR=Kode fravërsÜrsak;
                                                              SVE=Kod fîr Orsak till frÜnvaro] }
    { 10  ;   ;Description         ;Text100       ;OnValidate=BEGIN
                                                                TestStatus;
                                                              END;

                                                   CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 11  ;   ;Work Type Code      ;Code10        ;TableRelation="Work Type";
                                                   OnValidate=BEGIN
                                                                IF ("Work Type Code" <> xRec."Work Type Code") AND ("Work Type Code" <> '') THEN
                                                                  CheckWorkType;
                                                              END;

                                                   CaptionML=[ENU=Work Type Code;
                                                              NOR=Arbeidstypekode;
                                                              SVE=Arbetstypkod] }
    { 12  ;   ;Approver ID         ;Code50        ;TableRelation="User Setup";
                                                   DataClassification=EndUserIdentifiableInformation;
                                                   CaptionML=[ENU=Approver ID;
                                                              NOR=Godkjenner-ID;
                                                              SVE=GodkÑnnar-ID];
                                                   Editable=No }
    { 13  ;   ;Service Order No.   ;Code20        ;TableRelation=IF (Posted=CONST(No)) "Service Header".No. WHERE (Document Type=CONST(Order));
                                                   OnValidate=VAR
                                                                ServiceHeader@1000 : Record 5900;
                                                              BEGIN
                                                                IF "Service Order No." <> '' THEN BEGIN
                                                                  TESTFIELD(Type,Type::Service);
                                                                  ServiceHeader.GET(ServiceHeader."Document Type"::Order,"Service Order No.");
                                                                  Description := COPYSTR(
                                                                      STRSUBSTNO(Text003,"Service Order No.",ServiceHeader."Customer No."),
                                                                      1,
                                                                      MAXSTRLEN(Description));
                                                                END ELSE
                                                                  Description := '';
                                                              END;

                                                   CaptionML=[ENU=Service Order No.;
                                                              NOR=Serviceordrenr.;
                                                              SVE=Serviceordernr] }
    { 14  ;   ;Service Order Line No.;Integer     ;CaptionML=[ENU=Service Order Line No.;
                                                              NOR=Linjenr. for serviceordre;
                                                              SVE=Radnummer pÜ serviceorder] }
    { 15  ;   ;Total Quantity      ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Time Sheet Detail".Quantity WHERE (Time Sheet No.=FIELD(Time Sheet No.),
                                                                                                       Time Sheet Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=Total Quantity;
                                                              NOR=Antall i alt;
                                                              SVE=Totalt antal];
                                                   Editable=No }
    { 17  ;   ;Chargeable          ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[ENU=Chargeable;
                                                              NOR=Belastbar;
                                                              SVE=Debiterbar] }
    { 18  ;   ;Assembly Order No.  ;Code20        ;TableRelation=IF (Posted=CONST(No)) "Assembly Header".No. WHERE (Document Type=CONST(Order));
                                                   CaptionML=[ENU=Assembly Order No.;
                                                              NOR=Monteringsordrenr.;
                                                              SVE=Monteringsordernummer];
                                                   Editable=No }
    { 19  ;   ;Assembly Order Line No.;Integer    ;CaptionML=[ENU=Assembly Order Line No.;
                                                              NOR=Linjenr. for monteringsordre;
                                                              SVE=Radnummer fîr monteringsorder];
                                                   Editable=No }
    { 20  ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=[ENU=Open,Submitted,Rejected,Approved;
                                                                    NOR=èpen,Sendt,Avvist,Godkjent;
                                                                    SVE=ôppen,Skickad,Avvisad,GodkÑnd];
                                                   OptionString=Open,Submitted,Rejected,Approved;
                                                   Editable=No }
    { 21  ;   ;Approved By         ;Code50        ;TableRelation="User Setup";
                                                   DataClassification=EndUserIdentifiableInformation;
                                                   CaptionML=[ENU=Approved By;
                                                              NOR=Godkjent av;
                                                              SVE=GodkÑnd av];
                                                   Editable=No }
    { 22  ;   ;Approval Date       ;Date          ;CaptionML=[ENU=Approval Date;
                                                              NOR=Godkjenningsdato;
                                                              SVE=Datum godkÑnd];
                                                   Editable=No }
    { 23  ;   ;Posted              ;Boolean       ;CaptionML=[ENU=Posted;
                                                              NOR=Bokfõrt;
                                                              SVE=Bokfîrd];
                                                   Editable=No }
    { 26  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Time Sheet Comment Line" WHERE (No.=FIELD(Time Sheet No.),
                                                                                                      Time Sheet Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=Comment;
                                                              NOR=Merknad;
                                                              SVE=Kommentar];
                                                   Editable=No }
    { 480 ;   ;Dimension Set ID    ;Integer       ;TableRelation="Dimension Set Entry";
                                                   CaptionML=[ENU=Dimension Set ID;
                                                              NOR=Dimensjonssett-ID;
                                                              SVE=DimensionsuppsÑttnings-ID];
                                                   Editable=No }
    { 8002;   ;Job Id              ;GUID          ;CaptionML=[ENU=Job Id;
                                                              NOR=Jobb-ID] }
  }
  KEYS
  {
    {    ;Time Sheet No.,Line No.                 ;Clustered=Yes }
    {    ;Type                                     }
    {    ;Time Sheet No.,Status,Posted             }
    {    ;Type,Job No.                             }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ResourcesSetup@1005 : Record 314;
      Job@1001 : Record 11072003;
      JobTask@1000 : Record 11072600;
      TimeSheetHeader@1004 : Record 950;
      TimeSheetDetail@1003 : Record 952;
      Text001@1002 : TextConst 'ENU=There is no employee linked with resource %1.;NOR=Det finnes ingen ansatt tilknyttet ressursen %1.;SVE=Det finns ingen anstÑlld kopplad med resursen %1.';
      Text002@1006 : TextConst 'ENU="Status must be Open or Rejected in line with Time Sheet No.=''%1'', Line No.=''%2''.";NOR="Statusen mÜ vëre èpen eller Avvist i linjen med timelistenr. = %1, linjenr. = %2.";SVE="Status mÜste vara ôppen eller Avslagen pÜ raden med Tidrapportsnr=%1, Radnr=%2."';
      Text003@1007 : TextConst 'ENU=Service order %1 for customer %2;NOR=Serviceordre %1 for kunde %2;SVE=Serviceorder %1 fîr kund %2';
      Text005@1009 : TextConst 'ENU=Select a type before you enter an activity.;NOR=Velg en type fõr du angir en aktivitet.;SVE=VÑlj en typ innan du anger en aktivitet.';
      PrivacyBlockedErr@1008 : TextConst '@@@="%1=resource no.";ENU=You cannot use resource %1 because they are marked as blocked due to privacy.;NOR=Du kan ikkeˇbruke ressurs %1 fordi den er merket som sperret pÜ grunn av personvern.';

    [External]
    PROCEDURE TestStatus@3();
    BEGIN
      IF NOT (Status IN [Status::Open,Status::Rejected]) THEN
        ERROR(
          Text002,
          "Time Sheet No.",
          "Line No.");
    END;

    LOCAL PROCEDURE UpdateDetails@1();
    VAR
      TimeSheetDetail@1000 : Record 952;
    BEGIN
      TimeSheetDetail.SETRANGE("Time Sheet No.","Time Sheet No.");
      TimeSheetDetail.SETRANGE("Time Sheet Line No.","Line No.");
      IF TimeSheetDetail.FINDSET(TRUE) THEN
        REPEAT
          TimeSheetDetail.CopyFromTimeSheetLine(Rec);
          TimeSheetDetail.MODIFY;
        UNTIL TimeSheetDetail.NEXT = 0;
    END;

    LOCAL PROCEDURE GetTimeSheetResource@9(VAR Resource@1000 : Record 156);
    BEGIN
      TimeSheetHeader.GET("Time Sheet No.");
      Resource.GET(TimeSheetHeader."Resource No.");
    END;

    LOCAL PROCEDURE GetJobApproverID@6() : Code[50];
    VAR
      Resource@1000 : Record 156;
    BEGIN
      Job.GET("Job No.");
      Job.TESTFIELD("Person Responsible");
      Resource.GET(Job."Person Responsible");
      Resource.TESTFIELD("Time Sheet Owner User ID");
      EXIT(Resource."Time Sheet Owner User ID");
    END;

    [External]
    PROCEDURE UpdateApproverID@2();
    VAR
      Resource@1001 : Record 156;
    BEGIN
      ResourcesSetup.GET;
      GetTimeSheetResource(Resource);
      IF (Type = Type::Job) AND ("Job No." <> '') AND
         (((Resource.Type = Resource.Type::Person) AND
           (ResourcesSetup."Time Sheet by Job Approval" = ResourcesSetup."Time Sheet by Job Approval"::Always)) OR
          ((Resource.Type = Resource.Type::Machine) AND
           (ResourcesSetup."Time Sheet by Job Approval" IN [ResourcesSetup."Time Sheet by Job Approval"::"Machine Only",
                                                            ResourcesSetup."Time Sheet by Job Approval"::Always])))
      THEN
        "Approver ID" := GetJobApproverID
      ELSE BEGIN
        Resource.TESTFIELD("Time Sheet Approver User ID");
        "Approver ID" := Resource."Time Sheet Approver User ID";
      END;
    END;

    LOCAL PROCEDURE CheckWorkType@5();
    VAR
      Resource@1001 : Record 156;
      WorkType@1000 : Record 200;
    BEGIN
      IF WorkType.GET("Work Type Code") THEN BEGIN
        GetTimeSheetResource(Resource);
        WorkType.TESTFIELD("Unit of Measure Code",Resource."Base Unit of Measure");
      END;
    END;

    [External]
    PROCEDURE ShowLineDetails@4(ManagerRole@1004 : Boolean);
    VAR
      TimeSheetLineResDetail@1000 : Page 965;
      TimeSheetLineJobDetail@1003 : Page 966;
      TimeSheetLineServiceDetail@1005 : Page 967;
      TimeSheetLineAssembDetail@1006 : Page 968;
      TimeSheetLineAbsenceDetail@1007 : Page 969;
    BEGIN
      CASE Type OF
        Type::Resource:
          BEGIN
            TimeSheetLineResDetail.SetParameters(Rec,ManagerRole);
            IF TimeSheetLineResDetail.RUNMODAL = ACTION::OK THEN
              TimeSheetLineResDetail.GETRECORD(Rec);
          END;
        Type::Job:
          BEGIN
            TimeSheetLineJobDetail.SetParameters(Rec,ManagerRole);
            IF TimeSheetLineJobDetail.RUNMODAL = ACTION::OK THEN
              TimeSheetLineJobDetail.GETRECORD(Rec);
          END;
        Type::Absence:
          BEGIN
            TimeSheetLineAbsenceDetail.SetParameters(Rec,ManagerRole);
            IF TimeSheetLineAbsenceDetail.RUNMODAL = ACTION::OK THEN
              TimeSheetLineAbsenceDetail.GETRECORD(Rec);
          END;
        Type::Service:
          BEGIN
            TimeSheetLineServiceDetail.SetParameters(Rec,ManagerRole);
            IF TimeSheetLineServiceDetail.RUNMODAL = ACTION::OK THEN
              TimeSheetLineServiceDetail.GETRECORD(Rec);
          END;
        Type::"Assembly Order":
          BEGIN
            TimeSheetLineAssembDetail.SetParameters(Rec);
            IF TimeSheetLineAssembDetail.RUNMODAL = ACTION::OK THEN
              TimeSheetLineAssembDetail.GETRECORD(Rec);
          END;
        ELSE
          ERROR(Text005);
      END;
      MODIFY;
    END;

    [External]
    PROCEDURE GetAllowEdit@8(FldNo@1001 : Integer;ManagerRole@1000 : Boolean) : Boolean;
    BEGIN
      IF ManagerRole THEN
        EXIT((FldNo IN [FIELDNO("Work Type Code"),FIELDNO(Chargeable)]) AND (Status = Status::Submitted));

      EXIT(Status IN [Status::Open,Status::Rejected]);
    END;

    LOCAL PROCEDURE CheckResourcePrivacyBlocked@7(Resource@1000 : Record 156);
    BEGIN
      IF Resource."Privacy Blocked" THEN
        ERROR(PrivacyBlockedErr,Resource."No.");
    END;

    PROCEDURE UpdateJobId@10();
    VAR
      Job@1000 : Record 11072003;
    BEGIN
      IF "Job No." = '' THEN BEGIN
        CLEAR("Job Id");
        EXIT;
      END;

      IF NOT Job.GET("Job No.") THEN
        EXIT;

      "Job Id" := Job.Id;
    END;

    BEGIN
    {
      4PS, Objects renumbered
    }
    END.
  }
}

