OBJECT Table 11012512 Option Surcharge
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CheckOptionStatus();
               CheckBudgetRate;
               UpdateBudgSurchAndOptionPrice(DataTrigger::Insert);
             END;

    OnModify=BEGIN
               CheckOptionStatus();
               CheckBudgetRate;
               UpdateBudgSurchAndOptionPrice(DataTrigger::Modify);
             END;

    OnDelete=BEGIN
               CheckOptionStatus();
               UpdateBudgSurchAndOptionPrice(DataTrigger::Delete);
             END;

    CaptionML=[ENU=Option Surcharges;
               NOR=Alternativtilleggsgebyrer;
               SVE=Alternativtill„ggsavgifter];
    DrillDownPageID=Page11012539;
  }
  FIELDS
  {
    { 20  ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr] }
    { 30  ;   ;House Model         ;Code20        ;TableRelation="House Model".Model WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=[ENU=House Model;
                                                              NOR=Husmodell;
                                                              SVE=Husmodell] }
    { 40  ;   ;Main Group          ;Code20        ;TableRelation="Option Main Group".Code WHERE (Project No.=FIELD(Project No.),
                                                                                                 Plot No.=FIELD(Plot No.));
                                                   CaptionML=[ENU=Main Group;
                                                              NOR=Hovedgruppe;
                                                              SVE=Huvudgrupp] }
    { 50  ;   ;Group               ;Code20        ;TableRelation="Option Group".Code WHERE (Project No.=FIELD(Project No.),
                                                                                            Plot No.=FIELD(Plot No.));
                                                   CaptionML=[ENU=Group;
                                                              NOR=Gruppe;
                                                              SVE=Grupp] }
    { 60  ;   ;Sub Group           ;Code20        ;TableRelation="Option Sub Group".Code WHERE (Project No.=FIELD(Project No.),
                                                                                                Plot No.=FIELD(Plot No.));
                                                   CaptionML=[ENU=Sub Group;
                                                              NOR=Undergruppe;
                                                              SVE=Undergrupp] }
    { 70  ;   ;Plot No.            ;Code10        ;TableRelation=Plot."Plot No." WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=[ENU=Plot No.;
                                                              NOR=Tegningnr;
                                                              SVE=Ritningnr] }
    { 80  ;   ;Option              ;Code20        ;CaptionML=[ENU=Option;
                                                              NOR=Alternativ;
                                                              SVE=Alternativ] }
    { 90  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 100 ;   ;Cost Object         ;Code20        ;TableRelation="Dimension Value".Code;
                                                   OnValidate=BEGIN
                                                                IF ("Cost Object" <> xRec."Cost Object") OR (Description = '') THEN BEGIN
                                                                  DimMgtCU.GetDimValueRec(2, "Cost Object", DimValRec, TRUE, "Project No.");
                                                                  Description := DimValRec.Name;
                                                                END;

                                                                VALIDATE("Cost Type");
                                                                VALIDATE("Rate Code");
                                                              END;

                                                   CaptionML=[ENU=Global Dimension 2 Code;
                                                              NOR=Kostnadsobjekt;
                                                              SVE=Kostnadsobjekt];
                                                   NotBlank=Yes;
                                                   CaptionClass='1,1,2' }
    { 101 ;   ;Cost Type           ;Option        ;InitValue=Sundry;
                                                   OnValidate=VAR
                                                                lvDimValRec@1100409001 : Record 349;
                                                                lvDimMgtCU@1100409000 : Codeunit 408;
                                                              BEGIN
                                                                //Call C006736 sn DP00559
                                                                IF "Cost Object" = '' THEN BEGIN
                                                                  "Cost Type" := "Cost Type"::Sundry;
                                                                END ELSE BEGIN
                                                                  lvDimMgtCU.GetDimValueRec(2, "Cost Object", lvDimValRec, FALSE,'');
                                                                  "Cost Type" := lvDimValRec."Cost Type";
                                                                END;
                                                                //Call C006736 en DP00559

                                                                //IF CheckCostbjectIsLabor("Cost Object") THEN
                                                                //  "Cost Type" := "Cost Type"::Labor
                                                                //ELSE
                                                                //  "Cost Type" := "Cost Type"::Sundry;
                                                              END;

                                                   CaptionML=[ENU=Cost Type;
                                                              NOR=Kostnadstype;
                                                              SVE=Kostnadstyp];
                                                   OptionCaptionML=[ENU=Labor,Material,Subcontracting,Plant,Sundry;
                                                                    NOR=Arbeidskraft,Materiale,Underentrepen›r,Maskin,Diverse;
                                                                    SVE=Arbetskraft,Material,Underentreprenad,Maskin,Diverse];
                                                   OptionString=Labor,Material,Subcontracting,Plant,Sundry;
                                                   Description=Only Labor and Sundry allowed, see field 'Cost Object' --> Overruled bij Call C006736;
                                                   Editable=No }
    { 110 ;   ;Rate Code           ;Code10        ;TableRelation="Budget Rate" WHERE (Project Filter=FIELD(Project No.));
                                                   OnValidate=BEGIN
                                                                IF ("Rate Code" = '') THEN
                                                                  EXIT;

                                                                CheckBudgetRate;
                                                              END;

                                                   CaptionML=[ENU=Rate Code;
                                                              NOR=Avgiftskode;
                                                              SVE=Avgiftskod] }
    { 120 ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 130 ;   ;Surcharge %         ;Decimal       ;CaptionML=[ENU=Surcharge %;
                                                              NOR=Tilleggsgebyrsprosent;
                                                              SVE=Till„ggsavgiftsprocent] }
    { 140 ;   ;Surcharge Amount    ;Decimal       ;FieldClass=Normal;
                                                   CaptionML=[ENU=Surcharge Amount;
                                                              NOR=Tilleggsgebyrsbel›p;
                                                              SVE=Till„ggsbelopp] }
    { 150 ;   ;Budget Amount       ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Option Budget".Amount WHERE (Project No.=FIELD(Project No.),
                                                                                                 Plot No.=FIELD(Plot No.),
                                                                                                 House Model=FIELD(House Model),
                                                                                                 Main Group=FIELD(Main Group),
                                                                                                 Group=FIELD(Group),
                                                                                                 Sub Group=FIELD(Sub Group),
                                                                                                 Option=FIELD(Option)));
                                                   CaptionML=[ENU=Budget Amount;
                                                              NOR=Budsjettbel›p;
                                                              SVE=Budgetbelopp];
                                                   Editable=No }
    { 160 ;   ;Total Surcharge Amount;Decimal     ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Option Surcharge"."Surcharge Amount" WHERE (Project No.=FIELD(Project No.),
                                                                                                                House Model=FIELD(House Model),
                                                                                                                Main Group=FIELD(Main Group),
                                                                                                                Group=FIELD(Group),
                                                                                                                Sub Group=FIELD(Sub Group),
                                                                                                                Plot No.=FIELD(Plot No.),
                                                                                                                Option=FIELD(Option)));
                                                   CaptionML=[ENU=Total Surcharge Amount;
                                                              NOR=Tilleggsgebyr Totalt;
                                                              SVE=Till„ggsbelopp totalt];
                                                   Editable=No }
    { 180 ;   ;Element             ;Code20        ;TableRelation=IF (Project No.=FILTER(<>'')) "Project Element".Element WHERE (Project No.=FIELD(Project No.))
                                                                 ELSE IF (Project No.=FILTER('')) "Base Element".Code;
                                                   OnValidate=BEGIN
                                                                ElementFormat.FormatElem(Element,2,'');
                                                                IF Element <> '' THEN BEGIN
                                                                  IF "Project No." = '' THEN BEGIN
                                                                    ElemRec.GET(Element);
                                                                    Chapter := ElemRec.Chapter;
                                                                    Paragraph := ElemRec.Paragraph;
                                                                  END ELSE BEGIN
                                                                    ProjElemRec.GET("Project No.",Element);
                                                                    Chapter := ProjElemRec.Chapter;
                                                                    Paragraph := ProjElemRec.Paragraph;
                                                                  END;
                                                                END ELSE BEGIN
                                                                  Chapter := '';
                                                                  Paragraph := '';
                                                                END;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Element;
                                                              NOR=Element;
                                                              SVE=Element] }
    { 181 ;   ;Chapter             ;Code20        ;CaptionML=[ENU=Chapter;
                                                              NOR=Kapittel;
                                                              SVE=Kapitel];
                                                   Editable=No }
    { 182 ;   ;Paragraph           ;Code20        ;CaptionML=[ENU=Paragraph;
                                                              NOR=Avsnitt;
                                                              SVE=Stycke];
                                                   Editable=No }
    { 190 ;   ;Fixed Amount        ;Boolean       ;CaptionML=[ENU=Fixed Amount;
                                                              NOR=Fast bel›p;
                                                              SVE=Fast belopp] }
  }
  KEYS
  {
    {    ;Project No.,House Model,Main Group,Group,Sub Group,Plot No.,Option,Line No.;
                                                   SumIndexFields=Surcharge Amount;
                                                   MaintainSIFTIndex=No;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DimValRec@1210190003 : Record 349;
      ElemRec@1210190006 : Record 11012060;
      ProjElemRec@1210190007 : Record 11012010;
      RateRec@1210190004 : Record 11012000;
      DimMgtCU@1210190002 : Codeunit 408;
      BuyerMgtCU@1210190000 : Codeunit 11012500;
      ElementFormat@1210190005 : Codeunit 11012022;
      DataTrigger@1210190001 : 'Insert,Modify,Delete';

    PROCEDURE CheckCostbjectIsLabor@1210190000(ICostObject@1210190000 : Code[20]) : Boolean;
    VAR
      lvDimValRec@1210190001 : Record 349;
      lvDimMgtCU@1210190002 : Codeunit 408;
    BEGIN
      IF ICostObject = '' THEN
        EXIT(FALSE);

      lvDimMgtCU.GetDimValueRec(2, ICostObject, lvDimValRec, FALSE,'');
      IF lvDimValRec."Cost Type" = lvDimValRec."Cost Type"::Labor THEN
        EXIT(TRUE);

      EXIT(FALSE);
    END;

    PROCEDURE UpdateBudgSurchAndOptionPrice@6(Trigger@1210190000 : 'Insert,Modify,Delete');
    BEGIN
      CASE Trigger OF
        Trigger::Insert: INSERT;
        Trigger::Modify: MODIFY;
        Trigger::Delete: DELETE;
      END;

      BuyerMgtCU.BmCalcSurchargesUpdateOption(
        "Project No.", "Plot No.", "House Model", "Main Group", Group, "Sub Group", Option, TRUE, Rec);

      CASE Trigger OF
        Trigger::Insert: DELETE;
        Trigger::Delete: INSERT;
      END;
    END;

    PROCEDURE CheckBudgetRate@1100485000();
    BEGIN
      IF "Cost Type" <> "Cost Type"::Labor THEN BEGIN
        "Rate Code" := '';
        EXIT;
      END;

      IF ("Surcharge %" <> 0) THEN
        EXIT;

      TESTFIELD("Rate Code");
      RateRec.SETRANGE("Starting Date", 0D, TODAY);
      RateRec.SETFILTER("Ending Date",'%1|>=%2',0D, TODAY);
      RateRec.FINDLAST;
      RateRec.TESTFIELD(Rate);
    END;

    PROCEDURE CheckOptionStatus@1100525000();
    VAR
      lvOptionRec@1100525000 : Record 11012502;
      lvText000@1100525001 : TextConst 'ENU=has the status %1'', modify option surcharges not allowed;NOR=har status %1'', endre alternativtilleggsgebyrer er ikke tillatt;SVE=har status %1'', att „ndra alternativtill„ggsavgifter „r inte till†tet';
    BEGIN
      IF ("Project No." = '') OR ("Plot No." = '') OR (Option = '') THEN
        EXIT;

      IF lvOptionRec.GET("Project No.","Plot No.","House Model","Main Group",Group,"Sub Group",Option) THEN BEGIN
        IF (lvOptionRec.Status >= lvOptionRec.Status::Order) THEN BEGIN
          FIELDERROR(Option, STRSUBSTNO(lvText000, lvOptionRec.Status));
        END;
      END;
    END;

    BEGIN
    END.
  }
}

