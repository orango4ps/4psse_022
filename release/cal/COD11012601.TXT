OBJECT Codeunit 11012601 UAR Hour Acc.-Check Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1210190000 : TextConst 'DEU=darf kein Ultimodatum sein;ENU=cannot be a closing date;NLD=mag geen ultimodatum zijn;NOR=kan ikke vëre en avslutningsdato;SVE=kan inte vara ett avslutsdatum';
      HrsSetupRec@1210190008 : Record 11012600;
      ProjSetupRec@1210190011 : Record 315;
      GLSetupRec@1210190002 : Record 98;
      UserSetupRec@1210190001 : Record 91;
      Text001@1210190003 : TextConst 'DEU=liegt nicht innerhalb des zugelassenen Buchungszeitraums.;ENU=is not within your range of allowed posting dates;NLD=is geen geldige boekingsdatum. De datum valt buiten het toegestane bereik.;NOR=er ikke innenfor tillatte bokfõringsdatoer;SVE=Ñr inte i ditt tillÜtna intervall fîr bokfîringsdatum';
      Text002@1210190004 : TextConst 'DEU=darf nicht %1 ''%2'' aus %3 sein.;ENU=may not be %1 ''%2'' from the %3;NLD=mag niet %1 ''%2'' uit de %3 zijn;NOR=kan ikke vëre %1 ''%2'' fra %3;SVE=kan inte vara %1 ''%2'' frÜn %3';
      ProjRec@1210190012 : Record 11072003;
      ProjElemRec@1100485000 : Record 11012010;
      ServOrderRec@1100485001 : Record 11012823;
      ProjectType@1100529600 : Record 11012009;
      ServiceType@1100529602 : Record 11012814;
      HourAccFunctionsCU@1210190010 : Codeunit 11012607;
      AllowPostingFrom@1210190006 : Date;
      AllowPostingTo@1210190005 : Date;
      Text003@1210190007 : TextConst 'DEU=Anfangs- und Endzeit mÅssen beide eingegeben wrden, in Zeile %1, Woche %2-%3, Mitarbeiter ''%4''.;ENU=Starting and ending time must be entered, in line %1, week %2-%3, employee ''%4''.;NLD=Begin- en eindtijd moeten beide worden ingegeven, in regel %1, week %2-%3, werknemer ''%4''.;SVE=Start- och sluttid mÜste anges, pÜ rad %1, vecka %2-%3, anstÑlld ''%4''.';
      Text004@1210190009 : TextConst 'DEU=Fahrzeit nicht zulÑssig fÅr UAR-TÑtigkeit ''%1'' von Projekt ''%2'', in Woche %3-%4, Mitarbeiter ''%5'', Zeile %6.;ENU=Travel hours not allowed for UAR Task ''%1''  of project ''%2'', in week %3-%4, employee ''%5'', line %6.;NLD=Reisuren niet toegestaan voor UAR-taak ''%1''  van project ''%2'', in week %3-%4, werknemer ''%5''. regel %6.;SVE=Restimmar inte tillÜtet fîr UAR-uppgift ''%1'' fîr projekt ''%2'', i veckan %3-%4, anstÑlld ''%5'', rad %6.';
      Text005@1210190013 : TextConst 'DEU=Fahrzeit nicht zulÑssig in Kombination mit Arbeitszeit, in Woche %2-%3, Mitarbeiter ''%4'', Zeile %5, Projekt ''%1'', Serviceauftrag ''%6''.;ENU=Travel hours only allowed in combination with working hours, in week %2-%3, employee ''%4'', line %5, project ''%1'', service order ''%6''.;NLD=Reisuren alleen toegestaan in combinatie met werkuren, in week %2-%3, werknemer ''%4''. regel %5, project ''%1'', serviceorder ''%6''.;SVE=Restimmar Ñr endast tillÜtet i kombination med arbetstid, i veckan %2-%3, anstÑlld ''%4'', rad %5, projekt ''%1'', serviceorder ''%6''.';
      Text006@1210190014 : TextConst 'DEU=Pause nicht gestattet fÅr UAR-Aufgabe ''%1'' von Projekt ''%2'', in Woche %3-%4, Mitarbeiter ''%5'', Zeile %6.;ENU=Pause not allowed for UAR Task ''%1''  of project ''%2'', in week %3-%4, employee ''%5'', line %6.;NLD=Pauze niet toegestaan voor UAR-taak ''%1'' van project ''%2'', in week %3-%4, werknemer ''%5''. regel %6.;SVE=Paus inte tillÜtet fîr UAR-uppgift ''%1'' fîr projekt ''%2'', i veckan %3-%4, anstÑlld ''%5'', rad %6.';
      Text007@1210190015 : TextConst 'DEU=Anfangs- und Endzeit der Pause mÅssen beide fÅr Stundenzeile %1, Woche %2-%3, Mitarbeiter ''%4'' eingegeben werden.;ENU=Starting and ending time of the pause must be entered for hour line %1, week %2-%3, employee ''%4''.;NLD=Begin- en eindtijd van de pauze moeten beide worden ingegeven bij urenregel %1, week %2-%3, werknemer ''%4''.;SVE=Start- och sluttid fîr pausen mÜste anges fîr timrad %1, vecka %2-%3, anstÑlld ''%4''.';

    PROCEDURE RunCheck@1(VAR HourAccRec@1000 : Record 11012609;VAR HourAccLineRec@1210190000 : Record 11012610);
    VAR
      lvHourAccPauseRec@1210190001 : Record 11012611;
      CostComponent@1100529600 : Record 11012012;
      CostComponentsbyEmployee@1100529601 : Record 11012606;
    BEGIN
      IF HourAccLineRec.EmptyLine THEN
        EXIT;

      WITH HourAccRec DO BEGIN
        TESTFIELD(Year);
        TESTFIELD(Week);
        TESTFIELD("Document No.");
        TESTFIELD("Posting Date");
        TESTFIELD("Employee No.");
        CALCFIELDS("Company No. Paymaster", "Administration No. Paymaster");
        IF NOT External THEN BEGIN
          TESTFIELD("Company No. Paymaster");
          TESTFIELD("Administration No. Paymaster");
        END ELSE BEGIN
          TESTFIELD(HourAccRec.Employer);
        END;

        IF "Posting Date" <> NORMALDATE("Posting Date") THEN
          FIELDERROR("Posting Date",Text000);

        IF (AllowPostingFrom = 0D) AND (AllowPostingTo = 0D) THEN BEGIN
          HrsSetupRec.GET;
          HrsSetupRec.TESTFIELD("UAR Task Worked Hours");
          IF USERID <> '' THEN
            IF UserSetupRec.GET(USERID) THEN BEGIN
              AllowPostingFrom := UserSetupRec."Allow Posting From";
              AllowPostingTo := UserSetupRec."Allow Posting To";
            END;
          IF (AllowPostingFrom = 0D) AND (AllowPostingTo = 0D) THEN BEGIN
            GLSetupRec.GET;
            AllowPostingFrom := GLSetupRec."Allow Posting From";
            AllowPostingTo := GLSetupRec."Allow Posting To";
          END;
          IF AllowPostingTo = 0D THEN
            AllowPostingTo := 99991231D;
        END;
        IF ("Posting Date" < AllowPostingFrom) OR ("Posting Date" > AllowPostingTo) THEN
          FIELDERROR("Posting Date",Text001);
      END;

      WITH HourAccLineRec DO BEGIN
        IF "Receiving Company" <> '' THEN BEGIN
          ProjSetupRec.CHANGECOMPANY("Receiving Company");
          ProjRec.CHANGECOMPANY("Receiving Company");
          ProjectType.CHANGECOMPANY("Receiving Company");
          ProjElemRec.CHANGECOMPANY("Receiving Company");
          ServOrderRec.CHANGECOMPANY("Receiving Company");
          ServiceType.CHANGECOMPANY("Receiving Company");
        END ELSE BEGIN
          ProjSetupRec.CHANGECOMPANY(COMPANYNAME);
          ProjRec.CHANGECOMPANY(COMPANYNAME);
          ProjectType.CHANGECOMPANY(COMPANYNAME);
          ProjElemRec.CHANGECOMPANY(COMPANYNAME);
          ServOrderRec.CHANGECOMPANY(COMPANYNAME);
          ServiceType.CHANGECOMPANY(COMPANYNAME);
        END;
        ProjSetupRec.GET;

        TESTFIELD("Cost Component");
        CostComponent.GET("Cost Component");
        CostComponentsbyEmployee.GET("Employee No.", "Cost Component");

        IF "Project No." <> '' THEN BEGIN
          ProjRec.GET("Project No.");
          //IF (ProjSetupRec."Posting Element Mandatory") THEN     //**4PS01.o
          IF ProjRec."Posting Element Mandatory" THEN            //**4PS01.n
            TESTFIELD(Element);
          IF ProjSetupRec."Provisions at Closure" THEN
            ProjRec.CheckProjectStatus(-1)
          ELSE
            ProjRec.CheckProjectStatus(1);
          ProjRec.TESTFIELD("Project Type");
          ProjectType.GET(ProjRec."Project Type");
          IF Element <> '' THEN BEGIN //Call 6564
            ProjElemRec.GET("Project No.", Element);
            ProjElemRec.CheckProjectElementBlocked;
          END;
          ProjRec.TESTFIELD("UAR Task");
          ProjRec.CALCFIELDS("UAR Task Pause Allowed", "UAR Task Travel Hours Allowed");
        END;

        CASE Type OF
          Type::Project:
            BEGIN
              TESTFIELD("Project No.");
              TESTFIELD("Service Order No.", '');
              IF (ProjSetupRec."Provisions at Closure" = TRUE) AND
                 (ProjRec."Project Status" >= ProjRec."Project Status"::Finished)
              THEN
                ProjectType.TESTFIELD("Provision Account Labor")
              ELSE
                ProjectType.TESTFIELD("WIP Account Labor");
              ProjRec.TESTFIELD("Global Dimension 1 Code");
            END;
          Type::Service:
            BEGIN
              TESTFIELD("Service Order No.");
              ServOrderRec.GET("Service Order No.");
              ServOrderRec.CheckStatus(1);
              IF ServOrderRec.IsServiceContractMandatory THEN
                TESTFIELD("Service Contract No.");
              ServOrderRec.TestServiceContractMandatory;
              IF "Additional Cost (Service)" THEN BEGIN
                ServOrderRec.TESTFIELD("Service Type (Other)");
                ServiceType.GET(ServOrderRec."Service Type (Other)");
              END ELSE BEGIN
                ServOrderRec.TESTFIELD("Service Type");
                ServiceType.GET(ServOrderRec."Service Type");
              END;
              ServiceType.TESTFIELD("WIP Account Labor");
              ServOrderRec.TESTFIELD("Global Dimension 1 Code");
              IF "Project No." = '' THEN BEGIN
                ProjRec."UAR Task" := HrsSetupRec."UAR Task Worked Hours";
                ProjRec."UAR Task Pause Allowed" := TRUE;
                ProjRec."UAR Task Travel Hours Allowed" := TRUE;
                ProjRec."UAR Indirect" := FALSE;
              END;
            END;
        END;

        IF ("Cost Component" = HrsSetupRec."Cost Component Pause") THEN BEGIN
          FIELDERROR("Cost Component", STRSUBSTNO(Text002, HrsSetupRec.FIELDCAPTION("Cost Component Pause"),
            HrsSetupRec."Cost Component Pause", HrsSetupRec.TABLECAPTION));
        END;

        IF (("Monday From" <> 0T) AND ("Monday To" = 0T)) OR
           (("Monday From" = 0T) AND ("Monday To" <> 0T)) OR
           (("Tuesday From" <> 0T) AND ("Tuesday To" = 0T)) OR
           (("Tuesday From" = 0T) AND ("Tuesday To" <> 0T)) OR
           (("Wednesday From" <> 0T) AND ("Wednesday To" = 0T)) OR
           (("Wednesday From" = 0T) AND ("Wednesday To" <> 0T)) OR
           (("Thursday From" <> 0T) AND ("Thursday To" = 0T)) OR
           (("Thursday From" = 0T) AND ("Thursday To" <> 0T)) OR
           (("Friday From" <> 0T) AND ("Friday To" = 0T)) OR
           (("Friday From" = 0T) AND ("Friday To" <> 0T)) OR
           (("Saturday From" <> 0T) AND ("Saturday To" = 0T)) OR
           (("Saturday From" = 0T) AND ("Saturday To" <> 0T)) OR
           (("Sunday From" <> 0T) AND ("Sunday To" = 0T)) OR
           (("Sunday From" = 0T) AND ("Sunday To" <> 0T))
        THEN
          ERROR(Text003, "Line No.", Year, Week, "Employee No.");

         IF (NOT ProjRec."UAR Task Travel Hours Allowed") AND
            (("Monday Travel To" <> 0) OR ("Monday Travel Back" <> 0)  OR
             ("Tuesday Travel To" <> 0) OR ("Tuesday Travel Back" <> 0) OR
             ("Wednesday Travel To" <> 0) OR ("Wednesday Travel Back" <> 0) OR
             ("Thursday Travel To" <> 0) OR ("Thursday Travel Back" <> 0) OR
             ("Friday Travel To" <> 0) OR ("Friday Travel Back" <> 0) OR
             ("Saturday Travel To" <> 0) OR ("Saturday Travel Back" <> 0) OR
             ("Sunday Travel To" <> 0) OR ("Sunday Travel Back" <> 0) )
          THEN
            ERROR(Text004, ProjRec."UAR Task", "Project No.", Year, Week, "Employee No.", "Line No.");

        IF ( (("Monday From" = 0T) AND ("Monday To" = 0T)) AND
             (("Monday Travel To" <> 0) OR ("Monday Travel Back" <> 0)) ) OR
           ( (("Tuesday From" = 0T) AND ("Tuesday To" = 0T)) AND
             (("Tuesday Travel To" <> 0) OR ("Tuesday Travel Back" <> 0)) ) OR
           ( (("Wednesday From" = 0T) AND ("Wednesday To" = 0T)) AND
             (("Wednesday Travel To" <> 0) OR ("Wednesday Travel Back" <> 0)) ) OR
           ( (("Thursday From" = 0T) AND ("Thursday To" = 0T)) AND
             (("Thursday Travel To" <> 0) OR ("Thursday Travel Back" <> 0)) ) OR
           ( (("Friday From" = 0T) AND ("Friday To" = 0T)) AND
             (("Friday Travel To" <> 0) OR ("Friday Travel Back" <> 0)) ) OR
           ( (("Saturday From" = 0T) AND ("Saturday To" = 0T)) AND
             (("Saturday Travel To" <> 0) OR ("Saturday Travel Back" <> 0)) ) OR
           ( (("Sunday From" = 0T) AND ("Sunday To" = 0T)) AND
             (("Sunday Travel To" <> 0) OR ("Sunday Travel Back" <> 0)) )
          THEN
            ERROR(Text005, "Project No.", Year, Week, "Employee No.", "Line No.", "Service Order No.");
      END;

      WITH lvHourAccPauseRec DO BEGIN
        SETRANGE(Year, HourAccLineRec.Year);
        SETRANGE(Week, HourAccLineRec.Week);
        SETRANGE("Employee No.", HourAccLineRec."Employee No.");
        SETRANGE("Hour Acc. Line No.", HourAccLineRec."Line No.");
        IF FIND('-') THEN BEGIN
          REPEAT
            IF NOT EmptyLine THEN BEGIN
              IF (NOT ProjRec."UAR Task Pause Allowed") THEN
                ERROR(Text006, ProjRec."UAR Task", HourAccLineRec."Project No.", Year, Week, "Employee No.", "Line No.");

              IF (("Monday From" <> 0T) AND ("Monday To" = 0T)) OR
                 (("Monday From" = 0T) AND ("Monday To" <> 0T)) OR
                 (("Tuesday From" <> 0T) AND ("Tuesday To" = 0T)) OR
                 (("Tuesday From" = 0T) AND ("Tuesday To" <> 0T)) OR
                 (("Wednesday From" <> 0T) AND ("Wednesday To" = 0T)) OR
                 (("Wednesday From" = 0T) AND ("Wednesday To" <> 0T)) OR
                 (("Thursday From" <> 0T) AND ("Thursday To" = 0T)) OR
                 (("Thursday From" = 0T) AND ("Thursday To" <> 0T)) OR
                 (("Friday From" <> 0T) AND ("Friday To" = 0T)) OR
                 (("Friday From" = 0T) AND ("Friday To" <> 0T)) OR
                 (("Saturday From" <> 0T) AND ("Saturday To" = 0T)) OR
                 (("Saturday From" = 0T) AND ("Saturday To" <> 0T)) OR
                 (("Sunday From" <> 0T) AND ("Sunday To" = 0T)) OR
                 (("Sunday From" = 0T) AND ("Sunday To" <> 0T))
              THEN
                ERROR(Text007, "Hour Acc. Line No.", Year, Week, "Employee No.");

              HourAccFunctionsCU.CheckPauseDuringWorktime(1, lvHourAccPauseRec);
              HourAccFunctionsCU.CheckPauseDuringWorktime(2, lvHourAccPauseRec);
              HourAccFunctionsCU.CheckPauseDuringWorktime(3, lvHourAccPauseRec);
              HourAccFunctionsCU.CheckPauseDuringWorktime(4, lvHourAccPauseRec);
              HourAccFunctionsCU.CheckPauseDuringWorktime(5, lvHourAccPauseRec);
              HourAccFunctionsCU.CheckPauseDuringWorktime(6, lvHourAccPauseRec);
              HourAccFunctionsCU.CheckPauseDuringWorktime(7, lvHourAccPauseRec);
            END;
          UNTIL NEXT = 0;
        END;
      END;
    END;

    BEGIN
    {
      4PS01 JD, 8 aug 2007, Call 5990, "Boeken element veplicht" ook op project kaart
    }
    END.
  }
}

