OBJECT Report 11012340 Copy Estimate Recipe
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Copy Estimate Recipe;
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 4767;    ;DataItem;SourceEstimateLine  ;
               DataItemTable=Table11072072;
               DataItemTableView=SORTING(Estimate No.,Sub-Estimate No.,Part Group,Part,Recipe Group,Recipe)
                                 WHERE(Recipe=FILTER(<>''));
               OnPreDataItem=BEGIN
                               //C042683
                               IF EstFrom <> '' THEN BEGIN
                                 SourceEstimateLine.SETRANGE("Estimate No.", EstFrom);
                                 SourceEstimateLine.SETRANGE("Sub-Estimate No.", SubEstFrom);
                                 SourceEstimateLine.SETRANGE("Part Group", PartGrpFrom);
                                 SourceEstimateLine.SETRANGE(Part, EstPartFrom);
                                 SourceEstimateLine.SETRANGE("Line No.", LineFrom);
                               END ELSE BEGIN
                                 CurrReport.BREAK;
                               END;
                             END;

               OnAfterGetRecord=BEGIN
                                  StartUpdate;  //C042683
                                END;
                                 }

    { 1100525000;;DataItem;SourceRecipe      ;
               DataItemTable=Table11020581;
               DataItemTableView=SORTING(Recipe Group,No.);
               OnPreDataItem=BEGIN
                               //C042683
                               IF EstFrom = '' THEN BEGIN
                                 SourceRecipe.SETRANGE("Recipe Group", RecipeGrpFrom);
                                 SourceRecipe.SETRANGE("No.", RecipeFrom);
                               END ELSE BEGIN
                                 CurrReport.BREAK;
                               END;
                             END;

               OnAfterGetRecord=BEGIN
                                  StartUpdate;  //C042683
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100528906;;Container;
                  ContainerType=ContentArea }

      { 1100528905;1;Group  ;
                  GroupType=Group }

      { 1100525012;2;Field  ;
                  CaptionClass=Text998;
                  Style=Strong;
                  StyleExpr=TRUE }

      { 1100525011;2;Field  ;
                  Name=EstFrom;
                  CaptionML=[DEU=Kalkulation;
                             ENU=Estimate;
                             NLD=Calculatie;
                             NOR=Kalkyle;
                             SVE=Kalkyl];
                  SourceExpr=EstFrom;
                  OnValidate=BEGIN
                               OnValidateEstFrom;
                             END;

                  OnLookup=BEGIN
                             OnLookupEstFrom;
                           END;
                            }

      { 1100525010;2;Field  ;
                  Name=SubEstFrom;
                  CaptionML=[DEU=Teilkalkulation;
                             ENU=Sub-Estimate;
                             NLD=Deelcalculatie;
                             NOR=Delkalkyle;
                             SVE=Delkalkyl];
                  SourceExpr=SubEstFrom;
                  OnValidate=BEGIN
                               OnValidateSubEstFrom;
                             END;

                  OnLookup=BEGIN
                             OnLookupSubEstFrom;
                           END;
                            }

      { 1100525009;2;Field  ;
                  Name=EstPartFrom;
                  CaptionML=[DEU=Teil;
                             ENU=Part;
                             NLD=Onderdeel;
                             NOR=Del;
                             SVE=Del];
                  SourceExpr=EstPartFrom;
                  OnValidate=BEGIN
                               OnValidateEstPartFrom;
                             END;

                  OnLookup=BEGIN
                             OnLookupEstPartFrom;
                           END;
                            }

      { 1100525008;2;Field  ;
                  Name=RecipeGrpFrom;
                  CaptionML=[DEU=Rezeptgruppe;
                             ENU=Recipe Group;
                             NLD=Receptgroep;
                             NOR=Maltype;
                             SVE=Malltyp];
                  SourceExpr=RecipeGrpFrom;
                  OnValidate=BEGIN
                               OnValidateRecipeGrpFrom;
                             END;

                  OnLookup=BEGIN
                             OnLookupRecipeGrpFrom;
                           END;
                            }

      { 1100525007;2;Field  ;
                  Name=RecipeFrom;
                  CaptionML=[DEU=Rezept;
                             ENU=Recipe;
                             NLD=Recept;
                             NOR=Mal;
                             SVE=Mall];
                  SourceExpr=RecipeFrom;
                  OnValidate=BEGIN
                               OnValidateRecipeFrom;
                             END;

                  OnLookup=BEGIN
                             OnLookupRecipeFrom;
                           END;
                            }

      { 1100525006;2;Field  ;
                  CaptionClass=Text999;
                  Style=Strong;
                  StyleExpr=TRUE }

      { 1100525005;2;Field  ;
                  Name=EstTo;
                  CaptionML=[DEU=Kalkulation;
                             ENU=Estimate;
                             NLD=Calculatie;
                             NOR=Kalkyle;
                             SVE=Kalkyl];
                  SourceExpr=EstTo;
                  OnValidate=BEGIN
                               OnValidateEstTo;
                             END;

                  OnLookup=BEGIN
                             OnLookupEstTo;
                           END;
                            }

      { 1100525004;2;Field  ;
                  Name=SubEstTo;
                  CaptionML=[DEU=Teilkalkulation;
                             ENU=Sub-Estimate;
                             NLD=Deelcalculatie;
                             NOR=Delkalkyle;
                             SVE=Delkalkyl];
                  SourceExpr=SubEstTo;
                  OnValidate=BEGIN
                               OnValidateSubEstTo;
                             END;

                  OnLookup=BEGIN
                             OnLookupSubEstTo;
                           END;
                            }

      { 1100525003;2;Field  ;
                  Name=EstPartTo;
                  CaptionML=[DEU=Teil;
                             ENU=Part;
                             NLD=Onderdeel;
                             NOR=Del;
                             SVE=Del];
                  SourceExpr=EstPartTo;
                  OnValidate=BEGIN
                               OnValidateEstPartTo;
                             END;

                  OnLookup=BEGIN
                             OnLookupEstPartTo;
                           END;
                            }

      { 1100525002;2;Field  ;
                  Name=RecipeGrpTo;
                  CaptionML=[DEU=Rezeptgruppe;
                             ENU=Recipe Group;
                             NLD=Receptgroep;
                             NOR=Maltype;
                             SVE=Malltyp];
                  SourceExpr=RecipeGrpTo;
                  OnValidate=BEGIN
                               OnValidateRecipeGrpTo;
                             END;

                  OnLookup=BEGIN
                             OnLookupRecipeGrpTo;
                           END;
                            }

      { 1100525001;2;Field  ;
                  Name=RecipeTo;
                  CaptionML=[DEU=Rezept;
                             ENU=Recipe;
                             NLD=Recept;
                             NOR=Mal;
                             SVE=Mall];
                  SourceExpr=RecipeTo;
                  OnLookup=BEGIN
                             OnLookupRecipeTo;
                           END;
                            }

      { 1100525000;2;Field  ;
                  CaptionML=[DEU=Beschreibung Rezept;
                             ENU=Description Recipe;
                             NLD=Omschrijving recept];
                  SourceExpr=RecipeToTxt }

      { 1100528501;2;Field  ;
                  CaptionClass=Text997;
                  Style=Strong;
                  StyleExpr=TRUE }

      { 1100528500;2;Field  ;
                  Name=CopyPriceAgreement;
                  CaptionML=[DEU=Preisvereinbarung kopieren;
                             ENU=Copy Price Agreement;
                             NLD=Prijsafspraak kopi‰ren;
                             SVE=Kopiera prisavtal];
                  SourceExpr=CopyPriceAgreement }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      EstFrom@1100528955 : Code[20];
      EstTo@1100528954 : Code[20];
      SubEstFrom@1100528953 : Code[20];
      SubEstTo@1100528952 : Code[20];
      PartGrpFrom@1100528951 : Code[20];
      PartGrpTo@1100528950 : Code[20];
      EstPartFrom@1100528949 : Code[20];
      EstPartTo@1100528948 : Code[20];
      LineFrom@1100528947 : Integer;
      LineTo@1100528946 : Integer;
      RecipeGrpFrom@1100528945 : Code[20];
      RecipeGrpTo@1100528944 : Code[20];
      RecipeFrom@1100528943 : Code[20];
      RecipeTo@1100528942 : Code[20];
      EstFromTxt@1100528941 : Text[50];
      EstToTxt@1100528940 : Text[50];
      SubEstFromTxt@1100528939 : Text[50];
      SubEstToTxt@1100528938 : Text[50];
      EstPartFromTxt@1100528937 : Text[50];
      EstPartToTxt@1100528936 : Text[50];
      RecipeGrpFromTxt@1100528935 : Text[50];
      RecipeGrpToTxt@1100528934 : Text[50];
      RecipeFromTxt@1100528933 : Text[100];
      RecipeToTxt@1100528932 : Text[100];
      EstRec@1100528931 : Record 11012151;
      SubEstRec@1100528929 : Record 11072074;
      EstPartRec@1100528928 : Record 11072073;
      EstLineRec@1100528901 : Record 11072072;
      RecipeRec@1100528900 : Record 11020581;
      RgrpRec@1100528926 : Record 11020580;
      FormatMgt@1100528905 : Codeunit 11012022;
      Text006@1100528956 : TextConst 'DEU=Verarbeitung nur gestattet in Herkunftunternehmen Zentrale Artikelverwaltung;ENU=Transaction only allowed in company used as source for central itemmanagement;NLD=Verwerking alleen toegestaan in bronbedrijf centraal artikelbeheer;NOR=Transaksjonen er kun tillatt for selskap som er i bruk som kilde for sentral artikkelh†ndtering;SVE=Transaktionen „r endast till†ten f”r f”retag som anv„nds som k„lla f”r central artikelhantering';
      Text997@1100528500 : TextConst 'DEU=Einrichting;ENU=Setup;NLD=Instellingen;SVE=Inst„llning';
      Text998@1100525001 : TextConst 'DEU=Kopieren von;ENU=Copy From;NLD=Kopieer van;NOR=Kopier fra;SVE=Kopiera fr†n';
      Text999@1100528903 : TextConst 'DEU=Kopieren in;ENU=Copy To;NLD=Kopieer naar;NOR=Kopier til;SVE=Kopiera till';
      CopyPriceAgreement@1100528501 : Boolean;

    PROCEDURE FillDefaults@1(lEstFrom@11012000 : Code[20];lSubEstFrom@11012001 : Code[20];lPartGrpFrom@11012002 : Code[20];lEstPartFrom@11012003 : Code[20];lLineFrom@11012004 : Integer;lRecipeGrpFrom@11012005 : Code[20];lRecipeFrom@11012006 : Code[20]);
    BEGIN
      EstFrom  := lEstFrom;
      SubEstFrom := lSubEstFrom;
      PartGrpFrom := lPartGrpFrom;
      EstPartFrom := lEstPartFrom;
      LineFrom := lLineFrom;
      RecipeGrpFrom := lRecipeGrpFrom;
      RecipeFrom  := lRecipeFrom;

      EstTo := EstFrom;
      SubEstTo := SubEstFrom;
      PartGrpTo:= PartGrpFrom;
      EstPartTo := EstPartFrom;
      RecipeGrpTo := RecipeGrpFrom;
      RecipeTo := RecipeFrom;

      IF EstRec.GET(EstFrom) THEN BEGIN
        EstFromTxt := EstRec.Description;
        EstToTxt := EstRec.Description;
      END;

      IF SubEstRec.GET(EstFrom,SubEstFrom) THEN BEGIN
        SubEstFromTxt := SubEstRec.Description;
        SubEstToTxt := SubEstRec.Description;
      END;

      IF EstPartRec.GET(EstFrom,SubEstFrom,PartGrpFrom,EstPartFrom) THEN BEGIN
        EstPartFromTxt := EstPartRec.Description;
        EstPartToTxt := EstPartRec.Description;
      END;

      IF RgrpRec.GET(RecipeGrpFrom) THEN BEGIN
        RecipeGrpFromTxt := RgrpRec.Description;
        RecipeGrpToTxt := RgrpRec.Description;
      END;

      IF RecipeRec.GET(RecipeGrpFrom,RecipeFrom) THEN BEGIN
        RecipeFromTxt := RecipeRec.Description;
        RecipeToTxt := RecipeRec.Description;
      END;

      IF EstLineRec.GET(EstFrom,SubEstFrom,PartGrpFrom,EstPartFrom,LineFrom) THEN BEGIN
        IF (RecipeGrpFrom <> '') AND (RecipeFrom <> '') THEN BEGIN
          RecipeFromTxt := EstLineRec.Description;
          RecipeToTxt := EstLineRec.Description;
        END;
      END;
    END;

    PROCEDURE StartUpdate@2();
    VAR
      BufferMgt@1100528900 : Codeunit 11012230;
      RcpRec2@1100528901 : Record 11020581;
      CompRel@1100528907 : Record 11012059;
      CentrManagementCU@1100528906 : Codeunit 11012010;
      NavBln@1100528905 : Boolean;
      DiscBln@1100528904 : Boolean;
      DiscGrpBln@1100528903 : Boolean;
      RcpBln@1100528902 : Boolean;
      FPSLicenseManagement@1100527400 : Codeunit 11229289;
      SelectMarkedLines@1100528502 : Boolean;
      SelectDestination@1100528503 : Boolean;
      ApplySort@1100528501 : Boolean;
    BEGIN
      IF EstTo <> '' THEN BEGIN
        EstRec.GET(EstTo);
        EstRec.TESTFIELD(Fixed, FALSE);

        SubEstRec.GET(EstTo, SubEstTo);
        EstPartRec.GET(EstTo, SubEstTo, PartGrpTo, EstPartTo);

        EstLineRec.RESET;
        EstLineRec.SETRANGE("Estimate No.", EstTo);
        EstLineRec.SETRANGE("Sub-Estimate No.", SubEstTo);
        EstLineRec.SETRANGE("Part Group", PartGrpTo);
        EstLineRec.SETRANGE(Part, EstPartTo);
        IF EstLineRec.FINDLAST THEN
          LineTo := EstLineRec."Line No." + 10000
        ELSE
          LineTo := 10000;

        IF NOT EstLineRec.GET(EstFrom,SubEstFrom,PartGrpFrom,EstPartFrom,LineFrom) THEN BEGIN
          EstLineRec.INIT;
          IF RecipeRec.GET(RecipeGrpFrom, RecipeFrom) THEN
            EstLineRec.Manufacturer := RecipeRec.Manufacturer;
        END;
        EstLineRec."Estimate No." := EstTo;
        EstLineRec."Sub-Estimate No." := SubEstTo;
        EstLineRec."Part Group" := PartGrpTo;
        EstLineRec.Part := EstPartTo;
        EstLineRec."Line No." := LineTo;
        EstLineRec."Recipe Group" := RecipeGrpTo;
        EstLineRec.Recipe := RecipeTo;
        EstLineRec.Description := RecipeToTxt;
        EstLineRec.INSERT(TRUE);
      END ELSE BEGIN
        IF FPSLicenseManagement.LicenseAndReadPermissionForTable(CompRel) THEN BEGIN
          CentrManagementCU.ItemFormEditable(NavBln,DiscBln,DiscGrpBln,RcpBln);
          IF RcpBln THEN
            ERROR(Text006);
        END;
        RecipeRec.SETRANGE("Recipe Group",RecipeGrpFrom);
        IF RecipeFrom <> '' THEN
          RecipeRec.SETRANGE("No.",RecipeFrom);
        IF RecipeRec.FINDSET THEN BEGIN
          REPEAT
            RcpRec2.COPY(RecipeRec);
            RcpRec2."Recipe Group" := RecipeGrpTo;
            IF RecipeTo <> '' THEN BEGIN
              RcpRec2."No." := RecipeTo;
              RcpRec2.Description := RecipeToTxt;
            END;
            RcpRec2.Blocked := FALSE;
            RcpRec2.INSERT(TRUE);
          UNTIL RecipeRec.NEXT = 0;
        END ELSE BEGIN
          RcpRec2.INIT;
          RcpRec2."Recipe Group" := RecipeGrpTo;
          RcpRec2."No." := RecipeTo;
          RcpRec2.Description := RecipeToTxt;
          RcpRec2.Blocked := FALSE;
          RcpRec2.INSERT(TRUE);
        END;
      END;

      //C042683.sn
      IF (EstFrom = '') AND (EstTo <> '') THEN BEGIN
        BufferMgt.SetSelection(
          EstLineRec."Estimate No.",
          EstLineRec."Sub-Estimate No.",
          EstLineRec."Part Group",
          EstLineRec.Part,
          EstLineRec."Line No.",
          0,
          '',
          0,
          EstLineRec."Recipe Group",
          EstLineRec.Recipe,
          0);
      END;
      //C042683.en

      BufferMgt.SetBufferOptions(SelectMarkedLines, SelectDestination, ApplySort, CopyPriceAgreement); //C063348: removed CopyPriceAgreement as local variable while also defined as global
      BufferMgt.CopyDetails(
        EstFrom, SubEstFrom, PartGrpFrom, EstPartFrom, LineFrom, RecipeGrpFrom, RecipeFrom, 0,
        EstTo, SubEstTo, PartGrpTo, EstPartTo, LineTo, RecipeGrpTo, RecipeTo, 0,
        TRUE);

      IF EstLineRec.GET(EstTo, SubEstTo, PartGrpTo, EstPartTo, LineTo) THEN
        EstLineRec.VALIDATE("Adjustment Factor Hours");
    END;

    PROCEDURE "-CopyFrom-"@1100525019();
    BEGIN
    END;

    PROCEDURE OnValidateEstFrom@1100525017();
    BEGIN
      IF EstFrom = '' THEN BEGIN  //db, 22-12-17
        SubEstFrom := '';
        PartGrpFrom := '';
        EstPartFrom := '';
        LineFrom := 0;
        EstFromTxt := '';
        SubEstFromTxt := '';
        EstPartFromTxt := '';
      END ELSE BEGIN
        EstRec."No." := EstFrom;
        EstRec.DPA_FindRecWithDPAFilterAppld;
        EstFromTxt := EstRec.Description;
      END;
    END;

    PROCEDURE OnLookupEstFrom@1100525016();
    BEGIN
      EstRec.RESET;
      IF PAGE.RUNMODAL(0,EstRec) = ACTION::LookupOK THEN BEGIN
         EstFrom := EstRec."No.";
         EstFromTxt := EstRec.Description;
      END;
    END;

    PROCEDURE OnValidateSubEstFrom@1100525015();
    VAR
      Chapter@1100525002 : Code[20];
      Paragraph@1100525001 : Code[20];
      Level@1100525000 : Integer;
    BEGIN
      FormatMgt.FormatSubEstimate(SubEstFrom,Chapter,Paragraph,Level,EstFrom,FALSE);
      SubEstRec.GET(EstFrom, SubEstFrom);
      SubEstFromTxt := SubEstRec.Description;
    END;

    PROCEDURE OnLookupSubEstFrom@1100525014();
    BEGIN
      SubEstRec.RESET;
      SubEstRec.SETRANGE("Estimate No.",EstFrom);
      IF PAGE.RUNMODAL(0,SubEstRec) = ACTION::LookupOK THEN BEGIN
         EstRec.GET(SubEstRec."Estimate No.");
         EstFrom := EstRec."No.";
         EstFromTxt := EstRec.Description;
         SubEstFrom := SubEstRec."Sub-Estimate No.";
         SubEstFromTxt := SubEstRec.Description;
         PartGrpFrom := SubEstRec."Part Group";
      END;
    END;

    PROCEDURE OnValidateEstPartFrom@1100525013();
    BEGIN
      FormatMgt.FormatElem(EstPartFrom,1,'');
      EstPartRec.GET(EstFrom,SubEstFrom,PartGrpFrom,EstPartFrom);
      EstPartFromTxt := EstPartRec.Description;
    END;

    PROCEDURE OnLookupEstPartFrom@1100525012();
    BEGIN
      EstPartRec.RESET;
      EstPartRec.SETRANGE("Estimate No.",EstFrom);
      EstPartRec.SETRANGE("Sub-Estimate No.",SubEstFrom);
      IF PAGE.RUNMODAL(0,EstPartRec) = ACTION::LookupOK THEN BEGIN
         PartGrpFrom := EstPartRec."Part Group";
         EstPartFrom := EstPartRec.Part;
         EstPartFromTxt := EstPartRec.Description;
      END;
    END;

    PROCEDURE OnValidateRecipeGrpFrom@1100525011();
    BEGIN
      RgrpRec.GET(RecipeGrpFrom);
      RecipeGrpFromTxt := RgrpRec.Description;
    END;

    PROCEDURE OnLookupRecipeGrpFrom@1100525009();
    BEGIN
      IF PAGE.RUNMODAL(0, RgrpRec) = ACTION::LookupOK THEN BEGIN
         RecipeGrpFrom := RgrpRec."No.";
         RecipeGrpFromTxt := RgrpRec.Description;
      END;
    END;

    PROCEDURE OnValidateRecipeFrom@1100525018();
    BEGIN
      IF EstFrom = '' THEN BEGIN  //db, 22-12-17
        RecipeRec.GET(RecipeGrpFrom, RecipeFrom);
        RecipeRec.TESTFIELD(Blocked, FALSE);
        RecipeFromTxt := RecipeRec.Description;
      END ELSE BEGIN
        EstLineRec.SETRANGE("Estimate No.", EstFrom);
        EstLineRec.SETRANGE("Sub-Estimate No.", SubEstFrom);
        EstLineRec.SETRANGE("Part Group", PartGrpFrom);
        EstLineRec.SETRANGE(Part, EstPartFrom);
        EstLineRec.SETRANGE("Recipe Group", RecipeGrpFrom);
        EstLineRec.FINDFIRST;
        RecipeFromTxt := EstLineRec.Description;
        LineFrom := EstLineRec."Line No.";
      END;
      RecipeTo := RecipeFrom;
      RecipeToTxt := RecipeFromTxt;
    END;

    PROCEDURE OnLookupRecipeFrom@1100525006();
    BEGIN
      IF EstFrom = '' THEN BEGIN  //db, 22-12-17
        RecipeRec.SETRANGE("Recipe Group", RecipeGrpFrom);
        IF PAGE.RUNMODAL(0, RecipeRec) = ACTION::LookupOK THEN BEGIN
          RecipeFrom := RecipeRec."No.";
          RecipeFromTxt := RecipeRec.Description;
          RecipeTo := RecipeFrom;
          RecipeToTxt := RecipeFromTxt;
        END;
      END ELSE BEGIN
        EstLineRec.SETRANGE("Estimate No.", EstFrom);
        EstLineRec.SETRANGE("Sub-Estimate No.", SubEstFrom);
        EstLineRec.SETRANGE("Part Group", PartGrpFrom);
        EstLineRec.SETRANGE(Part, EstPartFrom);
        EstLineRec.SETRANGE("Recipe Group", RecipeGrpFrom);
        IF PAGE.RUNMODAL(0, EstLineRec) = ACTION::LookupOK THEN BEGIN
          EstLineRec.CALCFIELDS("Estimate Description", "Sub-Estimate Description", "Part Description");
          EstFrom := EstLineRec."Estimate No.";
          EstFromTxt := EstLineRec."Estimate Description";
          SubEstFrom := EstLineRec."Sub-Estimate No.";
          SubEstFromTxt := EstLineRec."Sub-Estimate Description";
          PartGrpFrom := EstLineRec."Part Group";
          EstPartFrom := EstLineRec.Part;
          EstPartFromTxt := EstLineRec."Part Description";
          RecipeFrom := EstLineRec.Recipe;
          RecipeFromTxt := EstLineRec.Description;
          RecipeTo := RecipeFrom;
          RecipeToTxt := RecipeFromTxt;
          LineFrom := EstLineRec."Line No.";
        END;
      END;
    END;

    PROCEDURE "-CopyTo-"@1100525020();
    BEGIN
    END;

    PROCEDURE OnValidateEstTo@1100525000();
    BEGIN
      IF EstTo = '' THEN BEGIN
        SubEstTo := '';
        PartGrpTo := '';
        EstPartTo := '';
        LineTo := 0;
        EstToTxt := '';
        SubEstToTxt := '';
        EstPartToTxt := '';
      END ELSE BEGIN
        EstRec."No." := EstTo;
        EstRec.DPA_FindRecWithDPAFilterAppld;
        EstToTxt := EstRec.Description;
      END;
    END;

    PROCEDURE OnLookupEstTo@1100525001();
    BEGIN
      EstRec.RESET;
      IF PAGE.RUNMODAL(0,EstRec) = ACTION::LookupOK THEN BEGIN
         EstTo := EstRec."No.";
         EstToTxt := EstRec.Description;
      END;
    END;

    PROCEDURE OnValidateSubEstTo@1100525002();
    VAR
      Chapter@1100525002 : Code[20];
      Paragraph@1100525001 : Code[20];
      Level@1100525000 : Integer;
    BEGIN
      FormatMgt.FormatSubEstimate(SubEstTo,Chapter,Paragraph,Level,EstTo,FALSE);
      SubEstRec.GET(EstTo,SubEstTo);
      SubEstToTxt := SubEstRec.Description;
    END;

    PROCEDURE OnLookupSubEstTo@1100525003();
    BEGIN
      SubEstRec.RESET;
      SubEstRec.SETRANGE("Estimate No.",EstTo);
      IF PAGE.RUNMODAL(0,SubEstRec) = ACTION::LookupOK THEN BEGIN
         EstRec.GET(SubEstRec."Estimate No.");
         EstTo := EstRec."No.";
         EstToTxt := EstRec.Description;
         SubEstTo := SubEstRec."Sub-Estimate No.";
         SubEstToTxt := SubEstRec.Description;
         PartGrpTo := SubEstRec."Part Group";
      END;
    END;

    PROCEDURE OnValidateEstPartTo@1100525004();
    BEGIN
      FormatMgt.FormatElem(EstPartTo,1,'');
      EstPartRec.GET(EstTo,SubEstTo,PartGrpTo,EstPartTo);
      EstPartToTxt := EstPartRec.Description;
    END;

    PROCEDURE OnLookupEstPartTo@1100525005();
    BEGIN
      EstPartRec.RESET;
      EstPartRec.SETRANGE("Estimate No.",EstTo);
      EstPartRec.SETRANGE("Sub-Estimate No.",SubEstTo);
      IF PAGE.RUNMODAL(0,EstPartRec) = ACTION::LookupOK THEN BEGIN
         PartGrpTo := EstPartRec."Part Group";
         EstPartTo := EstPartRec.Part;
         EstPartToTxt := EstPartRec.Description;
      END;
    END;

    PROCEDURE OnValidateRecipeGrpTo@1100525007();
    BEGIN
      RgrpRec.GET(RecipeGrpTo);
      RecipeGrpToTxt := RgrpRec.Description;
    END;

    PROCEDURE OnLookupRecipeGrpTo@1100525008();
    BEGIN
      IF PAGE.RUNMODAL(0, RgrpRec) = ACTION::LookupOK THEN BEGIN
         RecipeGrpTo := RgrpRec."No.";
         RecipeGrpToTxt := RgrpRec.Description;
      END;
    END;

    PROCEDURE OnLookupRecipeTo@1100525010();
    BEGIN
      IF EstTo = '' THEN BEGIN
        RecipeRec.SETRANGE("Recipe Group", RecipeGrpTo);
        PAGE.RUNMODAL(0, RecipeRec);
      END ELSE BEGIN
        EstLineRec.SETRANGE("Estimate No.", EstTo);
        EstLineRec.SETRANGE("Sub-Estimate No.", SubEstTo);
        EstLineRec.SETRANGE("Part Group", PartGrpTo);
        EstLineRec.SETRANGE(Part, EstPartTo);
        EstLineRec.SETRANGE("Recipe Group", RecipeGrpTo);
        PAGE.RUNMODAL(0, EstLineRec);
      END;
    END;

    BEGIN
    {
      DP00686: From-Selection editable; overrule tablefilter by DataItemTableView to disable selection/filter DataItem of this report
    }
    END.
  }
  RDLDATA
  {
  }
}

