OBJECT Codeunit 6085719 Purch. Doc. - Val. Doc. Date
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.5.00.05,4PS14.00;
  }
  PROPERTIES
  {
    TableNo=6085593;
    OnRun=VAR
            Document@161024014 : Record 6085590;
            Field@161024012 : Record 6085580;
            TemplField@161024016 : Record 6085580;
            Value@161024015 : Record 6085593;
            PaymentTerms@161024018 : Record 3;
            Vendor@161024019 : Record 23;
            Comment@161024020 : Record 6085594;
            CaptureMgnt@161024013 : Codeunit 6085576;
            DocDate@161024021 : Date;
            VendorNo@6085573 : Code[20];
            Template@1160040000 : Record 6085579;
            Filtertext@1100528201 : Text[50];
            PurchDocManagement@1100528202 : Codeunit 6085709;
            OurOrderNo@1100528203 : Code[20];
            PurchaseHeader@1100528204 : Record 38;
          BEGIN
            //**4PS.sn
            Document.GET(Rec."Document No.");
            Template.GET(Rec."Template No.");
            IF Template."Use E-mail Date for Doc. Date" AND (Document."E-Mail Date" <> 0DT) THEN
              DocDate := DT2DATE(Document."E-Mail Date")
            ELSE
            //**4PS.en
              DocDate := GetAsDate("Value (Text)");

            IF DocDate = 0D THEN
              EXIT;

            //Document.GET(Rec."Document No."); //**4PS.o

            VendorNo := Document.GetSourceID;
            IF NOT Vendor.GET(VendorNo) THEN BEGIN
              Template.GET(Document."Template No.");
              ERROR(VendorNotFountErr,VendorNo,Template."Search Text",Document."Template No.");
            END;

            IF NOT Field.GET(Document."Template No.",Field.Type::Header,'DUEDATE') THEN
              EXIT;

            IF (NOT CaptureMgnt.GetFieldValue(Document,Field,0,Value)) THEN
              EXIT;

            //**4PS.so
            //IF Value."Value (Date)" = 0D THEN BEGIN
            //**4PS.eo

            //**4PS.sn
            Comment.SETRANGE("Document No.", Document."No.");
            Filtertext := '@*'+ STRSUBSTNO(Text11012001) + '*';
            Comment.SETFILTER(Comment, Filtertext);
            IF Comment.FINDSET THEN
              Comment.DELETEALL;

            Comment.RESET;

            OurOrderNo := PurchDocManagement.GetOurDocumentNo(Document);
            IF (OurOrderNo <> '') THEN
              IF PurchaseHeader.GET(PurchaseHeader."Document Type"::Order,OurOrderNo) THEN BEGIN
                Vendor."Payment Terms Code" := PurchaseHeader."Payment Terms Code";
              END;
            //**4PS.en

            TemplField.GET(Rec."Template No.",Field.Type::Header,'DOCDATE');
            IF PaymentTerms.GET(Vendor."Payment Terms Code") AND (FORMAT(PaymentTerms."Due Date Calculation") <> '') THEN BEGIN
              Value."Value (Date)" := CALCDATE(PaymentTerms."Due Date Calculation", DocDate);
              Value."Is Valid" := TRUE;
              Value.MODIFY;
              //**4PS.sn
              IF Template."Use E-mail Date for Doc. Date" THEN //**4PS.n
                Comment.Add(Document, Field, 0, Comment.Area::Capture, Comment."Comment Type"::Warning,
                            STRSUBSTNO(Text11012000, Field."Field Name"))
              ELSE
              //**4PS.en
                Comment.Add(Document,Field,0,Comment.Area::Capture,Comment."Comment Type"::Warning,STRSUBSTNO(Text002,Field."Field Name",
                  TemplField."Field Name"));
            //END; //**4PS.o
            END;
          END;

  }
  CODE
  {
    VAR
      Text002@161024014 : TextConst 'DEU=%1 wurde berechnet auf Grundlage von %2.;ENU=WARNING: %1 was calculated based on %2.;NLD=WAARSCHUWING: %1 is berekend op basis van %2.';
      Text008@161024015 : TextConst 'DEU=T;ENU=T;NLD=H;NOR=T;SVE=t';
      VendorNotFountErr@1160040000 : TextConst 'ENU=Vendor %1 doesn''t exist. Please, remove search text %2 from template %3.';
      Text11012000@1100528200 : TextConst 'DEU=%1 wurde berechnet auf Grundlage von Datum E-Mail;ENU=%1 was calculated based on Date Email;NLD=%1 is berekend op basis van e-maildatum.';
      Text11012001@1100528201 : TextConst 'DEU=wurde berechnet auf Grundlage von;ENU=was calculated based on;NLD=is berekend op basis van';

    PROCEDURE GetAsDate@161024012(VAR Text@161024012 : Text[1024]) : Date;
    VAR
      ValidatedDate@161024013 : Date;
    BEGIN
      IF Text = '' THEN
        EXIT(0D);

      IF UPPERCASE(Text) = Text008 THEN
        EXIT(TODAY);
      IF EVALUATE(ValidatedDate,Text) THEN BEGIN
        IF ValidatedDate < 17540101D THEN
          EXIT(0D)
        ELSE
          EXIT(ValidatedDate)
      END;

      IF EVALUATE(ValidatedDate,DELCHR(Text,'=','-')) THEN BEGIN
        IF ValidatedDate < 17540101D THEN
          EXIT(0D)
        ELSE
          EXIT(ValidatedDate)
      END;
    END;

    BEGIN
    END.
  }
}

