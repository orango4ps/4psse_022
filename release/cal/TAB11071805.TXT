OBJECT Table 11071805 Serv. Survey Loc. Structure
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               ServiceSurveyStructureLine@1100528600 : Record 11071806;
             BEGIN
               ServiceSurveyStructureLine.SETRANGE("Survey No.", "Survey No.");
               ServiceSurveyStructureLine.SETRANGE("Location Code", "Location Code");
               ServiceSurveyStructureLine.SETRANGE("Structure No.", "Structure No.");
               IF NOT ServiceSurveyStructureLine.ISEMPTY THEN
                 ServiceSurveyStructureLine.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=Service Survey Location Structure;
               NOR=Servicekartlegging, lokasjonstruktur;
               SVE=Srv.kartl.plats struktur];
    LookupPageID=Page11126255;
    DrillDownPageID=Page11126255;
  }
  FIELDS
  {
    { 10  ;   ;Survey No.          ;Code20        ;TableRelation="Service Survey".No.;
                                                   CaptionML=[ENU=Survey No.;
                                                              NOR=Kartlegging nr.;
                                                              SVE=Kartl„ggningsnr];
                                                   NotBlank=Yes }
    { 20  ;   ;Location Code       ;Code10        ;TableRelation="Service Survey Location"."Location Code" WHERE (Survey No.=FIELD(Survey No.));
                                                   CaptionML=[ENU=Location Code;
                                                              NOR=Plasseringkode;
                                                              SVE=Lagerst„llekod];
                                                   NotBlank=Yes }
    { 30  ;   ;Structure No.       ;Integer       ;OnValidate=BEGIN
                                                                CheckStructureLinesPresent;  //DP00978
                                                              END;

                                                   CaptionML=[ENU=Structure No.;
                                                              NOR=Strukturnr;
                                                              SVE=Strukturnr];
                                                   NotBlank=Yes }
    { 40  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 50  ;   ;Base Structure      ;Boolean       ;OnValidate=VAR
                                                                ServiceSurvey@1100528602 : Record 11071803;
                                                                ServSurveyLocStructure@1100528600 : Record 11071805;
                                                                ServSurvLocationObject@1100528601 : Record 11071813;
                                                              BEGIN
                                                                ServiceSurvey.GET("Survey No.");
                                                                ServiceSurvey.TESTFIELD("Input Method", ServiceSurvey."Input Method"::Structure);

                                                                IF "Base Structure" THEN BEGIN
                                                                  ServSurveyLocStructure.SETRANGE("Survey No.", "Survey No.");
                                                                  ServSurveyLocStructure.SETRANGE("Location Code", "Location Code");
                                                                  ServSurveyLocStructure.SETFILTER("Structure No.", '<>%1', "Structure No.");
                                                                  ServSurveyLocStructure.SETRANGE("Base Structure", TRUE);
                                                                  IF ServSurveyLocStructure.FINDFIRST THEN BEGIN
                                                                    ServSurvLocationObject.SETRANGE("Survey No.", "Survey No.");
                                                                    ServSurvLocationObject.SETRANGE("Location Code", "Location Code");
                                                                    IF NOT ServSurvLocationObject.ISEMPTY THEN
                                                                      ERROR(Text000, FIELDCAPTION("Base Structure"))
                                                                    ELSE BEGIN
                                                                      ServSurveyLocStructure."Base Structure" := FALSE;
                                                                      ServSurveyLocStructure.MODIFY;
                                                                    END;
                                                                  END;
                                                                  TestAllObjectsInBaseStructure;
                                                                END ELSE BEGIN
                                                                  ServSurvLocationObject.SETRANGE("Survey No.", "Survey No.");
                                                                  ServSurvLocationObject.SETRANGE("Location Code", "Location Code");
                                                                  IF NOT ServSurvLocationObject.ISEMPTY THEN
                                                                    ERROR(Text001);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Base Structure;
                                                              NOR=Basisstruktur;
                                                              SVE=Basstruktur] }
    { 60  ;   ;Available for 4PS Mobile;Boolean   ;OnValidate=VAR
                                                                ServSurveyLocStructure@1210190000 : Record 11071805;
                                                              BEGIN
                                                                IF (xRec."Available for 4PS Mobile" <> "Available for 4PS Mobile") AND "Available for 4PS Mobile" THEN BEGIN
                                                                  ServSurveyLocStructure.SETRANGE("Survey No.", "Survey No.");
                                                                  ServSurveyLocStructure.SETRANGE("Location Code", "Location Code");
                                                                  ServSurveyLocStructure.SETFILTER("Structure No.", '<>%1', "Structure No.");
                                                                  ServSurveyLocStructure.SETRANGE("Available for 4PS Mobile", TRUE);
                                                                  IF ServSurveyLocStructure.FINDFIRST THEN
                                                                    IF CONFIRM(Text003, TRUE, ServSurveyLocStructure."Structure No.") THEN
                                                                      ServSurveyLocStructure.MODIFYALL("Available for 4PS Mobile", FALSE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Available for 4PS Mobile;
                                                              NOR=Tilgjengelig for 4PS mobil;
                                                              SVE=Tillg„nglig f”r 4PS-mobil] }
  }
  KEYS
  {
    {    ;Survey No.,Location Code,Structure No.  ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528600 : TextConst 'ENU=%1 already exists;NOR=%1 finnes allerede;SVE=%1 finns redan.';
      Text001@1100528601 : TextConst 'ENU=Objects already exist;NOR=Objekt finnes allerede;SVE=Objekt finns redan';
      Text002@1100528602 : TextConst 'ENU=Not all objects are assigned to this structure.;NOR=Ikke alle objekter er tilordnet denne strukturen;SVE=Inte alla objekt har tilldelats denna struktur.';
      Text003@1100528603 : TextConst 'ENU=Service Survey Location Structure No. %1 already selected for 4PS Mobile, continue?';

    PROCEDURE TestAllObjectsInBaseStructure@1100528600();
    VAR
      ServSurvLocationObject@1100528600 : Record 11071813;
      TmpServSurvLocationObject@1100528601 : TEMPORARY Record 11071813;
      ServiceSurveyStructureLine@1100528602 : Record 11071806;
    BEGIN
      ServSurvLocationObject.SETRANGE("Survey No.", "Survey No.");
      ServSurvLocationObject.SETRANGE("Location Code", "Location Code");
      IF ServSurvLocationObject.FINDSET THEN
        REPEAT
          TmpServSurvLocationObject := ServSurvLocationObject;
          TmpServSurvLocationObject.INSERT;
        UNTIL ServSurvLocationObject.NEXT = 0;

      ServiceSurveyStructureLine.SETRANGE("Survey No.", "Survey No.");
      ServiceSurveyStructureLine.SETRANGE("Location Code", "Location Code");
      ServiceSurveyStructureLine.SETRANGE("Structure No.", "Structure No.");
      IF ServiceSurveyStructureLine.FINDSET THEN
        REPEAT
          IF TmpServSurvLocationObject.GET(ServiceSurveyStructureLine."Survey No.",
               ServiceSurveyStructureLine."Location Code", ServiceSurveyStructureLine."Seq. No.")
          THEN
            TmpServSurvLocationObject.DELETE;
        UNTIL ServiceSurveyStructureLine.NEXT = 0;
      IF NOT TmpServSurvLocationObject.ISEMPTY THEN
        ERROR(Text002);
    END;

    LOCAL PROCEDURE CheckStructureLinesPresent@1100525000();
    VAR
      ServiceSurveyStructureLine@1100525000 : Record 11071806;
      Text000@1100525001 : TextConst 'ENU=Starting Code';
    BEGIN
      //DP00978
      ServiceSurveyStructureLine.SETRANGE("Survey No.", "Survey No.");
      ServiceSurveyStructureLine.SETRANGE("Location Code", "Location Code");
      ServiceSurveyStructureLine.SETRANGE("Structure No.", "Structure No.");
      IF NOT ServiceSurveyStructureLine.FINDFIRST THEN BEGIN
        ServiceSurveyStructureLine.INIT;
        ServiceSurveyStructureLine."Survey No." := "Survey No.";
        ServiceSurveyStructureLine."Location Code" := "Location Code";
        ServiceSurveyStructureLine."Structure No." := "Structure No.";
        ServiceSurveyStructureLine.VALIDATE(Code, '0');
        ServiceSurveyStructureLine."Code Description" := Text000;
        ServiceSurveyStructureLine.INSERT;
      END;
    END;

    BEGIN
    END.
  }
}

