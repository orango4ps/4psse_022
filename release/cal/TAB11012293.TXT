OBJECT Table 11012293 Vendor Assessment Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               CALCFIELDS(Mandatory);
               TESTFIELD(Mandatory, FALSE);
             END;

    OnRename=VAR
               lvVendAssLineRec@1210190000 : Record 11012293;
             BEGIN
               IF lvVendAssLineRec.GET(xRec."Project No.", xRec.Date, xRec."Vendor No.", xRec.Department, xRec.Criterion) THEN BEGIN
                 lvVendAssLineRec.CALCFIELDS(Mandatory);
                 lvVendAssLineRec.TESTFIELD(Mandatory, FALSE);
               END;
             END;

    CaptionML=[ENU=Vendor Assessment Line;
               NOR=Leverand›rvurderingsrad;
               SVE=Leverant”rsbed”mningsrad];
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr] }
    { 11  ;   ;Project Description ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Job.Description WHERE (No.=FIELD(Project No.)));
                                                   CaptionML=[ENU=Project Description;
                                                              NOR=Prosjektbeskrivelse;
                                                              SVE=Projektbeskrivning];
                                                   Editable=No }
    { 20  ;   ;Date                ;Date          ;CaptionML=[ENU=Date;
                                                              NOR=Dato;
                                                              SVE=Datum];
                                                   NotBlank=Yes }
    { 30  ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor;
                                                   CaptionML=[ENU=Vendor No.;
                                                              NOR=Leverand›rnr.;
                                                              SVE=Lev.nr];
                                                   NotBlank=Yes }
    { 31  ;   ;Vendor Name         ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor.Name WHERE (No.=FIELD(Vendor No.)));
                                                   CaptionML=[ENU=Vendor Name;
                                                              NOR=Leverand›rsnavn;
                                                              SVE=Leverant”rsnamn];
                                                   Editable=No }
    { 32  ;   ;Vendor Qualification;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor."Vendor Qualification" WHERE (No.=FIELD(Vendor No.)));
                                                   CaptionML=[ENU=Vendor Qualification;
                                                              NOR=Leverand›rkompetanse;
                                                              SVE=Leverant”rskompetens];
                                                   Editable=No }
    { 40  ;   ;Department          ;Code10        ;TableRelation="Department Assessment";
                                                   OnValidate=BEGIN
                                                                IF (Criterion <> '') THEN
                                                                  VALIDATE(Criterion);

                                                                CALCFIELDS("Department Name");
                                                              END;

                                                   CaptionML=[ENU=Department;
                                                              NOR=Avdeling;
                                                              SVE=Department];
                                                   NotBlank=Yes }
    { 41  ;   ;Department Name     ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Department Assessment".Name WHERE (Code=FIELD(Department)));
                                                   CaptionML=[ENU=Department Name;
                                                              NOR=Avdelingsnavn;
                                                              SVE=Avdelningsnamn];
                                                   Editable=No }
    { 50  ;   ;Criterion           ;Code10        ;TableRelation="Assessment Criterion".Criterion WHERE (Department=FIELD(Department));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Criterion Description", "Weight Factor Crit.", Mandatory);
                                                                IF (Criterion <> xRec.Criterion) AND (FIELDNO(Criterion) = CurrFieldNo) THEN
                                                                  "Weight Factor" := "Weight Factor Crit.";
                                                                IF Mandatory AND "Not Apply" THEN
                                                                  "Not Apply" := FALSE;
                                                              END;

                                                   CaptionML=[ENU=Criterion;
                                                              NOR=Kriterium;
                                                              SVE=Kriterium];
                                                   NotBlank=Yes }
    { 51  ;   ;Criterion Description;Text50       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Assessment Criterion".Description WHERE (Department=FIELD(Department),
                                                                                                                Criterion=FIELD(Criterion)));
                                                   CaptionML=[ENU=Criterion Description;
                                                              NOR=Beskrivelse av kriterier;
                                                              SVE=Kriteriebeskrivning];
                                                   Editable=No }
    { 52  ;   ;Weight Factor Crit. ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Assessment Criterion"."Weight Factor" WHERE (Department=FIELD(Department),
                                                                                                                    Criterion=FIELD(Criterion)));
                                                   CaptionML=[ENU=Weight Factor Crit.;
                                                              NOR=Vektfaktorkrit.;
                                                              SVE=Viktfaktorkrit.];
                                                   Editable=No }
    { 53  ;   ;Mandatory           ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Assessment Criterion".Mandatory WHERE (Department=FIELD(Department),
                                                                                                              Criterion=FIELD(Criterion)));
                                                   CaptionML=[ENU=Mandatory;
                                                              NOR=Obligatorisk;
                                                              SVE=Obligatoriskt];
                                                   Editable=No }
    { 55  ;   ;Weight Factor       ;Decimal       ;InitValue=1;
                                                   CaptionML=[ENU=Weight Factor;
                                                              NOR=Vektfaktor;
                                                              SVE=Viktfaktor];
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 60  ;   ;Score               ;Integer       ;TableRelation=Assessment;
                                                   OnValidate=BEGIN
                                                                PurSetupRec.GET;
                                                                PurSetupRec.TESTFIELD("Max. Score Assessment");

                                                                IF Score > PurSetupRec."Max. Score Assessment" THEN
                                                                  FIELDERROR(Score, STRSUBSTNO(Text000, PurSetupRec."Max. Score Assessment"));

                                                                IF Score > 0 THEN BEGIN
                                                                  TESTFIELD("Weight Factor");
                                                                  IF "Not Apply" THEN
                                                                    "Not Apply" := FALSE;
                                                                END ELSE BEGIN
                                                                  CALCFIELDS(Mandatory);
                                                                  IF Mandatory THEN
                                                                    MESSAGE(Text001, FIELDCAPTION(Score), FIELDCAPTION(Mandatory), FIELDCAPTION(Criterion));
                                                                END;

                                                                CALCFIELDS(Assessment);
                                                              END;

                                                   CaptionML=[ENU=Score;
                                                              NOR=Poengsum;
                                                              SVE=Po„ng];
                                                   MinValue=0;
                                                   MaxValue=10;
                                                   BlankZero=Yes }
    { 61  ;   ;Assessment          ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Assessment.Assessment WHERE (Score=FIELD(Score)));
                                                   CaptionML=[ENU=Assessment;
                                                              NOR=Vurdering;
                                                              SVE=Bed”mning];
                                                   Editable=No }
    { 65  ;   ;Not Apply           ;Boolean       ;OnValidate=BEGIN
                                                                IF "Not Apply" THEN BEGIN
                                                                  TESTFIELD(Score, 0);
                                                                  CALCFIELDS(Mandatory);
                                                                  TESTFIELD(Mandatory, FALSE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Not Apply;
                                                              NOR=Ikke bruk;
                                                              SVE=Verkst„ll inte];
                                                   Editable=No }
    { 70  ;   ;Motivation          ;Text100       ;CaptionML=[ENU=Motivation;
                                                              NOR=Motivasjon;
                                                              SVE=Motivation] }
  }
  KEYS
  {
    {    ;Project No.,Date,Vendor No.,Department,Criterion;
                                                   Clustered=Yes }
    {    ;Vendor No.,Department,Criterion          }
    {    ;Department,Project No.,Date,Vendor No.,Criterion }
    {    ;Vendor No.,Date,Department,Project No.   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PurSetupRec@1210190000 : Record 312;
      Text000@1210190001 : TextConst 'ENU=may not be greater than ''Max. Score Assessment'' (%1) in the Purchase Setup;NOR=kan ikke v‘re st›rre enn ''Maks. Poengsum vurdering'' (%1) i innkj›psinnstillingene.;SVE=f†r inte vara mer „n ÉMax. po„ngbed”mning'' (%1) i ink”psinst„llningar';
      Text001@1100525000 : TextConst 'ENU=Note: %1 is %2 for %3;NOR=Obs: %1 er %2 for %3;SVE=Obs! %1 „r %2 f”r %3';

    PROCEDURE HandleNotApplyOnAfterInput@1100525000(ScoreText@1100525000 : Text[10]);
    BEGIN
      // Function called in OnAfterInput form field 'Score' on form (F11012293).
      // Input Score in text format, so difference between 0 and empty.
      IF DELCHR(ScoreText) = '' THEN BEGIN
        IF "Not Apply" THEN
          "Not Apply" := FALSE;
      END ELSE BEGIN
        IF ScoreText = '0' THEN BEGIN
          CALCFIELDS(Mandatory);
          IF NOT Mandatory THEN
            "Not Apply" := TRUE;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

