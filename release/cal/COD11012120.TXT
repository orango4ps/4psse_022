OBJECT Codeunit 11012120 Graphical Work Order Planning
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            StartGraphicalWorkOrderPlanning();
          END;

  }
  CODE
  {

    PROCEDURE StartGraphicalWorkOrderPlanning@1100525000();
    VAR
      CompanyPlanGroup@1100528601 : Record 11229300;
      OperatingSystemMgt@1100525000 : Codeunit 11012364;
      BaseClientURI@1100409000 : Text[250];
      CurrentCompanyPlanGroup@1100528602 : Code[10];
      GWPArguments@1100525001 : Text;
      Hostname@1100525002 : Text;
      Uri@1100525004 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Uri";
      UriBuilder@1100529400 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.UriBuilder";
      UseSSL@1100528500 : Text;
      UseNtlm@1100528501 : Text;
    BEGIN
      CurrentCompanyPlanGroup := CompanyPlanGroup.GetCurrentCompanyPlanGroupCode;
      CompanyPlanGroup.GET(CurrentCompanyPlanGroup);

      IF CompanyPlanGroup."Auto. Determine Server Address" THEN
          Hostname := GetServerName
        ELSE
          Hostname := CompanyPlanGroup."Server Address";

      UriBuilder := UriBuilder.UriBuilder(Uri.Uri(GETURL(CLIENTTYPE::Windows)));//this results in something like dynamicsnav://serverName:portno/instanceName/

      UriBuilder.Host := Hostname;
      BaseClientURI := FORMAT(UriBuilder.Uri);

      IF CompanyPlanGroup.UseSSL THEN
        UseSSL := 'True'
      ELSE
        UseSSL := 'False';

      IF CompanyPlanGroup.UseNtlm THEN
        UseNtlm := 'True'
      ELSE
        UseNtlm := 'False';

      GWPArguments := STRSUBSTNO('Server:%1 WebserviceODataPort:%2 WebserviceSoapPort:%3 Instance:%4 CompanyPlanGroup:%5 BaseClientURI:%6 TenantID:%7 SoapSSLEnabled:%8 ODataSSLEnabled:%9 UseNTLM:%10',
          Hostname, CompanyPlanGroup."OData Service Port", CompanyPlanGroup."SOAP Service Port", CompanyPlanGroup."Instance Name",
          CompanyPlanGroup.Code, BaseClientURI, TENANTID, UseSSL, UseSSL, UseNtlm);

      OperatingSystemMgt.ClientShell(GetGWPInstallDir, GWPArguments, FALSE, FALSE);
    END;

    LOCAL PROCEDURE GetGWPInstallDir@1100525002() : Text;
    VAR
      baseKey@1100525003 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.Microsoft.Win32.RegistryKey" RUNONCLIENT;
      gwpKey@1100525002 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.Microsoft.Win32.RegistryKey" RUNONCLIENT;
      registryHiveEnum@1100525001 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.Microsoft.Win32.RegistryHive" RUNONCLIENT;
      registryViewEnum@1100525000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.Microsoft.Win32.RegistryView" RUNONCLIENT;
      Text001@1100525004 : TextConst 'DEU=GWP ist nicht auf diesem Client installiert.;ENU=GWP is not installed on this client.;NLD=GWP is niet geinstalleerd op deze client.';
    BEGIN
      baseKey := baseKey.OpenBaseKey(registryHiveEnum.LocalMachine, registryViewEnum.Registry32); //Registry32 stuff is needed to get the key from the 32Bits registry on 64bit machines
      gwpKey := baseKey.OpenSubKey('SOFTWARE\4PS\4PS GWP\', FALSE);
      IF ISNULL(gwpKey) THEN
        ERROR(Text001);
      EXIT(gwpKey.GetValue('InstallDir'));
    END;

    PROCEDURE GetServerName@1100525001() : Text;
    VAR
      Uri@1100525000 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Uri";
    BEGIN
      EXIT(FORMAT(Uri.Uri(GETURL(CLIENTTYPE::Windows)).Host));
    END;

    BEGIN
    END.
  }
}

