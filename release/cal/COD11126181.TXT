OBJECT Codeunit 11126181 Currency Exchange Rate
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVFI8.00 (32000001);
  }
  PROPERTIES
  {
    OnRun=BEGIN
            RefFileSetup.SETFILTER("Currency Exchange Rate File",'<>%1','');
            IF NOT RefFileSetup.FINDFIRST THEN
              EXIT;

            CurrencyFile.OPEN(RefFileSetup."Currency Exchange Rate File");
            CurrencyFile.TEXTMODE(TRUE);
            UusienlukuOK := FALSE;
            Counter2 := 0;
            Counter := ROUND(CurrencyFile.LEN / 152,1);
            IF Counter < 1 THEN BEGIN
              CurrencyFile.CLOSE;
              EXIT;
            END;

            GLSetup.GET;

            REPEAT
              CurrencyFile.READ(Tietuerivi);
              Tietuetunnus := COPYSTR(Tietuerivi,5,3);

              IF Tietuetunnus = '001' THEN BEGIN
                Valuuttakoodi := COPYSTR(Tietuerivi,26,3);
                TiedostonValuutta := COPYSTR(Tietuerivi,29,3);

                IF TiedostonValuutta <> 'EUR' THEN BEGIN
                  CurrencyFile.CLOSE;
                  ERROR(Text1090000);
                END;

                EVALUATE(Aloitusv,COPYSTR(Tietuerivi,8,4));
                EVALUATE(Aloituskk,COPYSTR(Tietuerivi,12,2));
                EVALUATE(Aloituspvm,COPYSTR(Tietuerivi,14,2));
                EVALUATE(Vaihtokurssisumma ,COPYSTR(Tietuerivi,32,13));
                EVALUATE(EuroValuutta ,COPYSTR(Tietuerivi,99,1));

                Vaihtokurssisumma := Vaihtokurssisumma / 10000000;
                Vaihtokurssisumma := ROUND(Vaihtokurssisumma,0.0000001);
                StartingDate := DMY2DATE(Aloituspvm,Aloituskk,Aloitusv);

                Currency.SETFILTER(Code,Valuuttakoodi);
                CurrencyExchRate.SETFILTER("Currency Code",Valuuttakoodi);
                CurrencyExchRate.SETRANGE("Starting Date",StartingDate);

                IF CurrencyExchRate.FINDFIRST = FALSE THEN
                  IF Currency.FINDFIRST THEN BEGIN
                    UusienlukuOK := TRUE;
                    CurrencyExchRate.VALIDATE("Currency Code",Valuuttakoodi);
                    CurrencyExchRate.VALIDATE("Starting Date",StartingDate);
                    CASE EuroValuutta OF
                      1:
                        BEGIN
                          CurrencyExchRate.VALIDATE("Fix Exchange Rate Amount",2);
                          Currency.VALIDATE("EMU Currency",TRUE);
                          IF Valuuttakoodi <> 'EUR' THEN
                            CurrencyExchRate.VALIDATE("Relational Currency Code",'EUR');
                          CurrencyExchRate.VALIDATE("Exchange Rate Amount",Vaihtokurssisumma);
                          CurrencyExchRate.VALIDATE("Adjustment Exch. Rate Amount",Vaihtokurssisumma);
                          CurrencyExchRate.VALIDATE("Relational Exch. Rate Amount",1);
                          CurrencyExchRate.VALIDATE("Relational Adjmt Exch Rate Amt",1);
                          CurrencyExchRate."Fix Exchange Rate Amount" := 2;
                        END;
                      0:
                        BEGIN
                          CurrencyExchRate.VALIDATE("Fix Exchange Rate Amount",0);
                          CurrencyExchRate.VALIDATE("Relational Currency Code",'');
                          CurrencyExchRate.VALIDATE("Exchange Rate Amount",Vaihtokurssisumma);
                          CurrencyExchRate.VALIDATE("Adjustment Exch. Rate Amount",Vaihtokurssisumma);
                          CurrencyExchRate.VALIDATE("Relational Exch. Rate Amount",1);
                          CurrencyExchRate.VALIDATE("Relational Adjmt Exch Rate Amt",1);
                          CurrencyExchRate."Fix Exchange Rate Amount" := 1;
                        END;
                    END;
                    Currency.MODIFY;
                    CurrencyExchRate.INSERT;
                  END;
              END;
              Counter2 := Counter2 + 1;
            UNTIL Counter2 >= Counter;

            IF UusienlukuOK THEN
              MESSAGE(Text1090001)
            ELSE
              MESSAGE(Text1090002);

            CurrencyFile.CLOSE;
            BackUp := '.000';
            WHILE FILE.EXISTS(RefFileSetup."Currency Exchange Rate File" + BackUp) DO
              BackUp := INCSTR(BackUp);
            FILE.RENAME(RefFileSetup."Currency Exchange Rate File",RefFileSetup."Currency Exchange Rate File" + BackUp);
          END;

  }
  CODE
  {
    VAR
      CurrencyExchRate@1090000 : Record 330;
      Currency@1090001 : Record 4;
      GLSetup@1090002 : Record 98;
      RefFileSetup@1090020 : Record 11126180;
      CurrencyFile@1090003 : File;
      Length@1090004 : Integer;
      Tietuerivi@1090005 : Text[155];
      Tietuetunnus@1090007 : Text[3];
      Counter@1090008 : Integer;
      Counter2@1090009 : Integer;
      Valuuttakoodi@1090010 : Code[10];
      StartingDate@1090011 : Date;
      Vaihtokurssisumma@1090012 : Decimal;
      Aloituspvm@1090014 : Integer;
      Aloituskk@1090015 : Integer;
      Aloitusv@1090016 : Integer;
      UusienlukuOK@1090017 : Boolean;
      EuroValuutta@1090018 : Integer;
      TiedostonValuutta@1090019 : Code[10];
      BackUp@1090021 : Text[30];
      Text1090000@1090024 : TextConst 'ENU=File does not contain Exchange rates in LCY Currency';
      Text1090001@1090022 : TextConst 'ENU="New Exchange Rates updated "';
      Text1090002@1090023 : TextConst 'ENU=No updated currencies';

    BEGIN
    END.
  }
}

