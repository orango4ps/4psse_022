OBJECT Table 99000771 Production BOM Header
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04;
  }
  PROPERTIES
  {
    DataCaptionFields=No.,Description;
    OnInsert=BEGIN
               MfgSetup.GET;
               IF "No." = '' THEN BEGIN
                 MfgSetup.TESTFIELD("Production BOM Nos.");
                 NoSeriesMgt.InitSeries(MfgSetup."Production BOM Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;

               "Creation Date" := TODAY;
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;
             END;

    OnDelete=VAR
               Item@1000 : Record 27;
             BEGIN
               Item.SETRANGE("Production BOM No.","No.");
               IF NOT Item.ISEMPTY THEN
                 ERROR(Text000);

               ProdBOMLine.SETRANGE("Production BOM No.","No.");
               ProdBOMLine.DELETEALL(TRUE);

               ProdBOMVersion.SETRANGE("Production BOM No.","No.");
               ProdBOMVersion.DELETEALL;

               MfgComment.SETRANGE("Table Name",MfgComment."Table Name"::"Production BOM Header");
               MfgComment.SETRANGE("No.","No.");
               MfgComment.DELETEALL;
             END;

    OnRename=BEGIN
               IF Status = Status::Certified THEN
                 ERROR(Text002,TABLECAPTION,FIELDCAPTION(Status),FORMAT(Status));
             END;

    CaptionML=[DEU=Fert.-StÅcklistenkopf;
               ENU=Production BOM Header;
               NLD=Productiestuklijstkop;
               NOR=Prod.stykklistehode;
               SVE=Prod.strukturhuvud];
    LookupPageID=Page99000787;
    DrillDownPageID=Page99000787;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;CaptionML=[DEU=Nr.;
                                                              ENU=No.;
                                                              NLD=Nr.;
                                                              NOR=Nr.;
                                                              SVE=Nr] }
    { 10  ;   ;Description         ;Text100       ;OnValidate=BEGIN
                                                                "Search Name" := Description;
                                                              END;

                                                   CaptionML=[DEU=Beschreibung;
                                                              ENU=Description;
                                                              NLD=Omschrijving;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 11  ;   ;Description 2       ;Text50        ;CaptionML=[DEU=Beschreibung 2;
                                                              ENU=Description 2;
                                                              NLD=Omschrijving 2;
                                                              NOR=Beskrivelse 2;
                                                              SVE=Beskrivning 2] }
    { 12  ;   ;Search Name         ;Code100       ;CaptionML=[DEU=Suchbegriff;
                                                              ENU=Search Name;
                                                              NLD=Zoeknaam;
                                                              NOR=Sõkenavn;
                                                              SVE=Sîknamn] }
    { 21  ;   ;Unit of Measure Code;Code10        ;TableRelation="Unit of Measure";
                                                   OnValidate=VAR
                                                                Item@1000 : Record 27;
                                                                ItemUnitOfMeasure@1001 : Record 5404;
                                                              BEGIN
                                                                IF Status = Status::Certified THEN
                                                                  FIELDERROR(Status);
                                                                Item.SETCURRENTKEY("Production BOM No.");
                                                                Item.SETRANGE("Production BOM No.","No.");
                                                                IF Item.FINDSET THEN
                                                                  REPEAT
                                                                    ItemUnitOfMeasure.GET(Item."No.","Unit of Measure Code");
                                                                  UNTIL Item.NEXT = 0;
                                                              END;

                                                   CaptionML=[DEU=Einheitencode;
                                                              ENU=Unit of Measure Code;
                                                              NLD=Eenheidscode;
                                                              NOR=Enhetskode;
                                                              SVE=Enhetskod] }
    { 22  ;   ;Low-Level Code      ;Integer       ;CaptionML=[DEU=StÅcklistenebene;
                                                              ENU=Low-Level Code;
                                                              NLD=Low-Levelcode;
                                                              NOR=LavnivÜkode;
                                                              SVE=LÑgsta-nivÜ-kod];
                                                   Editable=No }
    { 25  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Manufacturing Comment Line" WHERE (Table Name=CONST(Production BOM Header),
                                                                                                         No.=FIELD(No.)));
                                                   CaptionML=[DEU=Bemerkung;
                                                              ENU=Comment;
                                                              NLD=Opmerking;
                                                              NOR=Merknad;
                                                              SVE=Kommentar];
                                                   Editable=No }
    { 40  ;   ;Creation Date       ;Date          ;CaptionML=[DEU=Errichtungsdatum;
                                                              ENU=Creation Date;
                                                              NLD=Gemaakt op;
                                                              NOR=Opprettet den;
                                                              SVE=UpplÑggningsdatum];
                                                   Editable=No }
    { 43  ;   ;Last Date Modified  ;Date          ;CaptionML=[DEU=Korrigiert am;
                                                              ENU=Last Date Modified;
                                                              NLD=Gewijzigd op;
                                                              NOR=Endret den;
                                                              SVE=Uppdateringsdatum];
                                                   Editable=No }
    { 45  ;   ;Status              ;Option        ;OnValidate=VAR
                                                                PlanningAssignment@1000 : Record 99000850;
                                                                MfgSetup@1004 : Record 99000765;
                                                                ProdBOMCheck@1001 : Codeunit 99000769;
                                                              BEGIN
                                                                IF (Status <> xRec.Status) AND (Status = Status::Certified) THEN BEGIN
                                                                  MfgSetup.LOCKTABLE;
                                                                  MfgSetup.GET;
                                                                  ProdBOMCheck.ProdBOMLineCheck("No.",'');
                                                                  "Low-Level Code" := 0;
                                                                  ProdBOMCheck.RUN(Rec);
                                                                  PlanningAssignment.NewBOM("No.");
                                                                END;
                                                                IF Status = Status::Closed THEN BEGIN
                                                                  IF CONFIRM(
                                                                       Text001,FALSE)
                                                                  THEN BEGIN
                                                                    ProdBOMVersion.SETRANGE("Production BOM No.","No.");
                                                                    IF ProdBOMVersion.FIND('-') THEN
                                                                      REPEAT
                                                                        ProdBOMVersion.Status := ProdBOMVersion.Status::Closed;
                                                                        ProdBOMVersion.MODIFY;
                                                                      UNTIL ProdBOMVersion.NEXT = 0;
                                                                  END ELSE
                                                                    Status := xRec.Status;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Status;
                                                              ENU=Status;
                                                              NLD=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=[DEU=Neu,Zertifiziert,In Entwicklung,Abgelaufen;
                                                                    ENU=New,Certified,Under Development,Closed;
                                                                    NLD=Nieuw,Gecertificeerd,In ontwikkeling,Afgesloten;
                                                                    NOR=Ny,Sertifisert,Under utvikling,Lukket;
                                                                    SVE=Ny,GodkÑnd,Under utveckling,Avslutad];
                                                   OptionString=New,Certified,Under Development,Closed }
    { 50  ;   ;Version Nos.        ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=[DEU=Versionsnummern;
                                                              ENU=Version Nos.;
                                                              NLD=Versienrs.;
                                                              NOR=Versjonsnr.;
                                                              SVE=Version nr-serie] }
    { 51  ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=[DEU=Nummernserie;
                                                              ENU=No. Series;
                                                              NLD=Nr.-reeks;
                                                              NOR=Nr.serie;
                                                              SVE=Nr-serie];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Search Name                              }
    {    ;Description                              }
    {    ;Status                                   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Diese FertigungsstÅckliste wird bei Artikeln verwendet.;ENU=This Production BOM is being used on Items.;NLD=Deze productiestuklijst wordt gebruikt voor artikelen.;NOR=Denne produksjonsstykklisten brukes pÜ varer.;SVE=Denna prod.struktur anvÑnds fîr artiklar.';
      Text001@1001 : TextConst 'DEU=Alle angeknÅpften Versionen der StÅckliste werden auf Abgelaufen gestellt. Status auf abgelaufen stellen?;ENU=All versions attached to the BOM will be closed. Close BOM?;NLD=Alle aan de stuklijst gekoppelde versies worden afgesloten. Wilt u de stuklijst afsluiten ?;NOR=Alle versjoner som er knyttet til stykklisten, lukkes. Lukke stykklisten?;SVE=Alla versioner knutna till denna struktur kommer att avslutas. Ska strukturen avslutas?';
      MfgSetup@1002 : Record 99000765;
      ProdBOMHeader@1004 : Record 99000771;
      ProdBOMVersion@1005 : Record 99000779;
      ProdBOMLine@1006 : Record 99000772;
      MfgComment@1007 : Record 99000770;
      NoSeriesMgt@1008 : Codeunit 396;
      Text002@1009 : TextConst 'DEU=Sie kînnen den %1 nicht umbenennen, wenn der %2 %3 ist.;ENU=You cannot rename the %1 when %2 is %3.;NLD=U kunt de %1 niet hernoemen als %2 %3 is.;NOR=Du kan ikke gi %1 nytt navn nÜr %2 er %3.;SVE=Du kan inte byta namn pÜ %1 nÑr %2 Ñr %3.';

    [External]
    PROCEDURE AssistEdit@2(OldProdBOMHeader@1000 : Record 99000771) : Boolean;
    VAR
      SeriesSelected@1001 : Boolean;
      IsHandled@1002 : Boolean;
    BEGIN
      OnBeforeAsistEdit(Rec,OldProdBOMHeader,SeriesSelected,IsHandled);
      IF IsHandled THEN
        EXIT(SeriesSelected);

      WITH ProdBOMHeader DO BEGIN
        ProdBOMHeader := Rec;
        MfgSetup.GET;
        MfgSetup.TESTFIELD("Production BOM Nos.");
        IF NoSeriesMgt.SelectSeries(MfgSetup."Production BOM Nos.",OldProdBOMHeader."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("No.");
          Rec := ProdBOMHeader;
          EXIT(TRUE);
        END;
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeAsistEdit@1(VAR ProductionBOMHeader@1000 : Record 99000771;OldProductionBOMHeader@1001 : Record 99000771;VAR SeriesSelected@1002 : Boolean;VAR IsHandled@1003 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

