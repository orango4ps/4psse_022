OBJECT Table 11072241 Serv. Surv. Summary Sheet
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ServSurvSummarySheet@1100528600 : Record 11072241;
             BEGIN
               IF (Code = '') AND ("Summary Kind" = "Summary Kind"::Survey) THEN BEGIN
                 ServSurvSummarySheet.SETRANGE("Summary Kind", "Summary Kind");
                 ServSurvSummarySheet.SETRANGE("Survey No.", "Survey No.");
                 IF ServSurvSummarySheet.FINDLAST THEN
                   Code := INCSTR(ServSurvSummarySheet.Code)
                 ELSE
                   Code := '001';
               END;
             END;

    OnDelete=VAR
               ServSurvSummarySheetLine@1100528600 : Record 11072242;
             BEGIN
               ServSurvSummarySheetLine.SETRANGE("Summary Kind", "Summary Kind");
               ServSurvSummarySheetLine.SETRANGE("Survey No.", "Survey No.");
               ServSurvSummarySheetLine.SETRANGE(Code, Code);
               ServSurvSummarySheetLine.SETRANGE("Summary Sheet Seq. No.", "Seq. No.");
               ServSurvSummarySheetLine.DELETEALL(TRUE);
             END;

    CaptionML=ENU=Service Survey Summary Sheet;
    LookupPageID=Page11229408;
    DrillDownPageID=Page11229408;
  }
  FIELDS
  {
    { 10  ;   ;Summary Kind        ;Option        ;CaptionML=ENU=Summary Kind;
                                                   OptionCaptionML=ENU=Standard,Survey;
                                                   OptionString=Standard,Survey;
                                                   Editable=No }
    { 30  ;   ;Code                ;Code10        ;OnValidate=VAR
                                                                ServSurvSummarySheet@1100528600 : Record 11072241;
                                                                ServSurvSummarySheetLine@1100528601 : Record 11072242;
                                                              BEGIN
                                                                 IF Code <> xRec.Code THEN BEGIN
                                                                  ServSurvSummarySheetLine.SETRANGE("Summary Kind", "Summary Kind");
                                                                  ServSurvSummarySheetLine.SETRANGE("Survey No.", "Survey No.");
                                                                  ServSurvSummarySheetLine.SETRANGE(Code, xRec.Code);
                                                                  ServSurvSummarySheetLine.SETRANGE("Summary Sheet Seq. No.", "Seq. No.");
                                                                  WHILE ServSurvSummarySheetLine.FINDSET(TRUE, TRUE) DO
                                                                    ServSurvSummarySheetLine.RENAME("Summary Kind", "Survey No.", Code, "Seq. No.", ServSurvSummarySheetLine."Row No.");
                                                                 END;
                                                                IF "Seq. No." = 0 THEN BEGIN
                                                                  ServSurvSummarySheet.SETRANGE("Summary Kind", "Summary Kind");
                                                                  ServSurvSummarySheet.SETRANGE(Code, Code);
                                                                  ServSurvSummarySheet.SETRANGE("Survey No.", "Survey No.");
                                                                  IF ServSurvSummarySheet.FINDLAST THEN
                                                                    "Seq. No." := ServSurvSummarySheet."Seq. No." +1
                                                                  ELSE
                                                                    "Seq. No." := 1;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 40  ;   ;Survey No.          ;Code20        ;TableRelation=IF (Summary Kind=CONST(Survey)) "Service Survey".No.;
                                                   OnValidate=VAR
                                                                ServiceSurvey@1100525000 : Record 11071803;
                                                              BEGIN
                                                                IF "Survey No." <> '' THEN BEGIN
                                                                  TESTFIELD("Summary Kind", "Summary Kind"::Survey);
                                                                  ServiceSurvey.GET("Survey No.");
                                                                  VALIDATE("Index Date", ServiceSurvey."Reference Date");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Survey No.;
                                                              NOR=Kartlegging nr.;
                                                              SVE=Kartl„ggningsnr];
                                                   Editable=No }
    { 45  ;   ;Seq. No.            ;Integer       ;OnValidate=VAR
                                                                ServSurvSummarySheetLine@1100528600 : Record 11072242;
                                                              BEGIN
                                                                 IF "Seq. No." <> xRec."Seq. No." THEN BEGIN
                                                                  ServSurvSummarySheetLine.SETRANGE("Summary Kind", "Summary Kind");
                                                                  ServSurvSummarySheetLine.SETRANGE("Survey No.", "Survey No.");
                                                                  ServSurvSummarySheetLine.SETRANGE(Code, Code);
                                                                  ServSurvSummarySheetLine.SETRANGE("Summary Sheet Seq. No.", xRec."Seq. No.");
                                                                  WHILE ServSurvSummarySheetLine.FINDSET(TRUE, TRUE) DO
                                                                    ServSurvSummarySheetLine.RENAME("Summary Kind", "Survey No.", Code, "Seq. No.", ServSurvSummarySheetLine."Row No.");
                                                                 END;
                                                              END;

                                                   CaptionML=[ENU=Seq. No.;
                                                              NOR=Sekv. nr;
                                                              SVE=Sekv. nr];
                                                   MinValue=1;
                                                   NotBlank=Yes }
    { 50  ;   ;Index Date          ;Date          ;OnValidate=BEGIN
                                                                IF MODIFY(TRUE) THEN
                                                                  UpdateRates;
                                                              END;

                                                   CaptionML=[ENU=Index Date;
                                                              NOR=Indeksdato;
                                                              SVE=Indexdatum] }
    { 60  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 70  ;   ;Summary Type        ;Option        ;CaptionML=[ENU=Summary Type;
                                                              NOR=Sammendragstype;
                                                              SVE=Sammandragstyp];
                                                   OptionCaptionML=[ENU=Cost,Sales;
                                                                    NOR=Kostnad,Salg;
                                                                    SVE=Kostnad,F”rs„ljning];
                                                   OptionString=Cost,Sales }
    { 90  ;   ;Summary Sheet for Surcharge;Boolean;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Survey"."Summary Sheet for Surcharge" WHERE (No.=FIELD(Survey No.)));
                                                   CaptionML=ENU=Summary Sheet for Surcharge;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Summary Kind,Survey No.,Code,Seq. No.   ;Clustered=Yes }
    {    ;Summary Kind,Survey No.,Code,Index Date  }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE CopyFrom@1100528600(IFromServSurvSummarySheet@1100528607 : Record 11072241;IndexDate@1100525001 : Date);
    VAR
      FromServSurvSummarySheetLine@1100528604 : Record 11072242;
      ToServSurvSummarySheetLine@1100528605 : Record 11072242;
    BEGIN
      IF (Description = '') OR ("Index Date" = 0D) THEN BEGIN
        IF Description = '' THEN
          VALIDATE(Description, IFromServSurvSummarySheet.Description);
        IF ("Index Date" = 0D) AND (IndexDate <> 0D) THEN
          VALIDATE("Index Date", IndexDate);
        MODIFY(TRUE);
      END;

      FromServSurvSummarySheetLine.SETRANGE("Summary Kind", IFromServSurvSummarySheet."Summary Kind");
      FromServSurvSummarySheetLine.SETRANGE("Survey No.", IFromServSurvSummarySheet."Survey No.");
      FromServSurvSummarySheetLine.SETRANGE(Code, IFromServSurvSummarySheet.Code);
      FromServSurvSummarySheetLine.SETRANGE("Summary Sheet Seq. No.", IFromServSurvSummarySheet."Seq. No.");
      IF FromServSurvSummarySheetLine.FINDSET THEN
        REPEAT
          ToServSurvSummarySheetLine := FromServSurvSummarySheetLine;
          ToServSurvSummarySheetLine."Summary Kind" := "Summary Kind";
          ToServSurvSummarySheetLine.Code := Code;
          ToServSurvSummarySheetLine."Survey No." := "Survey No.";
          ToServSurvSummarySheetLine."Summary Sheet Seq. No." := "Seq. No.";
          IF ToServSurvSummarySheetLine."Cost Type" = ToServSurvSummarySheetLine."Cost Type"::Labor THEN
            ToServSurvSummarySheetLine.VALIDATE("Rate Code");
          ToServSurvSummarySheetLine.INSERT(TRUE);
        UNTIL FromServSurvSummarySheetLine.NEXT = 0;

      ReCalcSummaryLines;
    END;

    PROCEDURE ReCalcSummaryLines@1210190002();
    VAR
      ServSurvSummarySheetLine@1210190000 : Record 11072242;
      ServiceSurvey@1100528500 : Record 11071803;
    BEGIN
      IF NOT ServiceSurvey.GET("Survey No.") THEN
        EXIT;

      ServSurvSummarySheetLine.SETRANGE("Summary Kind", "Summary Kind");
      ServSurvSummarySheetLine.SETRANGE(Code, Code);
      ServSurvSummarySheetLine.SETRANGE("Survey No.", "Survey No.");
      ServSurvSummarySheetLine.SETRANGE("Summary Sheet Seq. No.", "Seq. No.");
      IF ServSurvSummarySheetLine.FINDSET THEN BEGIN
        REPEAT
          ServSurvSummarySheetLine.CalcTotals;
          ServSurvSummarySheetLine.MODIFY;
        UNTIL ServSurvSummarySheetLine.NEXT = 0;
      END;
    END;

    PROCEDURE NewerVersionExists@1100528601() : Boolean;
    VAR
      ServSurvSummarySheet@1100528600 : Record 11072241;
    BEGIN
      ServSurvSummarySheet := Rec;
      ServSurvSummarySheet.SETCURRENTKEY("Summary Kind", "Survey No.", Code, "Index Date");
      ServSurvSummarySheet.SETRECFILTER;
      ServSurvSummarySheet.SETRANGE("Seq. No.");
      ServSurvSummarySheet.SETFILTER("Index Date", '>=%1', "Index Date");
      IF ServSurvSummarySheet.FINDLAST THEN BEGIN
        IF ServSurvSummarySheet."Index Date" > "Index Date" THEN
          EXIT(TRUE);
        IF ServSurvSummarySheet."Seq. No." > "Seq. No." THEN
          EXIT(TRUE);
      END;
    END;

    PROCEDURE GetSummaryTotal@1100528602() : Decimal;
    VAR
      ServSurvSummarySheetLine@1100528600 : Record 11072242;
    BEGIN
      CALCFIELDS("Summary Sheet for Surcharge");
      ServSurvSummarySheetLine.SETRANGE("Summary Kind", ServSurvSummarySheetLine."Summary Kind"::Survey);
      ServSurvSummarySheetLine.SETRANGE("Survey No.", "Survey No.");
      ServSurvSummarySheetLine.SETRANGE(Code, Code);
      ServSurvSummarySheetLine.SETRANGE("Summary Sheet Seq. No.", "Seq. No.");
      IF "Summary Sheet for Surcharge" THEN
        ServSurvSummarySheetLine.SETRANGE("Amount for Surcharge", FALSE);
      ServSurvSummarySheetLine.CALCSUMS(Amount);
      EXIT(ServSurvSummarySheetLine.Amount);
    END;

    PROCEDURE GetSummarySurcharge@1100528604() : Decimal;
    VAR
      ServSurvSummarySheetLine@1100528600 : Record 11072242;
    BEGIN
      ServSurvSummarySheetLine.SETRANGE("Summary Kind", "Summary Kind");
      ServSurvSummarySheetLine.SETRANGE("Survey No.", "Survey No.");
      ServSurvSummarySheetLine.SETRANGE(Code, Code);
      ServSurvSummarySheetLine.SETRANGE("Summary Sheet Seq. No.", "Seq. No.");
      ServSurvSummarySheetLine.SETRANGE("Amount for Surcharge", TRUE);
      IF NOT ServSurvSummarySheetLine.ISEMPTY THEN BEGIN
        ServSurvSummarySheetLine.CALCSUMS(Amount, Subtotal);
        EXIT(ServSurvSummarySheetLine.Amount + ServSurvSummarySheetLine.Subtotal);
      END;
    END;

    LOCAL PROCEDURE UpdateRates@1100528603();
    VAR
      ServSurvSummarySheetLine@1100528600 : Record 11072242;
    BEGIN
      ServSurvSummarySheetLine.SETRANGE("Summary Kind", "Summary Kind");
      ServSurvSummarySheetLine.SETRANGE("Survey No.", "Survey No.");
      ServSurvSummarySheetLine.SETRANGE(Code, Code);
      ServSurvSummarySheetLine.SETRANGE("Summary Sheet Seq. No.", "Seq. No.");
      ServSurvSummarySheetLine.SETRANGE("Cost Type", ServSurvSummarySheetLine."Cost Type"::Labor);
      ServSurvSummarySheetLine.SETFILTER("Rate Code", '<>%1', '');
      IF ServSurvSummarySheetLine.FINDSET THEN
        REPEAT
          ServSurvSummarySheetLine.VALIDATE("Rate Code");
          ServSurvSummarySheetLine.MODIFY(TRUE);
        UNTIL ServSurvSummarySheetLine.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

