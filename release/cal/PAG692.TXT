OBJECT Page 692 Import Style Sheet
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Import Style Sheet;
               NOR=Importer stilark;
               SVE=Importera formatmall];
    DataCaptionExpr=AllObjWithCaption."Object Caption";
    PageType=StandardDialog;
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                CaptionML=[ENU=General;
                           NOR=Generelt;
                           SVE=Allm„nt] }

    { 5   ;2   ;Field     ;
                CaptionML=[ENU=Style Sheet;
                           NOR=Stilark;
                           SVE=Formatmall];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=FileName;
                OnValidate=VAR
                             TempBlob@1001 : Record 99008535;
                           BEGIN
                             IF NOT FileMgt.ClientFileExists(FileName) THEN
                               ERROR(Text002,FileName);

                             FileMgt.BLOBImport(TempBlob,FileName);
                             StyleSheet."Style Sheet" := TempBlob.Blob;
                           END;

                OnAssistEdit=BEGIN
                               LookupStyleSheet;
                             END;
                              }

    { 2   ;2   ;Field     ;
                CaptionML=[ENU=Name;
                           NOR=Navn;
                           SVE=Namn];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Description;
                OnValidate=BEGIN
                             StyleSheet.Name := Description;
                           END;
                            }

    { 8   ;2   ;Field     ;
                CaptionML=[ENU=Send-to Program;
                           NOR=Send til-program;
                           SVE=M†lprogram];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ApplicationName;
                OnValidate=BEGIN
                             FindApplication;
                           END;

                OnLookup=BEGIN
                           LookupApplication;
                         END;
                          }

  }
  CODE
  {
    VAR
      SendToProgram@1002 : Record 2000000065;
      StyleSheet@1006 : Record 2000000066;
      AllObjWithCaption@1010 : Record 2000000058;
      FileMgt@1011 : Codeunit 419;
      FileName@1000 : Text;
      Description@1003 : Text[1024];
      ApplicationName@1001 : Text[250];
      ObjType@1004 : Integer;
      ObjectID@1005 : Integer;
      Text001@1007 : TextConst 'ENU=No %1 found.;NOR=Fant ikke %1.;SVE=%1 hittades inte.';
      Text002@1008 : TextConst 'ENU=The file %1 could not be found.;NOR=Fant ikke filen %1.;SVE=Det gick inte att hitta filen %1.';

    [External]
    PROCEDURE SetObjectID@2(NewObjectType@1000 : Integer;NewObjectID@1001 : Integer;NewApplicationID@1002 : GUID);
    BEGIN
      IF NOT SendToProgram.GET(NewApplicationID) THEN
        SendToProgram.FINDFIRST;
      ApplicationName := SendToProgram.Name;
      ObjType := NewObjectType;
      ObjectID := NewObjectID;
      IF NOT AllObjWithCaption.GET(AllObjWithCaption."Object Type"::Page,ObjectID) THEN
        AllObjWithCaption.INIT;
      StyleSheet.INIT;
      StyleSheet."Style Sheet ID" := CREATEGUID;
      StyleSheet."Object ID" := ObjectID;
      StyleSheet."Object Type" := ObjType;
      StyleSheet."Program ID" := SendToProgram."Program ID";
      StyleSheet.Date := TODAY;
    END;

    [External]
    PROCEDURE GetStyleSheet@1(VAR ReturnStyleSheet@1000 : Record 2000000066);
    BEGIN
      ReturnStyleSheet := StyleSheet;
    END;

    LOCAL PROCEDURE LookupApplication@5();
    VAR
      SendToPrograms@1000 : Page 691;
    BEGIN
      SendToPrograms.LOOKUPMODE := TRUE;
      SendToPrograms.SETRECORD(SendToProgram);
      IF SendToPrograms.RUNMODAL = ACTION::LookupOK THEN BEGIN
        SendToPrograms.GETRECORD(SendToProgram);
        StyleSheet."Program ID" := SendToProgram."Program ID";
        ApplicationName := SendToProgram.Name;
      END;
    END;

    LOCAL PROCEDURE FindApplication@6();
    BEGIN
      SendToProgram.RESET;
      IF ApplicationName = '' THEN BEGIN
        SendToProgram.FINDFIRST;
        ApplicationName := SendToProgram.Name;
        EXIT;
      END;
      SendToProgram.SETRANGE(Name,ApplicationName);
      IF SendToProgram.FINDFIRST THEN BEGIN
        ApplicationName := SendToProgram.Name;
        EXIT;
      END;
      SendToProgram.SETFILTER(Name,'@*' + ApplicationName + '*');
      IF SendToProgram.FINDFIRST THEN BEGIN
        ApplicationName := SendToProgram.Name;
        EXIT;
      END;
      ERROR(Text001,SendToProgram.TABLECAPTION);
    END;

    LOCAL PROCEDURE LookupStyleSheet@8();
    VAR
      TempBlob@1001 : Record 99008535;
      PathLength@1000 : Integer;
    BEGIN
      FileName := FileMgt.BLOBImport(TempBlob,'*.xsl*');

      IF FileName = '' THEN BEGIN
        CLEAR(StyleSheet."Style Sheet");
        CLEAR(Description);
        EXIT;
      END;
      PathLength := STRLEN(FileMgt.GetDirectoryName(FileName));
      // If there is a path in the filename, we should cut away the path.
      IF PathLength <> 0 THEN
        Description := COPYSTR(FileName,PathLength + 2)
      ELSE
        Description := COPYSTR(FileName,1);
      StyleSheet.Name := Description;
      StyleSheet."Style Sheet" := TempBlob.Blob;
    END;

    BEGIN
    END.
  }
}

