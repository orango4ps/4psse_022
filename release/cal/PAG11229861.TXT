OBJECT Page 11229861 Copy Plant Stnd. Maint. Scheme
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Standard Wartungsschema kopieren;
               ENU=Copy Standard Maintenance Scheme;
               NLD=Standaard onderhoudsschema kopiâren;
               NOR=Kopier vedlikeholdskjema;
               SVE=Kopiera underhÜllsschema];
    SourceTable=Table11072673;
    DataCaptionExpr='';
    PageType=StandardDialog;
    OnOpenPage=BEGIN
                 FromMaintenanceScheme := Code;
                 ToMaintenanceScheme := FromMaintenanceScheme;
                 FromMaintenanceSchemeDescr := Description;
                 ToMaintenanceSchemeDescr := FromMaintenanceSchemeDescr;
                 ReferenceDate := TODAY;
                 DetermineFromIndexDate;
                 ToIndexDate := 0D;
                 ToMaintenanceSchemeDescrEditable := (FromMaintenanceScheme <> ToMaintenanceScheme);
               END;

    OnQueryClosePage=BEGIN
                       CloseActionOK := (CloseAction = ACTION::OK);
                       IF CloseActionOK THEN BEGIN
                         ValidateFromMaintenanceScheme;
                         ValidateReferenceDate;
                         ValidateToMaintenanceScheme;
                         ValidateToMaintenanceSchemeDescr;
                         ValidateToIndexDate;
                       END;
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100528601;1;Group  ;
                CaptionML=[DEU=Kopieren von;
                           ENU=Copy from;
                           NLD=Kopiâren van;
                           NOR=Kopier fra;
                           SVE=Kopiera frÜn];
                GroupType=Group }

    { 6   ;2   ;Field     ;
                CaptionML=[NOR=Vedlikeholdsskjema;
                           SVE=UnderhÜllsschema];
                NotBlank=Yes;
                SourceExpr=FromMaintenanceScheme;
                CaptionClass=gPlantMaintSchemeVersion.FIELDCAPTION("Maintenance Scheme");
                TableRelation="Plant Stnd. Maintenance Scheme";
                OnValidate=BEGIN
                             ValidateFromMaintenanceScheme;
                           END;
                            }

    { 8   ;2   ;Field     ;
                CaptionML=[NOR=Beskrivelse av vedlikeholdskjema;
                           SVE=Beskrivning av underhÜllsschema];
                SourceExpr=FromMaintenanceSchemeDescr;
                CaptionClass=gPlantMaintSchemeVersion.FIELDCAPTION("Maintenance Scheme Description");
                Editable=FALSE;
                QuickEntry=FALSE }

    { 1100528600;2;Field  ;
                CaptionML=[DEU=Stichtag;
                           ENU=Reference Date;
                           NLD=Peildatum;
                           NOR=Referansedato;
                           SVE=Referensdatum];
                NotBlank=Yes;
                SourceExpr=ReferenceDate;
                OnValidate=BEGIN
                             ValidateReferenceDate;
                           END;
                            }

    { 1100529000;2;Field  ;
                SourceExpr=FromIndexDate;
                CaptionClass=gPlantMaintSchemeVersion.FIELDCAPTION("Index Date");
                Editable=FALSE;
                QuickEntry=FALSE }

    { 1100528602;1;Group  ;
                CaptionML=[DEU=Kopieren in;
                           ENU=Copy to;
                           NLD=Kopiâren naar;
                           NOR=Kopier til;
                           SVE=Kopiera till];
                GroupType=Group }

    { 11  ;2   ;Field     ;
                CaptionML=[NOR=Vedlikeholdsskjema;
                           SVE=UnderhÜllsschema];
                NotBlank=Yes;
                SourceExpr=ToMaintenanceScheme;
                CaptionClass=gPlantMaintSchemeVersion.FIELDCAPTION("Maintenance Scheme");
                OnValidate=BEGIN
                             ValidateToMaintenanceScheme;
                           END;
                            }

    { 12  ;2   ;Field     ;
                CaptionML=[NOR=Beskrivelse av vedlikeholdskjema;
                           SVE=Beskrivning av underhÜllsschema];
                NotBlank=Yes;
                SourceExpr=ToMaintenanceSchemeDescr;
                CaptionClass=gPlantMaintSchemeVersion.FIELDCAPTION("Maintenance Scheme Description");
                Editable=ToMaintenanceSchemeDescrEditable;
                OnValidate=BEGIN
                             ValidateToMaintenanceSchemeDescr;
                           END;
                            }

    { 1100525000;2;Field  ;
                CaptionML=[NOR=Indeksdato;
                           SVE=Indexdatum];
                NotBlank=Yes;
                SourceExpr=ToIndexDate;
                CaptionClass=gPlantMaintSchemeVersion.FIELDCAPTION("Index Date");
                OnValidate=BEGIN
                             ValidateToIndexDate;
                           END;
                            }

  }
  CODE
  {
    VAR
      gPlantMaintSchemeVersion@1100529004 : Record 11072674;
      FromMaintenanceScheme@11012029 : Code[20];
      ToMaintenanceScheme@11012028 : Code[20];
      FromMaintenanceSchemeDescr@11012005 : Text[50];
      ToMaintenanceSchemeDescr@11012004 : Text[50];
      Text000a@1100529002 : TextConst 'DEU=von;ENU=from;NLD=van';
      Text000b@1100529003 : TextConst 'DEU=zu;ENU=to;NLD=naar';
      Text001@1100525001 : TextConst 'DEU=%1 (%2) ''%3'' bereits vorhanden;ENU=%1 (%2) ''%3'' already exists;NLD=%1 (%2) ''%3'' bestaat reeds;NOR=%1 %2 finnes allerede.;SVE=%1 %2 finns redan.';
      ReferenceDate@1100528600 : Date;
      FromIndexDate@1100529000 : Date;
      ToIndexDate@1100525000 : Date;
      CloseActionOK@1100528400 : Boolean;
      ToMaintenanceSchemeDescrEditable@1100529001 : Boolean INDATASET;
      Text002@1100529005 : TextConst 'DEU=%1 (%2) ''%3'' nicht vorhanden;ENU=%1 (%2) ''%3'' does not exist;NLD=%1 (%2) ''%3'' bestaat niet';
      Text003@1100529006 : TextConst 'DEU=Bei %1 (%2) ''%3'' wurde kein %5 fÅr Stichtag ''%4 gefunden, %5 (%2) kann nicht bestimmt werden;ENU=On %1 (%2) ''%3'' no %5 for Reference Date ''%4, %5 (%2) can be determined;NLD=Bij %1 (%2) ''%3'' geen %5 gevonden voor Peildatum ''%4, %5 (%2) kan niet bepaald worden';
      Text004@1100529007 : TextConst 'DEU=%1 (%2) darf nicht leer sein;ENU=%1 (%2) may not be empty;NLD=%1 (%2) mag niet leeg zijn';
      Text005@1100529008 : TextConst 'DEU=%1 (%2) ''%3'' muss grîsser zein als %1 (%4) ''%5'';ENU=%1 (%2) ''%3'' must be greater then %1 (%4) ''%5'';NLD=%1 (%2) ''%3'' moet groter zijn dan %1 (%4) ''%5''';
      Text006@1100529009 : TextConst 'DEU=%1 kopiert;ENU=%1 copied;NLD=%1 gekopieerd';

    LOCAL PROCEDURE ValidateFromMaintenanceScheme@1100529001();
    VAR
      PlantStndMaintenanceScheme@1100529000 : Record 11072673;
    BEGIN
      IF NOT PlantStndMaintenanceScheme.GET(FromMaintenanceScheme) THEN
        ERROR(Text002, gPlantMaintSchemeVersion.FIELDCAPTION("Maintenance Scheme"), Text000a, FromMaintenanceScheme);
      FromMaintenanceSchemeDescr := PlantStndMaintenanceScheme.Description;
      IF FromMaintenanceScheme = ToMaintenanceScheme THEN
        ToMaintenanceSchemeDescr := FromMaintenanceSchemeDescr;
      ToMaintenanceSchemeDescrEditable := (FromMaintenanceScheme <> ToMaintenanceScheme);
      DetermineFromIndexDate;
    END;

    LOCAL PROCEDURE ValidateReferenceDate@1100529003();
    BEGIN
      DetermineFromIndexDate;
      IF FromIndexDate = 0D THEN BEGIN
        ERROR(Text003,
          gPlantMaintSchemeVersion.FIELDCAPTION("Maintenance Scheme"), Text000a, FromMaintenanceScheme,
          ReferenceDate, gPlantMaintSchemeVersion.FIELDCAPTION("Index Date"));
      END;
    END;

    LOCAL PROCEDURE ValidateToMaintenanceScheme@1100485004();
    VAR
      PlantStndMaintenanceScheme@1100528600 : Record 11072673;
    BEGIN
      IF FromMaintenanceScheme <> ToMaintenanceScheme THEN BEGIN
        IF PlantStndMaintenanceScheme.GET(ToMaintenanceScheme) THEN
          ERROR(Text001, gPlantMaintSchemeVersion.FIELDCAPTION("Maintenance Scheme"), Text000b, ToMaintenanceScheme);
      END;
      IF FromMaintenanceScheme = ToMaintenanceScheme THEN
        ToMaintenanceSchemeDescr := FromMaintenanceSchemeDescr;
      ToMaintenanceSchemeDescrEditable := (FromMaintenanceScheme <> ToMaintenanceScheme);
    END;

    LOCAL PROCEDURE ValidateToMaintenanceSchemeDescr@1100529005();
    BEGIN
      IF (FromMaintenanceScheme <> ToMaintenanceScheme) AND (ToMaintenanceSchemeDescr = '')THEN
        ERROR(Text004, gPlantMaintSchemeVersion.FIELDCAPTION("Maintenance Scheme Description"), Text000b);
    END;

    LOCAL PROCEDURE ValidateToIndexDate@1100529004();
    BEGIN
      IF ToIndexDate = 0D THEN
        ERROR(Text004, gPlantMaintSchemeVersion.FIELDCAPTION("Index Date"), Text000b);
      IF (FromMaintenanceScheme = ToMaintenanceScheme) AND (ToIndexDate <= FromIndexDate) THEN
        ERROR(Text005, gPlantMaintSchemeVersion.FIELDCAPTION("Index Date"), Text000b, ToIndexDate, Text000a, FromIndexDate);
    END;

    LOCAL PROCEDURE DetermineFromIndexDate@1100529002();
    VAR
      PlantMaintSchemeVersion@1100529000 : Record 11072674;
    BEGIN
      FromIndexDate := 0D;
      IF (FromMaintenanceScheme <> '') AND (ReferenceDate <> 0D) THEN BEGIN
        PlantMaintSchemeVersion.SETRANGE(Level, PlantMaintSchemeVersion.Level::"Maintenance Scheme");
        PlantMaintSchemeVersion.SETRANGE("Maintenance Scheme", FromMaintenanceScheme);
        PlantMaintSchemeVersion.SETFILTER("Index Date", '<=%1', ReferenceDate);
        IF PlantMaintSchemeVersion.FINDLAST THEN
          FromIndexDate := PlantMaintSchemeVersion."Index Date";
      END;
    END;

    PROCEDURE StdDialogRunModal@1100528404();
    BEGIN
      IF StdDialogConfirm THEN
        StdDialogDoAction;
    END;

    LOCAL PROCEDURE StdDialogConfirm@1100528400() : Boolean;
    BEGIN
      CurrPage.RUNMODAL;
      EXIT(CloseActionOK);
    END;

    LOCAL PROCEDURE StdDialogDoAction@1100528401();
    BEGIN
      StartProcess;
    END;

    LOCAL PROCEDURE StartProcess@1100485002();
    VAR
      PlantMaintenanceManagement@1100485001 : Codeunit 11012872;
    BEGIN
      IF PlantMaintenanceManagement.CopyStandardMaintenanceScheme(
        FromMaintenanceScheme, FromIndexDate, ToMaintenanceScheme, ToMaintenanceSchemeDescr, ToIndexDate)
      THEN
        MESSAGE(Text006, gPlantMaintSchemeVersion.FIELDCAPTION("Maintenance Scheme"));
    END;

    BEGIN
    END.
  }
}

