OBJECT Codeunit 99000769 Production BOM-Check
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03;
  }
  PROPERTIES
  {
    TableNo=99000771;
    Permissions=TableData 27=r,
                TableData 99000764=r,
                TableData 99000765=r;
    OnRun=BEGIN
            Code(Rec,'');
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Artikel prÅfen          #1########## @2@@@@@@@@@@@@@;ENU=Checking Item           #1########## @2@@@@@@@@@@@@@;NLD=Controleren artikel     #1########## @2@@@@@@@@@@@@@;NOR=Kontrollerer vare       #1########## @2@@@@@@@@@@@@@;SVE=Kontrollerar artikel    #1########## @2@@@@@@@@@@@@@';
      Text001@1001 : TextConst 'DEU=Die maximale Anzahl der StÅcklistenebenen %1 wurde Åberschritten. Der Vorgang wurde bei Artikelnummer %2, StÅcklistenkopfnummer %3, StÅcklistenebene %4 angehalten.;ENU=The maximum number of BOM levels, %1, was exceeded. The process stopped at item number %2, BOM header number %3, BOM level %4.;NLD=Het maximaal aantal stuklijstniveaus van %1 is overschreden. Het proces is gestopt bij artikelnummer %2, stuklijstkopnummer %3, stuklijstniveau %4.;NOR=Maksimalt antall stykklistenivÜer, %1, er overskredet. Prosessen stoppet pÜ varenummer %2, stykklistehodenummer %3, stykklistenivÜ %4.;SVE=Det maximala antalet strukturnivÜer (%1) îverskreds. Processen stoppades vid artikelnumret %2, strukturhuvudnummer %3, strukturnivÜ %4.';
      Text003@1002 : TextConst 'DEU=Der %1 mit dem %2 %3 konnte nicht gefunden werden. Bitte ÅberprÅfen Sie %4 %5 %6 %7.;ENU=%1 with %2 %3 cannot be found. Check %4 %5 %6 %7.;NLD=Kan %1 met %2 %3 niet vinden. Controleer %4 %5 %6 %7.;NOR=Finner ikke %1 med %2 %3. Kontroller %4 %5 %6 %7.;SVE=%1 med %2 %3 kan inte hittas. Kontrollera %4 %5 %6 %7.';
      Item@1003 : Record 27;
      ItemUnitOfMeasure@1004 : Record 5404;
      RtngLine@1005 : Record 99000764;
      MfgSetup@1006 : Record 99000765;
      VersionMgt@1007 : Codeunit 99000756;
      CalcLowLevel@1008 : Codeunit 99000793;
      Window@1009 : Dialog;
      NoOfItems@1010 : Integer;
      ItemCounter@1011 : Integer;

    [External]
    PROCEDURE Code@1(VAR ProdBOMHeader@1000 : Record 99000771;VersionCode@1001 : Code[20]);
    BEGIN
      ProdBOMHeader.TESTFIELD("Unit of Measure Code");
      MfgSetup.GET;
      IF MfgSetup."Dynamic Low-Level Code" THEN BEGIN
        CalcLowLevel.SetActualProdBOM(ProdBOMHeader);
        ProdBOMHeader."Low-Level Code" := CalcLowLevel.CalcLevels(2,ProdBOMHeader."No.",ProdBOMHeader."Low-Level Code",1);
        CalcLowLevel.RecalcLowerLevels(ProdBOMHeader."No.",ProdBOMHeader."Low-Level Code",FALSE);
        ProdBOMHeader.MODIFY;
      END;

      Item.SETCURRENTKEY("Production BOM No.");
      Item.SETRANGE("Production BOM No.",ProdBOMHeader."No.");

      IF Item.FIND('-') THEN BEGIN
        IF GUIALLOWED THEN
          Window.OPEN(Text000);
        NoOfItems := Item.COUNT;
        ItemCounter := 0;
        REPEAT
          ItemCounter := ItemCounter + 1;

          IF GUIALLOWED THEN BEGIN
            Window.UPDATE(1,Item."No.");
            Window.UPDATE(2,ROUND(ItemCounter / NoOfItems * 10000,1));
          END;
          IF MfgSetup."Dynamic Low-Level Code" THEN
            CalcLowLevel.RUN(Item);
          IF Item."Routing No." <> '' THEN
            CheckBOMStructure(ProdBOMHeader."No.",VersionCode,1);
          ItemUnitOfMeasure.GET(Item."No.",ProdBOMHeader."Unit of Measure Code");
        UNTIL Item.NEXT = 0;
      END;

      OnAfterCode(ProdBOMHeader,VersionCode);
    END;

    LOCAL PROCEDURE CheckBOMStructure@2(BOMHeaderNo@1000 : Code[20];VersionCode@1001 : Code[20];Level@1002 : Integer);
    VAR
      ProdBOMHeader@1003 : Record 99000771;
      ProdBOMComponent@1004 : Record 99000772;
    BEGIN
      IF Level > 99 THEN
        ERROR(
          Text001,
          99,BOMHeaderNo,Item."Production BOM No.",Level);

      ProdBOMHeader.GET(BOMHeaderNo);

      ProdBOMComponent.SETRANGE("Production BOM No.",BOMHeaderNo);
      ProdBOMComponent.SETRANGE("Version Code",VersionCode);
      ProdBOMComponent.SETFILTER("No.",'<>%1','');

      IF ProdBOMComponent.FIND('-') THEN
        REPEAT
          CASE ProdBOMComponent.Type OF
            ProdBOMComponent.Type::Item:
              IF ProdBOMComponent."Routing Link Code" <> '' THEN BEGIN
                Item.TESTFIELD("Routing No.");
                RtngLine.SETRANGE("Routing No.",Item."Routing No.");
                RtngLine.SETRANGE("Routing Link Code",ProdBOMComponent."Routing Link Code");
                IF NOT RtngLine.FINDFIRST THEN
                  ERROR(
                    Text003,
                    RtngLine.TABLECAPTION,
                    RtngLine.FIELDCAPTION("Routing Link Code"),
                    ProdBOMComponent."Routing Link Code",
                    ProdBOMComponent.FIELDCAPTION("Production BOM No."),
                    ProdBOMComponent."Production BOM No.",
                    ProdBOMComponent.FIELDCAPTION("Line No."),
                    ProdBOMComponent."Line No.");
              END;
            ProdBOMComponent.Type::"Production BOM":
              CheckBOMStructure(
                ProdBOMComponent."No.",
                VersionMgt.GetBOMVersion(ProdBOMComponent."No.",WORKDATE,TRUE),Level + 1);
          END;
        UNTIL ProdBOMComponent.NEXT = 0;
    END;

    [External]
    PROCEDURE ProdBOMLineCheck@3(ProdBOMNo@1001 : Code[20];VersionCode@1002 : Code[20]);
    VAR
      ProdBOMLine@1000 : Record 99000772;
    BEGIN
      ProdBOMLine.SETRANGE("Production BOM No.",ProdBOMNo);
      ProdBOMLine.SETRANGE("Version Code",VersionCode);
      ProdBOMLine.SETFILTER(Type,'<>%1',ProdBOMLine.Type::" ");
      ProdBOMLine.SETRANGE("No.",'');
      IF ProdBOMLine.FINDFIRST THEN
        ProdBOMLine.FIELDERROR("No.");

      OnAfterProdBomLineCheck(ProdBOMLine,VersionCode);
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCode@4(VAR ProductionBOMHeader@1000 : Record 99000771;VersionCode@1001 : Code[20]);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterProdBomLineCheck@5(ProductionBOMLine@1000 : Record 99000772;VersionCode@1001 : Code[20]);
    BEGIN
    END;

    BEGIN
    END.
  }
}

