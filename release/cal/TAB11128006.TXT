OBJECT Table 11128006 Configurable buttons
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PSSE;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Configurable buttons;
               NOR=Konfigurerbare knapper;
               SVE=Inst„llningsbar knappar];
  }
  FIELDS
  {
    { 1   ;   ;Page ID             ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Page),
                                                                                           Object ID=FILTER(22|50|448|11012281|11072003|11072004|11125209|11128081|11128082|11128089));
                                                   CaptionML=SVE=Sid-ID }
    { 2   ;   ;Button ID           ;Integer        }
    { 10  ;   ;RunType             ;Option        ;OptionString=,Page,Report,Codeunit,XMLport }
    { 11  ;   ;RunId               ;Integer       ;TableRelation=IF (RunType=CONST(Page)) Object.ID WHERE (Type=CONST(Page))
                                                                 ELSE IF (RunType=CONST(Report)) Object.ID WHERE (Type=CONST(Report))
                                                                 ELSE IF (RunType=CONST(Codeunit)) Object.ID WHERE (Type=CONST(Codeunit))
                                                                 ELSE IF (RunType=CONST(XMLport)) Object.ID WHERE (Type=CONST(XMLport)) }
    { 12  ;   ;RunOnCurrentRec     ;Boolean        }
  }
  KEYS
  {
    {    ;Page ID,Button ID                       ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE openTypeID@1100525000(recref@1100525000 : RecordRef;callingPageID@1100525003 : Integer;buttonID@1100525001 : Integer);
    VAR
      CustSelButt@1100525002 : Record 11128006;
      VarRecRef@1100525005 : Variant;
    BEGIN
      IF NOT CustSelButt.GET(callingPageID,buttonID) THEN
        EXIT;

      // IF NOT (recref.NUMBER IN [23,11072003,11072004]) THEN
      //  CustSelButt.RunOnCurrentRec := FALSE;

      IF NOT CustSelButt.RunOnCurrentRec THEN BEGIN
        CASE CustSelButt.RunType OF
          CustSelButt.RunType::Page:      PAGE.RUN(CustSelButt.RunId);
          CustSelButt.RunType::Report:    REPORT.RUN(CustSelButt.RunId);
          CustSelButt.RunType::XMLport:   XMLPORT.RUN(CustSelButt.RunId);
          CustSelButt.RunType::Codeunit:  CODEUNIT.RUN(CustSelButt.RunId);
        END;
      END
      ELSE BEGIN
        //http://www.kauffmann.nl/2013/04/09/run-a-page-or-report-with-recordref-variable/
        recref.SETRECFILTER;
        VarRecRef := recref;
        CASE CustSelButt.RunType OF
          CustSelButt.RunType::Page:      PAGE.RUN(CustSelButt.RunId,VarRecRef);
          CustSelButt.RunType::Report:    REPORT.RUN(CustSelButt.RunId,TRUE,FALSE,VarRecRef);
          CustSelButt.RunType::XMLport:   XMLPORT.RUN(CustSelButt.RunId,TRUE,FALSE,VarRecRef);
          CustSelButt.RunType::Codeunit:  CODEUNIT.RUN(CustSelButt.RunId,VarRecRef);
        END;
      END;
    END;

    PROCEDURE copyLineToAllCompanies@1100285100();
    VAR
      Company@1100285100 : Record 2000000006;
      Configurablebuttons@1100285101 : Record 11128006;
    BEGIN
      Company.SETFILTER(Name,'<>%1',COMPANYNAME);
      IF Company.FINDSET(FALSE) THEN
      REPEAT
        Configurablebuttons.CHANGECOMPANY(Company.Name);
        IF NOT Configurablebuttons.GET("Page ID","Button ID") THEN BEGIN
          Configurablebuttons.INIT;
          Configurablebuttons."Page ID" := "Page ID";
          Configurablebuttons."Button ID" := "Button ID";
          Configurablebuttons.INSERT(FALSE);
        END;
        Configurablebuttons.RunType := RunType;
        Configurablebuttons.RunId := RunId;
        Configurablebuttons.RunOnCurrentRec := RunOnCurrentRec;
        Configurablebuttons.MODIFY(FALSE);
      UNTIL Company.NEXT =0;
    END;

    PROCEDURE removeLineFromAllCompanies@1100285101();
    VAR
      Company@1100285101 : Record 2000000006;
      Configurablebuttons@1100285100 : Record 11128006;
    BEGIN
      Company.SETFILTER(Name,'<>%1',COMPANYNAME);
      IF Company.FINDSET(FALSE) THEN
      REPEAT
        Configurablebuttons.CHANGECOMPANY(Company.Name);
        IF Configurablebuttons.GET("Page ID","Button ID") THEN
          Configurablebuttons.DELETE(FALSE);
      UNTIL Company.NEXT =0;
    END;

    BEGIN
    END.
  }
}

