OBJECT Codeunit 11012838 Replacement Management
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000a@1100525000 : TextConst 'DEU=Zuerst Zeilen w„hlen;ENU=Select lines first;NLD=Selecteer eerst regels;NOR=Velg rader f›rst;SVE=V„lj rader f”rst';
      DoNotConfirmMaintenanceSchemeCreation@1100529600 : Boolean;
      Text001@1100529601 : TextConst 'DEU=%1 wurde ersetzt.;ENU=%1 was replaced.;NLD=%1 is vervangen.';
      Text002@1100529602 : TextConst 'DEU=%1 Objekt šbersicht;ENU=%1 Service Objects;NLD=%1 Objecten';

    PROCEDURE ProcessReplacements@1100526209(VAR IReplacementLine@1100526200 : Record 11071710);
    VAR
      ReplacementLine@1100526204 : Record 11071710;
      ServiceContractNo@1100526202 : Code[20];
      StartingDate@1100526203 : Date;
      ServiceObjectToReplace@1100526205 : Record 11071691;
      ServiceObjectReplacer@1100526206 : Record 11071691;
    BEGIN
      ReplacementLine.COPY(IReplacementLine);
      WITH ReplacementLine DO BEGIN
        IF FINDSET(TRUE) THEN BEGIN
          REPEAT
            ServiceContractNo := "Contract No. Replacement";
            IF "Contract No. Replacement" = '' THEN
              ServiceContractNo := "Service Contract No.";
            StartingDate := "Starting Date Replacement";
            IF "Starting Date Replacement" = 0D THEN BEGIN
              CALCFIELDS("Service Order Ending Date");
              StartingDate := "Service Order Ending Date";
            END;
            IF ServiceContractNo = '' THEN
              TESTFIELD("Contract No. Replacement");
            IF StartingDate = 0D THEN
              TESTFIELD("Starting Date Replacement");

            ServiceObjectToReplace.GET("Object No.");
            ServiceObjectToReplace.TESTFIELD("Replaced by", '');
            InsertObject(ReplacementLine, ServiceObjectToReplace, ServiceObjectReplacer, StartingDate);

            InsertContractObject(ServiceObjectReplacer, ServiceContractNo, StartingDate);
            CloseContractObject(ServiceObjectToReplace, "Service Contract No.", StartingDate -1);

            "Object No. Replacement" := ServiceObjectReplacer."No.";
            MODIFY(TRUE);
          UNTIL NEXT = 0;
        END ELSE
          MESSAGE(Text000a);
      END;
    END;

    PROCEDURE InsertObject@1100526200(IReplacementLine@1100526201 : Record 11071710;VAR OServiceObjectToReplace@1100526202 : Record 11071691;VAR OServiceObjectReplacer@1100526204 : Record 11071691;IStartingDate@1100526203 : Date);
    BEGIN
      CLEAR(OServiceObjectReplacer);
      OServiceObjectReplacer.INSERT(TRUE);
      OServiceObjectReplacer.VALIDATE("Standard Object No.", OServiceObjectToReplace."Replace with Standard Object");
      OServiceObjectReplacer."Service Location No." :=  OServiceObjectToReplace."Service Location No.";
      OServiceObjectReplacer.VALIDATE("Warranty Code Customer", OServiceObjectToReplace."Warranty Code Customer");
      OServiceObjectReplacer."Warranty Starting Date Cust." := IStartingDate;
      OServiceObjectReplacer."In use since" := IStartingDate;
      OServiceObjectReplacer.Floor := OServiceObjectToReplace.Floor;
      OServiceObjectReplacer.Room := OServiceObjectToReplace.Room;
      OServiceObjectReplacer.Building := OServiceObjectToReplace.Building;
      OServiceObjectReplacer."Customer Feature" := IReplacementLine."Customer Feature Replacement";
      OServiceObjectReplacer.Replaces := OServiceObjectToReplace."No.";
      OServiceObjectReplacer.MODIFY(TRUE);

      OServiceObjectToReplace."Replaced by" := OServiceObjectReplacer."No.";
      OServiceObjectToReplace."System-Created Replacement" := TRUE;
      OServiceObjectToReplace.MODIFY(TRUE);
    END;

    PROCEDURE InsertContractObject@1100526203(IServiceObjectReplacer@1100526201 : Record 11071691;IServiceContractNo@1100526202 : Code[20];IStartingDate@1210190000 : Date);
    VAR
      ContractObject@1100526200 : Record 11071702;
      ServiceContract@1210190001 : Record 11012812;
    BEGIN
      ServiceContract.GET(IServiceContractNo);
      ServiceContract.TESTFIELD("Service Package");

      ContractObject.SetDoNotConfirmMaintenanceSchemeCreation(DoNotConfirmMaintenanceSchemeCreation);
      ContractObject.INIT;
      ContractObject.VALIDATE("Service Contract No.", ServiceContract."No.");
      ContractObject.VALIDATE("Service Object No.", IServiceObjectReplacer."No.");
      ContractObject.VALIDATE("Service Package", ServiceContract."Service Package");
      ContractObject.VALIDATE("Starting Date", IStartingDate);
      ContractObject.VALIDATE("Ending Date", ServiceContract."Ending Date");
      ContractObject.INSERT(TRUE);
    END;

    PROCEDURE CloseContractObject@1210190000(IServiceObjectToReplace@1210190000 : Record 11071691;IServiceContractNo@1210190002 : Code[20];IEndingDate@1210190001 : Date);
    VAR
      ContractObject@1210190003 : Record 11071702;
    BEGIN
      IF IServiceContractNo = '' THEN
        EXIT;
      IF ContractObject.GET(IServiceContractNo, IServiceObjectToReplace."No.") THEN BEGIN
        ContractObject.VALIDATE("Ending Date", IEndingDate);
        ContractObject.MODIFY(TRUE);
      END;
    END;

    PROCEDURE CreateReplacement@1100529600(VAR TmpServiceObject@1100529603 : TEMPORARY Record 11071724;LocReplacementDate@1100529602 : Date;LocReplaceWithStdObjNo@1100529601 : Code[20]);
    VAR
      LocServiceContract@1100529606 : Record 11012812;
      LocServiceObject@1100529600 : Record 11071691;
      LocObjectReplacement@1100529604 : Record 11071697;
      LocReplacementLine@1100529605 : Record 11071710;
    BEGIN
      IF (LocReplacementDate = 0D) OR (LocReplaceWithStdObjNo = '') THEN
        EXIT;
      IF TmpServiceObject.ISEMPTY THEN
        EXIT;

      LocObjectReplacement.INIT;
      LocObjectReplacement.INSERT(TRUE);

      TmpServiceObject.FINDSET;
      REPEAT
        LocServiceObject.GET(TmpServiceObject."Object No.");
        IF (LocServiceObject."Replace from" <> LocReplacementDate) OR (LocServiceObject."Replace with Standard Object" <> LocReplaceWithStdObjNo) THEN BEGIN
          LocServiceObject."Replace from" := LocReplacementDate;
          LocServiceObject."Replace with Standard Object" := LocReplaceWithStdObjNo;
          LocServiceObject.MODIFY;
        END;

        LocReplacementLine.INIT;
        LocReplacementLine.VALIDATE("Replacement No.", LocObjectReplacement."No.");
        LocReplacementLine.VALIDATE("Object No.", LocServiceObject."No.");
        LocReplacementLine."Replacement Date" := LocReplacementDate;
        LocReplacementLine."Starting Date Replacement" := LocReplacementDate;
        IF LocReplacementLine."Replace with Std. Object No." <> LocReplaceWithStdObjNo THEN
          LocReplacementLine.VALIDATE("Replace with Std. Object No.", LocReplaceWithStdObjNo);
        IF TmpServiceObject."Service Contract No." <> '' THEN BEGIN
          IF LocReplacementLine."Service Contract No." <> TmpServiceObject."Service Contract No." THEN
            LocReplacementLine.VALIDATE("Service Contract No.", TmpServiceObject."Service Contract No.");
          IF LocReplacementLine."Contract No. Replacement" <> TmpServiceObject."Service Contract No." THEN
            LocReplacementLine.VALIDATE("Contract No. Replacement", TmpServiceObject."Service Contract No.");
        END;
        IF TmpServiceObject."Service Order No." <> '' THEN
          LocReplacementLine."Service Order No." := TmpServiceObject."Service Order No.";
        LocReplacementLine.INSERT(TRUE);
      UNTIL TmpServiceObject.NEXT = 0;

      DoNotConfirmMaintenanceSchemeCreation := TRUE;
      LocReplacementLine.SETRANGE("Replacement No.", LocObjectReplacement."No.");
      ProcessReplacements(LocReplacementLine);

      IF NOT GUIALLOWED THEN
        EXIT;
      TmpServiceObject.FINDFIRST;
      IF TmpServiceObject.COUNT = 1 THEN BEGIN
        IF TmpServiceObject."Service Contract No." = '' THEN
          MESSAGE(Text001, STRSUBSTNO('%1 %2', LocServiceObject.TABLECAPTION, TmpServiceObject."Object No."))
        ELSE
          MESSAGE(Text001, STRSUBSTNO('%1 %2: %3 %4',
            LocServiceContract.TABLECAPTION, TmpServiceObject."Service Contract No.", LocServiceObject.TABLECAPTION, TmpServiceObject."Object No."));
        EXIT;
      END;

      TmpServiceObject.SETFILTER("Service Contract No.", '<>%1', TmpServiceObject."Service Contract No.");
      IF TmpServiceObject.ISEMPTY THEN BEGIN
        TmpServiceObject.SETRANGE("Service Contract No.");
        IF TmpServiceObject."Service Contract No." = '' THEN
          MESSAGE(Text001, STRSUBSTNO(Text002, TmpServiceObject.COUNT))
        ELSE
          MESSAGE(Text001,
            STRSUBSTNO('%1 %2: ', LocServiceContract.TABLECAPTION, TmpServiceObject."Service Contract No.") +
            STRSUBSTNO(Text002, TmpServiceObject.COUNT));
        EXIT;
      END;

      TmpServiceObject.SETRANGE("Service Contract No.");
      MESSAGE(Text001, STRSUBSTNO(Text002, TmpServiceObject.COUNT));
    END;

    BEGIN
    END.
  }
}

