OBJECT Table 11126024 SH Workplan Process Report
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Document Group Code");
               "Delete Allowed" := TRUE;
               "Standard Report" := FALSE;
             END;

    OnModify=VAR
               DF@1100528300 : DateFormula;
             BEGIN
               TESTFIELD("Document Group Code");
               IF ("Print Interval" = DF) THEN
                 "Last Print Date" := 0D;
             END;

    OnDelete=VAR
               SHWorkpProcessReportDetail@1100529500 : Record 11126025;
             BEGIN
               TESTFIELD("Delete Allowed", TRUE);

               SHWorkpProcessReportDetail.SETRANGE("Source Type", "Source Type");
               SHWorkpProcessReportDetail.SETRANGE("Source Code", "Source Code");
               SHWorkpProcessReportDetail.SETRANGE("Workplan Date", "Workplan Date");
               SHWorkpProcessReportDetail.SETRANGE("Main Process Code", "Main Process Code");
               SHWorkpProcessReportDetail.SETRANGE("Sub Process Code", "Sub Process Code");
               SHWorkpProcessReportDetail.SETRANGE("Location Code", "Location Code");
               SHWorkpProcessReportDetail.DELETEALL;
             END;

    CaptionML=ENU=Workplan Process Report (SH);
    LookupPageID=Page11130740;
    DrillDownPageID=Page11130740;
  }
  FIELDS
  {
    { 5   ;   ;Source Type         ;Option        ;CaptionML=[ENU=Source Type;
                                                              NOR=Kildetype;
                                                              SVE=Ursprungstyp];
                                                   OptionCaptionML=ENU=" ,Estimate,Project,Company";
                                                   OptionString=[ ,Estimate,Project,Company];
                                                   NotBlank=Yes }
    { 10  ;   ;Source Code         ;Code20        ;TableRelation=IF (Source Type=CONST(Estimate)) Estimate.No.
                                                                 ELSE IF (Source Type=CONST(Project)) Job.No.;
                                                   CaptionML=[ENU=Source Code;
                                                              NOR=Kildekode;
                                                              SVE=Ursprungskod];
                                                   NotBlank=Yes }
    { 20  ;   ;Workplan Date       ;Date          ;TableRelation="SH Workplan"."Workplan Date" WHERE (Source Type=FIELD(Source Type),
                                                                                                      Source Code=FIELD(Source Code));
                                                   CaptionML=ENU=Workplan Date }
    { 30  ;   ;Main Process Code   ;Code20        ;TableRelation="SH Workplan"."Main Process Code" WHERE (Source Type=FIELD(Source Type),
                                                                                                          Source Code=FIELD(Source Code),
                                                                                                          Workplan Date=FIELD(Workplan Date));
                                                   CaptionML=ENU=Main Process Code;
                                                   NotBlank=Yes }
    { 40  ;   ;Sub Process Code    ;Code20        ;TableRelation="SH Workplan"."Sub Process Code" WHERE (Source Type=FIELD(Source Type),
                                                                                                         Source Code=FIELD(Source Code),
                                                                                                         Workplan Date=FIELD(Workplan Date),
                                                                                                         Main Process Code=FIELD(Main Process Code));
                                                   CaptionML=ENU=Sub Process Code;
                                                   NotBlank=Yes }
    { 45  ;   ;Location Code       ;Code20        ;TableRelation="SH Workplan"."Location Code" WHERE (Source Type=FIELD(Source Type),
                                                                                                      Source Code=FIELD(Source Code),
                                                                                                      Workplan Date=FIELD(Workplan Date),
                                                                                                      Main Process Code=FIELD(Main Process Code),
                                                                                                      Sub Process Code=FIELD(Sub Process Code));
                                                   CaptionML=[ENU=Location Code;
                                                              NOR=Lokasjonskode;
                                                              SVE=Lagerst„llekod] }
    { 50  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 100 ;   ;Document Source Type;Option        ;CaptionML=ENU=Document Source Type;
                                                   OptionCaptionML=ENU=" ,Document Template,Report";
                                                   OptionString=[ ,DocTemplate,Report] }
    { 110 ;   ;Document Group Code ;Code10        ;TableRelation="Document Group";
                                                   CaptionML=ENU=Document Group Code;
                                                   NotBlank=Yes }
    { 120 ;   ;Document Template Code;Code20      ;TableRelation=IF (Document Source Type=CONST(DocTemplate)) "Document Template"."Template Code" WHERE (Document Group=FIELD(Document Group Code));
                                                   CaptionML=ENU=Document Template Code }
    { 130 ;   ;Report ID           ;Integer       ;TableRelation=IF (Document Source Type=CONST(Report)) Object.ID WHERE (Type=CONST(Report),
                                                                                                                          Company Name=CONST(''),
                                                                                                                          Name=FILTER(SH*));
                                                   CaptionML=[ENU=Report ID;
                                                              NOR=Rapport-ID;
                                                              SVE=Rapport-ID];
                                                   BlankZero=Yes }
    { 140 ;   ;Output Type OLD     ;Option        ;CaptionML=[ENU=Output Type;
                                                              NOR=Utdatatype;
                                                              SVE=Utdatatyp];
                                                   OptionCaptionML=[ENU=Old;
                                                                    NOR=Gammel;
                                                                    SVE=Gammal];
                                                   OptionString=Old;
                                                   Editable=No }
    { 141 ;   ;ReportDetail Lines  ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("SH Workp Process Report Detail" WHERE (Source Type=FIELD(Source Type),
                                                                                                             Source Code=FIELD(Source Code),
                                                                                                             Workplan Date=FIELD(Workplan Date),
                                                                                                             Main Process Code=FIELD(Main Process Code),
                                                                                                             Sub Process Code=FIELD(Sub Process Code),
                                                                                                             Location Code=FIELD(Location Code),
                                                                                                             Report Line No.=FIELD(Line No.)));
                                                   CaptionML=ENU=Report Detail Lines;
                                                   Editable=No }
    { 142 ;   ;Generated Report Detail Lines;Integer;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Count("SH Workp Process Report Detail" WHERE (Source Type=FIELD(Source Type),
                                                                                                             Source Code=FIELD(Source Code),
                                                                                                             Workplan Date=FIELD(Workplan Date),
                                                                                                             Main Process Code=FIELD(Main Process Code),
                                                                                                             Sub Process Code=FIELD(Sub Process Code),
                                                                                                             Location Code=FIELD(Location Code),
                                                                                                             Report Line No.=FIELD(Line No.),
                                                                                                             Generated=CONST(Yes)));
                                                   CaptionML=ENU=Generated Documents;
                                                   Editable=No }
    { 143 ;   ;Printed Report Detail Lines;Integer;FieldClass=FlowField;
                                                   CalcFormula=Count("SH Workp Process Report Detail" WHERE (Source Type=FIELD(Source Type),
                                                                                                             Source Code=FIELD(Source Code),
                                                                                                             Workplan Date=FIELD(Workplan Date),
                                                                                                             Main Process Code=FIELD(Main Process Code),
                                                                                                             Sub Process Code=FIELD(Sub Process Code),
                                                                                                             Location Code=FIELD(Location Code),
                                                                                                             Report Line No.=FIELD(Line No.),
                                                                                                             Printed=CONST(Yes)));
                                                   CaptionML=ENU=Printed Documents;
                                                   Editable=No }
    { 150 ;   ;Print Interval      ;DateFormula   ;OnValidate=VAR
                                                                Job@1100528301 : Record 11072003;
                                                                DF@1100528300 : DateFormula;
                                                              BEGIN
                                                                IF ("Source Type" <> "Source Type"::Estimate) THEN BEGIN //#C039411.n
                                                                  IF ("Print Interval" <> DF) THEN BEGIN
                                                                    IF (NOT Job.GET("Source Code")) THEN
                                                                      Job.INIT;
                                                                    IF ("First Print Date" = 0D) THEN
                                                                      "First Print Date" := Job."Starting Date";
                                                                    IF ("Last Print Date" = 0D) THEN
                                                                      "Last Print Date" := Job."Ending Date";
                                                                  END ELSE BEGIN
                                                                    "First Print Date" := 0D;
                                                                    "Last Print Date" := 0D;
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Print Interval }
    { 160 ;   ;Delete Allowed      ;Boolean       ;CaptionML=ENU=Delete Allowed;
                                                   Editable=No }
    { 200 ;   ;First Print Date    ;Date          ;OnValidate=BEGIN
                                                                IF ("Source Type" <> "Source Type"::Estimate) THEN //#C039411.n
                                                                  IF ("First Print Date" > "Last Print Date") AND ("First Print Date" <> 0D) AND ("Last Print Date" <> 0D) THEN
                                                                    FIELDERROR("First Print Date");
                                                              END;

                                                   CaptionML=ENU=First Print Date }
    { 210 ;   ;Last Print Date     ;Date          ;OnValidate=BEGIN
                                                                IF ("Source Type" <> "Source Type"::Estimate) THEN //#C039411.n
                                                                  IF ("Last Print Date" < "First Print Date") AND ("First Print Date" <> 0D) AND ("Last Print Date" <> 0D) THEN
                                                                    FIELDERROR("Last Print Date");
                                                              END;

                                                   CaptionML=ENU=Last Print Date }
    { 220 ;   ;Standard Report     ;Boolean       ;CaptionML=ENU=Standard Report;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Source Type,Source Code,Workplan Date,Main Process Code,Sub Process Code,Location Code,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE CreateReportDetails@1100528304(VAR SHWorkplanProcessReport@1100529500 : Record 11126024);
    VAR
      SHWorkpProcessReportDetail@1100528302 : Record 11126025;
      DF@1100528300 : DateFormula;
      CurrDate@1100528301 : Date;
      FirstPrintingDate@1100525000 : Date;
      LastPrintingDate@1100525001 : Date;
    BEGIN
      IF (NOT SHWorkplanProcessReport.FINDSET) THEN
        EXIT;

      REPEAT
        TESTFIELD("Document Group Code");
        IF ("Print Interval" <> DF) THEN  BEGIN
          TESTFIELD("First Print Date");
          TESTFIELD("Last Print Date");
          TESTFIELD("Print Interval");
        END;
        DeleteReportDetails(SHWorkplanProcessReport);

        FirstPrintingDate := GetWorkProcessReportPrintingDate(SHWorkplanProcessReport);
        IF (SHWorkplanProcessReport."Print Interval" = DF) THEN  BEGIN
          LastPrintingDate := FirstPrintingDate;
        END ELSE
          LastPrintingDate := SHWorkplanProcessReport."Last Print Date";

        CurrDate := CheckDate(FirstPrintingDate);
        REPEAT
          SHWorkpProcessReportDetail.CreateRecord(SHWorkplanProcessReport, CurrDate);
          CurrDate := CALCDATE(SHWorkplanProcessReport."Print Interval", CurrDate);
          CurrDate := CheckDate(CurrDate);
        UNTIL (CurrDate > SHWorkplanProcessReport."Last Print Date");
      UNTIL (SHWorkplanProcessReport.NEXT = 0);
    END;

    PROCEDURE GenerateReportDetailDocuments@1100529501(VAR SHWorkplanProcessReport@1100529500 : Record 11126024);
    VAR
      SHPrintManagement@1100529501 : Codeunit 11125986;
    BEGIN
      SHPrintManagement.GenerateWorkplanSelectedReports(SHWorkplanProcessReport);
    END;

    PROCEDURE GetCaption@1100528300() : Text;
    VAR
      Object@1100528300 : Record 2000000001;
      DocumentTemplate@1100529500 : Record 11012401;
    BEGIN
      CASE "Document Source Type" OF
        "Document Source Type"::DocTemplate: BEGIN
          IF (NOT DocumentTemplate.GET("Document Group Code", "Document Template Code")) THEN
            DocumentTemplate.INIT;
          EXIT(DocumentTemplate.Description);
        END;
        "Document Source Type"::Report: BEGIN
          IF (NOT Object.GET(Object.Type::Report, '', "Report ID")) THEN
            Object.INIT;
          Object.CALCFIELDS(Caption);
          EXIT(Object.Caption);
        END;
      END;
      EXIT('');
    END;

    PROCEDURE PrintSelectedReportDetailDocuments@1100528302(VAR SHWorkplanProcessReport@1100528300 : Record 11126024);
    VAR
      UserSetup@1100528303 : Record 91;
      TempEmailAttachment@1100528302 : TEMPORARY Record 11229294;
      SHPrintManagement@1100528301 : Codeunit 11125986;
    BEGIN
      UserSetup.GET(USERID);
      UserSetup.TESTFIELD("PDF Reader Path");
      TempEmailAttachment.DELETEALL;

      SHPrintManagement.PrintWorkplanSelectedReports(SHWorkplanProcessReport, TempEmailAttachment);
      SHPrintManagement.PrintCreatedPDFOutputFile(TempEmailAttachment);
    END;

    LOCAL PROCEDURE "---Locals---"@1100529504();
    BEGIN
    END;

    LOCAL PROCEDURE CheckDate@1100525001(CheckDate@1100529502 : Date) : Date;
    VAR
      SHCopyManagement@1100528300 : Codeunit 11125987;
    BEGIN
      EXIT(SHCopyManagement.GetFirstWorkDayDate(CheckDate));
    END;

    LOCAL PROCEDURE DeleteReportDetails@1100529503(SHWorkplanProcessReport@1100529500 : Record 11126024);
    VAR
      SHWorkpProcessReportDetail@1100529501 : Record 11126025;
    BEGIN
      SHWorkpProcessReportDetail.SETRANGE("Source Type", SHWorkplanProcessReport."Source Type");
      SHWorkpProcessReportDetail.SETRANGE("Source Code", SHWorkplanProcessReport."Source Code");
      SHWorkpProcessReportDetail.SETRANGE("Workplan Date", SHWorkplanProcessReport."Workplan Date");
      SHWorkpProcessReportDetail.SETRANGE("Main Process Code", SHWorkplanProcessReport."Main Process Code");
      SHWorkpProcessReportDetail.SETRANGE("Sub Process Code", SHWorkplanProcessReport."Sub Process Code");
      SHWorkpProcessReportDetail.SETRANGE("Location Code", SHWorkplanProcessReport."Location Code");
      SHWorkpProcessReportDetail.SETRANGE("Report Line No.", SHWorkplanProcessReport."Line No.");
      SHWorkpProcessReportDetail.SETRANGE(Generated, FALSE);
      SHWorkpProcessReportDetail.DELETEALL;
    END;

    LOCAL PROCEDURE GetWorkProcessReportPrintingDate@1100525000(SHWorkplanProcessReportIn@1100525001 : Record 11126024) : Date;
    VAR
      SHWorkpProcessReportDetail@1100525000 : Record 11126025;
    BEGIN
      SHWorkpProcessReportDetail.SETRANGE("Source Type", SHWorkplanProcessReportIn."Source Type");
      SHWorkpProcessReportDetail.SETRANGE("Source Code", SHWorkplanProcessReportIn."Source Code");
      SHWorkpProcessReportDetail.SETRANGE("Workplan Date", SHWorkplanProcessReportIn."Workplan Date");
      SHWorkpProcessReportDetail.SETRANGE("Main Process Code", SHWorkplanProcessReportIn."Main Process Code");
      SHWorkpProcessReportDetail.SETRANGE("Sub Process Code", SHWorkplanProcessReportIn."Sub Process Code");
      SHWorkpProcessReportDetail.SETRANGE("Location Code", SHWorkplanProcessReportIn."Location Code");
      SHWorkpProcessReportDetail.SETRANGE("Report Line No.", SHWorkplanProcessReportIn."Line No.");
      SHWorkpProcessReportDetail.SETFILTER("Print Date", '<>%1', 0D);
      IF (SHWorkpProcessReportDetail.FINDLAST) THEN
        EXIT(CALCDATE(SHWorkplanProcessReportIn."Print Interval", SHWorkpProcessReportDetail."Print Date"));
      IF (SHWorkplanProcessReportIn."First Print Date" <> 0D) THEN
        EXIT(SHWorkplanProcessReportIn."First Print Date");
      EXIT(TODAY);
    END;

    BEGIN
    END.
  }
}

