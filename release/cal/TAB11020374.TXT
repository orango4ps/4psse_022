OBJECT Table 11020374 Rejection Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Rejection Line;
               NOR=Avslagsrad;
               SVE=Avslagsrad];
  }
  FIELDS
  {
    { 10  ;   ;Document Type       ;Option        ;CaptionML=[ENU=Document Type;
                                                              NOR=Dokumenttype;
                                                              SVE=Dokumenttyp];
                                                   OptionCaptionML=[ENU=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order;
                                                                    NOR=Tilbud,Ordre,Faktura,Kreditnota,Rammeordre,Returordre;
                                                                    SVE=Offert,Order,Faktura,Kreditnota,Avropsorder,Returorder];
                                                   OptionString=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order;
                                                   Editable=No }
    { 20  ;   ;Document No.        ;Code20        ;TableRelation="Purchase Header".No. WHERE (Document Type=FIELD(Document Type));
                                                   CaptionML=[ENU=Document No.;
                                                              NOR=Dokumentnr.;
                                                              SVE=Dokumentnr];
                                                   Editable=No }
    { 30  ;   ;Document Line No.   ;Integer       ;TableRelation="Purchase Line"."Line No." WHERE (Document Type=FIELD(Document Type),
                                                                                                   Document No.=FIELD(Document No.));
                                                   CaptionML=[ENU=Document Line No.;
                                                              NOR=Dokumentlinjenr.;
                                                              SVE=Dokumentradnr];
                                                   Editable=No }
    { 40  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr];
                                                   Editable=No }
    { 50  ;   ;Rejection Reason Code;Code10       ;TableRelation="Rejection Reason";
                                                   OnValidate=BEGIN
                                                                CheckModifyAllowed;
                                                              END;

                                                   CaptionML=[ENU=Rejection Reason Code;
                                                              NOR=Avslag for èrsakskode;
                                                              SVE=Avslag fîr orsakskod] }
    { 60  ;   ;Rejected Quantity   ;Decimal       ;OnValidate=BEGIN
                                                                CheckModifyAllowed;
                                                                CheckQuantity;
                                                                IF "Return Quantity" = 0 THEN
                                                                  "Return Quantity" := "Rejected Quantity";
                                                                IF "Reorder Quantity" = 0 THEN
                                                                  "Reorder Quantity" := "Rejected Quantity";
                                                                "Destroy Quantity" := "Rejected Quantity" - "Return Quantity";
                                                              END;

                                                   CaptionML=[ENU=Rejected Quantity;
                                                              NOR=Avvist antall;
                                                              SVE=Avvisat antal];
                                                   DecimalPlaces=0:5 }
    { 70  ;   ;Return Quantity     ;Decimal       ;OnValidate=BEGIN
                                                                IF "Return Quantity" > "Rejected Quantity" THEN
                                                                  ERROR(Text003,FIELDCAPTION("Return Quantity"),FIELDCAPTION("Rejected Quantity"));
                                                                IF "Return Quantity" <> xRec."Return Quantity" THEN
                                                                  TESTFIELD("Purchase Return Order",'');
                                                                "Destroy Quantity" := "Rejected Quantity" - "Return Quantity";
                                                              END;

                                                   CaptionML=[ENU=Return Quantity;
                                                              NOR=Returantall;
                                                              SVE=Returantal];
                                                   DecimalPlaces=0:5 }
    { 80  ;   ;Reorder Quantity    ;Decimal       ;OnValidate=BEGIN
                                                                IF "Reorder Quantity" > "Rejected Quantity" THEN
                                                                  ERROR(Text003,FIELDCAPTION("Reorder Quantity"),FIELDCAPTION("Rejected Quantity"));
                                                                IF "Reorder Quantity" <> xRec."Reorder Quantity" THEN
                                                                  TESTFIELD("New Purchase Order",'');
                                                              END;

                                                   CaptionML=[ENU=Reorder Quantity;
                                                              NOR=Gjenbestillingsantall;
                                                              SVE=BestÑllningsantal];
                                                   DecimalPlaces=0:5 }
    { 90  ;   ;Destroy Quantity    ;Decimal       ;CaptionML=[ENU=Destroy Quantity;
                                                              NOR=ùdelegg antall;
                                                              SVE=Fîrstîr antal];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 100 ;   ;New Purchase Order  ;Code20        ;TableRelation="Purchase Header".No. WHERE (Document Type=CONST(Order),
                                                                                              Purchase Order Type=CONST(Purchase Order));
                                                   CaptionML=[ENU=New Purchase Order;
                                                              NOR=Ny innkjõpsordre;
                                                              SVE=Ny inkîpsorder];
                                                   Editable=No }
    { 110 ;   ;Purchase Return Order;Code20       ;TableRelation="Purchase Header".No. WHERE (Document Type=CONST(Order),
                                                                                              Purchase Order Type=CONST(Return Order));
                                                   CaptionML=[ENU=Purchase Return Order;
                                                              NOR=Bestillingsretur;
                                                              SVE=Inkîpsreturorder];
                                                   Editable=No }
    { 120 ;   ;Purchase Receipts No.;Code20       ;TableRelation="Purch. Rcpt. Header" WHERE (Order No.=FIELD(Document No.));
                                                   OnValidate=BEGIN
                                                                CheckModifyAllowed;
                                                              END;

                                                   CaptionML=[ENU=Purchase Receipts No.;
                                                              NOR=Innkjõpskvitteringsnr.;
                                                              SVE=Inkîpskvittonr] }
    { 5000;   ;Type                ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purchase Line".Type WHERE (Document Type=FIELD(Document Type),
                                                                                                  Document No.=FIELD(Document No.),
                                                                                                  Line No.=FIELD(Document Line No.)));
                                                   CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Typ];
                                                   OptionCaptionML=[ENU=" ,G/L Account,Item,,Fixed Asset,Charge (Item)";
                                                                    NOR=" ,Finanskonto,Vare,,Aktiva,Gebyr (vare)";
                                                                    SVE=" ,Redov.konto,Artikel,,Anl.tillgÜng,Omkostnad (artikel)"];
                                                   OptionString=[ ,G/L Account,Item,,Fixed Asset,Charge (Item)];
                                                   Editable=No }
    { 5010;   ;No.                 ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purchase Line".No. WHERE (Document Type=FIELD(Document Type),
                                                                                                 Document No.=FIELD(Document No.),
                                                                                                 Line No.=FIELD(Document Line No.)));
                                                   CaptionML=[ENU=No.;
                                                              NOR=Nr.;
                                                              SVE=Nr];
                                                   Editable=No }
    { 5020;   ;Location Code       ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purchase Line"."Location Code" WHERE (Document Type=FIELD(Document Type),
                                                                                                             Document No.=FIELD(Document No.),
                                                                                                             Line No.=FIELD(Document Line No.)));
                                                   CaptionML=[ENU=Location Code;
                                                              NOR=Lokasjonskode;
                                                              SVE=LagerstÑllekod];
                                                   Editable=No }
    { 5030;   ;Posting Group       ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purchase Line"."Posting Group" WHERE (Document Type=FIELD(Document Type),
                                                                                                             Document No.=FIELD(Document No.),
                                                                                                             Line No.=FIELD(Document Line No.)));
                                                   CaptionML=[ENU=Posting Group;
                                                              NOR=Bokfõringsgruppe;
                                                              SVE=Bokfîringsmall];
                                                   Editable=No }
    { 5040;   ;Description         ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purchase Line".Description WHERE (Document Type=FIELD(Document Type),
                                                                                                         Document No.=FIELD(Document No.),
                                                                                                         Line No.=FIELD(Document Line No.)));
                                                   CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning];
                                                   Editable=No }
    { 5050;   ;Description 2       ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purchase Line"."Description 2" WHERE (Document Type=FIELD(Document Type),
                                                                                                             Document No.=FIELD(Document No.),
                                                                                                             Line No.=FIELD(Document Line No.)));
                                                   CaptionML=[ENU=Description 2;
                                                              NOR=Beskrivelse 2;
                                                              SVE=Beskrivning 2];
                                                   Editable=No }
    { 5060;   ;Unit of Measure     ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purchase Line"."Unit of Measure" WHERE (Document Type=FIELD(Document Type),
                                                                                                               Document No.=FIELD(Document No.),
                                                                                                               Line No.=FIELD(Document Line No.)));
                                                   CaptionML=[ENU=Unit of Measure;
                                                              NOR=Enhet;
                                                              SVE=MÜttenhet];
                                                   Editable=No }
    { 5070;   ;Quantity            ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purchase Line".Quantity WHERE (Document Type=FIELD(Document Type),
                                                                                                      Document No.=FIELD(Document No.),
                                                                                                      Line No.=FIELD(Document Line No.)));
                                                   CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 5080;   ;Unit Cost (LCY)     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purchase Line"."Unit Cost (LCY)" WHERE (Document Type=FIELD(Document Type),
                                                                                                               Document No.=FIELD(Document No.),
                                                                                                               Line No.=FIELD(Document Line No.)));
                                                   CaptionML=[ENU=Unit Cost (LCY);
                                                              NOR=Enhetskost (NOK);
                                                              SVE=Styckkostnad (BVA)];
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 5090;   ;Vendor Authorization No.;Code35    ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purchase Header"."Vendor Authorization No." WHERE (Document Type=CONST(Order),
                                                                                                                          Purchase Order Type=CONST(Return Order),
                                                                                                                          No.=FIELD(Purchase Return Order)));
                                                   CaptionML=[ENU=Vendor Authorization No.;
                                                              NOR=Leverandõrens godkjenningsnr;
                                                              SVE=Lev.godkÑnnande nr];
                                                   Editable=No }
    { 5100;   ;Return Reason Code  ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Rejection Reason"."Return Reason Code" WHERE (Code=FIELD(Rejection Reason Code)));
                                                   TableRelation="Return Reason Purchase";
                                                   CaptionML=[ENU=Return Reason Code;
                                                              NOR=ReturÜrsakskode;
                                                              SVE=Returorsakskod];
                                                   Editable=No }
    { 5110;   ;Destroy Reason Code ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Rejection Reason"."Destroy Reason Code" WHERE (Code=FIELD(Rejection Reason Code)));
                                                   TableRelation="Return Reason Purchase";
                                                   CaptionML=[ENU=Destroy Reason Code;
                                                              NOR=ùdelegg èrsakskode;
                                                              SVE=Fîrstîr orsakskod];
                                                   Editable=No }
    { 5120;   ;Quantity Received   ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purch. Rcpt. Line".Quantity WHERE (Document No.=FIELD(Purchase Receipts No.),
                                                                                                          Order No.=FIELD(Document No.),
                                                                                                          Order Line No.=FIELD(Document Line No.)));
                                                   CaptionML=[ENU=Quantity Received;
                                                              NOR=Mottatt (antall);
                                                              SVE=Inlevererat antal];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 5130;   ;Job No.             ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purchase Line"."Job No." WHERE (Document Type=FIELD(Document Type),
                                                                                                       Document No.=FIELD(Document No.),
                                                                                                       Line No.=FIELD(Document Line No.)));
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr] }
    { 5140;   ;Service Order No.   ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purchase Line"."Service Order No." WHERE (Document Type=FIELD(Document Type),
                                                                                                                 Document No.=FIELD(Document No.),
                                                                                                                 Line No.=FIELD(Document Line No.)));
                                                   CaptionML=[ENU=Service Order No.;
                                                              NOR=Serviceordrenr.;
                                                              SVE=Serviceordernr];
                                                   Editable=No }
    { 5150;   ;Manufacturer        ;Code15        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purchase Line".Manufacturer WHERE (Document Type=FIELD(Document Type),
                                                                                                          Document No.=FIELD(Document No.),
                                                                                                          Line No.=FIELD(Document Line No.)));
                                                   TableRelation="ETIM Vendor - Central Mgt";
                                                   CaptionML=[ENU=Manufacturer;
                                                              NOR=Produsent;
                                                              SVE=Tillverkare];
                                                   Editable=No }
    { 5160;   ;Basic Item          ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purchase Line"."Basic Item" WHERE (Document Type=FIELD(Document Type),
                                                                                                          Document No.=FIELD(Document No.),
                                                                                                          Line No.=FIELD(Document Line No.)));
                                                   TableRelation="Basic Item"."Product Code" WHERE (Manufacturer=FIELD(Manufacturer));
                                                   CaptionML=[ENU=Basic Item;
                                                              NOR=Fabrikantartikkel;
                                                              SVE=Basartikel];
                                                   Editable=No }
    { 5170;   ;Vendor (Trade Item) ;Code15        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purchase Line"."Vendor (Trade Item)" WHERE (Document Type=FIELD(Document Type),
                                                                                                                   Document No.=FIELD(Document No.),
                                                                                                                   Line No.=FIELD(Document Line No.)));
                                                   TableRelation="ETIM Vendor - Central Mgt";
                                                   CaptionML=[ENU=Vendor (Trade Item);
                                                              NOR=Leverandõr (katalogartikkel);
                                                              SVE=Leverantîr (skaffningsartikel)];
                                                   Editable=No }
    { 5180;   ;Trade Item          ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purchase Line"."Trade Item" WHERE (Document Type=FIELD(Document Type),
                                                                                                          Document No.=FIELD(Document No.),
                                                                                                          Line No.=FIELD(Document Line No.)));
                                                   TableRelation=IF (Vendor (Trade Item)=FILTER(<>'')) "Trade Item"."Item Code" WHERE (Vendor=FIELD("Vendor (Trade Item)"))
                                                                 ELSE IF (Vendor (Trade Item)=FILTER('')) "Trade Item"."Item Code";
                                                   CaptionML=[ENU=Trade Item;
                                                              NOR=Katalogartikkel;
                                                              SVE=Anskaffningsartikel];
                                                   Editable=No }
    { 5190;   ;Purchaser Code      ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purchase Header"."Purchaser Code" WHERE (Document Type=FIELD(Document Type),
                                                                                                                No.=FIELD(Document No.)));
                                                   CaptionML=[ENU=Purchaser Code;
                                                              NOR=Innkjõperkode;
                                                              SVE=Inkîparkod];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Document Type,Document No.,Document Line No.,Line No.;
                                                   MaintainSIFTIndex=No;
                                                   Clustered=Yes }
    {    ;Document Type,Document No.,Purchase Receipts No.,Document Line No.;
                                                   MaintainSIFTIndex=No }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100525002 : TextConst 'ENU=You can''t reject more than %1 units.;NOR=Du kan ikke Avvise mer enn %1 enheter.;SVE=Du kan inte avvisa mer Ñn %1 enheter.';
      Text002@1100525001 : TextConst 'ENU=%1 must be grater than 0.;NOR=%1 mÜ vëre stõrre enn 0.;SVE=%1 mÜste vara stîrre Ñn 0.';
      Text003@1100525000 : TextConst 'ENU=%1 can''t be greater than %2.;NOR=%1 kan ikke vëre stõrre enn %2;SVE=%1 kan inte vara stîrre Ñn %2';
      Text004@1100525003 : TextConst 'ENU=Changes not allowed. There are already Purchase Order(s) created for this Rejection Line.;NOR=Endringer er ikke tillatte. Det finnes allerede Innkjõpsordre opprettet for denne Avslagsraden.;SVE=éndringar Ñr inte tillÜtna. Det finns redan inkîpsorder skapade fîr denna avslagsrad.';

    PROCEDURE CreateOrders@1100525003();
    VAR
      Text001@1100525000 : TextConst 'ENU=Created Purchase Order(s): %1 %2;NOR=Oppretted(e) Innkjõpsordre: %1 %2;SVE=Skapad(e) inkîpsorder: %1 %2';
      PurchaseOrderNo@1100525001 : Code[20];
      PurchaseReturnOrderNo@1100525002 : Code[20];
      Text002@1100525003 : TextConst 'ENU=No Orders created.;NOR=Ingen ordre opprettet.;SVE=Inga order skapade.';
    BEGIN
      PurchaseOrderNo := CreatePurchOrder;
      PurchaseReturnOrderNo := CreatePurchReturnOrder;
      IF (PurchaseOrderNo <> '') OR (PurchaseReturnOrderNo <> '') THEN
        MESSAGE(Text001,PurchaseOrderNo,PurchaseReturnOrderNo)
      ELSE
        MESSAGE(Text002);
    END;

    PROCEDURE CreatePurchOrder@1100525002() PurchaseOrderNo : Code[20];
    VAR
      PurchaseHeader@1100525004 : Record 38;
      PurchaseLine@1100525000 : Record 39;
      PurchOrderHdr@1100525001 : Record 38;
      PurchOrderLine@1100525002 : Record 39;
      RejectionLine@1100525005 : Record 11020374;
      LineNo@1100525003 : Integer;
    BEGIN
      PurchaseHeader.GET("Document Type","Document No.");
      SETRANGE("Document Type",PurchaseHeader."Document Type");
      SETRANGE("Document No.",PurchaseHeader."No.");
      SETRANGE("New Purchase Order",'');
      SETFILTER("Rejected Quantity",'>%1',0);
      SETFILTER("Reorder Quantity",'>%1',0);
      IF FINDSET THEN BEGIN
        PurchOrderHdr.INIT;
        PurchOrderHdr."Document Type" := PurchOrderHdr."Document Type"::Order;
        PurchOrderHdr."Purchase Order Type" := PurchOrderHdr."Purchase Order Type"::"Purchase Order";
        PurchOrderHdr."Job No." := PurchaseHeader."Job No."; //kzwerver, 130902, n, #C003526
        PurchOrderHdr."Service Order No." := PurchaseHeader."Service Order No."; //kzwerver, 130902, n, #C003526
        PurchOrderHdr.INSERT(TRUE);
        PurchaseOrderNo := PurchOrderHdr."No.";
        PurchOrderHdr.VALIDATE("Buy-from Vendor No.",PurchaseHeader."Buy-from Vendor No.");
        PurchOrderHdr.VALIDATE("Pay-to Vendor No.",PurchaseHeader."Pay-to Vendor No.");
        PurchOrderHdr."Buy-from Vendor Name" := PurchaseHeader."Buy-from Vendor Name";
        PurchOrderHdr."Pay-to Name" := PurchaseHeader."Pay-to Name";
        PurchOrderHdr."Ship-to Name" := PurchaseHeader."Ship-to Name";
        PurchOrderHdr."Original Order No." := PurchaseHeader."No.";
        PurchOrderHdr."Purchase Requisition No." := PurchaseHeader."Purchase Requisition No.";
        PurchOrderHdr."Vendor Order No." := PurchaseHeader."Vendor Order No.";
        PurchOrderHdr.MODIFY;

        REPEAT
          TESTFIELD("Rejection Reason Code");
          PurchaseLine.GET("Document Type","Document No.","Document Line No.");
          LineNo += 10000;
          PurchOrderLine."Document Type" := PurchOrderHdr."Document Type";
          PurchOrderLine."Document No." := PurchOrderHdr."No.";
          PurchOrderLine."Line No." := LineNo;
          PurchOrderLine.INSERT;

          CopyPurchLine(PurchaseLine,PurchOrderLine);

          PurchOrderLine.VALIDATE(Quantity,"Reorder Quantity");
          PurchOrderLine.VALIDATE("Qty. to Receive","Reorder Quantity");
          PurchOrderLine."Modified by" := USERID;
          PurchOrderLine."Last Date Modified" := TODAY;
          PurchOrderLine.MODIFY;

          CopyPurchLineDedetails(PurchaseLine,PurchOrderLine);
          RejectionLine := Rec;
          RejectionLine."New Purchase Order" := PurchOrderHdr."No.";
          RejectionLine.MODIFY;
        UNTIL NEXT = 0;
      END;

      SETRANGE("New Purchase Order");
      SETRANGE("Reorder Quantity");
    END;

    PROCEDURE CreatePurchReturnOrder@1100525000() PurchaseOrderNo : Code[20];
    VAR
      PurchaseHeader@1100525006 : Record 38;
      PurchaseLine@1100525000 : Record 39;
      PurchReturnOrderHdr@1100525001 : Record 38;
      PurchReturnOrderLine@1100525002 : Record 39;
      RejectionReason@1100525004 : Record 11020375;
      RejectionLine@1100525005 : Record 11020374;
      ReturnReasonPurch@1100409000 : Record 11012087;
      LineNo@1100525003 : Integer;
    BEGIN
      PurchaseHeader.GET("Document Type","Document No.");
      SETRANGE("Document Type",PurchaseHeader."Document Type");
      SETRANGE("Document No.",PurchaseHeader."No.");
      SETRANGE("Purchase Return Order",'');
      SETFILTER("Rejected Quantity",'>%1',0);
      IF FINDSET THEN BEGIN
        PurchReturnOrderHdr.INIT;
        PurchReturnOrderHdr."Document Type" := PurchReturnOrderHdr."Document Type"::Order;
        PurchReturnOrderHdr."Purchase Order Type" := PurchReturnOrderHdr."Purchase Order Type"::"Return Order";
        PurchReturnOrderHdr."Job No." := PurchaseHeader."Job No."; //kzwerver, 130902, n, #C003526
        PurchReturnOrderHdr."Service Order No." := PurchaseHeader."Service Order No."; //kzwerver, 130902, n, #C003526
        PurchReturnOrderHdr.INSERT(TRUE);
        PurchaseOrderNo := PurchReturnOrderHdr."No.";
        PurchReturnOrderHdr.VALIDATE("Buy-from Vendor No.",PurchaseHeader."Buy-from Vendor No.");
        PurchReturnOrderHdr.VALIDATE("Pay-to Vendor No.",PurchaseHeader."Pay-to Vendor No.");
        PurchReturnOrderHdr."Buy-from Vendor Name" := PurchaseHeader."Buy-from Vendor Name";
        PurchReturnOrderHdr."Pay-to Name" := PurchaseHeader."Pay-to Name";
        PurchReturnOrderHdr."Ship-to Name" := PurchaseHeader."Ship-to Name";
        PurchReturnOrderHdr."Original Order No." := PurchaseHeader."No.";
        PurchReturnOrderHdr."Purchase Requisition No." := PurchaseHeader."Purchase Requisition No.";
        PurchReturnOrderHdr."Vendor Order No." := PurchaseHeader."Vendor Order No.";
        PurchReturnOrderHdr.MODIFY;
        SetDefaultReturnAddress(PurchReturnOrderHdr);

        REPEAT
          IF ("Return Quantity" <> 0) OR ("Destroy Quantity" <> 0) THEN BEGIN
            PurchaseLine.GET("Document Type","Document No.","Document Line No.");
            RejectionReason.GET("Rejection Reason Code");
            IF "Return Quantity" <> 0 THEN BEGIN
              RejectionReason.TESTFIELD("Return Reason Code");
              ReturnReasonPurch.GET(RejectionReason."Return Reason Code");
              ReturnReasonPurch.TESTFIELD(Destroyed, FALSE);
            END;
            IF "Destroy Quantity" <> 0 THEN BEGIN
              RejectionReason.TESTFIELD("Destroy Reason Code");
              ReturnReasonPurch.GET(RejectionReason."Destroy Reason Code");
              ReturnReasonPurch.TESTFIELD(Destroyed, TRUE);
            END;
            LineNo += 10000;
            PurchReturnOrderLine."Document Type" := PurchReturnOrderHdr."Document Type";
            PurchReturnOrderLine."Document No." := PurchReturnOrderHdr."No.";
            PurchReturnOrderLine."Line No." := LineNo;
            PurchReturnOrderLine."Input by" := USERID;
            PurchReturnOrderLine."Input Date" := TODAY;
            PurchReturnOrderLine.INSERT;

            CopyPurchLine(PurchaseLine,PurchReturnOrderLine);

            IF "Return Quantity" <> 0 THEN BEGIN
              PurchReturnOrderLine.VALIDATE(Quantity,-"Return Quantity");
              PurchReturnOrderLine.VALIDATE("Return Reason Code",RejectionReason."Return Reason Code");
              PurchReturnOrderLine.VALIDATE("Qty. to Receive",-"Return Quantity");
              PurchReturnOrderLine."Modified by" := USERID;
              PurchReturnOrderLine."Last Date Modified" := TODAY;
              PurchReturnOrderLine.MODIFY;
              CopyPurchLineDedetails(PurchaseLine,PurchReturnOrderLine);
              IF "Destroy Quantity" <> 0 THEN BEGIN
                PurchReturnOrderLine."Line No." += 5000;
                PurchReturnOrderLine.VALIDATE(Quantity,-"Destroy Quantity");
                PurchReturnOrderLine.VALIDATE("Return Reason Code",RejectionReason."Destroy Reason Code");
                PurchReturnOrderLine.VALIDATE("Qty. to Receive",-"Destroy Quantity");
                PurchReturnOrderLine."Input by" := USERID;
                PurchReturnOrderLine."Input Date" := TODAY;
                PurchReturnOrderLine.INSERT;
                CopyPurchLineDedetails(PurchaseLine,PurchReturnOrderLine);
              END;
            END ELSE BEGIN
              PurchReturnOrderLine.VALIDATE(Quantity,-"Destroy Quantity");
              PurchReturnOrderLine.VALIDATE("Return Reason Code",RejectionReason."Destroy Reason Code");
              PurchReturnOrderLine.VALIDATE("Qty. to Receive",-"Destroy Quantity");
              PurchReturnOrderLine."Modified by" := USERID;
              PurchReturnOrderLine."Last Date Modified" := TODAY;
              PurchReturnOrderLine.MODIFY;
              CopyPurchLineDedetails(PurchaseLine,PurchReturnOrderLine);
            END;

            RejectionLine := Rec;
            RejectionLine."Purchase Return Order" := PurchReturnOrderHdr."No.";
            RejectionLine.MODIFY;
          END;
        UNTIL NEXT = 0;
      END;

      SETRANGE("Purchase Return Order");
      SETRANGE("Return Quantity");
    END;

    PROCEDURE CopyPurchLine@1100525008(FromPurchLine@1100525000 : Record 39;VAR ToPurchLine@1100525001 : Record 39);
    BEGIN
      WITH ToPurchLine DO BEGIN
        SetCheckItemPrice(TRUE);  //C062009
        "System-Created Entry" := FromPurchLine."System-Created Entry";
        Type := FromPurchLine.Type;
        "No." := FromPurchLine."No.";  //C056308: fill No for lines without project or serviceorder
        IF FromPurchLine."Job No." <> '' THEN
          VALIDATE("Job No.",FromPurchLine."Job No.");
        IF FromPurchLine.Element <> '' THEN
          VALIDATE(Element,FromPurchLine.Element);
        "Service Contract No." := FromPurchLine."Service Contract No.";
        "Service Order No." := FromPurchLine."Service Order No.";
        "Service Location No." := FromPurchLine."Service Location No.";
        IF FromPurchLine."Cost Type" <> FromPurchLine."Cost Type"::" " THEN
          VALIDATE("Cost Type",FromPurchLine."Cost Type");
        //C056308.sn: switch priority on validate item data
        IF FromPurchLine."Trade Item" <> '' THEN BEGIN
          "Vendor (Trade Item)" := FromPurchLine."Vendor (Trade Item)";
          "Trade Item" := '';
          VALIDATE("Trade Item", FromPurchLine."Trade Item");
        END ELSE BEGIN
          IF FromPurchLine."Basic Item" <> '' THEN BEGIN
            Manufacturer := FromPurchLine.Manufacturer;
            "Basic Item" := '';
            VALIDATE("Basic Item", FromPurchLine."Basic Item");
          END ELSE BEGIN
            IF FromPurchLine."Item No." <> '' THEN BEGIN
              "Item No." := '';  //check on xRec in validate itemdata
              VALIDATE("Item No.", FromPurchLine."Item No.");
            END;
          END;
        END;
        //C056308.en
        IF FromPurchLine."Plant Type" <> '' THEN
          VALIDATE("Plant Type",FromPurchLine."Plant Type");
        IF FromPurchLine."Plant No." <> '' THEN
          VALIDATE("Plant No.",FromPurchLine."Plant No.");
        IF FromPurchLine."Cost Component Plant" <> '' THEN
          VALIDATE("Cost Component Plant",FromPurchLine."Cost Component Plant");
        IF FromPurchLine."Shortcut Dimension 2 Code" <> '' THEN
          VALIDATE("Shortcut Dimension 2 Code",FromPurchLine."Shortcut Dimension 2 Code");
        "Cost Component" := FromPurchLine."Cost Component";
        "Variant Code" := FromPurchLine."Variant Code";
        "Unit of Measure Code" := FromPurchLine."Unit of Measure Code";
        "Unit of Measure" := FromPurchLine."Unit of Measure";
        Description := FromPurchLine.Description;
        "Description 2" := FromPurchLine."Description 2";
        VALIDATE("Location Code",FromPurchLine."Location Code");
        IF "Purchase Order Type" = "Purchase Order Type"::"Return Order" THEN
          "Bin Code" := FromPurchLine."Bin Code";
        VALIDATE("Direct Unit Cost",FromPurchLine."Direct Unit Cost");
        "Unit Price (LCY)" := FromPurchLine."Unit Price (LCY)";
        "Purchase Route Reference" := FromPurchLine."Purchase Route Reference";
        VALIDATE("Purchase Action",FromPurchLine."Purchase Action");
        CALCFIELDS("Purchase Requisition No.");
        SetCheckItemPrice(FALSE);  //C062009
      END;
    END;

    PROCEDURE CheckQuantity@1100525001();
    VAR
      PurchaseLine@1100525000 : Record 39;
      RejectionLine@1100525001 : Record 11020374;
      QuantityToReject@1100525002 : Decimal;
    BEGIN
      CALCFIELDS(Type);
      //TESTFIELD(Type,Type::Item);
      IF "Rejected Quantity" < 0 THEN
        ERROR(Text002,FIELDCAPTION("Rejected Quantity"));

      PurchaseLine.GET("Document Type","Document No.","Document Line No.");
      CALCFIELDS("Quantity Received");
      QuantityToReject := "Quantity Received";
      RejectionLine.SETRANGE("Document Type","Document Type");
      RejectionLine.SETRANGE("Document No.","Document No.");
      RejectionLine.SETRANGE("Document Line No.","Document Line No.");
      RejectionLine.SETRANGE("Purchase Receipts No.","Purchase Receipts No.");
      RejectionLine.SETFILTER("Line No.",'<>%1',"Line No.");
      IF RejectionLine.FINDSET THEN
        REPEAT
          IF (RejectionLine."New Purchase Order" <> '') OR (RejectionLine."Purchase Return Order" <> '') THEN
            QuantityToReject -= RejectionLine."Rejected Quantity";
        UNTIL RejectionLine.NEXT = 0;
      IF "Rejected Quantity" > QuantityToReject THEN
        ERROR(Text001,QuantityToReject);
    END;

    PROCEDURE SetDefaultReturnAddress@1100525004(VAR PurchaseHeader@1100525000 : Record 38);
    VAR
      OrderAddress@1100525001 : Record 224;
    BEGIN
      OrderAddress.SETRANGE("Vendor No.",PurchaseHeader."Buy-from Vendor No.");
      OrderAddress.SETRANGE("Default Return Address",TRUE);
      IF OrderAddress.FINDFIRST THEN BEGIN
        PurchaseHeader."Ship-to Name" := OrderAddress.Name;
        PurchaseHeader."Ship-to Name 2" := OrderAddress."Name 2";
        PurchaseHeader."Ship-to Address" := OrderAddress.Address;
        PurchaseHeader."Ship-to Address 2" := OrderAddress."Address 2";
        PurchaseHeader."Ship-to City" := OrderAddress.City;
        PurchaseHeader."Ship-to Contact" := OrderAddress.Contact;
        PurchaseHeader."Ship-to Post Code" := OrderAddress."Post Code";
        PurchaseHeader."Ship-to County" := OrderAddress.County;
        PurchaseHeader."Ship-to Country/Region Code" := OrderAddress."Country/Region Code";;
        PurchaseHeader.MODIFY;
      END;
    END;

    PROCEDURE CreateRejectionLines@1100525005(PurchaseLine@1100525000 : Record 39);
    VAR
      RejectionLine@1100525001 : Record 11020374;
      PurchRcptLine@1100525003 : Record 121;
      LineNo@1100525002 : Integer;
    BEGIN
      RejectionLine.SETRANGE("Document Type", PurchaseLine."Document Type");
      RejectionLine.SETRANGE("Document No.", PurchaseLine."Document No.");
      RejectionLine.SETRANGE("Document Line No.", PurchaseLine."Line No.");
      IF RejectionLine.FINDLAST THEN
        LineNo := RejectionLine."Line No." + 10000
      ELSE
        LineNo := 10000;

      PurchRcptLine.SETRANGE("Order No.", PurchaseLine."Document No.");
      PurchRcptLine.SETRANGE("Order Line No.", PurchaseLine."Line No.");
      PurchRcptLine.SETCURRENTKEY("Document No.");
      IF PurchRcptLine.FINDSET THEN
        REPEAT
          IF NOT RejectionLineWithoutReturnQtyExists(PurchaseLine, PurchRcptLine) THEN BEGIN
            RejectionLine.INIT;
            RejectionLine."Document Type" := PurchaseLine."Document Type";
            RejectionLine."Document No." := PurchaseLine."Document No.";
            RejectionLine."Document Line No." := PurchaseLine."Line No.";
            RejectionLine."Line No." := LineNo;
            LineNo := LineNo + 10000;
            RejectionLine.INSERT;
            RejectionLine."Purchase Receipts No." := PurchRcptLine."Document No.";
            RejectionLine.MODIFY;
          END;
        UNTIL PurchRcptLine.NEXT = 0;
    END;

    LOCAL PROCEDURE RejectionLineWithoutReturnQtyExists@1100528400(PurchaseLine@1100528402 : Record 39;PurchRcptLine@1100528400 : Record 121) : Boolean;
    VAR
      RejectionLine@1100528401 : Record 11020374;
    BEGIN
      RejectionLine.SETRANGE("Document Type", PurchaseLine."Document Type");
      RejectionLine.SETRANGE("Document No.", PurchaseLine."Document No.");
      RejectionLine.SETRANGE("Document Line No.", PurchaseLine."Line No.");
      RejectionLine.SETRANGE("Purchase Receipts No.", PurchRcptLine."Document No.");
      RejectionLine.SETRANGE("Return Quantity", 0);
      EXIT(NOT RejectionLine.ISEMPTY);
    END;

    PROCEDURE CheckModifyAllowed@1100525006();
    BEGIN
      IF ("New Purchase Order" <> '') OR ("Purchase Return Order" <> '') THEN
        ERROR(Text004);
    END;

    PROCEDURE CheckRejectionForPurchRcptLine@1100525007(PurchRcptLine@1100525000 : Record 121);
    VAR
      RejectionLine@1100525001 : Record 11020374;
      Text001@1100525002 : TextConst 'ENU=Warning! There are rejections for purchase receipt %1 line %2 (purchase order %3, line %4).;NOR=Advarsel! Det finnes avslag for innkjõpskvittering %1 rad %2 (innkjõpsordre %3, rad %4).;SVE=Varning! Det finns avslag fîr inkîpskvitto %1 rad %2 (inkîpsorder %3, rad %4).';
    BEGIN
      RejectionLine.SETRANGE("Document Type",RejectionLine."Document Type"::Order);
      RejectionLine.SETRANGE("Document No.",PurchRcptLine."Order No.");
      RejectionLine.SETRANGE("Document Line No.",PurchRcptLine."Order Line No.");
      RejectionLine.SETRANGE("Purchase Receipts No.",PurchRcptLine."Document No.");
      RejectionLine.SETFILTER("Rejected Quantity",'>%1',0);
      IF NOT RejectionLine.ISEMPTY THEN
        MESSAGE(Text001,
          PurchRcptLine."Document No.",
          PurchRcptLine."Line No.",
          PurchRcptLine."Order No.",
          PurchRcptLine."Order Line No.");
    END;

    PROCEDURE CopyPurchLineDedetails@1210190000(FromPurchLine@1210190001 : Record 39;ToPurchLine@1210190000 : Record 39);
    VAR
      PurchLineDetail@1210190002 : Record 11020660;
      PurchLineDetail2@1210190003 : Record 11020660;
    BEGIN
      IF FromPurchLine."Document Type" <> FromPurchLine."Document Type"::Order THEN
        EXIT;

      PurchLineDetail.SETCURRENTKEY("Line Type", "Document No.", "Line No.");
      PurchLineDetail.SETRANGE("Line Type", PurchLineDetail."Line Type"::"Purchase Order");
      PurchLineDetail.SETRANGE("Document No.", FromPurchLine."Document No.");
      PurchLineDetail.SETRANGE("Line No.", FromPurchLine."Line No.");
      IF PurchLineDetail.FINDSET THEN BEGIN
        REPEAT
          PurchLineDetail2.TRANSFERFIELDS(PurchLineDetail, TRUE);
          PurchLineDetail2."Line Type" := PurchLineDetail2."Line Type"::"Purchase Order";
          PurchLineDetail2."Document No." := ToPurchLine."Document No.";
          PurchLineDetail2."Line No." := ToPurchLine."Line No.";
          PurchLineDetail2."Detail No." := PurchLineDetail2.DetermineDetailNo();
          PurchLineDetail2.INSERT;
        UNTIL PurchLineDetail.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}

