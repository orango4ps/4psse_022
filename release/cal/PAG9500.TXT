OBJECT Page 9500 Debugger
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[ENU=Debugger;
               NOR=Feilsõkingsprogram;
               SVE=Felsîkaren];
    LinksAllowed=No;
    SourceTable=Table2000000110;
    DataCaptionExpr=DataCaption;
    PageType=Card;
    RefreshOnActivate=Yes;
    PromotedActionCategoriesML=[ENU=New,Process,Report,Code Tracking,Running Code,Breakpoints,Show;
                                NOR=Ny,Prosess,Rapport,Kodesporing,Kodekjõring,Stoppunkter,Vis;
                                SVE=Ny,Process,Rapport,KodspÜrning,Kîrkod,Brytpunkter,Visa];
    ShowFilter=No;
    OnInit=BEGIN
             BreakOnError := TRUE;
             BreakpointHit := DEBUGGER.ISBREAKPOINTHIT;
             BreakEnabled := NOT BreakpointHit;
           END;

    OnOpenPage=VAR
                 DebuggedSession@1000 : Record 2000000110;
               BEGIN
                 StartTime := CURRENTDATETIME;
                 FinishTime := StartTime;
                 DebuggerManagement.GetDebuggedSession(DebuggedSession);
                 IF DebuggedSession."Session ID" = 0 THEN
                   DEBUGGER.ACTIVATE
                 ELSE BEGIN
                   DEBUGGER.ATTACH(DebuggedSession."Session ID");
                   SetAttachedSession := TRUE;
                 END;

                 IF UserPersonalization.GET(USERSECURITYID) THEN BEGIN
                   BreakOnError := UserPersonalization."Debugger Break On Error";
                   BreakOnRecordChanges := UserPersonalization."Debugger Break On Rec Changes";
                   SkipSystemTriggers := UserPersonalization."Debugger Skip System Triggers";
                 END;

                 IF BreakOnError THEN
                   DEBUGGER.BREAKONERROR(TRUE);
                 IF BreakOnRecordChanges THEN
                   DEBUGGER.BREAKONRECORDCHANGES(TRUE);
                 IF SkipSystemTriggers THEN
                   DEBUGGER.SKIPSYSTEMTRIGGERS(TRUE);

                 DebuggerManagement.ResetActionState;
               END;

    OnClosePage=BEGIN
                  IF DEBUGGER.DEACTIVATE THEN;
                  SetAttachedSession := FALSE;
                END;

    OnFindRecord=VAR
                   DebuggedSession@1003 : Record 2000000110;
                   IsBreakOnErrorMessageNew@1001 : Boolean;
                   BreakOnErrorMessage@1002 : Text;
                 BEGIN
                   IF NOT DEBUGGER.ISACTIVE AND (Which = '=') THEN
                     MESSAGE(Text007Msg);

                   IF NOT DEBUGGER.ISACTIVE THEN BEGIN
                     CurrPage.CLOSE;
                     EXIT(FALSE);
                   END;

                   BreakpointHit := DEBUGGER.ISBREAKPOINTHIT;

                   IF BreakpointHit THEN BEGIN
                     BreakOnErrorMessage := DebuggerManagement.GetLastErrorMessage(IsBreakOnErrorMessageNew);

                     IF IsBreakOnErrorMessageNew AND (BreakOnErrorMessage <> '') THEN
                       MESSAGE(STRSUBSTNO(Text002Msg,BreakOnErrorMessage));

                     ShowLastErrorEnabled := (BreakOnErrorMessage <> '') OR (DEBUGGER.GETLASTERRORTEXT <> '');

                     BreakEnabled := FALSE;
                     IF NOT SetAttachedSession THEN BEGIN
                       DebuggedSession."Session ID" := DEBUGGER.DEBUGGEDSESSIONID;
                       DebuggerManagement.SetDebuggedSession(DebuggedSession);
                       SetAttachedSession := TRUE;
                     END;
                   END ELSE BEGIN
                     IsBreakOnErrorMessageNew := FALSE;
                     ShowLastErrorEnabled := FALSE;
                     DataCaption := Text004Cap;
                   END;

                   EXIT(TRUE);
                 END;

    OnAfterGetRecord=BEGIN
                       IF BreakpointHit THEN BEGIN
                         CurrPage.Callstack.PAGE.GetCurrentCallstack(DebuggerCallstack);
                         WITH DebuggerCallstack DO BEGIN
                           IF ID <> 0 THEN
                             DataCaption := STRSUBSTNO(Text003Cap,"Object Type","Object ID","Object Name")
                           ELSE
                             DataCaption := Text004Cap;
                         END;
                         FinishTime := CURRENTDATETIME;
                       END;
                     END;

    ActionList=ACTIONS
    {
      { 4       ;    ;ActionContainer;
                      Name=Debugger Actions;
                      CaptionML=[ENU=Debugger Actions;
                                 NOR=Feilsõkingshandlinger;
                                 SVE=FelsîkningsÜtgÑrder];
                      ActionContainerType=ActionItems }
      { 25      ;1   ;Separator  }
      { 26      ;1   ;ActionGroup;
                      Name=Code Tracking;
                      CaptionML=[ENU=Code Tracking;
                                 NOR=Kodesporing;
                                 SVE=KodspÜrning] }
      { 8       ;2   ;Action    ;
                      Name=Step Into;
                      ShortCutKey=F11;
                      CaptionML=[ENU=Step Into;
                                 NOR=Steg inn i;
                                 SVE=GÜ in i];
                      ToolTipML=[ENU=Execute the current statement. If the statement contains a function call, then execute the function and break at the first statement inside the function.;
                                 NOR=Kjõr gjeldende setning. Hvis setningen inneholder et funksjonskall, kjõrer du funksjonen og stopper ved den fõrste setningen innenfor funksjonen.;
                                 SVE=Kîr den aktuella satsen. Om den innehÜller ett funktionsanrop kîr du funktionen och bryter vid den fîrsta satsen inuti funktionen.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Enabled=BreakpointHit;
                      PromotedIsBig=Yes;
                      Image=StepInto;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 StartTime := CURRENTDATETIME;
                                 WaitingForBreak;
                                 DebuggerManagement.SetCodeTrackingAction;
                                 DEBUGGER.STEPINTO;
                                 FinishTime := CURRENTDATETIME;
                               END;
                                }
      { 7       ;2   ;Action    ;
                      Name=Step Over;
                      ShortCutKey=F10;
                      CaptionML=[ENU=Step Over;
                                 NOR=Steg over;
                                 SVE=Hoppa îver];
                      ToolTipML=[ENU=Execute the current statement. If the statement contains a function call, then execute the function and break at the first statement outside the function.;
                                 NOR=Kjõr gjeldende setning. Hvis setningen inneholder et funksjonskall, kjõrer du funksjonen og stopper ved den fõrste setningen utenfor funksjonen.;
                                 SVE=Kîr den aktuella satsen. Om den innehÜller ett funktionsanrop kîr du funktionen och bryter vid den fîrsta satsen utanfîr funktionen.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Enabled=BreakpointHit;
                      PromotedIsBig=Yes;
                      Image=StepOver;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 StartTime := CURRENTDATETIME;
                                 WaitingForBreak;
                                 DebuggerManagement.SetCodeTrackingAction;
                                 DEBUGGER.STEPOVER;
                                 FinishTime := CURRENTDATETIME;
                               END;
                                }
      { 9       ;2   ;Action    ;
                      Name=Step Out;
                      ShortCutKey=Shift+F11;
                      CaptionML=[ENU=Step Out;
                                 NOR=Steg ut av;
                                 SVE=GÜ ut];
                      ToolTipML=[ENU=Execute the remaining statements in the current function and break at the next statement in the calling function.;
                                 NOR=Kjõr de gjenstÜende setningene i den gjeldende funksjonen, og stopp ved den fõrste setningen i funksjonen som utfõrer kallet.;
                                 SVE=Kîr de ÜterstÜende satserna i den aktuella funktionen och bryt vid nÑsta sats i anropsfunktionen.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Enabled=BreakpointHit;
                      PromotedIsBig=Yes;
                      Image=StepOut;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 StartTime := CURRENTDATETIME;
                                 WaitingForBreak;
                                 DebuggerManagement.SetCodeTrackingAction;
                                 DEBUGGER.STEPOUT;
                                 FinishTime := CURRENTDATETIME;
                               END;
                                }
      { 11      ;1   ;Separator  }
      { 27      ;1   ;ActionGroup;
                      Name=Running Code;
                      CaptionML=[ENU=Running Code;
                                 NOR=Kjõrer kode;
                                 SVE=Kîrkod] }
      { 12      ;2   ;Action    ;
                      Name=Continue;
                      ShortCutKey=F5;
                      CaptionML=[ENU=Continue;
                                 NOR=Fortsett;
                                 SVE=FortsÑtt];
                      ToolTipML=[ENU=Continue until the next break.;
                                 NOR=Fortsett til neste stopp.;
                                 SVE=FortsÑtt till nÑsta brytpunkt.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Enabled=BreakpointHit;
                      PromotedIsBig=Yes;
                      Image=Continue;
                      PromotedCategory=Category5;
                      OnAction=BEGIN
                                 StartTime := CURRENTDATETIME;
                                 WaitingForBreak;
                                 DebuggerManagement.SetRunningCodeAction;
                                 DEBUGGER.CONTINUE;
                                 FinishTime := CURRENTDATETIME;
                               END;
                                }
      { 31      ;2   ;Action    ;
                      Name=Break;
                      ShortCutKey=Shift+Ctrl+B;
                      CaptionML=[ENU=Break;
                                 NOR=Stopp;
                                 SVE=Bryt];
                      ToolTipML=[ENU=Break at the next statement.;
                                 NOR=Stopp ved neste setning.;
                                 SVE=Bryt vid nÑsta sats.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Enabled=BreakEnabled;
                      PromotedIsBig=Yes;
                      Image=Pause;
                      PromotedCategory=Category5;
                      OnAction=BEGIN
                                 BreakEnabled := FALSE;
                                 DebuggerManagement.SetRunningCodeAction;
                                 DEBUGGER."BREAK";
                                 FinishTime := CURRENTDATETIME;
                               END;
                                }
      { 15      ;2   ;Action    ;
                      Name=Stop;
                      ShortCutKey=Shift+F5;
                      CaptionML=[ENU=Stop;
                                 NOR=Stopp;
                                 SVE=Stoppa];
                      ToolTipML=[ENU=Stop the current activity in the session being debugged while continuing to debug the session.;
                                 NOR=Stopp den gjeldende aktiviteten i õkten som feilsõkes, mens du fortsetter Ü feilsõke õkten.;
                                 SVE=Stoppa den aktuella aktiviteten i den session som felsîks nÑr sessionen fortsÑtter att felsîkas.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Enabled=BreakpointHit;
                      PromotedIsBig=Yes;
                      Image=Stop;
                      PromotedCategory=Category5;
                      OnAction=BEGIN
                                 WaitingForBreak;
                                 DebuggerManagement.SetRunningCodeAction;
                                 DEBUGGER.STOP;
                                 FinishTime := CURRENTDATETIME;
                               END;
                                }
      { 21      ;1   ;Separator  }
      { 28      ;1   ;ActionGroup;
                      Name=Breakpoints Group;
                      CaptionML=[ENU=Breakpoints;
                                 NOR=Stoppunkter;
                                 SVE=Brytpunkter] }
      { 14      ;2   ;Action    ;
                      Name=Toggle;
                      ShortCutKey=F9;
                      CaptionML=[ENU=Toggle;
                                 NOR=Aktiver/deaktiver;
                                 SVE=Byt];
                      ToolTipML=[ENU=Toggle a breakpoint at the current line.;
                                 NOR=Aktiver/deaktiver et stoppunkt pÜ den gjeldende linjen.;
                                 SVE=Byt ett brytpunktsvillkor pÜ den aktuella raden.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ToggleBreakpoint;
                      PromotedCategory=Category6;
                      OnAction=BEGIN
                                 CurrPage.CodeViewer.PAGE.ToggleBreakpoint;
                               END;
                                }
      { 13      ;2   ;Action    ;
                      Name=Set/Clear Condition;
                      ShortCutKey=Shift+F9;
                      CaptionML=[ENU=Set/Clear Condition;
                                 NOR=Angi/fjern betingelse;
                                 SVE=StÑll in/rensa villkor];
                      ToolTipML=[ENU=Set or clear a breakpoint condition at the current line.;
                                 NOR=Angi eller fjern en stoppunktbetingelse pÜ den gjeldende linjen.;
                                 SVE=StÑll in eller rensa ett brytpunktsvillkor pÜ den aktuella raden.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ConditionalBreakpoint;
                      PromotedCategory=Category6;
                      OnAction=BEGIN
                                 CurrPage.CodeViewer.PAGE.SetBreakpointCondition;
                               END;
                                }
      { 19      ;2   ;Action    ;
                      Name=Disable All;
                      CaptionML=[ENU=Disable All;
                                 NOR=Deaktiver alle;
                                 SVE=Inaktivera alla];
                      ToolTipML=[ENU=Disable all breakpoints in the breakpoint list. You can edit the list by using the Breakpoints action.;
                                 NOR=Deaktiver alle stoppunkter i stoppunktlisten. Du kan redigere listen ved hjelp av handlingen Stoppunkter.;
                                 SVE=Inaktivera alla brytpunkter i brytpunktslistan. Du kan redigera listan genom att anvÑnda brytpunktsÜtgÑrden.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Image=DisableAllBreakpoints;
                      PromotedCategory=Category6;
                      OnAction=VAR
                                 DebuggerBreakpoint@1001 : Record 2000000100;
                               BEGIN
                                 DebuggerBreakpoint.MODIFYALL(Enabled,FALSE,TRUE);
                               END;
                                }
      { 20      ;2   ;Action    ;
                      Name=Breakpoints;
                      CaptionML=[ENU=Breakpoints;
                                 NOR=Stoppunkter;
                                 SVE=Brytpunkter];
                      ToolTipML=[ENU=Edit the breakpoint list for all objects.;
                                 NOR=Rediger stoppunktlisten for alle objekter.;
                                 SVE=Redigera brytpunktslistan fîr alla objekt.];
                      ApplicationArea=#All;
                      RunObject=Page 9505;
                      Promoted=Yes;
                      Image=BreakpointsList;
                      PromotedCategory=Category6 }
      { 35      ;2   ;Action    ;
                      Name=Break Rules;
                      CaptionML=[ENU=Break Rules;
                                 NOR=Stoppregler;
                                 SVE=Brytregler];
                      ToolTipML=[ENU=Change settings for break rules. The debugger breaks code execution for certain configurable rules as well as for breakpoints.;
                                 NOR=Endre innstillinger for stoppregler. Feilsõkingsprogrammet stopper kodekjõringen for visse konfigurerbare regler og for stoppunkter.;
                                 SVE=éndra instÑllningar fîr brytregler. Felsîkaren bryter kodkîrningen fîr vissa konfigurerbara regler samt fîr brytpunkter.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=BreakRulesList;
                      PromotedCategory=Category6;
                      OnAction=VAR
                                 DebuggerBreakRulesPage@1000 : Page 9509;
                               BEGIN
                                 DebuggerBreakRulesPage.SetBreakOnError(BreakOnError);
                                 DebuggerBreakRulesPage.SetBreakOnRecordChanges(BreakOnRecordChanges);
                                 DebuggerBreakRulesPage.SetSkipSystemTriggers(SkipSystemTriggers);

                                 IF DebuggerBreakRulesPage.RUNMODAL = ACTION::OK THEN BEGIN
                                   BreakOnError := DebuggerBreakRulesPage.GetBreakOnError;
                                   DEBUGGER.BREAKONERROR(BreakOnError);
                                   BreakOnRecordChanges := DebuggerBreakRulesPage.GetBreakOnRecordChanges;
                                   DEBUGGER.BREAKONRECORDCHANGES(BreakOnRecordChanges);
                                   SkipSystemTriggers := DebuggerBreakRulesPage.GetSkipSystemTriggers;
                                   DEBUGGER.SKIPSYSTEMTRIGGERS(SkipSystemTriggers);

                                   SaveConfiguration;
                                 END;
                               END;
                                }
      { 17      ;2   ;Action    ;
                      Name=BreakOnErrorIsOn;
                      CaptionML=[ENU=Break On Error is On;
                                 NOR=Stopp ved feil er PÜ;
                                 SVE=Brytning vid fel Ñr aktiverat];
                      ToolTipML=[ENU=The debugger breaks code execution on error.;
                                 NOR=Feilsõkingsprogrammet stopper kodekjõring ved feil.;
                                 SVE=Felsîkaren bryter kodkîrningen vid fel.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Visible=BreakOnError;
                      PromotedIsBig=Yes;
                      Image=BreakRulesOn;
                      PromotedCategory=Category6;
                      OnAction=BEGIN
                                 BreakOnError := FALSE;
                                 DEBUGGER.BREAKONERROR(BreakOnError);
                               END;
                                }
      { 18      ;2   ;Action    ;
                      Name=BreakOnErrorIsOff;
                      CaptionML=[ENU=Break On Error is Off;
                                 NOR=Stopp ved feil er Av;
                                 SVE=Brytning vid fel Ñr inaktiverat];
                      ToolTipML=[ENU=The debugger does not break code execution on error.;
                                 NOR=Feilsõkingsprogrammet stopper ikke kodekjõring ved feil.;
                                 SVE=Felsîkaren bryter inte kodkîrningen vid fel.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Visible=NOT BreakOnError;
                      PromotedIsBig=Yes;
                      Image=BreakRulesOff;
                      PromotedCategory=Category6;
                      OnAction=BEGIN
                                 BreakOnError := TRUE;
                                 DEBUGGER.BREAKONERROR(BreakOnError);
                               END;
                                }
      { 22      ;1   ;Separator  }
      { 33      ;1   ;ActionGroup;
                      Name=Show;
                      CaptionML=[ENU=Show;
                                 NOR=Vis;
                                 SVE=Visa];
                      Image=View }
      { 6       ;2   ;Action    ;
                      Name=Variables;
                      ShortCutKey=Shift+Ctrl+V;
                      CaptionML=[ENU=Variables;
                                 NOR=Variabler;
                                 SVE=Variabler];
                      ToolTipML=[ENU=View the list of variables in the current scope.;
                                 NOR=Vis listen over variabler i det gjeldende omfanget.;
                                 SVE=Visa listan îver variabler i det aktuella omfÜnget.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Enabled=BreakpointHit;
                      PromotedIsBig=Yes;
                      Image=VariableList;
                      PromotedCategory=Category7;
                      OnAction=VAR
                                 DebuggerCallstack@1001 : Record 2000000101;
                                 DebuggerVariable@1003 : Record 2000000102;
                               BEGIN
                                 CurrPage.Callstack.PAGE.GetCurrentCallstack(DebuggerCallstack);

                                 DebuggerVariable.FILTERGROUP(2);
                                 DebuggerVariable.SETRANGE("Call Stack ID",DebuggerCallstack.ID);
                                 DebuggerVariable.FILTERGROUP(0);

                                 PAGE.RUNMODAL(PAGE::"Debugger Variable List",DebuggerVariable);
                               END;
                                }
      { 36      ;2   ;Action    ;
                      Name=LastErrorMessage;
                      CaptionML=[ENU=Last Error;
                                 NOR=Siste feil;
                                 SVE=Senaste fel];
                      ToolTipML=[ENU=View the last error message shown by the session being debugged.;
                                 NOR=Vis den siste feilmeldingen fra õkten som feilsõkes.;
                                 SVE=Visa det senaste felmeddelande som visas i den session som felsîks.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Enabled=ShowLastErrorEnabled;
                      PromotedIsBig=Yes;
                      Image=PrevErrorMessage;
                      PromotedCategory=Category7;
                      OnAction=VAR
                                 DebuggerManagement@1003 : Codeunit 9500;
                                 LastErrorMessage@1001 : Text;
                                 IsLastErrorMessageNew@1002 : Boolean;
                               BEGIN
                                 LastErrorMessage := DebuggerManagement.GetLastErrorMessage(IsLastErrorMessageNew);

                                 IF LastErrorMessage = '' THEN
                                   LastErrorMessage := DEBUGGER.GETLASTERRORTEXT;

                                 MESSAGE(STRSUBSTNO(Text005Msg,LastErrorMessage));
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Part      ;
                Name=CodeViewer;
                CaptionML=[ENU=Code;
                           NOR=Kode;
                           SVE=Kod];
                ApplicationArea=#All;
                SubPageLink=Object Type=FIELD(Object Type),
                            Object ID=FIELD(Object ID),
                            Line No.=FIELD(Line No.),
                            ID=FIELD(ID);
                PagePartID=Page9504;
                ProviderID=10 }

    { 16  ;1   ;Field     ;
                CaptionML=[ENU=Duration;
                           NOR=Varighet;
                           SVE=Varaktighet];
                ToolTipML=[ENU=Specifies how long the debugger will take to run.;
                           NOR=Angir hvor lang tid feilsõkingsprogrammet tar for Ü kjõre.;
                           SVE=Anger hur lÜng tid det tar att kîra felsîkaren.];
                ApplicationArea=#All;
                SourceExpr=FinishTime - StartTime }

    { 3   ;0   ;Container ;
                ContainerType=FactBoxArea }

    { 5   ;1   ;Part      ;
                Name=Watches;
                CaptionML=[ENU=Watches;
                           NOR=OvervÜkinger;
                           SVE=Bevakningar];
                ApplicationArea=#All;
                SubPageLink=Call Stack ID=FIELD(ID);
                PagePartID=Page9503;
                ProviderID=10 }

    { 10  ;1   ;Part      ;
                Name=Callstack;
                CaptionML=[ENU=Call Stack;
                           NOR=Kallstakk;
                           SVE=Anropsstack];
                ApplicationArea=#All;
                PagePartID=Page9502 }

  }
  CODE
  {
    VAR
      DebuggerCallstack@1005 : Record 2000000101;
      UserPersonalization@1009 : Record 2000000073;
      DebuggerManagement@1012 : Codeunit 9500;
      BreakEnabled@1004 : Boolean INDATASET;
      BreakpointHit@1002 : Boolean INDATASET;
      BreakOnError@1000 : Boolean INDATASET;
      Text002Msg@1007 : TextConst '@@@=Message shown when Break On Error occurs. Include the original error message.;ENU=Break On Error Message:\ \%1;NOR=Stopp ved feilmelding:\ \%1;SVE=Brytning i felmeddelande:\ \%1';
      BreakOnRecordChanges@1003 : Boolean INDATASET;
      SkipSystemTriggers@1010 : Boolean;
      DataCaption@1001 : Text[100];
      ShowLastErrorEnabled@1006 : Boolean INDATASET;
      Text003Cap@1011 : TextConst '@@@=DataCaption when debugger is broken in application code. Example: Codeunit 1:  Application Management;ENU=%1 %2 : %3;NOR=%1 %2 : %3;SVE=%1 %2 : %3';
      Text004Cap@1013 : TextConst '@@@=DataCaption when waiting for break;ENU=Waiting for break...;NOR=Venter pÜ stopp ...;SVE=VÑntar pÜ avbrott...';
      SetAttachedSession@1008 : Boolean;
      Text005Msg@1014 : TextConst 'ENU=Last Error Message:\ \%1;NOR=Siste feilmelding:\ \%1;SVE=Senaste felmeddelande:\ \%1';
      Text007Msg@1016 : TextConst 'ENU=The session that was being debugged has closed. The Debugger Page will close.;NOR=ùkten som ble feilsõkt, er lukket. Siden for feilsõkingsprogrammet blir nÜ lukket.;SVE=Sessionen som felsîktes har stÑngts. Felsîkningssidan stÑngs.';
      StartTime@1015 : DateTime;
      FinishTime@1017 : DateTime;

    LOCAL PROCEDURE SaveConfiguration@1();
    BEGIN
      IF UserPersonalization.GET(USERSECURITYID) THEN BEGIN
        UserPersonalization."Debugger Break On Error" := BreakOnError;
        UserPersonalization."Debugger Break On Rec Changes" := BreakOnRecordChanges;
        UserPersonalization."Debugger Skip System Triggers" := SkipSystemTriggers;
        UserPersonalization.MODIFY;
      END;
    END;

    LOCAL PROCEDURE WaitingForBreak@3();
    BEGIN
      BreakEnabled := TRUE;
      CurrPage.Callstack.PAGE.ResetCallstackToTop;
    END;

    BEGIN
    END.
  }
}

