OBJECT Page 1486 Role Center Overview
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Role Center Overview;
               NOR=Oversikt over rollesenter;
               SVE=™versikt ”ver rollcenter];
    PageType=NavigatePage;
    OnInit=VAR
             ConfPersonalizationMgt@1000 : Codeunit 9170;
           BEGIN
             ConfPersonalizationMgt.GetCurrentProfileNoError(CurrAllProfile);

             IF CurrAllProfile.ISEMPTY THEN
               IF CurrAllProfile.FINDFIRST THEN
                 EXIT;

             CurrRoleCenterID := CurrAllProfile."Role Center ID";
           END;

    ActionList=ACTIONS
    {
      { 6       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
    }
  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                Name=control;
                ContainerType=ContentArea }

    { 5   ;1   ;Field     ;
                Name=ControlAddin;
                ApplicationArea=#All;
                ControlAddIn=[Microsoft.Dynamics.Nav.Client.RoleCenterSelector;PublicKeyToken=31bf3856ad364e35] }

  }
  CODE
  {
    VAR
      CurrAllProfile@1001 : Record 2000000178;
      SkipSessionUpdateRequest@1000 : Boolean;
      AcceptAction@1003 : Boolean;
      CurrRoleCenterID@1002 : Integer;

    [External]
    PROCEDURE DelaySessionUpdateRequest@1();
    BEGIN
      SkipSessionUpdateRequest := TRUE;
    END;

    [External]
    PROCEDURE SetSelectedProfile@3(SelectedScope@1001 : Option;SelectedAppId@1002 : GUID;SelectedProfileID@1000 : Code[30]);
    BEGIN
      IF CurrAllProfile.GET(SelectedScope,SelectedAppId,SelectedProfileID) THEN
        IF CurrAllProfile.FINDFIRST THEN;
    END;

    [External]
    PROCEDURE GetSelectedProfile@2(VAR ProfileScope@1000 : Option;VAR ProfileAppId@1001 : GUID;VAR ProfileId@1002 : Code[30]);
    BEGIN
      ProfileScope := CurrAllProfile.Scope;
      ProfileAppId := CurrAllProfile."App ID";
      ProfileId := CurrAllProfile."Profile ID";
    END;

    LOCAL PROCEDURE ChangeProfile@5(NewAllProfile@1000 : Record 2000000178);
    VAR
      UserPersonalization@1001 : Record 2000000073;
      AllProfile@1004 : Record 2000000178;
      ConfPersonalizationMgt@1002 : Codeunit 9170;
      SessionSet@1003 : SessionSettings;
    BEGIN
      IF NOT ConfPersonalizationMgt.GetCurrentProfileNoError(AllProfile) THEN
        EXIT;

      ConfPersonalizationMgt.SetCurrentProfile(NewAllProfile);

      UserPersonalization.GET(USERSECURITYID);

      WITH SessionSet DO BEGIN
        INIT;
        PROFILEID := NewAllProfile."Profile ID";
        PROFILEAPPID := NewAllProfile."App ID";
        PROFILESYSTEMSCOPE := NewAllProfile.Scope = NewAllProfile.Scope::System;
        LANGUAGEID := UserPersonalization."Language ID";
        LOCALEID := UserPersonalization."Locale ID";
        TIMEZONE := UserPersonalization."Time Zone";
        REQUESTSESSIONUPDATE(TRUE);
      END;
    END;

    LOCAL PROCEDURE SendJsonToControlAddIn@8();
    VAR
      RolecenterSelectorMgt@1001 : Codeunit 1485;
      Json@1000 : Text;
    BEGIN
      Json := RolecenterSelectorMgt.BuildJsonFromPageActionTable(CurrRoleCenterID);
      CurrPage.ControlAddin.LoadRoleCenterFromJson(Json);
    END;

    [External]
    PROCEDURE GetAcceptAction@7() : Boolean;
    BEGIN
      EXIT(AcceptAction);
    END;

    EVENT ControlAddin@-5::ControlAddInReady@4();
    VAR
      RolecenterSelectorMgt@1000 : Codeunit 1485;
      Json@1001 : Text;
    BEGIN
      Json := RolecenterSelectorMgt.BuildPageDataJsonForRolecenterSelector;
      CurrPage.ControlAddin.LoadPageDataFromJson(Json);
      SendJsonToControlAddIn;
      CurrPage.ControlAddin.SetCurrentProfileId(FORMAT(CurrAllProfile.RECORDID));
    END;

    EVENT ControlAddin@-5::OnAcceptAction@5();
    BEGIN
      AcceptAction := TRUE;
      IF NOT SkipSessionUpdateRequest THEN
        ChangeProfile(CurrAllProfile);
      CurrPage.CLOSE;
    END;

    EVENT ControlAddin@-5::OnProfileSelected@6(profileId@1000 : Text);
    VAR
      AllProfileRecordId@1002 : RecordID;
    BEGIN
      EVALUATE(AllProfileRecordId,profileId);
      IF CurrAllProfile.GET(AllProfileRecordId) THEN BEGIN
        CurrRoleCenterID := CurrAllProfile."Role Center ID";
        SendJsonToControlAddIn;
      END;
    END;

    BEGIN
    END.
  }
}

