OBJECT Codeunit 5377 Webhook Management
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      ClientTypeManagement@1000 : Codeunit 4030;

    [External]
    PROCEDURE GetNotificationUrl@3() NotificationUrl : Text[250];
    BEGIN
      NotificationUrl := GETURL(CLIENTTYPE::OData);
      NotificationUrl := COPYSTR(NotificationUrl,1,STRPOS(NotificationUrl,FORMAT(CLIENTTYPE::OData)) - 1) + 'api/webhooks';
    END;

    [External]
    PROCEDURE IsCurrentClientTypeAllowed@6() : Boolean;
    BEGIN
      EXIT(NOT (ClientTypeManagement.GetCurrentClientType IN [CLIENTTYPE::Management,CLIENTTYPE::NAS]));
    END;

    [External]
    PROCEDURE IsSyncAllowed@5() : Boolean;
    VAR
      CompanyInformation@1003 : Record 79;
      CompanyInformationMgt@1002 : Codeunit 1306;
      PermissionManager@1001 : Codeunit 9002;
    BEGIN
      IF NOT CompanyInformation.GET THEN
        EXIT(FALSE);

      IF NOT PermissionManager.SoftwareAsAService THEN
        EXIT(FALSE);

      IF CompanyInformationMgt.IsDemoCompany THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    [External]
    PROCEDURE FindMatchingWebhookSubscription@8(VAR WebhookSubscription@1000 : Record 2000000199;SubscriptionEndpoint@1006 : Text) : Boolean;
    BEGIN
      WebhookSubscription.SETRANGE("Company Name",COMPANYNAME);
      IF WebhookSubscription.FIND('-') THEN
        REPEAT
          IF WebhookSubscription.Endpoint = SubscriptionEndpoint THEN
            EXIT(TRUE);
        UNTIL WebhookSubscription.NEXT = 0;
    END;

    PROCEDURE FindMatchingWebhookSubscriptionRegex@9(VAR WebhookSubscription@1000 : Record 2000000199;EndpointRegex@1001 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.RegularExpressions.Regex") : Boolean;
    BEGIN
      WebhookSubscription.SETRANGE("Company Name",COMPANYNAME);
      IF WebhookSubscription.FINDSET THEN
        REPEAT
          IF EndpointRegex.IsMatch(WebhookSubscription.Endpoint) THEN
            EXIT(TRUE);
        UNTIL WebhookSubscription.NEXT = 0;
    END;

    PROCEDURE FindWebhookSubscriptionMatchingEndPoint@1(VAR WebhookSubscription@1000 : Record 2000000199;EndpointUri@1001 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Uri";StartIndex@1003 : Integer;PathLength@1006 : Integer) : Boolean;
    VAR
      SubscriptionEndpointUri@1005 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Uri";
      SearchSubString@1004 : Text;
      IsSameEndpoint@1002 : Boolean;
    BEGIN
      IF WebhookSubscription.FINDSET THEN
        REPEAT
          SubscriptionEndpointUri := SubscriptionEndpointUri.Uri(WebhookSubscription.Endpoint);
          IsSameEndpoint := FALSE;
          IF StartIndex <= 0 THEN
            IsSameEndpoint := (EndpointUri.ToString = SubscriptionEndpointUri.ToString)
          ELSE
            IF (SubscriptionEndpointUri.Scheme = EndpointUri.Scheme) AND
               (SubscriptionEndpointUri.Host = EndpointUri.Host)
            THEN BEGIN
              SearchSubString := COPYSTR(EndpointUri.PathAndQuery,StartIndex + PathLength);
              IF STRPOS(SubscriptionEndpointUri.PathAndQuery,SearchSubString) > 0 THEN
                IsSameEndpoint := TRUE;
            END;

          IF IsSameEndpoint AND (COMPANYNAME = WebhookSubscription."Company Name") THEN
            EXIT(TRUE);
        UNTIL WebhookSubscription.NEXT = 0;
    END;

    [External]
    PROCEDURE FindWebhookSubscriptionMatchingEndPointUri@7(VAR WebhookSubscription@1000 : Record 2000000199;EndpointUriTxt@1001 : Text;StartIndex@1003 : Integer;PathLength@1006 : Integer) : Boolean;
    VAR
      EndpointUri@1007 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Uri";
      SubscriptionEndpointUri@1005 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Uri";
      SearchSubString@1004 : Text;
      IsSameEndpoint@1002 : Boolean;
    BEGIN
      EndpointUri := EndpointUri.Uri(EndpointUriTxt);
      IF WebhookSubscription.FINDSET THEN
        REPEAT
          SubscriptionEndpointUri := SubscriptionEndpointUri.Uri(WebhookSubscription.Endpoint);
          IsSameEndpoint := FALSE;
          IF StartIndex <= 0 THEN
            IsSameEndpoint := (EndpointUri.ToString = SubscriptionEndpointUri.ToString)
          ELSE
            IF (SubscriptionEndpointUri.Scheme = EndpointUri.Scheme) AND
               (SubscriptionEndpointUri.Host = EndpointUri.Host)
            THEN BEGIN
              SearchSubString := COPYSTR(EndpointUri.PathAndQuery,StartIndex + PathLength);
              IF STRPOS(SubscriptionEndpointUri.PathAndQuery,SearchSubString) > 0 THEN
                IsSameEndpoint := TRUE;
            END;

          IF IsSameEndpoint AND (COMPANYNAME = WebhookSubscription."Company Name") THEN
            EXIT(TRUE);
        UNTIL WebhookSubscription.NEXT = 0;
    END;

    [External]
    PROCEDURE IsValidNotificationRunAsUser@2(UserSecurityId@1001 : GUID) : Boolean;
    VAR
      User@1000 : Record 2000000120;
      EmptyGUID@1002 : GUID;
    BEGIN
      IF UserSecurityId = EmptyGUID THEN
        EXIT(FALSE);

      IF NOT User.GET(UserSecurityId) THEN
        EXIT(FALSE);

      EXIT(User."License Type" <> User."License Type"::"External User");
    END;

    BEGIN
    END.
  }
}

