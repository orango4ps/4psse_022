OBJECT Report 5756 Create Warehouse Location
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.01;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Lagerort erstellen;
               ENU=Create Warehouse Location;
               NLD=Magazijnvestiging maken;
               NOR=Opprett lagerlokasjon;
               SVE=Skapa dist.lagerplats];
    ApplicationArea=#Warehouse;
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  WhseEntry@1000 : Record 7312;
                BEGIN
                  IF LocCode = '' THEN
                    ERROR(Text001);
                  IF AdjBinCode = '' THEN
                    ERROR(Text002);

                  ItemLedgEntry.RESET;
                  ItemLedgEntry.SETCURRENTKEY("Item No.",Open);
                  IF ItemLedgEntry.FIND('-') THEN
                    REPEAT
                      ItemLedgEntry.SETRANGE("Item No.",ItemLedgEntry."Item No.");
                      ItemLedgEntry.SETRANGE(Open,TRUE);
                      ItemLedgEntry.SETRANGE("Location Code",LocCode);
                      Found := ItemLedgEntry.FIND('-');
                      IF Found AND NOT HideValidationDialog THEN
                        IF NOT CONFIRM(STRSUBSTNO('%1 %2 %3 %4 %5 %6 %7 %8',
                               Text010,Text011,Text012,Text013,Text014,
                               Text015,STRSUBSTNO(Text016,LocCode),Text017),FALSE)
                        THEN
                          CurrReport.QUIT;
                      ItemLedgEntry.SETRANGE("Location Code");
                      ItemLedgEntry.SETRANGE(Open);
                      ItemLedgEntry.FIND('+');
                      ItemLedgEntry.SETRANGE("Item No.");
                    UNTIL (ItemLedgEntry.NEXT = 0) OR Found;

                  IF NOT Found THEN
                    ERROR(Text018,Location.TABLECAPTION,Location.FIELDCAPTION(Code),LocCode);
                  CLEAR(ItemLedgEntry);

                  WhseEntry.SETRANGE("Location Code",LocCode);
                  IF NOT WhseEntry.ISEMPTY THEN
                    ERROR(
                      Text019,LocCode,WhseEntry.TABLECAPTION);

                  TempWhseJnlLine.RESET;
                  TempWhseJnlLine.DELETEALL;

                  LastLineNo := 0;

                  WITH ItemLedgEntry DO BEGIN
                    SETCURRENTKEY(
                      "Item No.","Location Code",Open,"Variant Code","Unit of Measure Code","Lot No.","Serial No.");

                    Location.GET(LocCode);
                    Location.TESTFIELD("Adjustment Bin Code",'');
                    CheckWhseDocs;

                    Bin.GET(LocCode,AdjBinCode);

                    IF FIND('-') THEN BEGIN
                      IF NOT HideValidationDialog THEN BEGIN
                        Window.OPEN(STRSUBSTNO(Text020,"Location Code") + Text003);
                        i := 1;
                        CountItemLedgEntries := COUNT;
                      END;

                      REPEAT
                        IF NOT HideValidationDialog THEN BEGIN
                          Window.UPDATE(100,i);
                          Window.UPDATE(102,ROUND(i / CountItemLedgEntries * 10000,1));
                        END;

                        SETRANGE("Item No.","Item No.");
                        IF FIND('-') THEN BEGIN
                          SETRANGE("Location Code",LocCode);
                          SETRANGE(Open,TRUE);
                          IF FIND('-') THEN
                            REPEAT
                              SETRANGE("Variant Code","Variant Code");
                              IF FIND('-') THEN
                                REPEAT
                                  SETRANGE("Unit of Measure Code","Unit of Measure Code");
                                  IF FIND('-') THEN
                                    REPEAT
                                      SETRANGE("Lot No.","Lot No.");
                                      IF FIND('-') THEN
                                        REPEAT
                                          SETRANGE("Serial No.","Serial No.");
                                          CALCSUMS("Remaining Quantity");
                                          IF "Remaining Quantity" < 0 THEN
                                            ERROR(
                                              STRSUBSTNO(Text005,BuildErrorText) +
                                              STRSUBSTNO(Text009,ItemsWithNegativeInventory.OBJECTID));
                                          IF "Remaining Quantity" > 0 THEN
                                            CreateWhseJnlLine;
                                          FIND('+');
                                          SETRANGE("Serial No.");
                                        UNTIL NEXT = 0;
                                      FIND('+');
                                      SETRANGE("Lot No.");
                                    UNTIL NEXT = 0;
                                  FIND('+');
                                  SETRANGE("Unit of Measure Code")
                                UNTIL NEXT = 0;
                              FIND('+');
                              SETRANGE("Variant Code");
                            UNTIL NEXT = 0;
                        END;

                        SETRANGE(Open);
                        SETRANGE("Location Code");
                        FIND('+');
                        IF NOT HideValidationDialog THEN
                          i := i + COUNT;
                        SETRANGE("Item No.");
                      UNTIL NEXT = 0;
                    END;
                  END;
                END;

    OnPostReport=VAR
                   WhseJnlRegisterLine@1000 : Codeunit 7301;
                 BEGIN
                   Location."Bin Mandatory" := TRUE;
                   Location.VALIDATE("Directed Put-away and Pick",TRUE);
                   Location.VALIDATE("Adjustment Bin Code",AdjBinCode);
                   Location.MODIFY;

                   IF TempWhseJnlLine.FIND('-') THEN
                     REPEAT
                       WhseJnlRegisterLine.RegisterWhseJnlLine(TempWhseJnlLine);
                     UNTIL TempWhseJnlLine.NEXT = 0;

                   IF NOT HideValidationDialog THEN BEGIN
                     Window.CLOSE;
                     MESSAGE(Text004);
                   END;
                 END;

    UsageCategory=Tasks;
  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1   ;2   ;Field     ;
                  CaptionML=[DEU=Lagerortcode;
                             ENU=Location Code;
                             NLD=Vestiging;
                             NOR=Lokasjonskode;
                             SVE=LagerstÑllekod];
                  ToolTipML=[DEU="Gibt den Lagerort an, an dem die LageraktivitÑt erfolgt. ";
                             ENU="Specifies the location where the warehouse activity takes place. ";
                             NLD="Hiermee wordt de locatie opgegeven waar de magazijnactiviteit wordt uitgevoerd. ";
                             NOR="Angir lokasjonen der lageraktiviteten finner sted. ";
                             SVE="Anger lagerstÑllet dÑr distributionslageraktiviteten utfîrs. "];
                  ApplicationArea=#Warehouse;
                  SourceExpr=LocCode;
                  OnValidate=BEGIN
                               Location.GET(LocCode);
                               Location.TESTFIELD("Bin Mandatory",FALSE);
                               Location.TESTFIELD("Use As In-Transit",FALSE);
                               Location.TESTFIELD("Directed Put-away and Pick",FALSE);
                             END;

                  OnLookup=BEGIN
                             CLEAR(Location);
                             IF LocCode <> '' THEN
                               Location.Code := LocCode;
                             IF PAGE.RUNMODAL(0,Location) = ACTION::LookupOK THEN BEGIN
                               Location.TESTFIELD("Bin Mandatory",FALSE);
                               Location.TESTFIELD("Use As In-Transit",FALSE);
                               Location.TESTFIELD("Directed Put-away and Pick",FALSE);
                               LocCode := Location.Code;
                             END;
                           END;
                            }

      { 3   ;2   ;Field     ;
                  CaptionML=[DEU=Ausgleichslagerplatzcode;
                             ENU=Adjustment Bin Code;
                             NLD=Herwaarderingsopslaglocatie;
                             NOR=Hyllekode for justering;
                             SVE=Justering lagerplatskod];
                  ToolTipML=[DEU=Gibt den Code des Artikels in der LagerplatzÅbersicht an.;
                             ENU=Specifies the code of the item on the bin list.;
                             NLD=Hiermee wordt de code opgegeven van het artikel in het opslaglocatieoverzicht.;
                             NOR=Angir koden for varen i hylleoversikten.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=AdjBinCode;
                  OnValidate=BEGIN
                               IF AdjBinCode <> '' THEN
                                 IF LocCode <> '' THEN
                                   Bin.GET(LocCode,AdjBinCode)
                                 ELSE BEGIN
                                   Bin.SETRANGE(Code,AdjBinCode);
                                   Bin.FINDFIRST;
                                   LocCode := Bin."Location Code";
                                 END;
                             END;

                  OnLookup=BEGIN
                             Bin.RESET;
                             IF LocCode <> '' THEN
                               Bin.SETRANGE("Location Code",LocCode);

                             IF PAGE.RUNMODAL(0,Bin) = ACTION::LookupOK THEN BEGIN
                               IF LocCode = '' THEN
                                 LocCode := Bin."Location Code";
                               AdjBinCode := Bin.Code;
                             END;
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TempWhseJnlLine@1007 : TEMPORARY Record 7311;
      Item@1020 : Record 27;
      ItemLedgEntry@1011 : Record 32;
      ItemUnitOfMeasure@1001 : Record 5404;
      Location@1002 : Record 14;
      Bin@1003 : Record 7354;
      ItemsWithNegativeInventory@1010 : Report 5757;
      WMSMgt@1019 : Codeunit 7302;
      UOMMgt@1014 : Codeunit 5402;
      Window@1013 : Dialog;
      LocCode@1000 : Code[10];
      Text001@1042 : TextConst 'DEU=Geben Sie einen Lagerortcode ein.;ENU=Enter a location code.;NLD=Voer een vestigingscode in.;NOR=Angi en lokasjonskode.';
      Text002@1012 : TextConst 'DEU=Geben Sie einen Ausgleichslagerplatzcode ein.;ENU=Enter an adjustment bin code.;NLD=Voer een herwaarderingsopslaglocatie in.;NOR=Angi en hyllekode for justering.';
      Text003@1004 : TextConst 'DEU=Anzahl #100##### @102@@@@@@@@;ENU=Count #100##### @102@@@@@@@@;NLD=Tellen #100##### @102@@@@@@@@;NOR=Ant.  #100##### @102@@@@@@@@';
      AdjBinCode@1009 : Code[20];
      i@1005 : Integer;
      CountItemLedgEntries@1029 : Integer;
      LastLineNo@1008 : Integer;
      Text004@1015 : TextConst 'DEU=Die Konvertierung wurde erfolgreich beendet.;ENU=The conversion was successfully completed.;NLD=De conversie is voltooid.;NOR=Konverteringen er fullfõrt.';
      Text005@1016 : TextConst 'DEU=Der Lagerort weist negative BestÑnde auf. Bereinigen Sie bitte diese negativen LagerbestÑnde, bevor Sie mit der Umwandlung fortfahren.\\%1.\\;ENU=Negative inventory was found in the location. You must clear this negative inventory in the program before you can proceed with the conversion.\\%1.\\;NLD=Negatieve voorraad gevonden in de vestiging. U moet deze negatieve voorraad in het programma wissen voordat u kunt doorgaan met de conversie.\\%1.\\;NOR=Fant negativ beholdning pÜ lokasjonen. Du mÜ fjerne denne negative beholdningen i programmet fõr du kan fortsette med konverteringen.\\%1.\\';
      HideValidationDialog@1018 : Boolean;
      Text006@1022 : TextConst 'DEU=Der Lagerort %1 kann nicht umgewandelt werden, da mindestens ein %2 noch nicht vollstÑndig gebucht ist.\\Buchen oder lîschen Sie diese alle, bevor Sie diese Stapelverarbeitung erneut starten.;ENU=Location %1 cannot be converted because at least one %2 is not completely posted yet.\\Post or delete all of them before restarting the conversion batch job.;NLD=Locatie %1 kan niet worden geconverteerd omdat ten minste ÇÇn %2 nog niet volledig is geboekt.\\Boek of verwijder alles voordat u opnieuw de conversie batchverwerking opstart.;NOR=Lokasjonen %1 kan ikke konverteres fordi minst Çn %2 ikke er fullstendig bokfõrt ennÜ.\\Bokfõr eller slett alle fõr du starter konverteringskjõrselen pÜ nytt.';
      Text007@1023 : TextConst 'DEU=Der Lagerort %1 kann nicht umgewandelt werden, da mindestens ein %2 noch nicht vollstÑndig registriert ist.\\Registrieren oder lîschen Sie diese bitte alle, bevor Sie diese Stapelverarbeitung erneut starten.;ENU=Location %1 cannot be converted because at least one %2 is not completely registered yet.\\Register or delete all of them before restarting the conversion batch job.;NLD=Locatie %1 kan niet worden geconverteerd omdat ten minste ÇÇn %2 nog niet volledig is geregistreerd.\\Registreer of verwijder alles voordat u opnieuw de conversie batchverwerking opstart.;NOR=Lokasjonen %1 kan ikke konverteres fordi minst Çn %2 ikke er fullstendig registrert ennÜ.\\Registrer eller slett alle fõr du starter konverteringskjõrselen pÜ nytt.';
      Text008@1024 : TextConst 'DEU=Der Lagerort %1 kann nicht umgewandelt werden, da mindestens eine %2 existiert.\\Lîschen Sie diese bitte alle, bevor Sie diese Stapelverarbeitung erneut starten.;ENU=Location %1 cannot be converted because at least one %2 exists.\\Delete all of them before restarting the conversion batch job.;NLD=Locatie %1 kan niet worden geconverteerd omdat ten minste ÇÇn %2 bestaat.\\Verwijder alles voordat u opnieuw de conversie batchverwerking opstart.;NOR=Lokasjonen %1 kan ikke konverteres fordi det finnes minst Çn %2.\\Slett alle fõr du starter konverteringskjõrselen pÜ nytt.';
      Text009@1030 : TextConst 'DEU=Verwenden Sie bitte den Bericht %1, um alle negativen LagerbestÑnde des Lagerortes zu ermitteln.;ENU=Run %1 for a report of all negative inventory in the location.;NLD=Voer %1 uit om een rapport te genereren van de totale negatieve voorraad in de vestiging.;NOR=Kjõr %1 for en rapport om alle negative beholdninger pÜ lokasjonen.';
      Text010@1039 : TextConst 'DEU=Dieser Lagerort weist LagerbestÑnde auf. Klicken Sie bitte auf "Ja", um zu bestÑtigen, dass dieser Lagerort fÅr den Logistikbereich zur VerfÅgung gestellt werden soll. Dies erfolgt mit Hilfe einer Stapelverarbeitung, die Lagerplatzposten fÅr den Lagerbestand dieses Lagerortes erstellt.\\;ENU=Inventory exists on this location. By choosing Yes from this warning, you are confirming that you want to enable this location to use Warehouse Management Systems by running a batch job to create warehouse entries for the inventory in this location.\\;NLD=Deze vestiging heeft voorraad. Klik op Ja, als u deze vestiging wilt activeren voor WMS door een batchverwerking uit te voeren om magazijnposten voor de voorraad in de vestiging te maken.\\;NOR=Det finnes beholdning pÜ denne lokasjonen. Hvis du velger Ja i denne advarselen, bekrefter du at du vil gjõre det mulig for lokasjonen Ü bruke lagerstyring ved Ü kjõre en kjõrsel som oppretter lagerposter for beholdningen pÜ lokasjonen.\\';
      Text011@1038 : TextConst
        'DEU="Falls Sie fortfahren mîchten, stellen Sie bitte sicher, dass keine negativen LagerbestÑnde am Lagerort vorhanden sind. Negative LagerbestÑnde an Lagerorten, die im Lagerverwaltungssystem verwendet werden, sind nicht zulÑssig und mÅssen durch eine entsprechende Lagerbuchung bereinigt werden. "',
        'ENU="If you want to proceed, you must first ensure that no negative inventory exists in the location. Negative inventory is not allowed in a location that uses warehouse management logic and must be cleared by posting a suitable quantity to inventory. "',
        'NLD="Als u wilt doorgaan, moet u zeker zijn dat er geen negatieve voorraad is in de locatie. Negatieve voorraad in een locatie is niet toegestaan als magazijnbeheer wordt gebruikt. Dit moet worden verwerkt door een passend aantal in voorraad te boeken. "',
        'NOR="Hvis du vil fortsette, mÜ du fõrst kontrollere at det ikke finnes noen negativ beholdning pÜ lokasjonen. Negativ beholdning er ikke tillatt pÜ en lokasjon som bruker lagerstyringslogikk, og mÜ fjernes ved Ü bokfõre et passende antall i beholdningen. "';
      Text012@1037 : TextConst 'DEU=Sie kînnen eine PrÅfung auf negative LagerbestÑnde durchfÅhren. Verwenden Sie hierzu den Bericht "Artikel mit negativem Lagerbestand".\\;ENU=You can perform a check for negative inventory by using the Items with Negative Inventory report.\\;NLD=U kunt een controle uitvoeren voor negatieve voorraad. Gebruik hiervoor het rapport Artikelen met het negatieve voorraad.\\;NOR=Du kan utfõre en kontroll for negativ beholdning ved hjelp av rapporten Varer med negativ beholdning.\\';
      Text013@1036 : TextConst 'DEU="Wenn Sie sichergestellt haben, dass an diesem Lagerort keine negativen LagerbestÑnde existieren, setzen Sie diese Stapelverarbeitung fort. Sollten dennoch negative BestÑnde vorhanden sein, wird die Stapelverarbeitung mit einer Fehlermeldung abgebrochen. ";ENU="If you can confirm that no negative inventory exists in the location, proceed with the conversion batch job. If negative inventory is found, the batch job will stop with an error message. ";NLD="Als u bevestigt dat er geen negatieve voorraad in de locatie is, kunt u doorgaan conversie batchverwerking. Als er negatieve voorraad wordt gevonden, wordt de batchverwerking gestopt met een foutmelding. ";NOR="Hvis du kan bekrefte at det ikke finnes noen negativ beholdning pÜ lokasjonen, fortsetter du med konverteringskjõrselen. Hvis det finnes negativ beholdning, stoppes kjõrselen med en feilmelding. "';
      Text014@1035 : TextConst
        'DEU=Diese Stapelverarbeitung wird Erîffnungslagerplatzposten erstellen. Sie mÅssen diese Erîffnungslagerplatzposten im Ausgleichslagerplatz ausgleichen, indem Sie ein Logistik Inventur Buch.-Blatt oder ein Logistik Artikel Buch.-Blatt fÅr die Zuweisung von Zonen und LagerplÑtzen buchen.\',
        'ENU=The result of this batch job is that initial warehouse entries will be created. You must balance these initial warehouse entries on the adjustment bin by posting a warehouse physical inventory journal or a warehouse item journal to assign zones and bins to items.\',
        'NLD=Deze batchverwerking zorgt ervoor dat initiâle magazijnposten worden gemaakt. U moet deze initiâle magazijnposten salderen op de herwaarderingsopslaglocatie door artikelen aan zones en opslaglocaties toe te wijzen d.m.v. een magazijninventarisatiedagboek of een magazijnartikeldagboek.\',
        'NOR=Resultatet av denne kjõrselen vil vëre at det blir opprettet  innledende lagerposter. Du mÜ balansere disse innledende lagerpostene i justeringshyllen ved Ü bokfõre en vareopptellingskladd for lager eller en lagerkladd for Ü tilordne soner og hyller til varer.\';
      Text015@1034 : TextConst 'DEU=Sie mÅssen erst Zonen und LagerplÑtze erstellen, bevor Sie eine Logistik Inventur buchen kînnen.\\;ENU=You must create zones and bins before posting a warehouse physical inventory.\\;NLD=U moet zones en opslaglocaties maken voordat een magazijninventarisatie wordt geboekt.\\;NOR=Du mÜ opprette soner og hyller fõr du bokfõrer en vareopptelling for lager.\\';
      Text016@1033 : TextConst 'DEU=Nach erfolgreichem Abschluss der Stapelverarbeitung wird der Lagerort %1 ein Lagerort fÅr das Lagerverwaltungssystem sein. Diese Umwandlung kann anschlie·end nicht zurÅckgefÅhrt oder rÅckgÑngig gemacht werden.;ENU=Location %1 will be a warehouse management location after the batch job has run successfully. This conversion cannot be reversed or undone after it has run.;NLD=Locatie %1 wordt een vestiging voor magazijnbeheer als de batchverwerking probleemloos is uitgevoerd. Deze conversie kan na de batchverwerking niet worden teruggedraaid of ongedaan worden gemaakt.;NOR=Lokasjonen %1 vil vëre en lokasjon for lagerstyring etter at kjõrselen er fullfõrt. Denne konverteringen kan ikke tilbakefõres eller angres etter kjõring.';
      Text017@1032 : TextConst 'DEU=\\Mîchten Sie wirklich fortfahren?;ENU=\\Do you really want to proceed?;NLD=\\Wilt u doorgaan?;NOR=\\Vil du fortsette?';
      Text018@1031 : TextConst 'DEU=FÅr den %1 %2 ''%3'' gibt es nichts zu konvertieren.;ENU=There is nothing to convert for %1 %2 ''%3''.;NLD=Niets te converteren voor %1 %2 ''%3''.;NOR=Det finnes ingenting Ü konvertere for %1 %2 %3.';
      Text019@1041 : TextConst 'DEU=Der Lagerort %1 kann nicht umgewandelt werden, da mindestens ein %2 fÅr diesen Lagerort existiert.;ENU=Location %1 cannot be converted because at least one %2 exists for this location.;NLD=Locatie %1 kan niet worden geconverteerd omdat ten minste ÇÇn %2 bestaat voor deze locatie.;NOR=Lokasjonen %1 kan ikke konverteres fordi det finnes minst Çn %2 for denne lokasjonen.';
      Text020@1006 : TextConst 'DEU=Der Lagerort %1 wird in einen Lagerort fÅr das Lagerverwaltungssystem umgewandelt.\\Dies kann einige Zeit in Anspruch nehmen. Haben Sie bitte etwas Geduld.;ENU=Location %1 will be converted to a WMS location.\\This might take some time so please be patient.;NLD=Locatie %1 wordt geconverteerd naar een WMS-vestiging.\\Dit kan enige minuten duren, een ogenblik geduld.;NOR=Lokasjonen %1 vil bli konvertert til en lokasjon for lagerstyring.\\Dette kan ta litt tid, sÜ vër tÜlmodig.';
      Found@1017 : Boolean;

    LOCAL PROCEDURE CheckWhseDocs@5();
    VAR
      WhseRcptHeader@1000 : Record 7316;
      WarehouseShipmentHeader@1001 : Record 7320;
      WhseActivHeader@1002 : Record 5766;
      WhseWkshLine@1003 : Record 7326;
    BEGIN
      WhseRcptHeader.SETRANGE("Location Code",Location.Code);
      IF NOT WhseRcptHeader.ISEMPTY THEN
        ERROR(
          Text006,
          Location.Code,
          WhseRcptHeader.TABLECAPTION);

      WarehouseShipmentHeader.SETRANGE("Location Code",Location.Code);
      IF NOT WarehouseShipmentHeader.ISEMPTY THEN
        ERROR(
          Text006,
          Location.Code,
          WarehouseShipmentHeader.TABLECAPTION);

      WhseActivHeader.SETCURRENTKEY("Location Code");
      WhseActivHeader.SETRANGE("Location Code",Location.Code);
      IF WhseActivHeader.FINDFIRST THEN
        ERROR(
          Text007,
          Location.Code,
          WhseActivHeader.Type);

      WhseWkshLine.SETRANGE("Location Code",Location.Code);
      IF NOT WhseWkshLine.ISEMPTY THEN
        ERROR(
          Text008,
          Location.Code,
          WhseWkshLine.TABLECAPTION);
    END;

    LOCAL PROCEDURE CreateWhseJnlLine@4();
    BEGIN
      LastLineNo := LastLineNo + 10000;

      WITH ItemLedgEntry DO BEGIN
        TempWhseJnlLine.INIT;
        TempWhseJnlLine."Entry Type" := TempWhseJnlLine."Entry Type"::"Positive Adjmt.";
        TempWhseJnlLine."Line No." := LastLineNo;
        TempWhseJnlLine."Location Code" := "Location Code";
        TempWhseJnlLine."Registering Date" := TODAY;
        TempWhseJnlLine."Item No." := "Item No.";
        TempWhseJnlLine.Quantity := ROUND("Remaining Quantity" / "Qty. per Unit of Measure",UOMMgt.QtyRndPrecision);
        TempWhseJnlLine."Qty. (Base)" := "Remaining Quantity";
        TempWhseJnlLine."Qty. (Absolute)" := ROUND(ABS("Remaining Quantity") / "Qty. per Unit of Measure",UOMMgt.QtyRndPrecision);
        TempWhseJnlLine."Qty. (Absolute, Base)" := ABS("Remaining Quantity");
        TempWhseJnlLine."User ID" := USERID;
        TempWhseJnlLine."Variant Code" := "Variant Code";
        IF "Unit of Measure Code" = '' THEN BEGIN
          Item.GET("Item No.");
          "Unit of Measure Code" := Item."Base Unit of Measure";
        END;
        TempWhseJnlLine."Unit of Measure Code" := "Unit of Measure Code";
        TempWhseJnlLine."Qty. per Unit of Measure" := "Qty. per Unit of Measure";
        TempWhseJnlLine."Lot No." := "Lot No.";
        TempWhseJnlLine."Serial No." := "Serial No.";
        TempWhseJnlLine.VALIDATE("Zone Code",Bin."Zone Code");
        TempWhseJnlLine."Bin Code" := AdjBinCode;
        TempWhseJnlLine."To Bin Code" := AdjBinCode;
        GetItemUnitOfMeasure("Item No.","Unit of Measure Code");
        TempWhseJnlLine.Cubage := TempWhseJnlLine."Qty. (Absolute)" * ItemUnitOfMeasure.Cubage;
        TempWhseJnlLine.Weight := TempWhseJnlLine."Qty. (Absolute)" * ItemUnitOfMeasure.Weight;
        OnCreateWhseJnlLineOnBeforeCheck(TempWhseJnlLine,ItemLedgEntry);
        WMSMgt.CheckWhseJnlLine(TempWhseJnlLine,0,0,FALSE);
        TempWhseJnlLine.INSERT;
      END;
    END;

    LOCAL PROCEDURE GetItemUnitOfMeasure@15(ItemNo@1000 : Code[20];UOMCode@1001 : Code[10]);
    BEGIN
      IF (ItemUnitOfMeasure."Item No." <> ItemNo) OR
         (ItemUnitOfMeasure.Code <> UOMCode)
      THEN
        IF NOT ItemUnitOfMeasure.GET(ItemNo,UOMCode) THEN
          ItemUnitOfMeasure.INIT;
    END;

    LOCAL PROCEDURE BuildErrorText@7() : Text[250];
    VAR
      Text@1000 : Text[250];
    BEGIN
      WITH ItemLedgEntry DO BEGIN
        Text :=
          STRSUBSTNO(
            '%1: %2, %3: %4',
            FIELDCAPTION("Location Code"),"Location Code",
            FIELDCAPTION("Item No."),"Item No.");
        IF "Variant Code" <> '' THEN
          Text :=
            STRSUBSTNO('%1, %2: %3',Text,
              FIELDCAPTION("Variant Code"),"Variant Code");
        IF "Unit of Measure Code" <> '' THEN
          Text :=
            STRSUBSTNO('%1, %2: %3',Text,
              FIELDCAPTION("Unit of Measure Code"),
              "Unit of Measure Code");
        IF "Lot No." <> '' THEN
          Text :=
            STRSUBSTNO('%1, %2: %3',Text,
              FIELDCAPTION("Lot No."),"Lot No.");
        IF "Serial No." <> '' THEN
          Text :=
            STRSUBSTNO('%1, %2: %3',Text,
              FIELDCAPTION("Serial No."),"Serial No.");
      END;
      EXIT(Text);
    END;

    [External]
    PROCEDURE InitializeRequest@2(LocationCode@1000 : Code[10];AdjustmentBinCode@1001 : Code[20]);
    BEGIN
      LocCode := LocationCode;
      AdjBinCode := AdjustmentBinCode;
    END;

    [External]
    PROCEDURE SetHideValidationDialog@7380(NewHideValidationDialog@1000 : Boolean);
    BEGIN
      HideValidationDialog := NewHideValidationDialog;
    END;

    [Integration]
    LOCAL PROCEDURE OnCreateWhseJnlLineOnBeforeCheck@1(VAR WarehouseJournalLine@1000 : Record 7311;ItemLedgerEntry@1001 : Record 32);
    BEGIN
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

