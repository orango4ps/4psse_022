OBJECT Page 6086028 Web Portal User Company (WS)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCOW17.10.00,DCW17.10.00.5.00.08;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Web Portal User Company (WS);
               ENU=Web Portal User Company (WS);
               NLD=Web Portal User Company (WS)];
    SourceTable=Table6086006;
    PageType=List;
    SourceTableTemporary=Yes;
    OnOpenPage=BEGIN
                 UpdateWebUserCompany;
               END;

    OnAfterGetRecord=BEGIN
                       Display := "Company Name";
                     END;

  }
  CONTROLS
  {
    { 161024012;;Container;
                Name=ContentArea;
                ContainerType=ContentArea }

    { 161024013;1;Group   ;
                Name=Group;
                GroupType=Repeater }

    { 161024014;2;Field   ;
                Name=CompanyCode;
                SourceExpr="Company Code" }

    { 161024015;2;Field   ;
                Name=UserId;
                SourceExpr="User ID" }

    { 161024016;2;Field   ;
                Name=NoOfDocForApproval;
                SourceExpr="No. of Documents for Approval" }

    { 6085573;2;Field     ;
                Name=NoOfSharedDocuments;
                SourceExpr="No. of Shared Documents" }

    { 161024017;2;Field   ;
                Name=Display;
                SourceExpr=Display }

  }
  CODE
  {
    VAR
      WebUserCompany@1000 : TEMPORARY Record 6086006;
      Display@161024013 : Text[1024] INDATASET;

    PROCEDURE UpdateWebUserCompany@161024013();
    VAR
      Company@161024015 : Record 2000000006;
      ContiniaCompanySetup@6085575 : Record 6192773;
      ContiniaUserSetup@161024017 : Record 6086002;
      ApprEntry@161024018 : Record 454;
      DCApprovalMgnt@6085574 : Codeunit 6085722;
      AccessMgt@6085573 : Codeunit 6085626;
      User@161024013 : Code[50];
    BEGIN
      IF Company.FINDSET THEN
        REPEAT
          IF AccessMgt.HasAccessToTable(Company.Name,DATABASE::"Continia Company Setup") THEN
            IF ContiniaCompanySetup.CHANGECOMPANY(Company.Name) THEN BEGIN
              IF ContiniaCompanySetup.GET THEN BEGIN
                ContiniaUserSetup.CHANGECOMPANY(Company.Name);
                IF ContiniaUserSetup.READPERMISSION THEN BEGIN
                  IF ContiniaUserSetup.GET(USERID) THEN BEGIN
                    WebUserCompany."Company Code" := GetCompanyCode(Company.Name);
                    IF WebUserCompany."Company Code" = '' THEN
                      WebUserCompany."Company Code" := 'DEFAULT';
                    WebUserCompany."Company Name" := Company.Name;
                    WebUserCompany."User ID" := USERID;
                    IF NOT WebUserCompany.FIND('=') THEN BEGIN
                      MarkApprovalEntries(Company.Name,USERID,ApprEntry);
                      MarkWebMenuApprovalEntries(Company.Name,USERID,ApprEntry);
                      CountDocuments(WebUserCompany,ApprEntry);

                      WebUserCompany.INSERT;
                      Rec := WebUserCompany;
                      Rec.INSERT;
                    END;
                  END;
                END;
              END;
            END;
        UNTIL Company.NEXT = 0;
    END;

    PROCEDURE GetCompanyCode@1160040001(_CompanyName@1160040004 : Text[30]) : Code[10];
    VAR
      ContiniaCompanySetup@6085573 : Record 6192773;
      AllObj@1160040003 : Record 2000000038;
      RecRef@1160040001 : RecordRef;
      FieldRef@1160040002 : FieldRef;
    BEGIN
      ContiniaCompanySetup.CHANGECOMPANY(_CompanyName);
      IF ContiniaCompanySetup.GET THEN
        EXIT(ContiniaCompanySetup."Company Code");

      IF NOT AllObj.GET(AllObj."Object Type"::Table,6086300) THEN
        EXIT;

      RecRef.OPEN(6086300,FALSE,_CompanyName);
      IF NOT RecRef.FINDFIRST THEN
        EXIT;

      FieldRef := RecRef.FIELD(2);
      EXIT(FieldRef.VALUE);
    END;

    LOCAL PROCEDURE MarkApprovalEntries@6085573(_CompanyName@6085574 : Text[30];_UserID@6085575 : Code[50];VAR ApprEntry@6085573 : Record 454);
    VAR
      DCApprovalMgnt@6085577 : Codeunit 6085722;
    BEGIN
      ApprEntry.RESET;
      ApprEntry.CHANGECOMPANY(_CompanyName);
      ApprEntry.SETCURRENTKEY("Approver ID",Status);
      ApprEntry.SETRANGE("Approver ID",_UserID);
      ApprEntry.SETRANGE(Status,ApprEntry.Status::Open);
      ApprEntry.SETFILTER("Table ID",GetAppEntryTableIDFilter);
      IF ApprEntry.FINDSET THEN
        REPEAT
          ApprEntry.MARK(TRUE);
        UNTIL ApprEntry.NEXT = 0;

      ApprEntry.SETFILTER("Approver ID",DCApprovalMgnt.GetAppSharingToUserCompFilter(_UserID,_CompanyName));
      IF ApprEntry.FINDSET THEN
        REPEAT
          ApprEntry.MARK(TRUE);
        UNTIL ApprEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE MarkWebMenuApprovalEntries@6085577(_CompanyName@6085575 : Text[30];_UserID@6085574 : Code[50];VAR ApprEntry@6085573 : Record 454);
    VAR
      WebSubMenu@6085577 : Record 6086011;
      DCApprovalMgnt@6085576 : Codeunit 6085722;
    BEGIN
      ApprEntry.CHANGECOMPANY(_CompanyName);
      ApprEntry.SETCURRENTKEY("Approver ID",Status);
      ApprEntry.SETRANGE(Status,ApprEntry.Status::Open);
      WebSubMenu.CHANGECOMPANY(_CompanyName);
      IF WebSubMenu.FINDSET THEN
        REPEAT
          ApprEntry.SETRANGE("Approver ID",_UserID);
          IF WebSubMenu."Approval Code Filter" <> '' THEN
            ApprEntry.SETFILTER("Approval Code",WebSubMenu."Approval Code Filter");
          IF WebSubMenu."Table ID Filter" <> '' THEN
            ApprEntry.SETFILTER("Table ID",WebSubMenu."Table ID Filter");
          IF WebSubMenu."Document Type Filter" <> '' THEN
            ApprEntry.SETFILTER("Document Type",WebSubMenu."Document Type Filter");

          IF ApprEntry.FINDSET THEN
            REPEAT
              ApprEntry.MARK(TRUE);
            UNTIL ApprEntry.NEXT = 0;

          ApprEntry.SETFILTER("Approver ID",DCApprovalMgnt.GetAppSharingToUserCompFilter(_UserID,_CompanyName));
          IF ApprEntry.FINDSET THEN
            REPEAT
              ApprEntry.MARK(TRUE);
            UNTIL ApprEntry.NEXT = 0;
        UNTIL WebSubMenu.NEXT = 0;
    END;

    LOCAL PROCEDURE CountDocuments@6085579(VAR WebUserCompany@6085573 : Record 6086006;VAR ApprEntry@6085575 : Record 454);
    VAR
      DCApprovalMgnt@6085574 : Codeunit 6085722;
    BEGIN
      ApprEntry.SETRANGE("Table ID");
      ApprEntry.SETRANGE("Approver ID");
      ApprEntry.SETRANGE("Document Type");
      ApprEntry.MARKEDONLY(TRUE);
      WebUserCompany."No. of Documents for Approval" := ApprEntry.COUNT;

      ApprEntry.SETFILTER("Approver ID",
        DCApprovalMgnt.GetAppSharingToUserCompFilter(WebUserCompany."User ID",WebUserCompany."Company Name"));
      WebUserCompany."No. of Shared Documents" := ApprEntry.COUNT;
    END;

    LOCAL PROCEDURE GetAppEntryTableIDFilter@6085576() TableIDFilter : Text[1024];
    VAR
      DCSetup@1003 : Record 6085573;
      EMSetup@1000 : Record 6086300;
      DCActivationState@1001 : 'Never activated,Need reactivation,Activated,Pending offline activation';
      ContiniaOnlineLicenseMgt@1004 : Codeunit 6192773;
      EMActivationState@1005 : 'Never Activated,Need Reactivation,Activated,Offline Started';
      ContiniaLicenseMgt@1002 : Codeunit 6192771;
    BEGIN
      IF ContiniaLicenseMgt.HasLicenseAccessToEM THEN BEGIN
        EMActivationState := ContiniaOnlineLicenseMgt.GetProductActivationState('CEM',COMPANYNAME);
        IF EMSetup.GET AND (EMActivationState = EMActivationState::Activated) THEN BEGIN
          AddToFilter(TableIDFilter,'6086320'); //Expenses
          IF EMSetup."Enable Settlement" THEN
            AddToFilter(TableIDFilter,'6086339'); //Settlements
          IF EMSetup."Enable Mileage" THEN
            AddToFilter(TableIDFilter,'6086338'); //Milage
        END;
      END;

      IF ContiniaLicenseMgt.HasLicenseAccessToDC THEN BEGIN
        DCActivationState := ContiniaOnlineLicenseMgt.GetProductActivationState('CDC',COMPANYNAME);
        IF DCSetup.GET AND (DCActivationState = DCActivationState::Activated) THEN
          AddToFilter(TableIDFilter,FORMAT(DATABASE::"Purchase Header"));
      END;
    END;

    LOCAL PROCEDURE AddToFilter@2(VAR Filter@1000 : Text[1024];FilterToAppend@1001 : Text[1024]);
    BEGIN
      IF Filter <> '' THEN
        Filter += '|' + FilterToAppend
      ELSE
        Filter := FilterToAppend;
    END;

    BEGIN
    END.
  }
}

