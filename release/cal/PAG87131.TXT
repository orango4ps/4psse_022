OBJECT Page 87131 WorkOrderHoursRemaining
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=;
  }
  PROPERTIES
  {
    CaptionML=[DAN=Resterende timer i arbejdsordre;
               ENU=Work Order Hours Remaining;
               NOR=Gjenv‘rende timer p† arbeidsordre;
               SVE=Kvarvarande timmar p† arbetsorder];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table11229279;
    SourceTableView=SORTING(No.);
    PageType=List;
    SourceTableTemporary=Yes;
    OnOpenPage=BEGIN
                 GetRemainingHoursPerCC;
                 ZoomInActive := TRUE;
               END;

    ActionList=ACTIONS
    {
      { 1101285007;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1101285008;1 ;Action    ;
                      Name=ZoomIn;
                      ShortCutKey=Return;
                      CaptionML=[DAN=Zoom ind;
                                 ENU=Drilldown;
                                 NOR=Zoom inn;
                                 SVE=Zooma in];
                      Enabled=ZoomInActive;
                      Image=MoveDown;
                      OnAction=BEGIN
                                 CASE Level OF
                                   0: BEGIN
                                     Level := 1;
                                     CostCenterDrillDown;
                                     ShowSource := TRUE;
                                     ZoomOutActive := TRUE;
                                     BackToTopActive := TRUE;
                                   END;
                                   1: BEGIN
                                     Level := 2;
                                     ProjOrServiceOrderDrillDown;
                                     ShowNo := TRUE;
                                     ZoomInActive := FALSE;
                                   END;
                                 END;
                               END;
                                }
      { 1101285009;1 ;Action    ;
                      Name=ZoomOut;
                      CaptionML=[DAN=Zoom ud;
                                 ENU=Zoom Out;
                                 NOR=Zoom ut;
                                 SVE=Zooma ut];
                      Enabled=ZoomOutActive;
                      Image=MoveUp;
                      OnAction=BEGIN
                                 CASE Level OF
                                   1: BEGIN
                                     Level := 0;
                                     GetRemainingHoursPerCC;
                                     ShowSource := FALSE;
                                     ZoomOutActive := FALSE;
                                     BackToTopActive := FALSE;
                                   END;
                                   2: BEGIN
                                     Level := 1;
                                     CostCenterDrillDown;
                                     ShowNo := FALSE;
                                     ZoomInActive := TRUE;
                                   END;
                                 END;
                               END;
                                }
      { 1101285011;1 ;Action    ;
                      Name=BackToTop;
                      CaptionML=[DAN=Tilbage til toppen;
                                 ENU=Back to Top;
                                 NOR=Tilbake til toppen;
                                 SVE=Tillbaka till toppen];
                      Enabled=BackToTopActive;
                      Image=CollapseAll;
                      OnAction=BEGIN

                                 GetRemainingHoursPerCC;
                                 Level := 0;
                                 ShowSource := FALSE;
                                 ShowNo := FALSE;
                                 ZoomInActive := TRUE;
                                 BackToTopActive := FALSE;
                                 ZoomOutActive := FALSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1101285000;0;Container;
                ContainerType=ContentArea }

    { 1101285001;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1101285002;2;Field  ;
                SourceExpr="Global Dimension 1 Code" }

    { 1101285010;2;Field  ;
                SourceExpr=Description }

    { 1101285003;2;Field  ;
                SourceExpr="Source Type";
                Visible=ShowSource }

    { 1101285004;2;Field  ;
                SourceExpr="Source No.";
                Visible=ShowSource }

    { 1101285005;2;Field  ;
                CaptionML=[DAN=Arbejdsordrenr.;
                           ENU=Work Order No.;
                           NOR=Arbeidsordrenr.;
                           SVE=Arbetsordernr.];
                SourceExpr="No.";
                Visible=ShowNo }

    { 1101285006;2;Field  ;
                SourceExpr="Expected Hours" }

  }
  CODE
  {
    VAR
      Level@1101285001 : Integer;
      CostCenterFilter@1101285002 : Code[20];
      SourceTypeFilter@1101285003 : Integer;
      SourceNoFilter@1101285004 : Code[20];
      TempWorkOrder@1101285005 : TEMPORARY Record 11229279;
      TempRecCreated@1101285006 : Boolean;
      ShowNo@1101285000 : Boolean;
      ShowSource@1101285007 : Boolean;
      CostCenterDescription@1101285008 : Text;
      ZoomInActive@1101285009 : Boolean;
      ZoomOutActive@1101285010 : Boolean;
      BackToTopActive@1101285011 : Boolean;

    LOCAL PROCEDURE GetRemainingHoursPerCC@1101285005();
    VAR
      DimensionValue@1101285002 : Record 349;
      GLSetup@1101285001 : Record 98;
      WorkOrder@1101285003 : Record 11229279;
    BEGIN
      GLSetup.GET;
      CLEAR(Rec);
      Rec.DELETEALL(FALSE);

      DimensionValue.SETRANGE("Dimension Code", GLSetup."Global Dimension 1 Code");
      DimensionValue.SETCURRENTKEY(Code);
      DimensionValue.SETASCENDING(Code, TRUE);
      IF DimensionValue.FINDSET(FALSE) AND NOT TempRecCreated THEN REPEAT
        WorkOrder.SETRANGE("Global Dimension 1 Code", DimensionValue.Code);
        WorkOrder.SETRANGE(Finished, FALSE);
        IF WorkOrder.FINDSET(FALSE) THEN BEGIN
          WorkOrder.CALCSUMS("Expected Hours");
          TempWorkOrder.INIT;
          TempWorkOrder."No." := DimensionValue.Code;
          TempWorkOrder."Global Dimension 1 Code" :=  DimensionValue.Code;
          TempWorkOrder.Description := DimensionValue.Name;
          TempWorkOrder."Expected Hours" := WorkOrder."Expected Hours";
          TempWorkOrder.INSERT(FALSE);
        END;
        TempRecCreated := TRUE;
      UNTIL DimensionValue.NEXT = 0;

      IF TempWorkOrder.FINDSET(FALSE) THEN REPEAT
        Rec.TRANSFERFIELDS(TempWorkOrder);
        Rec.INSERT(FALSE);
      UNTIL TempWorkOrder.NEXT = 0;

      //Rec.SETASCENDING("No.",TRUE);
    END;

    LOCAL PROCEDURE CostCenterDrillDown@1101285102();
    VAR
      WorkOrder@1101285000 : Record 11229279;
      i@1101285001 : Integer;
      GetWorkOrders@1101285002 : Query 83801;
      WorkOrder2@1101285003 : Record 11229279;
      time2@1101285004 : Time;
    BEGIN
      CostCenterFilter :=  Rec."Global Dimension 1 Code";
      CostCenterDescription := Rec.Description;

      CLEAR(Rec);
      Rec.DELETEALL(FALSE);

      i := 1;

      GetWorkOrders.SETRANGE(Global_Dim_1_Code_Filter, CostCenterFilter);
      GetWorkOrders.SETRANGE(Source_Company_Filter, COMPANYNAME);
      GetWorkOrders.SETRANGE(Finished_Filter, FALSE);
      GetWorkOrders.OPEN;
      WHILE GetWorkOrders.READ DO BEGIN
        i += 1;
        Rec.INIT;
        Rec."No." := FORMAT(i) ;
        Rec."Source Type" :=  GetWorkOrders.Source_Type;
        Rec."Source No." :=  GetWorkOrders.Source_No;
        Rec."Global Dimension 1 Code" :=  CostCenterFilter;
        Rec.Description := CostCenterDescription;
        Rec."Expected Hours" := GetWorkOrders.Sum_Expected_Hours;
        Rec.INSERT(FALSE);
      END;
      COMMIT;
      GetWorkOrders.CLOSE;

      Rec.SETCURRENTKEY("Source Type","Source No.");
    END;

    LOCAL PROCEDURE ProjOrServiceOrderDrillDown@1101285001();
    VAR
      i@1101285001 : Integer;
      CostCenter@1101285000 : Code[20];
      WorkOrder@1101285002 : Record 11229279;
    BEGIN
      SourceTypeFilter := Rec."Source Type";
      SourceNoFilter := Rec."Source No.";

      CLEAR(Rec);
      Rec.DELETEALL(FALSE);

      WorkOrder.SETRANGE("Global Dimension 1 Code", CostCenterFilter);
      WorkOrder.SETRANGE(Finished, FALSE);
      WorkOrder.SETRANGE("Source Company", COMPANYNAME);
      WorkOrder.SETRANGE("Source Type", SourceTypeFilter);
      WorkOrder.SETRANGE("Source No.", SourceNoFilter);

      IF WorkOrder.FINDSET(FALSE) THEN REPEAT
        Rec.INIT;
        Rec."No." := WorkOrder."No.";
        Rec."Source Type" :=  WorkOrder."Source Type";
        Rec."Source No." :=  WorkOrder."Source No.";
        Rec."Source Company" := WorkOrder."Source Company";
        Rec."Global Dimension 1 Code" :=  WorkOrder."Global Dimension 1 Code";
        Rec.Description := CostCenterDescription;
        Rec."Expected Hours" := WorkOrder."Expected Hours";
        Rec.INSERT(FALSE);
      UNTIL WorkOrder.NEXT = 0;
    END;

    BEGIN
    {
      190924 ORANGO.FH Gap 27
    }
    END.
  }
}

