OBJECT Page 6086370 EM Comment App. Subpage
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.3.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Bemerkungen;
               ENU=Comments;
               NLD=Opmerkingen;
               SVE=Kommentarer];
    SourceTable=Table6086356;
    SourceTableView=SORTING(Importance);
    PageType=ListPart;
    SourceTableTemporary=Yes;
    OnFindRecord=VAR
                   TableID@1160040001 : Integer;
                 BEGIN
                   FILTERGROUP(4);

                   IF EVALUATE(TableID,GETFILTER("Table ID")) THEN
                     LoadComments(TableID,GETRANGEMIN("Doc. Ref. No. (Code)"));

                   FILTERGROUP(0);

                   EXIT(FIND(Which));
                 END;

    OnAfterGetRecord=BEGIN
                       ActivateStyle := Importance = Importance::Error;
                     END;

    OnNewRecord=BEGIN
                  EMCmtMgt.InitApprovalComment(Rec,GlobalTableID,GlobalAppDocNo);
                END;

    OnInsertRecord=BEGIN
                     EMCmtMgt.AddApprovalComment("Table ID","Doc. Ref. No. (Code)",Comment);

                     LoadComments("Table ID","Doc. Ref. No. (Code)");
                     EXIT(FALSE);
                   END;

    OnModifyRecord=BEGIN
                     CheckEditAllowed;
                     IF "App. Cmt. Entry No." <> 0 THEN
                       EMCmtMgt.ModifyApprovalComment("App. Cmt. Entry No.",Comment)
                     ELSE
                       EMCmtMgt.AddApprovalComment("Table ID","Doc. Ref. No. (Code)",Comment);

                     LoadComments("Table ID","Doc. Ref. No. (Code)");
                     EXIT(FALSE);
                   END;

    OnDeleteRecord=BEGIN
                     CheckEditAllowed;
                     CurrPage.SETSELECTIONFILTER(Rec);
                     EMCmtMgt.DeleteApprovalComments(Rec);
                     LoadComments("Table ID","Doc. Ref. No. (Code)");
                   END;

  }
  CONTROLS
  {
    { 1160040000;;Container;
                Name=ContentArea;
                ContainerType=ContentArea }

    { 1160040001;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1160040003;2;Field  ;
                Name=Source;
                SourceExpr=Source;
                Style=Strong }

    { 1160040002;2;Field  ;
                Name=Comment;
                SourceExpr=Comment;
                Style=Unfavorable;
                StyleExpr=ActivateStyle }

    { 1160040006;2;Field  ;
                Name=Created by;
                SourceExpr="Created by" }

    { 1160040004;2;Field  ;
                Name=Created Date/Time;
                SourceExpr="Created Date/Time" }

  }
  CODE
  {
    VAR
      EMCmtMgt@1160040002 : Codeunit 6086323;
      GlobalAppDocNo@1160040006 : Code[20];
      GlobalTableID@1160040009 : Integer;
      ActivateStyle@1160040000 : Boolean INDATASET;
      EditAllowed@1160040001 : TextConst 'DEU=Es ist nur zul„ssig, die Genehmigungskommentarzeilen zu bearbeiten.;ENU=It is only allowed to edit the approval comment lines.;NLD=Het is slechts toegestaan om de goedkeuringsopmerkingsregels te wijzigen.';

    LOCAL PROCEDURE LoadComments@1160040000(TableID@1160040000 : Integer;DocRefNo@1160040001 : Code[20]) : Boolean;
    VAR
      ExpHeader@1160040006 : Record 6086339;
      EmAppMgt@1160040005 : Codeunit 6086312;
      ExpCommMgt@1160040003 : Codeunit 6086323;
    BEGIN
      GlobalTableID := TableID;
      GlobalAppDocNo := DocRefNo;

      CASE TableID OF
        DATABASE::Expense,DATABASE::Mileage:
          IF NOT ExpCommMgt.LoadCommentsInTemp(GlobalTableID,0,'',EmAppMgt.Code2Int(DocRefNo),Rec) THEN BEGIN
            SETRANGE("Table ID",-1);
            SETRANGE("Doc. Ref. No. (Code)",' ');
          END;

        DATABASE::"Expense Header":
          IF NOT ExpCommMgt.LoadCommentsInTemp(GlobalTableID,ExpHeader."Document Type"::Settlement,DocRefNo,0,Rec) THEN BEGIN
            SETRANGE("Table ID",-1);
            SETRANGE("Doc. Ref. No. (Code)",' ');
          END;
      END;

      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE CheckEditAllowed@1160040001();
    BEGIN
      IF Area <> 'APPRCOMM' THEN
        ERROR(EditAllowed);
    END;

    BEGIN
    END.
  }
}

