OBJECT Codeunit 12057141 Replace User Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF502000;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE ReplaceUser@1100285040(CompanyName@1100285004 : Text[30];Approver@1100285000 : Code[50];ReplaceWithUser@1100285003 : Code[50];StartDate@1100285001 : Date;EndDate@1100285002 : Date);
    VAR
      EXUserGroup@1100285006 : Record 12013606;
      EXUserGroupLine@1100285005 : Record 12013607;
    BEGIN
      IF (Approver = '') OR (ReplaceWithUser = '') THEN
        EXIT;

      EXUserGroup.CHANGECOMPANY(CompanyName);
      EXUserGroupLine.CHANGECOMPANY(CompanyName);

      EXUserGroup.RESET;
      EXUserGroup.SETRANGE(Blocked, FALSE);
      IF EXUserGroup.FINDSET THEN BEGIN
        REPEAT
          EXUserGroupLine.RESET;
          EXUserGroupLine.SETRANGE(Code, EXUserGroup.Code);
          EXUserGroupLine.SETRANGE("User ID", Approver);
          EXUserGroupLine.SETRANGE("Starting Date", 0D);
          EXUserGroupLine.SETRANGE("Ending Date", 0D);
          IF EXUserGroupLine.FINDFIRST THEN BEGIN
            InsertEXUserGroupLine(CompanyName, EXUserGroupLine, EXUserGroup.Code, Approver, ReplaceWithUser, StartDate, EndDate)
          END;
        UNTIL EXUserGroup.NEXT = 0;
      END;
    END;

    PROCEDURE RemoveReplaceUser@1100285000(Approver@1100285000 : Code[50];ReplaceWithUser@1100285001 : Code[50]);
    VAR
      EXUserGroupLine@1100285003 : Record 12013607;
    BEGIN
      IF (Approver = '') OR (ReplaceWithUser = '') THEN
        EXIT;

      DeleteInOwnGroup(Approver,ReplaceWithUser);

      EXUserGroupLine.RESET;
      EXUserGroupLine.SETRANGE("User ID", ReplaceWithUser);
      EXUserGroupLine.SETRANGE("Replacer Added For User", Approver);
      IF EXUserGroupLine.FINDSET THEN BEGIN
        REPEAT
          IF EXUserGroupLine."Ending Date" > TODAY THEN BEGIN
            EXUserGroupLine.DELETE;
          END;
        UNTIL EXUserGroupLine.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE IsReplacerInGroup@1100285001(CompanyName@1100285003 : Text[30];UserGroup@1100285000 : Code[50];ReplaceWithUser@1100285001 : Code[50];StartDate@1100285004 : Date;EndDate@1100285005 : Date) : Boolean;
    VAR
      EXUserGroupLine@1100285002 : Record 12013607;
    BEGIN
      EXUserGroupLine.CHANGECOMPANY(CompanyName);
      EXUserGroupLine.RESET;
      EXUserGroupLine.SETRANGE(Code, UserGroup);
      EXUserGroupLine.SETRANGE("User ID", ReplaceWithUser);
      EXUserGroupLine.SETFILTER("Starting Date", '%1|%2', 0D, StartDate);
      EXUserGroupLine.SETFILTER("Ending Date", '%1|%2', 0D, EndDate);

      IF EXUserGroupLine.ISEMPTY THEN
        EXIT(FALSE)
      ELSE
        EXIT(TRUE);
    END;

    LOCAL PROCEDURE GetNewLineNo@1100285004(CompanyName@1100285002 : Text[30];UserGroup@1100285000 : Code[50]) : Integer;
    VAR
      EXUserGroupLine@1100285001 : Record 12013607;
    BEGIN
      EXUserGroupLine.CHANGECOMPANY(CompanyName);
      EXUserGroupLine.RESET;
      EXUserGroupLine.SETRANGE(Code, UserGroup);
      IF EXUserGroupLine.FINDLAST THEN
        EXIT(EXUserGroupLine."Line No." + 10000);
    END;

    LOCAL PROCEDURE DeleteInOwnGroup@1100285002(Approver@1100285001 : Code[50];ReplaceWithUser@1100285000 : Code[50]);
    VAR
      EXUserGroupLine@1100285002 : Record 12013607;
    BEGIN
      //To handle local replacers in older versions which do not have replacer added field populated
      EXUserGroupLine.RESET;
      EXUserGroupLine.SETRANGE(Code,Approver);
      EXUserGroupLine.SETRANGE("User ID",ReplaceWithUser);
      EXUserGroupLine.SETFILTER("Ending Date", '>%1',TODAY);
      IF EXUserGroupLine.FINDFIRST THEN
        EXUserGroupLine.DELETE;
    END;

    LOCAL PROCEDURE InsertEXUserGroupLine@1100285009(CompanyName@1100285006 : Text[30];VAR EXUserGroupLine@1100285000 : Record 12013607;UserGroupCode@1100285001 : Code[50];Approver@1100285005 : Code[50];ReplaceWithUser@1100285002 : Code[50];StartDate@1100285003 : Date;EndDate@1100285004 : Date);
    BEGIN
      IF IsReplacerInGroup(CompanyName, UserGroupCode, ReplaceWithUser, StartDate, EndDate) THEN
        EXIT;

      EXUserGroupLine.INIT;
      EXUserGroupLine.Code := UserGroupCode;
      EXUserGroupLine."Line No." := GetNewLineNo(CompanyName, UserGroupCode);
      EXUserGroupLine."User ID" := ReplaceWithUser;
      EXUserGroupLine."Starting Date" := StartDate;
      EXUserGroupLine."Ending Date" := EndDate;
      EXUserGroupLine."Replacer Added For User" := Approver;
      EXUserGroupLine.INSERT;
    END;

    BEGIN
    END.
  }
}

