OBJECT Codeunit 5522 Order Planning Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04,4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      TempUnplannedDemand@1007 : Record 5520;
      ProdOrderComp@1002 : Record 5407;
      CompanyInfo@1008 : Record 79;
      UOMMgt@1001 : Codeunit 5402;
      DemandType@1000 : ' ,Production,Sales,Service,Jobs,Assembly,,,,Plant,Item Journal';
      HasGotCompanyInfo@1006 : Boolean;
      Text000@1011 : TextConst 'ENU=Generating Lines to Plan @1@@@@@@@;NOR=Genererer linjer i plan  @1@@@@@@@;SVE=Rader genereras till plan @1@@@@@@@';
      Text001@1015 : TextConst 'ENU=Item Substitution is not possible for the active line.;NOR=Vareerstatning er ikke mulig for den aktive linjen.;SVE=Det g†r inte att ers„tta artikeln p† den aktiva raden.';
      Text003@1010 : TextConst 'ENU=You cannot use this function because the active line has no %1.;NOR=Du kan ikke bruke denne funksjonen fordi den aktive linjen ikke har noen %1.;SVE=Du kan inte anv„nda den h„r funktionen eftersom den aktiva raden saknar %1.';
      Text004@1016 : TextConst 'ENU=All items are available and no planning lines are created.;NOR=Alle varer er tilgjengelige, og det opprettes ingen planleggingslinjer.;SVE=Alla artiklar „r disponibla och inga planeringsrader har skapats.';
      DelReqLine@1017 : Boolean;

    [External]
    PROCEDURE GetOrdersToPlan@1(VAR ReqLine@1000 : Record 246);
    BEGIN
      PrepareRequisitionRecord(ReqLine);
      CODEUNIT.RUN(CODEUNIT::"Get Unplanned Demand",TempUnplannedDemand);
      TransformUnplannedDemandToRequisitionLines(ReqLine);
    END;

    [External]
    PROCEDURE PlanSpecificSalesOrder@32(VAR ReqLine@1000 : Record 246;SalesOrderNo@1002 : Code[20]);
    VAR
      SalesHeader@1004 : Record 36;
      GetUnplannedDemand@1001 : Codeunit 5520;
    BEGIN
      SetSalesOrder;
      PrepareRequisitionRecord(ReqLine);

      GetUnplannedDemand.SetIncludeMetDemandForSpecificSalesOrderNo(SalesOrderNo);
      GetUnplannedDemand.RUN(TempUnplannedDemand);

      TransformUnplannedDemandToRequisitionLines(ReqLine);

      ReqLine.SETRANGE("Demand Order No.",SalesOrderNo);
      ReqLine.SETRANGE("Demand Subtype",SalesHeader."Document Type"::Order);
    END;

    [External]
    PROCEDURE PrepareRequisitionRecord@23(VAR RequisitionLine@1000 : Record 246);
    BEGIN
      RequisitionLine.LOCKTABLE;
      RequisitionLine.SETCURRENTKEY("User ID","Worksheet Template Name");
      RequisitionLine.SETRANGE("User ID",USERID);
      RequisitionLine.SETRANGE("Worksheet Template Name",'');
      RequisitionLine.DELETEALL(TRUE);

      RequisitionLine.RESET;
      RequisitionLine.SETRANGE("Worksheet Template Name",'');
      IF RequisitionLine.FINDLAST THEN;
    END;

    LOCAL PROCEDURE TransformUnplannedDemandToRequisitionLines@24(VAR RequisitionLine@1000 : Record 246);
    VAR
      Window@1004 : Dialog;
      WindowUpdateDateTime@1003 : DateTime;
      i@1002 : Integer;
      NoOfRecords@1001 : Integer;
    BEGIN
      WITH TempUnplannedDemand DO BEGIN
        SETCURRENTKEY("Demand Date",Level);
        SETRANGE(Level,0);

        i := 0;
        Window.OPEN(Text000);
        WindowUpdateDateTime := CURRENTDATETIME;
        NoOfRecords := COUNT;

        IF FIND('-') THEN
          REPEAT
            i := i + 1;
            IF CURRENTDATETIME - WindowUpdateDateTime >= 300 THEN BEGIN
              WindowUpdateDateTime := CURRENTDATETIME;
              Window.UPDATE(1,ROUND(i / NoOfRecords * 10000,1));
            END;

            InsertDemandLines(RequisitionLine);
            DELETE;
          UNTIL NEXT = 0;

        Window.CLOSE;

        RequisitionLine.RESET;
        RequisitionLine.SETCURRENTKEY("User ID","Worksheet Template Name");
        RequisitionLine.SETRANGE("User ID",USERID);
        IF NOT RequisitionLine.FINDFIRST THEN
          ERROR(Text004);

        OnAfterTransformUnplannedDemandToRequisitionLines(RequisitionLine);
        COMMIT;
      END;
    END;

    LOCAL PROCEDURE InsertDemandLines@9(VAR ReqLine@1000 : Record 246);
    VAR
      UnplannedDemand@1002 : Record 5520;
      Item@1003 : Record 27;
      SalesLine@1005 : Record 37;
      ProdOrderComp@1006 : Record 5407;
      ServLine@1008 : Record 5902;
      AsmLine@1009 : Record 901;
      PlanningLineMgt@1004 : Codeunit 99000809;
      ItemTrackingMgt@1007 : Codeunit 6500;
      HeaderExists@1001 : Boolean;
    BEGIN
      WITH TempUnplannedDemand DO BEGIN
        UnplannedDemand.COPY(TempUnplannedDemand);

        RESET;
        SETRECFILTER;
        SETRANGE("Demand Line No.");
        SETRANGE("Demand Ref. No.");
        SETRANGE(Level,1);
        FIND('-');
        HeaderExists := FALSE;

        REPEAT
          IF DemandType IN ["Demand Type",DemandType::" "] THEN BEGIN
            IF NOT HeaderExists THEN
              InsertDemandHeader(UnplannedDemand,ReqLine);
            HeaderExists := TRUE;

            ReqLine.TransferFromUnplannedDemand(TempUnplannedDemand);
            ReqLine.SetSupplyQty("Quantity (Base)","Needed Qty. (Base)");
            ReqLine.SetSupplyDates("Demand Date");
            OnInsertDemandLinesOnBeforeReqLineInsert(ReqLine);
            ReqLine.INSERT;

            IF Item."No." <> "Item No." THEN
              Item.GET("Item No.");
            IF Item."Item Tracking Code" <> '' THEN
              CASE "Demand Type" OF
                "Demand Type"::Sales:
                  BEGIN
                    SalesLine.GET("Demand SubType","Demand Order No.","Demand Line No.");
                    ItemTrackingMgt.CopyItemTracking(SalesLine.RowID1,ReqLine.RowID1,TRUE);
                  END;
                "Demand Type"::Production:
                  BEGIN
                    ProdOrderComp.GET("Demand SubType","Demand Order No.","Demand Line No.","Demand Ref. No.");
                    ItemTrackingMgt.CopyItemTracking(ProdOrderComp.RowID1,ReqLine.RowID1,TRUE);
                  END;
                "Demand Type"::Service:
                  BEGIN
                    ServLine.GET("Demand SubType","Demand Order No.","Demand Line No.");
                    ItemTrackingMgt.CopyItemTracking(ServLine.RowID1,ReqLine.RowID1,TRUE);
                  END;
                "Demand Type"::Assembly:
                  BEGIN
                    AsmLine.GET("Demand SubType","Demand Order No.","Demand Line No.");
                    ItemTrackingMgt.CopyItemTracking(AsmLine.RowID1,ReqLine.RowID1,TRUE);
                  END;
              END;
            IF ReqLine.Quantity > 0 THEN
              PlanningLineMgt.Calculate(ReqLine,1,TRUE,TRUE,0);
            ReqLine.FIND('+');
          END;
          DELETE;
        UNTIL NEXT = 0;

        COPY(UnplannedDemand);
      END;
    END;

    LOCAL PROCEDURE InsertDemandHeader@19(UnplannedDemand@1001 : Record 5520;VAR ReqLine@1000 : Record 246);
    BEGIN
      WITH ReqLine DO BEGIN
        INIT;
        CASE UnplannedDemand."Demand Type" OF
          UnplannedDemand."Demand Type"::Sales:
            "Demand Type" := DATABASE::"Sales Line";
          UnplannedDemand."Demand Type"::Production:
            "Demand Type" := DATABASE::"Prod. Order Component";
          UnplannedDemand."Demand Type"::Service:
            "Demand Type" := DATABASE::"Service Line";
          UnplannedDemand."Demand Type"::Job:
            "Demand Type" := DATABASE::"Job Planning Line";
          UnplannedDemand."Demand Type"::Assembly:
            "Demand Type" := DATABASE::"Assembly Line";
          //**4PS.sn
          UnplannedDemand."Demand Type"::Plant:
            "Demand Type" := DATABASE::"Plant Order Line";
          UnplannedDemand."Demand Type"::"Item Journal":
            "Demand Type" := DATABASE::"Item Journal Line";
          //**4PS.en
        END;
        "Demand Subtype" := UnplannedDemand."Demand SubType";
        "Demand Order No." := UnplannedDemand."Demand Order No.";
        Status := UnplannedDemand.Status;
        "Demand Date" := UnplannedDemand."Demand Date";
        Description := UnplannedDemand.Description;

        Level := 0;
        "Replenishment System" := "Replenishment System"::" ";
        "Line No." := "Line No." + 10000;
        "User ID" := USERID;
        INSERT;
      END;
    END;

    [External]
    PROCEDURE DeleteLine@8() : Boolean;
    BEGIN
      EXIT(DelReqLine);
    END;

    [External]
    PROCEDURE CalcNeededQty@15(AvailableQty@1000 : Decimal;DemandQty@1001 : Decimal) : Decimal;
    BEGIN
      CASE TRUE OF
        AvailableQty >= DemandQty:
          EXIT(0);
        AvailableQty < 0:
          EXIT(DemandQty);
        ELSE
          EXIT(DemandQty - AvailableQty);
      END;
    END;

    [External]
    PROCEDURE CalcATPQty@12(ItemNo@1009 : Text[250];VariantFilter@1008 : Text[250];LocationFilter@1007 : Text[250];DemandDate@1006 : Date) : Decimal;
    VAR
      Item@1004 : Record 27;
      AvailableToPromise@1005 : Codeunit 5790;
      GrossRequirement@1000 : Decimal;
      ScheduledRcpt@1001 : Decimal;
      ODF@1003 : DateFormula;
    BEGIN
      IF ItemNo = '' THEN
        EXIT(0);

      Item.GET(ItemNo);
      Item.SETRANGE("Variant Filter",VariantFilter);
      Item.SETRANGE("Location Filter",LocationFilter);
      Item.SETRANGE("Date Filter",0D,DemandDate);
      Item.SETRANGE("Drop Shipment Filter",FALSE);
      IF DemandDate = 0D THEN
        DemandDate := WORKDATE;

      EXIT(AvailableToPromise.QtyAvailabletoPromise(Item,GrossRequirement,ScheduledRcpt,DemandDate,0,ODF))
    END;

    [External]
    PROCEDURE CalcATPEarliestDate@5(ItemNo@1007 : Text[250];VariantFilter@1006 : Text[250];LocationFilter@1002 : Text[250];DemandDate@1000 : Date;Quantity@1008 : Decimal) : Date;
    VAR
      Item@1004 : Record 27;
      AvailableToPromise@1005 : Codeunit 5790;
      AvailableQty@1003 : Decimal;
    BEGIN
      IF ItemNo = '' THEN
        EXIT(0D);

      GetCompanyInfo;
      IF DemandDate = 0D THEN
        DemandDate := WORKDATE;

      Item.GET(ItemNo);
      Item.SETRANGE("Variant Filter",VariantFilter);
      Item.SETRANGE("Location Filter",LocationFilter);
      Item.SETRANGE("Drop Shipment Filter",FALSE);
      EXIT(
        AvailableToPromise.EarliestAvailabilityDate(
          Item,Quantity,DemandDate,Quantity,DemandDate,AvailableQty,
          CompanyInfo."Check-Avail. Time Bucket",CompanyInfo."Check-Avail. Period Calc."));
    END;

    LOCAL PROCEDURE GetCompanyInfo@11();
    BEGIN
      IF HasGotCompanyInfo THEN
        EXIT;
      HasGotCompanyInfo := CompanyInfo.GET;
    END;

    [External]
    PROCEDURE SetSalesOrder@2();
    BEGIN
      DemandType := DemandType::Sales;
    END;

    [External]
    PROCEDURE SetProdOrder@3();
    BEGIN
      DemandType := DemandType::Production;
    END;

    [External]
    PROCEDURE SetServOrder@7();
    BEGIN
      DemandType := DemandType::Service;
    END;

    [External]
    PROCEDURE SetJobOrder@13();
    BEGIN
      DemandType := DemandType::Jobs;
    END;

    [External]
    PROCEDURE SetAsmOrder@14();
    BEGIN
      DemandType := DemandType::Assembly;
    END;

    [External]
    PROCEDURE InsertAltSupplySubstitution@16(VAR ReqLine@1000 : Record 246);
    VAR
      TempItemSub@1007 : TEMPORARY Record 5715;
      ReqLine2@1008 : TEMPORARY Record 246;
      ItemSubstMgt@1006 : Codeunit 5701;
      PlanningLineMgt@1004 : Codeunit 99000809;
      UnAvailableQtyBase@1002 : Decimal;
    BEGIN
      IF NOT SubstitutionPossible(ReqLine) THEN
        ERROR(Text001);

      CLEAR(ItemSubstMgt);
      IF NOT ItemSubstMgt.PrepareSubstList(
           ReqLine."No.",
           ReqLine."Variant Code",
           ReqLine."Location Code",
           ReqLine."Due Date",
           TRUE)
      THEN
        ItemSubstMgt.ErrorMessage(ReqLine."No.",ReqLine."Variant Code");

      ItemSubstMgt.GetTempItemSubstList(TempItemSub);

      TempItemSub.RESET;
      TempItemSub.SETRANGE("Variant Code",ReqLine."Variant Code");
      TempItemSub.SETRANGE("Location Filter",ReqLine."Location Code");
      IF TempItemSub.FINDFIRST THEN;
      IF PAGE.RUNMODAL(PAGE::"Item Substitution Entries",TempItemSub) = ACTION::LookupOK THEN BEGIN
        // Update sourceline
        WITH ReqLine DO
          ProdOrderComp.GET("Demand Subtype","Demand Order No.","Demand Line No.","Demand Ref. No.");
        ItemSubstMgt.UpdateComponent(
          ProdOrderComp,TempItemSub."Substitute No.",TempItemSub."Substitute Variant Code");
        ProdOrderComp.MODIFY(TRUE);
        ProdOrderComp.AutoReserve;

        IF TempItemSub."Quantity Avail. on Shpt. Date" >= ReqLine."Needed Quantity (Base)" THEN BEGIN
          ReqLine.DELETE(TRUE);
          DelReqLine := TRUE;
        END ELSE BEGIN
          ReqLine2 := ReqLine; // Save Original Line

          UnAvailableQtyBase :=
            CalcNeededQty(
              TempItemSub."Quantity Avail. on Shpt. Date",ReqLine2."Demand Quantity (Base)");

          // Update Req.Line
          ReqLine."Worksheet Template Name" := ReqLine2."Worksheet Template Name";
          ReqLine."Journal Batch Name" := ReqLine2."Journal Batch Name";
          ReqLine."Line No." := ReqLine2."Line No.";
          ReqLine."Location Code" := ProdOrderComp."Location Code";
          ReqLine."Bin Code" := ProdOrderComp."Bin Code";
          ReqLine.VALIDATE("No.",ProdOrderComp."Item No.");
          ReqLine.VALIDATE("Variant Code",ProdOrderComp."Variant Code");
          ReqLine."Unit Of Measure Code (Demand)" := ProdOrderComp."Unit of Measure Code";
          ReqLine."Qty. per UOM (Demand)" := ProdOrderComp."Qty. per Unit of Measure";
          ReqLine.SetSupplyQty(ReqLine2."Demand Quantity (Base)",UnAvailableQtyBase);
          ReqLine.SetSupplyDates(ReqLine2."Demand Date");
          ReqLine."Original Item No." := ReqLine2."No.";
          ReqLine."Original Variant Code" := ReqLine2."Variant Code";
          OnBeforeReqLineModify(ReqLine,ReqLine2,ProdOrderComp);
          ReqLine.MODIFY;
          PlanningLineMgt.Calculate(ReqLine,1,TRUE,TRUE,0);
        END;
      END;
    END;

    [External]
    PROCEDURE SubstitutionPossible@10(ReqLine@1000 : Record 246) : Boolean;
    VAR
      Item@1001 : Record 27;
    BEGIN
      IF (ReqLine.Type <> ReqLine.Type::Item) OR
         (ReqLine."Demand Type" <> DATABASE::"Prod. Order Component")
      THEN
        EXIT(FALSE);

      IF NOT Item.GET(ReqLine."No.") THEN
        EXIT(FALSE);

      IF Item."Manufacturing Policy" = Item."Manufacturing Policy"::"Make-to-Order" THEN
        EXIT(FALSE);

      ReqLine.CALCFIELDS("Reserved Qty. (Base)");
      IF ReqLine."Reserved Qty. (Base)" <> 0 THEN
        EXIT(FALSE);

      IF ProdOrderComp.GET(
           ReqLine."Demand Subtype",
           ReqLine."Demand Order No.",
           ReqLine."Demand Line No.",
           ReqLine."Demand Ref. No.")
      THEN
        IF ProdOrderComp."Supplied-by Line No." <> 0 THEN
          EXIT(FALSE);

      Item.CALCFIELDS("Substitutes Exist");
      EXIT(Item."Substitutes Exist");
    END;

    [External]
    PROCEDURE InsertAltSupplyLocation@6(VAR ReqLine@1000 : Record 246);
    VAR
      Location@1001 : Record 14;
      TempReqLine@1003 : TEMPORARY Record 246;
      AvailableQtyBase@1005 : Decimal;
      NextLineNo@1004 : Integer;
    BEGIN
      ReqLine.TESTFIELD(Type,ReqLine.Type::Item);
      IF ReqLine."Location Code" = '' THEN
        ERROR(Text003,ReqLine.FIELDCAPTION("Location Code"));

      NextLineNo := 0;

      Location.RESET;
      Location.SETRANGE("Use As In-Transit",FALSE);
      Location.SETFILTER(Code,'<>%1',ReqLine."Location Code");
      IF Location.FIND('-') THEN
        REPEAT
          AvailableQtyBase :=
            CalcATPQty(
              ReqLine."No.",ReqLine."Variant Code",Location.Code,ReqLine."Demand Date");

          IF AvailableQtyBase > 0 THEN
            AvailableQtyBase -= PromisedTransferQty(ReqLine,Location.Code);

          IF AvailableQtyBase > 0 THEN BEGIN
            NextLineNo += 10000;
            TempReqLine := ReqLine;
            TempReqLine."Line No." += NextLineNo;
            TempReqLine."Transfer-from Code" := Location.Code;

            IF TempReqLine."Qty. per Unit of Measure" = 0 THEN
              TempReqLine."Qty. per Unit of Measure" := 1;

            TempReqLine."Demand Qty. Available" :=
              ROUND(AvailableQtyBase / TempReqLine."Qty. per Unit of Measure",UOMMgt.QtyRndPrecision);
            TempReqLine.Quantity := ROUND(AvailableQtyBase / TempReqLine."Qty. per Unit of Measure",UOMMgt.QtyRndPrecision);
            TempReqLine."Quantity (Base)" := AvailableQtyBase;

            OnInsertAltSupplyLocationOnBeforeTempReqLineInsert(TempReqLine);
            TempReqLine.INSERT;
          END;
        UNTIL Location.NEXT = 0;

      TempReqLine.SETRANGE("No.",TempReqLine."No.");
      IF PAGE.RUNMODAL(PAGE::"Get Alternative Supply",TempReqLine) = ACTION::LookupOK THEN BEGIN
        ReqLine.VALIDATE("Replenishment System",ReqLine."Replenishment System"::Transfer);
        ReqLine.VALIDATE("Supply From",TempReqLine."Transfer-from Code");
        ReqLine.CalcStartingDate('');
        IF TempReqLine."Quantity (Base)" < ReqLine."Quantity (Base)" THEN
          ReqLine.VALIDATE(
            Quantity,ROUND(TempReqLine."Quantity (Base)" / ReqLine."Qty. per Unit of Measure" ,UOMMgt.QtyRndPrecision));
      END;
    END;

    LOCAL PROCEDURE PromisedTransferQty@20(ReqLine@1001 : Record 246;LocationCode@1002 : Code[10]) OrderQtyBase : Decimal;
    VAR
      ReqLine2@1000 : Record 246;
    BEGIN
      WITH ReqLine DO BEGIN
        ReqLine2.RESET;
        ReqLine2.SETCURRENTKEY("Worksheet Template Name","Journal Batch Name",Type,"No.","Due Date");
        ReqLine2.SETRANGE("Worksheet Template Name",'');
        ReqLine2.SETRANGE("Journal Batch Name",'');
        ReqLine2.SETRANGE(Type,ReqLine2.Type::Item);
        ReqLine2.SETRANGE("No.","No.");
        ReqLine2.SETRANGE("User ID",USERID);
        ReqLine2.SETRANGE("Variant Code","Variant Code");
        ReqLine2.SETRANGE("Replenishment System",ReqLine2."Replenishment System"::Transfer);
        ReqLine2.SETRANGE("Supply From",LocationCode);
        ReqLine2.SETFILTER("Line No.",'<>%1',"Line No.");
        IF ReqLine2.FIND('-') THEN
          REPEAT
            OrderQtyBase += ReqLine2."Quantity (Base)";
          UNTIL ReqLine2.NEXT = 0;
      END;
    END;

    [External]
    PROCEDURE AvailQtyOnOtherLocations@4(ReqLine@1001 : Record 246) : Decimal;
    VAR
      Location@1002 : Record 14;
      AvailableQtyBase@1000 : Decimal;
      AvailableQtyBaseTotal@1003 : Decimal;
    BEGIN
      IF ReqLine."Location Code" = '' THEN
        EXIT(0);

      AvailableQtyBaseTotal := 0;
      Location.RESET;
      Location.SETRANGE("Use As In-Transit",FALSE);
      IF ReqLine."Location Code" <> '' THEN
        Location.SETFILTER(Code,'<>%1',ReqLine."Location Code")
      ELSE
        Location.SETFILTER(Code,'<>''''');
      IF Location.FIND('-') THEN
        REPEAT
          AvailableQtyBase :=
            CalcATPQty(
              ReqLine."No.",ReqLine."Variant Code",Location.Code,ReqLine."Demand Date");

          IF AvailableQtyBase > 0 THEN
            AvailableQtyBase -= PromisedTransferQty(ReqLine,Location.Code);
          IF AvailableQtyBase > 0 THEN
            AvailableQtyBaseTotal += AvailableQtyBase;
        UNTIL Location.NEXT = 0;

      EXIT(AvailableQtyBaseTotal);
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterTransformUnplannedDemandToRequisitionLines@22(VAR RequisitionLine@1000 : Record 246);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeReqLineModify@21(VAR RequisitionLine@1000 : Record 246;RequisitionLine2@1001 : Record 246;ProdOrderComponent@1002 : Record 5407);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnInsertAltSupplyLocationOnBeforeTempReqLineInsert@18(VAR RequisitionLine@1000 : Record 246);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnInsertDemandLinesOnBeforeReqLineInsert@17(VAR RequisitionLine@1000 : Record 246);
    BEGIN
    END;

    PROCEDURE SetPlantOrder@1100485000();
    BEGIN
      //**4PS
      DemandType := DemandType::Plant;
    END;

    BEGIN
    END.
  }
}

