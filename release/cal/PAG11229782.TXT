OBJECT Page 11229782 BasePart Quantity Sheet Lines
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Basisteil Mengenblatt Zeilen;
               ENU=Base Part Quantity Sheet Lines;
               NLD=Uittrekstaat onderdeel (regels)];
    SourceTable=Table11020460;
    PageType=ListPart;
    AutoSplitKey=Yes;
    OnInit=BEGIN
             FormulaEditable := TRUE;
             HoogteEditable := TRUE;
             BreedteEditable := TRUE;
             LengteEditable := TRUE;
             AantalEditable := TRUE;
           END;

    OnAfterGetRecord=BEGIN
                       GetResult;
                       GetRelation;
                     END;

    OnNewRecord=BEGIN
                  QuantDec := 0;
                  LengthDec := 0;
                  WidthDec := 0;
                  HeightDec := 0;
                  ResultDec := 0;
                  TotLengthDec := 0;
                  TotWidthDec := 0;
                  TotHeightDec := 0;
                END;

    OnAfterGetCurrRecord=BEGIN
                           AantalEditable := (Total <> Total::Subtotal);
                           LengteEditable := (Total <> Total::Subtotal);
                           BreedteEditable := (Total <> Total::Subtotal);
                           HoogteEditable := (Total <> Total::Subtotal);
                           FormulaEditable := (Total <> Total::Subtotal);
                         END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1906587504;1 ;ActionGroup;
                      CaptionML=[DEU=F&unktion;
                                 ENU=F&unctions;
                                 NLD=F&uncties;
                                 NOR=Fu&nksjon;
                                 SVE=F&unktion] }
      { 1905582204;2 ;Action    ;
                      ShortCutKey=Shift+Ctrl+D;
                      CaptionML=[DEU=Beziehung l”schen;
                                 ENU=Remove Relation;
                                 NLD=Relatie verwijderen;
                                 NOR=Fjern relasjon;
                                 SVE=Ta bort relation];
                      Image=Delete;
                      OnAction=BEGIN
                                 DeleteRelation;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100525000;1;Group  ;
                GroupType=Repeater }

    { 1100525001;2;Field  ;
                CaptionML=[DEU=Rubrikennr.;
                           ENU=Row No.;
                           NLD=Rubriek - R;
                           NOR=Rad-nr;
                           SVE=Rad-nr];
                SourceExpr="Row No." }

    { 1100525013;2;Field  ;
                SourceExpr=Description;
                OnValidate=BEGIN
                             CurrPage.SAVERECORD;
                           END;
                            }

    { 1100525003;2;Field  ;
                Name=Aantal;
                CaptionML=[DEU=Anzahl - A;
                           ENU=Quantity - A;
                           NLD=Hoeveelheid - A;
                           NOR=Antall Í A;
                           SVE=Antal Í A];
                BlankZero=Yes;
                SourceExpr=QuantDec;
                Editable=AantalEditable;
                OnValidate=BEGIN
                             VALIDATE(Quantity,QuantDec);
                             UpdateSheet;
                           END;

                OnAssistEdit=BEGIN
                               IF QuantDec <> 0 THEN
                                 SetRelation('A',QuantDec)
                               ELSE
                                 ERROR(Text001);
                             END;
                              }

    { 1100525005;2;Field  ;
                Name=Lengte;
                CaptionML=[DEU=L„nge - B;
                           ENU=Length - B;
                           NLD=Lengte - B;
                           NOR=Lengde Í B;
                           SVE=L„ngd Í B];
                BlankZero=Yes;
                SourceExpr=LengthDec;
                Editable=LengteEditable;
                OnValidate=BEGIN
                             VALIDATE(Length,LengthDec);
                             UpdateSheet;
                           END;

                OnAssistEdit=BEGIN
                               IF LengthDec <> 0 THEN
                                 SetRelation('B',LengthDec)
                               ELSE
                                 ERROR(Text001);
                             END;
                              }

    { 1100525021;2;Field  ;
                Name=TotLengte;
                CaptionML=[DEU=Gesamtl„nge - C;
                           ENU=Total Length - C;
                           NLD=Totale lengte - C;
                           NOR=Total lengde Í C;
                           SVE=Totall„ngd Í C];
                BlankZero=Yes;
                SourceExpr=TotLengthDec;
                Visible=FALSE;
                Editable=FALSE;
                OnAssistEdit=BEGIN
                               SetRelation('C',TotLengthDec);
                             END;
                              }

    { 1100525007;2;Field  ;
                Name=Breedte;
                CaptionML=[DEU=Breite - D;
                           ENU=Width - D;
                           NLD=Breedte - D;
                           NOR=Bredde Í D;
                           SVE=Bredd Í D];
                BlankZero=Yes;
                SourceExpr=WidthDec;
                Editable=BreedteEditable;
                OnValidate=BEGIN
                             VALIDATE(Width,WidthDec);
                             UpdateSheet;
                           END;

                OnAssistEdit=BEGIN
                               IF WidthDec <> 0 THEN
                                 SetRelation('D',WidthDec)
                               ELSE
                                 ERROR(Text001);
                             END;
                              }

    { 1100525023;2;Field  ;
                Name=TotBreedte;
                CaptionML=[DEU=Gesamtbreite - E;
                           ENU=Total Width - E;
                           NLD=Totale Breedte - E;
                           NOR=Total bredde Í E;
                           SVE=Totalbredd Í E];
                BlankZero=Yes;
                SourceExpr=TotWidthDec;
                Visible=FALSE;
                Editable=FALSE;
                OnAssistEdit=BEGIN
                               SetRelation('E',TotWidthDec);
                             END;
                              }

    { 1100525009;2;Field  ;
                Name=Hoogte;
                CaptionML=[DEU=H”he - F;
                           ENU=Height - F;
                           NLD=Hoogte - F;
                           NOR=H›yde Í F;
                           SVE=H”jd Í F];
                BlankZero=Yes;
                SourceExpr=HeightDec;
                Editable=HoogteEditable;
                OnValidate=BEGIN
                             VALIDATE(Height,HeightDec);
                             UpdateSheet;
                           END;

                OnAssistEdit=BEGIN
                               IF HeightDec <> 0 THEN
                                 SetRelation('F',HeightDec)
                               ELSE
                                 ERROR(Text001);
                             END;
                              }

    { 1100525025;2;Field  ;
                Name=TotHoogte;
                CaptionML=[DEU=Gesamth”he - G;
                           ENU=Total Height - G;
                           NLD=Totale Hoogte - G;
                           NOR=Total h›yde Í G;
                           SVE=Total h”jd Í G];
                BlankZero=Yes;
                SourceExpr=TotHeightDec;
                Visible=FALSE;
                Editable=FALSE;
                OnAssistEdit=BEGIN
                               SetRelation('G',TotHeightDec);
                             END;
                              }

    { 1100525011;2;Field  ;
                Name=Resultaat;
                CaptionML=[DEU=Result;
                           ENU=Result;
                           NLD=Resultaat;
                           NOR=Resultat;
                           SVE=Resultat];
                BlankZero=Yes;
                SourceExpr=ResultDec;
                Editable=FALSE;
                OnAssistEdit=BEGIN
                               SetRelation('R',ResultDec);
                             END;
                              }

    { 1100525015;2;Field  ;
                SourceExpr=Formula;
                Editable=FormulaEditable;
                OnValidate=BEGIN
                             CurrPage.UPDATE;
                           END;
                            }

    { 1100525017;2;Field  ;
                SourceExpr=Total;
                OnValidate=BEGIN
                             CurrPage.UPDATE;
                           END;
                            }

    { 1100525019;2;Field  ;
                SourceExpr=Totaling;
                OnValidate=BEGIN
                             CurrPage.UPDATE;
                           END;
                            }

    { 1100409004;2;Field  ;
                SourceExpr=Feature }

    { 1100409002;2;Field  ;
                SourceExpr="Feature Description";
                Importance=Additional }

    { 1100409003;2;Field  ;
                SourceExpr="Related to Column" }

    { 1100409000;2;Field  ;
                SourceExpr="Related to Part Quantity" }

    { 1100409001;2;Field  ;
                Name=RelationTxt;
                CaptionML=[DEU=Kontakt;
                           ENU=Relation;
                           NLD=Relatie;
                           NOR=Relasjon;
                           SVE=Relation];
                SourceExpr=RelationTxt;
                Editable=FALSE }

  }
  CODE
  {
    VAR
      Text001@1100525011 : TextConst 'DEU=Kein Wert im betreffenden Feld;ENU=No value in concerning field;NLD=Geen waarde in het betreffende veld;NOR=Ingen verdi i relevant felt;SVE=Inget v„rde i relevant f„lt';
      QuantDec@1100528915 : Decimal;
      LengthDec@1100528914 : Decimal;
      WidthDec@1100528913 : Decimal;
      HeightDec@1100528912 : Decimal;
      ResultDec@1100528911 : Decimal;
      TotLengthDec@1100528910 : Decimal;
      TotWidthDec@1100528909 : Decimal;
      TotHeightDec@1100528908 : Decimal;
      gvFromLineNo@1100528907 : Integer;
      gvFromFieldNo@1100528906 : Code[1];
      gvEstimate@1100528905 : Code[20];
      gvSubEstimate@1100528904 : Code[20];
      gvPartGroup@1100528903 : Code[20];
      gvPart@1100528902 : Code[20];
      gvEstLine@1100528901 : Integer;
      gvRecipeGroup@1100409003 : Code[20];
      gvRecipe@1100409002 : Code[20];
      gvRecipeLine@1100409001 : Integer;
      AantalEditable@1100528932 : Boolean INDATASET;
      LengteEditable@1100528931 : Boolean INDATASET;
      BreedteEditable@1100528930 : Boolean INDATASET;
      HoogteEditable@1100528929 : Boolean INDATASET;
      FormulaEditable@1100528928 : Boolean INDATASET;
      RelationTxt@1100409000 : Code[10];
      Text002@1100409004 : TextConst 'DEU=M”chten Sie eine Beziehung zur Herkunft erstellen: \%1 \%2 \%3;ENU=Do you want to create Relation with Source: \%1 \%2 \%3;NLD="Wilt u een relatie leggen met bron: \%1 \%2 \%3 "';
      Text002a@1100409005 : TextConst 'DEU=Quelle w„hlen;ENU=Select Source;NLD=Selecteer bron;NOR=Velg kilde;SVE=V„lj k„lla';
      Selection@1100409006 : Integer;
      Text002b@1100409007 : TextConst 'DEU=Teilzeile, Rezeptzeile;ENU=Part Line,Recipe Line;NLD=Onderdeelregel,Receptregel';

    PROCEDURE UpdateSheet@1100525000();
    VAR
      SheetRec@1100525000 : Record 11020460;
    BEGIN
      IF "Row No." = 0 THEN EXIT; //db, 17-11-14: C019160 (due to call function from OnValidate instead of OnAfterValidate in classic version)

      CurrPage.SAVERECORD;
      SheetRec.SETRANGE("Part Group","Part Group");
      SheetRec.SETRANGE(Part,Part);
      SheetRec.SETRANGE("Quantity Sheet","Quantity Sheet");
      IF SheetRec.FINDSET THEN BEGIN
        REPEAT
          SheetRec.CalculateResultLine;
          SheetRec.MODIFY;
        UNTIL SheetRec.NEXT = 0;
      END;
      CurrPage.UPDATE(FALSE);
    END;

    PROCEDURE SetDefaults@1100525001(lvEstimate@1100528907 : Code[20];lvSubEstimate@1100528906 : Code[20];lvPartGroup@1100528905 : Code[20];lvPart@1100528904 : Code[20];lvEstLine@1100528903 : Integer;lvRecipeGroup@1100409002 : Code[20];lvRecipe@1100409001 : Code[20];lvRecipeLine@1100409000 : Integer;lvFromLineNo@1100528901 : Integer;lvFromFieldNo@1100528900 : Code[1]);
    BEGIN
      gvEstimate := lvEstimate;
      gvSubEstimate := lvSubEstimate;
      gvPartGroup := lvPartGroup;
      gvPart := lvPart;
      gvEstLine := lvEstLine;
      gvRecipeGroup := lvRecipeGroup;
      gvRecipe := lvRecipe;
      gvRecipeLine := lvRecipeLine;
      gvFromLineNo := lvFromLineNo;
      gvFromFieldNo := lvFromFieldNo;
    END;

    PROCEDURE SetRelation@1100525002(FieldCde@1100525000 : Code[1];ValueDec@1100525002 : Decimal);
    VAR
      QuantitySheetRelation@1100525001 : Record 11020463;
      PartLineNEW@1100528900 : Record 11072076;
    BEGIN
      IF NOT SourceSelected THEN EXIT;
      IF (gvPart = '') THEN EXIT;

      QuantitySheetRelation.EditRelation(
        gvEstimate, gvSubEstimate, gvPartGroup, gvPart, gvEstLine, gvRecipeGroup, gvRecipe, gvRecipeLine,
        "Quantity Sheet", "Row No.", FieldCde);

      IF Selection < 0 THEN BEGIN
        PartLineNEW.GET(gvPartGroup, gvPart, gvEstLine);
        PartLineNEW.VALIDATE(Quantity,ValueDec);
        PartLineNEW.MODIFY;
      END ELSE BEGIN
        gvPartGroup := '';
        gvPart := '';
        gvEstLine := 0;
        gvRecipeGroup := '';
        gvRecipe := '';
        gvRecipeLine := 0;
      END;

      QuantitySheetRelation.CLEARMARKS;
      gvFromLineNo := "Row No.";
      gvFromFieldNo := FieldCde;
      CurrPage.UPDATE;
    END;

    PROCEDURE DeleteRelation@1100525003();
    BEGIN
      DeleteLineRelation;
      gvFromLineNo := 0;
      gvFromFieldNo := '';
      CurrPage.UPDATE(FALSE);
    END;

    PROCEDURE GetResult@1100409000();
    BEGIN
      IF Total = Total::Subtotal THEN BEGIN
        CALCFIELDS("Sum Quantity","Sum Length","Sum Width","Sum Height","Sum Result","Sum Total Length","Sum Total Width",
                   "Sum Total Height");
        QuantDec := "Sum Quantity";
        LengthDec := "Sum Length";
        WidthDec := "Sum Width";
        HeightDec := "Sum Height";
        ResultDec := "Sum Result";
        TotLengthDec := "Sum Total Length";
        TotWidthDec := "Sum Total Width";
        TotHeightDec := "Sum Total Height";
      END ELSE BEGIN
        QuantDec := Quantity;
        LengthDec := Length;
        WidthDec := Width;
        HeightDec := Height;
        ResultDec := Result;
        TotLengthDec := "Total Length";
        TotWidthDec := "Total Width";
        TotHeightDec := "Total Height";
      END;
    END;

    PROCEDURE GetRelation@1100409002();
    VAR
      QuanRelation@1100409000 : Record 11020463;
    BEGIN
      //DP00686
      RelationTxt := '';
      QuanRelation.SETRANGE("Part Group", "Part Group");
      QuanRelation.SETRANGE(Part, Part);
      QuanRelation.SETRANGE("Quantity Sheet", "Quantity Sheet");
      QuanRelation.SETRANGE("Quantity Sheet Line", "Row No.");
      IF QuanRelation.FINDFIRST THEN
        RelationTxt := QuanRelation."Quantity Sheet Field";
    END;

    PROCEDURE SourceSelected@1100409001() : Boolean;
    VAR
      PartLineNEW@1100409004 : Record 11072076;
      RecipeLineNEW@1100409002 : Record 11020582;
      LineDesc@1100409001 : Text[250];
    BEGIN
      Selection := 0;
      IF gvPart <> '' THEN BEGIN
        IF NOT PartLineNEW.GET(gvPartGroup, gvPart, gvEstLine) THEN PartLineNEW.INIT;
        LineDesc := PartLineNEW.Description;
        IF CONFIRM(Text002, TRUE, gvPartGroup, gvPart, LineDesc) THEN
          Selection := -1;  //use predefined global variables
      END;

      IF Selection = 0 THEN
        Selection := DIALOG.STRMENU(Text002b, 1, Text002a);  //Select Source: 1=PartLine; 2=RecipeLine
      IF Selection = 0 THEN EXIT(FALSE);

      IF Selection > 0 THEN BEGIN
        PartLineNEW.SETRANGE("Part Group", "Part Group");
        PartLineNEW.SETRANGE(Part, Part);
        IF Selection = 1 THEN
          PartLineNEW.SETFILTER(Recipe, '%1', '');
        IF Selection = 2 THEN
          PartLineNEW.SETFILTER(Recipe, '<>%1', '');
        IF PAGE.RUNMODAL(PAGE::"Estimate Part Lines", PartLineNEW) = ACTION::LookupOK THEN BEGIN
          gvPartGroup := PartLineNEW."Part Group";
          gvPart := PartLineNEW.Part;
          gvEstLine := PartLineNEW."Line No.";
        END ELSE BEGIN
          EXIT(FALSE);
        END;
        IF Selection = 2 THEN BEGIN
          RecipeLineNEW.SETRANGE("Recipe Group", PartLineNEW."Recipe Group");
          RecipeLineNEW.SETRANGE(Recipe, PartLineNEW.Recipe);
          IF PAGE.RUNMODAL(PAGE::"Base Recipe Lines List", RecipeLineNEW) = ACTION::LookupOK THEN BEGIN
            gvRecipeGroup := RecipeLineNEW."Recipe Group";
            gvRecipe := RecipeLineNEW.Recipe;
            gvRecipeLine := RecipeLineNEW."Line No.";
          END ELSE BEGIN
            EXIT(FALSE);
          END;
        END;
      END;

      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

