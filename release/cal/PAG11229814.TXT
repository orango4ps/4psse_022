OBJECT Page 11229814 Copy SUP Rooms
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=SUP-R„ume kopieren;
               ENU=Copy SUP Rooms;
               NLD=SEP ruimtes kopi‰ren];
    PageType=StandardDialog;
    OnInit=BEGIN
             ToType := ToType::Customer;
           END;

    OnOpenPage=BEGIN
                 UpdateEnabled;
               END;

    OnQueryClosePage=BEGIN
                       CloseActionOK := CloseAction IN [ACTION::OK,ACTION::LookupOK];
                     END;

  }
  CONTROLS
  {
    { 1100528600;;Container;
                ContainerType=ContentArea }

    { 1100528601;1;Group  ;
                CaptionML=[DEU=Kopieren von;
                           ENU=Copy from;
                           NLD=Kopi‰ren van;
                           NOR=Kopier fra;
                           SVE=Kopiera fr†n];
                GroupType=Group }

    { 1100528605;2;Field  ;
                CaptionML=[DEU=Art;
                           ENU=Type;
                           NLD=Type;
                           NOR=Radtype;
                           SVE=Radtyp];
                OptionCaptionML=[DEU=Standard,Debitorenspezifisch;
                                 ENU=Standard,Customer;
                                 NLD=Standaard,Klant;
                                 NOR=Standard,Kunde;
                                 SVE=Standard,Kund];
                SourceExpr=FromType;
                OnValidate=BEGIN
                             FromCustomerNo := '';
                             UpdateEnabled;
                           END;
                            }

    { 1100528602;2;Field  ;
                CaptionML=[DEU=Debitorennr.;
                           ENU=Customer No.;
                           NLD=Klantnr.;
                           NOR=Kundenr;
                           SVE=Kundnr];
                SourceExpr=FromCustomerNo;
                TableRelation=Customer;
                Enabled=FromCustomerNoEnabled }

    { 1100528603;1;Group  ;
                CaptionML=[DEU=Kopieren in;
                           ENU=Copy to;
                           NLD=Kopi‰ren naar;
                           NOR=Kopier til;
                           SVE=Kopiera till];
                GroupType=Group }

    { 1100528606;2;Field  ;
                CaptionML=[DEU=Art;
                           ENU=Type;
                           NLD=Type;
                           NOR=Radtype;
                           SVE=Radtyp];
                OptionCaptionML=[DEU=Standard,Debitorenspezifisch;
                                 ENU=Standard,Customer;
                                 NLD=Standaard,Klant;
                                 NOR=Standard,Kunde;
                                 SVE=Standard,Kund];
                SourceExpr=ToType;
                OnValidate=BEGIN
                             ToCustomerNo := '';
                             UpdateEnabled;
                           END;
                            }

    { 1100528604;2;Field  ;
                CaptionML=[DEU=Debitorennr.;
                           ENU=Customer No.;
                           NLD=Klantnr.;
                           NOR=Kundenr;
                           SVE=Kundnr];
                SourceExpr=ToCustomerNo;
                TableRelation=Customer;
                Enabled=ToCustomerNoEnabled }

  }
  CODE
  {
    VAR
      FromType@1100528603 : 'Standard,Customer';
      FromCustomerNo@1100528600 : Code[20];
      FromCustomerNoEnabled@1100528607 : Boolean INDATASET;
      ToType@1100528604 : 'Standard,Customer';
      ToCustomerNo@1100528601 : Code[20];
      ToCustomerNoEnabled@1100528608 : Boolean INDATASET;
      CloseActionOK@1100528602 : Boolean;
      Text001@1100528605 : TextConst 'DEU=Quelle und Ziel k”nnen nicht gleich sein;ENU=Source and Destination cannot be equal;NLD=Bron en doel kunnen niet gelijk zijn';
      Text002@1100528606 : TextConst 'DEU=Tragen Sie zuerst alle Felder ein;ENU=Please, fill all fields first;NLD=Vul eerst alle velden;NOR=Fyll ut alle felt f›rst;SVE=Fyll i alla f„lt f”rst';

    PROCEDURE SetDefaults@1210190001(ICustomerNo@1210190005 : Code[20]);
    BEGIN
      FromCustomerNo := ICustomerNo;
      IF FromCustomerNo = '' THEN
        FromType := FromType::Standard
      ELSE
        FromType := FromType::Customer;
    END;

    LOCAL PROCEDURE CopyRooms@1100528402();
    VAR
      FromServiceUnitPriceRoom@1100528600 : Record 11071950;
      ToServiceUnitPriceRoom@1100528601 : Record 11071950;
    BEGIN
      IF (FromType = FromType::Customer) AND (FromCustomerNo = '') THEN
        ERROR(Text002);
      IF (ToType = ToType::Customer) AND (ToCustomerNo = '') THEN
        ERROR(Text002);
      IF (FromType = ToType) AND (FromCustomerNo = ToCustomerNo) THEN
        ERROR(Text001);

      IF FromType = FromType::Standard THEN
        FromServiceUnitPriceRoom.SETRANGE("Customer No.", '')
      ELSE
        FromServiceUnitPriceRoom.SETRANGE("Customer No.", FromCustomerNo);
      IF FromServiceUnitPriceRoom.FINDSET THEN
        REPEAT
          ToServiceUnitPriceRoom := FromServiceUnitPriceRoom;
          IF ToType = ToType::Standard THEN
            ToServiceUnitPriceRoom."Customer No." := ''
          ELSE
            ToServiceUnitPriceRoom."Customer No." := ToCustomerNo;
          ToServiceUnitPriceRoom.INSERT(TRUE);
        UNTIL FromServiceUnitPriceRoom.NEXT = 0;
    END;

    LOCAL PROCEDURE StdDialogConfirm@1100528400() : Boolean;
    BEGIN
      CurrPage.RUNMODAL;
      EXIT(CloseActionOK);
    END;

    LOCAL PROCEDURE StdDialogDoAction@1100528401();
    BEGIN
      CopyRooms;
    END;

    PROCEDURE StdDialogRunModal@1100528404();
    BEGIN
      IF StdDialogConfirm THEN
        StdDialogDoAction;
    END;

    PROCEDURE UpdateEnabled@1100528600();
    BEGIN
      FromCustomerNoEnabled := FromType = FromType::Customer;
      ToCustomerNoEnabled := ToType = ToType::Customer;
    END;

    BEGIN
    END.
  }
}

