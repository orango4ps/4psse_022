OBJECT Codeunit 11012380 User Management 4PS Upgtk
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 17=rm,
                TableData 21=rm,
                TableData 25=rm,
                TableData 45=rm,
                TableData 46=rm,
                TableData 96=rm,
                TableData 110=rm,
                TableData 112=rm,
                TableData 114=rm,
                TableData 120=rm,
                TableData 122=rm,
                TableData 124=rm,
                TableData 11072005=rm,
                TableData 203=rm,
                TableData 240=rm,
                TableData 254=rm,
                TableData 271=rm,
                TableData 272=rm,
                TableData 281=rm,
                TableData 297=rm,
                TableData 300=rm,
                TableData 304=rm,
                TableData 337=rm,
                TableData 339=rm,
                TableData 379=rm,
                TableData 380=rm,
                TableData 910=rm,
                TableData 1104=rm,
                TableData 1105=rm,
                TableData 1109=rm,
                TableData 1111=rm,
                TableData 11072027=rm,
                TableData 11072035=rm,
                TableData 5601=rm,
                TableData 5617=rm,
                TableData 11072085=rm,
                TableData 11072088=rm,
                TableData 11072093=rm,
                TableData 5802=rm,
                TableData 5907=rm,
                TableData 5934=rm,
                TableData 5969=rm,
                TableData 5970=rm,
                TableData 5990=rm,
                TableData 5992=rm,
                TableData 5994=rm,
                TableData 6650=rm,
                TableData 6660=rm,
                TableData 7134=rm,
                TableData 7312=rm,
                TableData 7313=rm;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Der Benutzername %1 ist nicht vorhanden.;ENU=The user name %1 does not exist.;NLD=De gebruikersnaam %1 bestaat niet.;NOR=Brukernavnet %1 finnes ikke.;SVE=Anv„ndarnamnet %1 finns inte.';
      Text002@1001 : TextConst 'DEU=Das Konto ''%1'' ist bereits vorhanden.;ENU=The account %1 already exists.;NLD=Account %1 bestaat al.;NOR=Kontoen %1 finnes allerede.;SVE=Kontot %1 finns redan.';

    PROCEDURE ValidateUserID@6(UserName@1000 : Code[50]);
    VAR
      User@1001 : Record 2000000120;
    BEGIN
      IF UserName <> '' THEN BEGIN
        User.SETCURRENTKEY("User Name");
        User.SETRANGE("User Name",UserName);
        IF NOT User.FINDFIRST THEN BEGIN
          User.RESET;
          IF NOT User.ISEMPTY THEN
            ERROR(Text000,UserName);
        END;
      END;
    END;

    PROCEDURE LookupUserID@2(VAR UserName@1000 : Code[50]);
    VAR
      SID@1002 : GUID;
    BEGIN
      LookupUser(UserName,SID);
    END;

    PROCEDURE LookupUser@3(VAR UserName@1000 : Code[50];VAR SID@1001 : GUID) : Boolean;
    VAR
      User@1002 : Record 2000000120;
    BEGIN
      User.RESET;
      User.SETCURRENTKEY("User Name");
      User."User Name" := UserName;
      IF User.FIND('=><') THEN;
      IF PAGE.RUNMODAL(PAGE::Users,User) = ACTION::LookupOK THEN BEGIN
        UserName := User."User Name";
        SID := User."User Security ID";
        EXIT(TRUE);
      END;

      EXIT(FALSE);
    END;

    PROCEDURE ValidateUserName@4(NewUser@1007 : Record 2000000120;OldUser@1008 : Record 2000000120;WindowsUserName@1005 : Text);
    VAR
      User@1000 : Record 2000000120;
    BEGIN
      IF NewUser."User Name" <> OldUser."User Name" THEN BEGIN
        User.SETRANGE("User Name",NewUser."User Name");
        User.SETFILTER("User Security ID",'<>%1',OldUser."User Security ID");
        IF User.FINDFIRST THEN
          ERROR(Text002,NewUser."User Name");

        IF NewUser."Windows Security ID" <> '' THEN
          NewUser.TESTFIELD("User Name",WindowsUserName);

        IF OldUser."User Name" <> '' THEN
          RenameUser(OldUser."User Name",NewUser."User Name",0);
      END;
    END;

    LOCAL PROCEDURE IsPrimaryKeyField@30(TableID@1005 : Integer;FieldID@1006 : Integer;VAR NumberOfPrimaryKeyFields@1004 : Integer) : Boolean;
    VAR
      ConfigValidateMgt@1000 : Codeunit 8617;
      RecRef@1003 : RecordRef;
      KeyRef@1002 : KeyRef;
    BEGIN
      RecRef.OPEN(TableID);
      KeyRef := RecRef.KEYINDEX(1);
      NumberOfPrimaryKeyFields := KeyRef.FIELDCOUNT;
      EXIT(ConfigValidateMgt.IsKeyField(TableID,FieldID));
    END;

    LOCAL PROCEDURE RenameRecord@8(VAR RecRef@1000 : RecordRef;TableNo@1004 : Integer;NumberOfPrimaryKeyFields@1001 : Integer;UserName@1003 : Code[50];Company@1014 : Text[30]);
    VAR
      UserTimeRegister@1005 : Record 51;
      PrinterSelection@1006 : Record 78;
      SelectedDimension@1007 : Record 369;
      OutlookSynchUserSetup@1008 : Record 5305;
      FAJournalSetup@1009 : Record 5605;
      AnalysisSelectedDimension@1010 : Record 7159;
      WarehouseEmployee@1002 : Record 7301;
      MyCustomer@1011 : Record 9150;
      MyVendor@1012 : Record 9151;
      MyItem@1013 : Record 9152;
    BEGIN
      IF NumberOfPrimaryKeyFields = 1 THEN
        RecRef.RENAME(UserName)
      ELSE
        CASE TableNo OF
          DATABASE::"User Time Register":
            BEGIN
              UserTimeRegister.CHANGECOMPANY(Company);
              RecRef.SETTABLE(UserTimeRegister);
              UserTimeRegister.RENAME(UserName,UserTimeRegister.Date);
            END;
          DATABASE::"Printer Selection":
            BEGIN
              RecRef.SETTABLE(PrinterSelection);
              PrinterSelection.RENAME(UserName,PrinterSelection."Report ID");
            END;
          DATABASE::"Selected Dimension":
            BEGIN
              SelectedDimension.CHANGECOMPANY(Company);
              RecRef.SETTABLE(SelectedDimension);
              SelectedDimension.RENAME(UserName,SelectedDimension."Object Type",SelectedDimension."Object ID",
                SelectedDimension."Analysis View Code",SelectedDimension."Dimension Code");
            END;
          DATABASE::"Outlook Synch. User Setup":
            BEGIN
              OutlookSynchUserSetup.CHANGECOMPANY(Company);
              RecRef.SETTABLE(OutlookSynchUserSetup);
              OutlookSynchUserSetup.RENAME(UserName,OutlookSynchUserSetup."Synch. Entity Code");
            END;
          DATABASE::"FA Journal Setup":
            BEGIN
              FAJournalSetup.CHANGECOMPANY(Company);
              RecRef.SETTABLE(FAJournalSetup);
              FAJournalSetup.RENAME(FAJournalSetup."Depreciation Book Code",UserName);
            END;
          DATABASE::"Analysis Selected Dimension":
            BEGIN
              AnalysisSelectedDimension.CHANGECOMPANY(Company);
              RecRef.SETTABLE(AnalysisSelectedDimension);
              AnalysisSelectedDimension.RENAME(UserName,AnalysisSelectedDimension."Object Type",AnalysisSelectedDimension."Object ID",
                AnalysisSelectedDimension."Analysis Area",AnalysisSelectedDimension."Analysis View Code",
                AnalysisSelectedDimension."Dimension Code");
            END;
          DATABASE::"Warehouse Employee":
            BEGIN
              WarehouseEmployee.CHANGECOMPANY(Company);
              RecRef.SETTABLE(WarehouseEmployee);
              WarehouseEmployee.RENAME(UserName,WarehouseEmployee."Location Code");
            END;
          DATABASE::"My Customer":
            BEGIN
              MyCustomer.CHANGECOMPANY(Company);
              RecRef.SETTABLE(MyCustomer);
              MyCustomer.RENAME(UserName,MyCustomer."Customer No.");
            END;
          DATABASE::"My Vendor":
            BEGIN
              MyVendor.CHANGECOMPANY(Company);
              RecRef.SETTABLE(MyVendor);
              MyVendor.RENAME(UserName,MyVendor."Vendor No.");
            END;
          DATABASE::"My Item":
            BEGIN
              MyItem.CHANGECOMPANY(Company);
              RecRef.SETTABLE(MyItem);
              MyItem.RENAME(UserName,MyItem."Item No.");
            END;
        END;
    END;

    PROCEDURE RenameUser@5(OldUserName@1000 : Code[50];NewUserName@1001 : Code[50];TableID@1100525002 : Integer);
    VAR
      Company@1100525001 : Record 2000000006;
      UpgradeSkipTableUserMigr@1100525000 : Record 11229893;
      User@1008 : Record 2000000120;
      Field@1007 : Record 2000000041;
      RecRef@1005 : RecordRef;
      FieldRef@1004 : FieldRef;
      FieldRef2@1003 : FieldRef;
      NumberOfPrimaryKeyFields@1002 : Integer;
      InstallationMgt@1100525003 : Codeunit 11012268;
      FPSLicenseManagement@1100527400 : Codeunit 11229289;
    BEGIN
      IF TableID <> 0 THEN
        Field.SETRANGE(TableNo,TableID);
      Field.SETRANGE(RelationTableNo,DATABASE::User);
      Field.SETRANGE(RelationFieldNo,User.FIELDNO("User Name"));
      Field.SETRANGE(Enabled,TRUE); //**4PS.n
      IF Field.FINDSET THEN
        REPEAT
          CLEAR(Company);
          Company.FINDSET;
          REPEAT
            IF InstallationMgt.LicensePermissionForTableData(Field.TableNo) THEN BEGIN  //**4PS.n
              IF NOT UpgradeSkipTableUserMigr.GET(Company.Name,Field.TableNo) THEN
                IF NOT UpgradeSkipTableUserMigr.GET('',Field.TableNo) THEN BEGIN
                  RecRef.OPEN(Field.TableNo,FALSE,Company.Name);
                  IF FPSLicenseManagement.LicenseAndReadPermissionForTable(RecRef) THEN BEGIN
                    FieldRef := RecRef.FIELD(Field."No.");
                    FieldRef.SETRANGE(OldUserName);
                    IF RecRef.FINDSET(TRUE) THEN
                      REPEAT
                        IF IsPrimaryKeyField(Field.TableNo,Field."No.",NumberOfPrimaryKeyFields) THEN
                          RenameRecord(RecRef,Field.TableNo,NumberOfPrimaryKeyFields,NewUserName,Company.Name)
                        ELSE BEGIN
                          FieldRef2 := RecRef.FIELD(Field."No.");
                          FieldRef2.VALUE := NewUserName;
                          RecRef.MODIFY;
                        END;
                      UNTIL RecRef.NEXT = 0;
                  //**4PS.so                                                          GET on TableInformation does not work like this for DataPerCompany=No tables
                  //END ELSE BEGIN                                                    But why an error message in rename function anyway
                  //  TableInformation.GET(Company.Name,Field.TableNo);
                  //  IF TableInformation."No. of Records" > 0 THEN                    Just skip if no readpermission.
                  //    ERROR(Text003);                                                Just skip if no license permission.
                  //**4PS.eo                                                          Data in not licensed tables should be deleted. This function must not run into errors.
                  END;
                  RecRef.CLOSE;
                END;
            END;
          UNTIL Company.NEXT = 0;
        UNTIL Field.NEXT = 0;
    END;

    BEGIN
    {
      4PS, Renumbered objects
    }
    END.
  }
}

