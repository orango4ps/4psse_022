OBJECT Table 11229820 Additional Data (PPT)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnModify=BEGIN
               CASE Type OF
                 Type::Option: BEGIN
                   "Minimum Value" := 0;
                   "Maximum Value" := 0;
                   "DateTime Format" := 0;
                 END;
                 Type::Value: BEGIN
                   "DateTime Format" := 0;
                 END;
                 Type::DateTime: BEGIN
                   "Minimum Value" := 0;
                   "Maximum Value" := 0;
                 END;
               END;
             END;

    OnDelete=VAR
               AdditionMetadataOptionPPT@1100528300 : Record 11229821;
               ProjectPortalMetadata@1100528301 : Record 11229818;
             BEGIN
               ProjectPortalMetadata.SETRANGE("Subsite Type", "Subsite Type");
               ProjectPortalMetadata.SETRANGE(Code, Code);
               ProjectPortalMetadata.SETRANGE("Standard Data", FALSE);
               IF (ProjectPortalMetadata.FINDFIRST) THEN
                 ERROR(Text000, TABLECAPTION, Code, ProjectPortalMetadata.TABLECAPTION);

               AdditionMetadataOptionPPT.SETRANGE("Subsite Type", "Subsite Type");
               AdditionMetadataOptionPPT.SETRANGE("Additional Data Code", Code);
               AdditionMetadataOptionPPT.DELETEALL(TRUE);;
             END;

    CaptionML=ENU=Additional Metadata (DPT);
    LookupPageID=Page11229195;
    DrillDownPageID=Page11229195;
  }
  FIELDS
  {
    { 5   ;   ;Subsite Type        ;Option        ;CaptionML=ENU=Subsite Type;
                                                   OptionCaptionML=ENU=Project,Service Location;
                                                   OptionString=Project,ServLoc;
                                                   Editable=No }
    { 10  ;   ;Code                ;Text30        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 20  ;   ;Description         ;Text50        ;OnValidate=BEGIN
                                                                UpdateDefaultTranslationDescription;
                                                              END;

                                                   CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 30  ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=ENU=Text,Option,Value,DateTime;
                                                   OptionString=Text,Option,Value,DateTime }
    { 50  ;   ;Minimum Value       ;Integer       ;OnValidate=BEGIN
                                                                IF ("Minimum Value" > "Maximum Value") AND ("Maximum Value" <> 0) THEN
                                                                  FIELDERROR("Minimum Value");
                                                              END;

                                                   CaptionML=[ENU=Minimum Value;
                                                              NOR=Minimumsverdi;
                                                              SVE=Minimiv„rde] }
    { 60  ;   ;Maximum Value       ;Integer       ;OnValidate=BEGIN
                                                                IF ("Maximum Value" < "Minimum Value") THEN
                                                                  FIELDERROR("Maximum Value");
                                                              END;

                                                   CaptionML=ENU=Maximum Value }
    { 70  ;   ;DateTime Format     ;Option        ;CaptionML=ENU=DateTime Format;
                                                   OptionCaptionML=ENU=" ,Date Only,Date And Time";
                                                   OptionString=[ ,DateOnly,DateAndTime] }
  }
  KEYS
  {
    {    ;Subsite Type,Code                       ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100525000 : TextConst 'ENU=%1 ''%2'' still used in %3.';

    LOCAL PROCEDURE UpdateDefaultTranslationDescription@1100525010();
    VAR
      SharePointSetup4PS@1100525000 : Record 11012730;
      MetadataTranslationPPT@1100528300 : Record 11229819;
    BEGIN
      SharePointSetup4PS.GET;
      IF (NOT MetadataTranslationPPT.GET("Subsite Type", 0, Code, SharePointSetup4PS."Language ID")) THEN BEGIN
        MetadataTranslationPPT."Subsite Type" := "Subsite Type";
        MetadataTranslationPPT.Type := MetadataTranslationPPT.Type::Description;
        MetadataTranslationPPT."Metadata Code" := Code;
        MetadataTranslationPPT."Language Id" := SharePointSetup4PS."Language ID";
        MetadataTranslationPPT.INSERT;
      END;
      MetadataTranslationPPT.Description := Description;
      MetadataTranslationPPT.MODIFY;
    END;

    BEGIN
    END.
  }
}

