OBJECT Table 737 VAT Return Period
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03;
  }
  PROPERTIES
  {
    OnInsert=VAR
               NoSeriesMgt@1001 : Codeunit 396;
             BEGIN
               IF "No." = '' THEN
                 NoSeriesMgt.InitSeries(GetNoSeriesCode,xRec."No. Series",WORKDATE,"No.","No. Series");
             END;

    OnDelete=VAR
               VATReportHeader@1000 : Record 740;
             BEGIN
               IF VATReportHeader.GET(VATReportHeader."VAT Report Config. Code"::"VAT Return","VAT Return No.") THEN
                 ERROR(DeleteExistingVATRetErr);
             END;

    CaptionML=[ENU=VAT Return Period;
               NOR=Omsetningsoppgaveperiode;
               SVE=Period fîr momsretur];
    LookupPageID=Page737;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;CaptionML=[ENU=No.;
                                                              NOR=Nr.;
                                                              SVE=Nummer] }
    { 2   ;   ;No. Series          ;Code20        ;CaptionML=[ENU=No. Series;
                                                              NOR=Nummerserie;
                                                              SVE=Nummerserie] }
    { 3   ;   ;Period Key          ;Code10        ;CaptionML=[ENU=Period Key;
                                                              NOR=Periodenõkkel;
                                                              SVE=Periodnyckel] }
    { 4   ;   ;Start Date          ;Date          ;CaptionML=[ENU=Start Date;
                                                              NOR=Startdato;
                                                              SVE=Startdatum] }
    { 5   ;   ;End Date            ;Date          ;CaptionML=[ENU=End Date;
                                                              NOR=Sluttdato;
                                                              SVE=Slutdatum] }
    { 6   ;   ;Due Date            ;Date          ;CaptionML=[ENU=Due Date;
                                                              NOR=Forfallsdato;
                                                              SVE=Fîrfallodatum] }
    { 7   ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=[ENU=Open,Closed;
                                                                    NOR=èpen,Lukket;
                                                                    SVE=ôppna,StÑngd];
                                                   OptionString=Open,Closed }
    { 8   ;   ;Received Date       ;Date          ;CaptionML=[ENU=Received Date;
                                                              NOR=Mottaksdato;
                                                              SVE=Mottagningsdatum] }
    { 20  ;   ;VAT Return No.      ;Code20        ;TableRelation=IF (VAT Return No.=FILTER(<>'')) "VAT Report Header".No. WHERE (VAT Report Config. Code=CONST(VAT Return),
                                                                                                                                 No.=FIELD(VAT Return No.));
                                                   OnLookup=BEGIN
                                                              DrillDownVATReturn;
                                                            END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=VAT Return No.;
                                                              NOR=Omsetningsoppgavenummer;
                                                              SVE=Momsreturnummer];
                                                   Editable=No }
    { 21  ;   ;VAT Return Status   ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("VAT Report Header".Status WHERE (VAT Report Config. Code=CONST(VAT Return),
                                                                                                        No.=FIELD(VAT Return No.)));
                                                   CaptionML=[ENU=VAT Return Status;
                                                              NOR=Omsetningsoppgavestatus;
                                                              SVE=Momsreturstatus];
                                                   OptionCaptionML=[ENU=Open,Released,Submitted,Accepted,Closed,Rejected,Canceled;
                                                                    NOR=èpen,Frigitt,Sendt,Godtatt,Lukket,Avvist,Kansellert;
                                                                    SVE=ôppen,SlÑppt,Skickad,GodkÑnd,StÑngd,Avvisad,Annullerad];
                                                   OptionString=Open,Released,Submitted,Accepted,Closed,Rejected,Canceled;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DeleteExistingVATRetErr@1000 : TextConst 'ENU=You cannot delete a VAT return period that has a linked VAT return.;NOR=Du kan ikke slette en omsetningsoppgaveperiode med en tilknyttet omsetningsoppgave.;SVE=Du kan inte ta bort en momsreturperiod som har en lÑnkad momsretur.';
      OverdueTxt@1002 : TextConst '@@@="%1 - date; %2 - days count";ENU=Your VAT return is overdue since %1 (%2 days);NOR=Omsetningsoppgaven forfalt %1 (for %2 dager siden);SVE=Din momsretur Ñr fîrfallen sedan %1 (%2 dagar)';
      OpenTxt@1001 : TextConst '@@@="%1 - date; %2 - days count";ENU=Your VAT return is due %1 (in %2 days);NOR=Omsetningsoppgaven forfaller %1 (om %2 dager);SVE=Din momsretur fîrfaller %1 (om %2 dagar)';
      VATReportSetup@1003 : Record 743;
      VATReportSetupGot@1004 : Boolean;

    LOCAL PROCEDURE GetNoSeriesCode@4() : Code[20];
    VAR
      VATReportSetup@1000 : Record 743;
    BEGIN
      VATReportSetup.GET;
      VATReportSetup.TESTFIELD("VAT Return Period No. Series");
      EXIT(VATReportSetup."VAT Return Period No. Series");
    END;

    PROCEDURE CopyToVATReturn@1(VAR VATReportHeader@1000 : Record 740);
    BEGIN
      TESTFIELD("Start Date");
      TESTFIELD("End Date");
      VATReportHeader."Return Period No." := "No.";
      VATReportHeader."Period Year" := DATE2DMY("End Date",3);
      VATReportHeader."Start Date" := "Start Date";
      VATReportHeader."End Date" := "End Date";
      ParseDatePeriod(VATReportHeader);
    END;

    LOCAL PROCEDURE ParseDatePeriod@2(VAR VATReportHeader@1000 : Record 740);
    VAR
      Date@1001 : Record 2000000007;
    BEGIN
      Date.SETRANGE("Period Start",VATReportHeader."Start Date");
      Date.SETRANGE("Period End",CLOSINGDATE(VATReportHeader."End Date"));
      IF Date.FINDFIRST THEN BEGIN
        CASE Date."Period Type" OF
          Date."Period Type"::Month:
            VATReportHeader."Period Type" := VATReportHeader."Period Type"::Month;
          Date."Period Type"::Quarter:
            VATReportHeader."Period Type" := VATReportHeader."Period Type"::Quarter;
          Date."Period Type"::Year:
            VATReportHeader."Period Type" := VATReportHeader."Period Type"::Year;
        END;
        VATReportHeader."Period No." := Date."Period No.";
      END ELSE BEGIN
        VATReportHeader."Period Type" := VATReportHeader."Period Type"::" ";
        VATReportHeader."Period No." := 0;
      END;
    END;

    PROCEDURE FindVATReturnPeriod@3(VAR VATReturnPeriod@1000 : Record 737;StartDate@1001 : Date;EndDate@1002 : Date) : Boolean;
    BEGIN
      VATReturnPeriod.SETRANGE("Start Date",StartDate);
      VATReturnPeriod.SETRANGE("End Date",EndDate);
      EXIT(VATReturnPeriod.FINDFIRST);
    END;

    PROCEDURE DiffersFromVATReturnPeriod@5(VAR VATReturnPeriod@1000 : Record 737) : Boolean;
    BEGIN
      EXIT(
        ("Due Date" <> VATReturnPeriod."Due Date") OR
        (Status <> VATReturnPeriod.Status) OR
        ("Received Date" <> VATReturnPeriod."Received Date") OR
        ("Period Key" <> VATReturnPeriod."Period Key"));
    END;

    PROCEDURE DrillDownVATReturn@6();
    VAR
      VATReportMgt@1000 : Codeunit 737;
    BEGIN
      IF (Status = Status::Open) OR ("VAT Return No." <> '') THEN
        VATReportMgt.OpenVATReturnCardFromVATPeriod(Rec);
    END;

    LOCAL PROCEDURE GetVATReportSetup@8();
    BEGIN
      IF VATReportSetupGot THEN
        EXIT;

      VATReportSetup.GET;
      VATReportSetupGot := TRUE;
    END;

    PROCEDURE CheckOpenOrOverdue@7() : Text;
    BEGIN
      GetVATReportSetup;
      IF (Status = Status::Open) AND ("Due Date" <> 0D) THEN
        CASE TRUE OF
          // Overdue
          ("Due Date" < WORKDATE):
            EXIT(STRSUBSTNO(OverdueTxt,"Due Date",WORKDATE - "Due Date"));
          // Open
          VATReportSetup.IsPeriodReminderCalculation AND
          ("Due Date" >= WORKDATE) AND
          ("Due Date" <= CALCDATE(VATReportSetup."Period Reminder Calculation",WORKDATE)):
            EXIT(STRSUBSTNO(OpenTxt,"Due Date","Due Date" - WORKDATE));
        END;
    END;

    BEGIN
    END.
  }
}

