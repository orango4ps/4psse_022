OBJECT Table 11020670 Frost Out Registration
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ValidateRecord;
             END;

    OnModify=BEGIN
               ValidateRecord;
               "Last Date Modified" := CURRENTDATETIME;
               "Modified By" := USERID;
             END;

    OnRename=BEGIN
               ValidateRecord;
             END;

    CaptionML=[ENU=Frost Out Registration;
               NOR=Frost Out-registrering;
               SVE=Frost Out-registrering];
    LookupPageID=Page11125018;
  }
  FIELDS
  {
    { 10  ;   ;Employee No.        ;Code20        ;TableRelation=Employee.No. WHERE (Status=CONST(Active),
                                                                                     External=CONST(No));
                                                   OnValidate=BEGIN
                                                                IF (UnfinishedRegistrationsPresent) THEN
                                                                  ERROR(Text000, TABLECAPTION);

                                                                CALCFIELDS("Employee Name");
                                                              END;

                                                   CaptionML=[ENU=Employee No.;
                                                              NOR=Ansattnr;
                                                              SVE=AnstÑllningsnr];
                                                   NotBlank=Yes }
    { 20  ;   ;Employee Name       ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Full Name" WHERE (No.=FIELD(Employee No.)));
                                                   CaptionML=[ENU=Employee Name;
                                                              NOR=Navn pÜ ansatt;
                                                              SVE=Namn pÜ anstÑlld];
                                                   Editable=No }
    { 30  ;   ;Register Date       ;Date          ;OnValidate=BEGIN
                                                                IF (AbsenceHourPresent) THEN
                                                                  ERROR(Text003);
                                                              END;

                                                   CaptionML=[ENU=Register Date;
                                                              NOR=Registrer Dato;
                                                              SVE=Registrera datum];
                                                   NotBlank=Yes }
    { 40  ;   ;Register Time       ;Time          ;OnValidate=BEGIN
                                                                IF ("Register Date" <> 0D) AND ("Register Time" = 0T) THEN
                                                                  "Register Time" := TIME;

                                                                TESTFIELD("Register Time");
                                                              END;

                                                   CaptionML=[ENU=Register Time;
                                                              NOR=Registrer tid;
                                                              SVE=Reg. av anvÑndartid];
                                                   AutoFormatType=2 }
    { 50  ;   ;Unregister Date     ;Date          ;OnValidate=BEGIN
                                                                IF ("Unregister Date" <> 0D) THEN BEGIN
                                                                  IF ("Unregister Date" < "Register Date") THEN
                                                                    ERROR(Text002, FIELDCAPTION("Unregister Date"), FIELDCAPTION("Register Date"));
                                                                  IF ("Unregister Time" = 0T) THEN
                                                                    "Unregister Time" := TIME;
                                                                  TESTFIELD("Unregister Time");
                                                                END ELSE
                                                                  "Unregister Time" := 0T;
                                                              END;

                                                   CaptionML=[ENU=Unregister Date;
                                                              NOR=Avregistrert;
                                                              SVE=Avregistrera datum] }
    { 60  ;   ;Unregister Time     ;Time          ;OnValidate=BEGIN
                                                                IF ("Unregister Date" <> 0D) THEN BEGIN
                                                                  IF ("Unregister Date" = "Register Date") AND
                                                                     ("Unregister Time" < "Register Time")
                                                                  THEN
                                                                    ERROR(Text002, FIELDCAPTION("Unregister Time"), FIELDCAPTION("Register Time"));

                                                                  IF ("Unregister Time" = 0T) THEN
                                                                    "Unregister Time" := TIME;
                                                                END;
                                                                IF ("Unregister Date" <> 0D) THEN
                                                                  TESTFIELD("Unregister Time");
                                                              END;

                                                   CaptionML=[ENU=Unregister Time;
                                                              NOR=Avregistrer tid;
                                                              SVE=Avregistrera tid];
                                                   AutoFormatType=2 }
    { 70  ;   ;Global Dimension 1 Code;Code20     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Global Dimension 1 Code" WHERE (No.=FIELD(Employee No.)));
                                                   CaptionML=[ENU=Global Dimension 1 Code;
                                                              NOR=Global dimensjon 1 - kode;
                                                              SVE=Global dimension 1 kod];
                                                   Editable=No;
                                                   CaptionClass='1,1,1' }
    { 80  ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   OnValidate=BEGIN
                                                                ValidateProjectAndServiceOrder;
                                                                FillAddressFields;
                                                              END;

                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr] }
    { 90  ;   ;Service Order No.   ;Code20        ;TableRelation="Service Order";
                                                   OnValidate=BEGIN
                                                                ValidateProjectAndServiceOrder;
                                                                FillAddressFields;
                                                              END;

                                                   CaptionML=[ENU=Service Order No.;
                                                              NOR=Serviceordrenr.;
                                                              SVE=Serviceordernr] }
    { 100 ;   ;Address             ;Text100       ;CaptionML=[ENU=Address;
                                                              NOR=Adresse;
                                                              SVE=Adress] }
    { 110 ;   ;Post Code           ;Code20        ;TableRelation=IF (Country/Region Code=CONST()) "Post Code"
                                                                 ELSE IF (Country/Region Code=FILTER(<>'')) "Post Code" WHERE (Country/Region Code=FIELD(Country/Region Code));
                                                   CaptionML=[ENU=Address;
                                                              NOR=Postnr;
                                                              SVE=Postnr] }
    { 120 ;   ;City                ;Text30        ;FieldClass=Normal;
                                                   TableRelation=IF (Country/Region Code=CONST()) "Post Code".City
                                                                 ELSE IF (Country/Region Code=FILTER(<>'')) "Post Code".City WHERE (Country/Region Code=FIELD(Country/Region Code));
                                                   OnValidate=BEGIN
                                                                PostCode.ValidateCity(City, "Post Code",County,"Country/Region Code",(CurrFieldNo <> 0) AND GUIALLOWED);
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=City;
                                                              NOR=Sted;
                                                              SVE=Summa inklusive moms] }
    { 125 ;   ;County              ;Text30        ;CaptionML=[ENU=County;
                                                              NOR=Delstat;
                                                              SVE=Delstat];
                                                   Editable=Yes }
    { 126 ;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   CaptionML=[ENU=Country/Region Code;
                                                              NOR=Lands-/regionkode;
                                                              SVE=Lands-/regionkod] }
    { 130 ;   ;Reason Code         ;Code10        ;TableRelation="Frost Out Reason";
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Reason Description");
                                                              END;

                                                   CaptionML=[ENU=Reason Code;
                                                              NOR=OvervÜkingskode;
                                                              SVE=Uppfîljningskod] }
    { 140 ;   ;Reason Description  ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Frost Out Reason".Description WHERE (Code=FIELD(Reason Code)));
                                                   CaptionML=[ENU=Reason Description;
                                                              NOR=èrsaksbeskrivelse;
                                                              SVE=Orsaksbeskrivning];
                                                   Editable=No }
    { 150 ;   ;Print Date Time     ;DateTime      ;CaptionML=[ENU=Print Date Time;
                                                              NOR=Skriv ut dato/tid;
                                                              SVE=Skriv ut datum/tid];
                                                   Editable=No }
    { 160 ;   ;Printed by          ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Printed by;
                                                              NOR=Skrevet ut av;
                                                              SVE=Utskrivet av];
                                                   Editable=No }
    { 170 ;   ;Last Date Modified  ;DateTime      ;CaptionML=[ENU=Last Date Modified;
                                                              NOR=Endret den;
                                                              SVE=Uppdateringsdatum];
                                                   Editable=No }
    { 180 ;   ;Modified By         ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Modified By;
                                                              NOR=Modified By;
                                                              SVE=Modified By];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Employee No.,Register Date              ;Clustered=Yes }
    {    ;Register Date,Register Time,Reason Code,Post Code }
    {    ;Employee No.,Unregister Date             }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1210190000 : TextConst 'ENU=There are unfinished %1s for this employee.;NOR=Det finnes uferdige %1er for denne ansatte.;SVE=Det finns oavslutade %1 fîr denna arbetstagare.';
      Text001@1210190001 : TextConst 'ENU=%1 and %2 can''t be filled both.;NOR=%1 og %2 kan ikke begge vëre fylt ut.;SVE=%1 och %2 kan inte fyllas i bÜda.';
      Text002@1210190002 : TextConst 'ENU=%1 is before %2.;NOR=%1 er fõr %2.;SVE=%1 Ñr fîre %2.';
      Text003@1210190003 : TextConst 'ENU=There are Absence Hours for this employee.;NOR=Det finnes fravërstimer for denne ansatte.;SVE=Det finns frÜnvarotimmar fîr denna arbetstagare.';
      PostCode@1210190004 : Record 225;
      DPA_Management@1210190005 : Codeunit 11012783;

    PROCEDURE FillAddressFields@1210190001();
    VAR
      Job@1210190000 : Record 11072003;
      ServiceOrder@1210190001 : Record 11012823;
    BEGIN
      CASE TRUE OF
        ("Project No." <> ''): BEGIN
          Job.GET("Project No.");
          Address := Job.Address;
          "Post Code" := Job."Post Code";
          City := Job.City;
        END;
        ("Service Order No." <> ''): BEGIN
          ServiceOrder.GET("Service Order No.");
          Address := ServiceOrder.Address;
          "Post Code" := ServiceOrder."Post Code";
          City := ServiceOrder.City;
        END;
      END;
    END;

    PROCEDURE ValidateRecord@1210190008();
    BEGIN
      VALIDATE("Register Time");
      VALIDATE("Unregister Time");
    END;

    PROCEDURE ValidateProjectAndServiceOrder@1210190000();
    BEGIN
      IF (("Project No." <> '') AND ("Service Order No." <> '')) THEN
        ERROR(Text001, FIELDCAPTION("Project No."), FIELDCAPTION("Service Order No."));
    END;

    PROCEDURE UnfinishedRegistrationsPresent@1210190002() : Boolean;
    VAR
      FrostOutRegistration@1210190000 : Record 11020670;
    BEGIN
      FrostOutRegistration.SETCURRENTKEY("Employee No.", "Unregister Date");
      FrostOutRegistration.SETRANGE("Employee No.", "Employee No.");
      FrostOutRegistration.SETRANGE("Unregister Date", 0D);
      EXIT(FrostOutRegistration.FINDFIRST);
    END;

    PROCEDURE AbsenceHourPresent@1210190003() : Boolean;
    VAR
      EmployeeAbsence@1210190000 : Record 5207;
    BEGIN
      EmployeeAbsence.SETCURRENTKEY("Employee No.", "From Date");
      EmployeeAbsence.SETRANGE("Employee No.", "Employee No.");
      EmployeeAbsence.SETFILTER("From Date", '<=%1', "Register Date");
      EmployeeAbsence.SETFILTER("To Date", '>=%1', "Register Date");
      EXIT(EmployeeAbsence.FINDFIRST);
    END;

    PROCEDURE DPA_GetFieldNoDepartmentFilter@1100000993() : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      EXIT(0);
    END;

    PROCEDURE DPA_GetFiltersFltrGrp@1100000997(FilterGroupNo@1100525000 : Integer) FilterText : Text[250];
    BEGIN
      //**4PS Generated Code - Do Not Change
      FILTERGROUP(FilterGroupNo);
      FilterText := COPYSTR(GETFILTERS, 1, MAXSTRLEN(FilterText));
      FILTERGROUP(0);
    END;

    PROCEDURE DPA_SetFilters@1100000998(Context@1100525000 : Text[30]) : Boolean;
    VAR
      RecRef@1100525003 : RecordRef;
      i@1100525004 : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN
        EXIT;

      RecRef.GETTABLE(Rec);
      DPA_Management.SetDPAFilterFromContext(
        RecRef,
        Context,
        DPA_GetFieldNoDepartmentCode,
        DPA_GetFieldNoDepartmentFilter );

      FOR i := 10 DOWNTO 0 DO BEGIN
        RecRef.FILTERGROUP(i);
        FILTERGROUP(i);
        SETVIEW(RecRef.GETVIEW(FALSE));
      END;

      EXIT(DPA_Management.IsHardFilter);
    END;

    LOCAL PROCEDURE DPA_IsActive@1100000999() : Boolean;
    VAR
      DepartmentAuthorizObject@1100526000 : Record 11012466;
      FPSLicenseManagement@1100527400 : Codeunit 11229289;
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT FPSLicenseManagement.LicenseAndReadPermissionForTable(DepartmentAuthorizObject) THEN
        EXIT(FALSE);

      IF NOT DPA_Management.AuthorizationActive THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    PROCEDURE DPA_FindRecWithDPAFilterAppld@1100000989();
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN BEGIN
        FIND;
        EXIT;
      END;

      IF NOT DPA_Management.HasTableHardFilter(11020670) THEN BEGIN
        FIND;
        EXIT;
      END;

      DPA_SetFilters('Table 11020670');
      IF NOT FIND THEN
        DPA_Management.RaiseDPAErrorForTable(TABLECAPTION);
    END;

    PROCEDURE DPA_ChkDepartmentCodeAllowed@1100000990(IsHardFilter@1100526000 : Boolean);
    VAR
      DimensionManagement@1100526001 : Codeunit 408;
    BEGIN
      //**4PS Generated Code - Do Not Change
      DimensionManagement.CheckDepartmAuthWithFilterType(
        DATABASE::"Frost Out Registration", "Global Dimension 1 Code", IsHardFilter);
    END;

    PROCEDURE DPA_GetFieldNoDepartmentCode@1100000994() : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      EXIT(FIELDNO("Global Dimension 1 Code"));
    END;

    PROCEDURE DPA_SetDefaultDepartment@1100000996();
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN
        EXIT;

      DPA_Management.SetDepartmentCodeUser(TABLENAME, "Global Dimension 1 Code");
    END;

    BEGIN
    END.
  }
}

