OBJECT Report 11012529 Export Opt. Crosslist to Excel
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Budget nach Excel exportieren;
               ENU=Export Budget to Excel;
               NLD=Budget naar Excel exporteren;
               NOR=Eksporter budsjett til Excel;
               SVE=Exportera budget till Excel];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 4274;    ;DataItem;                    ;
               DataItemTable=Table11012515;
               DataItemTableView=SORTING(Project No.,House Model,Main Group,Group,Sub Group,Option)
                                 ORDER(Ascending);
               OnPreDataItem=BEGIN
                               RowNo := 1;
                               EnterCell(RowNo,1,"Option Cross List".FIELDCAPTION("House Model"),TRUE,FALSE,'','',ExcelBuf."Cell Type"::Text);
                               EnterCell(RowNo,2,"Option Cross List".FIELDCAPTION("Main Group"),TRUE,FALSE,'','',ExcelBuf."Cell Type"::Text);
                               EnterCell(RowNo,3,"Option Cross List".FIELDCAPTION(Group),TRUE,FALSE,'','',ExcelBuf."Cell Type"::Text);
                               EnterCell(RowNo,4,"Option Cross List".FIELDCAPTION("Sub Group"),TRUE,FALSE,'','',ExcelBuf."Cell Type"::Text);
                               EnterCell(RowNo,5,"Option Cross List".FIELDCAPTION(Option),TRUE,FALSE,'','',ExcelBuf."Cell Type"::Text);
                               EnterCell(RowNo,6,"Option Cross List".FIELDCAPTION("Option No. External"),TRUE,FALSE,'','',ExcelBuf."Cell Type"::Text);
                               EnterCell(RowNo,7,"Option Cross List".FIELDCAPTION("Last Date Modified"),TRUE,FALSE,'','',ExcelBuf."Cell Type"::Text);
                               EnterCell(RowNo,8,"Option Cross List".FIELDCAPTION("Last Time Modified"),TRUE,FALSE,'','',ExcelBuf."Cell Type"::Text);
                               EnterCell(RowNo,9,"Option Cross List".FIELDCAPTION("Last Date Processed"),TRUE,FALSE,'','',ExcelBuf."Cell Type"::Text);
                               EnterCell(RowNo,10,"Option Cross List".FIELDCAPTION("Last Time Processed"),TRUE,FALSE,'','',ExcelBuf."Cell Type"::Text);
                               EnterCell(RowNo,11,"Option Cross List".FIELDCAPTION("Option Description"),TRUE,FALSE,'','',ExcelBuf."Cell Type"::Text);

                               ColNo := 11;
                               PlotRec.SETRANGE("Project No.","Option Cross List".GETRANGEMAX("Project No."));
                               IF PlotRec.FIND('-') THEN
                                 REPEAT
                                   ColNo += 1;
                                   EnterCell(1,ColNo,STRSUBSTNO('%1/%2',PlotRec."Plot No.",PlotRec."House Model"),TRUE,FALSE,'','',ExcelBuf."Cell Type"::Text);
                                 UNTIL PlotRec.NEXT = 0;

                               FirstBodyRow := 2;
                               FirstBodyColumn := 12;
                               LastColNo := ColNo;
                             END;

               OnAfterGetRecord=VAR
                                  OptionRec@1100528300 : Record 11012502;
                                BEGIN
                                  {
                                  IF (StatusFilter > 0) THEN BEGIN
                                    IF (Option.GET("Project No.", "House Model", "Main Group", Group, "Sub Group", Option)) THEN BEGIN
                                      IF Option.Status <> StatusFilter - 1 THEN
                                        CurrReport.SKIP;
                                    END;
                                  END;
                                  }

                                  RowNo += 1;
                                  EnterCell(RowNo,1,FORMAT("House Model"),FALSE,FALSE,'','',ExcelBuf."Cell Type"::Text);
                                  EnterCell(RowNo,2,FORMAT("Main Group"),FALSE,FALSE,'','',ExcelBuf."Cell Type"::Text);
                                  EnterCell(RowNo,3,FORMAT(Group),FALSE,FALSE,'','',ExcelBuf."Cell Type"::Text);
                                  EnterCell(RowNo,4,FORMAT("Sub Group"),FALSE,FALSE,'','',ExcelBuf."Cell Type"::Text);
                                  EnterCell(RowNo,5,FORMAT(Option),FALSE,FALSE,'','',ExcelBuf."Cell Type"::Text);
                                  EnterCell(RowNo,6,FORMAT("Option No. External"),FALSE,FALSE,'','',ExcelBuf."Cell Type"::Text);
                                  EnterCell(RowNo,7,FORMAT("Last Date Modified"),FALSE,FALSE,'','',ExcelBuf."Cell Type"::Date);
                                  EnterCell(RowNo,8,FORMAT("Last Time Modified"),FALSE,FALSE,'','',ExcelBuf."Cell Type"::Time);
                                  EnterCell(RowNo,9,FORMAT("Last Date Processed"),FALSE,FALSE,'','',ExcelBuf."Cell Type"::Date);
                                  EnterCell(RowNo,10,FORMAT("Last Time Processed"),FALSE,FALSE,'','',ExcelBuf."Cell Type"::Time);
                                  CALCFIELDS("Option Description");
                                  EnterCell(RowNo,11,FORMAT("Option Description"),FALSE,FALSE,'','',ExcelBuf."Cell Type"::Text);

                                  ColNo := FirstBodyColumn;
                                  PlotRec.SETRANGE("Project No.","Option Cross List".GETRANGEMAX("Project No."));

                                  IF PlotRec.FIND('-') THEN
                                    REPEAT
                                      IF OptionCrossListLineRec.GET("Project No.","House Model","Main Group",Group,"Sub Group",Option,PlotRec."Plot No.") THEN BEGIN
                                        //EnterCell(RowNo,ColNo,'',TRUE,FALSE,'','Wingdings');  niet meer vinkje maar aantal
                                        IF (StatusFilter > 0) THEN BEGIN
                                          IF (OptionRec.GET("Project No.", PlotRec."Plot No.", "House Model", "Main Group", Group, "Sub Group", Option)) THEN BEGIN
                                            IF OptionRec.Status = StatusFilter - 1 THEN
                                              EnterCell(RowNo,ColNo,FORMAT(OptionCrossListLineRec.Quantity),FALSE,FALSE,'','',ExcelBuf."Cell Type"::Number);
                                          END;
                                        END ELSE
                                          EnterCell(RowNo,ColNo,FORMAT(OptionCrossListLineRec.Quantity),FALSE,FALSE,'','',ExcelBuf."Cell Type"::Number);
                                      END;
                                      ColNo += 1;
                                    UNTIL PlotRec.NEXT = 0;
                                END;

               OnPostDataItem=VAR
                                FileManagement@1100528701 : Codeunit 419;
                                ExcelEnumeration@1100528703 : Codeunit 11012375;
                                ExcelAutomation@1100528700 : Codeunit 11012402;
                                FileNameClient@1100528702 : Text;
                                HeaderRangeTxt@1100485000 : Text[30];
                                ListRangeTxt@1100485001 : Text[30];
                                BodyRangeTxt@1100485002 : Text[30];
                                SheetRangeTxt@1100485003 : Text[30];
                              BEGIN
                                LastRowNo := RowNo;

                                ExcelBuf.CreateBook('',"Option Cross List"."Project No.");
                                ExcelBuf.WriteSheet(Text11012000, COMPANYNAME, USERID);
                                ExcelBuf.CloseBook;
                                FileNameClient := FileManagement.ClientTempFolderName + 'Book1.xlsx';
                                ExcelBuf.MoveFileToClient(FileNameClient);

                                ExcelAutomation.InitExcel;
                                ExcelAutomation.OpenWorkbook(FileNameClient);
                                ExcelAutomation.SetCurrentSheet("Option Cross List"."Project No.");

                                HeaderRangeTxt := ExcelBuf.RC2Range(1, 1, FirstBodyRow - 1, LastColNo);
                                ListRangeTxt := ExcelBuf.RC2Range(1, 1, LastRowNo, FirstBodyColumn - 1);
                                BodyRangeTxt := ExcelBuf.RC2Range(1, FirstBodyColumn, LastRowNo, LastColNo);
                                SheetRangeTxt := ExcelBuf.RC2Range(1, 1, LastRowNo, LastColNo);

                                ExcelAutomation.SetHorzAlignmentInRange(HeaderRangeTxt, ExcelEnumeration.XlHAlign_xlHAlignLeft);
                                ExcelAutomation.SetOrientationInRange(HeaderRangeTxt, 90); //vertical
                                ExcelAutomation.SetPrintHeader(HeaderRangeTxt);

                                ExcelAutomation.FreezePane(ExcelBuf.RC2Range(FirstBodyRow, FirstBodyColumn, 0, 0));

                                ExcelAutomation.SetBordersInRange(SheetRangeTxt, 11, 1, 1);
                                ExcelAutomation.SetBordersInRange(SheetRangeTxt, 12, 1, 1);

                                ExcelAutomation.SetBordersInRange(HeaderRangeTxt, 7, 1, 2);
                                ExcelAutomation.SetBordersInRange(HeaderRangeTxt, 8, 1, 2);
                                ExcelAutomation.SetBordersInRange(HeaderRangeTxt, 9, 1, 2);
                                ExcelAutomation.SetBordersInRange(HeaderRangeTxt, 10, 1, 2);

                                ExcelAutomation.SetBordersInRange(BodyRangeTxt, 9, 1, 2);
                                ExcelAutomation.SetBordersInRange(BodyRangeTxt, 10, 1, 2);

                                ExcelAutomation.SetBordersInRange(ListRangeTxt, 7, 1, 2);
                                ExcelAutomation.SetBordersInRange(ListRangeTxt, 9, 1, 2);
                                ExcelAutomation.SetBordersInRange(ListRangeTxt, 10, 1, 2);

                                ExcelAutomation.GiveUserControl;
                              END;

               ReqFilterFields=Project No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100528300;;Container;
                  ContainerType=ContentArea }

      { 1100528301;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100528302;2;Field  ;
                  CaptionML=[DEU=Status;
                             ENU=Status;
                             NLD=Status;
                             NOR=Status;
                             SVE=Status];
                  OptionCaptionML=[DEU=" ,Angewendet,Angeboten,Auftrag,Berechenbar,Abgelaufen";
                                   ENU=" ,Applied,Offered,Order,Chargeable,Expired";
                                   NLD=" ,Aangevraagd,Aangeboden,Opdracht,Factureerbaar,Vervallen"];
                  SourceExpr=StatusFilter }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ExcelBuf@1016 : TEMPORARY Record 370;
      RowNo@1024 : Integer;
      ColNo@1025 : Integer;
      PlotRec@1100485000 : Record 11012500;
      OptionCrossListLineRec@1100485003 : Record 11012516;
      FirstBodyRow@1100485005 : Integer;
      Text11012000@1100485004 : TextConst 'DEU=Optionskreuzliste;ENU=Option-Crosslist;NLD=Optie-kruisjeslijst;NOR=Alternativkryssliste;SVE=Alternativkorslista';
      FirstBodyColumn@1100485006 : Integer;
      LastRowNo@1100485007 : Integer;
      LastColNo@1100485008 : Integer;
      StatusFilter@1100528300 : ' ,Applied,Offered,Order,Invoice,Expired';

    LOCAL PROCEDURE EnterCell@3(RowNo@1000 : Integer;ColumnNo@1001 : Integer;CellValue@1002 : Text[250];Bold@1003 : Boolean;UnderLine@1004 : Boolean;NumberFormat@1005 : Text[30];FontName@1100485000 : Text[30];CellType@1100525000 : Option);
    BEGIN
      ExcelBuf.INIT;
      ExcelBuf.VALIDATE("Row No.",RowNo);
      ExcelBuf.VALIDATE("Column No.",ColumnNo);
      ExcelBuf."Cell Value as Text" := CellValue;
      ExcelBuf.Formula := '';
      ExcelBuf.Bold := Bold;
      ExcelBuf.Underline := UnderLine;
      ExcelBuf.NumberFormat := NumberFormat;
      ExcelBuf.FontName := FontName; //**4PS.n
      ExcelBuf."Cell Type" := CellType;
      ExcelBuf.INSERT;
    END;

    LOCAL PROCEDURE EnterFilterInCell@5(Filter@1000 : Text[250];FieldName@1001 : Text[100]);
    BEGIN
      IF Filter <> '' THEN BEGIN
        RowNo := RowNo + 1;
        EnterCell(RowNo,1,FieldName,FALSE,FALSE,'@','', ExcelBuf."Cell Type"::Text);
        EnterCell(RowNo,2,Filter,FALSE,FALSE,'@','', ExcelBuf."Cell Type"::Text);
      END;
    END;

    LOCAL PROCEDURE EnterFormula@8(RowNo@1000 : Integer;ColumnNo@1001 : Integer;CellValue@1002 : Text[50];RangeName1@1003 : Text[250];RangeName2@1004 : Text[250];Bold@1005 : Boolean;UnderLine@1006 : Boolean;CellType@1100525000 : Option);
    BEGIN
      ExcelBuf.INIT;
      ExcelBuf.VALIDATE("Row No.",RowNo);
      ExcelBuf.VALIDATE("Column No.",ColumnNo);
      ExcelBuf."Cell Value as Text" := '';
      ExcelBuf.Formula := CellValue; // is converted to formula later.
      ExcelBuf.Bold := Bold;
      ExcelBuf.Underline := UnderLine;
      ExcelBuf."Cell Type" := CellType;
      ExcelBuf.INSERT;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

