OBJECT Report 11125357 Send Time Sheet Reminders SM
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Send Time Sheet Reminder Site Manager;
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   WeekNo := DATE2DWY(TODAY, 2) -1;
                   Year := DATE2DWY(TODAY, 3);
                 END;

    OnPreReport=BEGIN
                  IF (Year = 0) OR (WeekNo = 0) THEN
                    ERROR(Text002);
                END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 1100527000;;DataItem;                  ;
               DataItemTable=Table5200;
               DataItemTableView=WHERE(Status=CONST(Active));
               OnPreDataItem=VAR
                               Employee@1100527600 : Record 5200;
                             BEGIN
                               SETFILTER("Account Hours",'%1',Employee."Account Hours"::"Required according to Norm");
                             END;

               OnAfterGetRecord=BEGIN
                                  SiteMngmntHoursHeader.SETRANGE(Year, Year);
                                  SiteMngmntHoursHeader.SETRANGE(Week, WeekNo);
                                  SiteMngmntHoursHeader.SETRANGE("Employee No.", "No.");
                                  IF NOT SiteMngmntHoursHeader.FINDFIRST THEN
                                    SendMessage("No.", STRSUBSTNO(Text000, WeekNo, Year));

                                  SiteMngmntHoursHeader.Year := Year;
                                  SiteMngmntHoursHeader.Week := WeekNo;
                                  IF NOT SiteMngmntHoursHeader.GetEmployeeHours("No.") THEN
                                    SendMessage("No.", STRSUBSTNO(Text000, WeekNo, Year));
                                END;

               ReqFilterFields=No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100527003;;Container;
                  ContainerType=ContentArea }

      { 1100527002;1;Group  ;
                  CaptionML=[ENU=Options;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100527000;2;Field  ;
                  CaptionML=[ENU=Year;
                             NOR=èr;
                             SVE=èr];
                  SourceExpr=Year }

      { 1100527001;2;Field  ;
                  CaptionML=[ENU=Week;
                             NOR=Uke;
                             SVE=Vecka];
                  SourceExpr=WeekNo }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      WeekNo@1100527001 : Integer;
      Year@1100527000 : Integer;
      Text000@1100527004 : TextConst 'ENU=Reminder: Time Sheet for week %1.%2 is due.';
      Text002@1100527002 : TextConst 'ENU=Please specify a week no. and a year.';
      SiteMngmntHoursHeader@1100527600 : Record 11012360;
      MailTemplateManagement@1100525000 : Codeunit 11012622;

    PROCEDURE SendMessage@1100527000(EmployeeNo@1100527001 : Code[20];Message@1100527000 : Text[250]);
    VAR
      Employee@1100527004 : Record 5200;
      TimeSheetReminderSent@1100527003 : Record 11229377;
      MailTemplate@1100525001 : Record 11229450;
      DocumentMailing@1100527005 : Codeunit 260;
      MailTemplateNo@1100525000 : Integer;
      Subject@1100525002 : Text;
    BEGIN
      IF TimeSheetReminderSent.GET(Year, WeekNo, EmployeeNo) THEN
        EXIT;

      Employee.GET(EmployeeNo);
      IF Employee."Company E-Mail" = '' THEN
        EXIT;

      Subject := Message;
      SiteMngmntHoursHeader.Year := Year;
      SiteMngmntHoursHeader.Week := WeekNo;
      MailTemplateNo := 113;
      MailTemplateManagement.GetMailTemplate(MailTemplate, MailTemplateNo, Employee."Global Dimension 1 Code");
      IF (MailTemplate."Use Mail Template Subject Text") THEN
        Subject := MailTemplateManagement.GetLayoutTextSubstitutedSubjectOnly(MailTemplateNo, '',
          SiteMngmntHoursHeader);

      DocumentMailing.EmailFile4PS(
        '',
        '',
        '',
        Employee."No.",
        Employee."Company E-Mail",
        MailTemplateNo,
        Employee."Global Dimension 1 Code",
        '',
        SiteMngmntHoursHeader,
        Subject,
        '',
        TRUE
      );

      TimeSheetReminderSent.Year := Year;
      TimeSheetReminderSent."Week No." := WeekNo;
      TimeSheetReminderSent."Employee No." := EmployeeNo;
      TimeSheetReminderSent."Date/Time" := CURRENTDATETIME;
      TimeSheetReminderSent.Message := Message;
      TimeSheetReminderSent.INSERT;

      // Don't let future failures roll-back the INSERT above,
      // because when we get here, reminders have actually been sent.
      COMMIT;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

