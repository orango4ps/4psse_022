OBJECT Report 11012900 Proj. Dev. Budget Distribution
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Budget verteilen;
               ENU=Distribute Budget;
               NLD=Budget verdelen;
               SVE=Distribuera budget];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   ProjDevSeupRec.GET;
                 END;

    OnPreReport=BEGIN
                  BudgetToDistrFilterRec.COPYFILTERS("Rubric Budget to Distribute");
                END;

  }
  DATASET
  {
    { 8998;    ;DataItem;                    ;
               DataItemTable=Table11012912;
               DataItemTableView=SORTING(Project No.,Version)
                                 WHERE(Fixed=CONST(No));
               OnPreDataItem=BEGIN
                               BudgetToDistrFilterRec.COPYFILTER("Project No.", "Project No.");
                               BudgetToDistrFilterRec.COPYFILTER(Version, Version);
                               CLEAR(ProjDevRec);
                             END;

               OnAfterGetRecord=BEGIN
                                  InvPropBudgetIsUpdated := FALSE;
                                  LowestBudgetDate := "Reference Date";
                                  IF ("Project No." <> ProjDevRec."Project No.") THEN BEGIN
                                    ProjDevRec.GET("Project No.");
                                    DeleteModelSalesByDate();
                                    IF (ProjDevRec."Budget Level" = ProjDevRec."Budget Level"::Model) AND (NOT gFromSurchCostDistrAsPercRev) THEN
                                      FillModelSalesByDate();
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                DeleteModelSalesByDate();
                              END;
                               }

    { 8359;1   ;DataItem;                    ;
               DataItemTable=Table11012913;
               DataItemTableView=SORTING(Project No.,Version,Rubric Code)
                                 WHERE(Type=FILTER(Costs|Revenues|Costs to Distr. as Perc. Rev.),
                                       Budget Distribution=FILTER(<>' '));
               OnPreDataItem=BEGIN
                               BudgetToDistrFilterRec.COPYFILTER("Rubric Code", "Rubric Code");
                             END;

               OnAfterGetRecord=BEGIN
                                  IF gFromSurchCostDistrAsPercRev THEN  //* Nieuwe nog niet weggeschreven waarden oppakken
                                    "Investm. Proposal Rubric Line" := gInvPropRubricRec;

                                  DeleteRubricBudget();
                                  DetermineRubricBudgNextLineNo("Investm. Proposal Rubric Line");
                                END;

               OnPostDataItem=VAR
                                lvInvPropRubricRec@1100485000 : Record 11012913;
                                lvInvPropRec@1100485001 : Record 11012912;
                              BEGIN
                                IF InvPropBudgetIsUpdated THEN BEGIN
                                  InvPropBudgetIsUpdated := FALSE;
                                  IF (LowestBudgetDate < "Investment Proposal"."Reference Date") THEN BEGIN
                                    //* Eigenlijk is dit niet toegestaan, maar we willen vaak niet dat het proces hierop afbreekt.
                                    //* Dan in dit geval altijd de referentiedatum maar aanpassen en melding geven (ook niet via vraag!).
                                    IF lvInvPropRec.GET("Investment Proposal"."Project No.", "Investment Proposal".Version) THEN BEGIN
                                      lvInvPropRec."Reference Date" := LowestBudgetDate;
                                      lvInvPropRec.MODIFY;
                                      MESSAGE(Text001, "Investment Proposal"."Reference Date", lvInvPropRec."Reference Date");
                                    END;
                                  END;
                                  IF (NOT gFromSurchCostDistrAsPercRev) THEN BEGIN
                                    lvInvPropRubricRec."Project No." := "Investment Proposal"."Project No.";
                                    lvInvPropRubricRec.Version := "Investment Proposal".Version;
                                    UpdateSurcharges(lvInvPropRubricRec, FALSE);
                                  END;
                                END;
                              END;

               DataItemLink=Project No.=FIELD(Project No.),
                            Version=FIELD(Version) }

    { 1916;2   ;DataItem;                    ;
               DataItemTable=Table11012909;
               DataItemTableView=SORTING(Project No.,Version,Rubric Code,House Model)
                                 WHERE(Amount=FILTER(<>0));
               OnAfterGetRecord=BEGIN
                                  ClearFields();
                                  IF (ProjDevRec."Budget Level" = ProjDevRec."Budget Level"::Model) AND ("House Model" <> '') THEN BEGIN
                                    HouseModel := "House Model";
                                    ProjDevModelRec.GET("Project No.", "House Model");
                                    ProjDevModelRec.TESTFIELD(Quantity);
                                    IF "Investm. Proposal Rubric Line".Type = "Investm. Proposal Rubric Line".Type::Revenues THEN BEGIN
                                      ProjDevModelRec.TESTFIELD("Starting Date Sales");
                                      ProjDevModelRec.TESTFIELD("Prognosis No. of Sales");
                                    END;
                                  END;

                                  CASE "Investm. Proposal Rubric Line".Type OF
                                    "Investm. Proposal Rubric Line".Type::Costs,
                                    "Investm. Proposal Rubric Line".Type::"Costs to Distr. as Perc. Rev.":
                                      BEGIN
                                        CASE "Investm. Proposal Rubric Line"."Budget Distribution" OF
                                          "Investm. Proposal Rubric Line"."Budget Distribution"::Linear:
                                            BEGIN
                                              IF (FORMAT("Frequency Budget Distr.") <> '') THEN
                                                FreqBudgetDistr := "Frequency Budget Distr.";
                                              IF (FORMAT(FreqBudgetDistr) = '') THEN BEGIN
                                                IF (HouseModel <> '') THEN BEGIN
                                                  ProjDevModelRec.TESTFIELD("Frequency Cost Budget Distr.");
                                                  FreqBudgetDistr := ProjDevModelRec."Frequency Cost Budget Distr.";
                                                END ELSE BEGIN
                                                  ProjDevRec.TESTFIELD("Frequency Cost Budget Distr.");
                                                  FreqBudgetDistr := ProjDevRec."Frequency Cost Budget Distr.";
                                                END;
                                              END;
                                            END;
                                          "Investm. Proposal Rubric Line"."Budget Distribution"::Scheme:
                                            BEGIN
                                              IF ("Scheme Budget Distr." <> '') THEN
                                                SchemeBudgetDistr := "Scheme Budget Distr.";
                                              IF (SchemeBudgetDistr = '') THEN BEGIN
                                                IF (HouseModel <> '') THEN BEGIN
                                                  ProjDevModelRec.TESTFIELD("Scheme Cost Budget Distr.");
                                                  SchemeBudgetDistr := ProjDevModelRec."Scheme Cost Budget Distr.";
                                                END ELSE BEGIN
                                                  ProjDevRec.TESTFIELD("Scheme Cost Budget Distr.");
                                                  SchemeBudgetDistr := ProjDevRec."Scheme Cost Budget Distr.";
                                                END;
                                              END;
                                            END;
                                        END;
                                        IF ("Start Date Budget Distr." <> 0D) THEN BEGIN
                                          //TESTFIELD("Start Date Budget Distr.");
                                          TESTFIELD("End Date Budget Distr.");  //* Both or none on this level
                                          StartDateDistr := "Start Date Budget Distr.";
                                          EndDateDistr := "End Date Budget Distr.";
                                        END;
                                        IF (StartDateDistr = 0D) THEN BEGIN
                                          IF (HouseModel <> '') THEN BEGIN
                                            ProjDevModelRec.TESTFIELD("Start Date Cost Budget Distr.");
                                            ProjDevModelRec.TESTFIELD("End Date Cost Budget Distr.");
                                            StartDateDistr := ProjDevModelRec."Start Date Cost Budget Distr.";
                                            EndDateDistr := ProjDevModelRec."End Date Cost Budget Distr.";
                                          END ELSE BEGIN
                                            ProjDevRec.TESTFIELD("Start Date Cost Budget Distr.");
                                            ProjDevRec.TESTFIELD("End Date Cost Budget Distr.");
                                            StartDateDistr := ProjDevRec."Start Date Cost Budget Distr.";
                                            EndDateDistr := ProjDevRec."End Date Cost Budget Distr.";
                                          END;
                                        END;
                                      END;
                                    "Investm. Proposal Rubric Line".Type::Revenues:
                                      BEGIN
                                        CASE "Investm. Proposal Rubric Line"."Budget Distribution" OF
                                          "Investm. Proposal Rubric Line"."Budget Distribution"::Linear:
                                            BEGIN
                                              IF (FORMAT("Frequency Budget Distr.") <> '') THEN
                                                FreqBudgetDistr := "Frequency Budget Distr.";
                                              IF (FORMAT(FreqBudgetDistr) = '') THEN BEGIN
                                                IF (HouseModel <> '') THEN BEGIN
                                                  ProjDevModelRec.TESTFIELD("Frequency Rev. Budget Distr.");
                                                  FreqBudgetDistr := ProjDevModelRec."Frequency Rev. Budget Distr.";
                                                END ELSE BEGIN
                                                  ProjDevRec.TESTFIELD("Frequency Rev. Budget Distr.");
                                                  FreqBudgetDistr := ProjDevRec."Frequency Rev. Budget Distr.";
                                                END;
                                              END;
                                            END;
                                          "Investm. Proposal Rubric Line"."Budget Distribution"::Scheme:
                                            BEGIN
                                              IF ("Scheme Budget Distr." <> '') THEN
                                                SchemeBudgetDistr := "Scheme Budget Distr.";
                                              IF (SchemeBudgetDistr = '') THEN BEGIN
                                                IF (HouseModel <> '') THEN BEGIN
                                                  ProjDevModelRec.TESTFIELD("Scheme Rev. Budget Distr.");
                                                  SchemeBudgetDistr := ProjDevModelRec."Scheme Rev. Budget Distr.";
                                                END ELSE BEGIN
                                                  ProjDevRec.TESTFIELD("Scheme Rev. Budget Distr.");
                                                  SchemeBudgetDistr := ProjDevRec."Scheme Rev. Budget Distr.";
                                                END;
                                              END;
                                            END;
                                        END;
                                        IF ("Start Date Budget Distr." <> 0D) OR ("End Date Budget Distr." <> 0D) THEN BEGIN
                                          TESTFIELD("Start Date Budget Distr.");   //* Both or none on this level
                                          TESTFIELD("End Date Budget Distr.");
                                          StartDateDistr := "Start Date Budget Distr.";
                                          EndDateDistr := "End Date Budget Distr.";
                                        END;
                                        IF (StartDateDistr = 0D) THEN BEGIN
                                          IF (HouseModel <> '') THEN BEGIN
                                            ProjDevModelRec.TESTFIELD("Start Date Rev. Budget Distr.");
                                            ProjDevModelRec.TESTFIELD("End Date Rev. Budget Distr.");
                                            StartDateDistr := ProjDevModelRec."Start Date Rev. Budget Distr.";
                                            EndDateDistr := ProjDevModelRec."End Date Rev. Budget Distr.";
                                          END ELSE BEGIN
                                            ProjDevRec.TESTFIELD("Start Date Rev. Budget Distr.");
                                            ProjDevRec.TESTFIELD("End Date Rev. Budget Distr.");
                                            StartDateDistr := ProjDevRec."Start Date Rev. Budget Distr.";
                                            EndDateDistr := ProjDevRec."End Date Rev. Budget Distr.";
                                          END;
                                        END;
                                      END;
                                  END;
                                  IF EndDateDistr < StartDateDistr THEN
                                    EndDateDistr := StartDateDistr;

                                  DistributeBudget(Amount);
                                END;

               ReqFilterFields=Project No.,Version,Rubric Code,House Model;
               DataItemLink=Project No.=FIELD(Project No.),
                            Version=FIELD(Version),
                            Rubric Code=FIELD(Rubric Code) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ProjDevSeupRec@1100485020 : Record 11012900;
      ProjDevRec@1100485000 : Record 11012915;
      ProjDevModelRec@1100485002 : Record 11012919;
      gInvPropRubricRec@1100485022 : Record 11012913;
      BudgetDistrInstallRec@1100485016 : Record 11012908;
      BudgetToDistrFilterRec@1100485001 : Record 11012909;
      TmpModelSalesByDateRec@1100485010 : TEMPORARY Record 11012914;
      FreqBudgetDistr@1100485003 : DateFormula;
      StartDateDistr@1100485006 : Date;
      EndDateDistr@1100485007 : Date;
      LowestBudgetDate@1100485023 : Date;
      UseTimePerc@1100485019 : Boolean;
      InvPropBudgetIsUpdated@1100485008 : Boolean;
      gFromSurchCostDistrAsPercRev@1100485021 : Boolean;
      NoOfPeriods@1100485018 : Integer;
      PeriodNo@1100485017 : Integer;
      NextLineNo@1100485009 : Integer;
      BudgetQuantity@1100485011 : Decimal;
      BudgetAmount@1100485014 : Decimal;
      QuanFactor@1100485012 : Decimal;
      WritenBudget@1100485013 : Decimal;
      HouseModel@1100485004 : Code[20];
      SchemeBudgetDistr@1100485005 : Code[10];
      Text000@1100485015 : TextConst 'DEU=darf nicht kleiner als die der vorigen Rate sein;ENU=may not be less than that of the previous installment;NLD=mag niet kleiner zijn dan die van de vorige termijn;SVE=f†r inte vara mindre „n f”reg†ende avbetalning';
      Text001@1100485024 : TextConst 'DEU=Budgets vor dem Referenzdatum %1 hinzugefgt. Referenzdatum wurde ge„ndert in (%2).;ENU="Budget inserted before the Reference Date %1, Reference Date is modified in %2. ";NLD="Budgetten voor de referentiedatum %1 toegevoegd, referentiedatum is gewijzigd in (%2). ";SVE="Budget infogad f”re referensdatumet %1. Referensdatum „ndras i %2. "';

    PROCEDURE FillModelSalesByDate@1100485006();
    VAR
      lvProjDevModelRec@1100485000 : Record 11012919;
      lvDate@1100485003 : Date;
      lvNextLineNo@1100485001 : Integer;
      lvSoldQuan@1100485004 : Decimal;
      lvDateExpression@1100485002 : Code[10];
    BEGIN
      WITH lvProjDevModelRec DO BEGIN
        SETRANGE("Project No.", "Investment Proposal"."Project No.");
        BudgetToDistrFilterRec.COPYFILTER("House Model", Model);
        IF NOT FIND('-') THEN
          EXIT;

        REPEAT
          IF (Quantity > 0) AND ("Prognosis No. of Sales" > 0) AND ("Starting Date Sales" <> 0D) THEN BEGIN
            lvNextLineNo := 1;
            lvSoldQuan := 0;
            lvDate := "Starting Date Sales";
            CASE "Prognosis by" OF
              "Prognosis by"::Week:  lvDateExpression := '<1W>';
              "Prognosis by"::Month: lvDateExpression := '<1M>';
            END;
            REPEAT
              lvDate := CALCDATE(lvDateExpression, lvDate);
              CLEAR(TmpModelSalesByDateRec);
              TmpModelSalesByDateRec."Project No." := Model;      //* House Model
              TmpModelSalesByDateRec."Line No." := lvNextLineNo;
              TmpModelSalesByDateRec.Date := CALCDATE(ProjDevSeupRec."Time to Consider", lvDate);
              TmpModelSalesByDateRec.Frequency :=
                ROUND((lvSoldQuan + "Prognosis No. of Sales"), 1, '<') - ROUND(lvSoldQuan, 1, '<');  //* Sales Quantity in period
              lvSoldQuan := lvSoldQuan + "Prognosis No. of Sales";
              TmpModelSalesByDateRec.Quantity := ROUND(lvSoldQuan, 1, '<');                          //* Cum. Sales Quantity
              IF (TmpModelSalesByDateRec.Quantity > Quantity) THEN BEGIN
                TmpModelSalesByDateRec.Frequency := TmpModelSalesByDateRec.Frequency - (TmpModelSalesByDateRec.Quantity - Quantity);
                TmpModelSalesByDateRec.Quantity := Quantity;
              END;
              TmpModelSalesByDateRec.INSERT;
              lvNextLineNo := lvNextLineNo + 1;
            UNTIL (lvSoldQuan >= Quantity);
          END;
        UNTIL NEXT = 0;
      END;
    END;

    PROCEDURE DeleteModelSalesByDate@1100485007();
    BEGIN
      TmpModelSalesByDateRec.RESET;
      TmpModelSalesByDateRec.DELETEALL;
    END;

    PROCEDURE DeleteRubricBudget@1100485001();
    VAR
      lvInvPropBudgetRec@1100485001 : Record 11012914;
    BEGIN
      WITH lvInvPropBudgetRec DO BEGIN
        SETRANGE("Project No.", "Investm. Proposal Rubric Line"."Project No.");
        SETRANGE(Version, "Investm. Proposal Rubric Line".Version);
        SETRANGE("Rubric Code", "Investm. Proposal Rubric Line"."Rubric Code");
        BudgetToDistrFilterRec.COPYFILTER("House Model", "House Model");
        DELETEALL;
        InvPropBudgetIsUpdated := TRUE;
      END;
    END;

    PROCEDURE ClearFields@1100485012();
    BEGIN
      HouseModel := '';
      CLEAR(FreqBudgetDistr);
      SchemeBudgetDistr := '';
      StartDateDistr := 0D;
      EndDateDistr := 0D;
    END;

    PROCEDURE DistributeBudget@1100485000(IBudgetToDistribute@1100485000 : Decimal);
    VAR
      lvDate@1100485002 : Date;
    BEGIN
      WITH "Investm. Proposal Rubric Line" DO BEGIN
        WritenBudget := 0;
        CASE "Budget Distribution" OF
          "Budget Distribution"::Linear:
            BEGIN
              NoOfPeriods := GetNoOfPeriodsLinear();
              FOR PeriodNo := 1 TO NoOfPeriods DO BEGIN
                lvDate := CalculateNewDateLinear(PeriodNo, lvDate);
                DetermineBudgetQuantity(lvDate);
                CalculateBudgetAmountLinear(IBudgetToDistribute);
                WriteRubricBudget("Investm. Proposal Rubric Line", lvDate, BudgetAmount);
                CheckDelayedBudget(lvDate, IBudgetToDistribute);
              END;
            END;
          "Budget Distribution"::Scheme:
            BEGIN
              NoOfPeriods := GetNoOfPeriodsScheme();
              UseTimePerc := GetTimePercUsedInScheme();
              BudgetDistrInstallRec.RESET;
              BudgetDistrInstallRec.SETRANGE(Scheme, SchemeBudgetDistr);  //* Only filter on this field allowed !
              IF BudgetDistrInstallRec.FIND('-') THEN BEGIN
                PeriodNo := 0;
                REPEAT
                  BudgetDistrInstallRec.SETFILTER("Installment No. Filter", '..%1', BudgetDistrInstallRec."Installment No.");
                  BudgetDistrInstallRec.CALCFIELDS("Cum. Percentage");
                  //
                  PeriodNo := PeriodNo + 1;
                  lvDate := CalculateNewDateScheme(PeriodNo, BudgetDistrInstallRec."Time Percentage");
                  DetermineBudgetQuantity(lvDate);
                  CalculateBudgetAmountScheme(IBudgetToDistribute);
                  WriteRubricBudget("Investm. Proposal Rubric Line", lvDate, BudgetAmount);
                  CheckDelayedBudget(lvDate, IBudgetToDistribute);
                UNTIL BudgetDistrInstallRec.NEXT = 0;
              END;
            END;
        END;
      END;
    END;

    PROCEDURE CheckDelayedBudget@1100485011(IDate@1100485003 : Date;IBudgetToDistribute@1100485000 : Decimal);
    VAR
      lvNextDate@1100485001 : Date;
      lvTimePerc@1100485002 : Decimal;
      lvPrevTimePerc@1100485004 : Decimal;
    BEGIN
      IF (HouseModel = '') OR (BudgetQuantity >= ProjDevModelRec.Quantity) OR
         ("Investm. Proposal Rubric Line".Type <> "Investm. Proposal Rubric Line".Type::Revenues)
      THEN
        EXIT;

      TmpModelSalesByDateRec.RESET;
      TmpModelSalesByDateRec.SETCURRENTKEY("Project No.", Version, Date);
      TmpModelSalesByDateRec.SETRANGE("Project No.", HouseModel);

      IF (PeriodNo < NoOfPeriods) THEN BEGIN
        CASE "Investm. Proposal Rubric Line"."Budget Distribution" OF
          "Investm. Proposal Rubric Line"."Budget Distribution"::Linear:
            BEGIN
              lvNextDate := CalculateNewDateLinear((PeriodNo+1), IDate);
            END;
          "Investm. Proposal Rubric Line"."Budget Distribution"::Scheme:
            BEGIN
              IF UseTimePerc THEN BEGIN
                lvPrevTimePerc := BudgetDistrInstallRec."Time Percentage";
                BudgetDistrInstallRec.NEXT;
                lvTimePerc := BudgetDistrInstallRec."Time Percentage";
                BudgetDistrInstallRec.NEXT(-1);
                BudgetDistrInstallRec.CALCFIELDS("Cum. Percentage");  //* Restore Cum. Perc of period
                IF (lvTimePerc <= lvPrevTimePerc) THEN
                  EXIT;
              END;
              lvNextDate := CalculateNewDateScheme((PeriodNo+1), lvTimePerc);
            END;
        END;
      END ELSE BEGIN
        TmpModelSalesByDateRec.FIND('+');
        lvNextDate := TmpModelSalesByDateRec.Date;
      END;

      TmpModelSalesByDateRec.SETRANGE(Date, (IDate + 1), lvNextDate);
      IF TmpModelSalesByDateRec.FIND('-') THEN BEGIN
        REPEAT
          BudgetQuantity := TmpModelSalesByDateRec.Quantity;
          QuanFactor := BudgetQuantity / ProjDevModelRec.Quantity;
          CASE "Investm. Proposal Rubric Line"."Budget Distribution" OF
            "Investm. Proposal Rubric Line"."Budget Distribution"::Linear:
              CalculateBudgetAmountLinear(IBudgetToDistribute);
            "Investm. Proposal Rubric Line"."Budget Distribution"::Scheme:
              CalculateBudgetAmountScheme(IBudgetToDistribute);
          END;
          BudgetQuantity := TmpModelSalesByDateRec.Frequency;
          WriteRubricBudget("Investm. Proposal Rubric Line", TmpModelSalesByDateRec.Date, BudgetAmount);
        UNTIL TmpModelSalesByDateRec.NEXT = 0;
      END;
    END;

    PROCEDURE GetNoOfPeriodsLinear@1100485004() : Integer;
    VAR
      lvDate@1100485003 : Date;
      lvPeriods@1100485004 : Integer;
    BEGIN
      lvPeriods := 0;
      lvDate := StartDateDistr;
      IF (CALCDATE(FreqBudgetDistr, lvDate) <= lvDate) THEN
        EXIT(1);

      REPEAT
        lvPeriods := lvPeriods + 1;
        lvDate := CALCDATE(FreqBudgetDistr, lvDate);
      UNTIL lvDate > EndDateDistr;
      IF ((lvDate - 1) = EndDateDistr) AND (StartDateDistr <> EndDateDistr) THEN
        lvPeriods := lvPeriods + 1;

      EXIT(lvPeriods);
    END;

    PROCEDURE GetTimePercUsedInScheme@1100485002() : Boolean;
    VAR
      lvBudgetDistrInstallRec@1100485003 : Record 11012908;
      lvPrevTimePerc@1100485000 : Decimal;
    BEGIN
      WITH lvBudgetDistrInstallRec DO BEGIN
        SETRANGE(Scheme, SchemeBudgetDistr);
        SETFILTER("Time Percentage", '<>%1', 0);
        IF NOT FIND('-') THEN
          EXIT(FALSE);

        RESET;
        SETRANGE(Scheme, SchemeBudgetDistr);
        lvPrevTimePerc := -1000;
        FIND('-');
        REPEAT
          IF ("Time Percentage" < lvPrevTimePerc) THEN
            FIELDERROR("Time Percentage", Text000);
          lvPrevTimePerc := "Time Percentage";
        UNTIL NEXT = 0;

        EXIT(TRUE);
      END;
    END;

    PROCEDURE GetNoOfPeriodsScheme@1100485010() : Integer;
    VAR
      lvBudgetDistrInstallRec@1100485003 : Record 11012908;
    BEGIN
      WITH lvBudgetDistrInstallRec DO BEGIN
        SETRANGE(Scheme, SchemeBudgetDistr);
        FIND('-');  //* If no installments present then error.

        EXIT(COUNT);
      END;
    END;

    PROCEDURE CalculateNewDateLinear@1100485008(IPeriodNo@1100485000 : Integer;IDate@1100485001 : Date) : Date;
    VAR
      lvDate@1100485002 : Date;
    BEGIN
      IF (IPeriodNo = 1) THEN
        lvDate := StartDateDistr
      ELSE
        lvDate := CALCDATE(FreqBudgetDistr, IDate);

      IF (lvDate < StartDateDistr) THEN
        lvDate := StartDateDistr;
      IF (lvDate > EndDateDistr) THEN
        lvDate := EndDateDistr;

      EXIT(lvDate);
    END;

    PROCEDURE CalculateNewDateScheme@1100485005(IPeriodNo@1100485000 : Integer;ITimePerc@1100485005 : Decimal) : Date;
    VAR
      lvDate@1100485003 : Date;
      lvPeriodLen@1100485004 : Decimal;
    BEGIN
      IF (NOT UseTimePerc) THEN BEGIN
        IF (IPeriodNo = 1) THEN
          lvDate := StartDateDistr
        ELSE BEGIN
          lvPeriodLen := (EndDateDistr - StartDateDistr + 1) / (NoOfPeriods - 1);
          lvDate := CALCDATE('<' + FORMAT(ROUND(((IPeriodNo - 1) * lvPeriodLen), 1), 0,'<Integer>') + 'D>', StartDateDistr);
        END;
      END ELSE BEGIN
        lvPeriodLen := EndDateDistr - StartDateDistr + 1;
        lvDate := CALCDATE('<' + FORMAT(ROUND(((ITimePerc/100) * lvPeriodLen), 1), 0, '<Integer>') + 'D>', StartDateDistr);
      END;


      IF (lvDate < StartDateDistr) THEN
        lvDate := StartDateDistr;
      IF (lvDate > EndDateDistr) THEN
        lvDate := EndDateDistr;

      EXIT(lvDate);
    END;

    PROCEDURE DetermineBudgetQuantity@1100485014(IDate@1100485000 : Date);
    BEGIN
      BudgetQuantity := 1;
      QuanFactor := 1;
      IF (HouseModel <> '') THEN BEGIN
        IF "Investm. Proposal Rubric Line".Type <> "Investm. Proposal Rubric Line".Type::Revenues THEN BEGIN
          BudgetQuantity := ProjDevModelRec.Quantity;
        END ELSE BEGIN
          TmpModelSalesByDateRec.RESET;
          TmpModelSalesByDateRec.SETCURRENTKEY("Project No.", Version, Date);
          TmpModelSalesByDateRec.SETRANGE("Project No.", HouseModel);
          TmpModelSalesByDateRec.SETRANGE(Date, 0D, IDate);
          IF TmpModelSalesByDateRec.FIND('+') THEN
            BudgetQuantity := TmpModelSalesByDateRec.Quantity
          ELSE
            BudgetQuantity := 0;
          QuanFactor := BudgetQuantity / ProjDevModelRec.Quantity;
        END;
      END;
    END;

    PROCEDURE CalculateBudgetAmountLinear@1100485015(IBudgetToDistr@1100485000 : Decimal);
    BEGIN
      //* First calculate the budget amount upto the period for the quantity in the periode, then subtract the already added
      //* budget amount. This gives the budget for this period (and quantity).
      BudgetAmount := IBudgetToDistr * QuanFactor * (PeriodNo / NoOfPeriods);
      BudgetAmount := ROUND((BudgetAmount - WritenBudget), 0.01);
    END;

    PROCEDURE CalculateBudgetAmountScheme@1100485013(IBudgetToDistr@1100485000 : Decimal);
    BEGIN
      //* See Linear
      BudgetAmount := IBudgetToDistr * QuanFactor * (BudgetDistrInstallRec."Cum. Percentage" / 100);
      BudgetAmount := ROUND((BudgetAmount - WritenBudget), 0.01);
    END;

    PROCEDURE DetermineRubricBudgNextLineNo@1100485003(IInvPropRubricRec@1100485002 : Record 11012913);
    VAR
      lvInvPropBudgetRec@1100485001 : Record 11012914;
    BEGIN
      WITH lvInvPropBudgetRec DO BEGIN
        SETRANGE("Project No.", IInvPropRubricRec."Project No.");
        SETRANGE(Version, IInvPropRubricRec.Version);
        SETRANGE("Rubric Code", IInvPropRubricRec."Rubric Code");
        IF NOT FIND('+') THEN
          "Line No." := 0;

        NextLineNo := "Line No." + 10000;
      END;
    END;

    PROCEDURE WriteRubricBudget@1100485009(IInvPropRubricRec@1100485002 : Record 11012913;IDate@1100485003 : Date;IBudget@1100485000 : Decimal);
    VAR
      lvInvPropBudgetRec@1100485001 : Record 11012914;
    BEGIN
      IF IBudget = 0 THEN
        EXIT;

      WITH lvInvPropBudgetRec DO BEGIN
        INIT;
        "Project No." := IInvPropRubricRec."Project No.";
        Version := IInvPropRubricRec.Version;
        "Rubric Code" := IInvPropRubricRec."Rubric Code";
        "Line No." := NextLineNo;
        Date := IDate;
        IF ("Rubric Budget to Distribute".Description <> '') THEN
          Description := "Rubric Budget to Distribute".Description
        ELSE
          Description := IInvPropRubricRec.Description;
        "House Model" := HouseModel;
        Quantity := 1;
        "Unit of Measure" := '';
        IF (BudgetQuantity = 1) THEN BEGIN
          Frequency := 1;
          VALIDATE("Unit Price", IBudget);
        END ELSE BEGIN
          Frequency := BudgetQuantity;
          VALIDATE("Unit Price", (IBudget / BudgetQuantity));
        END;
        IF (IInvPropRubricRec.Type <> IInvPropRubricRec.Type::Revenues) THEN
          "Budget Type" := "Budget Type"::Cost
        ELSE
          "Budget Type" := "Budget Type"::Revenue;
        INSERT;
        NextLineNo := NextLineNo + 10000;
        WritenBudget := WritenBudget + Amount;
        InvPropBudgetIsUpdated := TRUE;
        IF Date < LowestBudgetDate THEN
          LowestBudgetDate := Date;
      END;
    END;

    PROCEDURE SetFromSurchCostDistrAsPercRev@1100485016(IvInvPropRubricRec@1100485000 : Record 11012913);
    BEGIN
      //* Bij aanroep vanuit 'Update surcharges' (in tbel 11012913) voor het type "Costs to Distr. As Perc. Rev."
      //* Wordt dan altijd maar voor 1 rubriek gelijk verdeeld.

      gFromSurchCostDistrAsPercRev := TRUE;
      gInvPropRubricRec := IvInvPropRubricRec;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

