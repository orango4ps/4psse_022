OBJECT Codeunit 11128572 OIOUBL Check Sales Header
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVDK9.00 (13602);
  }
  PROPERTIES
  {
    TableNo=36;
    OnRun=BEGIN
            IF "EAN No." <> '' THEN BEGIN
              CheckOIOUBLProfile(Rec);
              ReadCompanyInfo;
              ReadGLSetup;
              CompanyInfo.VerifyAndSetOIOUBLInfo;

              IF NOT OIOUBLDocumentEncode.IsValidEANNo("EAN No.") THEN
                FIELDERROR("EAN No.",Text13606);

              IF "External Document No." = '' THEN
                ERROR(EmptyExtDocNoErr,"Document Type","No.");
              TESTFIELD("Payment Terms Code");
              IF "Document Type" <> "Document Type"::"Credit Memo" THEN
                IF "Payment Channel" = "Payment Channel"::" " THEN
                  FIELDERROR("Payment Channel",MusthaveavalueErr);

              IF "Document Type" IN ["Document Type"::Invoice,"Document Type"::Order] THEN
                TESTFIELD("Order Date");
              TESTFIELD("Bill-to Name");
              TESTFIELD("Bill-to Address");
              TESTFIELD("Bill-to City");
              TESTFIELD("Bill-to Post Code");

              TESTFIELD("Sell-to Contact");
              TESTFIELD("VAT Registration No.");
              OIOUBLDocumentEncode.IsValidCountryCode("Sell-to Country/Region Code");
              OIOUBLDocumentEncode.GetOIOUBLCountryRegionCode("Bill-to Country/Region Code");
              OIOUBLDocumentEncode.GetOIOUBLCountryRegionCode(CompanyInfo."Country/Region Code");

              OIOUBLDocumentEncode.GetOIOUBLCurrencyCode("Currency Code");
              CheckSalesLines(Rec);
            END;
          END;

  }
  CODE
  {
    VAR
      CompanyInfo@1101100001 : Record 79;
      GLSetup@1101100003 : Record 98;
      OIOUBLDocumentEncode@1101100004 : Codeunit 11128570;
      CompanyInfoRead@1101100000 : Boolean;
      GLSetupRead@1101100002 : Boolean;
      Text13606@1101100005 : TextConst 'ENU=does not contain a valid, 13-digit EAN no.;NOR=inneholder ikke et gyldig 13-tegns Gtin nr.;SVE=inneh†ller inte ett giltigt 13-teckens EAN nr.';
      EmptyExtDocNoErr@1101100007 : TextConst '@@@=Parameter 1 - document type, 2 - document no.;ENU="You must specify the External Document No. in Sales Header Document Type=''%1'', No.=''%2''.\\If you are using OIOUBL, this field is mandatory regardless of the value in the Ext. Doc. No. Mandatory field in Sales & Receivables Setup.";NOR="Du m† angi %1 i %2 %3=''%4''.\\Om du bruker OIOXML, kreves en verdi i feltet %7 i %8."';
      EmptyDescriptionErr@13608 : TextConst '@@@=Parameter 1 - document type, 2 - document no.;ENU=The %1 %2 contains lines in which the Type and the No. are specified, but the Description is empty. This is not allowed for an OIOUBL document which might be created from the posted document.;NOR=%1 %2 inneholder rader hvor %3 og %4 er spesifisert, men ikke %5. Dette er ikke tillatt i ett OIOXML dokument som kanskje vil opprettes fra det bokf›rte dokumentet.';
      EmptyFieldsQst@13609 : TextConst '@@@=Parameter 1 - document type, 2 - document no.;ENU=The %1 %2 contains lines in which either the Type, the No. or the Description is empty. Such lines will not be taken into account when creating an OIOUBL document.\Do you want to continue?;NOR=%1 %2 inneholder rader hvor enten %3, %4 eller %5 er tomme. V‘r klar over at slike rader ikke regnes med n†r OIOXML dokument opprettes.';
      Text13611@13611 : TextConst 'ENU=The posting has been interrupted to respect the warning.;NOR=bokf›ringen ble avbrutt pga advarselen.;SVE=Bokf”ringen har avbrutits pga varningen.';
      EmptyUnitOfMeasureErr@1101100009 : TextConst '@@@=Parameter 1 - document type, 2 - document no.;ENU=The %1 %2 contains lines in which the Unit of Measure field is empty. This is not permitted for an OIOUBL document that is to be created from the posted document.;NOR=%1 %2 inneholder rader hvor %3 er tomme. Dette er ikke tillatt for et OIOXML dokument som kanskje vil opprettes fra det bokf›rte dokumentet.';
      Text000@1101100010 : TextConst 'ENU=cannot be negative';
      Text001@1101100011 : TextConst 'ENU=The total Line Amount cannot be negative.';
      Text002@1101100012 : TextConst 'ENU=The total Line Discount Amount cannot be negative.';
      MusthaveavalueErr@1060000 : TextConst 'ENU=must have a value';

    LOCAL PROCEDURE ReadCompanyInfo@1101100003();
    BEGIN
      IF NOT CompanyInfoRead THEN BEGIN
        CompanyInfo.GET;
        CompanyInfoRead := TRUE;
      END;
    END;

    LOCAL PROCEDURE ReadGLSetup@1101100011();
    BEGIN
      IF NOT GLSetupRead THEN BEGIN
        GLSetup.GET;
        GLSetupRead := TRUE;
      END;
    END;

    LOCAL PROCEDURE CheckSalesLines@1060000(SalesHeader@1060000 : Record 36);
    VAR
      SalesLine@1060001 : Record 37;
      EmptyLineFound@1060002 : Boolean;
      TotalLineAmount@1060003 : Decimal;
      TotalLineDiscountAmount@106004 : Decimal;
    BEGIN
      EmptyLineFound := FALSE;
      TotalLineAmount := 0;
      TotalLineDiscountAmount := 0;
      WITH SalesLine DO BEGIN
        RESET;
        SETRANGE("Document Type",SalesHeader."Document Type");
        SETRANGE("Document No.",SalesHeader."No.");
        IF FINDSET THEN
          REPEAT
            IF (Type <> Type::" ") AND ("No." <> '') AND ("Unit of Measure" = '') THEN
              ERROR(EmptyUnitOfMeasureErr,"Document Type","Document No.");
            IF Description = '' THEN
              IF (Type <> Type::" ") AND ("No." <> '') THEN
                ERROR(EmptyDescriptionErr,"Document Type","Document No.");
            TotalLineAmount += "Line Amount";
            TotalLineDiscountAmount += "Line Discount Amount";
            IF (Type = Type::" ") OR ("No." = '')
            THEN
              EmptyLineFound := TRUE;
              IF "Line Discount %" < 0 THEN
                FIELDERROR("Line Discount %",Text000);
          UNTIL (NEXT = 0);
        IF TotalLineAmount < 0 THEN
          ERROR(Text001);
        IF TotalLineDiscountAmount < 0 THEN
          ERROR(Text002);

        IF EmptyLineFound THEN
          IF NOT CONFIRM(EmptyFieldsQst,TRUE,"Document Type","Document No.") THEN
            ERROR(Text13611);
      END;
    END;

    PROCEDURE CheckOIOUBLProfile@1060001(SalesHeader@1060000 : Record 36);
    VAR
      OIOUBLProfile@1060001 : Record 11128570;
      Customer@1060002 : Record 18;
    BEGIN
      Customer.GET(SalesHeader."Sell-to Customer No.");
      IF Customer."OIOUBL Profile Code Required" THEN BEGIN
        SalesHeader.TESTFIELD("OIOUBL Profile Code");
        OIOUBLProfile.GET(SalesHeader."OIOUBL Profile Code");
        OIOUBLProfile.TESTFIELD("Profile ID");
      END;
    END;

    BEGIN
    END.
  }
}

