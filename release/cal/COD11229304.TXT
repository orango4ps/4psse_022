OBJECT Codeunit 11229304 Create Demo Data HPT and HRA
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1100527000 : TextConst 'ENU="Dear %1,%4 %4 Thank you for requesting a demo account. % This demo account allows you to record hours via our portal and via the app.%4% The instructions for using the 4PS Time Sheet Portal are listed below: %4%4 1. Open a browser and navigate to: <a href=""https://hra-demo.4ps.nl"">https://hra-demo.4ps.nl</a>%4%4 2. Log in on the portal using the following data:%4User name: %2%4Password: %3%4% The instructions for using the 4PS Time Sheet App are listed below:%4 1. 1. Download the 4PS Time Sheet app from the app store (Android, iOS)%4 2. Log in on the app using the following data:%4    Address: <a href=""https://hra-demo.4ps.nl"">https://hra-demo.4ps.nl</a>%4User name: %2%4Password: %3%4%4Kind regards,%4 4PS"';
      Text001@1100527001 : TextConst 'ENU=Login details for the Hourportal and Hour Registration App';

    PROCEDURE CreateDemoUser@1100528100(Employee@1100525001 : Record 5200) : Boolean;
    VAR
      Password@1100525000 : Text;
      SiteMngmntHrsReplicCU@1100525003 : Codeunit 11012180;
      HourPortalUser@1100525002 : Record 11020453;
    BEGIN
      IF Employee.CAO = '' THEN BEGIN
        Employee.CAO := 'CAO';
        Employee.MODIFY(TRUE);
      END;

      Password := GeneratePassword;
      IF NOT GetHourPortalUser(HourPortalUser, Employee."No.") THEN BEGIN
        HourPortalUser.INIT;
        HourPortalUser.Login := Employee."No.";
        HourPortalUser.Company := COMPANYNAME;
        HourPortalUser."Employee No" := Employee."No.";
        HourPortalUser."License Type" := HourPortalUser."License Type"::Both;
        HourPortalUser.INSERT(TRUE);
      END;

      HourPortalUser.VALIDATE(Password, Password);
      HourPortalUser.MODIFY(TRUE);

      COMMIT;
      SiteMngmntHrsReplicCU.CreateTables(TRUE, FALSE, FALSE, FALSE, FALSE, FALSE);  // Create Emplyees

      SendEmail(HourPortalUser, Employee, Password);
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE GeneratePassword@1100528302() : Text;
    VAR
      StringFunctions@1100525000 : Codeunit 11012273;
      PasswordPlain@1100525001 : Text;
    BEGIN
      StringFunctions.GeneratePasswordPlainText(PasswordPlain, 8);
      EXIT(PasswordPlain);
    END;

    LOCAL PROCEDURE SendEmail@1100528101(HourPortalUser@1100528304 : Record 11020453;Employee@1100528305 : Record 5200;Password@1100525002 : Text);
    VAR
      MailTemplate@1100528301 : Record 11229450;
      MailTemplateManagement@1100528303 : Codeunit 11012622;
      SMTPMail@1100528307 : Codeunit 400;
      StringFunctions@1100528300 : Codeunit 11012273;
      BodyText@1100528100 : Text;
      SubjectText@1100528306 : Text;
      MailTemplateNo@1100528302 : Integer;
    BEGIN
      MailTemplateNo := 180;
      MailTemplateManagement.GetMailTemplate(MailTemplate, MailTemplateNo, '');
      MailTemplateManagement.GetLayoutTextSubstitutedSubjectAndTotalBody(MailTemplateNo, '', HourPortalUser, SubjectText, BodyText);
      IF (NOT MailTemplate."Use Mail Template Subject Text") THEN
        SubjectText := Text001;
      IF (SubjectText = '') THEN
        SubjectText := Text001;
      IF (BodyText = '') THEN
        BodyText := STRSUBSTNO(Text000, Employee."First Name", Employee."No.", Password, STRSUBSTNO('%1%2', StringFunctions.CarriageReturn, StringFunctions.LineFeed));

      SMTPMail.CreateMessage(MailTemplate."From Name", MailTemplate.From, Employee."Company E-Mail", SubjectText, BodyText, TRUE);
      SMTPMail.Send;
    END;

    LOCAL PROCEDURE GetHourPortalUser@1100525001(VAR HourPortalUser@1100525000 : Record 11020453;EmployeeNo@1100525001 : Code[20]) : Boolean;
    BEGIN
      HourPortalUser.SETRANGE("Employee No", EmployeeNo);
      EXIT(HourPortalUser.FINDFIRST);
    END;

    BEGIN
    END.
  }
}

