OBJECT Report 11012401 Frost Out Request
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Anfrage frostbedingten Arbeitsausfalls;
               ENU=Frost Out Request;
               NLD=Aanvraag vorstverlet;
               NOR=Frost Out-foresp›rsel;
               SVE=Frost Out-f”rfr†gan];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  CompanyInformation.GET;
                  Now := CURRENTDATETIME;
                  BankOrGiroAccountNo := CompanyInformation."Bank Account No.";
                  IF (BankOrGiroAccountNo = '') THEN
                    BankOrGiroAccountNo := CompanyInformation."Giro No.";

                  HumanResourcesSetup.GET;
                END;

  }
  DATASET
  {
    { 1723;    ;DataItem;                    ;
               DataItemTable=Table11020671;
               DataItemTableView=SORTING(Employee No.,Year,Start Week);
               PrintOnlyIfDetail=Yes;
               OnPreDataItem=BEGIN
                               FileName := STRSUBSTNO('%1%2-%3', HumanResourcesSetup.GetFrostOutOutputDirectory,
                                 "Frost Out Request".TABLECAPTION,
                                 FORMAT(Now, 0, '<year4><Month,2><Day,2>-<hours24,2><minutes,2><seconds,2>'));
                             END;

               OnAfterGetRecord=BEGIN
                                  Weekcount := 0;
                                  DocCount += 1;
                                  Employee.GET("Employee No.");

                                  CreateNewDoc;
                                  CreateDocument(DocumentNo);

                                  TotalNormHours := "Norm Hours Monday" + "Norm Hours Tuesday" +
                                    "Norm Hours Wednesday" + "Norm Hours Thursday" +
                                    "Norm Hours Friday" + "Norm Hours Saturday" +
                                    "Norm Hours Sunday";

                                  FillHeaderBookmarks;

                                  "Print Date Time" := Now;
                                  MODIFY(TRUE);
                                END;

               ReqFilterFields=Employee No.,Year,Start Week }

    { 3621;1   ;DataItem;                    ;
               DataItemTable=Table11020672;
               DataItemTableView=SORTING(Employee No.,Start Year,Start Week,Year,Week);
               PrintOnlyIfDetail=Yes;
               OnPreDataItem=BEGIN
                               SETRANGE("Start Year", "Frost Out Request".Year);
                               SETRANGE("Start Week", "Frost Out Request"."Start Week");
                             END;

               OnAfterGetRecord=BEGIN
                                  Weekcount += 1;

                                  TotalHoursWorked := "Hours Worked Monday" +
                                    "Hours Worked Tuesday" + "Hours Worked Wednesday" +
                                    "Hours Worked Thursday" + "Hours Worked Friday" +
                                    "Hours Worked Saturday" + "Hours Worked Sunday";
                                  TotalHoursNotWorked := "Hours Not Worked Monday" +
                                    "Hours Not Worked Tuesday" + "Hours Not Worked Wednesday" +
                                    "Hours Not Worked Thursday" + "Hours Not Worked Friday" +
                                    "Hours Not Worked Saturday" + "Hours Not Worked Sunday";

                                  FillLineBookmarks;
                                END;

               OnPostDataItem=VAR
                                FileName2@1100528700 : Text[250];
                                DocumentServiceURL@1100528701 : Text;
                              BEGIN
                                CloseDoc(FileName2, DocumentServiceURL);
                                UpdateDocumentFileName(DocumentNo, FileName2, DocumentServiceURL);
                              END;

               DataItemLink=Employee No.=FIELD(Employee No.),
                            Start Year=FIELD(Year),
                            Start Week=FIELD(Start Week) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      CompanyInformation@1100528300 : Record 79;
      HumanResourcesSetup@1100528301 : Record 5218;
      Employee@1100528302 : Record 5200;
      BankOrGiroAccountNo@1100528303 : Code[20];
      TotalHoursWorked@1100528304 : Decimal;
      TotalHoursNotWorked@1100528305 : Decimal;
      TotalNormHours@1100528306 : Decimal;
      Weekcount@1210190004 : Integer;
      DocCount@1210190003 : Integer;
      FileName@1210190002 : Text[250];
      WordAutomation@1210190001 : Codeunit 11012400;
      GranulePermissionManagment@1100528701 : Codeunit 11012279;
      Now@1210190000 : DateTime;
      DocumentNo@1100528700 : Code[20];

    LOCAL PROCEDURE FillHeaderBookmarks@1210190002();
    BEGIN
      WordAutomation.FillBookmark('bedrijfsnaam', CompanyInformation.Name, FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark('loonheffingennr', CompanyInformation."Income Tax No.", FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark('aansluitingsnummer', CompanyInformation."Affiliation No.", FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark('rekeningnummer', BankOrGiroAccountNo, FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark('contactpersoon', HumanResourcesSetup."Frost Out Contact",
        FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark('telefoonnummer', HumanResourcesSetup."Contact Phone No.", FALSE, FALSE, FALSE, FALSE);

      WordAutomation.FillBookmark('voorEnAchternaam', Employee."Full Name", FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark('BSN', Employee."Social Security No.", FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark('functiewerknemer', Employee."Job Title", FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark('ingangsdatumdienst', FORMAT(Employee."Employment Date"), FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark('aardwerkzaamheden', '', FALSE, FALSE, FALSE, FALSE);


      WordAutomation.FillBookmark('ma', STRSUBSTNO('%1', "Frost Out Request"."Norm Hours Monday"),
        FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark('di', STRSUBSTNO('%1', "Frost Out Request"."Norm Hours Tuesday"),
        FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark('wo', STRSUBSTNO('%1', "Frost Out Request"."Norm Hours Wednesday"),
        FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark('do', STRSUBSTNO('%1', "Frost Out Request"."Norm Hours Thursday"),
        FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark('vr', STRSUBSTNO('%1', "Frost Out Request"."Norm Hours Friday"),
        FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark('za', STRSUBSTNO('%1', "Frost Out Request"."Norm Hours Saturday"),
        FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark('zo', STRSUBSTNO('%1', "Frost Out Request"."Norm Hours Sunday"),
        FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark('totaal', STRSUBSTNO('%1', TotalNormHours), FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark('BSN1', Employee."Social Security No.", FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark('FullName', Employee."Full Name" + ' (' + Employee."No." + ')',
        FALSE, FALSE, FALSE, FALSE);
    END;

    LOCAL PROCEDURE FillLineBookmarks@1210190005();
    BEGIN
      FillLineBookmarksGeneric('ma', "Frost Out Request Line".Year, "Frost Out Request Line".Week,
        "Frost Out Request Line"."Hours Worked Monday", "Frost Out Request Line"."Hours Not Worked Monday",
        "Frost Out Request Line"."Post Code Monday", "Frost Out Request Line"."Reason Code Monday");
      FillLineBookmarksGeneric('di', "Frost Out Request Line".Year, "Frost Out Request Line".Week,
        "Frost Out Request Line"."Hours Worked Tuesday", "Frost Out Request Line"."Hours Not Worked Tuesday",
        "Frost Out Request Line"."Post Code Tuesday", "Frost Out Request Line"."Reason Code Tuesday");
      FillLineBookmarksGeneric('wo', "Frost Out Request Line".Year, "Frost Out Request Line".Week,
        "Frost Out Request Line"."Hours Worked Wednesday", "Frost Out Request Line"."Hours Not Worked Wednesday",
        "Frost Out Request Line"."Post Code Wednesday", "Frost Out Request Line"."Reason Code Wednesday");
      FillLineBookmarksGeneric('do', "Frost Out Request Line".Year, "Frost Out Request Line".Week,
        "Frost Out Request Line"."Hours Worked Thursday", "Frost Out Request Line"."Hours Not Worked Thursday",
        "Frost Out Request Line"."Post Code Thursday", "Frost Out Request Line"."Reason Code Thursday");
      FillLineBookmarksGeneric('vr', "Frost Out Request Line".Year, "Frost Out Request Line".Week,
        "Frost Out Request Line"."Hours Worked Friday", "Frost Out Request Line"."Hours Not Worked Friday",
        "Frost Out Request Line"."Post Code Friday", "Frost Out Request Line"."Reason Code Friday");
      FillLineBookmarksGeneric('za', "Frost Out Request Line".Year, "Frost Out Request Line".Week,
        "Frost Out Request Line"."Hours Worked Saturday", "Frost Out Request Line"."Hours Not Worked Saturday",
        "Frost Out Request Line"."Post Code Saturday", "Frost Out Request Line"."Reason Code Saturday");
      FillLineBookmarksGeneric('zo', "Frost Out Request Line".Year, "Frost Out Request Line".Week,
        "Frost Out Request Line"."Hours Worked Sunday", "Frost Out Request Line"."Hours Not Worked Sunday",
        "Frost Out Request Line"."Post Code Sunday", "Frost Out Request Line"."Reason Code Sunday");

      WordAutomation.FillBookmark(STRSUBSTNO('wtot%1', Weekcount), STRSUBSTNO('%1', TotalHoursWorked),
        FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark(STRSUBSTNO('ntot%1', Weekcount), STRSUBSTNO('%1', TotalHoursNotWorked),
        FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark(STRSUBSTNO('tot%1', Weekcount), STRSUBSTNO('%1', TotalHoursWorked + TotalHoursNotWorked),
        FALSE, FALSE, FALSE, FALSE);
    END;

    LOCAL PROCEDURE CloseDoc@1210190003(VAR FileName2@1100528700 : Text[250];VAR DocumentServiceURL@1100528702 : Text);
    BEGIN
      FileName2 := STRSUBSTNO('%1-%2', FileName, DocCount);
      DocumentServiceURL := '';
      WordAutomation.SaveAsWithExtension(
        GetStorageType, FileName2, WordAutomation.DocumentExtension);
      IF NOT (HumanResourcesSetup."Frost Out File Storage" = HumanResourcesSetup."Frost Out File Storage"::"File System on Premise") THEN
        DocumentServiceURL := FileName2;
    END;

    LOCAL PROCEDURE CreateNewDoc@1210190004();
    BEGIN
      WordAutomation.Initialise;
      WordAutomation.Open(TRUE, GetStorageType, HumanResourcesSetup."Request Template", FALSE);
    END;

    LOCAL PROCEDURE FillLineBookmarksGeneric@1210190001(Day@1210190007 : Text[2];YearNo@1210190001 : Integer;WeekNo@1210190002 : Integer;HoursWorked@1210190003 : Decimal;HoursNotWorked@1210190004 : Decimal;PostCode@1210190005 : Code[20];ReasonCode@1210190006 : Code[10]);
    BEGIN
      WordAutomation.FillBookmark(STRSUBSTNO('week%1', Weekcount), STRSUBSTNO('%1-%2', YearNo, WeekNo),
        TRUE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark(STRSUBSTNO('g%1%2', Day, Weekcount), STRSUBSTNO('%1', HoursWorked),
        FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark(STRSUBSTNO('n%1%2', Day, Weekcount), STRSUBSTNO('%1', HoursNotWorked),
        FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark(STRSUBSTNO('t%1%2', Day, Weekcount), STRSUBSTNO('%1', HoursWorked + HoursNotWorked),
        FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark(STRSUBSTNO('p%1%2', Day, Weekcount), PostCode, FALSE, FALSE, FALSE, FALSE);
      WordAutomation.FillBookmark(STRSUBSTNO('r%1%2', Day, Weekcount), ReasonCode, FALSE, FALSE, FALSE, FALSE);
    END;

    LOCAL PROCEDURE CreateDocument@1100528700(VAR DocumentNo@1100528702 : Code[20]);
    VAR
      DocumentProperties@1100528703 : Record 11012746;
      RecRef@1100528700 : RecordRef;
      DocumentLinkRID@1100528701 : ARRAY [5] OF RecordID;
      DocumentLinkManagement@1100528706 : Codeunit 11012401;
    BEGIN
      IF NOT GranulePermissionManagment.PermissionDocumentMgtExtra THEN
        EXIT;

      DocumentNo := '';

      "Frost Out Request".CALCFIELDS("Employee Name");

      DocumentProperties.INIT;
      DocumentProperties.Description :=
        STRSUBSTNO('%1 %2 %3/%4',
          "Frost Out Request".TABLECAPTION,
          "Frost Out Request"."Employee Name",
          "Frost Out Request".Year,
          "Frost Out Request"."Start Week");
      DocumentProperties."External Document" := FALSE;
      DocumentProperties."Storage Type" := GetStorageType;
      DocumentProperties.INSERT(TRUE);

      RecRef.GETTABLE("Frost Out Request");
      DocumentLinkRID[2] := RecRef.RECORDID;
      DocumentLinkManagement.CreateDocumentLink(DocumentProperties, DocumentLinkRID);

      DocumentNo := DocumentProperties."No.";
    END;

    LOCAL PROCEDURE UpdateDocumentFileName@1100528701(DocumentNo@1100528700 : Code[20];FileName@1100528701 : Text[250];DocumentServiceURL@1100528703 : Text);
    VAR
      DocumentProperties@1100528702 : Record 11012746;
    BEGIN
      IF NOT GranulePermissionManagment.PermissionDocumentMgtExtra THEN
        EXIT;

      DocumentProperties.GET(DocumentNo);
      IF HumanResourcesSetup."Frost Out File Storage" = HumanResourcesSetup."Frost Out File Storage"::"File System on Premise" THEN BEGIN
        IF FileName <> '' THEN
          DocumentProperties.VALIDATE(File, FileName)
      END ELSE
        IF DocumentServiceURL <> '' THEN
          DocumentProperties.VALIDATE("Document Service URL", DocumentServiceURL);
      DocumentProperties.MODIFY;
    END;

    LOCAL PROCEDURE GetStorageType@1100528401() StorageType : Integer;
    BEGIN
      StorageType := HumanResourcesSetup.GetFrostOutDocumentStorageType;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

