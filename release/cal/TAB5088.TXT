OBJECT Table 5088 Profile Questionnaire Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW113.00,4PS14.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Profile Questionnaire Code,Description;
    OnDelete=VAR
               Rating@1000 : Record 11072059;
               ProfileQuestionnaireLine@1001 : Record 5088;
             BEGIN
               CALCFIELDS("No. of Contacts");
               TESTFIELD("No. of Contacts",0);

               Rating.SETRANGE("Rating Profile Quest. Code","Profile Questionnaire Code");
               Rating.SETRANGE("Rating Profile Quest. Line No.","Line No.");
               IF NOT Rating.ISEMPTY THEN
                 ERROR(Text002);

               Rating.RESET;
               Rating.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
               Rating.SETRANGE("Profile Questionnaire Line No.","Line No.");
               IF NOT Rating.ISEMPTY THEN
                 ERROR(Text003);

               IF Type = Type::Question THEN BEGIN
                 ProfileQuestionnaireLine.GET("Profile Questionnaire Code","Line No.");
                 IF (ProfileQuestionnaireLine.NEXT <> 0) AND
                    (ProfileQuestionnaireLine.Type = ProfileQuestnLine.Type::Answer)
                 THEN
                   ERROR(Text004);
               END;
             END;

    CaptionML=[ENU=Profile Questionnaire Line;
               NOR=Profilsp›rreskjema - linje];
    LookupPageID=Page11072091;
  }
  FIELDS
  {
    { 1   ;   ;Profile Questionnaire Code;Code20  ;TableRelation="Profile Questionnaire Header";
                                                   CaptionML=[ENU=Profile Questionnaire Code;
                                                              NOR=Profilsp›rreskjema - kode;
                                                              SVE=Profilfr†geformul„r kod] }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 3   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                CASE Type OF
                                                                  Type::Question:
                                                                    BEGIN
                                                                      CALCFIELDS("No. of Contacts");
                                                                      TESTFIELD("No. of Contacts",0);
                                                                      TESTFIELD("From Value",0);
                                                                      TESTFIELD("To Value",0);
                                                                    END;
                                                                  Type::Answer:
                                                                    BEGIN
                                                                      TESTFIELD("Multiple Answers",FALSE);
                                                                      TESTFIELD("Auto Contact Classification",FALSE);
                                                                      TESTFIELD("Customer Class. Field",0);
                                                                      TESTFIELD("Vendor Class. Field",0);
                                                                      TESTFIELD("Contact Class. Field",0);
                                                                      TESTFIELD("Starting Date Formula",ZeroDateFormula);
                                                                      TESTFIELD("Ending Date Formula",ZeroDateFormula);
                                                                      TESTFIELD("Classification Method",0);
                                                                      TESTFIELD("Sorting Method",0);
                                                                      TESTFIELD("No. of Decimals",0);
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              NOR=Type;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=[ENU=Question,Answer;
                                                                    NOR=Sp›rsm†l,Svar;
                                                                    SVE=Fr†ga,Svar];
                                                   OptionString=Question,Answer }
    { 4   ;   ;Description         ;Text250       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning];
                                                   NotBlank=Yes }
    { 5   ;   ;Multiple Answers    ;Boolean       ;OnValidate=BEGIN
                                                                IF "Multiple Answers" THEN
                                                                  TESTFIELD(Type,Type::Question);
                                                              END;

                                                   CaptionML=[ENU=Multiple Answers;
                                                              NOR=Flere svar;
                                                              SVE=Flera svar] }
    { 6   ;   ;Auto Contact Classification;Boolean;OnValidate=BEGIN
                                                                IF "Auto Contact Classification" THEN
                                                                  TESTFIELD(Type,Type::Question)
                                                                ELSE BEGIN
                                                                  TESTFIELD("Customer Class. Field","Customer Class. Field"::" ");
                                                                  TESTFIELD("Vendor Class. Field","Vendor Class. Field"::" ");
                                                                  TESTFIELD("Contact Class. Field","Contact Class. Field"::" ");
                                                                  TESTFIELD("Starting Date Formula",ZeroDateFormula);
                                                                  TESTFIELD("Ending Date Formula",ZeroDateFormula);
                                                                  TESTFIELD("Classification Method","Classification Method"::" ");
                                                                  TESTFIELD("Sorting Method","Sorting Method"::" ");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Auto Contact Classification;
                                                              NOR=Automatisk kontaktklassifisering] }
    { 7   ;   ;Customer Class. Field;Option       ;OnValidate=BEGIN
                                                                IF "Customer Class. Field" <> "Customer Class. Field"::" " THEN BEGIN
                                                                  TESTFIELD(Type,Type::Question);
                                                                  CLEAR("Vendor Class. Field");
                                                                  CLEAR("Contact Class. Field");
                                                                  IF "Classification Method" = "Classification Method"::" " THEN
                                                                    "Classification Method" := "Classification Method"::"Defined Value";
                                                                END ELSE
                                                                  ResetFields;
                                                              END;

                                                   CaptionML=[ENU=Customer Class. Field;
                                                              NOR=Kundeklass.felt];
                                                   OptionCaptionML=[ENU=" ,Sales (LCY),Profit (LCY),Sales Frequency (Invoices/Year),Avg. Invoice Amount (LCY),Discount (%),Avg. Overdue (Day)";
                                                                    NOR=" ,Salg (NOK),Bruttofortjeneste (NOK),Salgshyppiget (fakturaer/†r),Gj.snittl. fakturabel›p (NOK),Rabatt (%),Gj.snittl. forsinkelse (dag)"];
                                                   OptionString=[ ,Sales (LCY),Profit (LCY),Sales Frequency (Invoices/Year),Avg. Invoice Amount (LCY),Discount (%),Avg. Overdue (Day)] }
    { 8   ;   ;Vendor Class. Field ;Option        ;OnValidate=BEGIN
                                                                IF "Vendor Class. Field" <> "Vendor Class. Field"::" " THEN BEGIN
                                                                  TESTFIELD(Type,Type::Question);
                                                                  CLEAR("Customer Class. Field");
                                                                  CLEAR("Contact Class. Field");
                                                                  IF "Classification Method" = "Classification Method"::" " THEN
                                                                    "Classification Method" := "Classification Method"::"Defined Value";
                                                                END ELSE
                                                                  ResetFields;
                                                              END;

                                                   CaptionML=[ENU=Vendor Class. Field;
                                                              NOR=Leverand›rklass.felt];
                                                   OptionCaptionML=[ENU=" ,Purchase (LCY),Purchase Frequency (Invoices/Year),Avg. Ticket Size (LCY),Discount (%),Avg. Overdue (Day)";
                                                                    NOR=" ,Kj›p (NOK),Kj›pshyppighet (fakturaer/†r),Gj.snittlig st›rrelse (NOK),Rabatt (%),Gj.snittl. forsinkelse (dag)"];
                                                   OptionString=[ ,Purchase (LCY),Purchase Frequency (Invoices/Year),Avg. Ticket Size (LCY),Discount (%),Avg. Overdue (Day)] }
    { 9   ;   ;Contact Class. Field;Option        ;OnValidate=VAR
                                                                Rating@1000 : Record 11072059;
                                                              BEGIN
                                                                IF xRec."Contact Class. Field" = "Contact Class. Field"::Rating THEN BEGIN
                                                                  Rating.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
                                                                  Rating.SETRANGE("Profile Questionnaire Line No.","Line No.");
                                                                  IF Rating.FINDFIRST THEN
                                                                    IF CONFIRM(Text000,FALSE) THEN
                                                                      Rating.DELETEALL
                                                                    ELSE
                                                                      ERROR(Text001,FIELDCAPTION("Contact Class. Field"));
                                                                END;

                                                                IF "Contact Class. Field" <> "Contact Class. Field"::" " THEN BEGIN
                                                                  TESTFIELD(Type,Type::Question);
                                                                  CLEAR("Customer Class. Field");
                                                                  CLEAR("Vendor Class. Field");
                                                                  IF ("Classification Method" = "Classification Method"::" ") OR
                                                                     ("Contact Class. Field" = "Contact Class. Field"::Rating)
                                                                  THEN BEGIN
                                                                    "Classification Method" := "Classification Method"::"Defined Value";
                                                                    "Sorting Method" := "Sorting Method"::" ";
                                                                  END;
                                                                  IF "Contact Class. Field" = "Contact Class. Field"::Rating THEN BEGIN
                                                                    CLEAR("Starting Date Formula");
                                                                    CLEAR("Ending Date Formula");
                                                                  END;
                                                                END ELSE
                                                                  ResetFields;
                                                              END;

                                                   CaptionML=[ENU=Contact Class. Field;
                                                              NOR=Kontaktklass.felt];
                                                   OptionCaptionML=[ENU=" ,Interaction Quantity,Interaction Frequency (No./Year),Avg. Interaction Cost (LCY),Avg. Interaction Duration (Min.),Opportunity Won (%),Rating";
                                                                    NOR=" ,Samhandlingsantall,Samhandl.hyppighet (ant./†r),Gj.sn. samhandl.kost (NOK),Gj.sn. samhandl.varighet (min.),Salgsmulighetssuksess (%),Rangering"];
                                                   OptionString=[ ,Interaction Quantity,Interaction Frequency (No./Year),Avg. Interaction Cost (LCY),Avg. Interaction Duration (Min.),Opportunity Won (%),Rating] }
    { 10  ;   ;Starting Date Formula;DateFormula  ;OnValidate=BEGIN
                                                                IF FORMAT("Starting Date Formula") <> '' THEN
                                                                  TESTFIELD(Type,Type::Question);
                                                              END;

                                                   CaptionML=[ENU=Starting Date Formula;
                                                              NOR=Startdatoformel] }
    { 11  ;   ;Ending Date Formula ;DateFormula   ;OnValidate=BEGIN
                                                                IF FORMAT("Ending Date Formula") <> '' THEN
                                                                  TESTFIELD(Type,Type::Question);
                                                              END;

                                                   CaptionML=[ENU=Ending Date Formula;
                                                              NOR=Sluttdatoformel] }
    { 12  ;   ;Classification Method;Option       ;OnValidate=BEGIN
                                                                IF "Classification Method" <> "Classification Method"::" " THEN BEGIN
                                                                  TESTFIELD(Type,Type::Question);
                                                                  IF "Classification Method" <> "Classification Method"::"Defined Value" THEN
                                                                    "Sorting Method" := ProfileQuestnLine."Sorting Method"::Descending
                                                                  ELSE
                                                                    "Sorting Method" := ProfileQuestnLine."Sorting Method"::" ";
                                                                END ELSE
                                                                  "Sorting Method" := ProfileQuestnLine."Sorting Method"::" ";
                                                              END;

                                                   CaptionML=[ENU=Classification Method;
                                                              NOR=Klassifiseringsm†te];
                                                   OptionCaptionML=[ENU=" ,Defined Value,Percentage of Value,Percentage of Contacts";
                                                                    NOR=" ,Definert verdi,Prosent av verdi,Prosent av kontakter"];
                                                   OptionString=[ ,Defined Value,Percentage of Value,Percentage of Contacts] }
    { 13  ;   ;Sorting Method      ;Option        ;OnValidate=BEGIN
                                                                IF "Sorting Method" <> "Sorting Method"::" " THEN
                                                                  TESTFIELD(Type,Type::Question);
                                                              END;

                                                   CaptionML=[ENU=Sorting Method;
                                                              NOR=Sorteringsmetode;
                                                              SVE=Sorteringsmetod];
                                                   OptionCaptionML=[ENU=" ,Descending,Ascending";
                                                                    NOR=" ,Stigende,Synkende"];
                                                   OptionString=[ ,Descending,Ascending] }
    { 14  ;   ;From Value          ;Decimal       ;OnValidate=BEGIN
                                                                IF "From Value" <> 0 THEN
                                                                  TESTFIELD(Type,Type::Answer);
                                                              END;

                                                   CaptionML=[ENU=From Value;
                                                              NOR=Fra verdi;
                                                              SVE=Fr†n v„rde];
                                                   DecimalPlaces=0:25;
                                                   BlankZero=Yes }
    { 15  ;   ;To Value            ;Decimal       ;OnValidate=BEGIN
                                                                IF "To Value" <> 0 THEN
                                                                  TESTFIELD(Type,Type::Answer);
                                                              END;

                                                   CaptionML=[ENU=To Value;
                                                              NOR=Til verdi;
                                                              SVE=Till v„rde];
                                                   DecimalPlaces=0:25;
                                                   BlankZero=Yes }
    { 16  ;   ;No. of Contacts     ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Contact Profile Answer" WHERE (Profile Questionnaire Code=FIELD(Profile Questionnaire Code),
                                                                                                     Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=No. of Contacts;
                                                              NOR=Ant. kontakter];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 17  ;   ;Priority            ;Option        ;InitValue=Normal;
                                                   OnValidate=VAR
                                                                ContProfileAnswer@1000 : Record 5089;
                                                              BEGIN
                                                                TESTFIELD(Type,Type::Answer);
                                                                ContProfileAnswer.SETCURRENTKEY("Profile Questionnaire Code","Line No.");
                                                                ContProfileAnswer.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
                                                                ContProfileAnswer.SETRANGE("Line No.","Line No.");
                                                                ContProfileAnswer.MODIFYALL("Answer Priority",Priority);
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Priority;
                                                              NOR=Prioritet;
                                                              SVE=Prioritet];
                                                   OptionCaptionML=[ENU=Very Low (Hidden),Low,Normal,High,Very High;
                                                                    NOR=Sv‘rt lav (skjult),Lav,Normal,H›y,Sv‘rt h›y;
                                                                    SVE=Mycket l†g (g”md),L†g,Normal,H”g,Mycket h”g];
                                                   OptionString=Very Low (Hidden),Low,Normal,High,Very High }
    { 18  ;   ;No. of Decimals     ;Integer       ;OnValidate=BEGIN
                                                                IF "No. of Decimals" <> 0 THEN
                                                                  TESTFIELD(Type,Type::Question);
                                                              END;

                                                   CaptionML=[ENU=No. of Decimals;
                                                              NOR=Ant. desimaler];
                                                   MinValue=-25;
                                                   MaxValue=25 }
    { 19  ;   ;Min. % Questions Answered;Decimal  ;OnValidate=BEGIN
                                                                IF "Min. % Questions Answered" <> 0 THEN BEGIN
                                                                  TESTFIELD(Type,Type::Question);
                                                                  TESTFIELD("Contact Class. Field","Contact Class. Field"::Rating);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Min. % Questions Answered;
                                                              NOR=Min. av spm. som m† besv. (%)];
                                                   DecimalPlaces=0:0;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 9501;   ;Wizard Step         ;Option        ;CaptionML=[ENU=Wizard Step;
                                                              NOR=Trinn i veiviser;
                                                              SVE=Guidesteg];
                                                   OptionCaptionML=[ENU=" ,1,2,3,4,5,6";
                                                                    NOR=" ,1,2,3,4,5,6";
                                                                    SVE=" ,1,2,3,4,5,6"];
                                                   OptionString=[ ,1,2,3,4,5,6];
                                                   Editable=No }
    { 9502;   ;Interval Option     ;Option        ;CaptionML=[ENU=Interval Option;
                                                              NOR=Intervallalternativ];
                                                   OptionCaptionML=[ENU=Minimum,Maximum,Interval;
                                                                    NOR=Minimum,Maksimum,Intervall];
                                                   OptionString=Minimum,Maximum,Interval }
    { 9503;   ;Answer Option       ;Option        ;CaptionML=[ENU=Answer Option;
                                                              NOR=Svaralternativ];
                                                   OptionCaptionML=[ENU=HighLow,ABC,Custom;
                                                                    NOR=HighLow,ABC,Egendefinert];
                                                   OptionString=HighLow,ABC,Custom }
    { 9504;   ;Answer Description  ;Text250       ;CaptionML=[ENU=Answer Description;
                                                              NOR=Svarbeskrivelse] }
    { 9505;   ;Wizard From Value   ;Decimal       ;OnValidate=BEGIN
                                                                IF "From Value" <> 0 THEN
                                                                  TESTFIELD(Type,Type::Answer);
                                                              END;

                                                   CaptionML=[ENU=Wizard From Value;
                                                              NOR=Fra verdi for veiviser];
                                                   DecimalPlaces=0:25;
                                                   BlankZero=Yes }
    { 9506;   ;Wizard To Value     ;Decimal       ;OnValidate=BEGIN
                                                                IF "To Value" <> 0 THEN
                                                                  TESTFIELD(Type,Type::Answer);
                                                              END;

                                                   CaptionML=[ENU=Wizard To Value;
                                                              NOR=Til verdi for veiviser];
                                                   DecimalPlaces=0:25;
                                                   BlankZero=Yes }
    { 9707;   ;Wizard From Line No.;Integer       ;OnValidate=BEGIN
                                                                IF "To Value" <> 0 THEN
                                                                  TESTFIELD(Type,Type::Answer);
                                                              END;

                                                   CaptionML=[ENU=Wizard From Line No.;
                                                              NOR=Fra linjenummer for veiviser];
                                                   BlankZero=Yes }
  }
  KEYS
  {
    {    ;Profile Questionnaire Code,Line No.     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;Brick               ;Type,Description,Priority,Multiple Answers,Auto Contact Classification,No. of Contacts }
  }
  CODE
  {
    VAR
      ProfileQuestnLine@1000 : Record 5088;
      TempProfileQuestionnaireLine@1007 : TEMPORARY Record 5088;
      ZeroDateFormula@1001 : DateFormula;
      Text000@1002 : TextConst 'ENU=Do you want to delete the rating values?;NOR=Vil du slette rangeringsverdiene?';
      Text001@1003 : TextConst 'ENU=%1 cannot be changed until the rating value is deleted.;NOR=%1 kan ikke endres f›r rangeringsverdien er slettet.';
      Text002@1004 : TextConst 'ENU=You cannot delete this line because one or more questions are depending on it.;NOR=Du kan ikke slette denne linjen fordi det er knyttet ett eller flere sp›rsm†l til den.';
      Text003@1005 : TextConst 'ENU=You cannot delete this line because one or more rating values exists.;NOR=Du kan ikke slette denne linjen fordi det finnes ‚n eller flere rangeringsverdier.';
      Text004@1006 : TextConst 'ENU=You cannot delete this question while answers exists.;NOR=Du kan ikke slette dette sp›rsm†let s† lenge det finnes svar.';
      Text005@1011 : TextConst 'ENU=Please select for which questionnaire this rating should be created.;NOR=Angi hvilket sp›rreskjema denne rangeringen skal opprettes for.';
      Text006@1010 : TextConst 'ENU=Please describe the rating.;NOR=Beskriv rangeringen.';
      Text007@1009 : TextConst 'ENU=Please create one or more different answers.;NOR=Opprett ett eller flere ulike svar.';
      Text008@1008 : TextConst 'ENU=Please enter which range of points this answer should require.;NOR=Angi poengberegningsskala for dette svaret.';
      Text009@1012 : TextConst 'ENU=High;NOR=H›y';
      Text010@1013 : TextConst 'ENU=Low;NOR=Lav';
      Text011@1014 : TextConst '@@@=Selecting answer A;ENU=A;NOR=A;SVE=A';
      Text012@1015 : TextConst '@@@=Selecting answer B;ENU=B;NOR=B';
      Text013@1016 : TextConst '@@@=Selecting answer C;ENU=C;NOR=U;SVE=Text005';

    [External]
    PROCEDURE MoveUp@1();
    VAR
      UpperProfileQuestnLine@1000 : Record 5088;
      LineNo@1001 : Integer;
      UpperRecLineNo@1002 : Integer;
    BEGIN
      TESTFIELD(Type,Type::Answer);
      UpperProfileQuestnLine.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
      LineNo := "Line No.";
      UpperProfileQuestnLine.GET("Profile Questionnaire Code","Line No.");

      IF UpperProfileQuestnLine.FIND('<') AND
         (UpperProfileQuestnLine.Type = UpperProfileQuestnLine.Type::Answer)
      THEN BEGIN
        UpperRecLineNo := UpperProfileQuestnLine."Line No.";
        RENAME("Profile Questionnaire Code",-1);
        UpperProfileQuestnLine.RENAME("Profile Questionnaire Code",LineNo);
        RENAME("Profile Questionnaire Code",UpperRecLineNo);
      END;
    END;

    [External]
    PROCEDURE MoveDown@2();
    VAR
      LowerProfileQuestnLine@1000 : Record 5088;
      LineNo@1001 : Integer;
      LowerRecLineNo@1002 : Integer;
    BEGIN
      TESTFIELD(Type,Type::Answer);
      LowerProfileQuestnLine.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
      LineNo := "Line No.";
      LowerProfileQuestnLine.GET("Profile Questionnaire Code","Line No.");

      IF LowerProfileQuestnLine.FIND('>') AND
         (LowerProfileQuestnLine.Type = LowerProfileQuestnLine.Type::Answer)
      THEN BEGIN
        LowerRecLineNo := LowerProfileQuestnLine."Line No.";
        RENAME("Profile Questionnaire Code",-1);
        LowerProfileQuestnLine.RENAME("Profile Questionnaire Code",LineNo);
        RENAME("Profile Questionnaire Code",LowerRecLineNo);
      END;
    END;

    [External]
    PROCEDURE Question@4() : Text[250];
    BEGIN
      ProfileQuestnLine.RESET;
      ProfileQuestnLine.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
      ProfileQuestnLine.SETFILTER("Line No.",'<%1',"Line No.");
      ProfileQuestnLine.SETRANGE(Type,Type::Question);
      IF ProfileQuestnLine.FINDLAST THEN
        EXIT(ProfileQuestnLine.Description);
    END;

    [External]
    PROCEDURE FindQuestionLine@3() : Integer;
    BEGIN
      ProfileQuestnLine.RESET;
      ProfileQuestnLine.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
      ProfileQuestnLine.SETFILTER("Line No.",'<%1',"Line No.");
      ProfileQuestnLine.SETRANGE(Type,Type::Question);
      IF ProfileQuestnLine.FINDLAST THEN
        EXIT(ProfileQuestnLine."Line No.");
    END;

    LOCAL PROCEDURE ResetFields@5();
    BEGIN
      CLEAR("Starting Date Formula");
      CLEAR("Ending Date Formula");
      "Classification Method" := "Classification Method"::" ";
      "Sorting Method" := "Sorting Method"::" ";
      "No. of Decimals" := 0;
      "Min. % Questions Answered" := 0;
    END;

    [External]
    PROCEDURE CreateRatingFromProfQuestnLine@6(VAR ProfileQuestnLine@1001 : Record 5088);
    BEGIN
      INIT;
      "Profile Questionnaire Code" := ProfileQuestnLine."Profile Questionnaire Code";
      StartWizard;
    END;

    LOCAL PROCEDURE StartWizard@16();
    BEGIN
      "Wizard Step" := "Wizard Step"::"1";
      VALIDATE("Auto Contact Classification",TRUE);
      VALIDATE("Contact Class. Field","Contact Class. Field"::Rating);
      INSERT;

      ValidateAnswerOption;
      ValidateIntervalOption;

      PAGE.RUNMODAL(PAGE::"Create Rating",Rec);
    END;

    [External]
    PROCEDURE CheckStatus@9();
    BEGIN
      CASE "Wizard Step" OF
        "Wizard Step"::"1":
          BEGIN
            IF "Profile Questionnaire Code" = '' THEN
              ERROR(Text005);
            IF Description = '' THEN
              ERROR(Text006);
          END;
        "Wizard Step"::"2":
          BEGIN
            IF TempProfileQuestionnaireLine.COUNT = 0 THEN
              ERROR(Text007);
          END;
        "Wizard Step"::"3":
          IF ("Wizard From Value" = 0) AND ("Wizard To Value" = 0) THEN
            ERROR(Text008);
      END;
    END;

    [External]
    PROCEDURE PerformNextWizardStatus@8();
    BEGIN
      CASE "Wizard Step" OF
        "Wizard Step"::"1":
          "Wizard Step" := "Wizard Step" + 1;
        "Wizard Step"::"2":
          BEGIN
            "Wizard From Line No." := 0;
            "Wizard Step" := "Wizard Step" + 1;
            TempProfileQuestionnaireLine.SETRANGE("Line No.");
            TempProfileQuestionnaireLine.FIND('-');
            SetIntervalOption;
          END;
        "Wizard Step"::"3":
          BEGIN
            TempProfileQuestionnaireLine.SETFILTER("Line No.",'%1..',"Wizard From Line No.");
            TempProfileQuestionnaireLine.FIND('-');
            TempProfileQuestionnaireLine."From Value" := "Wizard From Value";
            TempProfileQuestionnaireLine."To Value" := "Wizard To Value";
            TempProfileQuestionnaireLine.MODIFY;
            IF TempProfileQuestionnaireLine.NEXT <> 0 THEN BEGIN
              TempProfileQuestionnaireLine.SETRANGE("Line No.",TempProfileQuestionnaireLine."Line No.");
              "Wizard From Line No." := TempProfileQuestionnaireLine."Line No.";
              "Wizard From Value" := TempProfileQuestionnaireLine."From Value";
              "Wizard To Value" := TempProfileQuestionnaireLine."To Value";
              SetIntervalOption;
            END ELSE BEGIN
              TempProfileQuestionnaireLine.SETRANGE("Line No.");
              TempProfileQuestionnaireLine.FIND('-');
              "Wizard Step" := "Wizard Step" + 1;
            END;
          END;
      END;
    END;

    [External]
    PROCEDURE PerformPrevWizardStatus@17();
    BEGIN
      CASE "Wizard Step" OF
        "Wizard Step"::"3":
          BEGIN
            TempProfileQuestionnaireLine.SETFILTER("Line No.",'..%1',"Wizard From Line No.");
            IF TempProfileQuestionnaireLine.FIND('+') THEN BEGIN
              TempProfileQuestionnaireLine."From Value" := "Wizard From Value";
              TempProfileQuestionnaireLine."To Value" := "Wizard To Value";
              TempProfileQuestionnaireLine.MODIFY;
            END;
            IF TempProfileQuestionnaireLine.NEXT(-1) <> 0 THEN BEGIN
              "Wizard From Line No." := TempProfileQuestionnaireLine."Line No.";
              "Wizard From Value" := TempProfileQuestionnaireLine."From Value";
              "Wizard To Value" := TempProfileQuestionnaireLine."To Value";
              SetIntervalOption
            END ELSE BEGIN
              TempProfileQuestionnaireLine.SETRANGE("Line No.");
              TempProfileQuestionnaireLine.FIND('-');
              "Wizard Step" := "Wizard Step" - 1;
            END;
          END;
        ELSE
          "Wizard Step" := "Wizard Step" - 1;
      END;
    END;

    [External]
    PROCEDURE FinishWizard@7();
    VAR
      ProfileQuestionnaireLine@1000 : Record 5088;
      ProfileMgt@1004 : Codeunit 5059;
      NextLineNo@1002 : Integer;
      QuestionLineNo@1001 : Integer;
    BEGIN
      ProfileQuestionnaireLine.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
      IF ProfileQuestionnaireLine.FINDLAST THEN
        QuestionLineNo := ProfileQuestionnaireLine."Line No." + 10000
      ELSE
        QuestionLineNo := 10000;

      ProfileQuestionnaireLine := Rec;
      ProfileQuestionnaireLine."Line No." := QuestionLineNo;
      ProfileQuestionnaireLine.INSERT(TRUE);

      NextLineNo := QuestionLineNo;
      TempProfileQuestionnaireLine.RESET;
      IF TempProfileQuestionnaireLine.FINDSET THEN
        REPEAT
          NextLineNo := NextLineNo + 10000;
          ProfileQuestionnaireLine := TempProfileQuestionnaireLine;
          ProfileQuestionnaireLine."Profile Questionnaire Code" := "Profile Questionnaire Code";
          ProfileQuestionnaireLine."Line No." := NextLineNo;
          ProfileQuestionnaireLine.INSERT(TRUE);
        UNTIL TempProfileQuestionnaireLine.NEXT = 0;

      COMMIT;

      ProfileQuestionnaireLine.GET("Profile Questionnaire Code",QuestionLineNo);
      ProfileMgt.ShowAnswerPoints(ProfileQuestionnaireLine);
    END;

    [External]
    PROCEDURE SetIntervalOption@10();
    BEGIN
      CASE TRUE OF
        (TempProfileQuestionnaireLine."From Value" = 0) AND (TempProfileQuestionnaireLine."To Value" <> 0):
          "Interval Option" := "Interval Option"::Maximum;
        (TempProfileQuestionnaireLine."From Value" <> 0) AND (TempProfileQuestionnaireLine."To Value" = 0):
          "Interval Option" := "Interval Option"::Minimum
        ELSE
          "Interval Option" := "Interval Option"::Interval
      END;

      ValidateIntervalOption;
    END;

    [External]
    PROCEDURE ValidateIntervalOption@14();
    BEGIN
      TempProfileQuestionnaireLine.SETFILTER("Line No.",'%1..',"Wizard From Line No.");
      TempProfileQuestionnaireLine.FIND('-');
      IF "Interval Option" = "Interval Option"::Minimum THEN
        TempProfileQuestionnaireLine."To Value" := 0;
      IF "Interval Option" = "Interval Option"::Maximum THEN
        TempProfileQuestionnaireLine."From Value" := 0;
      TempProfileQuestionnaireLine.MODIFY;
    END;

    [External]
    PROCEDURE ValidateAnswerOption@13();
    BEGIN
      IF "Answer Option" = "Answer Option"::Custom THEN
        EXIT;

      TempProfileQuestionnaireLine.DELETEALL;

      CASE "Answer Option" OF
        "Answer Option"::HighLow:
          BEGIN
            CreateAnswer(Text009);
            CreateAnswer(Text010);
          END;
        "Answer Option"::ABC:
          BEGIN
            CreateAnswer(Text011);
            CreateAnswer(Text012);
            CreateAnswer(Text013);
          END;
      END;
    END;

    LOCAL PROCEDURE CreateAnswer@15(AnswerDescription@1000 : Text[250]);
    BEGIN
      TempProfileQuestionnaireLine.INIT;
      TempProfileQuestionnaireLine."Line No." := (TempProfileQuestionnaireLine.COUNT + 1) * 10000;
      TempProfileQuestionnaireLine.Type := TempProfileQuestionnaireLine.Type::Answer;
      TempProfileQuestionnaireLine.Description := AnswerDescription;
      TempProfileQuestionnaireLine.INSERT;
    END;

    [External]
    PROCEDURE NoOfProfileAnswers@11() : Decimal;
    BEGIN
      EXIT(TempProfileQuestionnaireLine.COUNT);
    END;

    [External]
    PROCEDURE ShowAnswers@12();
    VAR
      TempProfileQuestionnaireLine2@1000 : TEMPORARY Record 5088;
    BEGIN
      IF "Answer Option" <> "Answer Option"::Custom THEN
        IF TempProfileQuestionnaireLine.FIND('-') THEN
          REPEAT
            TempProfileQuestionnaireLine2 := TempProfileQuestionnaireLine;
            TempProfileQuestionnaireLine2.INSERT;
          UNTIL TempProfileQuestionnaireLine.NEXT = 0;

      PAGE.RUNMODAL(PAGE::"Rating Answers",TempProfileQuestionnaireLine);

      IF "Answer Option" <> "Answer Option"::Custom THEN
        IF TempProfileQuestionnaireLine.COUNT <> TempProfileQuestionnaireLine2.COUNT THEN
          "Answer Option" := "Answer Option"::Custom
        ELSE BEGIN
          IF TempProfileQuestionnaireLine.FIND('-') THEN
            REPEAT
              IF NOT TempProfileQuestionnaireLine2.GET(
                   TempProfileQuestionnaireLine."Profile Questionnaire Code",TempProfileQuestionnaireLine."Line No.")
              THEN
                "Answer Option" := "Answer Option"::Custom
              ELSE
                IF TempProfileQuestionnaireLine.Description <> TempProfileQuestionnaireLine2.Description THEN
                  "Answer Option" := "Answer Option"::Custom
            UNTIL (TempProfileQuestionnaireLine.NEXT = 0) OR ("Answer Option" = "Answer Option"::Custom);
        END;
    END;

    [External]
    PROCEDURE GetProfileLineAnswerDesc@18() : Text[250];
    BEGIN
      TempProfileQuestionnaireLine.SETFILTER("Line No.",'%1..',"Wizard From Line No.");
      TempProfileQuestionnaireLine.FIND('-');
      EXIT(TempProfileQuestionnaireLine.Description);
    END;

    [External]
    PROCEDURE GetAnswers@19(VAR ProfileQuestionnaireLine@1000 : Record 5088);
    BEGIN
      TempProfileQuestionnaireLine.RESET;
      ProfileQuestionnaireLine.RESET;
      ProfileQuestionnaireLine.DELETEALL;
      IF TempProfileQuestionnaireLine.FIND('-') THEN
        REPEAT
          ProfileQuestionnaireLine.INIT;
          ProfileQuestionnaireLine := TempProfileQuestionnaireLine;
          ProfileQuestionnaireLine.INSERT;
        UNTIL TempProfileQuestionnaireLine.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

