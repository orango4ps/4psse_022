OBJECT Table 11020663 Assembly Object Mat. Req. Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CheckEntityNo;
             END;

    OnModify=BEGIN
               CheckEntityNo;
             END;

    OnDelete=VAR
               PurchaseLineDetail@1210190000 : Record 11020660;
             BEGIN
               //kkleermaker.sn, 110908, m29019
               IF PurchaseLineDetail.GET(
                 PurchaseLineDetail."Line Type"::"Assembly Object Material Requirement",
                 PurchaseLineDetail."Record Type"::Project,
                 "Project No.",
                 "Yard No.",
                 "System No.",
                 "Entity Type",
                 "Entity No.",
                 '',
                 '',
                 "Line No.")
               THEN
                 PurchaseLineDetail.DELETE;
               //kkleermaker.en, 110908, m29019

               DeleteDocumentLinks;
             END;

    CaptionML=[ENU=Assembly Object Mat. Req. Line;
               NOR=Monteringsobjekt mat.behovsrad;
               SVE=Monteringsobjekt mat.krav.rad];
    LookupPageID=Page11124849;
    DrillDownPageID=Page11124849;
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation=Job.No.;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr.;
                                                              SVE=Projektnr] }
    { 20  ;   ;Yard No.            ;Code20        ;TableRelation="Assembly Yard Number Object"."Yard No." WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=[ENU=Yard No.;
                                                              NOR=Plassnr;
                                                              SVE=Platsnr] }
    { 25  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 30  ;   ;Entity Type         ;Option        ;CaptionML=[ENU=Entity Type;
                                                              NOR=Enhetstype;
                                                              SVE=Enhetstyp];
                                                   OptionCaptionML=[ENU=None,Cable,Device,Extra Need,Cable Transit,,,,,Position;
                                                                    NOR=Ingen,Kabel,Enhet,Ekstra behov,Kabeloverf›ring,,,,,Posisjon;
                                                                    SVE=Ingen,Kabel,Anordning,Extra behov,Kabel”verf”ring,,,,,Position];
                                                   OptionString=None,Cable,Device,ExtraNeed,CableTransit,,,,,Position }
    { 40  ;   ;System No.          ;Code20        ;TableRelation="Assembly Yard Number Object"."System No." WHERE (Project No.=FIELD(Project No.),
                                                                                                                   Yard No.=FIELD(Yard No.));
                                                   CaptionML=[ENU=System No.;
                                                              NOR=Systemnr;
                                                              SVE=Systemnr] }
    { 50  ;   ;Entity No.          ;Code20        ;TableRelation="Assembly Yard Number Object"."Entity No." WHERE (Project No.=FIELD(Project No.),
                                                                                                                   Yard No.=FIELD(Yard No.),
                                                                                                                   Entity Type=FIELD(Entity Type),
                                                                                                                   System No.=FIELD(System No.));
                                                   CaptionML=[ENU=Entity No.;
                                                              NOR=Enhetsnr;
                                                              SVE=Enhetsnr] }
    { 55  ;   ;Origin              ;Option        ;InitValue=Manual;
                                                   CaptionML=[ENU=Origin;
                                                              NOR=Utstedt;
                                                              SVE=Origin];
                                                   OptionCaptionML=[ENU=Manual,,,,Import;
                                                                    NOR=Manuell,,,,Importer;
                                                                    SVE=Manuell,,,,Importera];
                                                   OptionString=Manual,,,,Import;
                                                   Description=#29022;
                                                   Editable=No }
    { 100 ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   OnValidate=BEGIN
                                                                ValidateItemNo;
                                                              END;

                                                   CaptionML=[ENU=Item No.;
                                                              NOR=Artikkelnr;
                                                              SVE=Artikelnr] }
    { 101 ;   ;Item Description    ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Item.Description WHERE (No.=FIELD(Item No.)));
                                                   CaptionML=[ENU=Item Description;
                                                              NOR=Artikkelbeskrivelse;
                                                              SVE=Artikelbeskrivning];
                                                   Description=#29019;
                                                   Editable=No }
    { 105 ;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=[ENU=Variant Code;
                                                              NOR=Variantkode;
                                                              SVE=Variantkod] }
    { 160 ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 170 ;   ;Description 2       ;Text50        ;CaptionML=[ENU=Description 2;
                                                              NOR=Beskrivelse 2;
                                                              SVE=Beskrivning 2] }
    { 200 ;   ;Quantity            ;Decimal       ;CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal] }
    { 230 ;   ;Item Category Code  ;Code20        ;TableRelation="Item Category";
                                                   CaptionML=[ENU=Item Category Code;
                                                              NOR=Varekategorikode;
                                                              SVE=Artikelkategorikod];
                                                   Description=#28683 }
  }
  KEYS
  {
    {    ;Project No.,Yard No.,Entity Type,System No.,Entity No.,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      FPSLicenseManagement@1100527400 : Codeunit 11229289;

    LOCAL PROCEDURE ValidateItemNo@1210190000();
    VAR
      Item@1210190000 : Record 27;
    BEGIN
      IF "Item No." = '' THEN BEGIN
        "Item Category Code" := '';
        Description := '';
        "Description 2" := '';
      END ELSE
        IF Item.GET("Item No.") THEN BEGIN
          "Item Category Code" := Item."Item Category Code";
          Description := Item.Description;
          "Description 2" := Item."Description 2";
        END;
    END;

    PROCEDURE EditPurchaseLineDetails@1210190008();
    VAR
      PurchaseLineDetail@1210190000 : Record 11020660;
    BEGIN
      //kkleermaker, 110908, M29019
      PurchaseLineDetail.ShowCard(
        PurchaseLineDetail."Line Type"::"Assembly Object Material Requirement",
        PurchaseLineDetail."Record Type"::Project,
        "Project No.",
        "Yard No.",
        "System No.",
        "Entity Type",
        "Entity No.",
        '',
        '',
        "Line No.",
        TRUE);
    END;

    PROCEDURE CopyPurLineDetailToMatReqLine@1210190001(MatReqLineNo@1210190003 : Integer);
    VAR
      ProjectMaterialReqLine@1210190000 : Record 11012526;
      PurchaseLineDetail@1210190001 : Record 11020660;
      PurchaseLineDetail2@1210190002 : Record 11020660;
    BEGIN
      //kkleermaker, 110908, M29019
      IF ProjectMaterialReqLine.GET(
        "Project No.",
        "Yard No.",
        "Entity Type",
        "System No.",
        "Entity No.",
        MatReqLineNo)
      THEN
        IF PurchaseLineDetail.GET(
            PurchaseLineDetail."Line Type"::"Assembly Object Material Requirement",
            PurchaseLineDetail."Record Type"::Project,
            "Project No.",
            "Yard No.",
            "System No.",
            "Entity Type",
            "Entity No.",
            '',
            '',
            "Line No.")
        THEN BEGIN
          IF PurchaseLineDetail2.GET(
              PurchaseLineDetail2."Line Type"::"Material Requirement",
              PurchaseLineDetail2."Record Type"::Project,
              "Project No.",
              "Yard No.",
              "System No.",
              "Entity Type",
              "Entity No.",
              '',
              '',
              MatReqLineNo)
          THEN
            PurchaseLineDetail2.DELETE;
          PurchaseLineDetail2 := PurchaseLineDetail;
          PurchaseLineDetail2."Line Type" := PurchaseLineDetail2."Line Type"::"Material Requirement";
          PurchaseLineDetail2."Line No." := MatReqLineNo;
          PurchaseLineDetail2."Detail No." := PurchaseLineDetail2.DetermineDetailNo();
          PurchaseLineDetail2.INSERT;
        END;
    END;

    PROCEDURE ShowDocuments@1210190005();
    VAR
      RecRef@1210190000 : RecordRef;
      DocumentFilters@1210190001 : Codeunit 11012432;
    BEGIN
      //kkleermaker, 110929, M29188
      RecRef.GETTABLE(Rec);
      DocumentFilters.CreateTempFilterAndExecute1(RecRef, '', FALSE, TRUE);
    END;

    PROCEDURE HasDocumentLinks@1210190007() : Boolean;
    VAR
      DocumentLink@1100528400 : Record 11012747;
      DocumentLinkMgt@1100525000 : Codeunit 11012401;
      RecRef@1100525001 : RecordRef;
    BEGIN
      //kkleermaker, 110929, M29188
      IF NOT FPSLicenseManagement.LicenseAndReadPermissionForTable(DocumentLink) THEN
        EXIT(FALSE);

      RecRef.GETTABLE(Rec);
      EXIT(DocumentLinkMgt.EntityHasDocumentLinks(RecRef));
    END;

    PROCEDURE CopyDocLinksToMatReqLine@1210190006(ProjectMaterialReqLine@1210190000 : Record 11012526);
    VAR
      AssemblyObjectMatReqLineRecRef@1210190001 : RecordRef;
      ProjectMaterialReqLineRecRef@1210190002 : RecordRef;
      DocumentLinkMgt@1210190003 : Codeunit 11012401;
    BEGIN
      //kkleermaker, 110929, M29188
      AssemblyObjectMatReqLineRecRef.GETTABLE(Rec);
      ProjectMaterialReqLineRecRef.GETTABLE(ProjectMaterialReqLine);
      DocumentLinkMgt.CopyDocLinks(AssemblyObjectMatReqLineRecRef, ProjectMaterialReqLineRecRef);
    END;

    LOCAL PROCEDURE DeleteDocumentLinks@1100528401();
    VAR
      DocumentLink@1100528402 : Record 11012747;
      DocumentLinkMgt@1100528400 : Codeunit 11012401;
      RecRef@1100528401 : RecordRef;
    BEGIN
      IF NOT FPSLicenseManagement.LicenseAndReadPermissionForTable(DocumentLink) THEN
        EXIT;

      RecRef.GETTABLE(Rec);
      DocumentLinkMgt.DeleteDocumentLinksFromObject(RecRef);
    END;

    LOCAL PROCEDURE CheckEntityNo@1100528400();
    VAR
      JobsSetup@1100528400 : Record 315;
    BEGIN
      IF "Entity Type" = "Entity Type"::None THEN
        EXIT;

      IF JobsSetup.GET AND
         JobsSetup."Entity No. Mandatory"
      THEN
        TESTFIELD("Entity No.");
    END;

    BEGIN
    END.
  }
}

