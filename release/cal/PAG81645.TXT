OBJECT Page 81645 Update Project
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=RFC137;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Update projects;
               NOR=Oppdater Prosjekter;
               SVE=Uppdatera Projekt];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=Yes;
    SourceTable=Table11072003;
    PageType=List;
    OnOpenPage=BEGIN
                 DPA_IsHardFilter := DPA_SetFilters(CurrPage.OBJECTID(FALSE));
               END;

    ActionList=ACTIONS
    {
      { 1100525012;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100525013;1 ;ActionGroup }
      { 1100525014;2 ;Action    ;
                      CaptionML=[ENU=Run update;
                                 NOR=Kj›r oppdatering;
                                 SVE=K”r uppdatering];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ChangeStatus;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 Job@1100525000 : Record 11072003;
                               BEGIN
                                 IF NOT CheckUpdateParameters() THEN
                                   EXIT;

                                 CurrPage.SETSELECTIONFILTER(Job);
                                 RunUpdate(Job);
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100525000;;Container;
                ContainerType=ContentArea }

    { 1100525008;1;Group  ;
                CaptionML=[ENU=Projects;
                           NOR=Prosjekter;
                           SVE=Projekt];
                Editable=FALSE;
                GroupType=Repeater }

    { 1100525011;2;Field  ;
                SourceExpr="No.";
                Enabled=FALSE;
                Editable=FALSE }

    { 1100409000;2;Field  ;
                SourceExpr="Main Project" }

    { 1100525010;2;Field  ;
                SourceExpr=Description }

    { 1100525015;2;Field  ;
                SourceExpr="Bill-to Customer No." }

    { 1100525002;2;Field  ;
                SourceExpr="Project Manager" }

    { 1000000002;2;Field  ;
                Name=ProjMgrName;
                CaptionML=[ENU=Project Manager Name;
                           NOR=Prosjektleders navn;
                           SVE=Platschef namn];
                SourceExpr=GetCurrentEmployeeName }

    { 1000000004;2;Field  ;
                SourceExpr="Global Dimension 1 Code" }

    { 1100525009;2;Field  ;
                OptionCaptionML=[ENU=Estimation,Preparation,Production,Technical Finished,Administrative Finished,Finished,Archive,Archived;
                                 NOR=Kalkulering,Forberedelse,Produksjon,Teknisk ferdig,Administrativt ferdig,Ferdig,Arkiv,Arkivert;
                                 SVE=Kalkyl,F”rberedelse,Produktion,Tekniskt f„rdig,Administrativt f„rdig,F„rdig,Arkiv,Arkiverad];
                SourceExpr="Project Status" }

    { 1100409001;2;Field  ;
                SourceExpr="Single/Main/Sub Project" }

    { 1000000000;1;Group  ;
                CaptionML=[ENU=Update;
                           NOR=Oppdatering;
                           SVE=Uppdatering];
                GroupType=Group;
                Layout=Columns }

    { 1000000001;2;Field  ;
                Name=ProjectManager;
                CaptionML=[ENU=Project Manager;
                           NOR=Prosjektleder;
                           SVE=Platschef];
                SourceExpr=gvProjectManager;
                TableRelation=Employee;
                Importance=Promoted;
                OnValidate=BEGIN
                             gvProjectManagerName := GetEmployeeName(gvProjectManager);
                           END;
                            }

    { 1000000003;2;Field  ;
                Name=ProjectManagerName;
                CaptionML=[ENU=Project Manager Name;
                           NOR=Prosjektleders navn;
                           SVE=Platschef namn];
                SourceExpr=gvProjectManagerName;
                Editable=FALSE;
                MultiLine=No;
                Style=Standard;
                StyleExpr=TRUE;
                QuickEntry=FALSE }

  }
  CODE
  {
    VAR
      gvProjectManager@1000000000 : Code[20];
      Text001@1000000001 : TextConst 'ENU=You must enter something under Update;NOR=Du m† angi noe under Oppdatering;SVE="Du m†ste ange n†got under Uppdatering "';
      gvProjectManagerName@1000000002 : Text[250];
      Text002@1000000003 : TextConst 'ENU=Do you want to start the update?;NOR=Vil du starte oppdateringen?;SVE=Vill du starta uppdateringen?';
      Text003@1000000004 : TextConst 'ENU=Marked projects will be uppdated with Project Manager %1 (%2)\\;NOR=De prosjekt som er markert i listen vil bli oppdatert med prosjektleder % 1 (% 2) \\;SVE=De projekt som markerats i listan kommer att uppdateras med platschef %1 (%2)\\';
      DPA_IsHardFilter@1000000005 : Boolean;

    PROCEDURE RunUpdate@1100525012(VAR Job@1100525000 : Record 11072003);
    VAR
      ProjStatusLocal@1100525001 : Integer;
      Job2@1100409000 : Record 11072003;
    BEGIN
      // Runs the update - This function is called from  <Action1100525014>
      IF NOT CheckUpdateParameters() THEN
        EXIT
      ELSE BEGIN
        IF Job.FINDSET(FALSE) THEN BEGIN
          IF ConfirmUpdate() THEN BEGIN
            REPEAT
              Job2 := Job;
              Job.VALIDATE("Project Manager", gvProjectManager);
              Job.OnModifyFunc(Job2);
              Job.MODIFY(FALSE);
            UNTIL Job.NEXT=0;
           END;
        END;
      END;
    END;

    PROCEDURE CheckUpdateParameters@1000000013() resultValue : Boolean;
    BEGIN
      // Checks that the user has entered/selected all mandatory data in the lower section
      resultValue := TRUE;

      IF gvProjectManager = '' THEN BEGIN
        MESSAGE(Text001);
        resultValue := FALSE;
      END;
    END;

    PROCEDURE GetEmployeeName@1000000019(pEmployeeNo@1000000001 : Code[20]) : Text[100];
    VAR
      lvEmployeeRec@1000000000 : Record 5200;
    BEGIN
      // Returns Full name for an Employee
      IF pEmployeeNo <> '' THEN BEGIN
        IF lvEmployeeRec.GET(pEmployeeNo) THEN
          EXIT(lvEmployeeRec."Full Name")
        ELSE
          EXIT('');
      END ELSE BEGIN
        EXIT('');
      END;
    END;

    PROCEDURE GetCurrentEmployeeName@1000000021() : Text[100];
    BEGIN
      // Used to display name of current project manager in the repeater/list
      EXIT(GetEmployeeName("Project Manager"));
    END;

    PROCEDURE ConfirmUpdate@1000000025() : Boolean;
    VAR
      lvMessage@1000000000 : Text;
    BEGIN
      // Display a question to the user
      lvMessage := STRSUBSTNO(Text003,gvProjectManager, GetEmployeeName(gvProjectManager));
      lvMessage += Text002;
      EXIT( CONFIRM(lvMessage));
    END;

    BEGIN
    {
      160907 ITERO.AC RFC137 New page for update of multiple projects/jobs.
             First version is intended for update of Project Manager
             (In the future we can add more fields to update in job table)
      160913 ITERO.AC RFC137 Adjusted Norwegian Captions
    }
    END.
  }
}

