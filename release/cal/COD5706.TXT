OBJECT Codeunit 5706 TransferOrder-Post (Yes/No)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04;
  }
  PROPERTIES
  {
    TableNo=5740;
    OnRun=BEGIN
            TransHeader.COPY(Rec);
            Code;
            Rec := TransHeader;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=&Ship,&Receive;NOR=&Lever,&Motta;SVE=&Leverera,&Inleverera';
      TransHeader@1001 : Record 5740;

    LOCAL PROCEDURE Code@1();
    VAR
      TransLine@1000 : Record 5741;
      TransferPostShipment@1002 : Codeunit 5704;
      TransferPostReceipt@1003 : Codeunit 5705;
      DefaultNumber@1001 : Integer;
      Selection@1004 : ' ,Shipment,Receipt';
      IsHandled@1005 : Boolean;
    BEGIN
      OnBeforePost(TransHeader,IsHandled);
      IF IsHandled THEN
        EXIT;

      WITH TransHeader DO BEGIN
        TransLine.SETRANGE("Document No.","No.");
        IF TransLine.FIND('-') THEN
          REPEAT
            IF (TransLine."Quantity Shipped" < TransLine.Quantity) AND
               (DefaultNumber = 0)
            THEN
              DefaultNumber := 1;
            IF (TransLine."Quantity Received" < TransLine.Quantity) AND
               (DefaultNumber = 0)
            THEN
              DefaultNumber := 2;
          UNTIL (TransLine.NEXT = 0) OR (DefaultNumber > 0);
        IF "Direct Transfer" THEN BEGIN
          TransferPostShipment.RUN(TransHeader);
          TransferPostReceipt.RUN(TransHeader);
        END ELSE BEGIN
          IF DefaultNumber = 0 THEN
            DefaultNumber := 1;
          Selection := STRMENU(Text000,DefaultNumber);
          CASE Selection OF
            0:
              EXIT;
            Selection::Shipment:
              TransferPostShipment.RUN(TransHeader);
            Selection::Receipt:
              TransferPostReceipt.RUN(TransHeader);
          END;
        END;
      END;

      OnAfterPost(TransHeader);
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPost@2(VAR TransHeader@1000 : Record 5740);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePost@3(VAR TransHeader@1000 : Record 5740;VAR IsHandled@1001 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

