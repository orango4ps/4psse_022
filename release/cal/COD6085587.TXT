OBJECT Codeunit 6085587 BigString Management
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW17.00.00.5.00.05;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      BigString@6085573 : Text;

    PROCEDURE Append@161024013(Text@161024012 : Text[1024]);
    BEGIN
      BigString += Text;
    END;

    PROCEDURE IndexOf@161024015(Text@161024012 : Text[1024]) : Integer;
    BEGIN
      // IndexOf is zero based
      EXIT(STRPOS(BigString,Text) - 1);
    END;

    PROCEDURE Clear@161024016();
    BEGIN
      BigString := '';
    END;

    PROCEDURE SubString@161024017(StartIndex@161024012 : Integer;Length@161024013 : Integer) : Text[1024];
    BEGIN
      EXIT(COPYSTR(BigString,StartIndex - 1,Length))
    END;

    PROCEDURE Replace@161024018(OldValue@1160330000 : Text[1024];NewValue@1160330001 : Text);
    VAR
      Index@1160330002 : Integer;
    BEGIN
      IF OldValue = NewValue THEN
        EXIT;

      Index := 1;

      WHILE (Index >= 0) DO BEGIN
        IF (STRPOS(COPYSTR(BigString,Index),OldValue) > 0) THEN
          Index := STRPOS(COPYSTR(BigString,Index),OldValue) + (Index-1)
        ELSE
          Index := -1;

        IF Index > 0 THEN BEGIN
          BigString := DELSTR(BigString,Index,STRLEN(OldValue));
          BigString := INSSTR(BigString,NewValue,Index);
          Index += STRLEN(NewValue);
        END;
      END;
    END;

    PROCEDURE Replace2@161024019(OldValue@161024012 : Text[1024];VAR NewBigStringMgnt@161024013 : Codeunit 6085587);
    BEGIN
      Replace(OldValue,NewBigStringMgnt.Text);
    END;

    PROCEDURE LoadFromStream@6085574(VAR ReadStream@6085573 : InStream);
    VAR
      Data@6085574 : Text[1024];
    BEGIN
      WHILE NOT ReadStream.EOS DO BEGIN
        ReadStream.READTEXT(Data);
        Append(Data);
      END;
    END;

    PROCEDURE Length@1160040000() : Integer;
    BEGIN
      EXIT(STRLEN(BigString));
    END;

    PROCEDURE Text@6085573() : Text;
    BEGIN
      EXIT(BigString);
    END;

    BEGIN
    END.
  }
}

