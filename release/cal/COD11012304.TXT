OBJECT Codeunit 11012304 Import Vendor Discount Terms
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00,4PSSE;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            PAGE.RUNMODAL(PAGE::"Import Vendor Discount Terms");
          END;

  }
  CODE
  {
    VAR
      ItemSetup@1100485000 : Record 11012300;
      ImportDiscRec@1100525000 : Record 11012736;
      TmpDiscRec@1100525001 : TEMPORARY Record 11012312;
      LogRec@1100525003 : Record 11012335;
      MessRec@1100525002 : Record 11012737;
      Text000@1100525008 : TextConst 'ENU=Check ...\;NOR=Check...\;SVE=Check...\';
      Text001@1100525007 : TextConst 'ENU=Update ...\;NOR=Oppdater...\;SVE=Uppdatera...\';
      Text002@1100525012 : TextConst 'ENU=File Name     #1#######################\\;NOR=Filnavn     #1#######################\\;SVE=Filnamn       #1#######################\\';
      Text003@1100525011 : TextConst 'ENU=Record          #2#######################\\;NOR=Post          #2#######################\\;SVE=Post          #2#######################\\';
      Text004@1100525010 : TextConst 'ENU=@3@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@;NOR=@3@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@;SVE=@3@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@';
      Window@1100525005 : Dialog;
      LastUpdateTime@1100525004 : Time;
      MaxLines@1100525006 : Integer;
      gvEndDate@1100525009 : Date;
      gvFullExport@1100525014 : Boolean;
      LastVendor@1100525013 : Text[30];
      LastDiscRef@1100525015 : Text[30];
      LastDiscLevel@1100525016 : Integer;
      ProductDate@1100525017 : Date;
      StringFunctions@1100528400 : Codeunit 11012273;
      StartDateLog@1100525018 : DateTime;

    PROCEDURE UpdateDiscountTerms@1100525000();
    VAR
      lvDiscHistRec@1100525001 : Record 11012312;
      lvDiscHistRec2@1100525005 : Record 11012312;
      WebshopItemMgt@1100528300 : Codeunit 11012306;
      RecordExists@1100525002 : Boolean;
      lvText000@1100525006 : TextConst 'ENU=Update Discount Terms;NOR=Oppdater rabattvilk†r;SVE=Uppdatera rabattvillkor';
      LinesRead@1100525000 : Integer;
      LinesProcessed@1100525004 : Integer;
      DiscountTermGroup@1100285000 : Record 11012338;
    BEGIN
      StartDateLog := CURRENTDATETIME;  //db, 08-03-13: C006329

      Window.OPEN(Text001 + Text002 + Text003 + Text004);
      Window.UPDATE(1, lvDiscHistRec.TABLECAPTION);

      LastUpdateTime := TIME;

      //UpdateOldDiscountTerms;  //db, 11-01-10: M14768

      LastVendor := ''; //mg, 20-01-11: M25050
      WITH ImportDiscRec DO BEGIN
        IF FINDSET(TRUE,FALSE) THEN BEGIN
          MaxLines := COUNT;
          REPEAT
            LinesRead := LinesRead + 1;
            IF (TIME > LastUpdateTime + 1000) THEN BEGIN
              Window.UPDATE(2,LinesRead);
              Window.UPDATE(3,100 * ROUND(LinesRead / MaxLines * 100,1));
              LastUpdateTime := TIME;
            END;

            IF NOT "Exclude from update" THEN BEGIN  //db, 19-08-10
              //db.sn, 19-01-10: M17848
              IF (LastVendor <> Vendor) OR (LastDiscLevel <> "Discount Level") OR (LastDiscRef <> "Discount Reference") THEN BEGIN
                 //mg.sn, 04-11-10: M23181
                 IF (LastVendor <> '') THEN
                   UpdateExpiredDiscountTerms;
                 //mg.en, 04-11-10: M23181
                 LastVendor := Vendor;
                 LastDiscLevel := "Discount Level";
                 LastDiscRef := "Discount Reference";
                 ProductDate := "Created on";
      //           UpdateOldDiscountTerms; //mg.o, 04-11-10: M23181
              END;
              //db.en, 19-01-10: M17848

              RecordExists :=
                lvDiscHistRec.GET(Vendor,"Discount Group","Item Code","Discount Level","Discount Reference","Starting Date");
              lvDiscHistRec.TRANSFERFIELDS(ImportDiscRec);
      //      lvDiscHistRec.Description := CharSetConversion.Ansi2Ascii(lvDiscHistRec.Description);  //db, 25-05-11: M26890 //DP01092.o
              lvDiscHistRec.Description := StringFunctions.DeleteInvalidXMLCharacters(lvDiscHistRec.Description);
              IF NOT RecordExists THEN BEGIN
                lvDiscHistRec.INSERT(TRUE);  //db, 05-05-09: M15258
                //db.sn, 16-01-09
                lvDiscHistRec2.SETRANGE(Vendor, lvDiscHistRec.Vendor);
                lvDiscHistRec2.SETRANGE("Discount Group", lvDiscHistRec."Discount Group");
                lvDiscHistRec2.SETRANGE("Item Code", lvDiscHistRec."Item Code");
                lvDiscHistRec2.SETRANGE("Discount Level", lvDiscHistRec."Discount Level");
                lvDiscHistRec2.SETRANGE("Discount Reference", lvDiscHistRec."Discount Reference");
                lvDiscHistRec2.GetPreviousData(lvDiscHistRec2, lvDiscHistRec."Starting Date");
                lvDiscHistRec."Sales Discount Group" := lvDiscHistRec2."Sales Discount Group";
                //DP00065.sn
                //lvDiscHistRec."Cost Object" := lvDiscHistRec2."Cost Object";
                //lvDiscHistRec."Cost Object (Removal)" := lvDiscHistRec2."Cost Object (Removal)";
                //DP00065.en
                lvDiscHistRec."Maximum % Price Difference" := lvDiscHistRec2."Maximum % Price Difference";
                lvDiscHistRec."Price Agreement (Sales)" := lvDiscHistRec2."Price Agreement (Sales)";
                lvDiscHistRec."Sales Discount Percentage" := lvDiscHistRec2."Sales Discount Percentage";
                lvDiscHistRec."Sales Price" := lvDiscHistRec2."Sales Price";
                lvDiscHistRec.MODIFY(TRUE);  //db, 05-05-09: M15258
                //db.en, 16-01-09
              END ELSE BEGIN
                lvDiscHistRec.MODIFY(TRUE);  //db, 05-05-09: M15258
              END;

              //>>131106 ITERO.DJ: Check if Type Sales
              IF ("Discount Level" = "Discount Level"::Customer) OR ("Discount Level" = "Discount Level"::Job) THEN BEGIN
                DiscountTermGroup.GET("Discount Level", "Discount Reference");
                IF DiscountTermGroup.Type = DiscountTermGroup.Type::Sales THEN BEGIN
                  lvDiscHistRec."Discount Percentage 1" := 0;
                  lvDiscHistRec."Discount Percentage 2" := 0;
                  lvDiscHistRec."Discount Percentage 3" := 0;
                  lvDiscHistRec."Purchase Price" := 0;
                  lvDiscHistRec.MODIFY(TRUE);
                END;

                // If the Discount Term Group is set to "Sales" the Sales Price will be set to the same value as the imported Purchase Price
                IF (DiscountTermGroup.Type = DiscountTermGroup.Type::Sales) OR (DiscountTermGroup.Type = DiscountTermGroup.Type::Common) THEN BEGIN
                  lvDiscHistRec."Sales Discount Percentage" := "Discount Percentage 1";
                  lvDiscHistRec."Sales Price" := ImportDiscRec."Purchase Price";
                  lvDiscHistRec.MODIFY(TRUE);
                END;
              END;
              //<<131106 ITERO.DJ

              UpdateSingleOldDiscountTerms(lvDiscHistRec, lvDiscHistRec."Starting Date"); //mg, 04-11-10: M23181
              LinesProcessed := LinesProcessed + 1;
            END;
          UNTIL NEXT = 0;
          //mg.sn, 04-11-10: M23181
          IF (LastVendor <> '') THEN
            UpdateExpiredDiscountTerms;
          //mg.en, 04-11-10: M23181
          DELETEALL;
          MessRec.DELETEALL;
        END;
      END;

      Window.CLOSE;
      LogRec.UpdateLogbook(lvText000, LinesRead, LinesProcessed, 1, StartDateLog);  //db, 08-03-13: C006329

      IF (WebshopItemMgt.Is2BAEnabled(FALSE)) THEN BEGIN
        COMMIT;
        REPORT.RUNMODAL(REPORT::"Create 2ba Discount File", FALSE);
      END;
    END;

    PROCEDURE CheckDiscountTerms@1100525001();
    VAR
      lvDiscHistRec@1100525007 : Record 11012312;
      lvImportDiscRec@1100525005 : Record 11012736;
      GLSetup@1100525014 : Record 98;
      RecordExists@1100525000 : Boolean;
      lvText000@1100525011 : TextConst 'ENU=Check Discount Terms;NOR=Kontrollere rabattvilk†r;SVE=Kontrollera rabattvillkor';
      lvText001@1100525010 : TextConst 'ENU=Discount Term %1 changed for Vendor %2 (from %3 to %4 %5);NOR=Rabattvilk†r %1 er endret for leverand›r %2 (fra %3 til %4 %5);SVE=Rabattvillkor %1 har „ndrats f”r leverant”r %2 (fr†n %3 till %4 %5)';
      lvText002@1100525009 : TextConst 'ENU=Discount Term %1 added for Vendor %2;NOR=Rabattvilk†r %1 er lagt til for leverand›r %2;SVE=Rabattvillkor %1 har lagts till f”r leverant”r %2';
      lvText003@1100525003 : TextConst 'ENU=Discount Term %1 not present for Vendor %2;NOR=Rabattvilk†r %1 finnes ikke for leverand›r %2;SVE=Rabattvillkor %1 finns inte f”r leverant”r %2';
      MessLine@1100525001 : Integer;
      LinesRead@1100525002 : Integer;
      lvDiscOld@1100525006 : Decimal;
      lvDiscNew@1100525008 : Decimal;
      lvDiscTerm@1100525012 : Text[30];
      lvUnit@1100525013 : Text[30];
      MaxDiff@1100525016 : Decimal;
      lvText004@1100525017 : TextConst 'ENU=Discount Term %1 and Item Code %2 should not be assigned both;NOR=Rabattvilk†r %1 og artikkelkode %2 kan ikke tilordnes samtidig;SVE=Rabattvillkor %1 och artikelkod %2 b”r inte tilldelas samtidigt';
    BEGIN
      StartDateLog := CURRENTDATETIME;  //db, 08-03-13: C006329

      Window.OPEN(Text000 + Text002 + Text003 + Text004);
      Window.UPDATE(1, ImportDiscRec.TABLECAPTION);

      MessRec.DELETEALL;
      GLSetup.GET;
      ItemSetup.GET;

      MessLine := 0;
      TmpDiscRec.Vendor := '~';
      LastUpdateTime := TIME;

      WITH ImportDiscRec DO BEGIN
        IF FINDSET(TRUE,FALSE) THEN BEGIN
          MaxLines := COUNT;
          REPEAT
            LinesRead := LinesRead + 1;
            IF (TIME > LastUpdateTime + 1000) THEN BEGIN
              Window.UPDATE(2,LinesRead);
              Window.UPDATE(3,100 * ROUND(LinesRead / MaxLines * 100,1));
              LastUpdateTime := TIME;
            END;

            IF (Vendor <> TmpDiscRec.Vendor) OR
               ("Discount Group" <> TmpDiscRec."Discount Group") OR
               ("Item Code" <> TmpDiscRec."Item Code") OR
               ("Discount Level" <> TmpDiscRec."Discount Level") OR
               ("Discount Reference" <> TmpDiscRec."Discount Reference") THEN
              RecordExists := FillTmpDiscRec;

            Warnings := FALSE;
            IF RecordExists THEN BEGIN
              IF ("Discount Percentage 1" <> TmpDiscRec."Discount Percentage 1") OR
                 ("Discount Percentage 2" <> TmpDiscRec."Discount Percentage 2") OR
                 ("Discount Percentage 3" <> TmpDiscRec."Discount Percentage 3") OR
                 ("Purchase Price" <> TmpDiscRec."Purchase Price") THEN BEGIN
                IF TmpDiscRec."Purchase Price" <> 0 THEN BEGIN
                  lvDiscOld := TmpDiscRec."Purchase Price";
                END ELSE BEGIN
                  lvDiscOld := TmpDiscRec.GetPurchaseDiscount;
                END;
                IF "Purchase Price" <> 0 THEN BEGIN
                  lvDiscNew := "Purchase Price";
                  lvUnit := GLSetup."LCY Code";
                  MaxDiff := ItemSetup."Maximum % Price Difference";
                END ELSE BEGIN
                  lvDiscNew := ImportDiscRec.GetPurchaseDiscount;
                  lvUnit := '%';
                  MaxDiff := ItemSetup."Maximum % Discount Difference";
                END;
                IF "Discount Group" <> '' THEN
                  lvDiscTerm := "Discount Group"
                ELSE
                  lvDiscTerm := "Item Code";
                IF CalcPercDiff(lvDiscNew,lvDiscOld,("Purchase Price"=0)) > MaxDiff THEN
                  WriteMessage(Vendor,"Item Code","Discount Group",
                    STRSUBSTNO(lvText001,lvDiscTerm,Vendor,lvDiscOld,lvDiscNew,lvUnit),1,TRUE,MessLine);
              END;
            END ELSE BEGIN
              WriteMessage(Vendor,"Item Code","Discount Group",
                STRSUBSTNO(lvText002,"Discount Group",Vendor),1,TRUE,MessLine);
            END;
            IF ("Item Code" <> '') AND ("Discount Group" <> '') THEN  //db, 10-08-09: M10666
              WriteMessage(Vendor,"Item Code","Discount Group",
                STRSUBSTNO(lvText004,"Discount Group","Item Code"),1,TRUE,MessLine);
          UNTIL NEXT = 0;
        END;
      END;

      Window.UPDATE(1, lvDiscHistRec.TABLECAPTION);

      IF LinesRead <> 0 THEN BEGIN
        TmpDiscRec.Vendor := '~';
        WITH lvDiscHistRec DO BEGIN
          IF ImportDiscRec.Vendor <> '' THEN
            SETRANGE(Vendor, ImportDiscRec.Vendor);
          SETRANGE("Discount Level", ImportDiscRec."Discount Level");
          IF ImportDiscRec."Discount Reference" <> '' THEN
            SETRANGE("Discount Reference", ImportDiscRec."Discount Reference")
          ELSE
            SETFILTER("Discount Reference", '%1', '');
          IF FINDSET(TRUE,FALSE) THEN BEGIN
            MaxLines := COUNT;
            REPEAT
              LinesRead := LinesRead + 1;
              IF (TIME > LastUpdateTime + 1000) THEN BEGIN
                Window.UPDATE(2,LinesRead);
                Window.UPDATE(3,100 * ROUND(LinesRead / MaxLines * 100,1));
                LastUpdateTime := TIME;
              END;

              IF (Vendor <> TmpDiscRec.Vendor) OR
                 ("Discount Group" <> TmpDiscRec."Discount Group") OR
                 ("Item Code" <> TmpDiscRec."Item Code") OR
                 ("Discount Level" <> TmpDiscRec."Discount Level") OR
                 ("Discount Reference" <> TmpDiscRec."Discount Reference") THEN
              BEGIN
                lvImportDiscRec.SETRANGE(Vendor, Vendor);
                lvImportDiscRec.SETRANGE("Discount Group", "Discount Group");
                lvImportDiscRec.SETRANGE("Item Code", "Item Code");
                lvImportDiscRec.SETRANGE("Discount Level", "Discount Level");
                lvImportDiscRec.SETRANGE("Discount Reference", "Discount Reference");
                IF NOT lvImportDiscRec.FINDFIRST THEN BEGIN
                  WriteMessage(Vendor,"Item Code","Discount Group",
                    STRSUBSTNO(lvText003,"Discount Group",Vendor),1,FALSE,MessLine);
                END;
              END;
            UNTIL NEXT = 0;
          END;
        END;
      END;

      Window.CLOSE;
      LogRec.UpdateLogbook(lvText000, LinesRead, 0, 1, StartDateLog);  //db, 08-03-13: C006329
    END;

    PROCEDURE WriteMessage@1100525002(lvVendor@1100485003 : Code[20];lvItem@1100485002 : Code[20];lvDiscGrp@1100525003 : Code[20];lvMessage@1100485000 : Text[250];ProcessStep@1100485004 : 'Import,Check,Process';lvUpdateDisc@1100485001 : Boolean;VAR MessLine@1100525000 : Integer);
    BEGIN
      IF MessLine = 0 THEN BEGIN
        IF MessRec.FINDLAST THEN
          MessLine := MessRec."Line No.";
      END;
      MessLine := MessLine + 1;

      MessRec.INIT;
      MessRec."Line No." := MessLine;
      MessRec."Item Code" := lvItem;
      MessRec."Discount Group" := lvDiscGrp;
      MessRec.Vendor := lvVendor;
      MessRec.Comment := lvMessage;
      MessRec."Process Step" := ProcessStep;
      MessRec.INSERT;

      ImportDiscRec.Warnings := TRUE;
      IF lvUpdateDisc THEN
        ImportDiscRec.MODIFY;
    END;

    PROCEDURE FillTmpDiscRec@1100525003() : Boolean;
    VAR
      lvDiscHistRec@1100525002 : Record 11012312;
    BEGIN
      WITH ImportDiscRec DO BEGIN
        lvDiscHistRec.SETRANGE(Vendor, Vendor);
        lvDiscHistRec.SETRANGE("Discount Group", "Discount Group");
        lvDiscHistRec.SETRANGE("Item Code", "Item Code");
        lvDiscHistRec.SETRANGE("Discount Level", "Discount Level");
        lvDiscHistRec.SETRANGE("Discount Reference", "Discount Reference");
        IF lvDiscHistRec.FINDLAST THEN BEGIN
          TmpDiscRec := lvDiscHistRec;
          IF TmpDiscRec.INSERT THEN;
          EXIT(TRUE);
        END;
      END;

      EXIT(FALSE);
    END;

    PROCEDURE CalcPercDiff@1100485009(NewPrice@1100485000 : Decimal;OldPrice@1100485001 : Decimal;lvDisc@1100525000 : Boolean) PercDiff : Decimal;
    BEGIN
      IF lvDisc = TRUE THEN
        EXIT(ABS(NewPrice-OldPrice));

      IF (NewPrice = 0)  AND (OldPrice <> 0) THEN
        EXIT(-100);

      IF (NewPrice <> 0) AND (OldPrice = 0) THEN
        EXIT(100);

      IF (NewPrice = 0) AND (OldPrice = 0) THEN
        EXIT(0);

      PercDiff := (OldPrice / NewPrice) * 100;
      EXIT(ABS(100-PercDiff));
    END;

    PROCEDURE UpdateOldDiscountTerms@1100525004();
    VAR
      TmpImportRec@1100525000 : TEMPORARY Record 11012736;
      lvImportRec@1100525002 : Record 11012736;
      lvDiscHistRec@1100525001 : Record 11012312;
    BEGIN
      //db, 11-01-10: M14768
      //db, 19-01-10: M17848
      lvImportRec.COPY(ImportDiscRec);
      WITH lvImportRec DO BEGIN
        IF gvFullExport = TRUE THEN BEGIN
          TmpImportRec.INIT;
          TmpImportRec.Vendor := LastVendor;
          TmpImportRec."Discount Level" := LastDiscLevel;
          TmpImportRec."Discount Reference" := LastDiscRef;
          IF TmpImportRec.INSERT THEN;
        END ELSE BEGIN
          SETRANGE(Vendor, LastVendor);
          SETRANGE("Discount Level", LastDiscLevel);
          SETRANGE("Discount Reference", LastDiscRef);
          IF FINDSET THEN BEGIN
            REPEAT
              TmpImportRec.INIT;
              TmpImportRec.Vendor := Vendor;
              TmpImportRec."Discount Level" := "Discount Level";
              TmpImportRec."Discount Reference" := "Discount Reference";
              TmpImportRec."Discount Group" := "Discount Group";
              TmpImportRec."Item Code" := "Item Code";
              IF TmpImportRec.INSERT THEN;
            UNTIL NEXT = 0;
          END;
        END;
      END;

      WITH TmpImportRec DO BEGIN
        IF FINDSET THEN BEGIN
          REPEAT
            lvDiscHistRec.SETRANGE(Vendor, Vendor);
            IF gvFullExport = FALSE THEN BEGIN
              lvDiscHistRec.SETRANGE("Discount Group", "Discount Group");
              lvDiscHistRec.SETRANGE("Item Code", "Item Code");
            END;
            lvDiscHistRec.SETRANGE("Discount Level", "Discount Level");
            lvDiscHistRec.SETRANGE("Discount Reference", "Discount Reference");
            lvDiscHistRec.SETRANGE("Ending Date", 0D);
            IF ProductDate <> 0D THEN
              lvDiscHistRec.MODIFYALL("Ending Date", ProductDate, TRUE)  //db, 29-03-11: M25867
            ELSE
              lvDiscHistRec.MODIFYALL("Ending Date", gvEndDate, TRUE);  //db, 29-03-11: M25867
          UNTIL NEXT = 0;
        END;
      END;
    END;

    PROCEDURE UpdateSingleOldDiscountTerms@1100525005(IDiscountTermHistory@1100525000 : Record 11012312;IStartDate@1100525001 : Date);
    VAR
      DiscountTermHistory@1100525002 : Record 11012312;
    BEGIN
      DiscountTermHistory.SETRANGE(Vendor, IDiscountTermHistory.Vendor);
      //mg.sn, 19-04-11: M26652
      DiscountTermHistory.SETRANGE("Discount Level", IDiscountTermHistory."Discount Level");
      DiscountTermHistory.SETRANGE("Discount Reference", IDiscountTermHistory."Discount Reference");
      //mg.en, 19-04-11: M26652
      DiscountTermHistory.SETRANGE("Discount Group", IDiscountTermHistory."Discount Group");
      DiscountTermHistory.SETRANGE("Item Code", IDiscountTermHistory."Item Code");
      DiscountTermHistory.SETFILTER("Starting Date", '<%1', IStartDate);
      DiscountTermHistory.SETRANGE("Ending Date", 0D);
      IF NOT DiscountTermHistory.FINDSET(TRUE) THEN
        EXIT;
      REPEAT
        DiscountTermHistory."Ending Date" := IStartDate -1;
        DiscountTermHistory.MODIFY(TRUE);  //db, 29-03-11: M25867
      UNTIL DiscountTermHistory.NEXT = 0;
    END;

    PROCEDURE UpdateExpiredDiscountTerms@1100525007();
    VAR
      ImportDiscountTerm@1100525000 : Record 11012736;
      DiscountTermHistory@1100525001 : Record 11012312;
    BEGIN
      //mg, 04-11-10: M23181
      IF NOT gvFullExport THEN
        EXIT;

      ImportDiscountTerm.COPY(ImportDiscRec);
      ImportDiscountTerm.SETRANGE(Vendor, LastVendor);
      ImportDiscountTerm.SETRANGE("Discount Level", LastDiscLevel);
      ImportDiscountTerm.SETRANGE("Discount Reference", LastDiscRef);

      DiscountTermHistory.SETRANGE(Vendor, LastVendor);
      DiscountTermHistory.SETRANGE("Discount Level", LastDiscLevel);
      DiscountTermHistory.SETRANGE("Discount Reference", LastDiscRef);
      DiscountTermHistory.SETRANGE("Ending Date", 0D);
      IF NOT DiscountTermHistory.FINDSET(TRUE) THEN
        EXIT;

      REPEAT
      //Vendor,Discount Group,Item Code,Discount Level,Discount Reference,Starting Date
        IF NOT ImportDiscountTerm.GET(
          DiscountTermHistory.Vendor,
          DiscountTermHistory."Discount Group",
          DiscountTermHistory."Item Code",
          DiscountTermHistory."Discount Level",
          DiscountTermHistory."Discount Reference",
          DiscountTermHistory."Starting Date") THEN
          IF ProductDate <> 0D THEN
            DiscountTermHistory."Ending Date" := ProductDate
          ELSE
            DiscountTermHistory."Ending Date" := gvEndDate;
          DiscountTermHistory.MODIFY(TRUE);  //db, 29-03-11: M25867
      UNTIL DiscountTermHistory.NEXT = 0;
    END;

    PROCEDURE SetSelection@1100525006(lvFullExport@1100525001 : Boolean;lvEndDate@1100525000 : Date);
    BEGIN
      gvFullExport := lvFullExport;
      IF lvEndDate = 0D THEN
        gvEndDate := TODAY-1
      ELSE
        gvEndDate := lvEndDate;
    END;

    BEGIN
    {
      4PSSE
      131106 ITERO.DJ Added import to Sales Price and Discount depending on Discount Term Group.
    }
    END.
  }
}

