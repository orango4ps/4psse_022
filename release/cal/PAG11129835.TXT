OBJECT Page 11129835 Visible Resource Grps  by User
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Sichtbare Ressourcengruppen nach Benutzer;
               ENU=Visible Resource Groups by User;
               NLD=Zichtbare resource groepen per gebruiker];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table11229385;
    PageType=List;
    SourceTableTemporary=Yes;
    OnInit=BEGIN
             UserIDFilter := USERID;
           END;

    OnOpenPage=BEGIN
                 BuildTmpTable;
               END;

    ActionList=ACTIONS
    {
      { 1100528609;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100528632;1 ;Action    ;
                      CaptionML=[DEU=Sichtbar machen;
                                 ENU=Make Visible;
                                 NLD=Zichtbaar maken;
                                 SVE=S„tt till synlig];
                      Promoted=Yes;
                      Image=SelectEntries;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 UpdateVisible(TRUE);
                               END;
                                }
      { 1100529000;1 ;Action    ;
                      CaptionML=[DEU=Unsichtbar machen;
                                 ENU=Make Invisible;
                                 NLD=Onzichtbaar maken];
                      Promoted=Yes;
                      Image=SelectLineToApply;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 UpdateVisible(FALSE);
                               END;
                                }
      { 1100528629;1 ;Action    ;
                      CaptionML=[DEU=Sichtbarkeit einrichten (Auswahl);
                                 ENU=Set Visibility (Selection);
                                 NLD=Zichtbaarheid instellen (selectie)];
                      Promoted=Yes;
                      Image=SelectEntries;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 UserSetup@1100529000 : Record 91;
                                 SetResourceGroupVisibility@1100528600 : Report 11229279;
                               BEGIN
                                 UserSetup.SETRANGE("User ID", UserIDFilter);
                                 SetResourceGroupVisibility.SETTABLEVIEW(UserSetup);
                                 SetResourceGroupVisibility.RUNMODAL;
                                 BuildTmpTable;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100528604;;Container;
                ContainerType=ContentArea }

    { 1100528619;1;Group  ;
                CaptionML=[DEU=Filter;
                           ENU=Filters;
                           NLD=Filters;
                           NOR=Filtre;
                           SVE=Filter];
                GroupType=Group }

    { 1100528620;2;Field  ;
                Name=<Control1100528619>;
                CaptionML=[DEU=Benutzer-ID;
                           ENU=User ID;
                           NLD=Gebruikers ID;
                           NOR=Bruker-ID;
                           SVE=Anv„ndar-ID];
                NotBlank=Yes;
                SourceExpr=UserIDFilter;
                TableRelation="User Setup";
                OnValidate=VAR
                             UserSetup@1100529000 : Record 91;
                           BEGIN
                             UserSetup.GET(UserIDFilter);
                             BuildTmpTable;
                           END;
                            }

    { 1100528605;1;Group  ;
                GroupType=Repeater }

    { 1100528607;2;Field  ;
                SourceExpr="Resource Group Type" }

    { 1100528608;2;Field  ;
                SourceExpr="Resource Group Company" }

    { 1100528601;2;Field  ;
                SourceExpr="Resource Group No.";
                Editable=FALSE }

    { 1100528606;2;Field  ;
                SourceExpr="Resource Group Name" }

    { 1100528612;2;Field  ;
                SourceExpr="Source No." }

    { 1100528614;2;Field  ;
                SourceExpr="Search Name" }

    { 1100528603;2;Field  ;
                SourceExpr="Visible in 4PS Construct";
                OnValidate=VAR
                             ResourceGroup@1100529000 : Record 11229384;
                           BEGIN
                             ResourceGroup.GET("Resource Group No.");
                             ResourceGroup.SetVisible(ResourceGroup.FIELDNO("Visible in 4PS Construct"), "Visible in 4PS Construct");
                           END;
                            }

    { 1100528611;2;Field  ;
                SourceExpr="Visible in GRP";
                OnValidate=VAR
                             ResourceGroup@1100529000 : Record 11229384;
                           BEGIN
                             ResourceGroup.GET("Resource Group No.");
                             ResourceGroup.SetVisible(ResourceGroup.FIELDNO("Visible in GRP"), "Visible in GRP");
                           END;
                            }

  }
  CODE
  {
    VAR
      UserIDFilter@1100528600 : Text;
      Text000@1100528608 : TextConst 'DEU=4PS-Konstrukt, GRP, Beide;ENU=4PS Construct,GRP,Both;NLD=4PS Construct,GRP,Beide';
      Text001@1100529000 : TextConst 'DEU=Sichtbar machen in;ENU=Make visible in;NLD=Zichtbaar maken in';
      Text002@1100529001 : TextConst 'DEU=Unsichtbar machen in;ENU=Make invisible in;NLD=Onzichtbaar maken in';

    PROCEDURE SetUserID@1100529000(UserIdCode@1100529000 : Text);
    BEGIN
      UserIDFilter := UserIdCode;
    END;

    PROCEDURE SetResourceGroupTypeFilter@1100529002(ResourceGroupType@1100529000 : Option);
    BEGIN
      SETRANGE("Resource Group Type", ResourceGroupType);
    END;

    LOCAL PROCEDURE BuildTmpTable@1100528601();
    VAR
      ResourceGroup@1100528601 : Record 11229384;
      VisibleResourceGroup@1100528600 : Record 11229385;
      UserSetup@1100528602 : Record 91;
      TypeNo@1100529000 : Integer;
    BEGIN
      IF (UserIDFilter = '') OR (NOT UserSetup.GET(UserIDFilter)) THEN
        EXIT;
      VisibleResourceGroup.COPYFILTERS(Rec);  //Save fiters
      RESET;
      DELETEALL;
      Rec.COPYFILTERS(VisibleResourceGroup);  //Restore filters
      VisibleResourceGroup.RESET;
      UserSetup.SETRANGE("User ID", UserIDFilter);
      UserSetup.FINDSET;
      REPEAT
        FOR TypeNo := 1 TO 2 DO BEGIN
          IF TypeNo = 1 THEN
            ResourceGroup.SETRANGE(Type, ResourceGroup.Type::Plant)
          ELSE
            ResourceGroup.SETRANGE(Type, ResourceGroup.Type::Discipline);
          ResourceGroup.SetCompanyPlanGroupFilters;
          IF ResourceGroup.FINDSET THEN BEGIN
            REPEAT
              IF NOT VisibleResourceGroup.GET(COMPANYNAME, UserSetup."User ID", ResourceGroup."No.") THEN BEGIN
                INIT;
                Company := COMPANYNAME;
                "User ID" := UserSetup."User ID";
                "Resource Group No." := ResourceGroup."No.";
              END ELSE
                Rec := VisibleResourceGroup;
              INSERT;
            UNTIL ResourceGroup.NEXT = 0;
          END;
        END
      UNTIL UserSetup.NEXT = 0;
    END;

    LOCAL PROCEDURE UpdateVisible@1100529001(NewVisible@1100529000 : Boolean);
    VAR
      TmpVisibleResourceGroup@1100529003 : TEMPORARY Record 11229385;
      ResourceGroup@1100529002 : Record 11229384;
      Choice@1100529001 : Integer;
      Instruction@1100529004 : Text;
    BEGIN
      TmpVisibleResourceGroup.COPY(Rec, TRUE);
      CurrPage.SETSELECTIONFILTER(TmpVisibleResourceGroup);
      IF TmpVisibleResourceGroup.FINDFIRST THEN BEGIN
        IF NewVisible THEN
          Instruction := Text001
        ELSE
          Instruction := Text002;
        Choice := STRMENU(Text000, 3, Instruction);
        IF Choice = 0 THEN
          EXIT;
        REPEAT
          ResourceGroup.GET(TmpVisibleResourceGroup."Resource Group No.");
          IF (Choice = 1) OR (Choice = 3) THEN
            ResourceGroup.SetVisible(ResourceGroup.FIELDNO("Visible in 4PS Construct"), NewVisible);
          IF (Choice = 2) OR (Choice = 3) THEN
            ResourceGroup.SetVisible(ResourceGroup.FIELDNO("Visible in GRP"), NewVisible);
        UNTIL TmpVisibleResourceGroup.NEXT = 0;
        BuildTmpTable;
      END;
    END;

    BEGIN
    END.
  }
}

