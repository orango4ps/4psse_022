OBJECT Table 5810 Rounding Residual Buffer
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    ReplicateData=No;
    CaptionML=[ENU=Rounding Residual Buffer;
               NOR=Buffer for avrundingsoverskudd];
  }
  FIELDS
  {
    { 1   ;   ;Item Ledger Entry No.;Integer      ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Item Ledger Entry No.;
                                                              NOR=Varepostnr.;
                                                              SVE=Artikeltrans.l”pnr] }
    { 2   ;   ;Adjusted Cost       ;Decimal       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Adjusted Cost;
                                                              NOR=Justert kost;
                                                              SVE=Justerad kostnad] }
    { 3   ;   ;Adjusted Cost (ACY) ;Decimal       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Adjusted Cost (ACY);
                                                              NOR=Justert kost (till.val)] }
    { 4   ;   ;Completely Invoiced ;Boolean       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Completely Invoiced;
                                                              NOR=Fullstendig fakturert;
                                                              SVE=F„rdigt fakturerat] }
    { 5   ;   ;No. of Hits         ;Integer       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=No. of Hits;
                                                              NOR=Antall treff;
                                                              SVE=Antal tr„ffar] }
  }
  KEYS
  {
    {    ;Item Ledger Entry No.                   ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    [External]
    PROCEDURE AddAdjustedCost@1(NewInboundEntryNo@1000 : Integer;NewAdjustedCost@1001 : Decimal;NewAdjustedCostACY@1002 : Decimal;NewCompletelyInvoiced@1003 : Boolean);
    BEGIN
      IF NOT HasNewCost(NewAdjustedCost,NewAdjustedCostACY) AND NewCompletelyInvoiced THEN BEGIN
        Retrieve(NewInboundEntryNo);
        EXIT;
      END;

      IF Retrieve(NewInboundEntryNo) THEN BEGIN
        "Adjusted Cost" := "Adjusted Cost" + NewAdjustedCost;
        "Adjusted Cost (ACY)" := "Adjusted Cost (ACY)" + NewAdjustedCostACY;
        IF NOT NewCompletelyInvoiced THEN
          "Completely Invoiced" := FALSE;
        MODIFY;
      END ELSE BEGIN
        INIT;
        "Adjusted Cost" := NewAdjustedCost;
        "Adjusted Cost (ACY)" := NewAdjustedCostACY;
        "Completely Invoiced" := NewCompletelyInvoiced;
        INSERT;
      END;
    END;

    [External]
    PROCEDURE UpdRoundingCheck@2(NewInboundEntryNo@1000 : Integer;NewAdjustedCost@1001 : Decimal;NewAdjustedCostACY@1002 : Decimal;RdngPrecision@1004 : Decimal;RndngPrecisionACY@1005 : Decimal);
    BEGIN
      IF NOT HasNewCost(NewAdjustedCost,NewAdjustedCostACY) THEN BEGIN
        Retrieve(NewInboundEntryNo);
        EXIT;
      END;

      IF Retrieve(NewInboundEntryNo) THEN BEGIN
        IF ((RdngPrecision >= NewAdjustedCost) OR
            (RndngPrecisionACY >= NewAdjustedCostACY)) AND
           (("Adjusted Cost" * NewAdjustedCost <= 0) AND
            ("Adjusted Cost (ACY)" * NewAdjustedCostACY <= 0))
        THEN
          "No. of Hits" := "No. of Hits" + 1
        ELSE
          "No. of Hits" := 0;

        "Adjusted Cost" := NewAdjustedCost;
        "Adjusted Cost (ACY)" := NewAdjustedCostACY;
        MODIFY;
      END ELSE BEGIN
        INIT;
        "Adjusted Cost" := NewAdjustedCost;
        "Adjusted Cost (ACY)" := NewAdjustedCostACY;
        INSERT;
      END;
    END;

    LOCAL PROCEDURE Retrieve@72(NewInboundEntryNo@1000 : Integer) : Boolean;
    BEGIN
      RESET;
      "Item Ledger Entry No." := NewInboundEntryNo;
      IF NOT FIND THEN BEGIN
        INIT;
        EXIT(FALSE);
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE HasNewCost@54(NewCost@1000 : Decimal;NewCostACY@1001 : Decimal) : Boolean;
    BEGIN
      EXIT((NewCost <> 0) OR (NewCostACY <> 0));
    END;

    BEGIN
    END.
  }
}

