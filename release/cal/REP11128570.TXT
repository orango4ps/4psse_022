OBJECT Report 11128570 Create DK Electronic Invoices
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVDK10.00 (13600);
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create Electronic Invoices;
               NOR=Opprette elektroniske fakturaer;
               SVE=Skapa elektroniska fakturor];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF NOT CurrReport.USEREQUESTPAGE THEN
                    InitLogInteraction;
                END;

    UsageCategory=ReportsAndAnalysis;
  }
  DATASET
  {
    { 5581;    ;DataItem;                    ;
               DataItemTable=Table112;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=VAR
                               SalesInvHeader@1101100000 : Record 112;
                             BEGIN
                               Counter := 0;

                               // Any electronic invoices?
                               SalesInvHeader.COPY("Sales Invoice Header");
                               SalesInvHeader.FILTERGROUP(8);
                               SalesInvHeader.SETFILTER("EAN No.",'<>%1','');
                               IF NOT SalesInvHeader.FINDFIRST THEN
                                 ERROR(Text003);

                               // All electronic invoices?
                               SalesInvHeader.SETRANGE("EAN No.",'');
                               IF SalesInvHeader.FINDFIRST THEN
                                 IF NOT CONFIRM(Text000,TRUE) THEN
                                   CurrReport.QUIT;
                               SalesInvHeader.SETRANGE("EAN No.");

                               // Some already sent?
                               SalesInvHeader.SETRANGE("Electronic Invoice Created",TRUE);
                               IF SalesInvHeader.FINDFIRST THEN
                                 IF NOT CONFIRM(Text001,TRUE) THEN
                                   CurrReport.QUIT;

                               SETFILTER("EAN No.",'<>%1','');
                             END;

               OnAfterGetRecord=VAR
                                  OIOUBLExportSalesInvoice@1060001 : Codeunit 11128579;
                                BEGIN
                                  OIOUBLExportSalesInvoice.ExportXML("Sales Invoice Header");

                                  IF LogInteraction THEN
                                    IF "Bill-to Contact No." <> '' THEN
                                      SegManagement.LogDocument(
                                        4,"No.",0,0,DATABASE::Contact,"Bill-to Contact No.","Salesperson Code",
                                        "Campaign No.","Posting Description",'')
                                    ELSE
                                      SegManagement.LogDocument(
                                        4,"No.",0,0,DATABASE::Customer,"Bill-to Customer No.","Salesperson Code",
                                        "Campaign No.","Posting Description",'');

                                  COMMIT;
                                  Counter := Counter + 1;
                                END;

               OnPostDataItem=BEGIN
                                MESSAGE(Text002, Counter);
                              END;

               ReqFilterFields=No.,Sell-to Customer No.,Bill-to Customer No.,EAN No.,Electronic Invoice Created }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnInit=BEGIN
               LogInteractionEnable := TRUE;
             END;

      OnOpenPage=BEGIN
                   InitLogInteraction;
                   LogInteractionEnable := LogInteraction;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1101100000;2;Field  ;
                  Name=LogInteraction;
                  CaptionML=[ENU=Log Interaction;
                             NOR=Logg Interaksjon;
                             SVE=Logga interaktion];
                  ToolTipML=ENU=Specifies if you want to record the related interactions with the involved contact person in the Interaction Log Entry table.;
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=LogInteraction;
                  Enabled=LogInteractionEnable }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1101100000 : TextConst 'ENU=One or more invoices that match your filter criteria are not electronic invoices and will be skipped.\\Do you want to continue?;NOR=En eller flere fakturaer som passer i ditt utvalg er ikke elektroniske fakturaer og hoppes over.\\Vil du fortsette?;SVE=En eller flera fakturor som passar i ditt urval „r inte elektroniska fakturor och hoppas ”ver.\\Vill du forts„tta?';
      Text001@1101100001 : TextConst 'ENU=One or more invoices that match your filter criteria have been created before.\\Do you want to continue?;NOR=En eller flere fakturaer som passer i ditt utvalg er opprettet tidligere.\\Vil du fortsette?;SVE=En eller flera fakturor som passar i ditt urval har skapats tidigare.\\Vill du forts„tta?';
      Text002@1101100003 : TextConst 'ENU=Successfully created %1 electronic invoice(s).;NOR=Opprettet %1 elektroniske fakturaer;SVE=Lyckades skapa %1 elektroniska fakturor';
      SegManagement@1101100007 : Codeunit 5051;
      Counter@1101100004 : Integer;
      Text003@1101100005 : TextConst 'ENU=There is nothing to create.;NOR=Det finnes ingenting † opprette.;SVE=Det finns inget att skapa.';
      LogInteraction@1101100006 : Boolean;
      LogInteractionEnable@19003940 : Boolean INDATASET;

    PROCEDURE InitLogInteraction@1101100000();
    BEGIN
      LogInteraction := SegManagement.FindInteractTmplCode(4) <> '';
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

