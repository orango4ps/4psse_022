OBJECT Report 11128521 Create NO Elec. Serv. Invoices
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVNO10.00 (10644),4PSSE.NO;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create Elec. Service Invoices;
               NOR=Opprette Elektroniske Servicefakturaer;
               SVE=Skapa elektroniska servicefakturor];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 5614;    ;DataItem;                    ;
               DataItemTable=Table5992;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=VAR
                               ServInvHeader@1101100000 : Record 5992;
                             BEGIN
                               Counter := 0;

                               // Any electronic service invoices?
                               ServInvHeader.COPY("Service Invoice Header");
                               ServInvHeader.FILTERGROUP(6);
                               ServInvHeader.SETRANGE("E-Invoice",TRUE);
                               IF NOT ServInvHeader.FINDFIRST THEN
                                 ERROR(Text003);

                               // All electronic service invoices?
                               ServInvHeader.SETRANGE("E-Invoice",FALSE);
                               IF ServInvHeader.FINDFIRST THEN
                                 IF NOT CONFIRM(Text000,TRUE) THEN
                                   CurrReport.QUIT;
                               ServInvHeader.SETRANGE("E-Invoice");

                               // Some already sent?
                               ServInvHeader.SETRANGE("E-Invoice Created",TRUE);
                               IF ServInvHeader.FINDFIRST THEN
                                 IF NOT CONFIRM(Text001) THEN
                                   CurrReport.QUIT;

                               SETRANGE("E-Invoice",TRUE);
                             END;

               OnAfterGetRecord=VAR
                                  EInvoiceExportServInvoice@1080000 : Codeunit 11128511;
                                BEGIN
                                  EInvoiceExportServInvoice.ExportServInvoice("Service Invoice Header");
                                  EInvoiceExportServInvoice.GetExportedFileInfo(TempEInvoiceTransferFile);
                                  TempEInvoiceTransferFile."Line No." := Counter + 1;
                                  TempEInvoiceTransferFile.INSERT;
                                  COMMIT;
                                  Counter := Counter + 1;
                                END;

               OnPostDataItem=VAR
                                EInvoiceExportCommon@1080000 : Codeunit 11128519;
                              BEGIN
                                EInvoiceExportCommon.DownloadEInvoiceFile(TempEInvoiceTransferFile);
                                MESSAGE(Text002,Counter);
                              END;

               ReqFilterFields=No.,Customer No.,Bill-to Customer No.,GLN,E-Invoice Created }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TempEInvoiceTransferFile@1080000 : TEMPORARY Record 11128516;
      Counter@1080002 : Integer;
      Text000@1080003 : TextConst 'ENU=One or more invoice documents that match your filter criteria are not electronic invoices and will be skipped.\\Do you want to continue?;NOR=Ett eller flere fakturadokumenter som samsvarer med filterkriteriene, er ikke elektroniske fakturaer og hoppes over.\\Vil du fortsette?';
      Text001@1080004 : TextConst 'ENU=One or more invoice documents that match your filter criteria have been created before.\\Do you want to continue?;NOR=Ett eller flere fakturadokumenter som samsvarer med filterkriteriene, er allerede opprettet.\\Vil du fortsette?';
      Text002@1080005 : TextConst 'ENU=Successfully created %1 electronic invoice documents.;NOR=Opprettet %1 elektroniske fakturadokumenter.';
      Text003@1080006 : TextConst 'ENU=Nothing to create.;NOR=Det finnes ingenting † opprette.;SVE=Det finns inget att skapa.';

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

