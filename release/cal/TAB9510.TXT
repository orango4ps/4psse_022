OBJECT Table 9510 Email Parameter
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Email Parameter;
               NOR=E-postparameter];
  }
  FIELDS
  {
    { 1   ;   ;Document No         ;Code20        ;CaptionML=[ENU=Document No;
                                                              NOR=Dokumentnr.;
                                                              SVE=Dokumentnr.] }
    { 2   ;   ;Document Type       ;Option        ;TableRelation="Sales Header"."Document Type";
                                                   CaptionML=[ENU=Document Type;
                                                              NOR=Dokumenttype;
                                                              SVE=Dokumenttyp];
                                                   OptionCaptionML=[ENU=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order;
                                                                    NOR=Tilbud,Ordre,Faktura,Kreditnota,Rammebestilling,Ordreretur;
                                                                    SVE=Offert,Order,Faktura,Kreditnota,Avropsorder,Returorder];
                                                   OptionString=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order }
    { 3   ;   ;Parameter Type      ;Option        ;CaptionML=[ENU=Parameter Type;
                                                              NOR=Parametertype];
                                                   OptionCaptionML=[ENU=" ,Subject,Address,Body";
                                                                    NOR=" ,Emne,Adresse,Br›dtekst"];
                                                   OptionString=[ ,Subject,Address,Body] }
    { 4   ;   ;Parameter Value     ;Text250       ;CaptionML=[ENU=Parameter Value;
                                                              NOR=Parameterverdi;
                                                              SVE=Parameterv„rde] }
    { 5   ;   ;Parameter BLOB      ;BLOB          ;CaptionML=[ENU=Parameter BLOB;
                                                              NOR=Parameter-BLOB] }
  }
  KEYS
  {
    {    ;Document No,Document Type,Parameter Type;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ParameterNotSupportedErr@1001 : TextConst 'ENU=Report usage is not supported.;NOR=Rapportbruk st›ttes ikke.';

    [External]
    PROCEDURE GetEntryWithReportUsage@2(DocumentNo@1002 : Code[20];ReportUsage@1001 : Integer;ParameterType@1000 : Option) : Boolean;
    VAR
      ReportSelections@1004 : Record 77;
      DocumentType@1003 : Option;
    BEGIN
      IF NOT ReportSelections.ReportUsageToDocumentType(DocumentType,ReportUsage) THEN
        EXIT(FALSE);
      EXIT(GET(DocumentNo,DocumentType,ParameterType));
    END;

    [External]
    PROCEDURE GetParameterValue@1() : Text;
    BEGIN
      CALCFIELDS("Parameter BLOB");
      IF "Parameter BLOB".HASVALUE THEN
        EXIT(GetTextFromBLOB);

      EXIT("Parameter Value");
    END;

    [External]
    PROCEDURE SaveParameterValue@3(DocumentNo@1000 : Code[20];DocumentType@1001 : Integer;ParameterType@1002 : Option;ParameterValue@1003 : Text);
    VAR
      ParameterAlreadyExists@1004 : Boolean;
    BEGIN
      ParameterAlreadyExists := GET(DocumentNo,DocumentType,ParameterType);
      IF NOT ParameterAlreadyExists THEN BEGIN
        INIT;
        "Document No" := DocumentNo;
        "Document Type" := DocumentType;
        "Parameter Type" := ParameterType;
      END;

      CLEAR("Parameter Value");
      CLEAR("Parameter BLOB");
      IF MAXSTRLEN("Parameter Value") > STRLEN(ParameterValue) THEN
        "Parameter Value" := COPYSTR(ParameterValue,1,MAXSTRLEN("Parameter Value"))
      ELSE
        WriteToBLOB(ParameterValue);

      IF ParameterAlreadyExists THEN
        MODIFY
      ELSE
        INSERT;
    END;

    [External]
    PROCEDURE SaveParameterValueWithReportUsage@5(DocumentNo@1000 : Code[20];ReportUsage@1001 : Integer;ParameterType@1002 : Option;ParameterValue@1003 : Text);
    VAR
      ReportSelections@1005 : Record 77;
      DocumentType@1004 : Option;
    BEGIN
      IF NOT ReportSelections.ReportUsageToDocumentType(DocumentType,ReportUsage) THEN
        ERROR(ParameterNotSupportedErr);
      SaveParameterValue(DocumentNo,DocumentType,ParameterType,ParameterValue);
    END;

    LOCAL PROCEDURE WriteToBLOB@15(ParameterValue@1004 : Text);
    VAR
      TempBlob@1000 : Record 99008535;
    BEGIN
      CLEAR("Parameter BLOB");
      TempBlob.WriteAsText(ParameterValue,TEXTENCODING::Windows);
      "Parameter BLOB" := TempBlob.Blob;
    END;

    LOCAL PROCEDURE GetTextFromBLOB@14() : Text;
    VAR
      TempBlob@1000 : Record 99008535;
      CR@1001 : Text[1];
    BEGIN
      CALCFIELDS("Parameter BLOB");
      CR[1] := 10;
      TempBlob.Blob := "Parameter BLOB";
      EXIT(TempBlob.ReadAsText(CR,TEXTENCODING::Windows));
    END;

    BEGIN
    END.
  }
}

