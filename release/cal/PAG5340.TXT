OBJECT Page 5340 CRM Systemuser List
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Users - Microsoft Dynamics 365 for Sales;
               NOR=Brukere - Microsoft Dynamics 365 for Sales;
               SVE=Anv„ndare - Microsoft Dynamics 365 for Sales];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table5340;
    SourceTableView=SORTING(FullName);
    PageType=List;
    OnInit=BEGIN
             CODEUNIT.RUN(CODEUNIT::"CRM Integration Management");
           END;

    OnOpenPage=BEGIN
                 SETRANGE(IsIntegrationUser,FALSE);
                 SETRANGE(IsDisabled,FALSE);
                 SETRANGE(IsLicensed,TRUE);
               END;

    OnAfterGetRecord=VAR
                       CRMIntegrationRecord@1000 : Record 5331;
                       SalespersonPurchaser@1004 : Record 13;
                       RecordID@1001 : RecordID;
                     BEGIN
                       IF CRMIntegrationRecord.FindRecordIDFromID(SystemUserId,DATABASE::"Salesperson/Purchaser",RecordID) THEN BEGIN
                         IF SalespersonPurchaser.GET(RecordID) THEN
                           InsertUpdateTempCRMSystemUser(SalespersonPurchaser.Code,FALSE)
                         ELSE
                           InsertUpdateTempCRMSystemUser('',FALSE);
                         IF CurrentlyCoupledCRMSystemuser.SystemUserId = SystemUserId THEN BEGIN
                           Coupled := Coupled::Current;
                           FirstColumnStyle := 'Strong';
                         END ELSE BEGIN
                           Coupled := Coupled::Yes;
                           FirstColumnStyle := 'Subordinate';
                         END
                       END ELSE BEGIN
                         InsertUpdateTempCRMSystemUser('',FALSE);
                         Coupled := Coupled::No;
                         FirstColumnStyle := 'None';
                       END;
                     END;

    ActionList=ACTIONS
    {
      { 7       ;    ;ActionContainer;
                      ActionContainerType=NewDocumentItems }
      { 8       ;1   ;Action    ;
                      Name=CreateFromCRM;
                      CaptionML=[ENU=Create Salesperson in Business Central;
                                 NOR=Opprett selger i Business Central;
                                 SVE=Skapa s„ljare i Business Central];
                      ToolTipML=[ENU=Create the Dynamics 365 for Sales user as a salesperson in Business Central.;
                                 NOR=Opprett Dynamics 365 for Sales-brukeren som en selger i Business Central.;
                                 SVE=Skapa Dynamics 365 for Sales-anv„ndaren som en s„ljare i Business Central.];
                      ApplicationArea=#Suite;
                      Promoted=Yes;
                      Visible=ShowCouplingControls;
                      PromotedIsBig=Yes;
                      Image=NewCustomer;
                      PromotedCategory=Process;
                      PromotedOnly=Yes;
                      OnAction=VAR
                                 CRMSystemuser@1000 : Record 5340;
                                 CRMIntegrationManagement@1001 : Codeunit 5330;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(CRMSystemuser);
                                 CRMIntegrationManagement.CreateNewRecordsFromCRM(CRMSystemuser);
                               END;
                                }
      { 10      ;1   ;Action    ;
                      Name=Couple;
                      CaptionML=[ENU=Couple;
                                 NOR=Koble;
                                 SVE=Koppla];
                      ToolTipML=[ENU=Link the user in Dynamics 365 for Sales to a user in Business Central.;
                                 NOR=Koble brukeren i Dynamics 365 for Sales til en bruker i Business Central.;
                                 SVE=Koppla anv„ndaren i Dynamics 365 for Sales till en anv„ndare i Business Central.];
                      ApplicationArea=#Suite;
                      Promoted=Yes;
                      Visible=ShowCouplingControls;
                      PromotedIsBig=Yes;
                      Image=LinkAccount;
                      PromotedCategory=Process;
                      PromotedOnly=Yes;
                      OnAction=VAR
                                 SalespersonPurchaser@1002 : Record 13;
                                 CRMIntegrationRecord@1003 : Record 5331;
                                 CRMIntegrationManagement@1000 : Codeunit 5330;
                                 CRMCouplingManagement@1001 : Codeunit 5331;
                                 OldRecordId@1006 : RecordID;
                                 Synchronize@1004 : Boolean;
                                 Direction@1005 : Option;
                               BEGIN
                                 TempCRMSystemuser.RESET;
                                 TempCRMSystemuser.SETRANGE(IsSyncWithDirectory,TRUE);
                                 IF TempCRMSystemuser.FINDSET THEN
                                   REPEAT
                                     IF TempCRMSystemuser.FirstName <> '' THEN BEGIN
                                       SalespersonPurchaser.GET(TempCRMSystemuser.FirstName);
                                       CRMIntegrationManagement.CoupleCRMEntity(
                                         SalespersonPurchaser.RECORDID,TempCRMSystemuser.SystemUserId,Synchronize,Direction);
                                     END ELSE BEGIN
                                       CRMIntegrationRecord.FindRecordIDFromID(
                                         TempCRMSystemuser.SystemUserId,DATABASE::"Salesperson/Purchaser",OldRecordId);
                                       CRMCouplingManagement.RemoveCoupling(OldRecordId);
                                     END;
                                   UNTIL TempCRMSystemuser.NEXT = 0;
                                 TempCRMSystemuser.MODIFYALL(IsSyncWithDirectory,FALSE);
                                 TempCRMSystemuser.MODIFYALL(IsDisabled,FALSE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                GroupType=Repeater }

    { 4   ;2   ;Field     ;
                CaptionML=[ENU=Name;
                           NOR=Navn;
                           SVE=Namn];
                ToolTipML=[ENU=Specifies data from a corresponding field in a Dynamics 365 for Sales entity. For more information about Dynamics 365 for Sales, see Dynamics 365 for Sales Help Center.;
                           NOR=Angir data fra et tilsvarende felt i en Dynamics 365 for Sales-enhet. Hvis du vil ha mer informasjon om Dynamics 365 for Sales, kan du se hjelpesenteret for Dynamics 365 for Sales.;
                           SVE=Anger data fr†n ett motsvarande f„lt i en Dynamics 365 for Sales-enhet. Mer information om Dynamics 365 for Sales finns i hj„lpcentret f”r Dynamics 365 for Sales.];
                ApplicationArea=#Suite;
                SourceExpr=FullName;
                Editable=FALSE;
                StyleExpr=FirstColumnStyle }

    { 5   ;2   ;Field     ;
                ExtendedDatatype=E-Mail;
                CaptionML=[ENU=Email Address;
                           NOR=E-postadresse;
                           SVE=E-postadress];
                ToolTipML=[ENU=Specifies the email address.;
                           NOR=Angir e-postadressen.;
                           SVE=Anger e-postadressen.];
                ApplicationArea=#Suite;
                SourceExpr=InternalEMailAddress;
                Editable=FALSE }

    { 6   ;2   ;Field     ;
                CaptionML=[ENU=Mobile Phone;
                           NOR=Mobiltelefon;
                           SVE=Mobiltelefon];
                ToolTipML=[ENU=Specifies data from a corresponding field in a Dynamics 365 for Sales entity. For more information about Dynamics 365 for Sales, see Dynamics 365 for Sales Help Center.;
                           NOR=Angir data fra et tilsvarende felt i en Dynamics 365 for Sales-enhet. Hvis du vil ha mer informasjon om Dynamics 365 for Sales, kan du se hjelpesenteret for Dynamics 365 for Sales.;
                           SVE=Anger data fr†n ett motsvarande f„lt i en Dynamics 365 for Sales-enhet. Mer information om Dynamics 365 for Sales finns i hj„lpcentret f”r Dynamics 365 for Sales.];
                ApplicationArea=#Suite;
                SourceExpr=MobilePhone;
                Editable=FALSE }

    { 3   ;2   ;Field     ;
                Name=Coupled;
                CaptionML=[ENU=Coupled;
                           NOR=Koblet;
                           SVE=Kopplade];
                ToolTipML=[ENU=Specifies if the Dynamics 365 for Sales record is coupled to Dynamics 365.;
                           NOR=Angir om Dynamics 365 for Sales-posten er koblet til Dynamics 365.;
                           SVE=Anger om Dynamics 365 for Sales-posten „r kopplad till Dynamics 365.];
                OptionCaptionML=[ENU=Yes,No,Current;
                                 NOR=Ja,Nei,Gjeldende;
                                 SVE=Ja,Nej,Aktuell];
                ApplicationArea=#Suite;
                SourceExpr=Coupled;
                Editable=FALSE }

    { 9   ;2   ;Field     ;
                Name=SalespersonPurchaserCode;
                CaptionML=[ENU=Salesperson/Purchaser Code;
                           NOR=Selger-/innkj›perkode;
                           SVE=S„ljar-/ink”parkod];
                ToolTipML=[ENU=Specifies the code for the salesperson or purchaser.;
                           NOR=Angir koden for selgeren eller innkj›peren.;
                           SVE=Anger kod f”r s„ljare eller ink”pare.];
                ApplicationArea=#Suite;
                SourceExpr=TempCRMSystemuser.FirstName;
                TableRelation=Salesperson/Purchaser.Code;
                Enabled=ShowCouplingControls;
                OnValidate=VAR
                             SalespersonPurchaser@1000 : Record 13;
                           BEGIN
                             IF TempCRMSystemuser.FirstName <> '' THEN BEGIN
                               SalespersonPurchaser.GET(TempCRMSystemuser.FirstName);
                               InsertUpdateTempCRMSystemUser(SalespersonPurchaser.Code,TRUE);
                               CleanDuplicateSalespersonRecords(SalespersonPurchaser.Code,SystemUserId);
                             END ELSE
                               IF (TempCRMSystemuser.FirstName = '') AND (Coupled = Coupled::Yes) THEN
                                 InsertUpdateTempCRMSystemUser('',TRUE);
                             CurrPage.UPDATE(FALSE);
                           END;

                OnLookup=VAR
                           SalespersonPurchaser@1001 : Record 13;
                           SalespersonsPurchasers@1000 : Page 14;
                         BEGIN
                           SalespersonsPurchasers.LOOKUPMODE(TRUE);
                           IF SalespersonsPurchasers.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             SalespersonsPurchasers.GETRECORD(SalespersonPurchaser);
                             InsertUpdateTempCRMSystemUser(SalespersonPurchaser.Code,TRUE);
                             CleanDuplicateSalespersonRecords(SalespersonPurchaser.Code,SystemUserId);
                           END;
                           CurrPage.UPDATE(FALSE);
                         END;
                          }

  }
  CODE
  {
    VAR
      CurrentlyCoupledCRMSystemuser@1003 : Record 5340;
      TempCRMSystemuser@1002 : TEMPORARY Record 5340;
      Coupled@1001 : 'Yes,No,Current';
      FirstColumnStyle@1004 : Text;
      ShowCouplingControls@1000 : Boolean;

    [External]
    PROCEDURE SetCurrentlyCoupledCRMSystemuser@2(CRMSystemuser@1000 : Record 5340);
    BEGIN
      CurrentlyCoupledCRMSystemuser := CRMSystemuser;
    END;

    LOCAL PROCEDURE InsertUpdateTempCRMSystemUser@7(SalespersonCode@1001 : Code[20];SyncNeeded@1002 : Boolean);
    BEGIN
      // FirstName is used to store coupled/ready to couple Salesperson
      // IsSyncWithDirectory is used to mark CRM User for coupling
      IF TempCRMSystemuser.GET(SystemUserId) THEN BEGIN
        IF NOT TempCRMSystemuser.IsDisabled OR SyncNeeded THEN BEGIN
          TempCRMSystemuser.FirstName := SalespersonCode;
          TempCRMSystemuser.IsSyncWithDirectory := SyncNeeded;
          TempCRMSystemuser.IsDisabled := SyncNeeded;
          TempCRMSystemuser.MODIFY;
        END
      END ELSE BEGIN
        TempCRMSystemuser.SystemUserId := SystemUserId;
        TempCRMSystemuser.FirstName := SalespersonCode;
        TempCRMSystemuser.IsSyncWithDirectory := SyncNeeded;
        TempCRMSystemuser.IsDisabled := SyncNeeded;
        TempCRMSystemuser.INSERT;
      END;
    END;

    LOCAL PROCEDURE CleanDuplicateSalespersonRecords@1(SalesPersonCode@1001 : Code[20];CRMUserId@1000 : GUID);
    BEGIN
      TempCRMSystemuser.RESET;
      TempCRMSystemuser.SETRANGE(FirstName,SalesPersonCode);
      TempCRMSystemuser.SETFILTER(SystemUserId,'<>' + FORMAT(CRMUserId));
      IF TempCRMSystemuser.FINDFIRST THEN BEGIN
        TempCRMSystemuser.IsDisabled := TRUE;
        TempCRMSystemuser.FirstName := '';
        TempCRMSystemuser.MODIFY;
      END;
    END;

    [External]
    PROCEDURE Initialize@3(NewShowCouplingControls@1000 : Boolean);
    BEGIN
      ShowCouplingControls := NewShowCouplingControls;
    END;

    BEGIN
    END.
  }
}

