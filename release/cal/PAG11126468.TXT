OBJECT Page 11126468 Plan Service Orders
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Serviceauftr„ge planen;
               ENU=Plan Service Orders;
               NLD=Serviceorders plannen];
    SourceTable=Table11012823;
    PageType=NavigatePage;
    OnInit=BEGIN
             MinimalUnoccupiedHoursEnable := TRUE;
             MinimalUnoccupiedHours := 1;
             KeepPlanAgreementWindow := TRUE;
           END;

    OnOpenPage=BEGIN
                 //**4PS.sn
                 DPA_IsHardFilter := DPA_SetFilters(CurrPage.OBJECTID(FALSE));
                 //**4PS.en
                 UpdateEditable;
                 WizardStep := 1;
                 ShowStep(TRUE);
                 UpdateValues;
               END;

    OnNewRecord=BEGIN
                  //**4PS.sn
                  DPA_SetDefaultDepartment;
                  //**4PS.en
                END;

    OnInsertRecord=VAR
                     xRecRef@1100525000 : RecordRef;
                   BEGIN
                     //**4PS.sn
                     xRecRef.GETTABLE(xRec);
                     DPA_SetDefaultDpmtFromNoSeries(xRecRef);
                     //**4PS.en
                   END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100528618;1 ;Action    ;
                      Name=Back;
                      CaptionML=[DEU=< &Zurck;
                                 ENU=< &Back;
                                 NLD=< &Vorige;
                                 NOR=< &Tilbake;
                                 SVE=< &Tillbaka];
                      Enabled=BackEnable;
                      InFooterBar=Yes;
                      Image=PreviousRecord;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 PreCheckStatus(FALSE);
                                 ShowStep(FALSE);
                                 PostCheckStatus(FALSE);
                                 WizardStep -= 1;
                                 ShowStep(TRUE);
                                 CurrPage.UPDATE(TRUE);
                               END;
                                }
      { 1100528617;1 ;Action    ;
                      Name=Next;
                      CaptionML=[DEU=&Weiter >;
                                 ENU=&Next >;
                                 NLD=Vo&lgende >;
                                 NOR=&Neste >;
                                 SVE=&N„sta >];
                      Enabled=NextEnable;
                      InFooterBar=Yes;
                      Image=NextRecord;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 PreCheckStatus(TRUE);
                                 ShowStep(FALSE);
                                 PostCheckStatus(TRUE);
                                 WizardStep += 1;
                                 ShowStep(TRUE);
                                 CurrPage.UPDATE(TRUE);
                               END;
                                }
      { 1100528616;1 ;Action    ;
                      Name=Finish;
                      CaptionML=[DEU=B&eenden;
                                 ENU=&Finish;
                                 NLD=Voltooie&n;
                                 NOR=&Avslutt;
                                 SVE=&Slutf”r];
                      Enabled=FinishEnable;
                      InFooterBar=Yes;
                      Image=Completed;
                      OnAction=BEGIN
                                 PostCheckStatus(TRUE);
                                 FinishWizard;
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100528602;1;Group  ;
                CaptionML=[DEU=Employee;
                           ENU=Employee;
                           NLD=Medewerker;
                           NOR=Ansatt;
                           SVE=Personal];
                Visible=Step1Visible;
                GroupType=Group }

    { 1100525000;2;Field  ;
                CaptionML=[DEU=Serviceauftr„ge planen;
                           ENU=Plan Serivce Orders;
                           NLD=Serviceorders plannen];
                Style=Strong;
                StyleExpr=TRUE }

    { 1100528601;2;Field  ;
                CaptionML=[DEU=Mitarbeiternr.;
                           ENU=Employee No.;
                           NLD=Werknemer;
                           NOR=Ansattnr;
                           SVE=Anst„llningsnr];
                SourceExpr=EmployeeResourceNo;
                TableRelation="Resource (WOP)" WHERE (Schedule=FILTER(ServiceOrder|Both));
                Editable=FALSE }

    { 1100528607;2;Field  ;
                CaptionML=[DEU=Name Mitarbeiter;
                           ENU=Employee Name;
                           NLD=Werknemer naam;
                           NOR=Navn p† ansatt;
                           SVE=Namn p† anst„lld];
                SourceExpr=EmployeeName;
                Editable=FALSE }

    { 1100528606;2;Field  ;
                CaptionML=[DEU=Anz. Teammitglieder;
                           ENU=No. of Team Members;
                           NLD=Aantal teamleden];
                BlankZero=Yes;
                SourceExpr=NoOfTeamMembers;
                Editable=FALSE }

    { 1100528603;2;Field  ;
                CaptionML=[DEU=Planungsdatum ab;
                           ENU=Plan Date From;
                           NLD=Plandatum vanaf;
                           NOR=Plandato fra;
                           SVE=Plandatum fr†n];
                SourceExpr=PlanDateFrom;
                OnValidate=BEGIN
                             IF PlanDateUntil = 0D THEN
                               PlanDateUntil := PlanDateFrom;
                             UpdateValues;
                           END;
                            }

    { 1100528608;2;Field  ;
                CaptionML=[DEU=Planungsdatum bis einschl.;
                           ENU=Plan Date Until;
                           NLD=Plandatum t/m;
                           NOR=Plandato til;
                           SVE=Plandatum till];
                SourceExpr=PlanDateUntil;
                OnValidate=BEGIN
                             UpdateValues;
                           END;
                            }

    { 1100528604;2;Field  ;
                CaptionML=[DEU=Gesamtdauer Zeit;
                           ENU=Total Duration Time;
                           NLD=Totale doorlooptijd];
                SourceExpr=TotalDurationTime;
                Editable=FALSE }

    { 1100528619;2;Field  ;
                CaptionML=[DEU=Korrigierte Gesamtdauer Zeit;
                           ENU=Corrected Total Duration Time;
                           NLD=Gecorrigeerde totale doorlooptijd];
                BlankZero=Yes;
                SourceExpr=CorrectedTotalDurationTime;
                MinValue=0 }

    { 1100528500;2;Field  ;
                CaptionML=[DEU=Total veranschlagte Stunden;
                           ENU=Total Budget Hours;
                           NLD=Totaal begrote uren];
                SourceExpr=TotalBudgetHours;
                Editable=False }

    { 1100528614;2;Field  ;
                CaptionML=[DEU=Anz. verfgbare Stunden (circa);
                           ENU=No. of Available Hours (approximate);
                           NLD=Aantal beschikbare uren (benaderd)];
                SourceExpr=NoOfAvailableHoursByTeam;
                Editable=FALSE;
                OnDrillDown=VAR
                              ResourceWOP@1100528600 : Record 11229278;
                              ServiceOrderPlanManagement@1100528601 : Codeunit 11012868;
                            BEGIN
                              ResourceWOP.SETFILTER("No.", TmpCollectivePlanTeam.GetResourceFilter(TmpCollectivePlanTeam));
                              ServiceOrderPlanManagement.ShowAvailableHoursResources(ResourceWOP, ServiceOrder, PlanDateFrom, PlanDateUntil, FALSE);
                            END;
                             }

    { 1100528609;2;Field  ;
                CaptionML=[DEU=Prozentuale šberlappung;
                           ENU=Overlap Percentage;
                           NLD=Overlap percentage];
                SourceExpr=OverlapPercentage;
                MinValue=-100;
                MaxValue=100 }

    { 1100528611;2;Field  ;
                CaptionML=[DEU=Planvereinbarung behalten;
                           ENU=Keep Plan Agreement;
                           NLD=Planningsafspraak vasthouden];
                SourceExpr=KeepPlanAgreementWindow }

    { 1100528613;2;Field  ;
                CaptionML=[DEU=Vorhandene Arbeitsauftr„ge ignorieren;
                           ENU=Ignore Existing Work Orders;
                           NLD=Bestaande werkorders negeren];
                SourceExpr=IgnoreExistingWorkOrders }

    { 1100528615;1;Group  ;
                CaptionML=[DEU=šbriges;
                           ENU=Other;
                           NLD=Overig;
                           NOR=vrige;
                           SVE=™vriga];
                Visible=Step2Visible;
                GroupType=Group }

    { 1100528605;2;Field  ;
                CaptionML=[DEU=Fahrtzeiten;
                           ENU=Travel Time;
                           NLD=Reistijd;
                           NOR=Reisetid;
                           SVE=Restid];
                SourceExpr=TravelDuration }

    { 1100528612;2;Field  ;
                CaptionML=[DEU=Zuordnen mehreren Tagen;
                           ENU=Assign to Several Days;
                           NLD=Toekennen aan meerdere dagen;
                           NOR=Tilordne flere dager;
                           SVE=Tilldela till flera dagar];
                SourceExpr=OccupySeveralDays;
                OnValidate=BEGIN
                             OccupySeveralDaysOnAfterValida;
                           END;
                            }

    { 1100528610;2;Field  ;
                Name=MinimalUnoccupiedHours;
                CaptionML=[DEU=Min. Anzahl freier Stunden;
                           ENU=Minimal Unoccupied Hours;
                           NLD=Minimaal aantal vrije uren;
                           NOR=Laveste antall ubemannede timer;
                           SVE=L„gsta obemannade timmar];
                SourceExpr=MinimalUnoccupiedHours;
                Enabled=MinimalUnoccupiedHoursEnable }

    { 1100528600;2;Field  ;
                CaptionML=[DEU=Prozentsatz Reservierte Stunden fr Meldungen;
                           ENU=Percentage Reserve Hours for Calls;
                           NLD=Percentage gereserveerde uren voor meldingen];
                SourceExpr=ReserveLaborPercentage }

    { 1100527000;2;Field  ;
                CaptionML=[DEU=Exportieren in Auáendienst;
                           ENU=Export to FSA;
                           NLD=Exporteren naar buitendienst;
                           NOR=Eksporter til FSA;
                           SVE=Export till FSA];
                SourceExpr=ExportToFSA }

  }
  CODE
  {
    VAR
      ServiceOrder@1100528604 : Record 11012823;
      TmpCollectivePlanTeam@1100528613 : TEMPORARY Record 11071979;
      ServiceOrderPlanManagement@1100528624 : Codeunit 11012868;
      EmployeeResourceNo@1100528600 : Code[20];
      EmployeeName@1100528603 : Text[100];
      PlanDateFrom@1100528601 : Date;
      PlanDateUntil@1100528607 : Date;
      TravelDuration@1100528602 : Duration;
      Text000@1100528605 : TextConst 'DEU=Mitarbeiter nicht eingegeben;ENU=Employee not filled;NLD=Werknemer niet gevuld;NOR=Ansatt er ikke fylt ut;SVE=Anst„lld „r inte ifyllt';
      Text001@1100528606 : TextConst 'DEU=''Planungsdatum ab'' und ''Planungsdatum bis einschl.'' mssen beide eingegeben worden sein;ENU=Plan Date From and Plan Date To must be filled;NLD=Plandatum vanaf en Plandatum t/m dienen beide gevuld te zijn;NOR=Plandato fra og Plandato til m† v‘re fylt ut;SVE=Plandatum fr†n och plandatum till m†ste fyllas i';
      Text002@1100528608 : TextConst 'DEU=''Planungsdatum ab'' muss vor dem ''Planungsdatum bis einschl.'' liegen;ENU=Plan Date From must occur before Plan Date Until;NLD=Plandatum vanaf moet liggen voor Plandatum t/m;NOR=Plandato fra m† v‘re f›r plandato til;SVE=Plandatum fr†n m†ste intr„ffa f”re plandatum till';
      MinimalUnoccupiedHours@1100528609 : Decimal;
      OccupySeveralDays@1100528610 : Boolean;
      MinimalUnoccupiedHoursEnable@19064514 : Boolean INDATASET;
      WizardStep@1100528616 : Integer;
      Step1Visible@1100528619 : Boolean INDATASET;
      Step2Visible@1100528618 : Boolean INDATASET;
      FinishEnable@1100528622 : Boolean INDATASET;
      BackEnable@1100528621 : Boolean INDATASET;
      NextEnable@1100528620 : Boolean INDATASET;
      DPA_IsHardFilter@1100525000 : Boolean;
      ReserveLaborPercentage@1100528611 : Decimal;
      TotalDurationTime@1100528612 : Decimal;
      CorrectedTotalDurationTime@1100528626 : Decimal;
      NoOfTeamMembers@1100528614 : Integer;
      OverlapPercentage@1100528615 : Decimal;
      PlanIndividually@1100525001 : Boolean;
      KeepPlanAgreementWindow@1100528617 : Boolean;
      NoOfAvailableHoursByTeam@1100528623 : Decimal;
      IgnoreExistingWorkOrders@1100528625 : Boolean;
      ExportToFSA@1100527000 : Boolean;
      TotalBudgetHours@1100528500 : Decimal;
      PlanSeqNo@1100527450 : Integer;

    PROCEDURE SetSelection@1100528603(VAR IServiceOrder@1100528600 : Record 11012823;VAR ICollectivePlanTeam@1100528601 : Record 11071979;IPlanIndividually@1100525000 : Boolean;IPlanDateFrom@1100528605 : Date;IPlanDateUntil@1100528606 : Date;IPlanSeqNo@1100527450 : Integer);
    VAR
      Resource@1100528602 : Record 11229278;
      ServiceOrderPlanManagement@1100528604 : Codeunit 11012868;
    BEGIN
      ServiceOrder.COPY(IServiceOrder);
      TmpCollectivePlanTeam.COPY(ICollectivePlanTeam, TRUE);
      IF NOT ServiceOrder.FINDFIRST THEN
        ServiceOrder.INIT;
      IF NOT IPlanIndividually THEN BEGIN
        EmployeeResourceNo := ICollectivePlanTeam.GetResourceNoTeamLeader(ICollectivePlanTeam);
        IF Resource.GET(EmployeeResourceNo) THEN
          EmployeeName := Resource.Name;
      END ELSE
        EmployeeResourceNo := ICollectivePlanTeam.GetNoOfResourcesInTeamText(ICollectivePlanTeam);
      IF NOT IPlanIndividually THEN
        NoOfTeamMembers := ICollectivePlanTeam.COUNT
      ELSE
        NoOfTeamMembers := 0;
      TotalDurationTime := ServiceOrderPlanManagement.GetTotalDurationTime(IServiceOrder);
      PlanIndividually := IPlanIndividually;
      PlanDateFrom := IPlanDateFrom;
      PlanDateUntil := IPlanDateUntil;
      TotalBudgetHours := ServiceOrderPlanManagement.GetTotalBudgetHours(IServiceOrder);
      PlanSeqNo := IPlanSeqNo;
    END;

    LOCAL PROCEDURE UpdateEditable@1100528600();
    BEGIN
      MinimalUnoccupiedHoursEnable := OccupySeveralDays;
    END;

    LOCAL PROCEDURE OccupySeveralDaysOnAfterValida@19062635();
    BEGIN
      UpdateEditable;
    END;

    LOCAL PROCEDURE PreCheckStatus@1100528602(IForward@1100528600 : Boolean);
    BEGIN
      CASE WizardStep OF
        1:
          BEGIN
            IF IForward THEN BEGIN
              IF EmployeeResourceNo = '' THEN
                ERROR(Text000);
              IF (PlanDateFrom = 0D) OR (PlanDateUntil = 0D) THEN
                ERROR(Text001);
              IF PlanDateFrom > PlanDateUntil THEN
                ERROR(Text002);
            END;
          END;
      END;
    END;

    LOCAL PROCEDURE PostCheckStatus@1100528604(IForward@1100528600 : Boolean);
    VAR
      Resource@1100528602 : Record 11229278;
      MaintenanceManagement@1100528601 : Codeunit 11012829;
    BEGIN
      CASE WizardStep OF
        1:
          BEGIN
            IF NOT Resource.GET(EmployeeResourceNo) THEN
              Resource.INIT;
            ReserveLaborPercentage := MaintenanceManagement.GetResLaborPerc(PlanDateFrom, EmployeeResourceNo, '', '');
          END;
      END;
    END;

    LOCAL PROCEDURE ShowStep@2(Visible@1001 : Boolean);
    BEGIN
      CASE WizardStep OF
        1:
          BEGIN
            Step1Visible := Visible;
            IF Visible THEN BEGIN
              FinishEnable := FALSE;
              BackEnable := FALSE;
              NextEnable := TRUE;
            END;
          END;
        2:
          BEGIN
            Step2Visible := Visible;
            IF Visible THEN BEGIN
              FinishEnable := TRUE;
              BackEnable := TRUE;
              NextEnable := FALSE;
            END;
          END;
      END;
    END;

    LOCAL PROCEDURE FinishWizard@1100528606();
    VAR
      TotalDurationTimeCorrectionFactor@1100528600 : Decimal;
    BEGIN
      CLEAR(ServiceOrderPlanManagement);
      IF (CorrectedTotalDurationTime <> 0) AND (TotalDurationTime <> 0) THEN
        TotalDurationTimeCorrectionFactor := CorrectedTotalDurationTime / TotalDurationTime;
      ServiceOrderPlanManagement.SetPlanSOExportToFSA(ExportToFSA);
      IF NOT PlanIndividually THEN
        ServiceOrderPlanManagement.PlanServiceOrdersByResource(
          ServiceOrder, TmpCollectivePlanTeam, PlanDateFrom, PlanDateUntil, TravelDuration,
          MinimalUnoccupiedHours, OccupySeveralDays, ReserveLaborPercentage, OverlapPercentage, KeepPlanAgreementWindow,
          IgnoreExistingWorkOrders, TotalDurationTimeCorrectionFactor, PlanSeqNo)
      ELSE
        ServiceOrderPlanManagement.PlanServiceOrdersResourceByResource(
          ServiceOrder, TmpCollectivePlanTeam, PlanDateFrom, PlanDateUntil, TravelDuration,
          MinimalUnoccupiedHours, OccupySeveralDays, ReserveLaborPercentage, OverlapPercentage, KeepPlanAgreementWindow,
          IgnoreExistingWorkOrders, TotalDurationTimeCorrectionFactor, PlanSeqNo);
      CurrPage.CLOSE;
    END;

    LOCAL PROCEDURE UpdateValues@1100528601();
    VAR
      ResourceWOP@1100528601 : Record 11229278;
      ServiceOrderPlanManagement@1100528600 : Codeunit 11012868;
    BEGIN
      ResourceWOP.SETFILTER("No.", TmpCollectivePlanTeam.GetResourceFilter(TmpCollectivePlanTeam));
      NoOfAvailableHoursByTeam := ServiceOrderPlanManagement.GetAvailableHoursByTeam(ResourceWOP, ServiceOrder, PlanDateFrom, PlanDateUntil, IgnoreExistingWorkOrders);
    END;

    PROCEDURE ShowErrorsIfAvailable@1100528605();
    BEGIN
      ServiceOrderPlanManagement.ShowErrorsIfAvailable;
    END;

    BEGIN
    END.
  }
}

