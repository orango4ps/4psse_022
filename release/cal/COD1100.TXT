OBJECT Codeunit 1100 Cost Account Mgt
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    Permissions=TableData 15=rm,
                TableData 17=rm,
                TableData 348=r,
                TableData 1104=rimd,
                TableData 1112=r,
                TableData 1113=r;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      CostAccSetup@1000 : Record 1108;
      GLAcc@1001 : Record 15;
      CostType@1002 : Record 1103;
      Window@1003 : Dialog;
      i@1004 : Integer;
      NoOfCostTypes@1005 : Integer;
      NoOfGLAcc@1006 : Integer;
      RecsProcessed@1007 : Integer;
      RecsCreated@1008 : Integer;
      CostTypeExists@1009 : Boolean;
      Text000@1012 : TextConst 'ENU=This function transfers all income statement accounts from the chart of accounts to the chart of cost types.\\All types including Heading, Begin-Total, and End-Total are transferred.\General ledger accounts that have the same number as an existing cost type are not transferred.\\Do you want to start the job?;NOR=Denne funksjonen overf›rer alle resultatregnskapskontoer fra kontoplanen til oversikten over kosttyper.\\Alle linjetyper, inkludert overskrift, fra-sum og til-sum, overf›res.\Finanskontoer som har samme nummer som en kosttype som finnes fra f›r, overf›res ikke.\\Vil du starte jobben?;SVE=I funktionen ”verf”rs alla inkommande transaktionskonton fr†n kontoplanen till kostnadstyper.\\Alla typer inklusive Rubrik, Fr†n-summa och Till-summa ”verf”rs.\Redovisningskonton som har samma nummer som en befintlig kostnadstyp ”verf”rs inte.\\Vill du starta jobbet?';
      Text001@1013 : TextConst 'ENU=Indent %1?;NOR=Vil du rykke inn %1?;SVE=Indrag %1?';
      Text002@1014 : TextConst 'ENU=Create cost types:\Number   #1########;NOR=Opprett kosttyper:\Nummer                           #1########;SVE=Skapa kostnadstyper:\nummer                                          #1########';
      Text003@1015 : TextConst 'ENU=%1 income statement accounts processed. %2 cost types created.;NOR=%1 resultatregnskapskonti behandlet. %2 kosttyper opprettet.;SVE=%1 resultatr„kningskonton har bearbetats. %2 kostnadstyper har skapats.';
      Text004@1016 : TextConst 'ENU=Indent chart of cost types\Number   #1########;NOR=Rykk inn diagram med kosttyper\Nummer               #1########;SVE=Indrag av lista ”ver kostnadstyper\nummer                            #1########';
      Text005@1017 : TextConst 'ENU=End-Total %1 does not belong to the corresponding Begin-Total.;NOR=Til-summen %1 tilh›rer ikke den tilsvarende fra-summen.;SVE=Slutsumman %1 tillh”r inte motsvarande inledande summa.';
      Text006@1018 : TextConst 'ENU=This function registers the cost types in the chart of accounts.\\This creates the link between chart of accounts and cost types and verifies that each income statement account is only linked to a cost type.\\Start job?;NOR=Denne funksjonen registrerer kosttypene i kontoplanen.\\Funksjonen oppretter deretter koblingen mellom kontoplanen og kosttypene samt kontrollerer at hver resultatregnskapskonto bare er koblet til en kosttype.\\Vil du starte jobben?;SVE=I funktionen registreras kostnadstyperna i kontoplanen.\\Det leder till att l„nken mellan kontoplanen och kostnadstyperna skapas och att varje inkommande transaktionskonto bara „r kopplat till en kostnadstyp.\\Vill du starta jobbet?';
      Text007@1019 : TextConst 'ENU=%1 cost types are processed and logged in %2 G/L accounts.;NOR=%1 kosttyper er behandlet og logget i %2 finanskonti.;SVE=%1 kostnadstyper har bearbetats och loggats i %2 redovisningskonton.';
      Text008@1020 : TextConst 'ENU=Check assignment cost type - G/L account\Number   #1########;NOR=Kontroller kosttype for tildeling - finans\Nummer   #1########;SVE=Kontrollera kostnadstyp f”r tilldelning - redovisningskonto\nummer   #1########';
      Text009@1021 : TextConst 'ENU=Cost type %1 should be assigned to G/L account %2.\Cost type %3 is already linked to G/L account %2.\\Each G/L account can only be linked to a single cost type.\However, it is possible to link multiple G/L accounts to a single cost type.;NOR=Kosttype %1 m† tilordnes til finanskonto %2.\Kosttype %3 er allerede koblet til finanskonto %2.\\Hver enkelt finanskonto kan bare kobles til ‚n kosttype.\Det er imidlertid mulig † koble flere finanskonti til ‚n kosttype.;SVE=Kostnadstypen %1 ska kopplas till redovisningskontot %2.\Kostnadstypen %3 „r redan kopplad till redovisningskontot %2.\\Varje redovisningskonto kan bara vara kopplat till en kostnadstyp.\Det g†r dock att koppla flera redovisningskonto till samma kostnadstyp.';
      Text010@1022 : TextConst 'ENU=Indenting chart\Number   #1########;NOR=Rykker inn diagram\Nummer                           #1########;SVE=Indrag av lista\nummer                                               #1########';
      Text011@1023 : TextConst 'ENU=End-Total %1 does not belong to Begin-Total.;NOR=Til-summen %1 tilh›rer ikke en fra-sum.;SVE=Slutsumman %1 tillh”r inte den inledande summan.';
      Text012@1024 : TextConst 'ENU=The range is too long and cannot be transferred to the End-Total field.\\Move End-Total closer to Begin-Total or use shorter codes.;NOR=Omr†det er for langt og kan ikke overf›res til Til-sum-feltet.\\Flytt til-summen n‘rmere fra-summen, eller bruk kortere koder.;SVE=Intervallet „r f”r l†ngt och g†r inte att ”verf”ra till f„ltet f”r slutsumman.\\Flytta slutsumman n„rmare den inledande summan eller anv„nd kortare koder.';
      Text013@1025 : TextConst '@@@="%1=Table caption Cost Center;%2=Field Value Cost Center Code";ENU=%1 %2 is not defined in Cost Accounting.;NOR=%1 %2 er ikke definert i kostregnskap.;SVE=%1 %2 har inte definierats i kostnadsredovisningen.';
      Text014@1026 : TextConst '@@@="%1=Table caption Cost Center;%2=Field Value Cost Center Code";ENU=%1 %2 is blocked in Cost Accounting.;NOR=%1 %2 er sperret i kostregnskap.;SVE=%1 %2 har sp„rrats i kostnadsredovisningen.';
      Text015@1027 : TextConst '@@@="%1=Table caption Cost Center;%2=Field Value Cost Center Code";ENU=%1 %2 does not have line type %1 or Begin-Total.;NOR=%1 %2 har ikke linjetype %1 eller fra-sum.;SVE=%1 %2 har inte radtypen %1 eller inledande summa.';
      Text016@1028 : TextConst '@@@="%1=Table caption Cost Center or Cost Object;%2=Field Value";ENU=Do you want to create %1 %2 in Cost Accounting?;NOR=Vil du opprette %1 %2 i kostregnskap?;SVE=Vill du skapa %1 %2 i kostnadsredovisningen?';
      Text017@1029 : TextConst '@@@="%1=Table caption Cost Center or Cost Object or Cost Type;%2=Field Value";ENU=%1 %2 has been updated in Cost Accounting.;NOR=%1 %2 er oppdatert i kostregnskap.;SVE=%1 %2 har uppdaterats i kostnadsredovisningen.';
      Text018@1030 : TextConst 'ENU=Create dimension\Number   #1########;NOR=Opprett dimensjon\Nummer                            #1########;SVE=Skapa dimension\nummer                                               #1########';
      Text019@1031 : TextConst 'ENU=%1 cost centers created.;NOR=%1 kostsentre er opprettet.;SVE=%1 kostnadsst„llen har skapats.';
      Text020@1032 : TextConst 'ENU=%1 cost objects created.;NOR=%1 kostobjekter er opprettet.;SVE=%1 kostnadsb„rare har skapats.';
      Text021@1033 : TextConst 'ENU=Do you want to get cost centers from dimension %1 ?;NOR=Vil du hente kostsentre fra dimensjon %1 ?;SVE=Vill du h„mta kostnadsst„llen fr†n dimensionen %1?';
      Text022@1034 : TextConst 'ENU=Do you want to get cost objects from dimension %1 ?;NOR=Vil du hente kostobjekter fra dimensjon %1?;SVE=Vill du h„mta kostnadsb„rare fr†n dimensionen %1?';
      Text023@1035 : TextConst '@@@="%1=Table caption Cost Center or Cost Object or Cost Type or Dimension Value;%2=Field Value";ENU=The %1 %2 cannot be inserted because it already exists as %3.;NOR=%1 %2 kan ikke settes inn fordi den allerede finnes som %3.;SVE=Det g†r inte att infoga %1 %2 eftersom det redan finns som %3.';
      Text024@1037 : TextConst '@@@="%1=Table caption Cost Center or Cost Object;%2=Field Value";ENU=Do you want to update %1 %2 in Cost Accounting?;NOR=Vil du oppdatere %1 %2 i kostregnskap?;SVE=Vill du uppdatera %1 %2 i kostnadsredovisningen?';
      Text025@1038 : TextConst '@@@="%1=Cost Budget Register tablecaption,%2=Cost Budget Entry tablecaption,%3=Entry No. fieldcaption";ENU=The %1 cannot be updated with this %2 because the %3 does not fall within the From/To range.;NOR=Kan ikke oppdatere %1 med %2 ettersom %3 ikke faller innenfor fra/til-intervallet.;SVE=Det g†r inte att uppdatera %1 med %2 eftersom %3 inte finns i fr†n/till-intervallet.';
      ArrayExceededErr@1039 : TextConst '@@@="%1 = A number bigger than 1";ENU=You can only indent %1 levels for entities of the type Begin-Total.;NOR=Du kan bare rykke inn %1 niv†er for enheter av typen Fra-sum.';

    [External]
    PROCEDURE GetCostTypesFromChartOfAccount@1();
    BEGIN
      IF NOT CONFIRM(Text000,TRUE) THEN
        ERROR('');

      GetCostTypesFromChartDirect;

      IndentCostTypes(TRUE);

      MESSAGE(Text003,NoOfGLAcc,RecsCreated)
    END;

    [External]
    PROCEDURE GetCostTypesFromChartDirect@2();
    BEGIN
      NoOfGLAcc := 0;
      RecsCreated := 0;
      Window.OPEN(Text002);

      WITH GLAcc DO BEGIN
        RESET;
        SETRANGE("Income/Balance","Income/Balance"::"Income Statement");
        IF FIND('-') THEN
          REPEAT
            GetCostType("No.",CostTypeExists);
            Window.UPDATE(1,"No.");
            NoOfGLAcc := NoOfGLAcc + 1;

            CostType.INIT;
            CostType."No." := "No.";
            CostType.Name := Name;
            CostType."Search Name" := "Search Name";
            CostType.Type := "Account Type";
            CostType.Blocked := "Account Type" <> "Account Type"::Posting;
            CostType."Cost Center Code" := GetCostCenterCodeFromDefDim(DATABASE::"G/L Account","No.");
            IF NOT CostCenterExists(CostType."Cost Center Code") THEN
              CostType."Cost Center Code" := '';
            CostType."Cost Object Code" := GetCostObjectCodeFromDefDim(DATABASE::"G/L Account","No.");
            IF NOT CostObjectExists(CostType."Cost Object Code") THEN
              CostType."Cost Object Code" := '';
            CostType."New Page" := "New Page";
            IF "No. of Blank Lines" > 0 THEN
              CostType."Blank Line" := TRUE;
            CostType.Totaling := Totaling;
            CostType."Modified Date" := TODAY;
            IF "Account Type" = "Account Type"::Posting THEN
              CostType."G/L Account Range" := "No."
            ELSE
              CostType."G/L Account Range" := '';
            IF NOT CostTypeExists THEN
              IF CostType.INSERT THEN BEGIN
                RecsCreated := RecsCreated + 1;
                "Cost Type No." := "No.";
              END;
            MODIFY;
          UNTIL NEXT = 0;
        Window.CLOSE;
      END;

      IndentCostTypes(TRUE);
    END;

    [External]
    PROCEDURE ConfirmUpdate@32(CallingTrigger@1000 : 'OnInsert,OnModify,,OnRename';TableCaption@1001 : Text[80];Value@1002 : Code[20]) : Boolean;
    BEGIN
      IF CallingTrigger = CallingTrigger::OnInsert THEN
        EXIT(CONFIRM(Text016,TRUE,TableCaption,Value));
      EXIT(CONFIRM(Text024,TRUE,TableCaption,Value));
    END;

    LOCAL PROCEDURE CanUpdate@46(Alignment@1000 : Option;NoAligment@1001 : Option;PromptAlignment@1002 : Option;DimValue@1003 : Record 349;DimensionCode@1004 : Code[20];CallingTrigger@1005 : Option;TableCaption@1006 : Text[80]) : Boolean;
    BEGIN
      IF DimValue."Dimension Code" <> DimensionCode THEN
        EXIT(FALSE);
      IF DimValue."Dimension Value Type" IN
         [DimValue."Dimension Value Type"::"Begin-Total",DimValue."Dimension Value Type"::"End-Total"]
      THEN
        EXIT(FALSE);
      CASE Alignment OF
        NoAligment:
          EXIT(FALSE);
        PromptAlignment:
          IF NOT ConfirmUpdate(CallingTrigger,TableCaption,DimValue.Code) THEN
            EXIT(FALSE);
      END;
      EXIT(TRUE);
    END;

    [External]
    PROCEDURE UpdateCostTypeFromGLAcc@3(VAR GLAcc@1000 : Record 15;VAR xGLAcc@1001 : Record 15;CallingTrigger@1002 : 'OnInsert,OnModify,,OnRename');
    VAR
      UpdateCostType@1003 : Boolean;
    BEGIN
      IF GLAcc."Income/Balance" <> GLAcc."Income/Balance"::"Income Statement" THEN
        EXIT;

      IF (CallingTrigger = CallingTrigger::OnModify) AND (FORMAT(GLAcc) = FORMAT(xGLAcc)) THEN
        EXIT;

      IF NOT CostAccSetup.GET THEN
        EXIT;

      IF CostType.GET(GLAcc."No.") AND (GLAcc."Cost Type No." = '') THEN
        EXIT;

      IF NOT CheckAlignment(GLAcc,CallingTrigger) THEN
        EXIT;

      CASE CallingTrigger OF
        CallingTrigger::OnInsert,CallingTrigger::OnModify:
          WITH GLAcc DO BEGIN
            IF CostType.GET("Cost Type No.") THEN
              UpdateCostType := IsGLAccNoFirstFromRange(CostType,"No.")
            ELSE BEGIN
              CostType."No." := "No.";
              UpdateCostType := CostType.INSERT;
            END;

            IF UpdateCostType THEN BEGIN
              CostType.Name := Name;
              CostType."Search Name" := "Search Name";
              CostType.Type := "Account Type";
              CostType.Blocked := "Account Type" <> "Account Type"::Posting;
              CostType."Cost Center Code" := GetCostCenterCodeFromDefDim(DATABASE::"G/L Account","No.");
              IF NOT CostCenterExists(CostType."Cost Center Code") THEN
                CostType."Cost Center Code" := '';
              CostType."Cost Object Code" := GetCostObjectCodeFromDefDim(DATABASE::"G/L Account","No.");
              IF NOT CostObjectExists(CostType."Cost Object Code") THEN
                CostType."Cost Object Code" := '';
              CostType."New Page" := "New Page";
              CostType."Blank Line" := "No. of Blank Lines" > 0;
              CostType.Totaling := Totaling;
              CostType."Modified Date" := TODAY;
              IF "Account Type" = "Account Type"::Posting THEN
                CostType."G/L Account Range" := "No."
              ELSE
                CostType."G/L Account Range" := '';

              CostType.MODIFY;
              "Cost Type No." := CostType."No.";
            END;
          END;
        CallingTrigger::OnRename:
          BEGIN
            IF CostType.GET(GLAcc."No.") THEN
              ERROR(Text023,GLAcc.TABLECAPTION,GLAcc."No.",CostType.TABLECAPTION);
            IF CostType.GET(xGLAcc."No.") THEN BEGIN
              CostType.RENAME(GLAcc."No.");
              CostType."G/L Account Range" := GLAcc."No.";
              CostType.MODIFY;
              GLAcc."Cost Type No." := GLAcc."No.";
            END ELSE
              EXIT;
          END;
      END;
      IndentCostTypes(FALSE);
      MESSAGE(Text017,CostType.TABLECAPTION,GLAcc."No.");
    END;

    [External]
    PROCEDURE UpdateCostCenterFromDim@4(VAR DimValue@1000 : Record 349;VAR xDimValue@1001 : Record 349;CallingTrigger@1002 : 'OnInsert,OnModify,,OnRename');
    VAR
      CostCenter@1003 : Record 1112;
    BEGIN
      CostAccSetup.GET;
      IF NOT CanUpdate(CostAccSetup."Align Cost Center Dimension",CostAccSetup."Align Cost Center Dimension"::"No Alignment",
           CostAccSetup."Align Cost Center Dimension"::Prompt,DimValue,CostAccSetup."Cost Center Dimension",CallingTrigger,
           CostCenter.TABLECAPTION)
      THEN
        EXIT;

      CASE CallingTrigger OF
        CallingTrigger::OnInsert:
          BEGIN
            IF CostCenterExists(DimValue.Code) THEN
              ERROR(Text023,CostCenter.TABLECAPTION,DimValue.Code,CostCenter.TABLECAPTION);
            InsertCostCenterFromDimValue(DimValue);
          END;
        CallingTrigger::OnModify:
          BEGIN
            IF NOT CostCenterExists(DimValue.Code) THEN
              InsertCostCenterFromDimValue(DimValue)
            ELSE
              ModifyCostCenterFromDimValue(DimValue);
          END;
        CallingTrigger::OnRename:
          BEGIN
            IF NOT CostCenterExists(xDimValue.Code) THEN
              EXIT;
            IF CostCenterExists(DimValue.Code) THEN
              ERROR(Text023,DimValue.TABLECAPTION,DimValue.Code,CostCenter.TABLECAPTION);
            CostCenter.GET(xDimValue.Code);
            CostCenter.RENAME(DimValue.Code);
          END;
      END;

      IndentCostCenters;
      MESSAGE(Text017,CostCenter.TABLECAPTION,DimValue.Code);
    END;

    [External]
    PROCEDURE UpdateCostObjectFromDim@24(VAR DimValue@1002 : Record 349;VAR xDimValue@1001 : Record 349;CallingTrigger@1000 : 'OnInsert,OnModify,,OnRename');
    VAR
      CostObject@1003 : Record 1113;
    BEGIN
      CostAccSetup.GET;
      IF NOT CanUpdate(CostAccSetup."Align Cost Object Dimension",CostAccSetup."Align Cost Object Dimension"::"No Alignment",
           CostAccSetup."Align Cost Object Dimension"::Prompt,DimValue,CostAccSetup."Cost Object Dimension",CallingTrigger,
           CostObject.TABLECAPTION)
      THEN
        EXIT;

      CASE CallingTrigger OF
        CallingTrigger::OnInsert:
          BEGIN
            IF CostObjectExists(DimValue.Code) THEN
              ERROR(Text023,CostObject.TABLECAPTION,DimValue.Code,CostObject.TABLECAPTION);
            InsertCostObjectFromDimValue(DimValue);
          END;
        CallingTrigger::OnModify:
          BEGIN
            IF NOT CostObjectExists(DimValue.Code) THEN
              InsertCostObjectFromDimValue(DimValue)
            ELSE
              ModifyCostObjectFromDimValue(DimValue);
          END;
        CallingTrigger::OnRename:
          BEGIN
            IF NOT CostObjectExists(xDimValue.Code) THEN
              EXIT;
            IF CostObjectExists(DimValue.Code) THEN
              ERROR(Text023,DimValue.TABLECAPTION,DimValue.Code,CostObject.TABLECAPTION);
            CostObject.GET(xDimValue.Code);
            CostObject.RENAME(DimValue.Code);
          END;
      END;

      IndentCostCenters;
      MESSAGE(Text017,CostObject.TABLECAPTION,DimValue.Code);
    END;

    [External]
    PROCEDURE UpdateCostTypeFromDefaultDimension@33(VAR DefaultDim@1000 : Record 352;VAR GLAcc@1001 : Record 15;CallingTrigger@1002 : 'OnInsert,OnModify,OnDelete');
    VAR
      CostType@1010 : Record 1103;
    BEGIN
      CostAccSetup.GET;

      WITH GLAcc DO
        IF CostType.GET("Cost Type No.") THEN BEGIN
          IF NOT IsGLAccNoFirstFromRange(CostType,"No.") THEN
            EXIT;
          IF NOT CheckAlignment(GLAcc,CallingTrigger::OnModify) THEN
            EXIT;

          IF CostAccSetup."Cost Center Dimension" = DefaultDim."Dimension Code" THEN
            IF CostCenterExists(DefaultDim."Dimension Value Code") AND NOT (CallingTrigger = CallingTrigger::OnDelete) THEN
              CostType."Cost Center Code" := DefaultDim."Dimension Value Code"
            ELSE
              CostType."Cost Center Code" := '';

          IF CostAccSetup."Cost Object Dimension" = DefaultDim."Dimension Code" THEN
            IF CostObjectExists(DefaultDim."Dimension Value Code") AND NOT (CallingTrigger = CallingTrigger::OnDelete) THEN
              CostType."Cost Object Code" := DefaultDim."Dimension Value Code"
            ELSE
              CostType."Cost Object Code" := '';

          CostType.MODIFY;
        END;
    END;

    [External]
    PROCEDURE ConfirmIndentCostTypes@5();
    BEGIN
      IF NOT CONFIRM(Text001,TRUE,CostType.TABLECAPTION) THEN
        ERROR('');

      IndentCostTypes(TRUE);
    END;

    [External]
    PROCEDURE IndentCostTypes@6(ShowMessage@1000 : Boolean);
    VAR
      CostTypeNo@1001 : ARRAY [10] OF Code[20];
    BEGIN
      i := 0;
      IF ShowMessage THEN
        Window.OPEN(Text004);

      WITH CostType DO BEGIN
        IF FIND('-') THEN
          REPEAT
            IF ShowMessage THEN
              Window.UPDATE(1,"No.");
            IF Type = Type::"End-Total" THEN BEGIN
              IF i < 1 THEN
                ERROR(Text005,"No.");
              Totaling := CostTypeNo[i] + '..' + "No.";
              i := i - 1;
            END;
            Indentation := i;
            MODIFY;
            IF Type = Type::"Begin-Total" THEN BEGIN
              i := i + 1;
              IF i > ARRAYLEN(CostTypeNo) THEN
                ERROR(ArrayExceededErr,ARRAYLEN(CostTypeNo));
              CostTypeNo[i] := "No.";
            END;
          UNTIL NEXT = 0;
      END;

      IF ShowMessage THEN
        Window.CLOSE;
    END;

    [External]
    PROCEDURE LinkCostTypesToGLAccountsYN@7();
    BEGIN
      IF NOT CONFIRM(Text006,TRUE) THEN
        ERROR('');

      CLEARALL;
      LinkCostTypesToGLAccounts;
      MESSAGE(Text007,NoOfCostTypes,NoOfGLAcc);
    END;

    [External]
    PROCEDURE LinkCostTypesToGLAccounts@8();
    BEGIN
      Window.OPEN(Text008);

      GLAcc.RESET;
      CostType.RESET;
      GLAcc.MODIFYALL("Cost Type No.",'');
      CostType.SETRANGE(Type,CostType.Type::"Cost Type");
      CostType.SETFILTER("G/L Account Range",'<>%1','');
      IF CostType.FINDSET THEN
        REPEAT
          Window.UPDATE(1,CostType."No.");
          NoOfCostTypes := NoOfCostTypes + 1;
          GLAcc.SETFILTER("No.",CostType."G/L Account Range");
          GLAcc.SETRANGE("Income/Balance",GLAcc."Income/Balance"::"Income Statement");
          IF GLAcc.FINDSET THEN
            REPEAT
              IF GLAcc."Cost Type No." <> '' THEN BEGIN
                Window.CLOSE;
                ERROR(Text009,CostType."No.",GLAcc."No.",GLAcc."Cost Type No.");
              END;
              GLAcc."Cost Type No." := CostType."No.";
              NoOfGLAcc := NoOfGLAcc + 1;
              GLAcc.MODIFY;
            UNTIL GLAcc.NEXT = 0;
        UNTIL CostType.NEXT = 0;

      Window.CLOSE;
    END;

    [External]
    PROCEDURE CreateCostCenters@9();
    VAR
      CostCenter@1000 : Record 1112;
      DimValue@1001 : Record 349;
    BEGIN
      CostAccSetup.GET;
      IF NOT CONFIRM(Text021,TRUE,CostAccSetup."Cost Center Dimension") THEN
        ERROR('');

      RecsProcessed := 0;
      RecsCreated := 0;
      Window.OPEN(Text018);

      WITH CostCenter DO BEGIN
        RESET;
        DimValue.SETRANGE("Dimension Code",CostAccSetup."Cost Center Dimension");
        IF DimValue.FIND('-') THEN BEGIN
          REPEAT
            Window.UPDATE(1,Code);
            IF InsertCostCenterFromDimValue(DimValue) THEN
              RecsProcessed := RecsProcessed + 1;
          UNTIL DimValue.NEXT = 0;
          Window.CLOSE;
        END;
      END;

      IndentCostCenters;

      MESSAGE(Text019,RecsProcessed);
    END;

    [External]
    PROCEDURE IndentCostCentersYN@10();
    VAR
      CostCenter@1000 : Record 1112;
    BEGIN
      IF NOT CONFIRM(Text001,TRUE,CostCenter.TABLECAPTION) THEN
        ERROR('');

      IndentCostCenters;
    END;

    [External]
    PROCEDURE IndentCostCenters@11();
    VAR
      CostCenter@1003 : Record 1112;
      CostCenterRange@1000 : Code[250];
      StartRange@1001 : ARRAY [10] OF Code[20];
      SpecialSort@1002 : Boolean;
    BEGIN
      SpecialSort := FALSE;
      i := 0;

      Window.OPEN(Text010);

      WITH CostCenter DO BEGIN
        SETCURRENTKEY("Sorting Order");
        SETFILTER("Sorting Order",'<>%1','');
        IF FIND('-') THEN
          SpecialSort := TRUE;

        RESET;
        IF SpecialSort THEN BEGIN
          SETCURRENTKEY("Sorting Order");
          IF FINDSET THEN
            REPEAT
              IF "Line Type" = "Line Type"::"End-Total" THEN BEGIN
                Totaling := CostCenterRange;
                IF i < 1 THEN
                  ERROR(Text011,Code);
                i := i - 1;
              END;
              Indentation := i;
              MODIFY;
              IF "Line Type" = "Line Type"::"Begin-Total" THEN BEGIN
                CostCenterRange := '';
                i := i + 1;
              END;
              IF (("Line Type" = "Line Type"::"Cost Center") AND (i = 1)) OR
                 ("Line Type" = "Line Type"::"Begin-Total")
              THEN BEGIN
                IF STRLEN(CostCenterRange) + STRLEN(Code) > MAXSTRLEN(CostCenterRange) THEN
                  ERROR(Text012);
                IF CostCenterRange = '' THEN
                  CostCenterRange := Code
                ELSE
                  CostCenterRange := CostCenterRange + '|' + Code;
              END;
            UNTIL NEXT = 0;
        END ELSE BEGIN
          SETCURRENTKEY(Code);
          IF FINDSET THEN
            REPEAT
              Window.UPDATE(1,Code);

              IF "Line Type" = "Line Type"::"End-Total" THEN BEGIN
                IF i < 1 THEN
                  ERROR(Text005,Code);
                Totaling := StartRange[i] + '..' + Code;
                i := i - 1;
              END;
              Indentation := i;
              MODIFY;
              IF "Line Type" = "Line Type"::"Begin-Total" THEN BEGIN
                i := i + 1;
                IF i > ARRAYLEN(StartRange) THEN
                  ERROR(ArrayExceededErr,ARRAYLEN(StartRange));
                StartRange[i] := Code;
              END;
            UNTIL NEXT = 0;
        END;
      END;
      Window.CLOSE;
    END;

    [External]
    PROCEDURE CreateCostObjects@12();
    VAR
      CostObject@1000 : Record 1113;
      DimValue@1001 : Record 349;
    BEGIN
      CostAccSetup.GET;
      IF NOT CONFIRM(Text022,TRUE,CostAccSetup."Cost Object Dimension") THEN
        ERROR('');

      RecsProcessed := 0;
      RecsCreated := 0;
      Window.OPEN(Text018);

      WITH CostObject DO BEGIN
        RESET;
        DimValue.SETRANGE("Dimension Code",CostAccSetup."Cost Object Dimension");
        IF DimValue.FIND('-') THEN BEGIN
          REPEAT
            Window.UPDATE(1,Code);
            IF InsertCostObjectFromDimValue(DimValue) THEN
              RecsProcessed := RecsProcessed + 1;
          UNTIL DimValue.NEXT = 0;
          Window.CLOSE;
        END;
      END;

      IndentCostObjects;
      MESSAGE(Text020,RecsProcessed);
    END;

    [External]
    PROCEDURE IndentCostObjectsYN@13();
    VAR
      CostObject@1000 : Record 1113;
    BEGIN
      IF NOT CONFIRM(Text001,TRUE,CostObject.TABLECAPTION) THEN
        ERROR('');

      IndentCostObjects;
    END;

    [External]
    PROCEDURE IndentCostObjects@14();
    VAR
      CostObject@1003 : Record 1113;
      CostObjRange@1000 : Code[250];
      StartRange@1001 : ARRAY [10] OF Code[20];
      SpecialSort@1002 : Boolean;
    BEGIN
      SpecialSort := FALSE;
      i := 0;

      Window.OPEN(Text010);

      WITH CostObject DO BEGIN
        SETCURRENTKEY("Sorting Order");
        SETFILTER("Sorting Order",'<>%1','');
        IF FIND('-') THEN
          SpecialSort := TRUE;

        RESET;
        IF SpecialSort THEN BEGIN
          SETCURRENTKEY("Sorting Order");
          IF FINDSET THEN
            REPEAT
              IF "Line Type" = "Line Type"::"End-Total" THEN BEGIN
                Totaling := CostObjRange;
                IF i < 1 THEN
                  ERROR(Text011,Code);
                i := i - 1;
              END;
              Indentation := i;
              MODIFY;
              IF "Line Type" = "Line Type"::"Begin-Total" THEN BEGIN
                CostObjRange := '';
                i := i + 1;
              END;

              IF (("Line Type" = "Line Type"::"Cost Object") AND (i = 1)) OR
                 ("Line Type" = "Line Type"::"Begin-Total")
              THEN BEGIN
                IF STRLEN(CostObjRange) + STRLEN(Code) > MAXSTRLEN(CostObjRange) THEN
                  ERROR(Text012);

                IF CostObjRange = '' THEN
                  CostObjRange := Code
                ELSE
                  CostObjRange := CostObjRange + '|' + Code;
              END;
            UNTIL NEXT = 0;
        END ELSE BEGIN
          SETCURRENTKEY(Code);
          IF FIND('-') THEN
            REPEAT
              Window.UPDATE(1,Code);
              IF "Line Type" = "Line Type"::"End-Total" THEN BEGIN
                IF i < 1 THEN
                  ERROR(Text005,Code);
                Totaling := StartRange[i] + '..' + Code;
                i := i - 1;
              END;
              Indentation := i;
              MODIFY;

              IF "Line Type" = "Line Type"::"Begin-Total" THEN BEGIN
                i := i + 1;
                IF i > ARRAYLEN(StartRange) THEN
                  ERROR(ArrayExceededErr,ARRAYLEN(StartRange));
                StartRange[i] := Code;
              END;
            UNTIL NEXT = 0;
        END;
      END;
      Window.CLOSE;
    END;

    [External]
    PROCEDURE CheckValidCCAndCOInGLEntry@15(DimSetID@1000 : Integer);
    VAR
      CostCenter@1003 : Record 1112;
      CostObject@1004 : Record 1113;
      CostCenterCode@1001 : Code[20];
      CostObjectCode@1002 : Code[20];
    BEGIN
      IF NOT CostAccSetup.GET THEN
        EXIT;
      IF NOT CostAccSetup."Check G/L Postings" THEN
        EXIT;

      CostCenterCode := GetCostCenterCodeFromDimSet(DimSetID);
      CostObjectCode := GetCostObjectCodeFromDimSet(DimSetID);

      IF CostCenterCode <> '' THEN BEGIN
        IF NOT CostCenter.GET(CostCenterCode) THEN
          ERROR(Text013,CostCenter.TABLECAPTION,CostCenterCode);
        IF CostCenter.Blocked THEN
          ERROR(Text014,CostCenter.TABLECAPTION,CostCenterCode);
        IF NOT (CostCenter."Line Type" IN [CostCenter."Line Type"::"Cost Center",CostCenter."Line Type"::"Begin-Total"]) THEN
          ERROR(Text015,CostCenter.TABLECAPTION,CostCenterCode);
      END;

      IF CostObjectCode <> '' THEN BEGIN
        IF NOT CostObject.GET(CostObjectCode) THEN
          ERROR(Text013,CostObject.TABLECAPTION,CostObjectCode);
        IF CostObject.Blocked THEN
          ERROR(Text014,CostObject.TABLECAPTION,CostObjectCode);
        IF NOT (CostObject."Line Type" IN [CostObject."Line Type"::"Cost Object",CostObject."Line Type"::"Begin-Total"]) THEN
          ERROR(Text015,CostObject.TABLECAPTION,CostObjectCode);
      END;
    END;

    [External]
    PROCEDURE GetCostCenterCodeFromDimSet@17(DimSetID@1002 : Integer) : Code[20];
    VAR
      DimSetEntry@1005 : Record 480;
    BEGIN
      CostAccSetup.GET;
      IF DimSetEntry.GET(DimSetID,CostAccSetup."Cost Center Dimension") THEN
        EXIT(DimSetEntry."Dimension Value Code");
      EXIT('');
    END;

    [External]
    PROCEDURE GetCostCenterCodeFromDefDim@18(TableID@1000 : Integer;No@1002 : Code[20]) : Code[20];
    VAR
      DefaultDim@1001 : Record 352;
    BEGIN
      CostAccSetup.GET;
      IF DefaultDim.GET(TableID,No,CostAccSetup."Cost Center Dimension") THEN
        EXIT(DefaultDim."Dimension Value Code");
    END;

    [External]
    PROCEDURE CostCenterExists@20(CostCenterCode@1001 : Code[20]) : Boolean;
    VAR
      CostCenter@1000 : Record 1112;
    BEGIN
      EXIT(CostCenter.GET(CostCenterCode));
    END;

    [External]
    PROCEDURE CostCenterExistsAsDimValue@22(CostCenterCode@1001 : Code[20]) : Boolean;
    VAR
      DimValue@1000 : Record 349;
    BEGIN
      CostAccSetup.GET;
      EXIT(DimValue.GET(CostAccSetup."Cost Center Dimension",CostCenterCode));
    END;

    [External]
    PROCEDURE LookupCostCenterFromDimValue@25(VAR CostCenterCode@1000 : Code[20]);
    VAR
      DimValue@1001 : Record 349;
    BEGIN
      CostAccSetup.GET;
      DimValue.LookupDimValue(CostAccSetup."Cost Center Dimension",CostCenterCode);
    END;

    [External]
    PROCEDURE GetCostObjectCodeFromDimSet@16(DimSetID@1002 : Integer) : Code[20];
    VAR
      DimSetEntry@1005 : Record 480;
    BEGIN
      CostAccSetup.GET;
      IF DimSetEntry.GET(DimSetID,CostAccSetup."Cost Object Dimension") THEN
        EXIT(DimSetEntry."Dimension Value Code");
      EXIT('');
    END;

    [External]
    PROCEDURE GetCostObjectCodeFromDefDim@19(TableID@1000 : Integer;No@1001 : Code[20]) : Code[20];
    VAR
      DefaultDim@1002 : Record 352;
    BEGIN
      CostAccSetup.GET;
      IF DefaultDim.GET(TableID,No,CostAccSetup."Cost Object Dimension") THEN
        EXIT(DefaultDim."Dimension Value Code");
    END;

    [External]
    PROCEDURE CostObjectExists@21(CostObjectCode@1001 : Code[20]) : Boolean;
    VAR
      CostObject@1000 : Record 1113;
    BEGIN
      EXIT(CostObject.GET(CostObjectCode));
    END;

    [External]
    PROCEDURE CostObjectExistsAsDimValue@23(CostObjectCode@1000 : Code[20]) : Boolean;
    VAR
      DimValue@1001 : Record 349;
    BEGIN
      CostAccSetup.GET;
      EXIT(DimValue.GET(CostAccSetup."Cost Object Dimension",CostObjectCode));
    END;

    [External]
    PROCEDURE LookupCostObjectFromDimValue@26(VAR COstObjectCode@1000 : Code[20]);
    VAR
      DimValue@1001 : Record 349;
    BEGIN
      CostAccSetup.GET;
      DimValue.LookupDimValue(CostAccSetup."Cost Object Dimension",COstObjectCode);
    END;

    LOCAL PROCEDURE InsertCostCenterFromDimValue@42(DimValue@1001 : Record 349) : Boolean;
    VAR
      CostCenter@1000 : Record 1112;
    BEGIN
      CopyDimValueToCostCenter(DimValue,CostCenter);
      EXIT(CostCenter.INSERT);
    END;

    LOCAL PROCEDURE ModifyCostCenterFromDimValue@45(DimValue@1000 : Record 349) : Boolean;
    VAR
      CostCenter@1001 : Record 1112;
    BEGIN
      CostCenter.GET(DimValue.Code);
      CopyDimValueToCostCenter(DimValue,CostCenter);
      EXIT(CostCenter.MODIFY);
    END;

    LOCAL PROCEDURE CopyDimValueToCostCenter@44(DimValue@1000 : Record 349;VAR CostCenter@1001 : Record 1112);
    BEGIN
      CostCenter.INIT;
      CostCenter.Code := DimValue.Code;
      CostCenter.Name := DimValue.Name;
      CostCenter."Line Type" := DimValue."Dimension Value Type";
      CostCenter.Blocked := DimValue.Blocked;
    END;

    LOCAL PROCEDURE InsertCostObjectFromDimValue@51(DimValue@1001 : Record 349) : Boolean;
    VAR
      CostObject@1000 : Record 1113;
    BEGIN
      CopyDimValueToCostObject(DimValue,CostObject);
      EXIT(CostObject.INSERT);
    END;

    LOCAL PROCEDURE ModifyCostObjectFromDimValue@50(DimValue@1000 : Record 349) : Boolean;
    VAR
      CostObject@1001 : Record 1113;
    BEGIN
      CostObject.GET(DimValue.Code);
      CopyDimValueToCostObject(DimValue,CostObject);
      EXIT(CostObject.MODIFY);
    END;

    LOCAL PROCEDURE CopyDimValueToCostObject@49(DimValue@1000 : Record 349;VAR CostObject@1001 : Record 1113);
    BEGIN
      CostObject.INIT;
      CostObject.Code := DimValue.Code;
      CostObject.Name := DimValue.Name;
      CostObject."Line Type" := DimValue."Dimension Value Type";
      CostObject.Blocked := DimValue.Blocked;
    END;

    [External]
    PROCEDURE InsertCostBudgetRegister@27(CostBudgetEntryNo@1002 : Integer;CostBudgetName@1003 : Code[10];CostBudgetAmount@1004 : Decimal) : Integer;
    VAR
      CostBudgetReg@1000 : Record 1111;
    BEGIN
      CostBudgetReg.LOCKTABLE;
      IF CostBudgetReg.FINDLAST THEN
        CostBudgetReg."No." := CostBudgetReg."No." + 1
      ELSE
        CostBudgetReg."No." := 1;
      CostBudgetReg.INIT;
      CostBudgetReg.Source := CostBudgetReg.Source::Manual;
      CostBudgetReg."From Cost Budget Entry No." := CostBudgetEntryNo;
      CostBudgetReg."To Cost Budget Entry No." := CostBudgetEntryNo;
      CostBudgetReg."No. of Entries" := 1;
      CostBudgetReg."Cost Budget Name" := CostBudgetName;
      CostBudgetReg.Amount := CostBudgetAmount;
      CostBudgetReg."Processed Date" := TODAY;
      CostBudgetReg."User ID" := USERID;
      CostBudgetReg.INSERT;

      EXIT(CostBudgetReg."No.");
    END;

    [External]
    PROCEDURE UpdateCostBudgetRegister@28(CostBudgetRegNo@1000 : Integer;CostBudgetEntryNo@1001 : Integer;CostBudgetAmount@1003 : Decimal);
    VAR
      CostBudgetReg@1002 : Record 1111;
      CostBudgetEntry@1004 : Record 1109;
    BEGIN
      IF CostBudgetRegNo = 0 THEN BEGIN
        CostBudgetReg.SETCURRENTKEY("From Cost Budget Entry No.","To Cost Budget Entry No.");
        CostBudgetReg.SETRANGE("From Cost Budget Entry No.",0,CostBudgetEntryNo);
        CostBudgetReg.SETFILTER("To Cost Budget Entry No.",'%1..',CostBudgetEntryNo);
        CostBudgetReg.FINDLAST
      END ELSE
        CostBudgetReg.GET(CostBudgetRegNo);

      IF (CostBudgetEntryNo > CostBudgetReg."To Cost Budget Entry No." + 1) OR
         (CostBudgetEntryNo < CostBudgetReg."From Cost Budget Entry No.")
      THEN
        ERROR(Text025,CostBudgetReg.TABLECAPTION,CostBudgetEntry.TABLECAPTION,CostBudgetEntry.FIELDCAPTION("Entry No."));
      IF CostBudgetEntryNo > CostBudgetReg."To Cost Budget Entry No." THEN BEGIN
        CostBudgetReg."To Cost Budget Entry No." := CostBudgetEntryNo;
        CostBudgetReg."No. of Entries" := CostBudgetReg."To Cost Budget Entry No." - CostBudgetReg."From Cost Budget Entry No." + 1
      END;
      CostBudgetReg.Amount := CostBudgetReg.Amount + CostBudgetAmount;
      CostBudgetReg.MODIFY(TRUE)
    END;

    LOCAL PROCEDURE CheckAlignment@30(VAR GLAcc@1001 : Record 15;CallingTrigger@1002 : Option) : Boolean;
    BEGIN
      CostAccSetup.GET;

      IF CostAccSetup."Align G/L Account" = CostAccSetup."Align G/L Account"::"No Alignment" THEN
        EXIT(FALSE);

      IF CostAccSetup."Align G/L Account" = CostAccSetup."Align G/L Account"::Prompt THEN
        IF NOT ConfirmUpdate(CallingTrigger,CostType.TABLECAPTION,GLAcc."No.") THEN
          EXIT(FALSE);

      EXIT(TRUE);
    END;

    [External]
    PROCEDURE IsGLAccNoFirstFromRange@29(CostType@1001 : Record 1103;GLAccNo@1002 : Code[20]) : Boolean;
    VAR
      GLAccCheck@1000 : Record 15;
    BEGIN
      GLAccCheck.SETFILTER("No.",CostType."G/L Account Range");
      IF GLAccCheck.FINDFIRST THEN
        EXIT(GLAccNo = GLAccCheck."No.");

      EXIT(FALSE);
    END;

    [External]
    PROCEDURE GetCostType@31(GLAccNo@1000 : Code[20];VAR CostTypeExists@1001 : Boolean);
    VAR
      GLAcc@1003 : Record 15;
      CostType@1002 : Record 1103;
    BEGIN
      CostType.RESET;
      CostType.SETRANGE("No.",GLAccNo);
      IF CostType.ISEMPTY THEN BEGIN
        CostTypeExists := FALSE;
        CostType.RESET;
        CostType.SETRANGE(Type,CostType.Type::"Cost Type");
        CostType.SETFILTER("G/L Account Range",'<>%1','');
        IF CostType.FINDSET THEN
          REPEAT
            GLAcc.RESET;
            GLAcc.SETRANGE("Income/Balance",GLAcc."Income/Balance"::"Income Statement");
            GLAcc.SETFILTER("No.",CostType."G/L Account Range");
            IF GLAcc.FINDSET THEN
              REPEAT
                IF GLAccNo = GLAcc."No." THEN
                  CostTypeExists := TRUE
              UNTIL (GLAcc.NEXT = 0) OR CostTypeExists;
          UNTIL (CostType.NEXT = 0) OR CostTypeExists;
      END;
    END;

    [External]
    PROCEDURE OpenDimValueListFiltered@34(FieldNo@1002 : Integer);
    VAR
      DimValue@1003 : Record 349;
      FieldRef@1001 : FieldRef;
      RecRef@1000 : RecordRef;
      DimCode@1004 : Code[20];
    BEGIN
      CostAccSetup.GET;
      RecRef.GETTABLE(CostAccSetup);
      FieldRef := RecRef.FIELD(FieldNo);
      EVALUATE(DimCode,FORMAT(FieldRef.VALUE));
      DimValue.SETRANGE("Dimension Code",DimCode);
      PAGE.RUN(0,DimValue);
    END;

    BEGIN
    END.
  }
}

