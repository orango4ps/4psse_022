OBJECT Table 11229317 Possible Web Service
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnInsert=BEGIN
               TESTFIELD("Is Standard", FALSE);
               GetInLicense;
             END;

    OnModify=BEGIN
               GetInLicense;
             END;

    OnDelete=BEGIN
               TESTFIELD("Is Standard", FALSE);
               CALCFIELDS("Service Set Lines", "Service Set Lines (Object)");
               TESTFIELD("Service Set Lines", 0);
               TESTFIELD("Service Set Lines (Object)", 0);
             END;

    OnRename=BEGIN
               ERROR('');
             END;

    CaptionML=ENU=Possible Web Service;
    LookupPageID=Page11126628;
    DrillDownPageID=Page11126628;
  }
  FIELDS
  {
    { 10  ;   ;Object Type         ;Option        ;CaptionML=[ENU=Object Type;
                                                              NOR=Objekttype;
                                                              SVE=Objekttyp];
                                                   OptionCaptionML=[ENU=,,,,,Codeunit,,,Page,Query;
                                                                    NOR=,,,,,Codeunit,,,Page,Query;
                                                                    SVE=,,,,,Codeunit,,,Page,Query];
                                                   OptionString=,,,,,Codeunit,,,Page,Query }
    { 20  ;   ;Object Id           ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=FIELD(Object Type));
                                                   OnValidate=BEGIN
                                                                TESTFIELD("Is Standard", FALSE);
                                                                CALCFIELDS("Object Name");
                                                                IF ("Service Name" = '') THEN
                                                                  "Service Name" := DELCHR("Object Name", '<=>', '(. )');
                                                              END;

                                                   CaptionML=[ENU=Object ID;
                                                              NOR=Objekt-ID;
                                                              SVE=Objekt-ID] }
    { 21  ;   ;Object Name         ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Name" WHERE (Object Type=FIELD(Object Type),
                                                                                                             Object ID=FIELD(Object Id)));
                                                   CaptionML=[ENU=Object Name;
                                                              NOR=Objektnavn;
                                                              SVE=Objektnamn];
                                                   Editable=No }
    { 30  ;   ;Service Name        ;Text250       ;OnValidate=BEGIN
                                                                TESTFIELD("Is Standard", FALSE);
                                                              END;

                                                   CaptionML=[ENU=Service Name;
                                                              NOR=Tjenestenavn;
                                                              SVE=Servicenamn] }
    { 40  ;   ;In License          ;Boolean       ;CaptionML=ENU=In License;
                                                   Editable=No }
    { 50  ;   ;Is Standard         ;Boolean       ;CaptionML=ENU=Is Standard;
                                                   Editable=No }
    { 60  ;   ;Service Set Lines   ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Web Service Set Line" WHERE (Object Type=FIELD(Object Type),
                                                                                                   Service Name=FIELD(Service Name)));
                                                   CaptionML=ENU=Service Set Lines;
                                                   Editable=No }
    { 61  ;   ;Published Service Set Lines;Integer;FieldClass=FlowField;
                                                   CalcFormula=Count("Web Service Set Line" WHERE (Object Type=FIELD(Object Type),
                                                                                                   Service Name=FIELD(Service Name),
                                                                                                   Published=CONST(Yes)));
                                                   CaptionML=ENU=Published Service Set Lines;
                                                   Editable=No }
    { 62  ;   ;Service Set Lines (Object);Integer ;FieldClass=FlowField;
                                                   CalcFormula=Count("Web Service Set Line" WHERE (Object Type=FIELD(Object Type),
                                                                                                   Object Id=FIELD(Object Id)));
                                                   CaptionML=ENU=Service Set Lines (Object);
                                                   Editable=No }
    { 63  ;   ;Web Services        ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Web Service" WHERE (Object Type=FIELD(Object Type),
                                                                                          Service Name=FIELD(Service Name)));
                                                   CaptionML=[ENU=Web Services;
                                                              NOR=Webtjenester;
                                                              SVE=Webbtj„nster];
                                                   Editable=No }
    { 70  ;   ;Custom Object Id OLD;Integer       ;DataClassification=ToBeClassified;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Object Type,Service Name                ;Clustered=Yes }
    {    ;Object Id                                }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Object Type,Service Name,Object Id,Object Name }
  }
  CODE
  {

    PROCEDURE GetInLicense@1100525000();
    VAR
      LicensePermission@1100525000 : Record 2000000043;
    BEGIN
      "In License" := FALSE;
      IF (NOT LicensePermission.GET("Object Type", "Object Id")) THEN
        EXIT;

      CASE "Object Type" OF
        "Object Type"::Codeunit,
        "Object Type"::Page,
        "Object Type"::Query:
          "In License" := (LicensePermission."Execute Permission" = LicensePermission."Read Permission"::Yes);
      END;
    END;

    PROCEDURE GetODataV4Url@1100525001() : Text;
    VAR
      WebServiceAggregate@1100528300 : Record 9900;
    BEGIN
      WebServiceAggregate."Object Type" := "Object Type";
      WebServiceAggregate."Service Name" := "Service Name";
      WebServiceAggregate."Object ID" := "Object Id";
      EXIT(WebServiceAggregate.GetODataV4Url);
    END;

    PROCEDURE GetODataUrl@2() : Text;
    VAR
      WebServiceAggregate@1100528300 : Record 9900;
    BEGIN
      WebServiceAggregate."Object Type" := "Object Type";
      WebServiceAggregate."Service Name" := "Service Name";
      WebServiceAggregate."Object ID" := "Object Id";
      EXIT(WebServiceAggregate.GetODataUrl);
    END;

    PROCEDURE GetSOAPUrl@3() : Text;
    VAR
      WebServiceAggregate@1100528300 : Record 9900;
    BEGIN
      WebServiceAggregate."Object Type" := "Object Type";
      WebServiceAggregate."Service Name" := "Service Name";
      WebServiceAggregate."Object ID" := "Object Id";
      EXIT(WebServiceAggregate.GetSOAPUrl);
    END;

    BEGIN
    END.
  }
}

