OBJECT Codeunit 11012881 Change Request Mgt WS
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      OK@1100528001 : TextConst 'ENU=ok;SVE=OK';
      Text000@1100528000 : TextConst 'ENU=Web Service is working!';
      Text001@1100525000 : TextConst 'DEU=Kommunikation hinzugefÅgt auf SPT;ENU=Communication added on SPT;NLD=Communicatie toegevoegd op het SPT';

    [External]
    PROCEDURE TestWebService@1100528000() : Text;
    BEGIN
      EXIT(Text000);
    END;

    [External]
    PROCEDURE AddTextLines@1100528001(ChangeRequestNo@1100528000 : Code[20];FieldNo@1100525000 : Integer;CommunicationTextWS@1100528001 : XMLport 11012807) : Text;
    VAR
      ChangeRequest@1100528002 : Record 11072193;
      UserSetup@1100525004 : Record 91;
      ChangeRequestCard@1100525005 : Page 11126268;
      RecordLinkManagement@1100525002 : Codeunit 447;
      RecRef@1100528003 : RecordRef;
      TextString@1100525003 : Text;
    BEGIN
      ChangeRequest.GET(ChangeRequestNo);
      RecRef.GETTABLE(ChangeRequest);

      CommunicationTextWS.SetTableNo(DATABASE::"Change Request");
      CommunicationTextWS.SetFieldNo(FieldNo);
      CommunicationTextWS.SetRecId(RecRef.RECORDID);
      CommunicationTextWS.IMPORT;

      TextString := STRSUBSTNO(Text001);
      IF (ChangeRequest."Change Manager No." <> '') THEN BEGIN
        UserSetup.SETRANGE("Employee No.", ChangeRequest."Change Manager No.");
        IF (UserSetup.FINDFIRST) THEN
          RecordLinkManagement.CreateNote(RecRef, ChangeRequestCard.OBJECTID(FALSE), TextString, UserSetup."User ID");
      END;
      SendServiceOrderNotification(ChangeRequest."Service Order No.", TextString);
      SendJobNotification(ChangeRequest."Project No.", TextString);
      EXIT(OK);
    END;

    [External]
    PROCEDURE SetUpdateFlagChangeRequest@1100525002(RequestNo@1100525000 : Code[20];Flag@1100525001 : Boolean) : Text;
    VAR
      ChangeRequest@1100525002 : Record 11072193;
    BEGIN
      IF (ChangeRequest.GET(RequestNo)) THEN BEGIN
        ChangeRequest."Communication Text Changed" := Flag;
        IF ChangeRequest.MODIFY THEN;
      END;
      EXIT(OK);
    END;

    LOCAL PROCEDURE SendServiceOrderNotification@1100525000(OrderNo@1100525000 : Code[20];TextString@1100525005 : Text);
    VAR
      UserSetup@1100525008 : Record 91;
      ServiceOrder@1100525007 : Record 11012823;
      ServiceOrderGeneral@1100525004 : Page 11125209;
      RecordLinkManagement@1100525003 : Codeunit 447;
      RecRef@1100525002 : RecordRef;
    BEGIN
      IF (OrderNo = '') THEN
        EXIT;
      IF (NOT ServiceOrder.GET(OrderNo)) THEN
        ServiceOrder.INIT;
      IF (ServiceOrder."Assigned to" <> '') THEN BEGIN
        RecRef.GETTABLE(ServiceOrder);
        UserSetup.SETRANGE("Employee No.", ServiceOrder."Assigned to");
        IF (UserSetup.FINDFIRST) THEN
          RecordLinkManagement.CreateNote(RecRef, ServiceOrderGeneral.OBJECTID(FALSE), TextString, UserSetup."User ID");
      END;
      SendServiceCallNotification(ServiceOrder."Call No.", TextString);
    END;

    LOCAL PROCEDURE SendServiceCallNotification@1100525001(CallNo@1100525004 : Code[20];TextString@1100525006 : Text);
    VAR
      UserSetup@1100525007 : Record 91;
      ServiceCall@1100525005 : Record 11012822;
      ServiceCallPage@1100525003 : Page 11125208;
      RecordLinkManagement@1100525002 : Codeunit 447;
      RecRef@1100525001 : RecordRef;
    BEGIN
      IF (CallNo = '') THEN
        EXIT;
      IF (NOT ServiceCall.GET(CallNo)) THEN
        ServiceCall.INIT;
      IF (ServiceCall."Assigned to" <> '') THEN BEGIN
        RecRef.GETTABLE(ServiceCall);
        UserSetup.SETRANGE("Employee No.", ServiceCall."Assigned to");
        IF (UserSetup.FINDFIRST) THEN
          RecordLinkManagement.CreateNote(RecRef, ServiceCallPage.OBJECTID(FALSE), TextString, UserSetup."User ID");
      END;
    END;

    LOCAL PROCEDURE SendJobNotification@1100525003(JobNo@1100525005 : Code[20];TextString@1100525006 : Text);
    VAR
      UserSetup@1100525007 : Record 91;
      Job@1100525004 : Record 11072003;
      JobCard@1100525003 : Page 11072003;
      RecordLinkManagement@1100525002 : Codeunit 447;
      RecRef@1100525001 : RecordRef;
    BEGIN
      IF (JobNo = '') THEN
        EXIT;
      IF (NOT Job.GET(JobNo)) THEN
        Job.INIT;
      IF (Job."Project Manager" <> '') THEN BEGIN
        RecRef.GETTABLE(Job);
        UserSetup.SETRANGE("Employee No.", Job."Project Manager");
        IF (UserSetup.FINDFIRST) THEN
          RecordLinkManagement.CreateNote(RecRef, JobCard.OBJECTID(FALSE), TextString, UserSetup."User ID");
      END;
    END;

    BEGIN
    END.
  }
}

