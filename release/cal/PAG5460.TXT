OBJECT Page 5460 Webhook Supported Resources
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[@@@={Locked};
               ENU=webhookSupportedResources;
               NOR=webhookSupportedResources;
               SVE=webhookSupportedResources];
    SourceTable=Table823;
    DelayedInsert=Yes;
    PageType=API;
    SourceTableTemporary=Yes;
    APIPublisher=microsoft;
    APIGroup=runtime;
    EntitySetName=webhookSupportedResources;
    EntityName=webhookSupportedResource;
    OnFindRecord=VAR
                   ApiWebhookEntity@1000 : Record 2000000097;
                   GraphMgtGeneralTools@1005 : Codeunit 5465;
                   View@1002 : Text;
                   I@1003 : Integer;
                 BEGIN
                   IF Initialized THEN
                     EXIT(TRUE);

                   IF NOT GraphMgtGeneralTools.IsApiSubscriptionEnabled THEN BEGIN
                     Initialized := TRUE;
                     EXIT(FALSE);
                   END;

                   View := GETVIEW;
                   RESET;

                   ApiWebhookEntity.SETRANGE("Object Type",ApiWebhookEntity."Object Type"::Page);
                   ApiWebhookEntity.SETRANGE("Table Temporary",FALSE);
                   IF NOT ApiWebhookEntity.FINDSET THEN
                     EXIT(FALSE);

                   REPEAT
                     IF NOT IsSystemTable(ApiWebhookEntity) THEN
                       IF NOT IsCompositeKey(ApiWebhookEntity) THEN BEGIN
                         I += 1;
                         ID := I;
                         "Value Long" := COPYSTR(GetResourceUri(ApiWebhookEntity),1,MAXSTRLEN("Value Long"));
                         INSERT;
                       END;
                   UNTIL ApiWebhookEntity.NEXT = 0;

                   SETVIEW(View);
                   FINDFIRST;
                   Initialized := TRUE;
                   EXIT(TRUE);
                 END;

    ODataKeyFields=Value Long;
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                Name=resource;
                CaptionML=[@@@={Locked};
                           ENU=resource;
                           NOR=resource;
                           SVE=resource];
                ApplicationArea=#All;
                SourceExpr="Value Long";
                Editable=FALSE }

  }
  CODE
  {
    VAR
      Initialized@1001 : Boolean;

    LOCAL PROCEDURE IsSystemTable@2(VAR ApiWebhookEntity@1000 : Record 2000000097) : Boolean;
    BEGIN
      EXIT(ApiWebhookEntity."Table No." > 2000000000);
    END;

    LOCAL PROCEDURE IsCompositeKey@8(VAR ApiWebhookEntity@1000 : Record 2000000097) : Boolean;
    BEGIN
      EXIT(STRPOS(ApiWebhookEntity."Key Fields",',') > 0);
    END;

    LOCAL PROCEDURE GetResourceUri@11(VAR ApiWebhookEntity@1000 : Record 2000000097) : Text;
    BEGIN
      IF (ApiWebhookEntity.Publisher <> '') AND (ApiWebhookEntity.Group <> '') THEN
        EXIT(STRSUBSTNO('%1/%2/%3/%4',
            ApiWebhookEntity.Publisher,ApiWebhookEntity.Group,ApiWebhookEntity.Version,ApiWebhookEntity.Name));
      EXIT(STRSUBSTNO('%1/%2',ApiWebhookEntity.Version,ApiWebhookEntity.Name));
    END;

    BEGIN
    END.
  }
}

