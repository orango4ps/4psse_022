OBJECT Table 11229918 Plant Quote
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 PlantSetup.GET;
                 PlantSetup.TESTFIELD("Plant Quote Nos.");
                 NoSeriesMgt.InitSeries(PlantSetup."Plant Quote Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;
               InitRecord();

               //**4PS Generated Code. Do Not Change.sn05
               DPA_OnInsert;
               //**4PS Generated Code.en05
             END;

    OnModify=BEGIN
               "Changed by" := USERID;
               "Changed on" := CURRENTDATETIME;
             END;

    OnDelete=VAR
               PlantQuoteLine@1100485002 : Record 11229919;
             BEGIN
               PlantQuoteLine.SETRANGE("Plant Quote No.","No.");
               PlantQuoteLine.DELETEALL;
             END;

    OnRename=BEGIN
               TESTFIELD(Status, Status::Open);
               TESTFIELD("Order No.", '');
               IF PlantQuoteHasLines(xRec."No.") THEN
                 ERROR(Text002);
               IF HasDocumentLinks(xRec) THEN
                 ERROR(Text002);
             END;

    CaptionML=ENU=Plant Quote;
    LookupPageID=Page11229915;
    DrillDownPageID=Page11229915;
  }
  FIELDS
  {
    { 10  ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                PlantSetup.GET;
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  NoSeriesMgt.TestManual(PlantSetup."Plant Quote Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              NOR=Nr.;
                                                              SVE=Nr] }
    { 20  ;   ;Type                ;Option        ;OnValidate=VAR
                                                                PlantQuoteLine@1100529000 : Record 11229919;
                                                              BEGIN
                                                                IF Type <> xRec.Type THEN BEGIN
                                                                  IF PlantQuoteHasLines("No.") THEN
                                                                    ERROR(Text001, PlantQuoteLine.TABLECAPTION, Type);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Typ];
                                                   OptionCaptionML=ENU=Rental,Sales;
                                                   OptionString=Rental,Sales;
                                                   Description=Sales is Exit }
    { 30  ;   ;Quote Date          ;Date          ;CaptionML=[ENU=Quote Date;
                                                              NOR=Tilbudsdato;
                                                              SVE=Offertdatum] }
    { 40  ;   ;Delivery Date       ;Date          ;CaptionML=[ENU=Delivery Date;
                                                              NOR=Leveringsdato;
                                                              SVE=Leveransdatum];
                                                   Description=Transfer Date or Exit Date }
    { 50  ;   ;Customer No.        ;Code20        ;TableRelation=Customer;
                                                   OnValidate=VAR
                                                                Customer@1100529000 : Record 18;
                                                              BEGIN
                                                                IF "Customer No." = '' THEN BEGIN
                                                                  IF "Customer No." <> xRec."Customer No." THEN
                                                                    "Contact No." := '';
                                                                END ELSE BEGIN
                                                                  Customer.GET("Customer No.");
                                                                  Customer.TESTFIELD(Blocked, Customer.Blocked::" ");
                                                                  FillCustomerRelatedContactNo;
                                                                  IF "Customer No." <> xRec."Customer No." THEN BEGIN
                                                                    Name := Customer.Name;
                                                                    "Name 2" := Customer."Name 2";
                                                                    Address := Customer.Address;
                                                                    "Address 2" := Customer."Address 2";
                                                                    "Post Code" := Customer."Post Code";
                                                                    City := Customer.City;
                                                                    "Contact Person" := Customer.Contact;
                                                                    "Contact Phone No." := Customer."Phone No.";
                                                                    "E-Mail" := Customer."E-Mail";
                                                                    "Customer Price Group" := Customer."Customer Price Group";
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Customer No.;
                                                              NOR=Kundenr;
                                                              SVE=Kundnr] }
    { 55  ;   ;Contact No.         ;Code20        ;TableRelation=Contact WHERE (Type=FILTER(Company));
                                                   OnValidate=VAR
                                                                Contact@1004 : Record 5050;
                                                              BEGIN
                                                                IF "Contact No." <> '' THEN BEGIN
                                                                  Contact.GET("Contact No.");
                                                                  Contact.TESTFIELD(Blocked, FALSE);
                                                                  FillContactRelatedCustomerNo;
                                                                  IF ("Contact No." <> xRec."Contact No.") AND ("Customer No." = '') THEN BEGIN
                                                                    Name := Contact.Name;
                                                                    "Name 2" := Contact."Name 2";
                                                                    Address := Contact.Address;
                                                                    "Address 2" := Contact."Address 2";
                                                                    "Post Code" := Contact."Post Code";
                                                                    City := Contact.City;
                                                                    "Contact Person" := '';
                                                                    "Contact Phone No." := Contact."Phone No.";
                                                                    "E-Mail" := Contact."E-Mail";
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Contact No.;
                                                              NOR=Kontaktnr.;
                                                              SVE=Kontaktnr] }
    { 60  ;   ;Name                ;Text100       ;CaptionML=[ENU=Name;
                                                              NOR=Navn;
                                                              SVE=Namn] }
    { 61  ;   ;Name 2              ;Text50        ;CaptionML=[ENU=Name 2;
                                                              NOR=Navn 2;
                                                              SVE=Namn 2] }
    { 70  ;   ;Address             ;Text100       ;CaptionML=[ENU=Address;
                                                              NOR=Adresse;
                                                              SVE=Adress] }
    { 71  ;   ;Address 2           ;Text50        ;CaptionML=[ENU=Address 2;
                                                              NOR=Adresse 2;
                                                              SVE=Adress 2] }
    { 80  ;   ;Post Code           ;Code20        ;TableRelation="Post Code";
                                                   OnValidate=BEGIN
                                                                IF PostCode.GET("Post Code") THEN
                                                                  City := PostCode.City;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Post Code;
                                                              NOR=Postnr;
                                                              SVE=Postnr] }
    { 81  ;   ;City                ;Text30        ;CaptionML=[ENU=City;
                                                              NOR=Sted;
                                                              SVE=Summa inklusive moms] }
    { 85  ;   ;Country             ;Text50        ;CaptionML=[ENU=Country;
                                                              NOR=Land;
                                                              SVE=Land] }
    { 90  ;   ;Contact Person      ;Text100       ;TableRelation=Contact.No. WHERE (Company No.=FIELD(Contact No.));
                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Contact;
                                                              NOR=Kontaktperson;
                                                              SVE=Kontakt] }
    { 91  ;   ;Contact Phone No.   ;Text30        ;ExtendedDatatype=Phone No.;
                                                   CaptionML=[ENU=Contact Phone No.;
                                                              NOR=Kontaktpersonens telefonnummer;
                                                              SVE=Kontaktens telefonnr] }
    { 100 ;   ;E-Mail              ;Text80        ;ExtendedDatatype=E-Mail;
                                                   CaptionML=[ENU=Email;
                                                              NOR=E-post;
                                                              SVE=E-post] }
    { 110 ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              NOR=Nr.serie;
                                                              SVE=Nr-serie];
                                                   Editable=No }
    { 120 ;   ;Created by          ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              LoginMgtCU@1100485000 : Codeunit 418;
                                                              UserCode@1100485001 : Code[50];
                                                            BEGIN
                                                              UserCode := "Created by";           //* Field not editable, may also not be changed via lookup,
                                                              LoginMgtCU.LookupUserID(UserCode);  //* for that reason send local.
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Created by;
                                                              NOR=Opprettet av;
                                                              SVE=Skapades av];
                                                   Editable=No }
    { 125 ;   ;Created on          ;DateTime      ;CaptionML=[ENU=Created on;
                                                              NOR=Opprettet den;
                                                              SVE=Skapades];
                                                   Editable=No }
    { 130 ;   ;Status              ;Option        ;OnValidate=VAR
                                                                PlantOrder@1100529000 : Record 11012556;
                                                                ExitOrder@1100529001 : Record 11012559;
                                                                PlantQuoteLine@1100529003 : Record 11229919;
                                                                OrderFound@1100529002 : Boolean;
                                                              BEGIN
                                                                IF Status = Status::Order THEN BEGIN
                                                                  PlantQuoteLine.SETRANGE("Plant Quote No.", "No.");
                                                                  PlantQuoteLine.SETFILTER(Type, '<>%1', PlantQuoteLine.Type::Text);
                                                                  PlantQuoteLine.FINDFIRST;
                                                                END;
                                                                IF "Order No." <> '' THEN BEGIN
                                                                  CASE Type OF
                                                                    Type::Rental:
                                                                      OrderFound := PlantOrder.GET("Order No.");
                                                                    Type::Sales:
                                                                      OrderFound := ExitOrder.GET("Order No.");
                                                                  END;
                                                                  IF Status < Status::Order THEN BEGIN
                                                                    "Order Type" := "Order Type"::" ";
                                                                    "Order No." := '';
                                                                  END;
                                                                  IF OrderFound THEN
                                                                    TESTFIELD(Status, Status::Order);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=ENU=Open,,,Offered,,,Order,,,Expired;
                                                   OptionString=Open,,,Offered,,,Order,,,Expired }
    { 140 ;   ;Your Reference      ;Text35        ;CaptionML=[ENU=Your Reference;
                                                              NOR=Deres referanse;
                                                              SVE=Er referens] }
    { 150 ;   ;Order Type          ;Option        ;CaptionML=[ENU=Order Type;
                                                              NOR=Ordretype;
                                                              SVE=Ordertyp];
                                                   OptionCaptionML=ENU=" ,,Plant Order,Exit Order";
                                                   OptionString=[ ,,PlantOrder,ExitOrder];
                                                   Editable=No }
    { 155 ;   ;Order No.           ;Code20        ;TableRelation=IF (Order Type=CONST(PlantOrder)) "Plant Order"
                                                                 ELSE IF (Order Type=CONST(ExitOrder)) "Exit Order";
                                                   OnLookup=VAR
                                                              PlantOrder@1100525000 : Record 11012556;
                                                              ExitOrder@1100529000 : Record 11012559;
                                                            BEGIN
                                                              IF "Order No." = '' THEN
                                                                EXIT;
                                                              CASE "Order Type" OF
                                                                "Order Type"::PlantOrder:
                                                                  BEGIN
                                                                    IF PlantOrder.GET("Order No.") THEN BEGIN
                                                                      IF NOT PlantOrder.Posted THEN
                                                                        PAGE.RUNMODAL(0, PlantOrder)
                                                                      ELSE
                                                                        PAGE.RUNMODAL(PAGE::"Posted Plant Order List", PlantOrder);
                                                                    END;
                                                                  END;
                                                                "Order Type"::ExitOrder:
                                                                  BEGIN
                                                                    IF ExitOrder.GET("Order No.") THEN BEGIN
                                                                      IF NOT PlantOrder.Posted THEN
                                                                        PAGE.RUNMODAL(0, ExitOrder)
                                                                      ELSE
                                                                        PAGE.RUNMODAL(PAGE::"Posted Exit Order List", ExitOrder);
                                                                    END;
                                                                  END;
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Order No.;
                                                              NOR=Ordrenr.;
                                                              SVE=Ordernr];
                                                   Editable=No }
    { 160 ;   ;Department Code     ;Code20        ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1, "Department Code");
                                                              END;

                                                   CaptionML=[ENU=Global Dimension 1 Code;
                                                              NOR=Avdelingskode;
                                                              SVE=Mottagande avdelningskod];
                                                   CaptionClass='1,1,1' }
    { 170 ;   ;Total Amount        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Quote Line".Amount WHERE (Plant Quote No.=FIELD(No.)));
                                                   CaptionML=[ENU=Total Amount;
                                                              NOR=Totalt bel›p;
                                                              SVE=Total Amount];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 200 ;   ;From Location       ;Code20        ;TableRelation=IF (Type=CONST(Rental)) "Plant Location" WHERE (Depot=CONST(Yes))
                                                                 ELSE "Plant Location";
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("From Location Description");
                                                              END;

                                                   CaptionML=[ENU=From Location;
                                                              NOR=Fra lokasjon;
                                                              SVE=Fr†n lagerst„lle] }
    { 201 ;   ;From Location Description;Text100  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Location".Description WHERE (Code=FIELD(From Location)));
                                                   CaptionML=[ENU=From Location Description;
                                                              NOR=Fra lokasjonsbeskrivelse;
                                                              SVE=Fr†n beskrivning av lagerst„lle];
                                                   Editable=No }
    { 210 ;   ;Plant Location      ;Code20        ;TableRelation="Plant Location".Code WHERE (Depot=CONST(No));
                                                   OnValidate=VAR
                                                                PlantLocation@1100529600 : Record 11012554;
                                                              BEGIN
                                                                CALCFIELDS("Plant Location Description");
                                                                FillRateType;

                                                                IF ("Plant Location" <> '') AND ("Plant Location" <> xRec."Plant Location") THEN BEGIN
                                                                  PlantLocation.GET("Plant Location");
                                                                  PlantLocation.TESTFIELD(Depot, FALSE);
                                                                  VALIDATE("Customer No.", PlantLocation."Customer No.");
                                                                  Address := PlantLocation.Address;
                                                                  "Address 2" := PlantLocation."Address 2";
                                                                  "Post Code" := PlantLocation."Post Code";
                                                                  City := PlantLocation.City;
                                                                  "Contact Person" := PlantLocation.Contact;
                                                                  "Contact Phone No." := PlantLocation."Phone No.";
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Plant Location;
                                                              NOR=Maskinplassering;
                                                              SVE=Maskinutplacering] }
    { 211 ;   ;Plant Location Description;Text100 ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Location".Description WHERE (Code=FIELD(Plant Location)));
                                                   CaptionML=[ENU=Plant Location Description;
                                                              NOR=Beskrivelse av maskinplassering;
                                                              SVE=Beskrivning av Maskinsplats];
                                                   Editable=No }
    { 220 ;   ;Rate Type           ;Option        ;CaptionML=[ENU=Rate Type;
                                                              NOR=Avgiftstype;
                                                              SVE=Avgiftstyp];
                                                   OptionCaptionML=[ENU=Day,Week,,Month;
                                                                    NOR=Dag,Uke,,M†ned;
                                                                    SVE=Dag,Vecka,,M†nad];
                                                   OptionString=Day,Week,,Month;
                                                   Editable=No }
    { 230 ;   ;Changed by          ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Changed by;
                                                              NOR=Endret av;
                                                              SVE=Žndrat av];
                                                   Editable=No }
    { 240 ;   ;Changed on          ;DateTime      ;CaptionML=[ENU=Changed on;
                                                              NOR=Endret den;
                                                              SVE=Žndrat den];
                                                   Editable=No }
    { 250 ;   ;Customer Price Group;Code10        ;TableRelation="Customer Price Group";
                                                   ValidateTableRelation=Yes;
                                                   TestTableRelation=Yes;
                                                   CaptionML=[ENU=Customer Price Group;
                                                              NOR=Kundeprisgruppe;
                                                              SVE=Kund prisgrupp] }
    { 260 ;   ;Amount Rental       ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Quote Line".Amount WHERE (Plant Quote No.=FIELD(No.),
                                                                                                    Line Type=CONST(Rental)));
                                                   CaptionML=ENU=Amount Rental;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 270 ;   ;Amount Sales/Buy Back;Decimal      ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Quote Line".Amount WHERE (Plant Quote No.=FIELD(No.),
                                                                                                    Line Type=CONST(Sales/Buy Back)));
                                                   CaptionML=ENU=Amount Sales/Buy Back;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 280 ;   ;Amount Other        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Quote Line".Amount WHERE (Plant Quote No.=FIELD(No.),
                                                                                                    Line Type=CONST(Other)));
                                                   CaptionML=ENU=Amount Other;
                                                   Editable=No;
                                                   AutoFormatType=1 }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Order Type,Order No.                     }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PlantSetup@1100485004 : Record 11012550;
      PostCode@1100485003 : Record 225;
      NoSeriesMgt@1100485000 : Codeunit 396;
      DimMgt@1100525008 : Codeunit 408;
      DPA_Management@1100000999 : Codeunit 11012783;
      SkipModify@1100528400 : Boolean;
      Text001@1100529000 : TextConst 'ENU=Already %1 present, modify %2 not allowed';
      Text002@1100529001 : TextConst 'ENU=Rename not allowed;NOR=Ikke tillatt † endre navn.;SVE=Det „r inte till†tet att byta namn.';
      Text003@1100529002 : TextConst 'ENU="%1 ''%2'' is modified in ''%3''. \Attention: There are already lines present, check/modify the prices. "';

    PROCEDURE AssistEdit@2(XRecPlantQuote@11012000 : Record 11229918) : Boolean;
    VAR
      PlantQuote@1100485000 : Record 11229918;
    BEGIN
      WITH PlantQuote DO BEGIN
        PlantQuote := Rec;
        PlantSetup.GET;
        PlantSetup.TESTFIELD("Plant Quote Nos.");
        IF NoSeriesMgt.SelectSeries(PlantSetup."Plant Quote Nos.",XRecPlantQuote."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("No.");
          Rec := PlantQuote;
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE InitRecord@1100485002();
    BEGIN
      Status := Status::Open;
      "Quote Date" := WORKDATE;
      "Created by" := USERID;
      "Created on" := CURRENTDATETIME;
      "Order Type" := 0;
      "Order No." := '';
      FillRateType;
    END;

    PROCEDURE PlantQuoteIsEditable@1100529014() : Boolean;
    BEGIN
      IF ("No." <> '') AND (Status = Status::Order) AND ("Order No." <> '') THEN
        EXIT(FALSE);
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE FillCustomerRelatedContactNo@1100529000();
    VAR
      ContactBusinessRelation@1100529000 : Record 5054;
      Contact@1100529001 : Record 5050;
      NewContactNo@1100529002 : Code[20];
    BEGIN
      IF "Customer No." = '' THEN
        EXIT;
      NewContactNo := '';
      ContactBusinessRelation.SETCURRENTKEY("Link to Table", "No.");
      ContactBusinessRelation.SETRANGE("Link to Table", ContactBusinessRelation."Link to Table"::Customer);
      ContactBusinessRelation.SETRANGE("No.", "Customer No.");
      IF ContactBusinessRelation.FINDFIRST THEN BEGIN
        IF Contact.GET(ContactBusinessRelation."Contact No.") THEN BEGIN
          IF Contact.Type = Contact.Type::Company THEN
            NewContactNo := ContactBusinessRelation."Contact No.";
        END;
      END;
      IF NewContactNo <> "Contact No." THEN
        VALIDATE("Contact No.", NewContactNo);
    END;

    LOCAL PROCEDURE FillContactRelatedCustomerNo@1100529005();
    VAR
      ContactBusinessRelation@1100529000 : Record 5054;
      NewCustomerNo@1100529001 : Code[20];
    BEGIN
      IF "Contact No." = '' THEN
        EXIT;
      NewCustomerNo := '';
      ContactBusinessRelation.SETCURRENTKEY("Link to Table", "Contact No.");
      ContactBusinessRelation.SETRANGE("Link to Table", ContactBusinessRelation."Link to Table"::Customer);
      ContactBusinessRelation.SETRANGE("Contact No.", "Contact No.");
      IF ContactBusinessRelation.FINDFIRST THEN
        NewCustomerNo := ContactBusinessRelation."No.";
      IF NewCustomerNo <> "Customer No." THEN BEGIN
        IF NewCustomerNo = '' THEN
          "Customer No." := ''
        ELSE
          VALIDATE("Customer No.", NewCustomerNo);
      END;
    END;

    LOCAL PROCEDURE ValidateShortcutDimCode@29(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateDimValueCode(FieldNumber,ShortcutDimCode);
      IF NOT SkipModify THEN
        MODIFY;
    END;

    LOCAL PROCEDURE FillRateType@1100529002();
    VAR
      PlantLocation@1100529000 : Record 11012554;
      NewPlantQuote@1100529001 : Record 11229918;
    BEGIN
      IF "Plant Location" = '' THEN BEGIN
        PlantSetup.GET;
        NewPlantQuote."Rate Type" := PlantSetup."Rate Type";
      END ELSE BEGIN
        PlantLocation.GET("Plant Location");
        NewPlantQuote."Rate Type" := PlantLocation."Rate Type";
      END;
      IF NewPlantQuote."Rate Type" <> "Rate Type" THEN BEGIN
        IF PlantQuoteHasLines("No.") THEN
          MESSAGE(Text003, FIELDCAPTION("Rate Type"),"Rate Type", NewPlantQuote."Rate Type");
        "Rate Type" := NewPlantQuote."Rate Type";
      END;
    END;

    LOCAL PROCEDURE PlantQuoteHasLines@1100529012(PlantQuoteNo@1100529001 : Code[20]) : Boolean;
    VAR
      PlantQuoteLine@1100529000 : Record 11229919;
    BEGIN
      PlantQuoteLine.SETRANGE("Plant Quote No.", PlantQuoteNo);
      EXIT(NOT PlantQuoteLine.ISEMPTY);
    END;

    PROCEDURE ContactCreateAsCustomer@1100529001();
    VAR
      Contact@1100529000 : Record 5050;
      ContactBusinessRelation@1100529001 : Record 5054;
    BEGIN
      TESTFIELD("Customer No.", '');
      TESTFIELD("Contact No.");
      Contact.GET("Contact No.");
      Contact.InsertingCustomerIsAllowed(TRUE);
      Contact.SelectTemplAndCreateCustomer;
      ContactBusinessRelation.SETCURRENTKEY("Link to Table", "Contact No.");
      ContactBusinessRelation.SETRANGE("Link to Table", ContactBusinessRelation."Link to Table"::Customer);
      ContactBusinessRelation.SETRANGE("Contact No.", "Contact No.");
      IF ContactBusinessRelation.FINDFIRST THEN
        "Customer No." := ContactBusinessRelation."No.";
    END;

    PROCEDURE EditWordDocuments@1100528702(SendMail@1100529000 : Boolean);
    VAR
      Contact@1100528701 : Record 5050;
      DocumentTemplate@1100529001 : Record 11012401;
      DocumentMailing@1210190000 : Codeunit 11012278;
      DocumentLinkRIDs@1100525001 : ARRAY [5] OF RecordID;
    BEGIN
      TESTFIELD("No.");
      TESTFIELD("Contact No.");
      Contact.GET("Contact No.");
      DocumentLinkRIDs[1] := RECORDID;
      DocumentMailing.EditDocuments(
        STRSUBSTNO('%1', DocumentTemplate.Type::"Plant Quote"), DocumentLinkRIDs,
        "Department Code", "Contact No.", '', SendMail, "E-Mail");
    END;

    PROCEDURE HasDocumentLinks@1100525006(PlantQuote@1100529000 : Record 11229918) : Boolean;
    VAR
      DocumentLinkMgt@1100525000 : Codeunit 11012401;
      RecRef@1100525001 : RecordRef;
    BEGIN
      RecRef.GETTABLE(PlantQuote);
      EXIT(DocumentLinkMgt.EntityHasDocumentLinks(RecRef));
    END;

    PROCEDURE DPA_GetFieldNoDepartmentFilter@1100000993() : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      EXIT(0);
    END;

    PROCEDURE DPA_GetFiltersFltrGrp@1100000997(FilterGroupNo@1100525000 : Integer) FilterText : Text[250];
    BEGIN
      //**4PS Generated Code - Do Not Change
      FILTERGROUP(FilterGroupNo);
      FilterText := COPYSTR(GETFILTERS, 1, MAXSTRLEN(FilterText));
      FILTERGROUP(0);
    END;

    PROCEDURE DPA_SetFilters@1100000998(Context@1100525000 : Text[30]) : Boolean;
    VAR
      RecRef@1100525003 : RecordRef;
      i@1100525004 : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN
        EXIT;

      RecRef.GETTABLE(Rec);
      DPA_Management.SetDPAFilterFromContext(
        RecRef,
        Context,
        DPA_GetFieldNoDepartmentCode,
        DPA_GetFieldNoDepartmentFilter );

      FOR i := 10 DOWNTO 0 DO BEGIN
        RecRef.FILTERGROUP(i);
        FILTERGROUP(i);
        SETVIEW(RecRef.GETVIEW(FALSE));
      END;

      EXIT(DPA_Management.IsHardFilter);
    END;

    LOCAL PROCEDURE DPA_IsActive@1100000999() : Boolean;
    VAR
      DepartmentAuthorizObject@1100526000 : Record 11012466;
      FPSLicenseManagement@1100528500 : Codeunit 11229289;
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT FPSLicenseManagement.LicenseAndReadPermissionForTable(DepartmentAuthorizObject) THEN
        EXIT(FALSE);

      IF NOT DPA_Management.AuthorizationActive THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    PROCEDURE DPA_SetDefaultDpmtFromNoSeries@1100000995(xRecRef@1100525002 : RecordRef);
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN
        EXIT;

      xRecRef.SETTABLE(xRec);
      DPA_Management.SetDepartmentCodeNoSeries(
        TABLENAME,
        "Department Code",
        "No. Series",
        xRec."No. Series" );
    END;

    LOCAL PROCEDURE DPA_OnInsert@1100000991();
    VAR
      Dimension1Code@1100528400 : Code[20];
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN
        EXIT;

      Dimension1Code :=
        DPA_Management.GetNoSeriesDepartmentCode(
          "No. Series",
          xRec."No. Series",
          DATABASE::"Plant Quote");
      IF Dimension1Code <> '' THEN
        "Department Code" := Dimension1Code;

      IF "Department Code" <> '' THEN BEGIN
        SkipModify := TRUE;
        VALIDATE("Department Code");
        SkipModify := FALSE;
      END;
    END;

    PROCEDURE DPA_FindRecWithDPAFilterAppld@1100000989();
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN BEGIN
        FIND;
        EXIT;
      END;

      IF NOT DPA_Management.HasTableHardFilter(11229918) THEN BEGIN
        FIND;
        EXIT;
      END;

      DPA_SetFilters('Table 11229918');
      IF NOT FIND THEN
        DPA_Management.RaiseDPAErrorForTable(TABLECAPTION);
    END;

    PROCEDURE DPA_ChkDepartmentCodeAllowed@1100000990(IsHardFilter@1100526000 : Boolean);
    VAR
      DimensionManagement@1100526001 : Codeunit 408;
    BEGIN
      //**4PS Generated Code - Do Not Change
      DimensionManagement.CheckDepartmAuthWithFilterType(
        DATABASE::"Plant Quote", "Department Code", IsHardFilter);
    END;

    PROCEDURE DPA_GetFieldNoDepartmentCode@1100000994() : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      EXIT(FIELDNO("Department Code"));
    END;

    PROCEDURE DPA_SetDefaultDepartment@1100000996();
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN
        EXIT;

      DPA_Management.SetDepartmentCodeUser(TABLENAME, "Department Code");
    END;

    BEGIN
    END.
  }
}

