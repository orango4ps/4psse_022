OBJECT Report 11125360 Update My Notifications
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 1518=rimd,
                TableData 11229374=r,
                TableData 2000000120=r;
    CaptionML=[DEU=Meine Benachrichtigungen aktualisieren;
               ENU=Update My Notifications;
               NLD=Mijn berichten bijwerken];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 1100529601;0;DataItem;                 ;
               DataItemTable=Table2000000120;
               DataItemTableView=SORTING(User Name);
               OnPreDataItem=BEGIN
                               IF UserFilter = '' THEN
                                 User.SETRANGE("User Name")
                               ELSE
                                 User.SETFILTER("User Name", UserFilter);
                             END;

               OnAfterGetRecord=BEGIN
                                  MyNotifications.DeleteUserNotifications(User."User Name");
                                END;
                                 }

    { 1100529600;1;DataItem;                 ;
               DataItemTable=Table11229374;
               DataItemTableView=SORTING(Notification Id);
               OnAfterGetRecord=BEGIN
                                  MyNotifications.UpdateUserNotifications(User."User Name", "Default My Notification Setup");
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100529600;;Container;
                  ContainerType=ContentArea }

      { 1100529601;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100529602;2;Field  ;
                  Name=UserFilter;
                  CaptionML=[DEU=Benutzerfilter;
                             ENU=User Filter;
                             NLD=Gebruikersfilter;
                             NOR=Brukerfilter;
                             SVE=Anv„ndarfilter];
                  SourceExpr=UserFilter;
                  OnLookup=VAR
                             LocUser@1100529601 : Record 2000000120;
                             UserLookup@1100529600 : Page 9843;
                           BEGIN
                             LocUser.SETCURRENTKEY("User Name");
                             UserLookup.SETTABLEVIEW(LocUser);
                             UserLookup.LOOKUPMODE := TRUE;
                             IF UserLookup.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               UserLookup.GetSelectionFilter(LocUser);
                               GetUserFilter(LocUser);
                             END;
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      MyNotifications@1100529602 : Record 1518;
      UserFilter@1100529600 : Text;

    PROCEDURE GetUserFilter@1100529629(VAR FilteredUser@1100529600 : Record 2000000120) CostObjFilter : Text;
    VAR
      LocUser@1100529602 : Record 2000000120;
      LocUser2@1100529601 : Record 2000000120;
      PrevUserName@1100529603 : Code[50];
    BEGIN
      IF LocUser.ISEMPTY THEN
        EXIT;

      UserFilter := '';

      FilteredUser.FINDSET;
      REPEAT
        LocUser := FilteredUser;
        LocUser.MARK(TRUE);
      UNTIL FilteredUser.NEXT = 0;

      LocUser.MARKEDONLY(TRUE);
      LocUser.SETCURRENTKEY("User Name");
      LocUser2.SETCURRENTKEY("User Name");

      LocUser.FINDSET;
      REPEAT
        IF UserFilter = '' THEN BEGIN
          UserFilter := LocUser."User Name";
          LocUser2.GET(LocUser."User Security ID");
        END ELSE BEGIN
          LocUser2.NEXT;
          IF LocUser2."User Name" = LocUser."User Name" THEN BEGIN
            IF STRLEN(UserFilter) < 3 THEN
              UserFilter := UserFilter + '..'
            ELSE
              IF COPYSTR(UserFilter, STRLEN(UserFilter) - 1) <> '..' THEN
                UserFilter := UserFilter + '..'
          END ELSE BEGIN
            IF STRLEN(UserFilter) >= 3 THEN
              IF COPYSTR(UserFilter, STRLEN(UserFilter) - 1) = '..' THEN
                UserFilter := UserFilter + PrevUserName;
            UserFilter := UserFilter + '|' + LocUser."User Name";
            LocUser2.GET(LocUser."User Security ID");
          END;
        END;
        PrevUserName := LocUser."User Name";
      UNTIL LocUser.NEXT = 0;

      IF STRLEN(UserFilter) >= 3 THEN
        IF COPYSTR(UserFilter, STRLEN(UserFilter) - 1) = '..' THEN
          UserFilter := UserFilter + PrevUserName;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

