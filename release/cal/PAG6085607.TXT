OBJECT Page 6085607 Document Split and Merge
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW16.00.00.5.00.01;
  }
  PROPERTIES
  {
    Permissions=TableData 6085599=imd;
    Editable=No;
    CaptionML=[DEU=Belege trennen und zusammenfÅhren;
               ENU=Split and Merge Documents;
               NLD=Documenten splitsen en samenvoegen];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table6085599;
    PageType=Worksheet;
    OnOpenPage=VAR
                 Channel@1000000000 : Code[50];
               BEGIN
                 UpdateList;

                 IF FirstDocument."No." <> '' THEN BEGIN
                   SETCURRENTKEY("Document No.");
                   SETRANGE("Document No.",FirstDocument."No.");
                   IF FINDFIRST THEN;
                   SETRANGE("Document No.");
                   SETCURRENTKEY("Entry No.");
                 END;

                 Channel := CREATEGUID;
                 EventNotifierSource.ADDTEXT(Channel);

                 CurrPage.CaptureUI.PAGE.SetConfig('','',Channel);
               END;

    OnAfterGetRecord=VAR
                       Template@1000000000 : Record 6085579;
                     BEGIN
                       IF Page = 1 THEN BEGIN
                         IF "Source ID" <> '' THEN BEGIN
                           SourceID := "Source ID";
                           Description := Name;
                         END ELSE BEGIN
                           SourceID := Text001;
                           Description := ' - ' + Text002 + ' ' + FORMAT(Page);
                         END;
                       END ELSE BEGIN
                         SourceID := '';
                         Description := ' - ' + Text002 + ' ' + FORMAT(Page);
                       END;
                     END;

    ActionList=ACTIONS
    {
      { 1000000028;  ;ActionContainer;
                      Name=ActionItems;
                      CaptionML=;
                      ActionContainerType=ActionItems }
      { 1000000026;1 ;ActionGroup;
                      Name=Functions;
                      CaptionML=[DEU=F&unktion;
                                 ENU=F&unctions;
                                 NLD=F&uncties;
                                 SVE=Fu&nktion] }
      { 1160040004;2 ;Action    ;
                      Name=Move Up;
                      ShortCutKey=Ctrl+U;
                      CaptionML=[DEU=Nach oben;
                                 ENU=Move Up;
                                 NLD=Naar boven;
                                 SVE=Flytta upp];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=MoveUp;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 MovePages(TRUE);
                               END;
                                }
      { 1160040005;2 ;Action    ;
                      Name=Move Down;
                      ShortCutKey=Ctrl+O;
                      CaptionML=[DEU=Nach unten;
                                 ENU=Move Down;
                                 NLD=Naar beneden;
                                 SVE=Flytta ner];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=MoveDown;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 MovePages(FALSE);
                               END;
                                }
      { 1160040001;2 ;Action    ;
                      Name=Split;
                      ShortCutKey=Ctrl+S;
                      CaptionML=[DEU=Auf&teilen;
                                 ENU=&Split;
                                 NLD=Splitsen;
                                 SVE=&Dela];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Splitlines;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 TempDocPage2@1160040007 : TEMPORARY Record 6085599;
                                 DocModMgt@1160040006 : Codeunit 6085613;
                                 NewDocNo@1160040005 : ARRAY [100] OF Code[20];
                                 NewDocCount@1160040004 : Integer;
                                 PageNo@1160040003 : Integer;
                                 EntryNo@1160040002 : Integer;
                                 i@1160040001 : Integer;
                               BEGIN
                                 EntryNo := "Entry No.";
                                 CLEAR(TempDocPage);
                                 CurrPage.SETSELECTIONFILTER(TempDocPage);
                                 NewDocCount := DocModMgt.SplitPages(TempDocPage,TempDocPage2,NewDocNo);

                                 TempDocPage2.FINDSET;
                                 REPEAT
                                   i += 1;
                                   PageNo := 0;
                                   SETRANGE("Document No.",TempDocPage2."Document No.");
                                   FINDSET(TRUE,FALSE);
                                   REPEAT
                                     IF Page >= TempDocPage2.Page THEN BEGIN
                                       PageNo += 1;
                                       "Document No." := NewDocNo[NewDocCount];
                                       Page := PageNo;
                                       IF PageNo = 1 THEN
                                         "Display Document No." := NewDocNo[NewDocCount];
                                       "Source ID" := '';
                                       Name := '';
                                       MODIFY;

                                       TempDocPage := Rec;
                                       TempDocPage.MODIFY;
                                     END;
                                   UNTIL NEXT = 0;
                                   NewDocCount -= 1;
                                 UNTIL TempDocPage2.NEXT = 0;

                                 SETRANGE("Document No.");
                                 GET(EntryNo);
                               END;
                                }
      { 1160040000;2 ;Action    ;
                      Name=Merge;
                      ShortCutKey=Ctrl+M;
                      CaptionML=[DEU=ZusammenfÅhren;
                                 ENU=Merge;
                                 NLD=Samenvoegen;
                                 SVE=Samsortering];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Trace;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 TempDocPage2@1160040005 : TEMPORARY Record 6085599;
                                 DocModMgt@1160040004 : Codeunit 6085613;
                                 FirstDocNo@1160040003 : Code[20];
                                 PageNo@1160040002 : Integer;
                                 EntryNo@1160040001 : Integer;
                                 SourceID1@6085573 : Text[250];
                                 SourceID2@6085574 : Text[250];
                                 SourceName1@6085576 : Text[250];
                                 SourceName2@6085577 : Text[250];
                                 Document2@6085575 : Record 6085590;
                                 SeveralSourceID@6085578 : Boolean;
                               BEGIN
                                 EntryNo := "Entry No.";
                                 CLEAR(TempDocPage);
                                 CurrPage.SETSELECTIONFILTER(TempDocPage);
                                 PageNo := DocModMgt.MergePages(TempDocPage,TempDocPage2);
                                 TempDocPage2.FINDSET;
                                 FirstDocNo := TempDocPage2."Document No.";

                                 SourceID1 := TempDocPage2."Source ID";
                                 SourceName1 := TempDocPage2.Name;
                                 TempDocPage2.NEXT;
                                 REPEAT
                                   SETRANGE("Document No.",TempDocPage2."Document No.");
                                   FINDSET(TRUE,FALSE);
                                   REPEAT
                                     IF (SourceID1 = '') OR (SourceID2 = '') THEN BEGIN
                                       SourceID2 := "Source ID";
                                       SourceName2 := Name;
                                       IF SourceID2 <> '' THEN BEGIN
                                         IF SourceID1 = '' THEN BEGIN
                                           SourceID1 := SourceID2;
                                           SourceName1 := SourceName2;
                                           SourceID2 := '';
                                           SourceName2 := '';
                                         END ELSE
                                           IF SourceID1 <> SourceID2 THEN
                                             SeveralSourceID := TRUE
                                           ELSE
                                             SourceID2 := '';
                                       END;
                                     END;

                                     PageNo += 1;
                                     "Document No." := FirstDocNo;
                                     Page := PageNo;
                                     "Display Document No." := '';
                                     "Source ID" := '';
                                     Name := '';
                                     MODIFY;

                                     TempDocPage := Rec;
                                     TempDocPage.MODIFY;
                                   UNTIL NEXT = 0;
                                 UNTIL TempDocPage2.NEXT = 0;

                                 IF SeveralSourceID THEN BEGIN
                                   SETRANGE("Document No.",FirstDocNo);
                                   IF FINDFIRST THEN BEGIN
                                     "Source ID" := '';
                                     MODIFY;
                                   END;
                                 END ELSE
                                   IF SourceID1 <> ''  THEN BEGIN
                                     SETRANGE("Document No.",FirstDocNo);
                                     IF FINDFIRST THEN
                                       IF "Source ID" = '' THEN BEGIN
                                         "Source ID" := SourceID1;
                                         Name := SourceName1;
                                         MODIFY;
                                       END;
                                   END;

                                 SETRANGE("Document No.");

                                 GET(EntryNo);
                                 UpdateImage;
                               END;
                                }
      { 1160040003;2 ;Action    ;
                      Name=Rotate;
                      ShortCutKey=Ctrl+R;
                      CaptionML=[DEU=Rotieren;
                                 ENU=Rotate;
                                 NLD=Roteren;
                                 SVE=Rotera];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Delegate;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 DocModMgt@1160040000 : Codeunit 6085613;
                               BEGIN
                                 CLEAR(TempDocPage);
                                 CurrPage.SETSELECTIONFILTER(TempDocPage);
                                 DocModMgt.RotatePages(TempDocPage);
                                 UpdateImage;
                               END;
                                }
      { 1160040002;2 ;Action    ;
                      Name=Delete;
                      ShortCutKey=Ctrl+Delete;
                      CaptionML=[DEU=Lîschen;
                                 ENU=Delete;
                                 NLD=Verwijder;
                                 SVE=Ta bort];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Reject;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 TempDocPage2@1160040004 : TEMPORARY Record 6085599;
                                 DocModMgt@1160040003 : Codeunit 6085613;
                                 PrevDocNo@1160040002 : Code[20];
                                 EntryNo@1160040001 : Integer;
                                 Count@1160040000 : Integer;
                               BEGIN
                                 EntryNo := "Entry No.";
                                 CLEAR(TempDocPage);
                                 CurrPage.SETSELECTIONFILTER(TempDocPage);
                                 IF NOT DocModMgt.DeletePages(TempDocPage,TempDocPage2) THEN
                                   EXIT;

                                 TempDocPage2.FINDSET;
                                 REPEAT
                                   IF PrevDocNo <> TempDocPage2."Document No." THEN BEGIN
                                     PrevDocNo := TempDocPage2."Document No.";
                                     Count := 0;
                                   END ELSE
                                     Count += 1;

                                   GET(TempDocPage2."Entry No.");
                                   DELETE;
                                   TempDocPage.GET(TempDocPage2."Entry No.");
                                   TempDocPage.DELETE;

                                   SETRANGE("Document No.",TempDocPage2."Document No.");
                                   IF FINDSET(TRUE,FALSE) THEN
                                     REPEAT
                                       IF Page >= TempDocPage2.Page - Count THEN BEGIN
                                         Page -= 1;
                                         "Display Document No." := '';
                                         IF Page = 1 THEN
                                           "Display Document No." := "Document No.";
                                         MODIFY;

                                         TempDocPage := Rec;
                                         TempDocPage.MODIFY;
                                       END;
                                     UNTIL NEXT = 0;
                                 UNTIL TempDocPage2.NEXT = 0;

                                 SETRANGE("Document No.");
                                 SETFILTER("Entry No.",'>=%1',EntryNo);
                                 IF NOT FINDFIRST THEN BEGIN
                                   SETRANGE("Entry No.");
                                   IF FINDLAST THEN;
                                 END ELSE
                                   SETRANGE("Entry No.");

                                 UpdateImage;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1000000000;;Container;
                Name=ContentArea;
                ContainerType=ContentArea }

    { 1000000016;1;Group  ;
                Name=Group1;
                CaptionML=[ENU="";
                           NLD=""];
                GroupType=Group }

    { 1000000021;2;Field  ;
                Name=EventNotifier;
                SourceExpr=EventNotifierSource;
                ControlAddIn=[Akeyto.DocumentCapture.Dynamics.Nav.EventNotifier;PublicKeyToken=0c53e51c0afe7001];
                OnControlAddIn=BEGIN
                                 IF Index = 0 THEN
                                   HandleSimpleCommand(CaptureAddinLib.BigTextToText(Data))
                               END;
                                }

    { 1000000001;1;Group  ;
                Name=Group2;
                CaptionML=[ENU="";
                           NLD=""];
                GroupType=Repeater }

    { 1000000002;2;Field  ;
                Name=Display Document No.;
                CaptionML=[DEU=Belegnr.;
                           ENU=Document No.;
                           NLD=Documentnr.;
                           SVE=Dokumentnr];
                SourceExpr="Display Document No.";
                Editable=FALSE }

    { 1000000003;2;Field  ;
                Name=SourceID;
                CaptionML=[DEU=Herkunfts-ID;
                           ENU=Source ID;
                           NLD=Bron-id;
                           SVE=Ursprungs-ID];
                SourceExpr=SourceID;
                CaptionClass=GetSourceIDCaption }

    { 1000000004;2;Field  ;
                Name=Description;
                CaptionML=[DEU=Name;
                           ENU=Name;
                           NLD=Naam;
                           SVE=Namn];
                SourceExpr=Description;
                CaptionClass=GetSourceNameCaption }

    { 1000000013;;Container;
                Name=FactBoxArea;
                ContainerType=FactBoxArea }

    { 1000000012;1;Part   ;
                Name=CaptureUI;
                CaptionML=[DEU=Originalbeleg;
                           ENU=Original document;
                           NLD=Oorspronkelijke document;
                           SVE=Ursprungligt dokument];
                SubPageLink=Document No.=FIELD(Document No.),
                            Page No.=FIELD(Page);
                PagePartID=Page6085608 }

  }
  CODE
  {
    VAR
      TempDocPage@1160040013 : TEMPORARY Record 6085599;
      Document@1160040012 : Record 6085590;
      DCSetup@1160040011 : Record 6085573;
      FirstDocument@1160040001 : Record 6085590;
      CaptureAddinLib@1000000011 : Codeunit 6085660;
      DocCatCode@1160040003 : Code[10];
      SourceID@1160040005 : Text[250];
      Description@1160040004 : Text[250];
      EventNotifierSource@1000000008 : BigText;
      Text001@1160040002 : TextConst 'DEU=[KEIN];ENU=[NONE];NLD=[NONE];SVE=[INGEN]';
      Text002@1160040000 : TextConst 'DEU=Seite;ENU=Page;NLD=Pagina;SVE=Sida';
      SourceIDText@6085574 : TextConst 'DEU=Herkunfts-ID;ENU=Source ID;NLD=Bron-id';
      SourceNameText@6085573 : TextConst 'DEU=Herkunftsname;ENU=Source Name;NLD=Bronnaam';

    PROCEDURE HandleSimpleCommand@1000000006(Command@1000000000 : Text[1024]);
    BEGIN
      CASE Command OF
        'UPDATEDOCUMENT',
        'UPDATEHEADER':
      //  CurrPage.UPDATE(FALSE); // May be necessary in 2009 and 2009 SP1
          ;
      END;
    END;

    PROCEDURE UpdateList@1000000001();
    BEGIN
      TempDocPage.BuildTable(DocCatCode,Document);
      DELETEALL;
      IF TempDocPage.FINDSET THEN
        REPEAT
          Rec := TempDocPage;
          Rec.INSERT;
        UNTIL TempDocPage.NEXT = 0;
      IF FINDFIRST THEN;
      CurrPage.UPDATE(FALSE);
    END;

    PROCEDURE UpdateImage@1160040000();
    BEGIN
      CurrPage.CaptureUI.PAGE.SetForceUpdate(TRUE);
      CurrPage.CaptureUI.PAGE.UpdatePage;
      CurrPage.UPDATE(FALSE);
    END;

    PROCEDURE SetParam@1160040001(NewDocCatCode@1160040001 : Code[10];VAR Document@1160040000 : Record 6085590);
    BEGIN
      DocCatCode := NewDocCatCode;
      FirstDocument := Document;
    END;

    PROCEDURE MovePages@1160040002(MoveUp@1160040000 : Boolean);
    VAR
      DocModMgt@1160040001 : Codeunit 6085613;
      EntryNo@1160040002 : Integer;
    BEGIN
      EntryNo := "Entry No.";
      CurrPage.SETSELECTIONFILTER(TempDocPage);
      IF NOT DocModMgt.MovePages(TempDocPage,MoveUp) THEN
        EXIT;

      IF MoveUp THEN BEGIN
        SETFILTER("Entry No.",'<%1',EntryNo);
        FINDLAST;
        SETRANGE("Entry No.");
      END ELSE
        NEXT;

      UpdateImage;
    END;

    PROCEDURE GetSourceIDCaption@6085574() Caption : Text[250];
    VAR
      DocCat@1160040001 : Record 6085575;
      AllObjWithCaption@1160040000 : Record 2000000058;
      RecIDMgt@1160040002 : Codeunit 6085604;
    BEGIN
      Caption := SourceIDText;
      IF DocCat.GET(DocCatCode) THEN
        IF DocCat."Source Table No." <> 0 THEN
          Caption := RecIDMgt.GetObjectCaption(AllObjWithCaption."Object Type"::Table,DocCat."Source Table No.");
    END;

    PROCEDURE GetSourceNameCaption@6085573() Caption : Text[250];
    VAR
      DocCat@1160040002 : Record 6085575;
      AllObjWithCaption@1160040001 : Record 2000000058;
      RecIDMgt@1160040000 : Codeunit 6085604;
    BEGIN
      Caption := SourceNameText;
      IF DocCat.GET(DocCatCode) THEN
        IF (DocCat."Source Table No." <> 0) AND (DocCat."Source Field No. (Name)" <> 0) THEN
          Caption := RecIDMgt.GetFieldCaption(DocCat."Source Table No.",DocCat."Source Field No. (Name)");
    END;

    BEGIN
    END.
  }
}

