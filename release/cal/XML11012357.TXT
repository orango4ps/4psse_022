OBJECT XMLport 11012357 Discount Terms (CSV) v10
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Discount Terms (CSV);
               SVE=Rabattvillkor (CSV)];
    DefaultFieldsValidation=No;
    PreserveWhiteSpace=Yes;
    TextEncoding=WINDOWS;
    OnPreXMLport=VAR
                   FPSLicenseManagement@1100527400 : Codeunit 11229289;
                 BEGIN
                   IF FPSLicenseManagement.LicenseAndReadPermissionForTable(CompRelationRec) THEN BEGIN
                     CentrManagementCU.ItemFormEditable(NavBln,DiscBln,DiscGrpBln,RcpBln);
                     IF (gvDiscLevel = 0) AND (DiscBln) THEN
                       ERROR(Text000);
                     IF (gvDiscLevel = 1) AND (DiscGrpBln) THEN
                       ERROR(Text000);
                   END;
                 END;

    Format=Variable Text;
    FieldDelimiter=<None>;
    FieldSeparator=[;];
    UseRequestPage=No;
  }
  ELEMENTS
  {
    { [{AE5B5AAC-6273-41CA-BDFF-3CF86A133295}];  ;Root                ;Element ;Text     }

    { [{AE3B6C07-5E43-4B19-8485-4774F09E7162}];1 ;ImportDiscountTerm  ;Element ;Table   ;
                                                  SourceTable=Table11012736;
                                                  Import::OnAfterInitRecord=BEGIN
                                                                              "Import Discount Term".INIT;
                                                                              CLEAR(MgtCode);
                                                                              CLEAR(ItemDiscCode);
                                                                              CLEAR(DescTxt);
                                                                              CLEAR(PriceTxt);
                                                                              CLEAR(Disc1Txt);
                                                                              CLEAR(Disc2Txt);
                                                                              CLEAR(Disc3Txt);
                                                                            END;

                                                  Import::OnBeforeInsertRecord=VAR
                                                                                 ImportDiscountTerm@1100525000 : Record 11012736;
                                                                               BEGIN
                                                                                 WITH "Import Discount Term" DO BEGIN

                                                                                 IF (MgtCode = '') AND
                                                                                    (ItemDiscCode = '') AND
                                                                                    (DescTxt = '')
                                                                                 THEN
                                                                                   currXMLport.SKIP;

                                                                                 IF MgtCode = 'T' THEN
                                                                                   currXMLport.SKIP;

                                                                                 "Discount Level" := gvDiscLevel;
                                                                                 "Discount Reference" := gvDiscRef;
                                                                                 Vendor := gvVendor;
                                                                                 IF Currency = '' THEN
                                                                                   Currency := gvCurrency;

                                                                                 CLEAR("Discount Group");
                                                                                 CLEAR("Item Code");

                                                                                 Description := DescTxt;  //db, 01-07-08: M12112

                                                                                 IF MgtCode = 'A' THEN
                                                                                   "Item Code" := ItemDiscCode
                                                                                 ELSE
                                                                                   "Discount Group" := ItemDiscCode;

                                                                                 CLEAR("Purchase Price");
                                                                                 CLEAR("Discount Percentage 1");
                                                                                 CLEAR("Discount Percentage 2");
                                                                                 CLEAR("Discount Percentage 3");

                                                                                 IF PriceTxt <> '' THEN EVALUATE("Purchase Price", PriceTxt);
                                                                                 IF Disc1Txt <> '' THEN EVALUATE("Discount Percentage 1", Disc1Txt);
                                                                                 IF Disc2Txt <> '' THEN EVALUATE("Discount Percentage 2", Disc2Txt);
                                                                                 IF Disc3Txt <> '' THEN EVALUATE("Discount Percentage 3", Disc3Txt);

                                                                                 CLEAR(MgtCode);
                                                                                 CLEAR(ItemDiscCode);
                                                                                 CLEAR(PriceTxt);
                                                                                 CLEAR(Disc1Txt);
                                                                                 CLEAR(Disc2Txt);
                                                                                 CLEAR(Disc3Txt);

                                                                                 "Starting Date" := gvStartDate;
                                                                                 VALIDATE("Starting Date");

                                                                                 //db, 01-07-04: store item price in local currency (used function: GetCurrencyRate)
                                                                                 VALIDATE(Currency, '');  //db, 07-12-04 (changed validate tablefield)

                                                                                 RecRef.GETTABLE("Import Discount Term");
                                                                                 XMLportStatusDialogMgt.ShowImportStatus(RecRef);

                                                                                 IF ImportDiscountTerm.GET(
                                                                                   Vendor,
                                                                                   "Discount Group",
                                                                                   "Item Code",
                                                                                   "Discount Level",
                                                                                   "Discount Reference",
                                                                                   "Starting Date")
                                                                                 THEN BEGIN
                                                                                   UpdateImportDiscountTerm(ImportDiscountTerm);
                                                                                   currXMLport.SKIP;
                                                                                 END;

                                                                                 END; //Belongs to With statement.
                                                                               END;
                                                                                }

    { [{02FB24B1-6E06-4F8A-9BE0-A514F028229F}];2 ;MgtCode             ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{7FC7FFCA-055D-4F0D-BBD4-D47209EA6B57}];2 ;ItemDiscCode        ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{5AAE15C7-0F1A-4F40-9171-024353E7B79E}];2 ;DescTxt             ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{C2D0B43E-3D46-405A-B9F3-ABAC73529613}];2 ;PriceTxt            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  PriceTxt := CONVERTSTR(PriceTxt,'.',',');
                                                                                  FillEmptyTextWithZero(PriceTxt);
                                                                                END;
                                                                                 }

    { [{DF6694AD-A791-4548-B2A5-D390F1C17A77}];2 ;Disc1Txt            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  Disc1Txt:= DELCHR(Disc1Txt, '<>=', '%');
                                                                                  Disc1Txt := CONVERTSTR(Disc1Txt,'.',',');
                                                                                  FillEmptyTextWithZero(Disc1Txt);
                                                                                END;
                                                                                 }

    { [{4FF40AA7-CC48-462E-9814-0B3B154C7AA7}];2 ;Disc2Txt            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  Disc2Txt := DELCHR(Disc2Txt, '<>=', '%');
                                                                                  Disc2Txt := CONVERTSTR(Disc2Txt,'.',',');
                                                                                  FillEmptyTextWithZero(Disc2Txt);
                                                                                END;
                                                                                 }

    { [{9D48C808-C7E1-4BEC-998E-6BA78FF45519}];2 ;Disc3Txt            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  Disc3Txt := DELCHR(Disc3Txt, '<>=', '%');
                                                                                  Disc3Txt := CONVERTSTR(Disc3Txt,'.',',');
                                                                                  FillEmptyTextWithZero(Disc3Txt);
                                                                                END;
                                                                                 }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      VendorRec@1100525018 : Record 11012304;
      CompRelationRec@1100525017 : Record 11012059;
      RecRef@1100525001 : RecordRef;
      XMLportStatusDialogMgt@1100525002 : Codeunit 11130006;
      CentrManagementCU@1100525016 : Codeunit 11012010;
      NavBln@1100525015 : Boolean;
      DiscBln@1100525014 : Boolean;
      DiscGrpBln@1100525003 : Boolean;
      RcpBln@1100525013 : Boolean;
      gvVendor@1100525012 : Code[20];
      gvCurrency@1100525011 : Code[10];
      gvDiscLevel@1100525010 : Integer;
      gvDiscRef@1100525009 : Code[20];
      gvStartDate@1100525007 : Date;
      Text000@1100525000 : TextConst 'ENU=Transaction only allowed in company used as source for central itemmanagement;SVE=Transaktionen „r endast till†ten f”r f”retag som anv„nds som k„lla f”r central artikelhantering';

    LOCAL PROCEDURE FillEmptyTextWithZero@1100525013(VAR Text@1100525000 : Text[1024]);
    BEGIN
      IF DELCHR(Text,'<>=',' ') = '' THEN
        Text := '0';
    END;

    PROCEDURE DetermineSelection@1(lvVendor@11012000 : Code[20];lvCurrency@11012001 : Code[10];lvDiscLevel@1100485002 : Integer;lvDiscRef@1100485001 : Code[20];lvStartDate@1100485003 : Date);
    BEGIN
      gvVendor := lvVendor;
      gvCurrency := lvCurrency;
      gvDiscLevel := lvDiscLevel;
      gvDiscRef := lvDiscRef;
      gvStartDate := lvStartDate;

      VendorRec.GET(gvVendor);
      IF VendorRec."Alternative Vendor Code" <> '' THEN BEGIN
        gvVendor := VendorRec."Alternative Vendor Code";
        VendorRec.GET(gvVendor);
      END;
    END;

    LOCAL PROCEDURE UpdateImportDiscountTerm@1100525000(ImportDiscountTerm@1100525000 : Record 11012736);
    BEGIN
      ImportDiscountTerm.Currency := "Import Discount Term".Currency;
      ImportDiscountTerm.Description := "Import Discount Term".Description;
      ImportDiscountTerm."Discount Percentage 1" := "Import Discount Term"."Discount Percentage 1";
      ImportDiscountTerm."Discount Percentage 2" := "Import Discount Term"."Discount Percentage 2";
      ImportDiscountTerm."Discount Percentage 3" := "Import Discount Term"."Discount Percentage 3";
      ImportDiscountTerm."Purchase Price" := "Import Discount Term"."Purchase Price";

      ImportDiscountTerm.MODIFY;
    END;

    BEGIN
    {
      CC: Dataport 11012357
    }
    END.
  }
}

