OBJECT Table 11125714 Prj.Plan. Team Resource Period
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               TeamResPeriodRec@1100525000 : Record 11125714;
             BEGIN
               TESTFIELD("Date in Team");
               CheckOverlap();

               TeamResPeriodRec.SETCURRENTKEY("Team Code", "Entry No.");
               TeamResPeriodRec.SETRANGE("Team Code", "Team Code");
               IF TeamResPeriodRec.FINDLAST THEN
                 "Entry No." := TeamResPeriodRec."Entry No." + 1
               ELSE
                 "Entry No." := 1;
             END;

    OnModify=BEGIN
               TESTFIELD("Date in Team");
               CheckOverlap();
             END;

    OnRename=BEGIN
               TESTFIELD("Date in Team");
               CheckOverlap();
             END;

    CaptionML=[ENU=Project Planning Team Resource Period;
               NOR=Ressursperiode for prosjektplanleggingsteam;
               SVE=Resursperiod f”r projektplaneringsteam];
    PasteIsValid=No;
  }
  FIELDS
  {
    { 10  ;   ;Team Code           ;Code20        ;TableRelation="Project Planning Team";
                                                   CaptionML=[ENU=Team Code;
                                                              NOR=Prosjektgruppekode;
                                                              SVE=Lagkkod] }
    { 20  ;   ;Resource Type       ;Option        ;CaptionML=[ENU=Resource Type;
                                                              NOR=Ressurstype;
                                                              SVE=Resurstyp];
                                                   OptionCaptionML=[ENU=Employee,Plant;
                                                                    NOR=Ansatt,Maskin;
                                                                    SVE=Anst„lld,Maskin];
                                                   OptionString=Employee,Plant }
    { 30  ;   ;Resource Code       ;Code20        ;TableRelation=IF (Resource Type=CONST(Employee)) Employee
                                                                 ELSE IF (Resource Type=CONST(Plant)) "Plant Type";
                                                   CaptionML=[ENU=Resource Code;
                                                              NOR=Ressurskode;
                                                              SVE=Resurskod];
                                                   NotBlank=Yes }
    { 40  ;   ;Plant No.           ;Code10        ;TableRelation=IF (Resource Type=CONST(Plant)) "Plant Number".No. WHERE (Plant Type=FIELD(Resource Code));
                                                   CaptionML=[ENU=Plant No.;
                                                              NOR=Maskinnr.;
                                                              SVE=Maskinnr] }
    { 50  ;   ;Date in Team        ;Date          ;OnValidate=BEGIN
                                                                IF ("Date in Team" <> 0D) AND ("Date out of Team" <> 0D) THEN BEGIN
                                                                  IF ("Date out of Team" <= "Date in Team") THEN
                                                                    FIELDERROR("Date in Team", STRSUBSTNO(Text000, FIELDCAPTION("Date out of Team")));
                                                                  CheckOverlap();
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Date in Team;
                                                              NOR=Dato i team;
                                                              SVE=Datum i team];
                                                   NotBlank=Yes }
    { 60  ;   ;Date out of Team    ;Date          ;OnValidate=BEGIN
                                                                IF ("Date out of Team" <> 0D) THEN BEGIN
                                                                  IF ("Date out of Team" <= "Date in Team") THEN
                                                                    FIELDERROR("Date out of Team", STRSUBSTNO(Text001, FIELDCAPTION("Date in Team")));
                                                                  CheckOverlap();
                                                                END ELSE BEGIN
                                                                  IF (xRec."Date out of Team" <> 0D) THEN
                                                                    CheckOverlap();
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Date out of Team;
                                                              NOR=Dato ute av team;
                                                              SVE=Datumet „r utanf”r teamet] }
    { 70  ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              NOR=Transaksjonsnr.;
                                                              SVE=L”pnr];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Team Code,Resource Type,Resource Code,Plant No.,Date in Team;
                                                   Clustered=Yes }
    {    ;Team Code,Entry No.                      }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100525002 : TextConst 'ENU=must be before %1;NOR=M† v‘re f›r %1;SVE=m†ste vara f”re %1';
      Text001@1100525000 : TextConst 'ENU=must be after %1;NOR=M† v‘re etter %'';SVE=m†ste vara efter %1';
      Text002@1100525001 : TextConst 'ENU="Overlap with %1 ''%2'', %3 ''%4'' ";NOR=Overlapper med %1 ''%2'', %3 ''%4'';SVE="™verlappande med %1 ''%2'', %3 ''%4'' "';

    PROCEDURE CheckOverlap@1100525000();
    VAR
      TeamResPeriodRec@1100525000 : Record 11125714;
      ResourceTxt@1100525001 : Text[50];
    BEGIN
      //* Only check for current team. Is allowed that a resource is in more than 1 team in the same period.
      //* It is up to the user that the total is not more than 100%.
      TeamResPeriodRec.SETRANGE("Team Code", "Team Code");
      TeamResPeriodRec.SETRANGE("Resource Type", "Resource Type");
      TeamResPeriodRec.SETRANGE("Resource Code", "Resource Code");
      TeamResPeriodRec.SETRANGE("Plant No.", "Plant No.");
      IF "Entry No." <> 0 THEN
        TeamResPeriodRec.SETFILTER("Entry No.", '<>%1', "Entry No.");  //* Skip current record
      IF TeamResPeriodRec.FINDFIRST THEN BEGIN
        REPEAT
          IF ((TeamResPeriodRec."Date in Team" < "Date out of Team") OR ("Date out of Team" = 0D)) AND
             ((TeamResPeriodRec."Date out of Team" > "Date in Team") OR (TeamResPeriodRec."Date out of Team" = 0D))
          THEN BEGIN
            ResourceTxt := "Resource Code";
            IF "Resource Type" = "Resource Type" ::Plant THEN
              ResourceTxt := ResourceTxt + '-' + "Plant No.";
            ERROR(Text002,
              FIELDCAPTION("Date in Team"), TeamResPeriodRec."Date in Team",
              FIELDCAPTION("Date out of Team"), TeamResPeriodRec."Date out of Team");
          END;
        UNTIL TeamResPeriodRec.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}

