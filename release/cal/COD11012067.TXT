OBJECT Codeunit 11012067 Small Project Plan Lines
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [External]
    PROCEDURE CreatePlanLines@1100485000(VAR TempPlanLines@1100485002 : TEMPORARY Record 11020265);
    VAR
      lProjAppRec@1100485001 : Record 11012081;
      lJobRec@1100485000 : Record 11072003;
    BEGIN
      WITH lProjAppRec DO BEGIN
        lJobRec.SETRANGE("Small Project",TRUE);
        lJobRec.SETFILTER("Small Project Status",'<%1',lJobRec."Small Project Status"::Finished);
        IF lJobRec.FINDSET THEN BEGIN
          REPEAT
            SETRANGE("Project No.",lJobRec."No.");
            SETRANGE("Appointment Type","Appointment Type"::Execution);
            IF FINDLAST THEN BEGIN
              IF "Starting Date" <> 0D THEN
                IF ("Ending Date" <> 0D) AND ("Ending Date" <> "Starting Date") THEN BEGIN
                  InsertPlanLine(lProjAppRec,lJobRec,TempPlanLines);
                  WHILE "Starting Date" <> "Ending Date" DO BEGIN
                    "Starting Date" := "Starting Date" + 1;
                    InsertPlanLine(lProjAppRec,lJobRec,TempPlanLines);
                  END;
                END ELSE
                  InsertPlanLine(lProjAppRec,lJobRec,TempPlanLines);
            END;
          UNTIL lJobRec.NEXT = 0;
        END;
      END;
    END;

    [External]
    PROCEDURE CreatePlanLine@1100525000(VAR TmpPlanLine@1100485002 : TEMPORARY Record 11020265;ProjApp@1100525000 : Record 11012081;Proj@1100525001 : Record 11072003);
    BEGIN
      WITH ProjApp DO BEGIN
        IF "Starting Date" <> 0D THEN BEGIN
          InsertPlanLine(ProjApp, Proj, TmpPlanLine);
          IF ("Ending Date" <> 0D) AND ("Ending Date" > "Starting Date") THEN BEGIN
            WHILE "Starting Date" < "Ending Date" DO BEGIN
              "Starting Date" := "Starting Date" + 1;
              InsertPlanLine(ProjApp, Proj, TmpPlanLine);
            END;
          END;
        END;
      END;
    END;

    [External]
    PROCEDURE InsertPlanLine@1100485004(lProjAppRec@1100485000 : Record 11012081;lJobRec@1100485001 : Record 11072003;VAR TempPlanLines@1100485003 : TEMPORARY Record 11020265);
    VAR
      DayInt@1100485002 : Integer;
      NextLineNo@1100525000 : Integer;
    BEGIN
      DayInt := DATE2DWY(lProjAppRec."Starting Date",1);
      IF (DayInt = 6) OR (DayInt = 7) THEN
        EXIT;
      TempPlanLines.RESET;
      TempPlanLines.SETRANGE("Project No.", lProjAppRec."Project No.");
      TempPlanLines.SETRANGE(Date, lProjAppRec."Starting Date");
      NextLineNo := 1;
      IF TempPlanLines.FINDLAST THEN
        NextLineNo := NextLineNo + TempPlanLines."Line No.";

      TempPlanLines.INIT;
      TempPlanLines."Project No." := lProjAppRec."Project No.";
      TempPlanLines.Date := lProjAppRec."Starting Date";
      TempPlanLines."Line No." := NextLineNo;
      TempPlanLines."Internal/External Action" := lProjAppRec."Internal/External Action";
      TempPlanLines."Action By" := lProjAppRec."Action by";
      TempPlanLines.Customer := lJobRec."Bill-to Customer No.";
      TempPlanLines."Small Project Status" := lJobRec."Small Project Status";
      TempPlanLines."Start Time" := lProjAppRec."Start Time";
      TempPlanLines."End Time" := lProjAppRec."End Time";
      TempPlanLines.INSERT;
    END;

    [External]
    PROCEDURE GetActionByName@1100485001(TempPlanLines@1100485000 : TEMPORARY Record 11020265) : Text[50];
    VAR
      lVendorRec@1100485001 : Record 23;
      lEmployeeRec@1100485002 : Record 5200;
    BEGIN
      CASE TempPlanLines."Internal/External Action" OF
        0:BEGIN
            IF lEmployeeRec.GET(TempPlanLines."Action By") THEN
              EXIT(COPYSTR(lEmployeeRec."Full Name",1,50))
            ELSE
              EXIT('');
          END;
        1:BEGIN
            IF lVendorRec.GET(TempPlanLines."Action By") THEN
              EXIT(lVendorRec.Name)
            ELSE
              EXIT('');
          END;
      END;
      EXIT('');
    END;

    BEGIN
    END.
  }
}

