OBJECT Page 1014 Job Transfer Job Planning Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW111.00,4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Job Transfer Job Planning Line;
               NOR=Prosjekt - overf›r til prosjektplanleggingslinje;
               SVE=Projekt - ™verf”r projektplaneringsrad];
    SaveValues=Yes;
    SourceTable=Table11072007;
    PageType=StandardDialog;
    OnOpenPage=BEGIN
                 InitializeValues;
               END;

  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                CaptionML=[ENU=General;
                           NOR=Generelt;
                           SVE=Allm„nt] }

    { 3   ;2   ;Field     ;
                CaptionML=[ENU=Posting Date;
                           NOR=Bokf›ringsdato;
                           SVE=Bokf”ringsdatum];
                ToolTipML=[ENU=Specifies the posting date for the document.;
                           NOR=Angir dokumentets bokf›ringsdato.;
                           SVE=Anger dokumentets bokf”ringsdatum.];
                ApplicationArea=#Jobs;
                SourceExpr=PostingDate }

    { 4   ;2   ;Field     ;
                Lookup=Yes;
                CaptionML=[ENU=Job Journal Template;
                           NOR=Prosjektkladdemal;
                           SVE=Projektjournalmall];
                ToolTipML=[ENU=Specifies the journal template that is used for the job journal.;
                           NOR=Angir kladdemalen som brukes for prosjektkladden.;
                           SVE="Anger journalmallen som anv„nds f”r projektjournalen. "];
                ApplicationArea=#Jobs;
                SourceExpr=JobJournalTemplateName;
                TableRelation="Job Journal Template".Name WHERE (Page ID=CONST(201),
                                                                 Recurring=CONST(No));
                OnLookup=BEGIN
                           SelectJobJournalTemplate;
                         END;
                          }

    { 5   ;2   ;Field     ;
                Lookup=Yes;
                CaptionML=[ENU=Job Journal Batch;
                           NOR=Prosjektkladd;
                           SVE=Projektjournal];
                ToolTipML=[ENU=Specifies the journal batch that is used for the job journal.;
                           NOR=Angir kladden som brukes for prosjektkladden.;
                           SVE="Anger journalen som anv„nds f”r projektjournalen. "];
                ApplicationArea=#Jobs;
                SourceExpr=JobJournalBatchName;
                TableRelation="Job Journal Batch".Name WHERE (Journal Template Name=FIELD(Name));
                OnLookup=BEGIN
                           SelectJobJournalBatch;
                         END;
                          }

  }
  CODE
  {
    VAR
      JobJournalTemplateName@1002 : Code[10];
      JobJournalBatchName@1003 : Code[10];
      PostingDate@1000 : Date;

    [External]
    PROCEDURE InitializeValues@14();
    VAR
      JobJnlTemplate@1000 : Record 11072007;
      JobJnlBatch@1001 : Record 11072011;
    BEGIN
      PostingDate := WORKDATE;

      JobJnlTemplate.SETRANGE("Page ID",PAGE::"Job Journal");
      JobJnlTemplate.SETRANGE(Recurring,FALSE);

      IF JobJnlTemplate.COUNT = 1 THEN BEGIN
        JobJnlTemplate.FINDFIRST;
        JobJournalTemplateName := JobJnlTemplate.Name;

        JobJnlBatch.SETRANGE("Journal Template Name",JobJournalTemplateName);

        IF JobJnlBatch.COUNT = 1 THEN BEGIN
          JobJnlBatch.FINDFIRST;
          JobJournalBatchName := JobJnlBatch.Name;
        END;
      END;
    END;

    LOCAL PROCEDURE SelectJobJournalTemplate@3();
    VAR
      JobJnlTemplate@1001 : Record 11072007;
      JobJnlBatch@1000 : Record 11072011;
    BEGIN
      JobJnlTemplate.SETRANGE("Page ID",PAGE::"Job Journal");
      JobJnlTemplate.SETRANGE(Recurring,FALSE);

      IF PAGE.RUNMODAL(0,JobJnlTemplate) = ACTION::LookupOK THEN BEGIN
        JobJournalTemplateName := JobJnlTemplate.Name;

        JobJnlBatch.SETRANGE("Journal Template Name",JobJournalTemplateName);

        IF JobJnlBatch.COUNT = 1 THEN BEGIN
          JobJnlBatch.FINDFIRST;
          JobJournalBatchName := JobJnlBatch.Name;
        END ELSE
          JobJournalBatchName := '';
      END;
    END;

    LOCAL PROCEDURE SelectJobJournalBatch@5();
    VAR
      JobJnlBatch@1000 : Record 11072011;
    BEGIN
      JobJnlBatch.SETRANGE("Journal Template Name",JobJournalTemplateName);

      IF PAGE.RUNMODAL(0,JobJnlBatch) = ACTION::LookupOK THEN
        JobJournalBatchName := JobJnlBatch.Name;
    END;

    [External]
    PROCEDURE GetPostingDate@2() : Date;
    BEGIN
      EXIT(PostingDate);
    END;

    [External]
    PROCEDURE GetJobJournalTemplateName@1() : Code[10];
    BEGIN
      EXIT(JobJournalTemplateName);
    END;

    [External]
    PROCEDURE GetJobJournalBatchName@4() : Code[10];
    BEGIN
      EXIT(JobJournalBatchName);
    END;

    BEGIN
    {
      4PS, Objects renumbered
    }
    END.
  }
}

