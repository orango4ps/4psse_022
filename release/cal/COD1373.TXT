OBJECT Codeunit 1373 Batch Posting Print Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [EventSubscriber(Codeunit,1380,OnAfterBatchProcessing)]
    LOCAL PROCEDURE PrintDocumentOnAfterBatchPosting@3(VAR RecRef@1000 : RecordRef;PostingResult@1001 : Boolean);
    VAR
      BatchPostParameterTypes@1002 : Codeunit 1370;
      BatchProcessingMgt@1004 : Codeunit 1380;
      Print@1003 : Boolean;
    BEGIN
      IF NOT PostingResult THEN
        EXIT;

      IF NOT BatchProcessingMgt.GetParameterBoolean(RecRef.RECORDID,BatchPostParameterTypes.Print,Print) OR NOT Print THEN
        EXIT;

      PrintSalesDocument(RecRef);
      PrintPurchaseDocument(RecRef);
    END;

    [External]
    PROCEDURE PrintSalesDocument@5(RecRef@1000 : RecordRef);
    VAR
      SalesHeader@1001 : Record 36;
      ReportSelections@1002 : Record 77;
      SalesInvoiceHeader@1003 : Record 112;
      SalesShipmentHeader@1004 : Record 110;
      SalesCrMemoHeader@1005 : Record 114;
    BEGIN
      IF RecRef.NUMBER <> DATABASE::"Sales Header" THEN
        EXIT;

      RecRef.SETTABLE(SalesHeader);

      WITH SalesHeader DO
        CASE "Document Type" OF
          "Document Type"::Order:
            BEGIN
              IF Ship THEN BEGIN
                SalesShipmentHeader."No." := "Last Shipping No.";
                SalesShipmentHeader.SETRECFILTER;
                PrintDocument(ReportSelections.Usage::"S.Shipment",SalesShipmentHeader);
              END;
              IF Invoice THEN BEGIN
                SalesInvoiceHeader."No." := "Last Posting No.";
                SalesInvoiceHeader.SETRECFILTER;
                PrintDocument(ReportSelections.Usage::"S.Invoice",SalesInvoiceHeader);
              END;
            END;
          "Document Type"::Invoice:
            BEGIN
              IF "Last Posting No." = '' THEN
                SalesInvoiceHeader."No." := "No."
              ELSE
                SalesInvoiceHeader."No." := "Last Posting No.";
              SalesInvoiceHeader.SETRECFILTER;
              PrintDocument(ReportSelections.Usage::"S.Invoice",SalesInvoiceHeader);
            END;
          "Document Type"::"Credit Memo":
            BEGIN
              IF "Last Posting No." = '' THEN
                SalesCrMemoHeader."No." := "No."
              ELSE
                SalesCrMemoHeader."No." := "Last Posting No.";
              SalesCrMemoHeader.SETRECFILTER;
              PrintDocument(ReportSelections.Usage::"S.Cr.Memo",SalesCrMemoHeader);
            END;
        END;

      OnAfterPrintSalesDocument(RecRef);
    END;

    [External]
    PROCEDURE PrintPurchaseDocument@6(RecRef@1000 : RecordRef);
    VAR
      PurchaseHeader@1001 : Record 38;
      ReportSelections@1002 : Record 77;
      PurchRcptHeader@1003 : Record 120;
      PurchInvHeader@1004 : Record 122;
      PurchCrMemoHdr@1005 : Record 124;
    BEGIN
      IF RecRef.NUMBER <> DATABASE::"Purchase Header" THEN
        EXIT;

      RecRef.SETTABLE(PurchaseHeader);

      WITH PurchaseHeader DO
        CASE "Document Type" OF
          "Document Type"::Order:
            BEGIN
              IF Receive THEN BEGIN
                PurchRcptHeader."No." := "Last Receiving No.";
                PurchRcptHeader.SETRECFILTER;
                PrintDocument(ReportSelections.Usage::"P.Receipt",PurchRcptHeader);
              END;
              IF Invoice THEN BEGIN
                PurchInvHeader."No." := "Last Posting No.";
                PurchInvHeader.SETRECFILTER;
                PrintDocument(ReportSelections.Usage::"P.Invoice",PurchInvHeader);
              END;
            END;
          "Document Type"::Invoice:
            BEGIN
              IF "Last Posting No." = '' THEN
                PurchInvHeader."No." := "No."
              ELSE
                PurchInvHeader."No." := "Last Posting No.";
              PurchInvHeader.SETRECFILTER;
              PrintDocument(ReportSelections.Usage::"P.Invoice",PurchInvHeader);
            END;
          "Document Type"::"Credit Memo":
            BEGIN
              IF "Last Posting No." = '' THEN
                PurchCrMemoHdr."No." := "No."
              ELSE
                PurchCrMemoHdr."No." := "Last Posting No.";
              PurchCrMemoHdr.SETRECFILTER;
              PrintDocument(ReportSelections.Usage::"P.Cr.Memo",PurchCrMemoHdr);
            END;
        END;

      OnAfterPrintPurchaseDocument(RecRef);
    END;

    LOCAL PROCEDURE PrintDocument@7(ReportUsage@1000 : Option;RecVar@1001 : Variant);
    VAR
      ReportSelections@1002 : Record 77;
      IsHandled@1003 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforePrintDocument(ReportUsage,RecVar,IsHandled);
      IF IsHandled THEN
        EXIT;

      ReportSelections.RESET;
      ReportSelections.SETRANGE(Usage,ReportUsage);
      ReportSelections.FINDSET;
      REPEAT
        ReportSelections.TESTFIELD("Report ID");
        REPORT.RUN(ReportSelections."Report ID",FALSE,FALSE,RecVar);
      UNTIL ReportSelections.NEXT = 0;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPrintSalesDocument@2(RecRef@1000 : RecordRef);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPrintPurchaseDocument@4(RecRef@1000 : RecordRef);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePrintDocument@1(ReportUsage@1001 : Option;RecVar@1000 : Variant;VAR IsHandled@1002 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

