OBJECT Page 5475 Sales Invoice Entity
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[@@@={Locked};
               ENU=salesInvoices;
               NOR=salesInvoices;
               SVE=salesInvoices];
    SourceTable=Table5475;
    DelayedInsert=Yes;
    PageType=API;
    ChangeTrackingAllowed=Yes;
    EntitySetName=salesInvoices;
    EntityName=salesInvoice;
    OnOpenPage=BEGIN
                 SetPermissionFilters;
               END;

    OnAfterGetRecord=VAR
                       SalesInvoiceAggregator@1001 : Codeunit 5477;
                     BEGIN
                       SetCalculatedFields;
                       IF NOT Posted THEN
                         IF HasWritePermissionForDraft THEN
                           SalesInvoiceAggregator.RedistributeInvoiceDiscounts(Rec);
                     END;

    OnNewRecord=BEGIN
                  ClearCalculatedFields;
                END;

    OnInsertRecord=VAR
                     SalesInvoiceAggregator@1002 : Codeunit 5477;
                   BEGIN
                     CheckCustomerSpecified;
                     ProcessBillingPostalAddress;

                     SalesInvoiceAggregator.PropagateOnInsert(Rec,TempFieldBuffer);
                     SetDates;

                     UpdateDiscount;

                     SetCalculatedFields;

                     EXIT(FALSE);
                   END;

    OnModifyRecord=VAR
                     SalesInvoiceAggregator@1002 : Codeunit 5477;
                   BEGIN
                     IF xRec.Id <> Id THEN
                       ERROR(CannotChangeIDErr);

                     ProcessBillingPostalAddress;

                     SalesInvoiceAggregator.PropagateOnModify(Rec,TempFieldBuffer);
                     UpdateDiscount;

                     SetCalculatedFields;

                     EXIT(FALSE);
                   END;

    OnDeleteRecord=VAR
                     SalesInvoiceAggregator@1000 : Codeunit 5477;
                   BEGIN
                     SalesInvoiceAggregator.PropagateOnDelete(Rec);

                     EXIT(FALSE);
                   END;

    ODataKeyFields=Id;
  }
  CONTROLS
  {
    { 21  ;0   ;Container ;
                ContainerType=ContentArea }

    { 20  ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 19  ;2   ;Field     ;
                Name=id;
                CaptionML=[@@@={Locked};
                           ENU=id;
                           NOR=id;
                           SVE=id];
                ApplicationArea=#All;
                SourceExpr=Id;
                Editable=FALSE;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO(Id));
                           END;
                            }

    { 18  ;2   ;Field     ;
                Name=number;
                CaptionML=[@@@={Locked};
                           ENU=Number;
                           NOR=Number;
                           SVE=Number];
                ApplicationArea=#All;
                SourceExpr="No.";
                Editable=FALSE;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("No."));
                           END;
                            }

    { 23  ;2   ;Field     ;
                Name=externalDocumentNumber;
                CaptionML=[@@@={Locked};
                           ENU=externalDocumentNumber;
                           NOR=externalDocumentNumber;
                           SVE=externalDocumentNumber];
                ApplicationArea=#All;
                SourceExpr="External Document No.";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("External Document No."));
                           END;
                            }

    { 17  ;2   ;Field     ;
                Name=invoiceDate;
                CaptionML=[@@@={Locked};
                           ENU=invoiceDate;
                           NOR=invoiceDate;
                           SVE=invoiceDate];
                ApplicationArea=#All;
                SourceExpr="Document Date";
                OnValidate=BEGIN
                             DocumentDateVar := "Document Date";
                             DocumentDateSet := TRUE;

                             RegisterFieldSet(FIELDNO("Document Date"));
                           END;
                            }

    { 31  ;2   ;Field     ;
                Name=dueDate;
                CaptionML=[@@@={Locked};
                           ENU=dueDate;
                           NOR=dueDate;
                           SVE=dueDate];
                ApplicationArea=#All;
                SourceExpr="Due Date";
                OnValidate=BEGIN
                             DueDateVar := "Due Date";
                             DueDateSet := TRUE;

                             RegisterFieldSet(FIELDNO("Due Date"));
                           END;
                            }

    { 16  ;2   ;Field     ;
                Name=customerPurchaseOrderReference;
                CaptionML=[@@@={Locked};
                           ENU=customerPurchaseOrderReference;
                           NOR=customerPurchaseOrderReference;
                           SVE=customerPurchaseOrderReference];
                ApplicationArea=#All;
                SourceExpr="Your Reference";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Your Reference"));
                           END;
                            }

    { 14  ;2   ;Field     ;
                Name=customerId;
                CaptionML=[@@@={Locked};
                           ENU=customerId;
                           NOR=customerId;
                           SVE=customerId];
                ApplicationArea=#All;
                SourceExpr="Customer Id";
                OnValidate=VAR
                             O365SalesInvoiceMgmt@1000 : Codeunit 2310;
                           BEGIN
                             Customer.SETRANGE(Id,"Customer Id");
                             IF NOT Customer.FINDFIRST THEN
                               ERROR(CouldNotFindCustomerErr);

                             O365SalesInvoiceMgmt.EnforceCustomerTemplateIntegrity(Customer);

                             "Sell-to Customer No." := Customer."No.";
                             RegisterFieldSet(FIELDNO("Customer Id"));
                             RegisterFieldSet(FIELDNO("Sell-to Customer No."));
                           END;
                            }

    { 25  ;2   ;Field     ;
                Name=contactId;
                CaptionML=[@@@={Locked};
                           ENU=contactId;
                           NOR=contactId;
                           SVE=contactId];
                ApplicationArea=#All;
                SourceExpr="Contact Graph Id";
                OnValidate=VAR
                             Contact@1000 : Record 5050;
                             Customer@1001 : Record 18;
                             GraphIntContact@1003 : Codeunit 5461;
                           BEGIN
                             RegisterFieldSet(FIELDNO("Contact Graph Id"));

                             IF "Contact Graph Id" = '' THEN
                               ERROR(ContactIdHasToHaveValueErr);

                             IF NOT GraphIntContact.FindOrCreateCustomerFromGraphContactSafe("Contact Graph Id",Customer,Contact) THEN
                               EXIT;

                             UpdateCustomerFromGraphContactId(Customer);

                             IF Contact."Company No." = Customer."No." THEN BEGIN
                               VALIDATE("Sell-to Contact No.",Contact."No.");
                               VALIDATE("Sell-to Contact",Contact.Name);

                               RegisterFieldSet(FIELDNO("Sell-to Contact No."));
                               RegisterFieldSet(FIELDNO("Sell-to Contact"));
                             END;
                           END;
                            }

    { 12  ;2   ;Field     ;
                Name=customerNumber;
                CaptionML=[@@@={Locked};
                           ENU=customerNumber;
                           NOR=customerNumber;
                           SVE=customerNumber];
                ApplicationArea=#All;
                SourceExpr="Sell-to Customer No.";
                OnValidate=VAR
                             O365SalesInvoiceMgmt@1000 : Codeunit 2310;
                           BEGIN
                             IF Customer."No." <> '' THEN BEGIN
                               IF Customer."No." <> "Sell-to Customer No." THEN
                                 ERROR(CustomerValuesDontMatchErr);
                               EXIT;
                             END;

                             IF NOT Customer.GET("Sell-to Customer No.") THEN
                               ERROR(CouldNotFindCustomerErr);

                             O365SalesInvoiceMgmt.EnforceCustomerTemplateIntegrity(Customer);

                             "Customer Id" := Customer.Id;
                             RegisterFieldSet(FIELDNO("Customer Id"));
                             RegisterFieldSet(FIELDNO("Sell-to Customer No."));
                           END;
                            }

    { 15  ;2   ;Field     ;
                Name=customerName;
                CaptionML=[@@@={Locked};
                           ENU=customerName;
                           NOR=customerName;
                           SVE=customerName];
                ApplicationArea=#All;
                SourceExpr="Sell-to Customer Name";
                Editable=FALSE }

    { 6   ;2   ;Field     ;
                Name=billingPostalAddress;
                CaptionML=[@@@={Locked};
                           ENU=billingPostalAddress;
                           NOR=billingPostalAddress;
                           SVE=billingPostalAddress];
                ToolTipML=[ENU=Specifies the billing address of the Sales Invoice.;
                           NOR=Angir fakturaadressen for salgsfakturaen.;
                           SVE=Anger faktureringsadressen i f”rs„ljningsfakturan.];
                ApplicationArea=#All;
                SourceExpr=BillingPostalAddressJSONText;
                OnValidate=BEGIN
                             BillingPostalAddressSet := TRUE;
                           END;

                ODataEDMType=POSTALADDRESS }

    { 27  ;2   ;Field     ;
                Name=currencyId;
                CaptionML=[@@@={Locked};
                           ENU=CurrencyId;
                           NOR=CurrencyId;
                           SVE=CurrencyId];
                ApplicationArea=#All;
                SourceExpr="Currency Id";
                OnValidate=BEGIN
                             IF "Currency Id" = BlankGUID THEN
                               "Currency Code" := ''
                             ELSE BEGIN
                               Currency.SETRANGE(Id,"Currency Id");
                               IF NOT Currency.FINDFIRST THEN
                                 ERROR(CurrencyIdDoesNotMatchACurrencyErr);

                               "Currency Code" := Currency.Code;
                             END;

                             RegisterFieldSet(FIELDNO("Currency Id"));
                             RegisterFieldSet(FIELDNO("Currency Code"));
                           END;
                            }

    { 22  ;2   ;Field     ;
                Name=currencyCode;
                CaptionML=[@@@={Locked};
                           ENU=currencyCode;
                           NOR=currencyCode;
                           SVE=currencyCode];
                ApplicationArea=#All;
                SourceExpr=CurrencyCodeTxt;
                OnValidate=BEGIN
                             "Currency Code" :=
                               GraphMgtGeneralTools.TranslateCurrencyCodeToNAVCurrencyCode(
                                 LCYCurrencyCode,COPYSTR(CurrencyCodeTxt,1,MAXSTRLEN(LCYCurrencyCode)));

                             IF Currency.Code <> '' THEN BEGIN
                               IF Currency.Code <> "Currency Code" THEN
                                 ERROR(CurrencyValuesDontMatchErr);
                               EXIT;
                             END;

                             IF "Currency Code" = '' THEN
                               "Currency Id" := BlankGUID
                             ELSE BEGIN
                               IF NOT Currency.GET("Currency Code") THEN
                                 ERROR(CurrencyCodeDoesNotMatchACurrencyErr);

                               "Currency Id" := Currency.Id;
                             END;

                             RegisterFieldSet(FIELDNO("Currency Id"));
                             RegisterFieldSet(FIELDNO("Currency Code"));
                           END;
                            }

    { 26  ;2   ;Field     ;
                Name=orderId;
                CaptionML=[@@@={Locked};
                           ENU=orderId;
                           NOR=orderId;
                           SVE=orderId];
                ApplicationArea=#All;
                SourceExpr="Order Id";
                Editable=False }

    { 24  ;2   ;Field     ;
                Name=orderNumber;
                CaptionML=[@@@={Locked};
                           ENU=orderNumber;
                           NOR=orderNumber;
                           SVE=orderNumber];
                ApplicationArea=#All;
                SourceExpr="Order No.";
                Editable=False }

    { 30  ;2   ;Field     ;
                Name=paymentTermsId;
                CaptionML=[@@@={Locked};
                           ENU=PaymentTermsId;
                           NOR=PaymentTermsId;
                           SVE=PaymentTermsId];
                ApplicationArea=#All;
                SourceExpr="Payment Terms Id";
                OnValidate=BEGIN
                             IF "Payment Terms Id" = BlankGUID THEN
                               "Payment Terms Code" := ''
                             ELSE BEGIN
                               PaymentTerms.SETRANGE(Id,"Payment Terms Id");
                               IF NOT PaymentTerms.FINDFIRST THEN
                                 ERROR(PaymentTermsIdDoesNotMatchAPaymentTermsErr);

                               "Payment Terms Code" := PaymentTerms.Code;
                             END;

                             RegisterFieldSet(FIELDNO("Payment Terms Id"));
                             RegisterFieldSet(FIELDNO("Payment Terms Code"));
                           END;
                            }

    { 5   ;2   ;Field     ;
                Name=shipmentMethodId;
                CaptionML=[@@@={Locked};
                           ENU=ShipmentMethodId;
                           NOR=ShipmentMethodId;
                           SVE=ShipmentMethodId];
                ApplicationArea=#All;
                SourceExpr="Shipment Method Id";
                OnValidate=BEGIN
                             IF "Shipment Method Id" = BlankGUID THEN
                               "Shipment Method Code" := ''
                             ELSE BEGIN
                               ShipmentMethod.SETRANGE(Id,"Shipment Method Id");
                               IF NOT ShipmentMethod.FINDFIRST THEN
                                 ERROR(ShipmentMethodIdDoesNotMatchAShipmentMethodErr);

                               "Shipment Method Code" := ShipmentMethod.Code;
                             END;

                             RegisterFieldSet(FIELDNO("Shipment Method Id"));
                             RegisterFieldSet(FIELDNO("Shipment Method Code"));
                           END;
                            }

    { 3   ;2   ;Field     ;
                Name=salesperson;
                CaptionML=[@@@={Locked};
                           ENU=salesperson;
                           NOR=salesperson;
                           SVE=salesperson];
                ApplicationArea=#All;
                SourceExpr="Salesperson Code";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Salesperson Code"));
                           END;
                            }

    { 28  ;2   ;Field     ;
                Name=pricesIncludeTax;
                CaptionML=[@@@={Locked};
                           ENU=pricesIncludeTax;
                           NOR=pricesIncludeTax;
                           SVE=pricesIncludeTax];
                ApplicationArea=#All;
                SourceExpr="Prices Including VAT";
                Editable=FALSE;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Prices Including VAT"));
                           END;
                            }

    { 1   ;2   ;Part      ;
                Name=salesInvoiceLines;
                CaptionML=[@@@={Locked};
                           ENU=Lines;
                           NOR=Lines;
                           SVE=Lines];
                ApplicationArea=#All;
                SubPageLink=Document Id=FIELD(Id);
                PagePartID=Page5476;
                EntitySetName=salesInvoiceLines;
                EntityName=salesInvoiceLine;
                PartType=Page }

    { 4   ;2   ;Part      ;
                Name=pdfDocument;
                CaptionML=[@@@={Locked};
                           ENU=PDF Document;
                           NOR=PDF Document;
                           SVE=PDF Document];
                ApplicationArea=#All;
                SubPageLink=Document Id=FIELD(Id);
                PagePartID=Page5529;
                EntitySetName=pdfDocument;
                EntityName=pdfDocument;
                PartType=Page }

    { 11  ;2   ;Field     ;
                Name=discountAmount;
                CaptionML=[@@@={Locked};
                           ENU=discountAmount;
                           NOR=discountAmount;
                           SVE=discountAmount];
                ApplicationArea=#All;
                SourceExpr="Invoice Discount Amount";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Invoice Discount Amount"));
                             InvoiceDiscountAmount := "Invoice Discount Amount";
                             DiscountAmountSet := TRUE;
                           END;
                            }

    { 10  ;2   ;Field     ;
                Name=discountAppliedBeforeTax;
                CaptionML=[@@@={Locked};
                           ENU=discountAppliedBeforeTax;
                           NOR=discountAppliedBeforeTax;
                           SVE=discountAppliedBeforeTax];
                ApplicationArea=#All;
                SourceExpr="Discount Applied Before Tax";
                Editable=FALSE }

    { 9   ;2   ;Field     ;
                Name=totalAmountExcludingTax;
                CaptionML=[@@@={Locked};
                           ENU=totalAmountExcludingTax;
                           NOR=totalAmountExcludingTax;
                           SVE=totalAmountExcludingTax];
                ApplicationArea=#All;
                SourceExpr=Amount;
                Editable=FALSE }

    { 8   ;2   ;Field     ;
                Name=totalTaxAmount;
                CaptionML=[@@@={Locked};
                           ENU=totalTaxAmount;
                           NOR=totalTaxAmount;
                           SVE=totalTaxAmount];
                ToolTipML=[ENU=Specifies the total tax amount for the sales invoice.;
                           NOR=Angir det totale mva-bel›pet for salgsfakturaen.;
                           SVE=Anger f”rs„ljningsfakturans totala momsbelopp.];
                ApplicationArea=#All;
                SourceExpr="Total Tax Amount";
                Editable=FALSE;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Total Tax Amount"));
                           END;
                            }

    { 7   ;2   ;Field     ;
                Name=totalAmountIncludingTax;
                CaptionML=[@@@={Locked};
                           ENU=totalAmountIncludingTax;
                           NOR=totalAmountIncludingTax;
                           SVE=totalAmountIncludingTax];
                ApplicationArea=#All;
                SourceExpr="Amount Including VAT";
                Editable=FALSE;
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Amount Including VAT"));
                           END;
                            }

    { 13  ;2   ;Field     ;
                Name=status;
                CaptionML=[@@@={Locked};
                           ENU=status;
                           NOR=status;
                           SVE=status];
                ToolTipML=[ENU=Specifies the status of the Sales Invoice (cancelled, paid, on hold, created).;
                           NOR=Angir statusen til salgsfakturaen (kansellert, betalt, sperret, opprettet).;
                           SVE=Anger f”rs„ljningsfakturans status (annullerad, betald, sp„rrad, skapad).];
                ApplicationArea=#All;
                SourceExpr=Status;
                Editable=FALSE }

    { 2   ;2   ;Field     ;
                Name=lastModifiedDateTime;
                CaptionML=[@@@={Locked};
                           ENU=lastModifiedDateTime;
                           NOR=lastModifiedDateTime;
                           SVE=lastModifiedDateTime];
                ApplicationArea=#All;
                SourceExpr="Last Modified Date Time";
                Editable=FALSE }

    { 32  ;2   ;Field     ;
                Name=phoneNumber;
                CaptionML=[@@@={Locked};
                           ENU=PhoneNumber;
                           NOR=PhoneNumber;
                           SVE=PhoneNumber];
                ApplicationArea=#All;
                SourceExpr="Sell-to Phone No.";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Sell-to Phone No."));
                           END;
                            }

    { 29  ;2   ;Field     ;
                Name=email;
                CaptionML=[@@@={Locked};
                           ENU=Email;
                           NOR=Email;
                           SVE=Email];
                ApplicationArea=#All;
                SourceExpr="Sell-to E-Mail";
                OnValidate=BEGIN
                             RegisterFieldSet(FIELDNO("Sell-to E-Mail"));
                           END;
                            }

  }
  CODE
  {
    VAR
      CannotChangeIDErr@1004 : TextConst '@@@={Locked};ENU=The id cannot be changed.;NOR=The id cannot be changed.;SVE=The id cannot be changed.';
      TempFieldBuffer@1001 : TEMPORARY Record 8450;
      Customer@1003 : Record 18;
      Currency@1014 : Record 4;
      PaymentTerms@1016 : Record 3;
      ShipmentMethod@1017 : Record 10;
      GraphMgtGeneralTools@1009 : Codeunit 5465;
      LCYCurrencyCode@1008 : Code[10];
      CurrencyCodeTxt@1006 : Text;
      BillingPostalAddressJSONText@1002 : Text;
      BillingPostalAddressSet@1000 : Boolean;
      CustomerNotProvidedErr@1010 : TextConst '@@@={Locked};ENU=A customerNumber or a customerId must be provided.;NOR=A customerNumber or a customerId must be provided.;SVE=A customerNumber or a customerId must be provided.';
      CustomerValuesDontMatchErr@1024 : TextConst '@@@={Locked};ENU=The customer values do not match to a specific Customer.;NOR=The customer values do not match to a specific Customer.;SVE=The customer values do not match to a specific Customer.';
      CouldNotFindCustomerErr@1005 : TextConst '@@@={Locked};ENU=The customer cannot be found.;NOR=The customer cannot be found.;SVE=The customer cannot be found.';
      ContactIdHasToHaveValueErr@1007 : TextConst '@@@={Locked};ENU=Contact Id must have a value set.;NOR=Contact Id must have a value set.;SVE=Contact Id must have a value set.';
      CurrencyValuesDontMatchErr@1013 : TextConst '@@@={Locked};ENU=The currency values do not match to a specific Currency.;NOR=The currency values do not match to a specific Currency.;SVE=The currency values do not match to a specific Currency.';
      CurrencyIdDoesNotMatchACurrencyErr@1012 : TextConst '@@@={Locked};ENU=The "currencyId" does not match to a Currency.;NOR=The "currencyId" does not match to a Currency.;SVE=The "currencyId" does not match to a Currency.';
      CurrencyCodeDoesNotMatchACurrencyErr@1011 : TextConst '@@@={Locked};ENU=The "currencyCode" does not match to a Currency.;NOR=The "currencyCode" does not match to a Currency.;SVE=The "currencyCode" does not match to a Currency.';
      BlankGUID@1015 : GUID;
      PaymentTermsIdDoesNotMatchAPaymentTermsErr@1022 : TextConst '@@@={Locked};ENU=The "paymentTermsId" does not match to a Payment Terms.;NOR=The "paymentTermsId" does not match to a Payment Terms.;SVE=The "paymentTermsId" does not match to a Payment Terms.';
      ShipmentMethodIdDoesNotMatchAShipmentMethodErr@1019 : TextConst '@@@={Locked};ENU=The "shipmentMethodId" does not match to a Shipment Method.;NOR=The "shipmentMethodId" does not match to a Shipment Method.;SVE=The "shipmentMethodId" does not match to a Shipment Method.';
      DiscountAmountSet@1025 : Boolean;
      InvoiceDiscountAmount@1026 : Decimal;
      DocumentDateSet@1030 : Boolean;
      DocumentDateVar@1029 : Date;
      DueDateSet@1028 : Boolean;
      DueDateVar@1027 : Date;
      PostedInvoiceActionErr@1033 : TextConst '@@@={Locked};ENU=The action can be applied to a posted invoice only.;NOR=The action can be applied to a posted invoice only.;SVE=The action can be applied to a posted invoice only.';
      DraftInvoiceActionErr@1031 : TextConst '@@@={Locked};ENU=The action can be applied to a draft invoice only.;NOR=The action can be applied to a draft invoice only.;SVE=The action can be applied to a draft invoice only.';
      CannotFindInvoiceErr@1032 : TextConst '@@@={Locked};ENU=The invoice cannot be found.;NOR=The invoice cannot be found.;SVE=The invoice cannot be found.';
      CancelingInvoiceFailedCreditMemoCreatedAndPostedErr@1039 : TextConst '@@@={Locked};ENU=Canceling the invoice failed because of the following error: \\%1\\A credit memo is posted.;NOR=Canceling the invoice failed because of the following error: \\%1\\A credit memo is posted.;SVE=Canceling the invoice failed because of the following error: \\%1\\A credit memo is posted.';
      CancelingInvoiceFailedCreditMemoCreatedButNotPostedErr@1038 : TextConst '@@@={Locked};ENU=Canceling the invoice failed because of the following error: \\%1\\A credit memo is created but not posted.;NOR=Canceling the invoice failed because of the following error: \\%1\\A credit memo is created but not posted.;SVE=Canceling the invoice failed because of the following error: \\%1\\A credit memo is created but not posted.';
      CancelingInvoiceFailedNothingCreatedErr@1037 : TextConst '@@@={Locked};ENU=Canceling the invoice failed because of the following error: \\%1.;NOR=Canceling the invoice failed because of the following error: \\%1.;SVE=Canceling the invoice failed because of the following error: \\%1.';
      EmptyEmailErr@1035 : TextConst '@@@={Locked};ENU=The send-to email is empty. Specify email either for the customer or for the invoice in email preview.;NOR=The send-to email is empty. Specify email either for the customer or for the invoice in email preview.;SVE=The send-to email is empty. Specify email either for the customer or for the invoice in email preview.';
      AlreadyCanceledErr@1036 : TextConst '@@@={Locked};ENU=The invoice cannot be canceled because it has already been canceled.;NOR=The invoice cannot be canceled because it has already been canceled.;SVE=The invoice cannot be canceled because it has already been canceled.';
      MailNotConfiguredErr@1034 : TextConst '@@@={Locked};ENU=An email account must be configured to send emails.;NOR=An email account must be configured to send emails.;SVE=An email account must be configured to send emails.';
      HasWritePermissionForDraft@1018 : Boolean;

    LOCAL PROCEDURE SetCalculatedFields@6();
    VAR
      GraphMgtSalesInvoice@1000 : Codeunit 5475;
    BEGIN
      BillingPostalAddressJSONText := GraphMgtSalesInvoice.BillToCustomerAddressToJSON(Rec);
      CurrencyCodeTxt := GraphMgtGeneralTools.TranslateNAVCurrencyCodeToCurrencyCode(LCYCurrencyCode,"Currency Code");
    END;

    LOCAL PROCEDURE ClearCalculatedFields@10();
    BEGIN
      CLEAR(BillingPostalAddressJSONText);
      CLEAR(InvoiceDiscountAmount);
      CLEAR(DiscountAmountSet);
      TempFieldBuffer.DELETEALL;
    END;

    LOCAL PROCEDURE RegisterFieldSet@11(FieldNo@1000 : Integer);
    VAR
      LastOrderNo@1001 : Integer;
    BEGIN
      LastOrderNo := 1;
      IF TempFieldBuffer.FINDLAST THEN
        LastOrderNo := TempFieldBuffer.Order + 1;

      CLEAR(TempFieldBuffer);
      TempFieldBuffer.Order := LastOrderNo;
      TempFieldBuffer."Table ID" := DATABASE::"Sales Invoice Entity Aggregate";
      TempFieldBuffer."Field ID" := FieldNo;
      TempFieldBuffer.INSERT;
    END;

    LOCAL PROCEDURE CheckCustomerSpecified@1();
    BEGIN
      IF ("Sell-to Customer No." = '') AND
         ("Customer Id" = BlankGUID)
      THEN
        ERROR(CustomerNotProvidedErr);
    END;

    LOCAL PROCEDURE ProcessBillingPostalAddress@5();
    VAR
      GraphMgtSalesInvoice@1000 : Codeunit 5475;
    BEGIN
      IF NOT BillingPostalAddressSet THEN
        EXIT;

      GraphMgtSalesInvoice.ProcessComplexTypes(Rec,BillingPostalAddressJSONText);

      IF xRec."Sell-to Address" <> "Sell-to Address" THEN
        RegisterFieldSet(FIELDNO("Sell-to Address"));

      IF xRec."Sell-to Address 2" <> "Sell-to Address 2" THEN
        RegisterFieldSet(FIELDNO("Sell-to Address 2"));

      IF xRec."Sell-to City" <> "Sell-to City" THEN
        RegisterFieldSet(FIELDNO("Sell-to City"));

      IF xRec."Sell-to Country/Region Code" <> "Sell-to Country/Region Code" THEN
        RegisterFieldSet(FIELDNO("Sell-to Country/Region Code"));

      IF xRec."Sell-to Post Code" <> "Sell-to Post Code" THEN
        RegisterFieldSet(FIELDNO("Sell-to Post Code"));

      IF xRec."Sell-to County" <> "Sell-to County" THEN
        RegisterFieldSet(FIELDNO("Sell-to County"));
    END;

    LOCAL PROCEDURE UpdateCustomerFromGraphContactId@2(VAR Customer@1001 : Record 18);
    VAR
      O365SalesInvoiceMgmt@1002 : Codeunit 2310;
      UpdateCustomer@1000 : Boolean;
    BEGIN
      UpdateCustomer := "Sell-to Customer No." = '';
      IF NOT UpdateCustomer THEN BEGIN
        TempFieldBuffer.RESET;
        TempFieldBuffer.SETRANGE("Field ID",FIELDNO("Customer Id"));
        UpdateCustomer := NOT TempFieldBuffer.FINDFIRST;
        TempFieldBuffer.RESET;
      END;

      IF UpdateCustomer THEN BEGIN
        VALIDATE("Customer Id",Customer.Id);
        VALIDATE("Sell-to Customer No.",Customer."No.");
        RegisterFieldSet(FIELDNO("Customer Id"));
        RegisterFieldSet(FIELDNO("Sell-to Customer No."));
      END;

      O365SalesInvoiceMgmt.EnforceCustomerTemplateIntegrity(Customer);
    END;

    LOCAL PROCEDURE SetPermissionFilters@4();
    VAR
      SalesHeader@1001 : Record 36;
      SalesInvoiceHeader@1000 : Record 112;
      FilterText@1002 : Text;
    BEGIN
      // Filtering out test documents
      SalesHeader.SETRANGE(IsTest,FALSE);

      SalesHeader.SETRANGE("Document Type",SalesHeader."Document Type"::Invoice);
      IF NOT SalesHeader.READPERMISSION THEN
        FilterText :=
          STRSUBSTNO('<>%1&<>%2',Status::Draft,Status::"In Review");

      IF NOT SalesInvoiceHeader.READPERMISSION THEN BEGIN
        IF FilterText <> '' THEN
          FilterText += '&';
        FilterText +=
          STRSUBSTNO(
            '<>%1&<>%2&<>%3&<>%4',Status::Canceled,Status::Corrective,
            Status::Open,Status::Paid);
      END;

      IF FilterText <> '' THEN BEGIN
        FILTERGROUP(2);
        SETFILTER(Status,FilterText);
        FILTERGROUP(0);
      END;

      HasWritePermissionForDraft := SalesHeader.WRITEPERMISSION;
    END;

    LOCAL PROCEDURE UpdateDiscount@3();
    VAR
      SalesHeader@1002 : Record 36;
      SalesInvoiceAggregator@1001 : Codeunit 5477;
      SalesCalcDiscountByType@1000 : Codeunit 56;
    BEGIN
      IF Posted THEN
        EXIT;

      IF NOT DiscountAmountSet THEN BEGIN
        SalesInvoiceAggregator.RedistributeInvoiceDiscounts(Rec);
        EXIT;
      END;

      SalesHeader.GET("Document Type"::Invoice,"No.");
      SalesCalcDiscountByType.ApplyInvDiscBasedOnAmt(InvoiceDiscountAmount,SalesHeader);
    END;

    LOCAL PROCEDURE SetDates@8();
    VAR
      SalesInvoiceAggregator@1000 : Codeunit 5477;
    BEGIN
      IF NOT (DueDateSet OR DocumentDateSet) THEN
        EXIT;

      TempFieldBuffer.RESET;
      TempFieldBuffer.DELETEALL;

      IF DocumentDateSet THEN BEGIN
        "Document Date" := DocumentDateVar;
        RegisterFieldSet(FIELDNO("Document Date"));
      END;

      IF DueDateSet THEN BEGIN
        "Due Date" := DueDateVar;
        RegisterFieldSet(FIELDNO("Due Date"));
      END;

      SalesInvoiceAggregator.PropagateOnModify(Rec,TempFieldBuffer);
      FIND;
    END;

    LOCAL PROCEDURE GetPostedInvoice@35(VAR SalesInvoiceHeader@1000 : Record 112);
    BEGIN
      IF NOT Posted THEN
        ERROR(PostedInvoiceActionErr);

      SalesInvoiceHeader.SETRANGE(Id,Id);
      IF NOT SalesInvoiceHeader.FINDFIRST THEN
        ERROR(CannotFindInvoiceErr);
    END;

    LOCAL PROCEDURE GetDraftInvoice@36(VAR SalesHeader@1000 : Record 36);
    BEGIN
      IF Posted THEN
        ERROR(DraftInvoiceActionErr);

      SalesHeader.SETRANGE(Id,Id);
      IF NOT SalesHeader.FINDFIRST THEN
        ERROR(CannotFindInvoiceErr);
    END;

    LOCAL PROCEDURE CheckSmtpMailSetup@30();
    VAR
      O365SetupEmail@1001 : Codeunit 2135;
    BEGIN
      IF NOT O365SetupEmail.SMTPEmailIsSetUp THEN
        ERROR(MailNotConfiguredErr);
    END;

    LOCAL PROCEDURE CheckSendToEmailAddress@21(DocumentNo@1000 : Code[20]);
    BEGIN
      IF GetSendToEmailAddress(DocumentNo) = '' THEN
        ERROR(EmptyEmailErr);
    END;

    LOCAL PROCEDURE GetSendToEmailAddress@26(DocumentNo@1001 : Code[20]) : Text[250];
    VAR
      EmailAddress@1000 : Text[250];
    BEGIN
      EmailAddress := GetDocumentEmailAddress(DocumentNo);
      IF EmailAddress <> '' THEN
        EXIT(EmailAddress);
      EmailAddress := GetCustomerEmailAddress;
      EXIT(EmailAddress);
    END;

    LOCAL PROCEDURE GetCustomerEmailAddress@24() : Text[250];
    BEGIN
      IF NOT Customer.GET("Sell-to Customer No.") THEN
        EXIT('');
      EXIT(Customer."E-Mail");
    END;

    LOCAL PROCEDURE GetDocumentEmailAddress@58(DocumentNo@1000 : Code[20]) : Text[250];
    VAR
      EmailParameter@1002 : Record 9510;
    BEGIN
      IF NOT EmailParameter.GET(DocumentNo,"Document Type",EmailParameter."Parameter Type"::Address) THEN
        EXIT('');
      EXIT(EmailParameter."Parameter Value");
    END;

    LOCAL PROCEDURE CheckInvoiceCanBeCanceled@32(VAR SalesInvoiceHeader@1000 : Record 112);
    VAR
      CorrectPostedSalesInvoice@1001 : Codeunit 1303;
    BEGIN
      IF IsInvoiceCanceled THEN
        ERROR(AlreadyCanceledErr);
      CorrectPostedSalesInvoice.TestCorrectInvoiceIsAllowed(SalesInvoiceHeader,TRUE);
    END;

    LOCAL PROCEDURE IsInvoiceCanceled@25() : Boolean;
    BEGIN
      EXIT(Status = Status::Canceled);
    END;

    LOCAL PROCEDURE PostInvoice@20(VAR SalesHeader@1001 : Record 36;VAR SalesInvoiceHeader@1000 : Record 112);
    VAR
      DummyO365SalesDocument@1003 : Record 2103;
      LinesInstructionMgt@1005 : Codeunit 1320;
      O365SendResendInvoice@1002 : Codeunit 2104;
      PreAssignedNo@1006 : Code[20];
    BEGIN
      O365SendResendInvoice.CheckDocumentIfNoItemsExists(SalesHeader,FALSE,DummyO365SalesDocument);
      LinesInstructionMgt.SalesCheckAllLinesHaveQuantityAssigned(SalesHeader);
      PreAssignedNo := SalesHeader."No.";
      SalesHeader.SendToPosting(CODEUNIT::"Sales-Post");
      SalesInvoiceHeader.SETCURRENTKEY("Pre-Assigned No.");
      SalesInvoiceHeader.SETRANGE("Pre-Assigned No.",PreAssignedNo);
      SalesInvoiceHeader.FINDFIRST;
    END;

    LOCAL PROCEDURE SendPostedInvoice@34(VAR SalesInvoiceHeader@1000 : Record 112);
    VAR
      GraphIntBusinessProfile@1001 : Codeunit 5442;
    BEGIN
      CheckSmtpMailSetup;
      CheckSendToEmailAddress(SalesInvoiceHeader."No.");
      GraphIntBusinessProfile.SyncFromGraphSynchronously;

      SalesInvoiceHeader.SETRECFILTER;
      SalesInvoiceHeader.EmailRecords(FALSE);
    END;

    LOCAL PROCEDURE SendDraftInvoice@15(VAR SalesHeader@1000 : Record 36);
    VAR
      DummyO365SalesDocument@1003 : Record 2103;
      LinesInstructionMgt@1002 : Codeunit 1320;
      O365SendResendInvoice@1001 : Codeunit 2104;
      GraphIntBusinessProfile@1004 : Codeunit 5442;
    BEGIN
      O365SendResendInvoice.CheckDocumentIfNoItemsExists(SalesHeader,FALSE,DummyO365SalesDocument);
      LinesInstructionMgt.SalesCheckAllLinesHaveQuantityAssigned(SalesHeader);
      CheckSmtpMailSetup;
      CheckSendToEmailAddress(SalesHeader."No.");

      GraphIntBusinessProfile.SyncFromGraphSynchronously;
      SalesHeader.SETRECFILTER;
      SalesHeader.EmailRecords(FALSE);
    END;

    LOCAL PROCEDURE SendCanceledInvoice@41(VAR SalesInvoiceHeader@1000 : Record 112);
    VAR
      JobQueueEntry@1001 : Record 472;
      GraphIntBusinessProfile@1004 : Codeunit 5442;
    BEGIN
      CheckSmtpMailSetup;
      CheckSendToEmailAddress(SalesInvoiceHeader."No.");
      GraphIntBusinessProfile.SyncFromGraphSynchronously;

      JobQueueEntry.INIT;
      JobQueueEntry."Object Type to Run" := JobQueueEntry."Object Type to Run"::Codeunit;
      JobQueueEntry."Object ID to Run" := CODEUNIT::"O365 Sales Cancel Invoice";
      JobQueueEntry."Maximum No. of Attempts to Run" := 3;
      JobQueueEntry."Record ID to Process" := SalesInvoiceHeader.RECORDID;
      CODEUNIT.RUN(CODEUNIT::"Job Queue - Enqueue",JobQueueEntry);
    END;

    LOCAL PROCEDURE CancelInvoice@39(VAR SalesInvoiceHeader@1000 : Record 112);
    VAR
      SalesCrMemoHeader@1007 : Record 114;
      SalesHeader@1001 : Record 36;
    BEGIN
      GetPostedInvoice(SalesInvoiceHeader);
      CheckInvoiceCanBeCanceled(SalesInvoiceHeader);
      IF NOT CODEUNIT.RUN(CODEUNIT::"Correct Posted Sales Invoice",SalesInvoiceHeader) THEN BEGIN
        SalesCrMemoHeader.SETRANGE("Applies-to Doc. No.",SalesInvoiceHeader."No.");
        IF SalesCrMemoHeader.FINDFIRST THEN
          ERROR(CancelingInvoiceFailedCreditMemoCreatedAndPostedErr,GETLASTERRORTEXT);
        SalesHeader.SETRANGE("Applies-to Doc. No.",SalesInvoiceHeader."No.");
        IF SalesHeader.FINDFIRST THEN
          ERROR(CancelingInvoiceFailedCreditMemoCreatedButNotPostedErr,GETLASTERRORTEXT);
        ERROR(CancelingInvoiceFailedNothingCreatedErr,GETLASTERRORTEXT);
      END;
    END;

    LOCAL PROCEDURE SetActionResponse@47(VAR ActionContext@1004 : DotNet "'Microsoft.Dynamics.Nav.Ncl, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Runtime.WebServiceActionContext";InvoiceId@1000 : GUID);
    VAR
      ODataActionManagement@1003 : Codeunit 6711;
    BEGIN
      ODataActionManagement.AddKey(FIELDNO(Id),InvoiceId);
      ODataActionManagement.SetDeleteResponseLocation(ActionContext,PAGE::"Sales Invoice Entity");
    END;

    [ServiceEnabled]
    [External]
    PROCEDURE Post@18(VAR ActionContext@1000 : DotNet "'Microsoft.Dynamics.Nav.Ncl, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Runtime.WebServiceActionContext");
    VAR
      SalesHeader@1001 : Record 36;
      SalesInvoiceHeader@1002 : Record 112;
    BEGIN
      GetDraftInvoice(SalesHeader);
      PostInvoice(SalesHeader,SalesInvoiceHeader);
      SetActionResponse(ActionContext,SalesInvoiceHeader.Id);
    END;

    [ServiceEnabled]
    [External]
    PROCEDURE PostAndSend@33(VAR ActionContext@1000 : DotNet "'Microsoft.Dynamics.Nav.Ncl, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Runtime.WebServiceActionContext");
    VAR
      SalesHeader@1005 : Record 36;
      SalesInvoiceHeader@1002 : Record 112;
    BEGIN
      GetDraftInvoice(SalesHeader);
      PostInvoice(SalesHeader,SalesInvoiceHeader);
      COMMIT;
      SendPostedInvoice(SalesInvoiceHeader);
      SetActionResponse(ActionContext,SalesInvoiceHeader.Id);
    END;

    [ServiceEnabled]
    [External]
    PROCEDURE Send@17(VAR ActionContext@1000 : DotNet "'Microsoft.Dynamics.Nav.Ncl, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Runtime.WebServiceActionContext");
    VAR
      SalesHeader@1005 : Record 36;
      SalesInvoiceHeader@1002 : Record 112;
    BEGIN
      IF Posted THEN BEGIN
        GetPostedInvoice(SalesInvoiceHeader);
        IF IsInvoiceCanceled THEN
          SendCanceledInvoice(SalesInvoiceHeader)
        ELSE
          SendPostedInvoice(SalesInvoiceHeader);
        SetActionResponse(ActionContext,SalesInvoiceHeader.Id);
        EXIT;
      END;
      GetDraftInvoice(SalesHeader);
      SendDraftInvoice(SalesHeader);
      SetActionResponse(ActionContext,SalesHeader.Id);
    END;

    [ServiceEnabled]
    [External]
    PROCEDURE Cancel@19(VAR ActionContext@1000 : DotNet "'Microsoft.Dynamics.Nav.Ncl, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Runtime.WebServiceActionContext");
    VAR
      SalesInvoiceHeader@1002 : Record 112;
    BEGIN
      GetPostedInvoice(SalesInvoiceHeader);
      CancelInvoice(SalesInvoiceHeader);
      SetActionResponse(ActionContext,SalesInvoiceHeader.Id);
    END;

    [ServiceEnabled]
    [External]
    PROCEDURE CancelAndSend@51(VAR ActionContext@1000 : DotNet "'Microsoft.Dynamics.Nav.Ncl, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Runtime.WebServiceActionContext");
    VAR
      SalesInvoiceHeader@1002 : Record 112;
    BEGIN
      GetPostedInvoice(SalesInvoiceHeader);
      CancelInvoice(SalesInvoiceHeader);
      SendCanceledInvoice(SalesInvoiceHeader);
      SetActionResponse(ActionContext,SalesInvoiceHeader.Id);
    END;

    BEGIN
    END.
  }
}

