OBJECT XMLport 11012067 Import GL Entries ADP Multilin
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Import GL Entries ADP Multilink;
               SVE=Importera redovisningstransaktioner ADP Multilink];
    Direction=Import;
    DefaultFieldsValidation=No;
    TextEncoding=WINDOWS;
    OnInitXMLport=BEGIN
                    HumanResourcesSetup.GET;
                    HumanResourcesSetup.TESTFIELD("Salary Application",HumanResourcesSetup."Salary Application"::Multipay);
                    HumanResourcesSetup.TESTFIELD("Pathname Files from Salary");
                    HumanResourcesSetup.TESTFIELD("GL Entry Journal Template");
                    HumanResourcesSetup.TESTFIELD("GL Entry Journal Batch");

                    currXMLport.FILENAME(HumanResourcesSetup."Pathname Files from Salary");
                  END;

    OnPreXMLport=BEGIN
                   GenJnlLine.LOCKTABLE;
                   GenJnlLine.SETRANGE("Journal Template Name",HumanResourcesSetup."GL Entry Journal Template");
                   GenJnlLine.SETRANGE("Journal Batch Name",HumanResourcesSetup."GL Entry Journal Batch");
                   IF GenJnlLine.FIND('+') THEN
                     LineCounter := GenJnlLine."Line No."
                   ELSE
                     LineCounter := 0;
                 END;

    OnPostXMLport=BEGIN
                    OldFile := currXMLport.FILENAME;
                    NewFile := HumanResourcesSetup.DetermineBackupFile(OldFile);
                    IF NewFile <> OldFile THEN
                      FileMgt.RenameClientFile(OldFile, NewFile, TRUE);
                  END;

    Format=Variable Text;
    FieldDelimiter=<None>;
    FieldSeparator=[;];
  }
  ELEMENTS
  {
    { [{5390BCED-B496-4FC8-AB89-1F1D14DC5BA8}];  ;Root                ;Element ;Text     }

    { [{204AFF83-FAA7-4D12-A5D3-FBA6518ACCCA}];1 ;GenJournalLine      ;Element ;Table   ;
                                                  SourceTable=Table81;
                                                  SourceTableView=SORTING(Field1,Field51,Field2);
                                                  Import::OnAfterInitRecord=BEGIN
                                                                              "Gen. Journal Line".INIT;
                                                                              CLEAR(Mem);
                                                                              CLEAR(Periode);
                                                                              CLEAR(Date);
                                                                              CLEAR(DocNo);
                                                                              CLEAR(Info);
                                                                              CLEAR(GLAccount);
                                                                              CLEAR(Department);
                                                                              CLEAR(CostObject);
                                                                              CLEAR(DCCode);
                                                                              CLEAR(ValueTxt);
                                                                              CLEAR(Sign);
                                                                            END;

                                                  Import::OnBeforeInsertRecord=BEGIN
                                                                                 WITH "Gen. Journal Line" DO BEGIN //With statement added because this code has been copied from dataport.

                                                                                 IF AllInputFieldsAreEmpty THEN
                                                                                   currXMLport.SKIP;

                                                                                 //End Date Wage Period in format ddmmyy provided
                                                                                 IF Date <> '' THEN BEGIN
                                                                                   EVALUATE(BookYear,COPYSTR(Date,5,2));
                                                                                   EVALUATE(BookMonth,COPYSTR(Date,3,2));
                                                                                   EVALUATE(BookDay,COPYSTR(Date,1,2));
                                                                                   PostingDate := DMY2DATE(BookDay,BookMonth,BookYear+2000);
                                                                                 END ELSE BEGIN
                                                                                   PostingDate := TODAY;
                                                                                 END;

                                                                                 //amount in format 7V2 (without delimiter); seperate +/- sign in file
                                                                                 Value := Value / 100.0;
                                                                                 IF Sign = '-' THEN
                                                                                   Value := -Value;
                                                                                 IF DCCode = Text002 THEN
                                                                                   Value := -Value;

                                                                                 "Journal Template Name" := HumanResourcesSetup."GL Entry Journal Template";
                                                                                 "Journal Batch Name"    := HumanResourcesSetup."GL Entry Journal Batch";
                                                                                 "Line No."     := LineCounter + 10000;
                                                                                 LineCounter    := "Line No.";
                                                                                 "Account Type" := "Account Type"::"G/L Account";

                                                                                 IF DocNo = '' THEN BEGIN
                                                                                   IF JnlBatchRec."No. Series" <> '' THEN
                                                                                     DocNo := NoSeriesMgt.GetNextNo(JnlBatchRec."No. Series",0D,FALSE)
                                                                                   ELSE
                                                                                     IF JnlTemplRec."No. Series" <> '' THEN
                                                                                       DocNo := NoSeriesMgt.GetNextNo(JnlTemplRec."No. Series",0D,FALSE);
                                                                                 END;
                                                                                 "Document No." := DocNo;

                                                                                 IF NOT JnlTemplRec.GET(HumanResourcesSetup."GL Entry Journal Template") THEN
                                                                                   JnlTemplRec.INIT;
                                                                                 IF NOT JnlBatchRec.GET(HumanResourcesSetup."GL Entry Journal Template", HumanResourcesSetup."GL Entry Journal Batch") THEN
                                                                                   JnlBatchRec.INIT;

                                                                                 StripZero(GLAccount);
                                                                                 StripZero(Department);
                                                                                 GlAccountRec.GET(GLAccount);
                                                                                 IF NOT GlAccountRec."Direct Posting" THEN BEGIN
                                                                                   "Account No." := GLAccount;
                                                                                   "Gen. Posting Type" := GlAccountRec."Gen. Posting Type";
                                                                                   "Gen. Bus. Posting Group" := GlAccountRec."Gen. Bus. Posting Group";
                                                                                   "Gen. Prod. Posting Group" := GlAccountRec."Gen. Prod. Posting Group";
                                                                                   "VAT Bus. Posting Group" := GlAccountRec."VAT Bus. Posting Group";
                                                                                   "VAT Prod. Posting Group" := GlAccountRec."VAT Prod. Posting Group";
                                                                                 END ELSE BEGIN
                                                                                   "Shortcut Dimension 1 Code" := Department;  //test db 12-07-02
                                                                                   VALIDATE("Account No.", GLAccount);
                                                                                 END;
                                                                                 //**Allocate department as cost center after validation of G/L account
                                                                                 "Shortcut Dimension 1 Code" := Department;
                                                                                 VALIDATE("Shortcut Dimension 2 Code",CostObject);
                                                                                 VALIDATE(Amount, Value);
                                                                                 IF EntryDesc <> '' THEN
                                                                                   Description := EntryDesc
                                                                                 ELSE
                                                                                   Description := Info;
                                                                                 "Posting Date"  := PostingDate;
                                                                                 "Document Date" := PostingDate;
                                                                                 "Source Code"   := JnlTemplRec."Source Code";
                                                                                 IF JnlBatchRec."Reason Code" <> '' THEN
                                                                                   "Reason Code" := JnlBatchRec."Reason Code"
                                                                                 ELSE
                                                                                   "Reason Code" := JnlTemplRec."Reason Code";
                                                                                 "Origin Salary Application" := TRUE;

                                                                                 ShowStatus("Journal Template Name", "Journal Batch Name", "Line No.");

                                                                                 INSERT;

                                                                                 END; //Belongs to With statement.

                                                                                 currXMLport.SKIP;
                                                                               END;
                                                                                }

    { [{DF22764F-4B43-479F-82C4-C4E120D879F1}];2 ;Mem                 ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{E33ABDA5-F108-4971-A7C3-16D31B1465A6}];2 ;Periode             ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{42D32C43-69CE-401B-AB34-C9DB04AFEE1B}];2 ;Date                ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{07EF8A87-E712-4E53-AA51-6FF5E6C151EE}];2 ;DocNo               ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{5768FC52-628B-4425-ACFD-7A82490F6D84}];2 ;Info                ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{F3993B21-B06B-4595-81C5-52C90B962681}];2 ;GLAccount           ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{93402B5A-8D09-450B-ACB7-E5D0BD8D26E2}];2 ;Department          ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{F822B374-38F9-409D-B092-73C5A3424FAE}];2 ;CostObject          ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{B358C90F-5BD4-4DDC-ABCA-5D119E8BF80C}];2 ;DCCode              ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{A2093162-BE2B-480C-9476-B6158CBAD9E6}];2 ;ValueTxt            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  FillEmptyTextWithZero(ValueTxt);
                                                                                  EVALUATE(Value, ValueTxt);
                                                                                END;
                                                                                 }

    { [{D414C7FB-D85C-4413-BAF2-F646E5BCABBF}];2 ;Sign                ;Element ;Text    ;
                                                  MinOccurs=Zero }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1100528402;;Container;
                  ContainerType=ContentArea }

      { 1100528401;1;Group  ;
                  CaptionML=[ENU=Options;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100528400;2;Field  ;
                  CaptionML=[ENU=Entry Description;
                             SVE=Transaktionsbeskrivning];
                  SourceExpr=EntryDesc }

    }
  }
  CODE
  {
    VAR
      HumanResourcesSetup@1100528424 : Record 5218;
      GenJnlLine@1100528423 : Record 81;
      JnlTemplRec@1100528422 : Record 80;
      JnlBatchRec@1100528421 : Record 232;
      GlAccountRec@1100528420 : Record 15;
      NoSeriesMgt@1100528419 : Codeunit 396;
      FileMgt@1100528400 : Codeunit 419;
      XMLportStatusDialogMgt@1100528401 : Codeunit 11130006;
      LineCounter@1100528418 : Integer;
      PostingDate@1100528417 : Date;
      BookYear@1100528416 : Integer;
      BookMonth@1100528415 : Integer;
      BookDay@1100528414 : Integer;
      Value@1100528411 : Decimal;
      EntryDesc@1100528405 : Text[30];
      OldFile@1100528404 : Text[250];
      NewFile@1100528403 : Text[250];
      Text002@1100528402 : TextConst 'ENU=C;SVE=Text005';

    PROCEDURE StripZero@1(VAR convtext@11012000 : Text[30]);
    VAR
      Position@11012001 : Integer;
    BEGIN
      Position := STRPOS(convtext,'0');
      WHILE Position = 1 DO
        BEGIN
          convtext := DELSTR(convtext,Position,1);
          Position  := STRPOS(convtext,'0');
        END;
    END;

    PROCEDURE FillEmptyTextWithZero@1100525013(VAR Text@1100525000 : Text[1024]);
    BEGIN
      IF DELCHR(Text,'<>=',' ') = '' THEN
        Text := '0';
    END;

    LOCAL PROCEDURE ShowStatus@1100409000(JournalTemplateName@1100409000 : Code[10];JournalBatchName@1100409001 : Code[10];LineNo@1100409002 : Integer);
    VAR
      GenJournalLine@1100409004 : Record 81;
      RecRef@1100409003 : RecordRef;
    BEGIN
      GenJournalLine."Journal Template Name" := JournalTemplateName;
      GenJournalLine."Journal Batch Name" := JournalBatchName;
      GenJournalLine."Line No." := LineNo;
      RecRef.GETTABLE(GenJournalLine);
      XMLportStatusDialogMgt.ShowImportStatus(RecRef);
    END;

    LOCAL PROCEDURE AllInputFieldsAreEmpty@1100525001() : Boolean;
    BEGIN
      EXIT(
        (Mem = '') AND
        (Periode = '') AND
        (Date = '') AND
        (DocNo = '') AND
        (Info = '') AND
        (GLAccount = '') AND
        (Department = '') AND
        (CostObject = '') AND
        (DCCode = '') AND
        (ValueTxt = '') AND
        (Sign = ''));
    END;

    PROCEDURE InitXMLPort@1100528502(iEntryDesc@1100528500 : Text[30]);
    BEGIN
      EntryDesc := iEntryDesc;
    END;

    BEGIN
    {
      CC: Dataport 11012340
    }
    END.
  }
}

