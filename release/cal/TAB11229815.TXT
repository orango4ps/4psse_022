OBJECT Table 11229815 Default Authority Perm (PPT)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnModify=BEGIN
               CALCFIELDS("High Security Subsite");
               IF (NOT "High Security Subsite") AND (Type = Type::Access) THEN
                 "Document Type" := '';
               CheckPermissions;
             END;

    CaptionML=ENU=Default Authority Perm (PPT);
  }
  FIELDS
  {
    { 3   ;   ;Subsite Type        ;Option        ;CaptionML=ENU=Subsite Type;
                                                   OptionCaptionML=ENU=Project,Service Location;
                                                   OptionString=Project,ServLoc;
                                                   Editable=No }
    { 5   ;   ;Type                ;Option        ;CaptionML=SVE=Typ;
                                                   OptionCaptionML=ENU=,Access,Filter;
                                                   OptionString=,Access,Field Filter;
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 10  ;   ;Authority Type      ;Code20        ;TableRelation="Authority Type";
                                                   CaptionML=[ENU=Authority Type;
                                                              NOR=Myndighetstype;
                                                              SVE=Beh”righetstyp];
                                                   NotBlank=Yes }
    { 11  ;   ;Authority Type Description;Text30  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Authority Type".Description WHERE (Code=FIELD(Authority Type)));
                                                   CaptionML=[ENU=Authority Type Description;
                                                              NOR=Beskrivelse av myndighetstype;
                                                              SVE=Beskrivning av beh”righetstyp];
                                                   Editable=No }
    { 20  ;   ;Project Portal Subsite;Code20      ;TableRelation="Project Portal Subsite".Code WHERE (Quote Request Subsite=CONST(No),
                                                                                                      Type=FIELD(Subsite Type));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("High Security Subsite", "Quote Request Subsite");
                                                              END;

                                                   CaptionML=ENU=Document Portal Subsite }
    { 21  ;   ;Project Portal Subsite Descr;Text30;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Portal Subsite".Description WHERE (Code=FIELD(Project Portal Subsite)));
                                                   CaptionML=ENU=Document Portal Subsite Description;
                                                   Editable=No }
    { 22  ;   ;High Security Subsite;Boolean      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Portal Subsite"."High Security Subsite" WHERE (Code=FIELD(Project Portal Subsite)));
                                                   CaptionML=ENU=High Security Subsite;
                                                   Editable=No }
    { 23  ;   ;Quote Request Subsite;Boolean      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Portal Subsite"."Quote Request Subsite" WHERE (Code=FIELD(Project Portal Subsite)));
                                                   CaptionML=ENU=Quote Request Subsite;
                                                   Editable=No }
    { 30  ;   ;Document Type       ;Code20        ;TableRelation=IF (Type=CONST(Access),
                                                                     High Security Subsite=CONST(Yes)) "Subsite-Doc Type (PPT)"."Document Type" WHERE (Project Portal Subsite=FIELD(Project Portal Subsite))
                                                                     ELSE IF (Type=CONST(Field Filter)) "Subsite-Doc Type (PPT)"."Document Type" WHERE (Project Portal Subsite=FIELD(Project Portal Subsite));
                                                   CaptionML=[ENU=Document Type;
                                                              NOR=Dokumenttype;
                                                              SVE=Dokumenttyp] }
    { 31  ;   ;Document Type Description;Text30   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Document Type".Description WHERE (Type=FIELD(Document Type)));
                                                   CaptionML=[ENU=Document Type Description;
                                                              NOR=Dokumenttype, Beskrivelse;
                                                              SVE=Dokumenttyp, beskrivning];
                                                   Editable=No }
    { 40  ;   ;Permission Level    ;Option        ;OnValidate=BEGIN
                                                                CheckPermissions;
                                                              END;

                                                   CaptionML=ENU=Permission Level;
                                                   OptionCaptionML=ENU=None,Visitor,Member;
                                                   OptionString=None,Visitor,Member }
    { 50  ;   ;Show in View        ;Boolean       ;CaptionML=ENU=Show in View }
  }
  KEYS
  {
    {    ;Subsite Type,Type,Authority Type,Project Portal Subsite,Document Type;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE AddProjectPortalPermissions@1100528302();
    VAR
      AuthorityType@1100528301 : Record 11020215;
      ProjectPortalSite@1100528300 : Record 11229810;
      SiteDocTypePPT@1100528302 : Record 11229811;
    BEGIN
      IF (NOT AuthorityType.FINDSET) THEN
        EXIT;

      REPEAT
        InsertRecord(0, 1, AuthorityType.Code, '', '');

        ProjectPortalSite.SETRANGE(Type, ProjectPortalSite.Type::Project);
        ProjectPortalSite.SETRANGE("Quote Request Subsite", FALSE);
        ProjectPortalSite.SETRANGE("High Security Subsite", TRUE);
        IF (ProjectPortalSite.FINDSET) THEN BEGIN
          REPEAT
            SiteDocTypePPT.SETRANGE("Subsite Type", ProjectPortalSite.Type);
            SiteDocTypePPT.SETRANGE("Project Portal Subsite", ProjectPortalSite.Code);
            IF (SiteDocTypePPT.FINDSET) THEN BEGIN
              InsertRecord(ProjectPortalSite.Type, 1, AuthorityType.Code, ProjectPortalSite.Code, '');
              REPEAT
                InsertRecord(ProjectPortalSite.Type, 1, AuthorityType.Code, ProjectPortalSite.Code, SiteDocTypePPT."Document Type");
              UNTIL (SiteDocTypePPT.NEXT = 0);
            END;
          UNTIL (ProjectPortalSite.NEXT = 0);
        END;

        ProjectPortalSite.SETRANGE("High Security Subsite", FALSE);
        IF (ProjectPortalSite.FINDSET) THEN BEGIN
          REPEAT
            InsertRecord(ProjectPortalSite.Type, 1, AuthorityType.Code, ProjectPortalSite.Code, '');
          UNTIL (ProjectPortalSite.NEXT = 0);
        END;
      UNTIL (AuthorityType.NEXT = 0);
    END;

    PROCEDURE AddProjectPortalFilters@1100528300();
    VAR
      AuthorityType@1100528300 : Record 11020215;
      ProjectPortalSite@1100528301 : Record 11229810;
      SiteDocTypePPT@1100528302 : Record 11229811;
    BEGIN
      IF (NOT AuthorityType.FINDSET) THEN
        EXIT;

      REPEAT
        ProjectPortalSite.SETRANGE(Type, ProjectPortalSite.Type::Project);
        IF (ProjectPortalSite.FINDSET) THEN BEGIN
          REPEAT
            SiteDocTypePPT.SETRANGE("Project Portal Subsite", ProjectPortalSite.Code);
            IF (SiteDocTypePPT.FINDSET) THEN BEGIN
              REPEAT
                InsertRecord(ProjectPortalSite.Type, 2, AuthorityType.Code, ProjectPortalSite.Code, SiteDocTypePPT."Document Type");
              UNTIL (SiteDocTypePPT.NEXT = 0);
            END;
          UNTIL (ProjectPortalSite.NEXT = 0);
        END;
      UNTIL (AuthorityType.NEXT = 0);
    END;

    LOCAL PROCEDURE InsertRecord@1100528301(SubsiteType@1100528305 : Option;Type@1100528304 : Option;AuthorityCode@1100528301 : Code[20];ProjectPortalSite@1100528302 : Code[20];DocumentType@1100528303 : Code[20]);
    VAR
      DefaultAuthorityPermPPT@1100528300 : Record 11229815;
    BEGIN
      DefaultAuthorityPermPPT.INIT;
      DefaultAuthorityPermPPT."Subsite Type" := SubsiteType;
      DefaultAuthorityPermPPT.Type := Type;
      DefaultAuthorityPermPPT."Authority Type" := AuthorityCode;
      DefaultAuthorityPermPPT."Project Portal Subsite" := ProjectPortalSite;
      DefaultAuthorityPermPPT."Document Type" := DocumentType;
      CASE Type OF
        1: DefaultAuthorityPermPPT."Permission Level" := DefaultAuthorityPermPPT."Permission Level"::Visitor;
        2: DefaultAuthorityPermPPT."Show in View" := TRUE;
      END;
      IF (DefaultAuthorityPermPPT.INSERT) THEN;
    END;

    PROCEDURE CheckPermissions@1100409001();
    BEGIN
      CheckPermissionLevel;
    END;

    LOCAL PROCEDURE CheckPermissionLevel@1100409000();
    VAR
      DefaultAuthorityPermPPT@1100528300 : Record 11229815;
    BEGIN
      IF (Type <> Type::Access) THEN
        EXIT;
      CALCFIELDS("High Security Subsite");
      IF (NOT "High Security Subsite") THEN
        EXIT;

      IF ("Document Type" = '') THEN BEGIN
        DefaultAuthorityPermPPT.SETRANGE("Subsite Type", "Subsite Type");
        DefaultAuthorityPermPPT.SETRANGE(Type, Type);
        DefaultAuthorityPermPPT.SETRANGE("Authority Type", "Authority Type");
        DefaultAuthorityPermPPT.SETRANGE("Project Portal Subsite", "Project Portal Subsite");
        DefaultAuthorityPermPPT.SETFILTER("Document Type", '<>%1', '');
        DefaultAuthorityPermPPT.MODIFYALL("Permission Level", "Permission Level");
      END ELSE BEGIN
        IF (DefaultAuthorityPermPPT.GET("Subsite Type", Type, "Authority Type", "Project Portal Subsite", '')) THEN BEGIN
          IF (DefaultAuthorityPermPPT."Permission Level" < "Permission Level") THEN BEGIN
            DefaultAuthorityPermPPT."Permission Level" := "Permission Level";
            DefaultAuthorityPermPPT.MODIFY;
          END;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

