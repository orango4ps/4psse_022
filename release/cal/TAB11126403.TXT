OBJECT Table 11126403 Subcontr.Port. Location
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               SubcontPortUser@1100528100 : Record 11126401;
             BEGIN
               IF Login <> '' THEN BEGIN
                 SubcontPortUser.GET("Vendor No.", Login);
                 SubcontPortUser.TESTFIELD("All Locations Visible",FALSE);
               END;
             END;

    OnDelete=VAR
               SubcontrPortLocation@1100528100 : Record 11126403;
               WorkOrder@1100528600 : Record 11229279;
               ServiceOrder@1100528601 : Record 11012823;
               RecordFound@1100528602 : Boolean;
             BEGIN
               IF ("Vendor No." <> '') AND  (Login = '') THEN BEGIN
                 //Check for existence of published Work Orders
                 CALCFIELDS("Resource No.");
                 WorkOrder.SETCURRENTKEY("Resource No.");
                 WorkOrder.SETRANGE("Resource No.", "Resource No.");
                 WorkOrder.SETRANGE("Source Type", WorkOrder."Source Type"::ServiceOrder);
                 WorkOrder.SETRANGE("Publish on Subcontr. Portal", TRUE);
                 IF WorkOrder.FINDSET THEN BEGIN
                   REPEAT
                     ServiceOrder.CHANGECOMPANY(WorkOrder."Source Company");
                     ServiceOrder.SETRANGE("No.", WorkOrder."Source No.");
                     ServiceOrder.SETRANGE("Service Location No.", "Service Location No.");
                     IF NOT ServiceOrder.ISEMPTY THEN
                       RecordFound := TRUE;
                   UNTIL (WorkOrder.NEXT = 0) OR RecordFound;
                   IF RecordFound THEN
                     IF NOT CONFIRM(Text001, FALSE) THEN
                       ERROR('');
                   WorkOrder.MODIFYALL("Publish on Subcontr. Portal", FALSE);
                 END;

                 //Delete user locations when deleting a vendor location
                 SubcontrPortLocation.SETRANGE("Vendor No.", "Vendor No.");
                 SubcontrPortLocation.SETRANGE("Service Location No.", "Service Location No.");
                 SubcontrPortLocation.SETFILTER(Login, '<>%1','');
                 SubcontrPortLocation.DELETEALL(TRUE);
               END;
             END;

    CaptionML=[ENU=Subcontractor Portal Location;
               NOR=Underentrepren›r portalbeliggenhet;
               SVE=Underentreprenadsportal, plats];
    LookupPageID=Page11126408;
    DrillDownPageID=Page11126408;
  }
  FIELDS
  {
    { 10  ;   ;Vendor No.          ;Code20        ;TableRelation="Subcont.Port. Vendor"."Vendor No.";
                                                   CaptionML=[ENU=Vendor No.;
                                                              NOR=Leverand›rnr.;
                                                              SVE=Lev.nr];
                                                   NotBlank=Yes }
    { 20  ;   ;Login               ;Text30        ;TableRelation="Subcont.Port. User".Login WHERE (Vendor No.=FIELD(Vendor No.));
                                                   CaptionML=[ENU=Login;
                                                              NOR=Innlogging;
                                                              SVE=Inloggning] }
    { 30  ;   ;Service Location No.;Code20        ;TableRelation=IF (Login=FILTER('')) "Service Location".No.
                                                                 ELSE IF (Login=FILTER(<>'')) "Subcontr.Port. Location"."Service Location No." WHERE (Vendor No.=FIELD(Vendor No.),
                                                                                                                                                      Login=FILTER(''));
                                                   CaptionML=[ENU=Service Location No.;
                                                              NOR=Servicelokasjon Nr.;
                                                              SVE=Serviceplatsnr] }
    { 40  ;   ;Service Location Description;Text50;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location".Description WHERE (No.=FIELD(Service Location No.)));
                                                   CaptionML=[ENU=Service Location Description;
                                                              NOR=Servicelokasjonbeskrivelse;
                                                              SVE=Serviceplatsbeskrivning];
                                                   Editable=No }
    { 50  ;   ;Resource No.        ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Subcont.Port. Vendor"."Resource No." WHERE (Vendor No.=FIELD(Vendor No.)));
                                                   CaptionML=[ENU=Resource No.;
                                                              NOR=Ressursnr.;
                                                              SVE=Resursnr];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Vendor No.,Login,Service Location No.   ;Clustered=Yes }
    {    ;Service Location No.                     }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100525000 : TextConst 'ENU=There are still published work orders on this location that wil not be published on the subcontractor portal anymore. Do you want to continue?';

    BEGIN
    END.
  }
}

