OBJECT Table 11072331 DPT Subsite Doc Type Project
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnModify=BEGIN
               CheckRecordChangeAllowed;
             END;

    OnDelete=VAR
               DPTRespPersPermProject@1100528303 : Record 11072332;
               DPTDocSetupPeripheralApp@1100528300 : Record 11072452;
               DPTAuthorityPermProject@1100528304 : Record 11072334;
             BEGIN
               CheckRecordChangeAllowed;
               DPTRespPersPermProject.SETRANGE("Project No.", "Project No.");
               DPTRespPersPermProject.SETRANGE("Subsite Code", "Subsite Code");
               DPTRespPersPermProject.SETRANGE("Document Type", "Document Type");
               DPTRespPersPermProject.DELETEALL(TRUE);

               DPTAuthorityPermProject.SETRANGE("Project No.", "Project No.");
               DPTAuthorityPermProject.SETRANGE("Subsite Code", "Subsite Code");
               DPTAuthorityPermProject.SETRANGE("Document Type", "Document Type");
               DPTAuthorityPermProject.DELETEALL(TRUE);

               DPTDocSetupPeripheralApp.SETRANGE("Project No.", "Project No.");
               DPTDocSetupPeripheralApp.SETRANGE("Document Portal Subsite", "Subsite Code");
               DPTDocSetupPeripheralApp.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               ERROR(Text000);
             END;

    CaptionML=ENU=DPT Document Type per Subsite (Project);
    LookupPageID=Page11229659;
    DrillDownPageID=Page11229659;
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Project Description");
                                                              END;

                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 11  ;   ;Project Description ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Job.Description WHERE (No.=FIELD(Project No.)));
                                                   CaptionML=SVE=Projektbeskrivning;
                                                   Editable=No }
    { 20  ;   ;Subsite Code        ;Code20        ;TableRelation="DPT Subsite Project".Code WHERE (Project No.=FIELD(Project No.),
                                                                                                   Quote Request Subsite=CONST(No));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Subsite Description", "Subsite Structure");
                                                              END;

                                                   CaptionML=ENU=Subsite Code;
                                                   NotBlank=Yes }
    { 21  ;   ;Subsite Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Subsite Project".Description WHERE (Project No.=FIELD(Project No.),
                                                                                                               Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Subsite Description;
                                                   Editable=No }
    { 22  ;   ;Subsite Structure   ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Subsite Project"."Subsite Structure" WHERE (Project No.=FIELD(Project No.),
                                                                                                                       Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Subsite Structure;
                                                   OptionCaptionML=ENU=1 Document Library,Document Library per Document Type,Permission per Document Type;
                                                   OptionString=OneDocLibrary,DocLibraryPerDocType,PermissionPerDocType;
                                                   Editable=No }
    { 30  ;   ;Document Type       ;Code10        ;TableRelation="Document Type".Type;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Document Type Description");
                                                              END;

                                                   CaptionML=[ENU=Document Type;
                                                              NOR=Dokumenttype;
                                                              SVE=Dokumenttyp];
                                                   NotBlank=Yes }
    { 31  ;   ;Document Type Description;Text50   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Document Type".Description WHERE (Type=FIELD(Document Type)));
                                                   CaptionML=[ENU=Document Type Description;
                                                              NOR=Dokumenttype, Beskrivelse;
                                                              SVE=Dokumenttyp, beskrivning];
                                                   Editable=No }
    { 60  ;   ;Synchronized On     ;DateTime      ;CaptionML=ENU=Synchronized On;
                                                   Editable=No }
    { 90  ;   ;Use Document CheckIn/-Out;Boolean  ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                UpdateSubsiteStructure;
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Manual Check-In/Check-Out }
  }
  KEYS
  {
    {    ;Project No.,Subsite Code,Document Type  ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528300 : TextConst 'ENU=Rename is not allowed.;NOR=Gi nytt navn ikke tillatt;SVE=Att ge nytt namn „r inte till†tet.';
      Text001@1100525000 : TextConst 'ENU=There can be only 1 %1.';
      Text002@1100525001 : TextConst 'ENU=%1 cann''t be equal to %2, because this is already the Quote Request %1.';

    PROCEDURE CreateAllDocumentTypeLinks@1100528304(ProjectNo@1100528303 : Code[20]);
    VAR
      DPTDefaultSubsiteDocType@1100528300 : Record 11229942;
      DPTSubsiteProject@1100528301 : Record 11072330;
      DPTSubsiteDocTypeProject@1100528302 : Record 11072331;
    BEGIN
      DPTSubsiteProject.SETRANGE("Project No.", ProjectNo);
      DPTSubsiteProject.SETRANGE("Quote Request Subsite", FALSE);
      IF (NOT DPTSubsiteProject.FINDSET) THEN
        EXIT;

      REPEAT
        DPTDefaultSubsiteDocType.SETRANGE("Source Type", DPTDefaultSubsiteDocType."Source Type"::Project);
        DPTDefaultSubsiteDocType.SETRANGE("Subsite Code", DPTSubsiteProject.Code);
        IF (DPTDefaultSubsiteDocType.FINDSET) THEN BEGIN
          REPEAT
            IF (NOT DPTSubsiteDocTypeProject.GET(ProjectNo, DPTSubsiteProject.Code, DPTDefaultSubsiteDocType."Document Type")) THEN BEGIN
              DPTSubsiteDocTypeProject.INIT;
              DPTSubsiteDocTypeProject."Project No." := ProjectNo;
              DPTSubsiteDocTypeProject."Subsite Code" := DPTDefaultSubsiteDocType."Subsite Code";
              DPTSubsiteDocTypeProject."Document Type" := DPTDefaultSubsiteDocType."Document Type";
              DPTSubsiteDocTypeProject."Use Document CheckIn/-Out" := DPTDefaultSubsiteDocType."Use Document CheckIn/-Out";
              DPTSubsiteDocTypeProject.INSERT(TRUE);
            END;
          UNTIL (DPTDefaultSubsiteDocType.NEXT = 0);
        END;
      UNTIL (DPTSubsiteProject.NEXT = 0);
    END;

    PROCEDURE ToggleUseDocumentCheckInOut@1100528302(NewValue@1100528300 : Boolean);
    BEGIN
      IF (NOT FINDSET(TRUE)) THEN
        EXIT;

      REPEAT
        "Use Document CheckIn/-Out" := NewValue;
        MODIFY(TRUE);
      UNTIL (NEXT = 0);
    END;

    LOCAL PROCEDURE "---Locals---"@1100528303();
    BEGIN
    END;

    LOCAL PROCEDURE CheckRecordChangeAllowed@1100528301();
    VAR
      DPTInterface@1100528300 : Codeunit 11012451;
    BEGIN
      TESTFIELD("Synchronized On", 0DT);
      DPTInterface.SetDPTType(1);
      DPTInterface.CheckDocumentsLinkedToSubsiteDocumentType("Project No.", "Subsite Code", "Document Type");
    END;

    LOCAL PROCEDURE UpdateSubsiteStructure@1100528305();
    VAR
      DPTSubsiteDocTypeProject@1100528300 : Record 11072331;
    BEGIN
      CALCFIELDS("Subsite Structure");
      IF ("Subsite Structure"<> "Subsite Structure"::OneDocLibrary) THEN
        EXIT;

      DPTSubsiteDocTypeProject.SETRANGE("Project No.", "Project No.");
      DPTSubsiteDocTypeProject.SETRANGE("Subsite Code", "Subsite Code");
      DPTSubsiteDocTypeProject.SETFILTER("Document Type", '<>%1', "Document Type");
      DPTSubsiteDocTypeProject.MODIFYALL("Use Document CheckIn/-Out", "Use Document CheckIn/-Out", TRUE);
    END;

    BEGIN
    END.
  }
}

