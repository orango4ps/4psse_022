OBJECT Table 11071700 Object Modification
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 ServSetup.GET;
                 ServSetup.TESTFIELD("Object Modification Nos.");
                 NoSeriesMgt.InitSeries(ServSetup."Object Modification Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;

               "Input by" := USERID;
               "Input Date" := CREATEDATETIME(TODAY,TIME);
             END;

    OnDelete=VAR
               ObjectModificationLine@1100528600 : Record 11071701;
               MaintSchemeVersionLine@1100528601 : Record 11071744;
               ServiceText@1100529600 : Record 11012882;
             BEGIN
               ObjectModificationLine.SETRANGE("Modification No.", "No.");
               ObjectModificationLine.DELETEALL(TRUE);

               MaintSchemeVersionLine.SETRANGE("Line Type", MaintSchemeVersionLine."Line Type"::Modification);
               MaintSchemeVersionLine.SETRANGE("Type Code", "No.");
               MaintSchemeVersionLine.DELETEALL(TRUE);

               ServiceText.SETRANGE("Table Name", ServiceText."Table Name"::"Object Modification");
               ServiceText.SETRANGE("No.", "No.");
               ServiceText.DELETEALL;
             END;

    CaptionML=[ENU=Modification;
               NOR=Endring;
               SVE=Žndring];
    LookupPageID=Page11125166;
    DrillDownPageID=Page11125166;
  }
  FIELDS
  {
    { 10  ;   ;No.                 ;Code10        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  ServSetup.GET;
                                                                  NoSeriesMgt.TestManual(ServSetup."Object Modification Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              NOR=Nr;
                                                              SVE=Nr] }
    { 20  ;   ;Standard Object No. ;Code20        ;TableRelation="Standard Object".No.;
                                                   OnLookup=VAR
                                                              ServiceObjectManagement@1100528600 : Codeunit 11012808;
                                                            BEGIN
                                                              IF ServiceObjectManagement.HandleLookupStandardObject('', FALSE, "Standard Object No.") THEN
                                                                VALIDATE("Standard Object No.");
                                                            END;

                                                   CaptionML=[ENU=Standard Object No.;
                                                              NOR=Standardobjektnr;
                                                              SVE=Standardobjektsnr] }
    { 30  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 40  ;   ;Resolve before      ;Date          ;CaptionML=[ENU=Resolve before;
                                                              NOR=L›s f›r;
                                                              SVE=L”s f”re] }
    { 60  ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              NOR=Nr-serie;
                                                              SVE=Nr-serie];
                                                   Editable=No }
    { 70  ;   ;Discipline          ;Code10        ;TableRelation=Discipline;
                                                   CaptionML=[ENU=Discipline;
                                                              NOR=Disiplin;
                                                              SVE=Disciplin];
                                                   Description=SERV_CONS }
    { 80  ;   ;Trade Speciality    ;Code10        ;TableRelation="Trade Speciality".Code WHERE (Discipline=FIELD(Discipline));
                                                   CaptionML=[ENU=Trade Speciality;
                                                              NOR=SNI-nummer;
                                                              SVE=SNI-nummer];
                                                   Description=SERV_CONS }
    { 90  ;   ;Trade Expertise     ;Code10        ;TableRelation="Trade Expertise";
                                                   OnValidate=VAR
                                                                TradeExpRec@1100525000 : Record 11020286;
                                                              BEGIN
                                                                TradeExpRec.GetRelatedValue(Discipline, "Trade Speciality", "Trade Expertise");
                                                              END;

                                                   CaptionML=[ENU=Trade Expertise;
                                                              NOR=Bransjeekspertise;
                                                              SVE=Branschexpertis];
                                                   Description=SERV_CONS }
    { 100 ;   ;Input by            ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Input by;
                                                              NOR=Input av;
                                                              SVE=Inmatat av] }
    { 110 ;   ;Input Date          ;DateTime      ;CaptionML=[ENU=Input Date;
                                                              NOR=Input Dato;
                                                              SVE=Inmatat den] }
    { 160 ;   ;Customer No.        ;Code20        ;TableRelation=Customer.No.;
                                                   OnValidate=VAR
                                                                Customer@1100528600 : Record 18;
                                                              BEGIN
                                                                IF "Customer No." <> '' THEN BEGIN
                                                                  Customer.GET("Customer No.");
                                                                  "Price Book Type" := Customer."Price Book Type";
                                                                  "Price Book Code" := Customer."Price Book Code";
                                                                  "Price Book Index Date" := Customer."Price Book Index Date";
                                                                  "Unit Price Set" := Customer."Unit Price Set";
                                                                  "Unit Price Status" := Customer."Unit Price Status";
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Customer No.;
                                                              NOR=Kundenr.;
                                                              SVE=Kundnr] }
    { 170 ;   ;Customer Name       ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Customer.Name WHERE (No.=FIELD(Customer No.)));
                                                   CaptionML=[ENU=Customer Name;
                                                              NOR=Kundnavn;
                                                              SVE=Kundens namn];
                                                   Editable=No }
    { 180 ;   ;Price Book Type     ;Option        ;OnValidate=VAR
                                                                ServiceUnitPriceManagement@1100528600 : Codeunit 11012846;
                                                              BEGIN
                                                                CASE "Price Book Type" OF
                                                                  "Price Book Type"::None:
                                                                    VALIDATE("Price Book Code", '');
                                                                  "Price Book Type"::Customer:
                                                                    BEGIN
                                                                      "Price Book Code" := "Customer No.";
                                                                      "Price Book Index Date" :=
                                                                        ServiceUnitPriceManagement.GetLatestPriceBookIndexDate("Price Book Type", "Customer No.");
                                                                      IF "Price Book Index Date" = 0D THEN
                                                                        "Price Book Code" := '';
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Price Book Type;
                                                              SVE=Enhetspristyp];
                                                   OptionCaptionML=[ENU=None,Internal,Customer;
                                                                    NOR=Ingen,Intern,Kunde);
                                                                    SVE=Inget,Internt,Kund];
                                                   OptionString=None,Internal,Customer }
    { 190 ;   ;Price Book Code     ;Code20        ;TableRelation="Service Price Book".Code WHERE (Type=FIELD(Price Book Type));
                                                   OnValidate=BEGIN
                                                                IF ("Price Book Code" = '') OR ("Price Book Code" <> xRec."Price Book Code") THEN BEGIN
                                                                  VALIDATE("Price Book Index Date", 0D);
                                                                  VALIDATE("Unit Price Set", '');
                                                                  VALIDATE("Unit Price Status", "Unit Price Status"::" ");
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              ServicePriceBook@1100528600 : Record 11071768;
                                                              ServicePriceBookList@1100528601 : Page 11125269;
                                                            BEGIN
                                                              ServicePriceBook.SETRANGE(Type, "Price Book Type");
                                                              ServicePriceBookList.LOOKUPMODE(TRUE);
                                                              ServicePriceBookList.SETTABLEVIEW(ServicePriceBook);
                                                              IF ServicePriceBookList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                ServicePriceBookList.GETRECORD(ServicePriceBook);
                                                                "Price Book Code" := ServicePriceBook.Code;
                                                                "Price Book Index Date" := ServicePriceBook."Index Date";
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Price Book Code;
                                                              SVE=Enhetspriskod] }
    { 200 ;   ;Price Book Index Date;Date         ;OnValidate=BEGIN
                                                                IF "Price Book Index Date" <> 0D THEN
                                                                  IF "Price Book Type" = "Price Book Type"::None THEN
                                                                    FIELDERROR("Price Book Index Date");
                                                              END;

                                                   CaptionML=[ENU=Price Book Index Date;
                                                              SVE=Indexdatum f”r enhetspris] }
    { 210 ;   ;Unit Price Status   ;Option        ;OnValidate=BEGIN
                                                                IF "Unit Price Status" <> "Unit Price Status"::" " THEN
                                                                  IF "Price Book Type" = "Price Book Type"::None THEN
                                                                    FIELDERROR("Unit Price Status");
                                                              END;

                                                   CaptionML=[ENU=Unit Price Status;
                                                              NOR=Enhetspris, status;
                                                              SVE=Enhetsprisstatus];
                                                   OptionCaptionML=[ENU=" ,Pre-Recording,Recording,Realization";
                                                                    NOR=" ,Forh†ndsregistrering,Registrering,Realisering";
                                                                    SVE=" ,F”rregistrering,Registrering,Realisering"];
                                                   OptionString=[ ,Pre-Recording,Recording,Realization] }
    { 220 ;   ;Settlement Method   ;Option        ;CaptionML=[ENU=Settlement Method;
                                                              NOR=Oppgj›rsmetode;
                                                              SVE=Avr„kningsmetod];
                                                   OptionCaptionML=[ENU=Cost Plus,Fixed Price;
                                                                    NOR=Selvkost,Fastpris;
                                                                    SVE=L”pande,Fast pris];
                                                   OptionString=Cost Plus,Fixed Price }
    { 225 ;   ;Master Company      ;Text30        ;TableRelation=Company;
                                                   OnValidate=VAR
                                                                CheckLicenceIC@1210190000 : Codeunit 11012780;
                                                              BEGIN
                                                                IF "Master Company" <> '' THEN
                                                                  CheckLicenceIC.CheckICEntry("Master Company");
                                                                IF "Master Company" <> xRec."Master Company" THEN BEGIN
                                                                  CheckCompanyIC;
                                                                  "Master Project" := '';
                                                                  "Master Element" := ''
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Master Company;
                                                              SVE=Huvudf”retag] }
    { 230 ;   ;Master Project      ;Code20        ;TableRelation=IF (Master Company=FILTER('')) Job.No. WHERE (Internal Project=CONST(No));
                                                   OnValidate=BEGIN
                                                                IF "Master Project" <> xRec."Master Project" THEN BEGIN
                                                                  CheckCompanyIC;
                                                                  VALIDATE("Master Element", '');
                                                                  IF "Master Project" <> '' THEN BEGIN
                                                                    MasterProjRec.GET("Master Project");
                                                                    IF NOT (MasterProjRec."Project Status" IN [MasterProjRec."Project Status"::Production]) THEN
                                                                      MasterProjRec.FIELDERROR("Project Status");
                                                                  END;
                                                                  IF NOT MasterProjElemRec.GET("Master Project", "Master Element") THEN
                                                                    "Master Element" := '';
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              OnLookupJobNo(TRUE);
                                                            END;

                                                   CaptionML=[ENU=Master Project;
                                                              NOR=Hovedprosjekt;
                                                              SVE=Originalprojekt] }
    { 235 ;   ;Master Project Description;Text100 ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Job.Description WHERE (No.=FIELD(Master Project)));
                                                   CaptionML=ENU=Master Project Description;
                                                   Editable=No }
    { 240 ;   ;Master Element      ;Code20        ;TableRelation=IF (Master Company=FILTER('')) "Project Element".Element WHERE (Project No.=FIELD(Master Project));
                                                   OnValidate=VAR
                                                                FormatManagement@1100528601 : Codeunit 11012022;
                                                              BEGIN
                                                                IF "Master Element" <> '' THEN BEGIN
                                                                  CheckCompanyIC;
                                                                  FormatManagement.FormatElem("Master Element",2,'');
                                                                  MasterProjElemRec.GET("Master Project", "Master Element");
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              CheckCompanyIC;
                                                              MasterProjElemRec."Project No." := "Master Project";
                                                              MasterProjElemRec.Element := "Master Element";
                                                              MasterProjElemRec.SETRANGE("Project No.", "Master Project");
                                                              IF PAGE.RUNMODAL(0, MasterProjElemRec) = ACTION::LookupOK THEN
                                                                VALIDATE("Master Element", MasterProjElemRec.Element);
                                                            END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Master Element;
                                                              NOR=Hovedelement;
                                                              SVE=Debitera till elementet] }
    { 250 ;   ;Order Kind          ;Code10        ;TableRelation="Service Order Indication".Code WHERE (Indication=CONST(Kind));
                                                   CaptionML=[ENU=Order Kind;
                                                              NOR=Type ordre;
                                                              SVE=Typ av order] }
    { 260 ;   ;Unit Price Set      ;Code20        ;TableRelation="Service Unit Price Set".Code WHERE (Price Book Type=FIELD(Price Book Type),
                                                                                                      Price Book Code=FIELD(Price Book Code),
                                                                                                      Price Book Index Date=FIELD(Price Book Index Date));
                                                   OnValidate=BEGIN
                                                                IF "Unit Price Set" <> '' THEN
                                                                  VALIDATE("Unit Price Status", "Unit Price Status"::"Pre-Recording");
                                                              END;

                                                   CaptionML=[ENU=Unit Price Set;
                                                              SVE=A-pris grupp] }
    { 270 ;   ;On Hold             ;Code3         ;TableRelation="On Hold Code".Code;
                                                   CaptionML=[ENU=On Hold;
                                                              NOR=Avvent;
                                                              SVE=Stoppad] }
    { 280 ;   ;Use Modification Clusters;Boolean  ;OnValidate=VAR
                                                                ServiceCluster@1100528600 : Record 11071742;
                                                                ObjectModificationLine@1100528604 : Record 11071701;
                                                              BEGIN
                                                                IF xRec."Use Modification Clusters" <> "Use Modification Clusters" THEN BEGIN
                                                                  IF NOT "Use Modification Clusters" THEN BEGIN
                                                                    ServiceCluster.SETRANGE(Type, ServiceCluster.Type::Modification);
                                                                    ServiceCluster.SETRANGE("Type Code", "No.");
                                                                    ServiceCluster.SETRANGE("Source Type", ServiceCluster."Source Type"::" ");
                                                                    IF NOT ServiceCluster.ISEMPTY THEN
                                                                      ERROR(Text000);
                                                                  END;
                                                                  ObjectModificationLine.SETRANGE("Modification No.", "No.");
                                                                  ObjectModificationLine.SETFILTER("Service Cluster", '<>%1', '');
                                                                  IF NOT ObjectModificationLine.ISEMPTY THEN
                                                                    ERROR(Text001, ObjectModificationLine.TABLECAPTION);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Use Modification Clusters }
    { 290 ;   ;Appointments by     ;Option        ;OnValidate=BEGIN
                                                                IF "Appointments by" <> "Appointments by"::Letter THEN
                                                                  "Appointment Letter Code" := '';
                                                              END;

                                                   CaptionML=ENU=Appointments by;
                                                   OptionCaptionML=[ENU=" ,Letter,E-Mail";
                                                                    SVE=" ,Brev,E-Post"];
                                                   OptionString=[ ,Letter,E-Mail] }
    { 300 ;   ;Appointment Letter Code;Code10     ;TableRelation="SO Appointment Letter";
                                                   OnValidate=BEGIN
                                                                IF "Appointment Letter Code" <> '' THEN
                                                                  TESTFIELD("Appointments by", "Appointments by"::Letter);
                                                              END;

                                                   CaptionML=[ENU=Appointment Letter;
                                                              NOR=M›tebrev;
                                                              SVE=M”tesbrev] }
    { 310 ;   ;Global Dimension 1 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   CaptionML=[ENU=Global Dimension 1 Code;
                                                              NOR=Global dimensjon 1 - kode;
                                                              SVE=Global dimension 1 kod];
                                                   CaptionClass='1,1,1' }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ServSetup@1100526200 : Record 11012800;
      NoSeriesMgt@1100526201 : Codeunit 396;
      MasterProjRec@1100528202 : Record 11072003;
      MasterProjElemRec@1100528201 : Record 11012010;
      Text000@1100528601 : TextConst 'ENU=Contract contains Clusters';
      Text001@1100528600 : TextConst 'ENU=Clusters used in %1;SVE=Innan du kan anv„nda Online Map m†ste du fylla i f”nstret Online Map - inst„llning.\Se Konfiguration av Online Map i hj„lpen';

    PROCEDURE AssistEdit@2(OldApplianceModification@1000 : Record 11071700) : Boolean;
    VAR
      ApplianceModification@1100526200 : Record 11071700;
    BEGIN
      //mg.sn, 15-12-10: M24139
      ServSetup.GET;
      ServSetup.TESTFIELD("Object Modification Nos.");
      ApplianceModification := Rec;
      IF NoSeriesMgt.SelectSeries(ServSetup."Object Modification Nos.", OldApplianceModification."No. Series","No. Series") THEN
      BEGIN
        ServSetup.GET;
        NoSeriesMgt.SetSeries("No.");
        Rec := ApplianceModification;
        EXIT(TRUE);
      END;
      //mg.en, 15-12-10: M24139
    END;

    PROCEDURE CheckCompanyIC@1100528200();
    VAR
      lvCompName@1210190027 : Text[50];
    BEGIN
      IF "Master Company" <> '' THEN
        lvCompName := "Master Company"
      ELSE
        lvCompName := COMPANYNAME;

      MasterProjRec.CHANGECOMPANY(lvCompName);
      MasterProjElemRec.CHANGECOMPANY(lvCompName);
    END;

    PROCEDURE OnLookupJobNo@1100528203(EditableMode@1100525000 : Boolean);
    VAR
      PageNo@1100525001 : Integer;
    BEGIN
      CheckCompanyIC;
      MasterProjRec."No." := "Master Project";
      IF RefersToOtherCompany THEN
        PageNo := PAGE::"Job List IC";
      IF NOT EditableMode THEN
        PAGE.RUNMODAL(PageNo, MasterProjRec)
      ELSE BEGIN
        IF PAGE.RUNMODAL(PageNo, MasterProjRec) = ACTION::LookupOK THEN
          VALIDATE("Master Project", MasterProjRec."No.");
      END;
    END;

    PROCEDURE RefersToOtherCompany@1100525008() : Boolean;
    BEGIN
      EXIT(("Master Company" <> '') AND ("Master Company" <> COMPANYNAME));
    END;

    BEGIN
    END.
  }
}

