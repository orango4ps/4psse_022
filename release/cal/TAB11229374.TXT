OBJECT Table 11229374 Default My Notification Setup
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Default My Notification Setup;
  }
  FIELDS
  {
    { 10  ;   ;Notification Id     ;GUID          ;CaptionML=[ENU=Notification Id;
                                                              NOR=Varslings-ID;
                                                              SVE=Meddelande-ID];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 20  ;   ;Apply to Table Id   ;Integer       ;CaptionML=[ENU=Apply to Table Id;
                                                              NOR=Bruk p† tabell-ID;
                                                              SVE=Koppla till tabell-ID];
                                                   Editable=No }
    { 30  ;   ;Enabled             ;Boolean       ;CaptionML=[ENU=Enabled;
                                                              NOR=Aktivert;
                                                              SVE=Aktiverad] }
    { 40  ;   ;Apply to Table Filter;BLOB         ;CaptionML=[ENU=Filter;
                                                              NOR=Filter;
                                                              SVE=Filtrera] }
    { 50  ;   ;Name                ;Text128       ;CaptionML=[ENU=Notification;
                                                              NOR=Melding;
                                                              SVE=Meddelande];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 60  ;   ;Description         ;BLOB          ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
  }
  KEYS
  {
    {    ;Notification Id                         ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ViewFilterDetailsTxt@1100529601 : TextConst 'ENU=(View filter details);NOR=(Vis filterdetaljer);SVE=(Vyfilterdetaljer)';
      DefineFiltersTxt@1100529600 : TextConst 'ENU=Define filters...;NOR=Definer filtre...;SVE=Definiera filter...';

    LOCAL PROCEDURE IsFilterEnabled@1100529602() : Boolean;
    BEGIN
      EXIT(("Apply to Table Id" <> 0) AND Enabled);
    END;

    PROCEDURE GetDescription@1100529604() Ret : Text;
    VAR
      InStream@1100529609 : InStream;
    BEGIN
      CALCFIELDS(Description);
      IF NOT Description.HASVALUE THEN
        EXIT;
      Description.CREATEINSTREAM(InStream);
      InStream.READ(Ret);
    END;

    LOCAL PROCEDURE GetFilteredRecord@1100529610(VAR RecordRef@1100529611 : RecordRef;Filters@1100529612 : Text);
    VAR
      TempBlob@1100529613 : Record 99008535;
      RequestPageParametersHelper@1100529614 : Codeunit 1530;
      FiltersOutStream@1100529615 : OutStream;
    BEGIN
      TempBlob.INIT;
      TempBlob.Blob.CREATEOUTSTREAM(FiltersOutStream);
      FiltersOutStream.WRITE(Filters);

      RecordRef.OPEN("Apply to Table Id");
      RequestPageParametersHelper.ConvertParametersToFilters(RecordRef,TempBlob);
    END;

    PROCEDURE GetFiltersAsDisplayText@1100529605() : Text;
    VAR
      RecordRef@1100529600 : RecordRef;
    BEGIN
      IF NOT IsFilterEnabled THEN
        EXIT;

      GetFilteredRecord(RecordRef,GetFiltersAsText);

      IF RecordRef.GETFILTERS <> '' THEN
        EXIT(RecordRef.GETFILTERS);

      EXIT(ViewFilterDetailsTxt);
    END;

    LOCAL PROCEDURE GetFiltersAsText@1100529606() Filters : Text;
    VAR
      FiltersInStream@1100529600 : InStream;
    BEGIN
      IF NOT IsFilterEnabled THEN
        EXIT;

      CALCFIELDS("Apply to Table Filter");
      IF NOT "Apply to Table Filter".HASVALUE THEN
        EXIT;
      "Apply to Table Filter".CREATEINSTREAM(FiltersInStream);
      FiltersInStream.READ(Filters);
    END;

    PROCEDURE OpenFilterSettings@1100529607() Changed : Boolean;
    VAR
      DummyMyNotifications@1100529600 : Record 1518;
      RecordRef@1100529601 : RecordRef;
      FiltersOutStream@1100529602 : OutStream;
      NewFilters@1100529603 : Text;
    BEGIN
      IF NOT IsFilterEnabled THEN
        EXIT;

      IF RunDynamicRequestPage(NewFilters,
           GetFiltersAsText,
           "Apply to Table Id")
      THEN BEGIN
        GetFilteredRecord(RecordRef,NewFilters);
        IF RecordRef.GETFILTERS = '' THEN
          "Apply to Table Filter" := DummyMyNotifications."Apply to Table Filter"
        ELSE BEGIN
          "Apply to Table Filter".CREATEOUTSTREAM(FiltersOutStream);
          FiltersOutStream.WRITE(NewFilters);
        END;
        MODIFY;
        Changed := TRUE;
      END;
    END;

    LOCAL PROCEDURE RunDynamicRequestPage@1100529608(VAR ReturnFilters@1100529600 : Text;Filters@1100529601 : Text;TableNum@1100529602 : Integer) FiltersSet : Boolean;
    VAR
      RequestPageParametersHelper@1100529605 : Codeunit 1530;
      FilterPageBuilder@1100529606 : FilterPageBuilder;
    BEGIN
      IF NOT RequestPageParametersHelper.BuildDynamicRequestPage(FilterPageBuilder,DefineFiltersTxt,TableNum) THEN
        EXIT(FALSE);

      IF Filters <> '' THEN
        IF NOT RequestPageParametersHelper.SetViewOnDynamicRequestPage(
             FilterPageBuilder,Filters,DefineFiltersTxt,TableNum)
        THEN
          EXIT(FALSE);

      FilterPageBuilder.PAGECAPTION := DefineFiltersTxt;
      IF NOT FilterPageBuilder.RUNMODAL THEN
        EXIT(FALSE);

      ReturnFilters :=
        RequestPageParametersHelper.GetViewFromDynamicRequestPage(FilterPageBuilder,DefineFiltersTxt,TableNum);

      FiltersSet := TRUE;
    END;

    PROCEDURE InitializeDefaultNotifications@1100525000();
    VAR
      MyNotifications@1100525000 : Record 1518;
      DefaultMyNotificationSetup@1100525001 : Record 11229374;
    BEGIN
      MyNotifications.SETAUTOCALCFIELDS("Apply to Table Filter",Description);
      MyNotifications.SETRANGE("User Id",USERID);
      IF MyNotifications.FINDSET THEN
        REPEAT
          IF NOT DefaultMyNotificationSetup.GET(MyNotifications."Notification Id") THEN BEGIN
            DefaultMyNotificationSetup.INIT;
            DefaultMyNotificationSetup."Notification Id" := MyNotifications."Notification Id";
            DefaultMyNotificationSetup."Apply to Table Id" := MyNotifications."Apply to Table Id";
            DefaultMyNotificationSetup.Enabled := MyNotifications.Enabled;
            DefaultMyNotificationSetup."Apply to Table Filter" := MyNotifications."Apply to Table Filter";
            DefaultMyNotificationSetup.Name := MyNotifications.Name;
            DefaultMyNotificationSetup.Description := MyNotifications.Description;
            DefaultMyNotificationSetup.INSERT;
          END;
        UNTIL MyNotifications.NEXT = 0;
    END;

    BEGIN
    {
      DP01533
    }
    END.
  }
}

