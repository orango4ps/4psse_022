OBJECT Report 11012960 Generate Installations by Plot
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Erzeugen Installationen nach Baunr.;
               ENU=Generate Installations by Plot;
               NLD=Genereren installaties per bouwnummer;
               NOR=Generer installasjoner etter tegning;
               SVE=Generera inst. efter plats];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  FillTheFilterFields();
                  GenerateProcess();
                END;

  }
  DATASET
  {
    { 6463;    ;DataItem;                    ;
               DataItemTable=Table11012968;
               DataItemTableView=SORTING(Project No.,Plot No.,Discipline,Installation,Inspection);
               OnPreDataItem=BEGIN
                               CurrReport.BREAK;
                             END;

               ReqFilterFields=Project No.,Plot No.,Discipline,Installation,Inspection }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      InstallationRec@1210190016 : Record 11012964;
      InstInspectionRec@1210190015 : Record 11012965;
      PlotRec@1210190013 : Record 11012500;
      TmpPlotInstallationRec@1210190012 : TEMPORARY Record 11012968;
      PlotInstallationRec@1210190011 : Record 11012968;
      Window@1210190010 : Dialog;
      DisciplineFilterFrom@1210190009 : Code[1024];
      InstallationFilterFrom@1210190008 : Code[1024];
      InspectionFilterFrom@1210190007 : Code[1024];
      ProjectTo@1210190006 : Code[20];
      PlotFilterTo@1210190004 : Code[1024];
      NextLineNo@1210190003 : Integer;
      Counter@1210190002 : Integer;
      CounterNoInspec@1210190001 : Integer;
      InstallationsNoInspec@1210190000 : Text[200];
      Text001@1210190020 : TextConst 'DEU=Installation #1##########\Baunummer  #2##########;ENU=Installation #1##########\Plot No.     #2##########;NLD=Installatie #1##########\Bouwnummer  #2##########;NOR=Installasjon #1##########\Tegning nr. #2##########;SVE=Installation #1##########\Ritningsnr.     #2##########';
      Text002@1210190019 : TextConst 'DEU=%1 Zeile(n) erstellt.;ENU=%1 line(s) created.;NLD=%1 regel(s) aangemaakt.;NOR=%1 rad(er) er opprettet.;SVE=%1 rad(er) har skapats.';
      Text003@1210190005 : TextConst 'DEU=Bei %1 Installation(en) noch keine ''šberprfung(en)'' eingegeben. U.a. %2;ENU="For %1 Instalation no ''Inspection(s) filled in. F.i.: %2  ";NLD="Bij %1 inslallatie(s) nog geen ''Controle(s)'' ingegeven. O.a. %2 ";NOR=For %1 Installasjon er ingen inspeksjon(er) fylt ut. F.u.: %2;SVE="F”r %1 installation ingen Inspektion har fyllts i. F.i.: %2  "';

    PROCEDURE FillTheFilterFields@1210190006();
    VAR
      lvText001@1210190000 : TextConst 'DEU=Es muss ein Projekt im Projektfilter (''%1'') ausgew„hlt sein.;ENU=There must be selecteted one project in the Project Filter (%1).;NLD=Er moet ‚‚n project geselecteerd zijn in het Projectfilter (''%1'').;NOR=Ett prosjekt m† v‘re valgt i prosjektfilteret (%1).;SVE=Ett projekt m†ste vara valt i projektfiltret (%1).';
      lvProjRec@1210190001 : Record 11072003;
    BEGIN
      WITH "KOMO Installation by Plot" DO BEGIN
        IF (GETFILTER("Project No.") = '') OR (STRLEN(GETFILTER("Project No.")) > MAXSTRLEN(lvProjRec."No.")) THEN
          ERROR(lvText001, GETFILTER("Project No."));
        IF NOT lvProjRec.GET(GETFILTER("Project No.")) THEN
          ERROR(lvText001, GETFILTER("Project No."));

        ProjectTo := GETFILTER("Project No.");
        PlotFilterTo := GETFILTER("Plot No.");
        DisciplineFilterFrom := GETFILTER(Discipline);
        InstallationFilterFrom := GETFILTER(Installation);
        InspectionFilterFrom := GETFILTER(Inspection);
      END;
    END;

    PROCEDURE GenerateProcess@1210190001();
    BEGIN
      Counter := 0;
      CounterNoInspec :=0;
      InstallationsNoInspec := '';
      Window.OPEN(Text001);

      TmpPlotInstallationRec.DELETEALL;

      ReadInstallations();
      ReadPlots();

      Window.CLOSE;
      IF CounterNoInspec > 0 THEN
        MESSAGE(Text003, CounterNoInspec, InstallationsNoInspec);

      MESSAGE(Text002, Counter);
    END;

    PROCEDURE ReadInstallations@1210190002();
    BEGIN
      NextLineNo := 1;

      InstInspectionRec.RESET;

      InstallationRec.RESET;
      InstallationRec.SETCURRENTKEY(Discipline, Code);
      InstallationRec.SETFILTER(Discipline, DisciplineFilterFrom);
      InstallationRec.SETFILTER(Code, InstallationFilterFrom);
      IF InstallationRec.FIND('-') THEN BEGIN
        REPEAT
          Window.UPDATE(1, InstallationRec.Code);
          InstInspectionRec.SETRANGE(Installation, InstallationRec.Code);
          InstInspectionRec.SETFILTER(Inspection, InspectionFilterFrom);
          IF InstInspectionRec.FIND('-') THEN BEGIN
            REPEAT
              WriteTmpPlotInstallation();
              NextLineNo := NextLineNo + 1;
            UNTIL InstInspectionRec.NEXT = 0;
          END ELSE BEGIN
            IF (STRLEN(InstallationRec.Code) + 2) <= (MAXSTRLEN(InstallationsNoInspec) - STRLEN(InstallationsNoInspec)) THEN BEGIN
              IF CounterNoInspec > 0 THEN
                InstallationsNoInspec := InstallationsNoInspec + ', ';
              InstallationsNoInspec := InstallationsNoInspec + InstallationRec.Code;
              CounterNoInspec := CounterNoInspec + 1;
            END;
          END;
        UNTIL InstallationRec.NEXT = 0;
      END;
    END;

    PROCEDURE WriteTmpPlotInstallation@1210190000();
    BEGIN
      WITH TmpPlotInstallationRec DO BEGIN
        INIT;
        "Project No." := ProjectTo;
        "Plot No." := '';
        "Line No."  := NextLineNo;
        Discipline := InstallationRec.Discipline;
        Installation := InstallationRec.Code;
        Inspection := InstInspectionRec.Inspection;
        INSERT;
      END;
    END;

    PROCEDURE ReadPlots@1210190005();
    BEGIN
      PlotInstallationRec.LOCKTABLE;

      PlotRec.RESET;
      PlotRec.SETRANGE("Project No.", ProjectTo);
      PlotRec.SETFILTER("Plot No.", PlotFilterTo);
      IF PlotRec.FIND('-') THEN BEGIN
        REPEAT
          Window.UPDATE(2, PlotRec."Plot No.");
          NextLineNo := DetermineFreeLineNo();
          TmpPlotInstallationRec.SETRANGE("Project No.", PlotRec."Project No.");
          IF TmpPlotInstallationRec.FIND('-') THEN BEGIN
            REPEAT
              Window.UPDATE(1, TmpPlotInstallationRec.Installation);
              IF NOT PlotCombinationAlreadyPresent() THEN BEGIN
                WritePlotInstallation();
                Counter := Counter + 1;
                NextLineNo := NextLineNo + 10000
              END;
            UNTIL TmpPlotInstallationRec.NEXT = 0;
          END
        UNTIL PlotRec.NEXT = 0;
      END;
    END;

    PROCEDURE DetermineFreeLineNo@1210190015() : Integer;
    VAR
      lvPlotInstallationRec@1210190000 : Record 11012968;
    BEGIN
      lvPlotInstallationRec.SETRANGE("Project No.", PlotRec."Project No.");
      lvPlotInstallationRec.SETRANGE("Plot No.", PlotRec."Plot No.");
      IF lvPlotInstallationRec.FIND('+') THEN
        EXIT(lvPlotInstallationRec."Line No." + 10000);

      EXIT(10000);
    END;

    PROCEDURE PlotCombinationAlreadyPresent@1210190017() : Boolean;
    VAR
      lvPlotInstallationRec@1210190000 : Record 11012968;
    BEGIN
      lvPlotInstallationRec.SETCURRENTKEY("Project No.","Plot No.",Discipline,Installation,Inspection);
      lvPlotInstallationRec.SETRANGE("Project No.", PlotRec."Project No.");
      lvPlotInstallationRec.SETRANGE("Plot No.", PlotRec."Plot No.");
      lvPlotInstallationRec.SETRANGE(Discipline, TmpPlotInstallationRec.Discipline);
      lvPlotInstallationRec.SETRANGE(Installation, TmpPlotInstallationRec.Installation);
      lvPlotInstallationRec.SETRANGE(Inspection, TmpPlotInstallationRec.Inspection);
      IF lvPlotInstallationRec.FIND('-') THEN
        EXIT(TRUE);

      EXIT(FALSE);
    END;

    PROCEDURE WritePlotInstallation@1210190008();
    BEGIN
      WITH PlotInstallationRec DO BEGIN
        PlotInstallationRec := TmpPlotInstallationRec;
        "Plot No." := PlotRec."Plot No.";
        "Line No." := NextLineNo;
        INSERT;
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

