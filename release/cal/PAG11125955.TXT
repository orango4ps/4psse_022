OBJECT Page 11125955 Copy Base Plant Insp. List
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Stamminspektionsliste kopieren;
               ENU=Copy Base Inspection List;
               NLD=Stam inspectielijst kopiâren;
               NOR=Kopier basisinspeksjonsliste;
               SVE=Kopiera basinspektionslista];
    SourceTable=Table11072680;
    DataCaptionExpr='';
    PageType=StandardDialog;
    OnOpenPage=BEGIN
                 FromBaseInspectionList := Code;
                 ToBaseInspectionList := FromBaseInspectionList;
                 FromBaseInspectionListDescr := Description;
                 ToBaseInspectionListDescr := FromBaseInspectionListDescr;
                 ReferenceDate := TODAY;
                 DetermineFromIndexDate;
                 ToIndexDate := 0D;
                 ToBaseInspectionListDescrEditable := (FromBaseInspectionList <> ToBaseInspectionList);
               END;

    OnQueryClosePage=BEGIN
                       CloseActionOK := (CloseAction = ACTION::OK);
                       IF CloseActionOK THEN BEGIN
                         ValidateFromBaseInspectionList;
                         ValidateReferenceDate;
                         ValidateToBaseInspectionList;
                         ValidateToBaseInspectionListDescr;
                         ValidateToIndexDate;
                       END;
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100528601;1;Group  ;
                CaptionML=[DEU=Kopieren von;
                           ENU=Copy from;
                           NLD=Kopiâren van;
                           NOR=Kopier fra;
                           SVE=Kopiera frÜn];
                GroupType=Group }

    { 6   ;2   ;Field     ;
                CaptionML=[NOR=Basisinspeksjonsliste;
                           SVE=Basinspektionslista];
                NotBlank=Yes;
                SourceExpr=FromBaseInspectionList;
                CaptionClass=gPlantInspectionVersion.FIELDCAPTION("Base Inspection List");
                TableRelation="Base Plant Inspection List";
                OnValidate=BEGIN
                             ValidateFromBaseInspectionList;
                           END;
                            }

    { 8   ;2   ;Field     ;
                CaptionML=[NOR=Beskrivelse av basisinspesjonsliste;
                           SVE=Beskrivning basinspektionslista];
                SourceExpr=FromBaseInspectionListDescr;
                CaptionClass=gPlantInspectionVersion.FIELDCAPTION("Inspection List Description");
                Editable=FALSE;
                QuickEntry=FALSE }

    { 1100528600;2;Field  ;
                CaptionML=[DEU=Stichtag;
                           ENU=Reference Date;
                           NLD=Peildatum;
                           NOR=Referansedato;
                           SVE=Referensdatum];
                NotBlank=Yes;
                SourceExpr=ReferenceDate;
                OnValidate=BEGIN
                             ValidateReferenceDate;
                           END;
                            }

    { 1100529000;2;Field  ;
                SourceExpr=FromIndexDate;
                CaptionClass=gPlantInspectionVersion.FIELDCAPTION("Index Date");
                Editable=FALSE;
                QuickEntry=FALSE }

    { 1100528602;1;Group  ;
                CaptionML=[DEU=Kopieren in;
                           ENU=Copy to;
                           NLD=Kopiâren naar;
                           NOR=Kopier til;
                           SVE=Kopiera till];
                GroupType=Group }

    { 11  ;2   ;Field     ;
                CaptionML=[DEU=Stamminspektionsliste;
                           ENU=Base Inspection List;
                           NLD=Stam inspectielijst;
                           NOR=Basisinspeksjonsliste;
                           SVE=Basinspektionslista];
                NotBlank=Yes;
                SourceExpr=ToBaseInspectionList;
                CaptionClass=gPlantInspectionVersion.FIELDCAPTION("Base Inspection List");
                OnValidate=BEGIN
                             ValidateToBaseInspectionList;
                           END;
                            }

    { 12  ;2   ;Field     ;
                CaptionML=[NOR=Beskrivelse av basisinspesjonsliste;
                           SVE=Beskrivning basinspektionslista];
                NotBlank=Yes;
                SourceExpr=ToBaseInspectionListDescr;
                CaptionClass=gPlantInspectionVersion.FIELDCAPTION("Inspection List Description");
                Editable=ToBaseInspectionListDescrEditable;
                OnValidate=BEGIN
                             ValidateToBaseInspectionListDescr;
                           END;
                            }

    { 1100525000;2;Field  ;
                CaptionML=[NOR=Indeksdato;
                           SVE=Indexdatum];
                NotBlank=Yes;
                SourceExpr=ToIndexDate;
                CaptionClass=gPlantInspectionVersion.FIELDCAPTION("Index Date");
                OnValidate=BEGIN
                             ValidateToIndexDate;
                           END;
                            }

  }
  CODE
  {
    VAR
      gPlantInspectionVersion@1100529000 : Record 11072682;
      FromBaseInspectionList@11012029 : Code[20];
      ToBaseInspectionList@11012028 : Code[20];
      FromBaseInspectionListDescr@11012005 : Text[50];
      ToBaseInspectionListDescr@11012004 : Text[50];
      ReferenceDate@1100528600 : Date;
      FromIndexDate@1100529009 : Date;
      ToIndexDate@1100525000 : Date;
      CloseActionOK@1100528400 : Boolean;
      Text000a@1100529008 : TextConst 'DEU=von;ENU=from;NLD=van';
      Text000b@1100529007 : TextConst 'DEU=zu;ENU=to;NLD=naar';
      Text001@1100529006 : TextConst 'DEU=%1 (%2) ''%3'' bereits vorhanden;ENU=%1 (%2) ''%3'' already exists;NLD=%1 (%2) ''%3'' bestaat reeds';
      Text002@1100529005 : TextConst 'DEU=%1 (%2) ''%3'' nicht vorhanden;ENU=%1 (%2) ''%3'' does not exist;NLD=%1 (%2) ''%3'' bestaat niet';
      Text003@1100529004 : TextConst 'DEU=Bei %1 (%2) ''%3'' wurde kein %5 fÅr Stichtag ''%4 gefunden, %5 (%2) kann nicht bestimmt werden;ENU=On %1 (%2) ''%3'' no %5 for Reference Date ''%4, %5 (%2) can be determined;NLD=Bij %1 (%2) ''%3'' geen %5 gevonden voor Peildatum ''%4, %5 (%2) kan niet bepaald worden';
      Text004@1100529003 : TextConst 'DEU=%1 (%2) darf nicht leer sein;ENU=%1 (%2) may not be empty;NLD=%1 (%2) mag niet leeg zijn';
      Text005@1100529002 : TextConst 'DEU=%1 (%2) ''%3'' muss grîsser zein als %1 (%4) ''%5'';ENU=%1 (%2) ''%3'' must be greater then %1 (%4) ''%5'';NLD=%1 (%2) ''%3'' moet groter zijn dan %1 (%4) ''%5''';
      Text006@1100529001 : TextConst 'DEU=%1 kopiert;ENU=%1 copied;NLD=%1 gekopieerd';
      ToBaseInspectionListDescrEditable@1100529010 : Boolean INDATASET;

    LOCAL PROCEDURE ValidateFromBaseInspectionList@1100529001();
    VAR
      BasePlantInspectionList@1100529000 : Record 11072680;
    BEGIN
      IF NOT BasePlantInspectionList.GET(FromBaseInspectionList) THEN
        ERROR(Text002, gPlantInspectionVersion.FIELDCAPTION("Base Inspection List"), Text000a, FromBaseInspectionList);
      FromBaseInspectionListDescr := BasePlantInspectionList.Description;
      IF FromBaseInspectionList = ToBaseInspectionList THEN
        ToBaseInspectionListDescr := FromBaseInspectionListDescr;
      ToBaseInspectionListDescrEditable := (FromBaseInspectionList <> ToBaseInspectionList);
      DetermineFromIndexDate;
    END;

    LOCAL PROCEDURE ValidateReferenceDate@1100529003();
    BEGIN
      DetermineFromIndexDate;
      IF FromIndexDate = 0D THEN BEGIN
        ERROR(Text003,
          gPlantInspectionVersion.FIELDCAPTION("Base Inspection List"), Text000a, FromBaseInspectionList,
          ReferenceDate, gPlantInspectionVersion.FIELDCAPTION("Index Date"));
      END;
    END;

    LOCAL PROCEDURE ValidateToBaseInspectionList@1100529000();
    VAR
      BasePlantInspectionList@1100528600 : Record 11072680;
    BEGIN
      IF FromBaseInspectionList <> ToBaseInspectionList THEN BEGIN
        IF BasePlantInspectionList.GET(ToBaseInspectionList) THEN
          ERROR(Text001, gPlantInspectionVersion.FIELDCAPTION("Base Inspection List"), Text000b, ToBaseInspectionList);
      END;
      IF FromBaseInspectionList = ToBaseInspectionList THEN
        ToBaseInspectionListDescr := FromBaseInspectionListDescr;
      ToBaseInspectionListDescrEditable := (FromBaseInspectionList <> ToBaseInspectionList);
    END;

    LOCAL PROCEDURE ValidateToBaseInspectionListDescr@1100529005();
    BEGIN
      IF (FromBaseInspectionList <> ToBaseInspectionList) AND (ToBaseInspectionListDescr = '')THEN
        ERROR(Text004, gPlantInspectionVersion.FIELDCAPTION("Inspection List Description"), Text000b);
    END;

    LOCAL PROCEDURE ValidateToIndexDate@1100529004();
    BEGIN
      IF ToIndexDate = 0D THEN
        ERROR(Text004, gPlantInspectionVersion.FIELDCAPTION("Index Date"), Text000b);
      IF (FromBaseInspectionList = ToBaseInspectionList) AND (ToIndexDate <= FromIndexDate) THEN
        ERROR(Text005, gPlantInspectionVersion.FIELDCAPTION("Index Date"), Text000b, ToIndexDate, Text000a, FromIndexDate);
    END;

    LOCAL PROCEDURE DetermineFromIndexDate@1100529002();
    VAR
      PlantInspectionVersion@1100529000 : Record 11072682;
    BEGIN
      FromIndexDate := 0D;
      IF (FromBaseInspectionList <> '') AND (ReferenceDate <> 0D) THEN BEGIN
        PlantInspectionVersion.SETRANGE(Level, PlantInspectionVersion.Level::"Base Inspection List");
        PlantInspectionVersion.SETRANGE("Base Inspection List", FromBaseInspectionList);
        PlantInspectionVersion.SETFILTER("Index Date", '<=%1', ReferenceDate);
        IF PlantInspectionVersion.FINDLAST THEN
          FromIndexDate := PlantInspectionVersion."Index Date";
      END;
    END;

    PROCEDURE StdDialogRunModal@1100528404();
    BEGIN
      IF StdDialogConfirm THEN
        StdDialogDoAction;
    END;

    LOCAL PROCEDURE StdDialogConfirm@1100528401() : Boolean;
    BEGIN
      CurrPage.RUNMODAL;
      EXIT(CloseActionOK);
    END;

    LOCAL PROCEDURE StdDialogDoAction@1100528402();
    BEGIN
      StartProcess;
    END;

    LOCAL PROCEDURE StartProcess@1100485002();
    VAR
      PlantMaintenanceManagement@1100485001 : Codeunit 11012872;
    BEGIN
      IF PlantMaintenanceManagement.CopyBaseInspectionList(
        FromBaseInspectionList, FromIndexDate, ToBaseInspectionList, ToBaseInspectionListDescr, ToIndexDate)
      THEN
        MESSAGE(Text006, gPlantInspectionVersion.FIELDCAPTION("Base Inspection List"));
    END;

    BEGIN
    END.
  }
}

