OBJECT Table 11229774 Sales Price by Estimate
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "Estimate Type" = "Estimate Type"::Recipe THEN BEGIN
                 TESTFIELD(Recipe);
                 TESTFIELD("House Model", '');
               END;
               IF "Estimate Type" = "Estimate Type"::HouseModel THEN BEGIN
                 TESTFIELD("House Model");
                 TESTFIELD(Recipe, '');
               END;
             END;

    CaptionML=ENU=Sales Price by Estimate;
    LookupPageID=Page11229892;
  }
  FIELDS
  {
    { 10  ;   ;Estimate No.        ;Code20        ;TableRelation=Estimate.No.;
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Estimate No.;
                                                              SVE=Kalkylnr] }
    { 30  ;   ;Estimate Type       ;Option        ;OnValidate=VAR
                                                                EstimateSetup@1100528900 : Record 11012150;
                                                              BEGIN
                                                                IF "Estimate Type" <> xRec."Estimate Type" THEN BEGIN
                                                                  IF "Estimate Type" = "Estimate Type"::Recipe THEN BEGIN
                                                                    "House Model" := '';
                                                                    EstimateSetup.GET;
                                                                    "Recipe Group":= EstimateSetup."Recipe Group";
                                                                  END;
                                                                  IF "Estimate Type" = "Estimate Type"::HouseModel THEN BEGIN
                                                                    "Recipe Group" := '';
                                                                    Recipe := '';
                                                                  END;
                                                                END;
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Estimate Type;
                                                   OptionCaptionML=ENU=House Model,Recipe;
                                                   OptionString=HouseModel,Recipe }
    { 35  ;   ;House Model         ;Code20        ;TableRelation="House Model".Model WHERE (Project No.=FILTER(''));
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=House Model;
                                                              SVE=Husmodell] }
    { 40  ;   ;Recipe              ;Code20        ;TableRelation="Base Recipe".No. WHERE (Recipe Group=FIELD(Recipe Group));
                                                   OnValidate=VAR
                                                                BaseRecipe@1100528900 : Record 11020581;
                                                                EstimateLine@1100528901 : Record 11072072;
                                                              BEGIN
                                                                IF NOT BaseRecipe.GET("Recipe Group", Recipe) THEN BEGIN
                                                                  EstimateLine.SETRANGE("Estimate No.", "Estimate No.");
                                                                  EstimateLine.SETRANGE("Recipe Group", "Recipe Group");
                                                                  EstimateLine.SETRANGE(Recipe, Recipe);
                                                                  EstimateLine.FINDFIRST;
                                                                END;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Recipe;
                                                              SVE=Mall] }
    { 45  ;   ;Recipe Group        ;Code10        ;TableRelation="Recipe Group";
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Recipe Group;
                                                              SVE=Malltyp] }
    { 50  ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                IF ("Starting Date" > "Ending Date") AND ("Ending Date" <> 0D) THEN
                                                                  ERROR(Text000,FIELDCAPTION("Starting Date"),FIELDCAPTION("Ending Date"));
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Starting Date;
                                                              SVE=Startdatum] }
    { 60  ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                VALIDATE("Starting Date");
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Ending Date;
                                                              SVE=Slutdatum] }
    { 70  ;   ;Price               ;Decimal       ;DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Price;
                                                              SVE=Pris];
                                                   MinValue=0;
                                                   AutoFormatType=2 }
  }
  KEYS
  {
    {    ;Estimate No.,Estimate Type,House Model,Recipe Group,Recipe,Starting Date;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528500 : TextConst 'ENU=%1 cannot be after %2;SVE=%1 kan inte vara efter %2';
      Text002@1100528900 : TextConst 'ENU=Sales Price updated in %1 Sub-Estimates (for House Model) and %2 Estimate Lines (for Recipe)';

    PROCEDURE OnAssistEditRecipe@1100528900();
    VAR
      EstimateLine@1100528900 : Record 11072072;
      BaseRecipe@1100528902 : Record 11020581;
      TmpBaseRecipe@1100528901 : TEMPORARY Record 11020581;
    BEGIN
      TESTFIELD("Estimate Type", "Estimate Type"::Recipe);
      EstimateLine.SETRANGE("Estimate No.", "Estimate No.");
      EstimateLine.SETFILTER(Recipe, '<>%1', '');
      IF EstimateLine.FINDSET THEN BEGIN
        REPEAT
          IF NOT BaseRecipe.GET(EstimateLine."Recipe Group", EstimateLine.Recipe) THEN BEGIN
            IF NOT TmpBaseRecipe.GET(EstimateLine."Recipe Group", EstimateLine.Recipe) THEN BEGIN
              TmpBaseRecipe."Recipe Group" := EstimateLine."Recipe Group";
              TmpBaseRecipe."No." := EstimateLine.Recipe;
              TmpBaseRecipe.INSERT;
              EstimateLine.MARK := TRUE;
            END;
          END;
        UNTIL EstimateLine.NEXT = 0;
        EstimateLine.MARKEDONLY(TRUE);
      END;
      IF PAGE.RUNMODAL(PAGE::"Estimate Line List", EstimateLine) = ACTION::LookupOK THEN BEGIN
        "Recipe Group":= EstimateLine."Recipe Group";
        Recipe:= EstimateLine.Recipe;
      END;
    END;

    PROCEDURE UpdateSalesPriceEstimate@1100528901();
    VAR
      SalesPricebyEstimate@1100528902 : Record 11229774;
      EstimateLine@1100528901 : Record 11072072;
      SubEstimate@1100528900 : Record 11072074;
      NumEstimateLine@1100528903 : Integer;
      NumSubEstimate@1100528904 : Integer;
    BEGIN
      SalesPricebyEstimate.SETRANGE("Estimate No.", "Estimate No.");
      IF SalesPricebyEstimate.FINDSET THEN BEGIN
        REPEAT
          IF SalesPricebyEstimate."Estimate Type" = SalesPricebyEstimate."Estimate Type"::HouseModel THEN BEGIN
            SubEstimate.SETRANGE("Estimate No.", "Estimate No.");
            SubEstimate.SETRANGE("House Model", SalesPricebyEstimate."House Model");
            IF SubEstimate.FINDSET THEN BEGIN
              REPEAT
                SubEstimate.VALIDATE("Sales Price (House Model)", SalesPricebyEstimate.Price);
                SubEstimate.MODIFY;
                NumSubEstimate := NumSubEstimate + 1;
              UNTIL SubEstimate.NEXT = 0;
            END;
          END;
          IF SalesPricebyEstimate."Estimate Type" = SalesPricebyEstimate."Estimate Type"::Recipe THEN BEGIN
            EstimateLine.SETRANGE("Estimate No.", "Estimate No.");
            EstimateLine.SETRANGE("Recipe Group", SalesPricebyEstimate."Recipe Group");
            EstimateLine.SETRANGE(Recipe, SalesPricebyEstimate.Recipe);
            IF EstimateLine.FINDSET THEN BEGIN
              REPEAT
                EstimateLine.VALIDATE("Sales Price (Recipe)", SalesPricebyEstimate.Price);
                EstimateLine.MODIFY;
                NumEstimateLine := NumEstimateLine + 1;
              UNTIL EstimateLine.NEXT = 0;
            END;
          END;
        UNTIL SalesPricebyEstimate.NEXT = 0;
        MESSAGE(Text002, NumSubEstimate, NumEstimateLine);
      END;
    END;

    BEGIN
    END.
  }
}

