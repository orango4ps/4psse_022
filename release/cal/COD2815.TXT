OBJECT Codeunit 2815 Native - Coupons
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      CouponDoesntExistErr@1003 : TextConst '@@@="%1=claimId value;%2=graphContactId value.";ENU=The coupon with "claimId" : "%1"  doesn''t exist for this Customer.;NOR=Kupongen med claimId %1 finnes ikke for denne kunden.';
      CouponAlreadyAppliedErr@1000 : TextConst 'ENU=The coupon is already applied in another invoice.;NOR=Kupongen er allerede brukt i en annen faktura.';
      CouponInvalidErr@1001 : TextConst 'ENU=The status on the coupon is not valid.;NOR=Statusen for kupongen er ugyldig.';

    [External]
    PROCEDURE CheckIfCouponCanBeUsed@7(O365CouponClaimDocLink@1000 : Record 2116);
    VAR
      O365CouponClaim@1001 : Record 2115;
      O365PostedCouponClaim@1002 : Record 2117;
    BEGIN
      O365CouponClaim.GET(O365CouponClaimDocLink."Claim ID",O365CouponClaimDocLink."Graph Contact ID");

      IF NOT O365CouponClaim."Is Valid" THEN
        ERROR(CouponInvalidErr);

      IF O365CouponClaim.Usage = O365CouponClaim.Usage::oneTime THEN BEGIN
        O365PostedCouponClaim.SETRANGE("Claim ID",O365CouponClaimDocLink."Claim ID");
        O365PostedCouponClaim.SETRANGE("Graph Contact ID",O365CouponClaimDocLink."Graph Contact ID");
        IF NOT O365PostedCouponClaim.ISEMPTY THEN
          ERROR(CouponAlreadyAppliedErr);
      END;
    END;

    [External]
    PROCEDURE CheckThatCouponCanBeAppliedToInvoice@2(O365CouponClaimDocLink@1002 : Record 2116);
    VAR
      DummySalesHeader@1005 : Record 36;
      O365CouponClaim@1006 : Record 2115;
    BEGIN
      IF (O365CouponClaimDocLink."Document Type" <> DummySalesHeader."Document Type"::Invoice) AND
         (O365CouponClaimDocLink."Document Type" <> DummySalesHeader."Document Type"::Quote)
      THEN
        EXIT;

      IF NOT O365CouponClaim.GET(O365CouponClaimDocLink."Claim ID",O365CouponClaimDocLink."Graph Contact ID") THEN
        ERROR(CouponDoesntExistErr,O365CouponClaimDocLink."Claim ID",O365CouponClaimDocLink."Graph Contact ID");
    END;

    [External]
    PROCEDURE WriteCouponsJSON@3(DocumentType@1000 : Option;DocumentNo@1001 : Code[20];Posted@1002 : Boolean) : Text;
    VAR
      NativeEDMTypes@1003 : Codeunit 2801;
    BEGIN
      IF Posted THEN
        EXIT(NativeEDMTypes.WritePostedCouponsJSON(DocumentNo));

      EXIT(NativeEDMTypes.WriteCouponsJSON(DocumentType,DocumentNo));
    END;

    BEGIN
    END.
  }
}

