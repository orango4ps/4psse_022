OBJECT Report 11128069 Total IN
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=PE7.02.01 (12047963),4PSSE;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Total IN;
               SVE=Total IN];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  gTextFile.TEXTMODE := TRUE;
                  IF gFileMgt.CanRunDotNetOnClient THEN
                    gServerFileName := gFileMgt.UploadFileSilent(gFileName)
                  ELSE
                    gServerFileName := gFileMgt.UploadFile(PEB004, gFileName);

                  IF gServerFileName = '' THEN
                    CurrReport.QUIT;

                  gTextFile.OPEN(gServerFileName,TEXTENCODING::MSDos);
                END;

    OnPostReport=BEGIN
                   gTextFile.CLOSE;
                   ERASE(gServerFileName);
                 END;

  }
  DATASET
  {
    { 5444;    ;DataItem;                    ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number);
               OnPreDataItem=BEGIN
                               gSalesRecSetup.GET;

                               gGenJnlBatch.GET(gGenJnlLine."Journal Template Name",gGenJnlLine."Journal Batch Name");
                               gGenJnlTemplate.GET(gGenJnlLine."Journal Template Name");
                               gGenJnlBatch.TESTFIELD("No. Series");
                               gDocNo := gNoSeriesMgt.GetNextNo(gGenJnlBatch."No. Series",WORKDATE,FALSE);
                               gGLSetup.GET;

                               gGenJnlLine.RESET;
                               gGenJnlLine.SETRANGE("Journal Template Name",gGenJnlLine."Journal Template Name");
                               gGenJnlLine.SETRANGE("Journal Batch Name",gGenJnlLine."Journal Batch Name");
                               gGenJnlLine.DELETEALL;

                               gtDocNoRec.RESET;
                               gtDocNoRec.DELETEALL;

                               DeleteInfoLine;

                               gW.OPEN('@1@@@@@@@@@@@@@@@@@@@@@@@@@');
                             END;

               OnAfterGetRecord=VAR
                                  lType@1100570000 : Text[2];
                                  lAmount@1100570001 : Decimal;
                                BEGIN
                                  IF gTextFile.LEN = gTextFile.POS THEN
                                    CurrReport.BREAK;
                                  gW.UPDATE(1,ROUND(gTextFile.POS/gTextFile.LEN*10000,1.0));
                                  gTextFile.READ(gTextLine);

                                  lType := COPYSTR(gTextLine,1,2);

                                  CASE lType OF
                                    '10':
                                      BEGIN // STARTPOST
                                        EVALUATE(gPostingDate,COPYSTR(gTextLine,42,8));
                                        gCurrencyCode := COPYSTR(gTextLine,39,3);
                                        IF gBankAccountNo = '' THEN BEGIN
                                          gCaseRecieptAccmapping.GET(gCaseRecieptAccmapping."File Type"::"Total IN",gCurrencyCode);
                                          gCaseRecieptAccmapping.TESTFIELD("Account Type");
                                          gCaseRecieptAccmapping.TESTFIELD("Account No.");
                                          CLEAR("gTotalAmount(LCY)");
                                        END;
                                      END;
                                    '20','25': // Start post
                                      BEGIN
                                        InsertGlLines(gPostingDate);
                                      END;
                                    '90': // Slutpost
                                      BEGIN
                                        InsertGlLines(gPostingDate);

                                        lAmount := Txt2Dec(COPYSTR(gTextLine,11,17));

                                        InitGlLine(gPostingDate);
                                        IF gBankAccountNo = '' THEN BEGIN
                                          CASE gCaseRecieptAccmapping."Account Type" OF
                                            gCaseRecieptAccmapping."Account Type"::"G/L Account":
                                              gGenJnlLine.VALIDATE("Account Type", gGenJnlLine."Account Type"::"G/L Account");
                                            gCaseRecieptAccmapping."Account Type"::"Bank Account":
                                              gGenJnlLine.VALIDATE("Account Type", gGenJnlLine."Account Type"::"Bank Account");
                                          END;
                                          gGenJnlLine.VALIDATE("Account No.", gCaseRecieptAccmapping."Account No.");
                                        END ELSE BEGIN
                                          CASE gAccountType OF
                                            gAccountType::"G/L Account":
                                              gGenJnlLine.VALIDATE("Account Type", gGenJnlLine."Account Type"::"G/L Account");
                                            gAccountType::"Bank Account":
                                              gGenJnlLine.VALIDATE("Account Type", gGenJnlLine."Account Type"::"Bank Account");
                                          END;
                                          gGenJnlLine.VALIDATE("Account No.", gBankAccountNo);
                                        END;

                                  //>>170822 ITERO.DL pure274678
                                        //gGenJnlLine.VALIDATE("Currency Code",SetCurrencyCode(gCurrencyCode));
                                        gGenJnlLine.VALIDATE("Currency Code",gCurrencyCode);
                                  //<<170822 ITERO.DL pure274678
                                        gGenJnlLine.VALIDATE(Amount,lAmount);
                                        IF gGenJnlLine."Currency Code" <> '' THEN
                                          gGenJnlLine.VALIDATE("Amount (LCY)",-"gTotalAmount(LCY)");

                                        gGenJnlLine.VALIDATE(Description,PEB002);
                                        gGenJnlLine.INSERT;
                                      END;
                                  END;
                                  InsertInfoLineTemp(lType,COPYSTR(gTextLine,3));
                                END;

               OnPostDataItem=BEGIN
                                gW.CLOSE;
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   gFileNameVisible := gFileMgt.CanRunDotNetOnClient;
                 END;

    }
    CONTROLS
    {
      { 1000000000;;Container;
                  Name=Cont;
                  ContainerType=ContentArea }

      { 1100570002;1;Group  ;
                  CaptionML=[ENU=Options;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 12047959;2;Field    ;
                  CaptionML=[ENU=File Name;
                             NOR=Filnavn;
                             SVE=Filnamn];
                  SourceExpr=gFileName;
                  Visible=gFileNameVisible;
                  OnAssistEdit=BEGIN
                                 gFileName := gFileMgt.OpenFileDialog(PEB004,gFileName,PEB003);
                               END;
                                }

      { 1000000002;2;Field  ;
                  CaptionML=[ENU=Account Type;
                             NOR=Kontotype;
                             SVE=Kontotyp];
                  OptionCaptionML=[ENU=G/L Account,,,Bank Account;
                                   NOR=Hovedbokskonto,,,Bankkonto;
                                   SVE=Redov.konto,,,Bankkonto];
                  SourceExpr=gAccountType;
                  OnValidate=BEGIN
                               CLEAR(gBankAccountNo);
                             END;
                              }

      { 1100570000;2;Field  ;
                  CaptionML=[ENU=Account No;
                             NOR=Nr;
                             SVE=Kontonr];
                  SourceExpr=gBankAccountNo;
                  OnValidate=BEGIN
                               IF gBankAccountNo = '' THEN
                                 EXIT;
                               CASE gAccountType OF
                                 gAccountType::"G/L Account":
                                   BEGIN
                                     gGLAccount.GET(gBankAccountNo);
                                   END;
                                 gAccountType::"Bank Account":
                                   BEGIN
                                     gBankAccount.GET(gBankAccountNo);
                                   END;
                               END;
                             END;

                  OnLookup=BEGIN
                             CASE gAccountType OF
                               gAccountType::"G/L Account":
                                 BEGIN
                                   IF PAGE.RUNMODAL(0,gGLAccount) = ACTION::LookupOK THEN
                                     gBankAccountNo := gGLAccount."No.";
                                 END;
                               gAccountType::"Bank Account":
                                 BEGIN
                                   IF PAGE.RUNMODAL(0,gBankAccount) = ACTION::LookupOK THEN
                                     gBankAccountNo := gBankAccount."No.";
                                 END;
                             END;
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      gFileName@12047960 : Text;
      gTextFile@1000 : File;
      gTextLine@1002 : Text[250];
      gDocNo@1005 : Code[20];
      gJnlTemplateName@1014 : Code[10];
      gJnlBatchName@1015 : Code[10];
      gGenJnlBatch@1016 : Record 232;
      PEB001@1021 : TextConst 'ENU=Does not Exist or is Paid;SVE=Finns inte eller „r betald';
      PEB002@1022 : TextConst 'ENU=Total IN Payment;SVE=Total IN betalning';
      gGLSetup@1000000002 : Record 98;
      gGenJnlTemplate@1000000007 : Record 80;
      PEB003@1070000 : TextConst 'ENU=All files|*.*|TXT-files (*.txt)|*.txt|WRI-files (*.wri)|*.wri;SVE=Alla filer|*.*|TXT-filer (*.txt)|*.txt|WRI-filer (*.wri)|*.wri';
      PEB004@1000000008 : TextConst 'ENU=Import Total IN;SVE=Import Total In';
      gSalesRecSetup@1000000014 : Record 311;
      gBaseCodeUnits@1000000015 : Codeunit 11128082;
      gW@1070002 : Dialog;
      gGenJnlLine@1070007 : Record 81;
      PEB005@1000000006 : TextConst 'ENU=The Sum of marked Lines Differ with %1;SVE=Summa m„rkta rader avviker med %1';
      gBankAccountNo@1100570003 : Code[20];
      gGLAccount@1100570002 : Record 15;
      gBankAccount@1100570001 : Record 270;
      gAccountType@1100570000 : 'G/L Account,Customer,Vendor,Bank Account,Fixed Asset';
      gCaseRecieptAccmapping@1100570005 : Record 11128070;
      gtTextLineEntry@1100570006 : TEMPORARY Record 11128080;
      gNoSeriesMgt@1100570007 : Codeunit 396;
      gPostingDate@1100570008 : Date;
      gCurrencyCode@1100570004 : Code[10];
      gtGLLines@1100570010 : TEMPORARY Record 81;
      "gTotalAmount(LCY)"@1000000003 : Decimal;
      PEB006@1000000001 : TextConst 'ENU=Amount Difference with %1;SVE=Beloppsdifferens p† %1';
      gtDocNoRec@1000000004 : TEMPORARY Record 11128072;
      PEB007@1000000005 : TextConst 'ENU=Paid more than once in this file;SVE=Dubbelbetald i samma fil';
      PEB008@1000000012 : TextConst 'ENU=Customer No. Diff.;SVE=Kundnr diff.';
      gFileMgt@12047958 : Codeunit 419;
      gServerFileName@12047959 : Text;
      gBaseTables@12047961 : Codeunit 11128080;
      gAppliesToDocNo@12047962 : Code[20];
      gFileNameVisible@12047963 : Boolean INDATASET;
      PEB001a@1100285100 : TextConst 'ENU=%1 Does not Exist or is Paid;SVE=%1 Finns inte eller „r betald';

    PROCEDURE SetGenJnlLine@1(NewGLJnlLine@1000 : Record 81);
    BEGIN
      gGenJnlLine := NewGLJnlLine;
      gJnlTemplateName := gGenJnlLine."Journal Template Name";
      gJnlBatchName := gGenJnlLine."Journal Batch Name";
    END;

    PROCEDURE InvCredExist@2(VAR pApplyToDocNo@1000000000 : Code[30];VAR pvLastCustomerNo@1000000002 : Code[20];VAR pvCustomerNoError@1000000001 : Boolean) : Boolean;
    VAR
      CustLedgerEntry@1000 : Record 21;
    BEGIN
      IF pApplyToDocNo = '' THEN
        EXIT(FALSE);
      CustLedgerEntry.SETCURRENTKEY("Document No.");
      CustLedgerEntry.SETRANGE("Document No.",pApplyToDocNo);
      CustLedgerEntry.SETRANGE(Open,TRUE);
      IF CustLedgerEntry.FIND('-') THEN BEGIN
        IF NOT pvCustomerNoError AND (pvLastCustomerNo <> '') THEN
          pvCustomerNoError := pvLastCustomerNo <> CustLedgerEntry."Customer No.";
        pvLastCustomerNo := CustLedgerEntry."Customer No.";
        EXIT(TRUE);
      //>>4PSSE
      END;
      CustLedgerEntry.SETRANGE("Document No.");
      CustLedgerEntry.SETCURRENTKEY("External Document No.");
      CustLedgerEntry.SETRANGE("External Document No.", pApplyToDocNo);
      IF CustLedgerEntry.FIND('-') THEN BEGIN
        IF NOT pvCustomerNoError AND (pvLastCustomerNo <> '') THEN
          pvCustomerNoError := pvLastCustomerNo <> CustLedgerEntry."Customer No.";
        pvLastCustomerNo := CustLedgerEntry."Customer No.";
        pApplyToDocNo := CustLedgerEntry."Document No.";
        EXIT(TRUE);
      END;
      // END ELSE
      //<<4PSSE
        EXIT(FALSE);
    END;

    PROCEDURE Txt2Dec@1000000006(pText@1000000000 : Text[30]) : Decimal;
    VAR
      TempAmount@1000000001 : Decimal;
    BEGIN
      EVALUATE(TempAmount,pText);
      EXIT(ROUND(TempAmount / 100));
    END;

    PROCEDURE SetCurrencyCode@1100570000(PCode@1100570000 : Code[10]) : Code[10];
    BEGIN
      gGLSetup.TESTFIELD("LCY Code");
      IF PCode = gGLSetup."LCY Code" THEN
        EXIT('')
      ELSE
        EXIT(PCode);
    END;

    PROCEDURE InsertInfoLineTemp@1100570002(pType@1100570000 : Text[2];pText@1000000000 : Text[1000]);
    BEGIN
      IF NOT (pType IN ['20'..'70']) THEN
        EXIT;
      gtTextLineEntry.INIT;
      gtTextLineEntry."Line No." += 10000;
      gtTextLineEntry.Description := pText;
      gtTextLineEntry."Line Code" := pType;
      gtTextLineEntry."File Type" := gtTextLineEntry."File Type"::"Total IN";
      gtTextLineEntry.INSERT;
    END;

    PROCEDURE CopyInfoLine@1100570011();
    VAR
      TextLineEntry@1100570000 : Record 11128080;
    BEGIN
      gtTextLineEntry.RESET;
      IF gtTextLineEntry.FIND('-') THEN REPEAT
        TextLineEntry := gtTextLineEntry;
        TextLineEntry."Journal Template Name" := gGenJnlLine."Journal Template Name";
        TextLineEntry."Journal Batch Name" := gGenJnlLine."Journal Batch Name";
        TextLineEntry."Journal Line No." := gGenJnlLine."Line No.";
        TextLineEntry.INSERT;
      UNTIL gtTextLineEntry.NEXT = 0;
      gtGLLines := gGenJnlLine;
      gtGLLines.INSERT;
    END;

    PROCEDURE DeleteInfoLine@1100570014();
    VAR
      TextLineEntry@1100570000 : Record 11128080;
    BEGIN
      TextLineEntry.RESET;
      TextLineEntry.SETRANGE("Journal Template Name",gGenJnlLine."Journal Template Name");
      TextLineEntry.SETRANGE("Journal Batch Name",gGenJnlLine."Journal Batch Name");
      TextLineEntry.DELETEALL;
    END;

    PROCEDURE InsertGlLines@1100570008(pPostingDate@1100570005 : Date);
    VAR
      Txt@1100570000 : Text[1024];
      StartPos@1100570001 : Integer;
      ApplyToDocNo@1100570002 : Code[30];
      tDocNoRec@1100570003 : TEMPORARY Record 11128072;
      RestAmount@1100570004 : Decimal;
      Amount@1100570006 : Decimal;
      "Amount(LCY)"@1100570007 : Decimal;
      CurrencyCode@1100570008 : Code[10];
      CurrencyRate@1000000000 : Decimal;
      IsCurrency@1000000001 : Boolean;
      OneLine@1000000002 : Boolean;
      LastCustomerNo@1000000004 : Code[20];
      CustomerNoError@1000000003 : Boolean;
      ROTRUTInformation@1100285100 : Record 11128101;
      Currency@1100285101 : Record 4;
      PayerName@1100285102 : Text;
    BEGIN
      StartPos := 1;

      tDocNoRec.RESET;
      tDocNoRec.DELETEALL;

      gtTextLineEntry.RESET;
      IF NOT gtTextLineEntry.FINDSET THEN
        EXIT;
      REPEAT
        Txt := '';
        CASE gtTextLineEntry."Line Code" OF
          //Betalning/Avdrag
          '20','25':
            BEGIN
             Txt := COPYSTR(gtTextLineEntry.Description,1,35);
             "Amount(LCY)" := Txt2Dec(COPYSTR(gtTextLineEntry.Description,36,15));

             // Kredit
             IF gtTextLineEntry."Line Code" = '25' THEN
               "Amount(LCY)" := -"Amount(LCY)";
            END;
          //Meddelande / Extra ref.nr
          '40','30': Txt := COPYSTR(gtTextLineEntry.Description,1,70);
          //Utlandsbetalning
          '70':
            BEGIN
              Amount := Txt2Dec(COPYSTR(gtTextLineEntry.Description,37,15));
              CurrencyCode := COPYSTR(gtTextLineEntry.Description,52,3);
              CurrencyRate := Txt2Dec(COPYSTR(gtTextLineEntry.Description,55,12))/100;
              IsCurrency := TRUE;
            END;
      //>>180208
          '50': PayerName := DELCHR(COPYSTR(gtTextLineEntry.Description,1,25),'<>',' ');  //Betalningsavs„ndare
          '61': PayerName := DELCHR(COPYSTR(gtTextLineEntry.Description,1,25),'<>',' ');  //Avs„ndarkonto
      //<<180208
          END;
        // Leta efter ett eller fler giltiga fakt/kredit-nr i textstr„ngen
        IF Txt <> '' THEN BEGIN
          REPEAT
            Txt := COPYSTR(Txt,StartPos);
            ApplyToDocNo := gBaseCodeUnits.GetInvNoFromString(Txt);
            IF InvCredExist(ApplyToDocNo,LastCustomerNo,CustomerNoError) THEN BEGIN
              // Undvika dubletter f”r den h„r inbet.
              tDocNoRec.INIT;
              tDocNoRec."No." := ApplyToDocNo;
              IF tDocNoRec.INSERT THEN;
            END;
            StartPos := STRPOS(Txt,ApplyToDocNo) + STRLEN(ApplyToDocNo)+1;
          UNTIL ApplyToDocNo = '';
        END;
      UNTIL gtTextLineEntry.NEXT = 0;

      IF NOT IsCurrency THEN
        Amount := "Amount(LCY)";

      RestAmount := Amount;

      tDocNoRec.RESET;
      IF tDocNoRec.FIND('-') THEN BEGIN
        OneLine := tDocNoRec.COUNT = 1;
        // Infoga rader i Journalen
        REPEAT
          InitGlLine(pPostingDate);
          gGenJnlLine.VALIDATE("Account Type", gGenJnlLine."Account Type"::Customer);

          gAppliesToDocNo := tDocNoRec."No.";

          //4PSSE pure274938
          gGenJnlLine.DontWarnValidateAppliesToDocNo;

          gGenJnlLine.VALIDATE("Applies-to Doc. No.",gAppliesToDocNo);
          gGenJnlLine."Applies-to Doc. No." := '';

          // Hittat olika kundnr
          IF CustomerNoError THEN
            SetTextForInfo(gGenJnlLine,PEB008);

          // Markerar dubletter i filen.
          gtDocNoRec.INIT;
          gtDocNoRec."No." := tDocNoRec."No.";
          IF NOT gtDocNoRec.INSERT THEN
            SetTextForInfo(gGenJnlLine,PEB007);

      //>>4PSSE, 180109
          IF OneLine AND (gGenJnlLine.Amount <> -Amount) THEN BEGIN
            ROTRUTInformation.RESET;
            ROTRUTInformation.SETRANGE(Type,ROTRUTInformation.Type::"Posted Invoice");
            ROTRUTInformation.SETRANGE("Invoice No.",gAppliesToDocNo);
            IF ROTRUTInformation.FINDSET THEN
              ROTRUTInformation.CALCSUMS(Amount);

            IF gGenJnlLine."Currency Code" = '' THEN
              Currency.InitRoundingPrecision
            ELSE BEGIN
              Currency.GET(gGenJnlLine."Currency Code");
              Currency.TESTFIELD("Amount Rounding Precision");
            END;
            gGenJnlLine.VALIDATE(Amount,ROUND(gGenJnlLine.Amount + ROTRUTInformation.Amount,Currency."Amount Rounding Precision",Currency.InvoiceRoundingDirection));
          END;
      //<<4PSSE

          //4PSSE
      //>>RFC1127
      //    gGenJnlLine."Received amount" := Amount;
          gGenJnlLine.VALIDATE("Received amount", Amount);
      //<<RFC1127

          // Om det „r en rad s† tas beloppet ur filen
          IF OneLine AND (gGenJnlLine.Amount <> -Amount) THEN BEGIN
            SetTextForInfo(gGenJnlLine,STRSUBSTNO(PEB006,gGenJnlLine.Amount + Amount));
      //      gGenJnlLine.Comment := STRSUBSTNO(PEB006,gGenJnlLine.Amount + Amount); //4PSSE
            gGenJnlLine.VALIDATE(Amount,-Amount);
          END;


          // Valuta (Rad 70)
          IF IsCurrency THEN BEGIN
            IF gCurrencyCode <> CurrencyCode THEN // Inte valuatkonto
              gGenJnlLine.VALIDATE("Amount (LCY)",gGenJnlLine.Amount * CurrencyRate);
          END;
          gGenJnlLine."Applies-to Doc. No." := gAppliesToDocNo;
          gGenJnlLine.INSERT;

          CopyInfoLine;
          "gTotalAmount(LCY)" += gGenJnlLine."Amount (LCY)";
          RestAmount += gGenJnlLine.Amount;
        UNTIL tDocNoRec.NEXT = 0;
      END ELSE BEGIN
        InitGlLine(pPostingDate);
        gGenJnlLine.VALIDATE("Account Type", gGenJnlLine."Account Type"::Customer);
        gGenJnlLine.VALIDATE("Currency Code",SetCurrencyCode(CurrencyCode));
        gGenJnlLine.VALIDATE(Amount,-Amount);
        // Valuta (Rad 70)
        IF IsCurrency THEN
          gGenJnlLine.VALIDATE("Amount (LCY)",-"Amount(LCY)");

      //>>4PSSE, 170822
      //>>RFC1127
      //    gGenJnlLine."Received amount" := Amount;
          gGenJnlLine.VALIDATE("Received amount", Amount);
      //<<RFC1127
        //gGenJnlLine.VALIDATE(Description,PEB001);
        gtTextLineEntry.RESET;
        gtTextLineEntry.SETFILTER("Line Code",'30|40');
        IF gtTextLineEntry.FINDFIRST THEN
          tDocNoRec."No." := COPYSTR(DELCHR(gtTextLineEntry.Description,'<>',' '),1,MAXSTRLEN(tDocNoRec."No."));
      //>>180208
        //gGenJnlLine.VALIDATE(Description,STRSUBSTNO(PEB001a,tDocNoRec."No."));
        gGenJnlLine.VALIDATE(Description,COPYSTR(PayerName + ' ' + tDocNoRec."No.",1,MAXSTRLEN(gGenJnlLine.Description)));
      //<<180208
        gGenJnlLine."Not Matched" := TRUE;
      //<<4PSSE
        gGenJnlLine."Information Type" := gGenJnlLine."Information Type"::Error;
        gGenJnlLine.INSERT;

        CopyInfoLine;
        "gTotalAmount(LCY)" += gGenJnlLine."Amount (LCY)";
        RestAmount += gGenJnlLine.Amount;
      END;

      // Om det „r en diff
      IF RestAmount <> 0 THEN BEGIN
        InitGlLine(pPostingDate);
        gGenJnlLine.VALIDATE("Currency Code",SetCurrencyCode(CurrencyCode));
        gGenJnlLine.VALIDATE(Amount,-RestAmount);
        // Valuta (Rad 70)
        IF IsCurrency THEN BEGIN
          IF gCurrencyCode <> CurrencyCode THEN // Inte valuatkonto
            gGenJnlLine.VALIDATE("Amount (LCY)",gGenJnlLine.Amount * CurrencyRate);
        END;

        gGenJnlLine.Description := STRSUBSTNO(PEB005,-RestAmount);
        gGenJnlLine."Information Type" := gGenJnlLine."Information Type"::Error;
        "gTotalAmount(LCY)" += gGenJnlLine."Amount (LCY)";
        gGenJnlLine.INSERT;
        CopyInfoLine;
        // M„rk raderna med Error
        gtGLLines.RESET;
        IF gtGLLines.FINDSET THEN REPEAT
          gGenJnlLine."Journal Template Name" := gtGLLines."Journal Template Name";
          gGenJnlLine."Journal Batch Name" := gtGLLines."Journal Batch Name";
          gGenJnlLine."Line No." := gtGLLines."Line No.";
          gGenJnlLine.FIND;
          gGenJnlLine."Information Type" := gGenJnlLine."Information Type"::Error;
          gGenJnlLine.MODIFY;
        UNTIL gtGLLines.NEXT = 0;
      END;

      gtGLLines.RESET;
      gtGLLines.DELETEALL;

      gtTextLineEntry.RESET;
      gtTextLineEntry.DELETEALL;
    END;

    PROCEDURE InitGlLine@1100570010(pPostingDate@1100570000 : Date);
    BEGIN
      gGenJnlLine.INIT;
      gGenJnlLine."Journal Template Name" := gJnlTemplateName;
      gGenJnlLine."Journal Batch Name" := gJnlBatchName;
      gGenJnlLine."Line No." += 10000;
      gGenJnlLine.VALIDATE("Document Type",gGenJnlLine."Document Type"::Payment);
      gGenJnlLine.VALIDATE("Document No.",gDocNo);
      gGenJnlLine."Posting No. Series" := gGenJnlBatch."Posting No. Series";
      gGenJnlLine."Source Code" := gGenJnlTemplate."Source Code";
      gGenJnlLine.VALIDATE("Posting Date",pPostingDate);
    END;

    PROCEDURE SetTextForInfo@1000000004(VAR pvGenJournalLine@1000000000 : Record 81;pTxt2@1000000001 : Text[50]);
    BEGIN
      pvGenJournalLine."Information Type" := pvGenJournalLine."Information Type"::Error;
      //IF pvGenJournalLine.Information = '' THEN
      //  pvGenJournalLine.Information := pTxt2
      //ELSE
      //  pvGenJournalLine.Information := COPYSTR(pvGenJournalLine.Information + ' | ' + pTxt2,1,50);
      IF pvGenJournalLine.Comment = '' THEN
        pvGenJournalLine.Comment := pTxt2
      ELSE
        pvGenJournalLine.Comment := COPYSTR(pvGenJournalLine.Comment + ' | ' + pTxt2,1,MAXSTRLEN(pvGenJournalLine.Comment ));
    END;

    BEGIN
    {
      PEB *** Begin ***
      PEB0068 Total IN
      PEB
      PEB *** End ***

      170704 ITERO.DL add OCR in InvCredExist
                      extend ApplyToDocNo 20->30
      170822 ITERO.DL find relevant reference if not matched
      170822 ITERO.DL pure274678
      170825 ITERO.DL pure274938
      190213 ORANGO.DL RFC1127 use publisher for "Received amount"
    }
    END.
  }
  RDLDATA
  {
  }
}

