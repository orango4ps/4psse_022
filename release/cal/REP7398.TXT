OBJECT Report 7398 Date Compress Whse. Entries
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.01;
  }
  PROPERTIES
  {
    Permissions=TableData 87=rimd,
                TableData 7312=rimd,
                TableData 7313=rimd;
    CaptionML=[DEU=Datumskompr. Lagerplatzposten;
               ENU=Date Compress Whse. Entries;
               NLD=Magazijnposten comprimeren;
               NOR=Datokomprimer lagerposter];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  WhseEntryFilter := COPYSTR("Warehouse Entry".GETFILTERS,1,MAXSTRLEN(DateComprReg.Filter));
                END;

  }
  DATASET
  {
    { 9585;    ;DataItem;                    ;
               DataItemTable=Table7312;
               DataItemTableView=SORTING(Item No.,Bin Code,Location Code,Variant Code,Unit of Measure Code,Lot No.,Serial No.,Entry Type);
               OnPreDataItem=BEGIN
                               IF NOT HideDialog THEN
                                 IF NOT CONFIRM(Text000,FALSE) THEN
                                   CurrReport.BREAK;

                               IF EntrdDateComprReg."Ending Date" = 0D THEN
                                 ERROR(Text003,EntrdDateComprReg.FIELDCAPTION("Ending Date"));

                               Window.OPEN(
                                 Text004 +
                                 Text005 +
                                 Text006 +
                                 Text007);

                               SourceCodeSetup.GET;
                               SourceCodeSetup.TESTFIELD("Compress Whse. Entries");

                               NewWhseEntry.LOCKTABLE;
                               WhseReg.LOCKTABLE;
                               DateComprReg.LOCKTABLE;

                               IF WhseEntry2.FINDLAST THEN;
                               LastEntryNo := WhseEntry2."Entry No.";
                               SETRANGE("Entry No.",0,LastEntryNo);
                               SETRANGE("Registering Date",EntrdDateComprReg."Starting Date",EntrdDateComprReg."Ending Date");

                               InitRegisters;

                               RetainSerialNo := RetainNo(FIELDNO("Serial No."));
                               RetainLotNo := RetainNo(FIELDNO("Lot No."));
                             END;

               OnAfterGetRecord=BEGIN
                                  WhseEntry2 := "Warehouse Entry";
                                  WITH WhseEntry2 DO BEGIN
                                    SETCURRENTKEY(
                                      "Item No.","Bin Code","Location Code","Variant Code",
                                      "Unit of Measure Code","Lot No.","Serial No.");
                                    COPYFILTERS("Warehouse Entry");
                                    SETRANGE("Item No.","Item No.");
                                    SETRANGE("Bin Code","Bin Code");
                                    SETRANGE("Location Code","Location Code");
                                    SETRANGE("Variant Code","Variant Code");
                                    SETRANGE("Unit of Measure Code","Unit of Measure Code");
                                    SETFILTER(
                                      "Registering Date",
                                      DateComprMgt.GetDateFilter("Registering Date",EntrdDateComprReg,FALSE));

                                    IF (NOT RetainSerialNo) OR (NOT RetainLotNo) THEN
                                      UpdateITWhseEntries;

                                    SETRANGE("Serial No.","Serial No.");
                                    SETRANGE("Lot No.","Lot No.");
                                    SETRANGE("Warranty Date","Warranty Date");
                                    SETRANGE("Expiration Date","Expiration Date");

                                    CalcCompressWhseEntry;

                                    NewWhseEntry.INIT;
                                    NewWhseEntry."Location Code" := "Location Code";
                                    NewWhseEntry."Bin Code" := "Bin Code";
                                    NewWhseEntry."Item No." := "Item No.";
                                    NewWhseEntry.Description := Text008;
                                    NewWhseEntry."Variant Code" := "Variant Code";
                                    NewWhseEntry."Unit of Measure Code" := "Unit of Measure Code";
                                    NewWhseEntry.Dedicated := Dedicated;
                                    NewWhseEntry."Zone Code" := "Zone Code";
                                    NewWhseEntry."Bin Type Code" := "Bin Type Code";
                                    NewWhseEntry."Registering Date" := GETRANGEMIN("Registering Date");
                                    NewWhseEntry."Serial No." := "Serial No.";
                                    NewWhseEntry."Lot No." := "Lot No.";
                                    NewWhseEntry."Warranty Date" := "Warranty Date";
                                    NewWhseEntry."Expiration Date" := "Expiration Date";

                                    OnAfterGetWarehouseEntryOnAfterInitNewWhseEntry(NewWhseEntry,"Warehouse Entry");

                                    Window.UPDATE(1,NewWhseEntry."Registering Date");
                                    Window.UPDATE(2,DateComprReg."No. of New Records");

                                    REPEAT
                                      DELETE;
                                      DateComprReg."No. Records Deleted" := DateComprReg."No. Records Deleted" + 1;
                                      Window.UPDATE(3,DateComprReg."No. Records Deleted");
                                    UNTIL NOT FINDFIRST;

                                    IF PosQtyBaseonBin > 0 THEN BEGIN
                                      InsertNewEntry(
                                        NewWhseEntry,PosQtyonBin,PosQtyBaseonBin,
                                        PosCubage,PosWeight,NewWhseEntry."Entry Type"::"Positive Adjmt.");
                                      DateComprReg."No. of New Records" := DateComprReg."No. of New Records" + 1;
                                    END;

                                    IF NegQtyBaseonBin < 0 THEN BEGIN
                                      InsertNewEntry(
                                        NewWhseEntry,NegQtyonBin,NegQtyBaseonBin,
                                        NegCubage,NegWeight,NewWhseEntry."Entry Type"::"Negative Adjmt.");
                                      DateComprReg."No. of New Records" := DateComprReg."No. of New Records" + 1;
                                    END;
                                  END;

                                  IF DateComprReg."No. Records Deleted" >= NoOfDeleted + 10 THEN BEGIN
                                    NoOfDeleted := DateComprReg."No. Records Deleted";
                                    InsertRegisters(WhseReg,DateComprReg);
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                IF DateComprReg."No. Records Deleted" > NoOfDeleted THEN
                                  InsertRegisters(WhseReg,DateComprReg);
                              END;

               ReqFilterFields=Item No.,Bin Code,Location Code,Zone Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF EntrdDateComprReg."Ending Date" = 0D THEN
                     EntrdDateComprReg."Ending Date" := TODAY;

                   WITH "Warehouse Entry" DO BEGIN
                     InsertField(FIELDNO("Serial No."),FIELDCAPTION("Serial No."));
                     InsertField(FIELDNO("Lot No."),FIELDCAPTION("Lot No."));
                   END;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1   ;2   ;Field     ;
                  Name=StartingDate;
                  CaptionML=[DEU=Startdatum;
                             ENU=Starting Date;
                             NLD=Begindatum;
                             NOR=Startdato;
                             SVE=Startdatum];
                  ToolTipML=[DEU=Gibt das erste Datum an, das in der Datumskomprimierung enthalten sein soll. Die Komprimierung wirkt sich auf alle Lagerplatzposten ab diesem Datum bis zum Enddatum aus.;
                             ENU=Specifies the first date to be included in the date compression. The compression will affect all warehouse entries from this date to the Ending Date.;
                             NLD=Hiermee wordt de eerste datum opgegeven die moet worden opgenomen in de datumcompressie. De compressie is van invloed op alle magazijnposten vanaf deze datum tot de einddatum.;
                             NOR=Angir den fõrste datoen som skal vëre med i datokomprimeringen. Komprimeringen vil pÜvirke alle lagerpostene fra denne datoen til sluttdatoen.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=EntrdDateComprReg."Starting Date" }

      { 2   ;2   ;Field     ;
                  Name=EndingDate;
                  CaptionML=[DEU=Enddatum;
                             ENU=Ending Date;
                             NLD=Einddatum;
                             NOR=Sluttdato;
                             SVE=Slutdatum];
                  ToolTipML=[DEU=Gibt das letzte Datum an, das in der Datumskomprimierung enthalten sein soll. Alle Lagerplatzposten vom Startdatum bis zu diesem Datum werden von der Komprimierung erfasst.;
                             ENU=Specifies the last date to be included in the date compression. The compression will affect all warehouse entries from the Starting Date to this date.;
                             NLD=Hiermee wordt de laatste datum opgegeven die moet worden opgenomen in de datumcompressie. De compressie is van invloed op alle magazijnposten vanaf de begindatum tot deze datum.;
                             NOR=Angir den siste datoen som skal vëre med i datokomprimeringen. Komprimeringen vil pÜvirke alle lagerpostene fra startdatoen til denne datoen.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=EntrdDateComprReg."Ending Date" }

      { 3   ;2   ;Field     ;
                  Name=PeriodLength;
                  CaptionML=[DEU=PeriodenlÑnge;
                             ENU=Period Length;
                             NLD=Periodelengte;
                             NOR=Periodelengde;
                             SVE=Period];
                  ToolTipML=[DEU=Gibt die LÑnge der Periode an, innerhalb der Posten zusammengefasst werden. WÑhlen Sie das Feld, um die Optionen anzuzeigen.;
                             ENU=Specifies the length of the period whose entries will be combined. Choose the field to see the options.;
                             NLD=Hiermee wordt de lengte opgegeven van de periode waarvoor de posten worden gecombineerd. Kies het veld om de opties te bekijken.;
                             NOR=Angir lengden pÜ perioden som har poster som vil bli kombinert. Velg feltet for Ü vise alternativene.];
                  OptionCaptionML=[DEU=Tag,Woche,Monat,Quartal,Jahr,Periode;
                                   ENU=Day,Week,Month,Quarter,Year,Period;
                                   NLD=Dag,Week,Maand,Kwartaal,Jaar,Periode;
                                   NOR=Dag,Uke,MÜned,Kvartal,èr,Periode;
                                   SVE=Dag,Vecka,MÜnad,Kvartal,èr,Period];
                  ApplicationArea=#Warehouse;
                  SourceExpr=EntrdDateComprReg."Period Length" }

      { 1907651001;2;Group  ;
                  CaptionML=[DEU=Feldinhalt behalten;
                             ENU=Retain Field Contents;
                             NLD=Te bewaren velden;
                             NOR=Behold feltinnhold;
                             SVE=BibehÜll fÑlt] }

      { 4   ;3   ;Field     ;
                  Name=SerialNo;
                  CaptionML=[DEU=Seriennr.;
                             ENU=Serial No.;
                             NLD=Serienummer;
                             NOR=Serienr.;
                             SVE=Serienr];
                  ToolTipML=[DEU=Gibt an, ob die Seriennummer in der Komprimierung beibehalten werden soll.;
                             ENU=Specifies if you want to retain the serial number in the compression.;
                             NLD=Hiermee wordt opgegeven of het serienummer moet worden behouden in de compressie.;
                             NOR=Angir om du vil beholde serienummeret i komprimeringen.];
                  ApplicationArea=#ItemTracking;
                  SourceExpr=Retain[1] }

      { 5   ;3   ;Field     ;
                  Name=LotNo;
                  CaptionML=[DEU=Chargennr.;
                             ENU=Lot No.;
                             NLD=Lotnr.;
                             NOR=Partinr.;
                             SVE=Partinr];
                  ToolTipML=[DEU=Gibt an, ob die Chargennummer in der Komprimierung beibehalten werden soll.;
                             ENU=Specifies if you want to retain the lot number in the compression.;
                             NLD=Hiermee wordt opgegeven of het lotnummer moet worden behouden in de compressie.;
                             NOR=Angir om du vil beholde partinummeret i komprimeringen.];
                  ApplicationArea=#ItemTracking;
                  SourceExpr=Retain[2] }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Durch diesen Batchauftrag werden Posten gelîscht. Daher muss vor dem AusfÅhren des Batchauftrags eine Datenbanksicherung erstellt werden.\\Mîchten Sie die Posten datumskomprimieren?;ENU=This batch job deletes entries. Therefore, it is important that you make a backup of the database before you run the batch job.\\Do you want to date compress the entries?;NLD=Met deze batchverwerking worden posten verwijderd. Maak daarom eerst een reservekopie van de database voor u de batchverwerking uitvoert.\\Wilt u de posten comprimeren?;NOR=Denne kjõrselen sletter poster. Derfor er det viktig at du tar en sikkerhetskopi av databasen fõr du starter kjõrselen.\\Vil du datokomprimere postene?;SVE=Detta batchjobb tar bort transaktioner. Det Ñr dÑrfîr viktigt att du tar en sÑkerhetskopia pÜ databasen innan du kîr batchjobbet.\\Vill du datumkomprimera transaktionerna?';
      Text003@1003 : TextConst 'DEU=Das %1 muss angegeben werden.;ENU=%1 must be specified.;NLD=%1 moet opgegeven worden.;NOR=%1 mÜ angis.;SVE=%1 mÜste specificeras.';
      Text004@1004 : TextConst 'DEU=Datumskomprimierung Lagerplatzposten ...\\;ENU=Date compressing warehouse entries...\\;NLD=Comprimeren magazijnposten...\\;NOR=Datokomprimerer lagerposter...\\';
      Text005@1005 : TextConst 'DEU=Datum                #1######\\;ENU=Date                 #1######\\;NLD=Datum                #1######\\;NOR=Dato                 #1######\\';
      Text006@1006 : TextConst 'DEU=Anz. neue Posten     #2######\;ENU=No. of new entries   #2######\;NLD=Nieuwe posten        #2######\;NOR=Antall nye poster    #2######\';
      Text007@1007 : TextConst 'DEU=Anz. gelîsch. Posten #3######;ENU=No. of entries del.  #3######;NLD=Verwijderde posten   #3######;NOR=Ant. slettede poster #3######';
      SourceCodeSetup@1008 : Record 242;
      DateComprReg@1009 : Record 87;
      EntrdDateComprReg@1010 : Record 87;
      WhseReg@1011 : Record 7313;
      NewWhseEntry@1012 : Record 7312;
      WhseEntry2@1013 : Record 7312;
      DateComprMgt@1014 : Codeunit 356;
      Window@1015 : Dialog;
      WhseEntryFilter@1016 : Text[250];
      PosQtyonBin@1031 : Decimal;
      PosQtyBaseonBin@1030 : Decimal;
      NegQtyonBin@1029 : Decimal;
      NegQtyBaseonBin@1028 : Decimal;
      PosWeight@1032 : Decimal;
      NegWeight@1033 : Decimal;
      PosCubage@1034 : Decimal;
      NegCubage@1035 : Decimal;
      NoOfFields@1017 : Integer;
      Retain@1018 : ARRAY [2] OF Boolean;
      FieldNumber@1019 : ARRAY [10] OF Integer;
      FieldNameArray@1020 : ARRAY [10] OF Text[100];
      LastEntryNo@1021 : Integer;
      NoOfDeleted@1022 : Integer;
      i@1024 : Integer;
      WhseRegExists@1023 : Boolean;
      RetainSerialNo@1026 : Boolean;
      RetainLotNo@1027 : Boolean;
      Text008@1038 : TextConst 'DEU=Datumskomprimiert;ENU=Date Compressed;NLD=Gecomprimeerde post;NOR=Dato komprimert;SVE=Datumkomprimerad';
      HideDialog@1036 : Boolean;

    LOCAL PROCEDURE InitRegisters@4();
    VAR
      NextRegNo@1000 : Integer;
    BEGIN
      IF WhseReg.FIND('+') THEN;
      WhseReg.INIT;
      WhseReg."No." := WhseReg."No." + 1;
      WhseReg."Creation Date" := TODAY;
      WhseReg."Creation Time" := TIME;
      WhseReg."Source Code" := SourceCodeSetup."Compress Whse. Entries";
      WhseReg."User ID" := USERID;
      WhseReg."From Entry No." := LastEntryNo + 1;

      IF DateComprReg.FINDLAST THEN
        NextRegNo := DateComprReg."No." + 1;

      DateComprReg.InitRegister(
        DATABASE::"Warehouse Entry",NextRegNo,
        EntrdDateComprReg."Starting Date",EntrdDateComprReg."Ending Date",EntrdDateComprReg."Period Length",
        WhseEntryFilter,WhseReg."No.",SourceCodeSetup."Compress Whse. Entries");

      FOR i := 1 TO NoOfFields DO
        IF Retain[i] THEN
          DateComprReg."Retain Field Contents" :=
            COPYSTR(
              DateComprReg."Retain Field Contents" + ',' + FieldNameArray[i],1,
              MAXSTRLEN(DateComprReg."Retain Field Contents"));
      DateComprReg."Retain Field Contents" := COPYSTR(DateComprReg."Retain Field Contents",2);

      WhseRegExists := FALSE;
      NoOfDeleted := 0;
    END;

    LOCAL PROCEDURE InsertRegisters@1(VAR WhseReg@1000 : Record 7313;VAR DateComprReg@1001 : Record 87);
    BEGIN
      WhseReg."To Entry No." := NewWhseEntry."Entry No.";

      IF WhseRegExists THEN BEGIN
        WhseReg.MODIFY;
        DateComprReg.MODIFY;
      END ELSE BEGIN
        WhseReg.INSERT;
        DateComprReg.INSERT;
        WhseRegExists := TRUE;
      END;
      COMMIT;

      NewWhseEntry.LOCKTABLE;
      WhseReg.LOCKTABLE;
      DateComprReg.LOCKTABLE;

      WhseEntry2.RESET;

      IF WhseEntry2.FINDLAST THEN;
      IF LastEntryNo <> WhseEntry2."Entry No." THEN BEGIN
        LastEntryNo := WhseEntry2."Entry No.";
        InitRegisters;
      END;
    END;

    LOCAL PROCEDURE InsertField@2(Number@1000 : Integer;Name@1001 : Text[100]);
    BEGIN
      NoOfFields := NoOfFields + 1;
      FieldNumber[NoOfFields] := Number;
      FieldNameArray[NoOfFields] := Name;
    END;

    LOCAL PROCEDURE RetainNo@3(Number@1000 : Integer) : Boolean;
    BEGIN
      EXIT(Retain[Index(Number)]);
    END;

    LOCAL PROCEDURE Index@6(Number@1000 : Integer) : Integer;
    BEGIN
      FOR i := 1 TO NoOfFields DO
        IF Number = FieldNumber[i] THEN
          EXIT(i);
    END;

    LOCAL PROCEDURE CalcCompressWhseEntry@8();
    VAR
      LocalWhseEntry@1002 : Record 7312;
    BEGIN
      PosQtyonBin := 0;
      PosQtyBaseonBin := 0;
      PosWeight := 0;
      PosCubage := 0;
      NegQtyonBin := 0;
      NegQtyBaseonBin := 0;
      NegWeight := 0;
      NegCubage := 0;
      LocalWhseEntry.COPY(WhseEntry2);
      IF LocalWhseEntry.FIND('-') THEN
        REPEAT
          IF LocalWhseEntry."Qty. (Base)" < 0 THEN BEGIN
            NegQtyonBin := NegQtyonBin + LocalWhseEntry.Quantity;
            NegQtyBaseonBin := NegQtyBaseonBin + LocalWhseEntry."Qty. (Base)";
            NegWeight := NegWeight + LocalWhseEntry.Weight;
            NegCubage := NegCubage + LocalWhseEntry.Cubage;
          END ELSE BEGIN
            PosQtyonBin := PosQtyonBin + LocalWhseEntry.Quantity;
            PosQtyBaseonBin := PosQtyBaseonBin + LocalWhseEntry."Qty. (Base)";
            PosWeight := PosWeight + LocalWhseEntry.Weight;
            PosCubage := PosCubage + LocalWhseEntry.Cubage;
          END;
        UNTIL LocalWhseEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE UpdateITWhseEntries@7();
    VAR
      LocalWhseEntry@1000 : Record 7312;
      LocalWhseEntry2@1001 : Record 7312;
      ItemTrackingMgt@1003 : Codeunit 6500;
      QtyonBin@1002 : Decimal;
      SNRequired@1004 : Boolean;
      LNRequired@1005 : Boolean;
    BEGIN
      ItemTrackingMgt.CheckWhseItemTrkgSetup(WhseEntry2."Item No.",SNRequired,LNRequired,FALSE);

      LocalWhseEntry.COPY(WhseEntry2);
      WITH LocalWhseEntry DO BEGIN
        IF RetainSerialNo OR RetainLotNo THEN BEGIN
          IF SNRequired OR LNRequired THEN BEGIN
            SETFILTER("Warranty Date",'<>%1',0D);
            SETFILTER("Expiration Date",'<>%1',0D);
            IF NOT FIND('-') THEN BEGIN
              SETRANGE("Warranty Date");
              SETRANGE("Expiration Date");
            END;
          END;
        END ELSE BEGIN
          SETRANGE("Warranty Date",WhseEntry2."Warranty Date");
          SETRANGE("Expiration Date",WhseEntry2."Expiration Date");
        END;

        IF NOT RetainSerialNo THEN BEGIN
          IF SNRequired THEN
            SETFILTER("Serial No.",'<>''''');
        END ELSE
          SETRANGE("Serial No.",WhseEntry2."Serial No.");
        IF NOT RetainLotNo THEN BEGIN
          IF LNRequired THEN
            SETFILTER("Lot No.",'<>''''');
        END ELSE
          SETRANGE("Lot No.",WhseEntry2."Lot No.");
        IF FIND('-') THEN
          REPEAT
            QtyonBin := 0;
            LocalWhseEntry2.COPY(LocalWhseEntry);

            IF NOT RetainSerialNo AND SNRequired THEN
              LocalWhseEntry2.SETRANGE("Serial No.","Serial No.");

            IF NOT RetainLotNo AND LNRequired THEN
              LocalWhseEntry2.SETRANGE("Lot No.","Lot No.");

            IF (NOT RetainSerialNo AND SNRequired) OR
               (NOT RetainLotNo AND LNRequired)
            THEN BEGIN
              LocalWhseEntry2.SETRANGE("Warranty Date","Warranty Date");
              LocalWhseEntry2.SETRANGE("Expiration Date","Expiration Date");
            END;

            IF LocalWhseEntry2.FIND('-') THEN
              REPEAT
                QtyonBin := QtyonBin + LocalWhseEntry2."Qty. (Base)";
              UNTIL LocalWhseEntry2.NEXT = 0;

            IF QtyonBin <= 0 THEN BEGIN
              IF LocalWhseEntry2.FIND('-') THEN
                REPEAT
                  IF NOT RetainSerialNo AND SNRequired THEN
                    LocalWhseEntry2."Serial No." := '';
                  IF NOT RetainLotNo AND LNRequired THEN
                    LocalWhseEntry2."Lot No." := '';
                  IF (NOT RetainSerialNo AND SNRequired) OR
                     (NOT RetainLotNo AND LNRequired)
                  THEN BEGIN
                    LocalWhseEntry2."Warranty Date" := 0D;
                    LocalWhseEntry2."Expiration Date" := 0D;
                  END;
                  OnUpdateITWhseEntriesOnBeforeLocalWhseEntry2Modify(LocalWhseEntry2,RetainSerialNo,SNRequired,RetainLotNo,LNRequired);
                  LocalWhseEntry2.MODIFY;
                UNTIL LocalWhseEntry2.NEXT = 0;

              IF (NOT RetainSerialNo AND SNRequired) OR
                 (NOT RetainLotNo AND LNRequired)
              THEN BEGIN
                WhseEntry2."Warranty Date" := 0D;
                WhseEntry2."Expiration Date" := 0D;
              END;
              IF NOT RetainSerialNo THEN
                WhseEntry2."Serial No." := '';
              IF NOT RetainLotNo THEN
                WhseEntry2."Lot No." := '';
              OnUpdateITWhseEntriesOnAfterSetWhseEntry2(WhseEntry2,RetainSerialNo,SNRequired,RetainLotNo,LNRequired);
            END;
          UNTIL NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE InsertNewEntry@9(VAR WhseEntry@1000 : Record 7312;Qty@1001 : Decimal;QtyBase@1002 : Decimal;Cubage@1004 : Decimal;Weight@1003 : Decimal;EntryType@1005 : Option);
    BEGIN
      LastEntryNo := LastEntryNo + 1;
      WhseEntry."Entry No." := LastEntryNo;
      WhseEntry.Quantity := Qty;
      WhseEntry."Qty. (Base)" := QtyBase;
      WhseEntry.Cubage := Cubage;
      WhseEntry.Weight := Weight;
      WhseEntry."Entry Type" := EntryType;
      OnBeforeInsertNewEntry(WhseEntry);
      WhseEntry.INSERT;
    END;

    [External]
    PROCEDURE InitializeReport@11(EntrdDateComprReg2@1000 : Record 87;SerialNo@1001 : Boolean;LotNo@1002 : Boolean);
    BEGIN
      EntrdDateComprReg.COPY(EntrdDateComprReg2);
      WITH WhseEntry2 DO BEGIN
        InsertField(FIELDNO("Serial No."),FIELDCAPTION("Serial No."));
        InsertField(FIELDNO("Lot No."),FIELDCAPTION("Lot No."));
      END;
      Retain[1] := SerialNo;
      Retain[2] := LotNo;
    END;

    [External]
    PROCEDURE SetHideDialog@10(NewHideDialog@1000 : Boolean);
    BEGIN
      HideDialog := NewHideDialog;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterGetWarehouseEntryOnAfterInitNewWhseEntry@14(VAR NewWarehouseEntry@1000 : Record 7312;OldWarehouseEntry@1001 : Record 7312);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeInsertNewEntry@5(VAR WarehouseEntry@1000 : Record 7312);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnUpdateITWhseEntriesOnAfterSetWhseEntry2@13(VAR WarehouseEntry@1004 : Record 7312;RetainSerialNo@1003 : Boolean;SNRequired@1002 : Boolean;RetainLotNo@1001 : Boolean;LNRequired@1000 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnUpdateITWhseEntriesOnBeforeLocalWhseEntry2Modify@12(VAR WarehouseEntry@1000 : Record 7312;RetainSerialNo@1001 : Boolean;SNRequired@1002 : Boolean;RetainLotNo@1003 : Boolean;LNRequired@1004 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

