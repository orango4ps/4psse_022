OBJECT Codeunit 11020510 IMOS Job Notice Doc. XML Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    TableNo=11020534;
    OnRun=VAR
            PartnerAgreement@1000000000 : Record 11020547;
            OutboundDocument@1000000001 : Record 11020543;
            InboundDocument@1000000002 : Record 11020538;
          BEGIN
            CASE Action OF
              Action::"Receive Document":
                BEGIN
                   GetInboundDocument(InboundDocument);
                   ReceiveDocument(InboundDocument);
                 END;
              Action::"Send Document":
                BEGIN
                   GetOutboundDocument(OutboundDocument);
                   GetPartnerAgreement(PartnerAgreement);
                   PartnerAgreement.TESTFIELD("XML Document Namespace");
                   PartnerAgreement.TESTFIELD("XML Document Name");
                   GlobalSentDate := "Date Sent";
                   GlobalSentTime := "Time Sent";
                   SendDocument(OutboundDocument,PartnerAgreement);
                   "XML Document Identifier" := GlobalDocumentIdentifier;
                   SetXMLDocument(XMLDOMDocument);
                 END;
            END;
          END;

  }
  CODE
  {
    VAR
      XMLDOMDocument@1000000000 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      Text002@1000000010 : TextConst 'DEU=Es ist kein XML verfgbar fr eingehende Belegnr. %1;ENU=No XML is available for Inbound Document No. %1;NLD=Er is geen XML beschikbaar voor inkomend documentnr. %1;NOR=Det finnes ingen XML tilgjengelig for inkommende dokumentnr %1;SVE=Det finns ingen XML tillg„nglig f”r ankommande dokumentnr %1';
      GlobalSentTime@1000000005 : Time;
      GlobalSentDate@1000000006 : Date;
      GlobalDocumentIdentifier@1000000004 : Text[30];
      ProcessingInstruction@1000000001 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlProcessingInstruction";
      XMLDOMManagement4PS@1100525000 : Codeunit 11020220;
      XMLFormat4PS@1100525001 : Codeunit 11020221;

    PROCEDURE ReceiveDocument@1000000011(InboundDocument@1000000000 : Record 11020538);
    VAR
      InboundJobNoticeRec@1000000003 : Record 11020539;
    BEGIN
      IF NOT InboundDocument.GetInboundXMLDOM(XMLDOMDocument) THEN
        ERROR(Text002);
      ReceiveXML(InboundDocument,InboundJobNoticeRec);
    END;

    PROCEDURE ReceiveXML@1000000013(InboundDocument@1000000012 : Record 11020538;VAR InboundJobNoticeRec@1000000001 : Record 11020539);
    VAR
      InboundJobNoticeLineRec@1000000000 : Record 11020540;
      DocumentNode@1000000002 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      CustomDataNode@1000000003 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      ActivityNodeList@1000000006 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeList";
      ActivityNode@1000000007 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      TmpValue@1000000004 : Text[1024];
      DateTimeCompleted@1000000013 : DateTime;
      prebontype@1100485000 : Integer;
      wbnr@1100485001 : Text[30];
      X@1000000015 : Text[30];
      JobNoticeRec@1100485002 : Record 11020536;
      I@1100525000 : Integer;
    BEGIN
      DocumentNode := XMLDOMDocument.DocumentElement;
      InboundJobNoticeLineRec.LOCKTABLE;
      WITH InboundJobNoticeRec DO
        BEGIN
          LOCKTABLE;
          IF FIND('+') THEN;
          INIT;
          "Date Received" := InboundDocument."Date Received";
          "Time Received" := InboundDocument."Time Received";
          "Inbound Document No." += 1;

          InboundDocument."Source Type" := DATABASE::"Inbound Job Notice";
          InboundDocument."Source No." := "Inbound Document No.";
          InboundDocument.MODIFY;

          DateTimeCompleted := XMLFormat4PS.FormatXMLDateTime(XMLDOMManagement4PS.GetAttributeValue(DocumentNode,'dt_completed'));
          "Date Completed" := DT2DATE(DateTimeCompleted);
          "Time Completed" := DT2TIME(DateTimeCompleted);

          XMLDOMManagement4PS.FindNode(DocumentNode,'customdata',CustomDataNode);
          "No." := XMLDOMManagement4PS.GetAttributeValue(CustomDataNode,'erp_serviceorder');
          "Project No." := XMLDOMManagement4PS.GetAttributeValue(CustomDataNode,'werkorder');
          EVALUATE(prebontype, XMLDOMManagement4PS.GetAttributeValue(CustomDataNode,'bon_type'));
          IF prebontype > 0 THEN
            "Bon Type" := prebontype - 1;

          TmpValue := XMLDOMManagement4PS.GetAttributeValue(CustomDataNode,'opmerking');
          "Comment 2" := DELSTR(TmpValue,MAXSTRLEN(InboundJobNoticeRec.Comment)+1);
          Status := InboundJobNoticeRec.Status::Received;

          JobNoticeRec.RESET;
          JobNoticeRec.SETRANGE("No.","No.");
          IF JobNoticeRec.FIND('+') THEN BEGIN
             "District Code" := JobNoticeRec."District Code";
             "Plant Location" := COPYSTR(JobNoticeRec."Plant Location",1,20);
          END;

          INSERT;
        END;

      WITH InboundJobNoticeLineRec DO BEGIN
        "Inbound Document No." := InboundJobNoticeRec."Inbound Document No.";
        XMLDOMManagement4PS.FindNodes(DocumentNode,'projectstep/activity',ActivityNodeList);
        IF NOT ISNULL(ActivityNodeList) THEN BEGIN
          FOR I := 0 TO (ActivityNodeList.Count - 1) DO BEGIN
              ActivityNode := ActivityNodeList.Item(I);
              INIT;
              "Line No." += 1;
              "Job Notice No." := InboundJobNoticeRec."No.";
              XMLDOMManagement4PS.FindNode(ActivityNode,'customdata',CustomDataNode);
              wbnr := XMLDOMManagement4PS.GetAttributeValue(CustomDataNode,'erp_activiteit');
              X := FORMAT(COPYSTR(wbnr,9));
              EVALUATE("Job Notice Line No.",X);
              "Info Engineer" := XMLDOMManagement4PS.GetAttributeValue(CustomDataNode,'info_van_monteur');
              Cause := XMLDOMManagement4PS.GetAttributeValue(CustomDataNode,'oorzaak');
              "Activities done" := XMLDOMManagement4PS.GetAttributeValue(CustomDataNode,'verrichte_werkzaamheden');
              Overtime := XMLFormat4PS.FormatXMLInteger(XMLDOMManagement4PS.GetAttributeValue(CustomDataNode,'overuren'));
              INSERT;
          END;
        END;
      END;
    END;

    LOCAL PROCEDURE SendDocument@1000000006(VAR OutboundDocument@1000000000 : Record 11020543;PartnerAgreement@1000000005 : Record 11020547);
    VAR
      OutboundJobNoticeRec@1000000003 : Record 11020544;
      CurXMLDomNode@1100525000 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      NewXMLDomNode@1100525001 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      WITH OutboundDocument DO
        BEGIN
          OutboundJobNoticeRec.GET("Source No.");
          GlobalDocumentIdentifier := OutboundJobNoticeRec."No.";

          XMLDOMDocument := XMLDOMDocument.XmlDocument;
          ProcessingInstruction := XMLDOMDocument.CreateProcessingInstruction('xml', 'version="1.0" encoding="UTF-8"');
          XMLDOMDocument.AppendChild(ProcessingInstruction);

          CurXMLDomNode := XMLDOMDocument.CreateElement('erporder', PartnerAgreement."XML Document Namespace");
          NewXMLDomNode := XMLDOMDocument.AppendChild(CurXMLDomNode);

          CreateXML(NewXMLDomNode,OutboundJobNoticeRec,PartnerAgreement);
        END;
    END;

    PROCEDURE CreateXML@1000000003(VAR XMLDOMNode@1000000010 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";OutboundJobNoticeRec@1000000000 : Record 11020544;PartnerAgreement@1000000005 : Record 11020547);
    VAR
      OutboundJobNoticeLineRec@1000000007 : Record 11020545;
      RetNode@1000000006 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      ProjectStepNode@1000000001 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      ActivityNode@1000000004 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      LocatieNode@1000000008 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      PlannerUnitNode@1000000011 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      ObjectNode@1000000014 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      prebontype@1100485000 : Integer;
    BEGIN
      //XMLDOMManagement4PS.SetNameSpace(PartnerAgreement."XML Document Namespace");

      XMLDOMManagement4PS.AddAttribute(XMLDOMNode,'ordertype','onderhoud');

      XMLDOMManagement4PS.AddElement(XMLDOMNode,'customdata','',PartnerAgreement."XML Document Namespace",RetNode);
      XMLDOMManagement4PS.AddAttribute(RetNode,'werkorder',OutboundJobNoticeRec."Project No.");
      XMLDOMManagement4PS.AddAttribute(RetNode,'erp_serviceorder',OutboundJobNoticeRec."No.");
      prebontype := OutboundJobNoticeRec."Bon Type" + 1;
      XMLDOMManagement4PS.AddAttribute(RetNode,'bon_type',FORMAT(prebontype));
      XMLDOMManagement4PS.AddAttribute(RetNode,'naam_werk',COPYSTR(OutboundJobNoticeRec."Job Description",1,45));
      XMLDOMManagement4PS.AddAttribute(RetNode,'contactpersoon',OutboundJobNoticeRec."Contact Person");
      XMLDOMManagement4PS.AddAttribute(RetNode,'telefoon',OutboundJobNoticeRec."Contact Phone");
      XMLDOMManagement4PS.AddAttribute(RetNode,'fax',OutboundJobNoticeRec."Contact Fax");
      XMLDOMManagement4PS.AddAttribute(RetNode,'email',OutboundJobNoticeRec."Contact Email");
      XMLDOMManagement4PS.AddAttribute(RetNode,'opmerking',OutboundJobNoticeRec.Comment);

      //* projectstep
      XMLDOMManagement4PS.AddElement(XMLDOMNode,'projectstep','',PartnerAgreement."XML Document Namespace",ProjectStepNode);
      XMLDOMManagement4PS.AddElement(ProjectStepNode,'customdata','',PartnerAgreement."XML Document Namespace",RetNode);
      XMLDOMManagement4PS.AddAttribute(RetNode,'meldtekst',OutboundJobNoticeRec."Call Text");

      //* location
      XMLDOMManagement4PS.AddElement(ProjectStepNode,'location_ref','',PartnerAgreement."XML Document Namespace",LocatieNode);
      XMLDOMManagement4PS.AddElement(LocatieNode,'customdata','',PartnerAgreement."XML Document Namespace",RetNode);
      IF OutboundJobNoticeRec."Plant Location" <> '' THEN //jth 24-03-2006
        XMLDOMManagement4PS.AddAttribute(RetNode,'locationcode',OutboundJobNoticeRec."Plant Location")
      ELSE
        XMLDOMManagement4PS.AddAttribute(RetNode,'locationcode',OutboundJobNoticeRec."Customer No.");

      //* plannerunit
      XMLDOMManagement4PS.AddElement(ProjectStepNode,'plannerunit_ref','',PartnerAgreement."XML Document Namespace",PlannerUnitNode);
      XMLDOMManagement4PS.AddElement(PlannerUnitNode,'customdata','',PartnerAgreement."XML Document Namespace",RetNode);
      XMLDOMManagement4PS.AddAttribute(RetNode,'erp_rayonnummer',OutboundJobNoticeRec."District Code");

      //* activities
      OutboundJobNoticeLineRec.SETRANGE("Outbound Document No.",OutboundJobNoticeRec."Outbound Document No.");
      IF OutboundJobNoticeLineRec.FIND('-') THEN
        REPEAT
          XMLDOMManagement4PS.AddElement(ProjectStepNode,'activity','',PartnerAgreement."XML Document Namespace",ActivityNode);
          XMLDOMManagement4PS.AddElement(ActivityNode,'customdata','',PartnerAgreement."XML Document Namespace",RetNode);
          XMLDOMManagement4PS.AddAttribute(RetNode,'erp_activiteit',((FORMAT(OutboundJobNoticeLineRec."Job Notice No.")) +
                                (FORMAT(OutboundJobNoticeLineRec."Line No."))));
          XMLDOMManagement4PS.AddAttribute(RetNode,'info_van_planner',OutboundJobNoticeLineRec."Info Planner");
          XMLDOMManagement4PS.AddAttribute(RetNode,'oorzaak',OutboundJobNoticeLineRec.Cause);

          //* object data
          XMLDOMManagement4PS.AddElement(ActivityNode,'imosobject_ref','',PartnerAgreement."XML Document Namespace",ObjectNode);
          XMLDOMManagement4PS.AddElement(ObjectNode,'customdata','',PartnerAgreement."XML Document Namespace",RetNode);
          IF OutboundJobNoticeLineRec."Line Type" = OutboundJobNoticeLineRec."Line Type"::Plant THEN
            XMLDOMManagement4PS.AddAttribute(RetNode,'erp_installatiecode',OutboundJobNoticeLineRec."CP Plant Number")
          ELSE
            XMLDOMManagement4PS.AddAttribute(RetNode,'erp_installatiecode',OutboundJobNoticeLineRec."Plant No.");

        UNTIL OutboundJobNoticeLineRec.NEXT=0;
    END;

    EVENT XMLDOMDocument@1000000000::NodeInserting@93(sender@1100525001 : Variant;e@1100525000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT XMLDOMDocument@1000000000::NodeInserted@94(sender@1100525001 : Variant;e@1100525000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT XMLDOMDocument@1000000000::NodeRemoving@95(sender@1100525001 : Variant;e@1100525000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT XMLDOMDocument@1000000000::NodeRemoved@96(sender@1100525001 : Variant;e@1100525000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT XMLDOMDocument@1000000000::NodeChanging@97(sender@1100525001 : Variant;e@1100525000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT XMLDOMDocument@1000000000::NodeChanged@98(sender@1100525001 : Variant;e@1100525000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    BEGIN
    END.
  }
}

