OBJECT Codeunit 5979 Service-Post and Send
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    TableNo=5900;
    OnRun=BEGIN
            ServiceHeader.COPY(Rec);
            Code;
            Rec := ServiceHeader;
          END;

  }
  CODE
  {
    VAR
      ServiceHeader@1003 : Record 5900;
      NotSupportedDocumentTypeErr@1012 : TextConst '@@@="%1=Document Type e.g. Invoice";ENU=Document type %1 is not supported.;NOR=Dokumenttypen %1 st›ttes ikke.;SVE=Dokumenttyp %1 st”ds inte.';

    LOCAL PROCEDURE Code@2();
    VAR
      TempDocumentSendingProfile@1004 : TEMPORARY Record 60;
      ServicePost@1002 : Codeunit 5980;
    BEGIN
      OnBeforeCode(ServiceHeader);

      WITH ServiceHeader DO
        CASE "Document Type" OF
          "Document Type"::Invoice,
          "Document Type"::"Credit Memo":
            IF NOT ConfirmPostAndSend(ServiceHeader,TempDocumentSendingProfile) THEN
              EXIT;
          ELSE
            ERROR(NotSupportedDocumentTypeErr,"Document Type");
        END;

      TempDocumentSendingProfile.CheckElectronicSendingEnabled;
      ValidateElectronicFormats(TempDocumentSendingProfile);

      CODEUNIT.RUN(CODEUNIT::"Service-Post",ServiceHeader);

      OnAfterPostAndBeforeSend(ServiceHeader);
      COMMIT;

      ServicePost.SendPostedDocumentRecord(ServiceHeader,TempDocumentSendingProfile);
    END;

    LOCAL PROCEDURE ConfirmPostAndSend@5(ServiceHeader@1000 : Record 5900;VAR TempDocumentSendingProfile@1001 : TEMPORARY Record 60) : Boolean;
    VAR
      Customer@1004 : Record 18;
      DocumentSendingProfile@1002 : Record 60;
    BEGIN
      Customer.GET(ServiceHeader."Bill-to Customer No.");
      IF NOT DocumentSendingProfile.GET(Customer."Document Sending Profile") THEN
        DocumentSendingProfile.GetDefault(DocumentSendingProfile);

      COMMIT;
      TempDocumentSendingProfile.COPY(DocumentSendingProfile);
      TempDocumentSendingProfile.SetDocumentUsage(ServiceHeader);
      TempDocumentSendingProfile.INSERT;
      IF PAGE.RUNMODAL(PAGE::"Post and Send Confirmation",TempDocumentSendingProfile) <> ACTION::Yes THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ValidateElectronicFormats@3(DocumentSendingProfile@1000 : Record 60);
    VAR
      ElectronicDocumentFormat@1001 : Record 61;
    BEGIN
      IF (DocumentSendingProfile."E-Mail" <> DocumentSendingProfile."E-Mail"::No) AND
         (DocumentSendingProfile."E-Mail Attachment" <> DocumentSendingProfile."E-Mail Attachment"::PDF)
      THEN BEGIN
        ElectronicDocumentFormat.ValidateElectronicFormat(DocumentSendingProfile."E-Mail Format");
        ElectronicDocumentFormat.ValidateElectronicServiceDocument(ServiceHeader,DocumentSendingProfile."E-Mail Format");
      END;

      IF (DocumentSendingProfile.Disk <> DocumentSendingProfile.Disk::No) AND
         (DocumentSendingProfile.Disk <> DocumentSendingProfile.Disk::PDF)
      THEN BEGIN
        ElectronicDocumentFormat.ValidateElectronicFormat(DocumentSendingProfile."Disk Format");
        ElectronicDocumentFormat.ValidateElectronicServiceDocument(ServiceHeader,DocumentSendingProfile."Disk Format");
      END;

      IF DocumentSendingProfile."Electronic Document" <> DocumentSendingProfile."Electronic Document"::No THEN BEGIN
        ElectronicDocumentFormat.ValidateElectronicFormat(DocumentSendingProfile."Electronic Format");
        ElectronicDocumentFormat.ValidateElectronicServiceDocument(ServiceHeader,DocumentSendingProfile."Electronic Format");
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPostAndBeforeSend@1(VAR ServiceHeader@1000 : Record 5900);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCode@4(VAR ServiceHeader@1000 : Record 5900);
    BEGIN
    END;

    BEGIN
    END.
  }
}

