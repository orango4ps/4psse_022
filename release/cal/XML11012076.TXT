OBJECT XMLport 11012076 Import GL Entries AFAS
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Import GL Entries AFAS;
               SVE=Importera redovisningstransaktioner AFAS];
    Direction=Import;
    DefaultFieldsValidation=No;
    PreserveWhiteSpace=Yes;
    TextEncoding=WINDOWS;
    OnInitXMLport=BEGIN
                    HumanResourcesSetup.GET;
                    HumanResourcesSetup.TESTFIELD("Pathname Files from Salary");
                    HumanResourcesSetup.TESTFIELD("GL Entry Journal Template");
                    HumanResourcesSetup.TESTFIELD("GL Entry Journal Batch");

                    GLSetup.GET;

                    currXMLport.FILENAME(HumanResourcesSetup."Pathname Files from Salary");
                  END;

    OnPreXMLport=BEGIN
                   GenJnlLine.LOCKTABLE;
                   GenJnlLine.SETRANGE("Journal Template Name",HumanResourcesSetup."GL Entry Journal Template");
                   GenJnlLine.SETRANGE("Journal Batch Name",HumanResourcesSetup."GL Entry Journal Batch");
                   IF GenJnlLine.FIND('+') THEN
                     LineCounter := GenJnlLine."Line No."
                   ELSE
                     LineCounter := 0;

                   JnlTemplRec.GET(HumanResourcesSetup."GL Entry Journal Template");
                   JnlBatchRec.GET(HumanResourcesSetup."GL Entry Journal Template", HumanResourcesSetup."GL Entry Journal Batch");

                   IF JnlBatchRec."No. Series" <> '' THEN
                     DocNo := NoSeriesMgt.GetNextNo(JnlBatchRec."No. Series",0D,FALSE)
                   ELSE
                     IF JnlTemplRec."No. Series" <> '' THEN
                       DocNo := NoSeriesMgt.GetNextNo(JnlTemplRec."No. Series",0D,FALSE);
                 END;

    OnPostXMLport=BEGIN
                    OldFile := currXMLport.FILENAME;
                    NewFile := HumanResourcesSetup.DetermineBackupFile(OldFile);
                    IF NewFile <> OldFile THEN
                      FileMgt.RenameClientFile(OldFile, NewFile, TRUE);
                  END;

    Format=Variable Text;
    FieldDelimiter=<None>;
    FieldSeparator=[;];
    UseRequestPage=No;
  }
  ELEMENTS
  {
    { [{5CA0C9E0-7B29-44BA-ACE2-F569DDE033EF}];  ;Root                ;Element ;Text     }

    { [{C6A46525-F3F9-45B4-9D95-155B0A0C73D2}];1 ;GenJournalLine      ;Element ;Table   ;
                                                  SourceTable=Table81;
                                                  SourceTableView=SORTING(Field1,Field51,Field2);
                                                  Import::OnAfterInitRecord=BEGIN
                                                                              "Gen. Journal Line".INIT;
                                                                              CLEAR(DescTxt);
                                                                              CLEAR(DebitDecTxt);
                                                                              CLEAR(CreditDecTxt);
                                                                              CLEAR(Dimension3Code);
                                                                            END;

                                                  Import::OnBeforeInsertRecord=VAR
                                                                                 RecRef@1100528400 : RecordRef;
                                                                               BEGIN
                                                                                 WITH "Gen. Journal Line" DO BEGIN //With statement added because this code has been copied from dataport.

                                                                                 IF AllInputFieldsAreEmpty THEN
                                                                                   currXMLport.SKIP;

                                                                                 //C027789.sn
                                                                                 IF (DebitDec = 0) AND (CreditDec = 0) THEN
                                                                                   currXMLport.SKIP;
                                                                                 //C027789.en

                                                                                 "Journal Template Name" := HumanResourcesSetup."GL Entry Journal Template";
                                                                                 "Journal Batch Name" := HumanResourcesSetup."GL Entry Journal Batch";
                                                                                 "Line No." := LineCounter + 10000;
                                                                                 LineCounter := "Line No.";
                                                                                 "Account Type" := "Account Type"::"G/L Account";
                                                                                 "Document No." := DocNo;
                                                                                 "Document Date" := "Posting Date";
                                                                                 "Source Code"   := JnlTemplRec."Source Code";
                                                                                 IF JnlBatchRec."Reason Code" <> '' THEN
                                                                                   "Reason Code" := JnlBatchRec."Reason Code"
                                                                                 ELSE
                                                                                   "Reason Code" := JnlTemplRec."Reason Code";

                                                                                 IF DebitDec <> 0 THEN
                                                                                   VALIDATE(Amount, DebitDec)
                                                                                 ELSE
                                                                                   VALIDATE(Amount, -CreditDec);

                                                                                 VALIDATE("Account No.");
                                                                                 VALIDATE("Shortcut Dimension 1 Code");
                                                                                 VALIDATE("Shortcut Dimension 2 Code");

                                                                                 IF (Dimension3Code <> '') AND (GLSetup."Shortcut Dimension 3 Code" <> '') THEN BEGIN
                                                                                   DimMgt.SetCompany("Receiving Company");  //DP00387 sn
                                                                                   "Dimension Set ID" := DimMgt.GetDimSetAddedDimVal(
                                                                                     "Dimension Set ID",
                                                                                     GLSetup."Shortcut Dimension 3 Code",
                                                                                     Dimension3Code);
                                                                                   DimMgt.SetCompany(COMPANYNAME);
                                                                                                                            //DP00387 en
                                                                                   //FIXED Fill Dimension Set ID
                                                                                   {
                                                                                   JnlLineDim.INIT;
                                                                                   JnlLineDim."Table ID" := DATABASE::"Gen. Journal Line";
                                                                                   JnlLineDim."Journal Template Name" := "Journal Template Name";
                                                                                   JnlLineDim."Journal Batch Name" := "Journal Batch Name";
                                                                                   JnlLineDim."Journal Line No." := "Line No.";
                                                                                   JnlLineDim."Dimension Code" := GLSetup."Shortcut Dimension 3 Code";
                                                                                   JnlLineDim.VALIDATE("Dimension Value Code", Dimension3Code);
                                                                                   JnlLineDim.INSERT;
                                                                                   }
                                                                                 END;

                                                                                 IF NOT GlAccRec.GET("Account No.") THEN
                                                                                   GlAccRec.INIT;
                                                                                 IF GlAccRec."Import Posting Information" = FALSE THEN
                                                                                   GlAccRec.INIT;
                                                                                 "Gen. Posting Type" := GlAccRec."Gen. Posting Type";
                                                                                 "Gen. Bus. Posting Group" := GlAccRec."Gen. Bus. Posting Group";
                                                                                 "Gen. Prod. Posting Group" := GlAccRec."Gen. Prod. Posting Group";
                                                                                 "VAT Bus. Posting Group" := GlAccRec."VAT Bus. Posting Group";
                                                                                 "VAT Prod. Posting Group" := GlAccRec."VAT Prod. Posting Group";
                                                                                 VALIDATE("VAT Prod. Posting Group");

                                                                                 Description := DescTxt;
                                                                                 "Origin Salary Application" := TRUE;

                                                                                 RecRef.GETTABLE("Gen. Journal Line");
                                                                                 XMLportStatusDialogMgt.ShowImportStatus(RecRef);

                                                                                 INSERT;

                                                                                 currXMLport.SKIP;

                                                                                 END; //Belongs to With statement.
                                                                               END;
                                                                                }

    { [{05BD980A-A84C-495E-BD82-670B183471A0}];2 ;PostingDate         ;Element ;Field   ;
                                                  DataType=Date;
                                                  SourceField=Gen. Journal Line::Posting Date;
                                                  MinOccurs=Zero }

    { [{9A19B9BE-D164-4FE4-872C-700464E399FA}];2 ;AccountNo           ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=Gen. Journal Line::Account No.;
                                                  MinOccurs=Zero }

    { [{4EC5792F-C5E4-4B6A-9832-A7349C205917}];2 ;DescTxt             ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{83DF2C18-28A2-43C5-A459-76EE922130F1}];2 ;DebitDecTxt         ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  FillEmptyTextWithZero(DebitDecTxt);
                                                                                  EVALUATE(DebitDec, DebitDecTxt);
                                                                                END;
                                                                                 }

    { [{C7EA63C7-F807-4CE2-9635-BE8F56C1AD7E}];2 ;CreditDecTxt        ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  FillEmptyTextWithZero(CreditDecTxt);
                                                                                  EVALUATE(CreditDec, CreditDecTxt);
                                                                                END;
                                                                                 }

    { [{FCED2188-0FEB-4FDF-A662-A619CFBF350C}];2 ;ShortcutDimension1Code;Element;Field  ;
                                                  DataType=Code;
                                                  SourceField=Gen. Journal Line::Shortcut Dimension 1 Code;
                                                  MinOccurs=Zero }

    { [{1F08F20E-377F-408D-976D-B6618B810ECD}];2 ;ShortcutDimension2Code;Element;Field  ;
                                                  DataType=Code;
                                                  SourceField=Gen. Journal Line::Shortcut Dimension 2 Code;
                                                  MinOccurs=Zero }

    { [{3D10B181-59E2-4D33-B808-72824DBC0551}];2 ;Dimension3Code      ;Element ;Text    ;
                                                  MinOccurs=Zero }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      HumanResourcesSetup@1100528415 : Record 5218;
      GLSetup@1100528416 : Record 98;
      GenJnlLine@1100528414 : Record 81;
      JnlTemplRec@1100528413 : Record 80;
      JnlBatchRec@1100528412 : Record 232;
      GlAccRec@1100528411 : Record 15;
      NoSeriesMgt@1100528409 : Codeunit 396;
      FileMgt@1100528400 : Codeunit 419;
      XMLportStatusDialogMgt@1100528403 : Codeunit 11130006;
      DimMgt@1100525000 : Codeunit 408;
      LineCounter@1100528407 : Integer;
      CreditDec@1100528406 : Decimal;
      DebitDec@1100528405 : Decimal;
      DocNo@1100528404 : Code[20];
      OldFile@1100528402 : Text[250];
      NewFile@1100528401 : Text[250];

    PROCEDURE FillEmptyTextWithZero@1100525013(VAR Text@1100525000 : Text[1024]);
    BEGIN
      IF DELCHR(Text,'<>=',' ') = '' THEN
        Text := '0';
    END;

    LOCAL PROCEDURE AllInputFieldsAreEmpty@1100525001() : Boolean;
    BEGIN
      EXIT(
        ("Gen. Journal Line"."Posting Date" = 0D) AND
        ("Gen. Journal Line"."Account No." = '') AND
        (DescTxt = '') AND
        (DebitDecTxt = '') AND
        (CreditDecTxt = '') AND
        ("Gen. Journal Line"."Shortcut Dimension 1 Code" = '') AND
        ("Gen. Journal Line"."Shortcut Dimension 2 Code" = '') AND
        (Dimension3Code = ''));
    END;

    BEGIN
    {
      CC: Dataport 11012346
    }
    END.
  }
}

