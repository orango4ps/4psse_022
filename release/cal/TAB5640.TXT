OBJECT Table 5640 Main Asset Component
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CheckMainAssetAllowed();  //**4PS.n
             END;

    OnDelete=BEGIN
               LockFixedAsset;
               IF "FA No." <> '' THEN BEGIN
                 FA.GET("FA No.");
                 UpdateMainAsset(FA,0);
               END;
             END;

    OnRename=BEGIN
               ERROR(Text000,TABLECAPTION);
             END;

    CaptionML=[ENU=Main Asset Component;
               NOR=Hovedaktivakomponent];
  }
  FIELDS
  {
    { 1   ;   ;Main Asset No.      ;Code20        ;TableRelation="Fixed Asset";
                                                   CaptionML=[ENU=Main Asset No.;
                                                              NOR=Hovedaktivanr.];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 3   ;   ;FA No.              ;Code20        ;TableRelation="Fixed Asset";
                                                   OnValidate=BEGIN
                                                                IF ("FA No." = '') OR ("Main Asset No." = '') THEN
                                                                  EXIT;
                                                                LockFixedAsset;
                                                                FA.GET("FA No.");
                                                                IF "FA No." = "Main Asset No." THEN
                                                                  CreateError("FA No.",1);
                                                                Description := FA.Description;
                                                                MainAssetComp.SETRANGE("Main Asset No.","FA No.");
                                                                IF MainAssetComp.FINDFIRST THEN
                                                                  CreateError("FA No.",1);
                                                                MainAssetComp.SETRANGE("Main Asset No.");
                                                                MainAssetComp.SETCURRENTKEY("FA No.");
                                                                MainAssetComp.SETRANGE("FA No.","FA No.");
                                                                IF MainAssetComp.FINDFIRST THEN
                                                                  CreateError("FA No.",2);
                                                                MainAssetComp.SETRANGE("FA No.","Main Asset No.");
                                                                IF MainAssetComp.FINDFIRST THEN
                                                                  CreateError("Main Asset No.",1);
                                                                UpdateMainAsset(FA,2);
                                                                FA.GET("Main Asset No.");
                                                                IF FA."Main Asset/Component" <> FA."Main Asset/Component"::"Main Asset" THEN BEGIN
                                                                  FA."Main Asset/Component" := FA."Main Asset/Component"::"Main Asset";
                                                                  ERROR(
                                                                    Text001,
                                                                    DepreciationCalc.FAName(FA,''),FA."Main Asset/Component");
                                                                END;
                                                                CheckMainAssetAllowed();  //**4PS.n
                                                                CheckComponentAllowed();  //**4PS.n
                                                              END;

                                                   CaptionML=[ENU=FA No.;
                                                              NOR=Aktivanr.;
                                                              SVE=Anl.nr];
                                                   NotBlank=Yes }
    { 4   ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Description] }
  }
  KEYS
  {
    {    ;Main Asset No.,FA No.                   ;Clustered=Yes }
    {    ;FA No.                                   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You cannot rename a %1.;NOR=Du kan ikke gi nytt navn til en %1.;SVE=Du kan inte byta namn p† %1.';
      Text001@1001 : TextConst 'ENU=%1 is not a %2.;NOR=%1 er ikke et %2.;SVE=%1 „r inte en %2.';
      Text002@1002 : TextConst 'ENU=%1 is a %2.;NOR=%1 er en %2.';
      FA@1003 : Record 5600;
      FADeprBook@1004 : Record 5612;
      MainAssetComp@1005 : Record 5640;
      DepreciationCalc@1006 : Codeunit 5616;
      Text003@1007 : TextConst 'ENU=Main Asset,Component;NOR=Hovedaktiva,Komponent';
      Text11012001@1100409001 : TextConst 'ENU=%1 ''%2'' is linked to %3 ''%4-%5'', components not allowed for ''%6''';
      Text11012002@1100409000 : TextConst 'ENU=%1 ''%2'' is linked to %3 ''%4-%5'', not allowed as ''Component''';

    LOCAL PROCEDURE LockFixedAsset@6();
    BEGIN
      FA.LOCKTABLE;
      FADeprBook.LOCKTABLE;
    END;

    LOCAL PROCEDURE UpdateMainAsset@1(VAR FA@1000 : Record 5600;ComponentType@1001 : ' ,Main Asset,Component');
    VAR
      FA2@1002 : Record 5600;
    BEGIN
      IF ComponentType = ComponentType::" " THEN BEGIN
        FA."Main Asset/Component" := FA."Main Asset/Component"::" ";
        FA."Component of Main Asset" := '';
      END;
      IF ComponentType = ComponentType::Component THEN BEGIN
        FA."Component of Main Asset" := "Main Asset No.";
        FA."Main Asset/Component" := FA."Main Asset/Component"::Component;
      END;
      FA.MODIFY(TRUE);
      UpdateFADeprBooks(FA);

      FA.RESET;
      FA.SETCURRENTKEY("Component of Main Asset");
      FA.SETRANGE("Component of Main Asset","Main Asset No.");
      FA.SETRANGE("Main Asset/Component",FA2."Main Asset/Component"::Component);
      FA2.GET("Main Asset No.");
      IF FA.FIND('=><') THEN BEGIN
        IF FA2."Main Asset/Component" <> FA2."Main Asset/Component"::"Main Asset" THEN BEGIN
          FA2."Main Asset/Component" := FA2."Main Asset/Component"::"Main Asset";
          FA2."Component of Main Asset" := FA2."No.";
          FA2.MODIFY(TRUE);
          UpdateFADeprBooks(FA2);
        END;
      END ELSE BEGIN
        FA2."Main Asset/Component" := FA2."Main Asset/Component"::" ";
        FA2."Component of Main Asset" := '';
        FA2.MODIFY(TRUE);
        UpdateFADeprBooks(FA2);
      END;
    END;

    LOCAL PROCEDURE UpdateFADeprBooks@3(VAR FA@1000 : Record 5600);
    BEGIN
      WITH FADeprBook DO BEGIN
        SETCURRENTKEY("FA No.");
        SETRANGE("FA No.",FA."No.");
        IF FIND('-') THEN
          REPEAT
            "Main Asset/Component" := FA."Main Asset/Component";
            "Component of Main Asset" := FA."Component of Main Asset";
            MODIFY(TRUE);
          UNTIL NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE CreateError@2(FANo@1000 : Code[20];MainAssetComponent@1001 : ' ,Main Asset,Component');
    BEGIN
      FA."No." := FANo;
      ERROR(
        Text002,
        DepreciationCalc.FAName(FA,''),SELECTSTR(MainAssetComponent,Text003));
    END;

    LOCAL PROCEDURE CheckMainAssetAllowed@1100525000();
    VAR
      FA@1100525000 : Record 5600;
      PlantNumber@1100525001 : Record 11012552;
    BEGIN
      //**4PS (C018452)
      IF FA.GET("Main Asset No.") THEN BEGIN
        FA.CALCFIELDS("Plant Type", "Plant No.");
        IF FA."Plant No." <> '' THEN BEGIN
          IF PlantNumber.GET(FA."Plant Type", FA."Plant No.") THEN BEGIN
            PlantNumber.CALCFIELDS(Bulk);
            IF PlantNumber.Bulk THEN
              ERROR(Text11012001, FIELDCAPTION("Main Asset No."), "Main Asset No.", FA.FIELDCAPTION("Plant No."),
                FA."Plant Type", FA."Plant No.", PlantNumber.FIELDCAPTION(Bulk));
          END;
        END ELSE BEGIN
          IF FA.CalcFields_PlantOrgAsset(FA, FA.FIELDNO("Plant No. (Org. Asset)")) <> '' THEN BEGIN
            IF PlantNumber.GET(FA."Plant Type (Org. Asset)", FA."Plant No. (Org. Asset)") THEN BEGIN
              PlantNumber.CALCFIELDS(Bulk);
              IF PlantNumber.Bulk THEN
                ERROR(Text11012001, FIELDCAPTION("Main Asset No."), "Main Asset No.", FA.FIELDCAPTION("Plant No."),
                  FA."Plant Type (Org. Asset)", FA."Plant No. (Org. Asset)", PlantNumber.FIELDCAPTION(Bulk));
            END;
          END;
        END;
      END;
    END;

    LOCAL PROCEDURE CheckComponentAllowed@1100525001();
    VAR
      FA@1100525000 : Record 5600;
    BEGIN
      //**4PS (C018452)
      IF FA.GET("FA No.") THEN BEGIN
        FA.CALCFIELDS("Plant Type", "Plant No.");
        IF FA."Plant No." <> '' THEN BEGIN
          ERROR(Text11012002, FIELDCAPTION("FA No."), "FA No.",
            FA.FIELDCAPTION("Plant No."), FA."Plant Type", FA."Plant No.");
        END ELSE BEGIN
          IF FA.CalcFields_PlantOrgAsset(FA, FA.FIELDNO("Plant No. (Org. Asset)")) <> '' THEN BEGIN
            ERROR(Text11012002, FIELDCAPTION("FA No."), "FA No.",
              FA.FIELDCAPTION("Plant No."), FA."Plant Type (Org. Asset)", FA."Plant No. (Org. Asset)");
          END;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

