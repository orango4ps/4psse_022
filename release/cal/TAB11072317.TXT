OBJECT Table 11072317 PB Installment Overview
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ServiceSetup@1100528601 : Record 11012800;
               NoSeriesManagement@1100528602 : Codeunit 396;
             BEGIN
               IF "No." = '' THEN BEGIN
                 ServiceSetup.GET;
                 ServiceSetup.TESTFIELD("Installment Overview Nos.");
                 NoSeriesManagement.InitSeries(ServiceSetup."Installment Overview Nos.", xRec."No. Series", 0D, "No.", "No. Series");
               END;
               DetermineSeqNo;
               "Created by" := USERID;
               "Created on" := CURRENTDATETIME;
             END;

    OnModify=BEGIN
               IF xRec.Status = Status THEN
                 TESTFIELD(Status, Status::Open);
             END;

    OnDelete=VAR
               PBInstallmentOverviewLine@1100528600 : Record 11072318;
             BEGIN
               TESTFIELD(Status, Status::Open);
               PBInstallmentOverviewLine.SETRANGE("Installment Overview No.", "No.");
               PBInstallmentOverviewLine.DELETEALL(TRUE);
             END;

    CaptionML=ENU=Price Book Installment Overview;
    LookupPageID=Page11126814;
    DrillDownPageID=Page11126814;
  }
  FIELDS
  {
    { 10  ;   ;No.                 ;Code20        ;OnValidate=VAR
                                                                ServiceSetup@1100528600 : Record 11012800;
                                                                NoSeriesManagement@1100528601 : Codeunit 396;
                                                              BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  ServiceSetup.GET;
                                                                  NoSeriesManagement.TestManual(ServiceSetup."Installment Overview Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              NOR=Nr;
                                                              SVE=Nr] }
    { 20  ;   ;Price Book Code     ;Code20        ;TableRelation="Service Price Book".Code;
                                                   OnValidate=BEGIN
                                                                DetermineSeqNo;
                                                              END;

                                                   CaptionML=[ENU=Price Book Code;
                                                              SVE=Enhetspriskod] }
    { 30  ;   ;Price Book Index Date;Date         ;TableRelation="Service Price Book"."Index Date" WHERE (Code=FIELD(Price Book Code));
                                                   OnValidate=BEGIN
                                                                DetermineSeqNo;
                                                              END;

                                                   CaptionML=[ENU=Price Book Index Date;
                                                              SVE=Indexdatum f”r enhetspris] }
    { 40  ;   ;Service Contract No.;Code20        ;TableRelation=Service-Contract;
                                                   OnValidate=BEGIN
                                                                TestIfNoLinesExist;
                                                                DetermineSeqNo;
                                                                DetermineCustomerNo;
                                                                DeterminePriceBookInfo;
                                                                DetermineStartingDate;
                                                                DetermineDescription;
                                                              END;

                                                   OnLookup=BEGIN
                                                              HandleServiceContractLookup;
                                                            END;

                                                   CaptionML=[ENU=Service Contract No.;
                                                              NOR=Servicekontraktnr;
                                                              SVE=Servicekontraktnr] }
    { 50  ;   ;Service Order No.   ;Code20        ;TableRelation="Service Order";
                                                   OnValidate=VAR
                                                                ServiceOrder@1100528601 : Record 11012823;
                                                                InstallmentOverviewMgtPB@1100528600 : Codeunit 11012887;
                                                              BEGIN
                                                                IF "Service Order No." <> '' THEN BEGIN
                                                                  ServiceOrder.GET("Service Order No.");
                                                                  ServiceOrder.TESTFIELD("Settlement Method", ServiceOrder."Settlement Method"::"Installment Overview by Service Order");
                                                                  IF ServiceOrder."Service Contract No." <> '' THEN
                                                                    "Service Contract No." := ServiceOrder."Service Contract No.";
                                                                  IF NOT ServiceOrder."Base Service Order" AND InstallmentOverviewMgtPB.HasBaseServiceOrderSameRequirements(ServiceOrder."No.") THEN
                                                                    ERROR(Text003, ServiceOrder."No.", ServiceOrder."Base Service Order No.");
                                                                END;
                                                                TESTFIELD("Service Contract No.");
                                                                TestIfNoLinesExist;
                                                                DetermineSeqNo;
                                                                DetermineCustomerNo;
                                                                DeterminePriceBookInfo;
                                                                DetermineStartingDate;
                                                                DetermineDescription;
                                                              END;

                                                   OnLookup=BEGIN
                                                              HandleServiceOrderLookup;
                                                            END;

                                                   CaptionML=[ENU=Service Order No.;
                                                              NOR=Serviceordrenr.;
                                                              SVE=Serviceordernr] }
    { 60  ;   ;Seq. No.            ;Integer       ;CaptionML=[ENU=Seq. No.;
                                                              NOR=Sekv. nr;
                                                              SVE=Sekv. nr];
                                                   MinValue=1;
                                                   NotBlank=Yes }
    { 70  ;   ;Status              ;Option        ;OnValidate=BEGIN
                                                                CheckStatus;
                                                                ProcessStatus;
                                                              END;

                                                   CaptionML=[ENU=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=ENU=Open,,,Offered,,,Approved,,,Rejected,,,Processed;
                                                   OptionString=Open,,,Offered,,,Approved,,,Rejected,,,Processed }
    { 80  ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                TestIfNoLinesExist;
                                                                DetermineEndingDate;
                                                                CheckDates;
                                                              END;

                                                   CaptionML=[ENU=Starting Date;
                                                              NOR=Startdato;
                                                              SVE=Startdatum] }
    { 90  ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                TestIfNoLinesExist;
                                                                CheckDates;
                                                              END;

                                                   CaptionML=[ENU=Ending Date;
                                                              NOR=Sluttdato;
                                                              SVE=Slutdatum] }
    { 100 ;   ;Created on          ;DateTime      ;CaptionML=[ENU=Created on;
                                                              NOR=Opprettet den;
                                                              SVE=Skapades];
                                                   Editable=No }
    { 110 ;   ;Created by          ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Created by;
                                                              NOR=Opprettet av;
                                                              SVE=Skapad av];
                                                   Editable=No }
    { 120 ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              NOR=Nr-serie;
                                                              SVE=Nr-serie];
                                                   Editable=No }
    { 130 ;   ;Bill-to Customer No.;Code20        ;TableRelation=Customer;
                                                   CaptionML=[ENU=Bill-to Customer No.;
                                                              NOR=Faktura til-kundenr.;
                                                              SVE=Faktureringskundnr];
                                                   Editable=No }
    { 140 ;   ;Period              ;DateFormula   ;OnValidate=BEGIN
                                                                TestIfNoLinesExist;
                                                                DetermineEndingDate;
                                                              END;

                                                   CaptionML=[ENU=Period;
                                                              NOR=Periode;
                                                              SVE=Period] }
    { 150 ;   ;Customer No.        ;Code20        ;TableRelation=Customer;
                                                   OnValidate=VAR
                                                                Customer@1100528600 : Record 18;
                                                              BEGIN
                                                                IF "Customer No." = '' THEN
                                                                  VALIDATE("Bill-to Customer No.", '')
                                                                ELSE BEGIN
                                                                  Customer.GET("Customer No.");
                                                                  IF Customer."Bill-to Customer No." <> '' THEN
                                                                    VALIDATE("Bill-to Customer No.", Customer."Bill-to Customer No.");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Customer No.;
                                                              NOR=Kundenr.;
                                                              SVE=Kundnr];
                                                   Editable=No }
    { 220 ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 230 ;   ;Total Overview Amount;Decimal      ;FieldClass=FlowField;
                                                   CalcFormula=Sum("PB Installment Overview Line"."Overview Amount" WHERE (Installment Overview No.=FIELD(No.)));
                                                   CaptionML=ENU=Total Overview Amount;
                                                   Editable=No }
    { 240 ;   ;Install. Collect. Overview No.;Code20;
                                                   TableRelation="PB Install. Collect. Overview" WHERE (Price Book Code=FIELD(Price Book Code),
                                                                                                        Price Book Index Date=FIELD(Price Book Index Date),
                                                                                                        Service Contract No.=FIELD(Service Contract No.),
                                                                                                        Service Order No.=FIELD(Service Order No.));
                                                   CaptionML=[ENU=Installment Collection Overview No.;
                                                              SVE=Betalplanssamling, ”versiktsnummer];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Price Book Code,Price Book Index Date,Service Contract No.,Service Order No. }
    {    ;Service Contract No.,Service Order No.   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528601 : TextConst 'ENU=must occur before %1;NOR=m† inntreffe f›r %1;SVE=m†ste intr„ffa f”re %1';
      Text001@1100528600 : TextConst 'ENU=Lines already exist, changing not allowed';
      Text003@1100528602 : TextConst 'ENU=Service Order %1 will be included by invoicing Base Service Order %2';
      Text004@1100528603 : TextConst 'ENU=Changing %1 to %2 is final and cannot be undone, continue?';
      Text005@1100528604 : TextConst 'ENU=Status %1 is final and cannot be changed';

    LOCAL PROCEDURE DetermineStartingDate@1100528601();
    VAR
      PBInstallmentOverview@1100528600 : Record 11072317;
    BEGIN
      PBInstallmentOverview.SETFILTER("No.", '<>%1', "No.");
      PBInstallmentOverview.SETRANGE("Price Book Code", "Price Book Code");
      PBInstallmentOverview.SETRANGE("Price Book Index Date", "Price Book Index Date");
      PBInstallmentOverview.SETRANGE("Service Order No.", "Service Order No.");
      PBInstallmentOverview.SETFILTER("Seq. No.", '<%1', "Seq. No.");
      PBInstallmentOverview.SETFILTER(Status, '<>%1', PBInstallmentOverview.Status::Rejected);
      IF PBInstallmentOverview.FINDLAST THEN
        IF PBInstallmentOverview."Ending Date" > 0D THEN BEGIN
          VALIDATE("Starting Date", PBInstallmentOverview."Ending Date" +1);
          IF FORMAT(PBInstallmentOverview.Period) <> '' THEN
            VALIDATE(Period, PBInstallmentOverview.Period);
      END;
    END;

    LOCAL PROCEDURE DetermineEndingDate@1100528606();
    BEGIN
      "Ending Date" := 0D;
      IF ("Starting Date" <> 0D) AND (FORMAT(Period) <> '') THEN
        VALIDATE("Ending Date", CALCDATE(Period, "Starting Date") -1);
    END;

    LOCAL PROCEDURE DetermineSeqNo@1100528605();
    VAR
      PBInstallmentOverview@1100528600 : Record 11072317;
    BEGIN
      PBInstallmentOverview.SETCURRENTKEY("Price Book Code");
      PBInstallmentOverview.SETRANGE("Price Book Code", "Price Book Code");
      PBInstallmentOverview.SETRANGE("Price Book Index Date", "Price Book Index Date");
      PBInstallmentOverview.SETRANGE("Service Contract No.", "Service Contract No.");
      PBInstallmentOverview.SETRANGE("Service Order No.", "Service Order No.");
      IF PBInstallmentOverview.FINDLAST THEN
        "Seq. No." := PBInstallmentOverview."Seq. No." +1
      ELSE
        "Seq. No." := 1;
    END;

    LOCAL PROCEDURE DetermineCustomerNo@1100528603();
    VAR
      ServiceContract@1100528600 : Record 11012812;
      ServiceOrder@1100528601 : Record 11012823;
    BEGIN
      CASE TRUE OF
        "Service Order No." <> '':
          BEGIN
            IF NOT ServiceOrder.GET("Service Order No.") THEN
              ServiceOrder.INIT;
            VALIDATE("Customer No.", ServiceOrder."Customer No.");
            VALIDATE("Bill-to Customer No.", ServiceOrder."Bill-to Customer No.");
          END;
        "Service Contract No." <> '':
          BEGIN
            IF NOT ServiceContract.GET("Service Contract No.") THEN
              ServiceContract.INIT;
            VALIDATE("Customer No.", ServiceContract."Customer No.");
            VALIDATE("Bill-to Customer No.", ServiceContract."Bill-to Customer No. (CP)");
          END;
      END;
    END;

    LOCAL PROCEDURE DeterminePriceBookInfo@1100528604();
    VAR
      ServiceContract@1100528600 : Record 11012812;
      ServiceOrderExtension@1100528602 : Record 11071727;
    BEGIN
      IF "Price Book Code" <> '' THEN
        EXIT;
      CASE TRUE OF
        "Service Order No." <> '':
          BEGIN
            ServiceOrderExtension.GetServOrderExtension("Service Order No.");
            ServiceOrderExtension.TESTFIELD("Price Book Code");
            VALIDATE("Price Book Code", ServiceOrderExtension."Price Book Code");
            VALIDATE("Price Book Index Date", ServiceOrderExtension."Price Book Index Date");
          END;
        "Service Contract No." <> '':
          BEGIN
            IF NOT ServiceContract.GET("Service Contract No.") THEN
              ServiceContract.INIT;
            ServiceContract.TESTFIELD("Price Book Code");
            VALIDATE("Price Book Code", ServiceContract."Price Book Code");
            VALIDATE("Price Book Index Date", ServiceContract."Price Book Index Date");
          END;
      END;
    END;

    LOCAL PROCEDURE DetermineDescription@1100528615();
    VAR
      PBInstallmentOverview@1100528600 : Record 11072317;
    BEGIN
      PBInstallmentOverview.SETFILTER("No.", '<>%1', "No.");
      PBInstallmentOverview.SETRANGE("Price Book Code", "Price Book Code");
      PBInstallmentOverview.SETRANGE("Price Book Index Date", "Price Book Index Date");
      PBInstallmentOverview.SETRANGE("Service Order No.", "Service Order No.");
      PBInstallmentOverview.SETFILTER("Seq. No.", '<%1', "Seq. No.");
      IF PBInstallmentOverview.FINDLAST THEN
        IF PBInstallmentOverview.Description <> '' THEN
          VALIDATE(Description, PBInstallmentOverview.Description);
    END;

    LOCAL PROCEDURE CheckDates@1100528600();
    BEGIN
      IF ("Starting Date" = 0D) OR ("Ending Date" = 0D) THEN
        EXIT;
      IF "Starting Date" > "Ending Date" THEN
        FIELDERROR("Starting Date", STRSUBSTNO(Text000, FIELDCAPTION("Ending Date")));
      IF FORMAT(Period) <> '' THEN
        IF CALCDATE(Period, "Starting Date") -1 <> "Ending Date" THEN
          CLEAR(Period);
    END;

    PROCEDURE TestIfNoLinesExist@1100528602();
    VAR
      PBInstallmentOverviewLine@1100528600 : Record 11072318;
    BEGIN
      PBInstallmentOverviewLine.SETRANGE("Installment Overview No.", "No.");
      IF PBInstallmentOverviewLine.ISEMPTY THEN
        EXIT;
      ERROR(Text001);
    END;

    LOCAL PROCEDURE HandleServiceContractLookup@1100528609();
    VAR
      ServiceContract@1100528601 : Record 11012812;
    BEGIN
      IF "Price Book Code" <> '' THEN BEGIN
        ServiceContract.SETRANGE("Price Book Code", "Price Book Code");
        ServiceContract.SETRANGE("Price Book Index Date", "Price Book Index Date");
      END ELSE BEGIN
        ServiceContract.SETFILTER("Price Book Code", '<>%1', '');
        ServiceContract.SETFILTER("Price Book Index Date", '<>%1', 0D);
      END;
      IF "Service Contract No." <> '' THEN
        ServiceContract.GET("Service Contract No.");
      IF PAGE.RUNMODAL(0, ServiceContract) = ACTION::LookupOK THEN
        VALIDATE("Service Contract No.", ServiceContract."No.");
    END;

    LOCAL PROCEDURE HandleServiceOrderLookup@1100528607();
    VAR
      ServiceOrder@1100528601 : Record 11012823;
      ServiceOrderExtension@1100528600 : Record 11071727;
    BEGIN
      ServiceOrder.SETCURRENTKEY("Service Contract No.");
      ServiceOrder.SETRANGE("Service Contract No.", "Service Contract No.");
      IF ServiceOrder.FINDSET THEN
        REPEAT
          ServiceOrderExtension.GetServOrderExtension(ServiceOrder."No.");
          IF (ServiceOrderExtension."Price Book Code" = "Price Book Code") AND
             (ServiceOrderExtension."Price Book Index Date" = "Price Book Index Date") AND
             (ServiceOrder."Settlement Method" = ServiceOrder."Settlement Method"::"Installment Overview by Service Order")
          THEN
            ServiceOrder.MARK(TRUE);
        UNTIL ServiceOrder.NEXT = 0;
      ServiceOrder.SETRANGE("Service Contract No.");
      ServiceOrder.MARKEDONLY(TRUE);
      ServiceOrder.SETCURRENTKEY("No.");
      IF PAGE.RUNMODAL(0, ServiceOrder) = ACTION::LookupOK THEN
        VALIDATE("Service Order No.", ServiceOrder."No.");
    END;

    PROCEDURE GetTotalSalesAmount@1100528608() : Decimal;
    VAR
      PBInstallmentOverviewLine@1100528600 : Record 11072318;
    BEGIN
      PBInstallmentOverviewLine.SETRANGE("Installment Overview No.", "No.");
      PBInstallmentOverviewLine.CALCSUMS("Overview Amount");
      EXIT(PBInstallmentOverviewLine."Overview Amount");
    END;

    LOCAL PROCEDURE CheckStatus@1100528610();
    VAR
      PBInstallmentOverviewMgt@1100528600 : Codeunit 11012887;
    BEGIN
      IF xRec.Status <> Status THEN BEGIN
        IF xRec.Status = xRec.Status::Open THEN
          PBInstallmentOverviewMgt.RecalculateSurcharges(Rec, FALSE, TRUE);
        CASE Status OF
          Status::Offered,
          Status::Approved:
            BEGIN
              TESTFIELD("Service Contract No.");
              TESTFIELD("Bill-to Customer No.");
              TESTFIELD("Customer No.");
            END;
          Status::Processed:
            IF CurrFieldNo = FIELDNO(Status) THEN
              FIELDERROR(Status);
        END;
        CASE xRec.Status OF
          xRec.Status::Approved:
            IF Status <> Status::Processed THEN
              TESTFIELD("Install. Collect. Overview No.", '');
          xRec.Status::Processed:
            IF (CurrFieldNo = FIELDNO(Status)) THEN
              FIELDERROR(Status);
          xRec.Status::Rejected:
            ERROR(Text005, xRec.Status);
        END;
      END;
    END;

    LOCAL PROCEDURE ProcessStatus@1100528614();
    VAR
      SUPProduction@1100528601 : Record 11071773;
      SUPProductionLine@1100528600 : Record 11071774;
      ServiceOrderCostPlusEntry@1100528602 : Record 11012825;
    BEGIN
      CASE Status OF
        Status::Approved:
          BEGIN
            SUPProductionLine.SETCURRENTKEY("Installment Overview No.");
            SUPProductionLine.SETRANGE("Installment Overview No.", "No.");
            SUPProductionLine.SETRANGE("SUP Production Status", SUPProductionLine."SUP Production Status"::Offered);
            IF SUPProductionLine.FINDSET THEN
              REPEAT
                IF SUPProduction.GET(SUPProductionLine."Service Order No.", SUPProductionLine."Production Seq. No.") THEN
                  SUPProduction.MARK(TRUE);
              UNTIL SUPProductionLine.NEXT = 0;
            SUPProduction.MARKEDONLY(TRUE);
            IF SUPProduction.FINDSET(TRUE) THEN
              REPEAT
                SUPProduction.VALIDATE(Status, SUPProduction.Status::Approved);
                SUPProduction.MODIFY(TRUE);
              UNTIL SUPProduction.NEXT = 0;
          END;
        Status::Rejected:
          BEGIN
            IF CONFIRM(STRSUBSTNO(Text004, FIELDCAPTION(Status), Status)) THEN BEGIN
              SUPProductionLine.SETCURRENTKEY("Installment Overview No.");
              SUPProductionLine.SETRANGE("Installment Overview No.", "No.");
              IF SUPProductionLine.FINDSET(TRUE) THEN
                REPEAT
                  SUPProductionLine."Installment Overview No." := '';
                  SUPProductionLine."Installment Overview Line No." := 0;
                  SUPProductionLine.MODIFY;
                UNTIL SUPProductionLine.NEXT = 0;

              ServiceOrderCostPlusEntry.SETCURRENTKEY("Installment Overview No.");
              ServiceOrderCostPlusEntry.SETRANGE("Installment Overview No.", "No.");
              IF ServiceOrderCostPlusEntry.FINDSET(TRUE) THEN
                REPEAT
                  ServiceOrderCostPlusEntry."Installment Overview No." := '';
                  ServiceOrderCostPlusEntry."Installment Overview Line No." := 0;
                  ServiceOrderCostPlusEntry.Chargeable := TRUE;
                  ServiceOrderCostPlusEntry.MODIFY;
                UNTIL ServiceOrderCostPlusEntry.NEXT = 0;
            END ELSE
              Status := xRec.Status;
          END;
      END;
    END;

    LOCAL PROCEDURE GetTotal@1100528617(VAR IPBInstallmentOverviewLine@1100528600 : Record 11072318;IAmountType@1100528602 : 'Previous Overviews,Current,Upto Current Overview') : Decimal;
    VAR
      TotalAmount@1100528601 : Decimal;
    BEGIN
      IF IPBInstallmentOverviewLine.FINDSET THEN
        REPEAT
          CASE IAmountType OF
            IAmountType::"Previous Overviews":
              BEGIN
                IPBInstallmentOverviewLine.CALCFIELDS("Amount incl. Previous Overview");
                TotalAmount += IPBInstallmentOverviewLine."Amount incl. Previous Overview";
              END;
            IAmountType::Current:
              TotalAmount += IPBInstallmentOverviewLine."Overview Amount";
            IAmountType::"Upto Current Overview":
              TotalAmount += IPBInstallmentOverviewLine.GetAmountinclOverview;
          END;
        UNTIL IPBInstallmentOverviewLine.NEXT = 0;
      EXIT(TotalAmount);
    END;

    PROCEDURE GetTotalPriceBookAmount@1100528611(IAmountType@1100528601 : 'Previous Overviews,Current,Upto Current Overview') : Decimal;
    VAR
      PBInstallmentOverviewLine@1100528600 : Record 11072318;
    BEGIN
      PBInstallmentOverviewLine.SETRANGE("Installment Overview No.", "No.");
      PBInstallmentOverviewLine.SETRANGE("Installm. Over. Seq.No. Filter", "Seq. No." -1);
      PBInstallmentOverviewLine.SETFILTER("Line Type", '%1|%2|%3',
        PBInstallmentOverviewLine."Line Type"::Initial,
        PBInstallmentOverviewLine."Line Type"::Uncoded,
        PBInstallmentOverviewLine."Line Type"::Administrative);
      EXIT(GetTotal(PBInstallmentOverviewLine, IAmountType));
    END;

    PROCEDURE GetTotalExtraDeclarationAmount@1100528612(IAmountType@1100528601 : 'Previous Overviews,Current,Upto Current Overview') : Decimal;
    VAR
      PBInstallmentOverviewLine@1100528600 : Record 11072318;
    BEGIN
      PBInstallmentOverviewLine.SETRANGE("Installment Overview No.", "No.");
      PBInstallmentOverviewLine.SETRANGE("Installm. Over. Seq.No. Filter", "Seq. No." -1);
      PBInstallmentOverviewLine.SETRANGE("Line Type", PBInstallmentOverviewLine."Line Type"::"Extra Declarations");
      EXIT(GetTotal(PBInstallmentOverviewLine, IAmountType));
    END;

    PROCEDURE GetTotalSurchargeAmount@1100528613(IAmountType@1100528601 : 'Previous Overviews,Current,Upto Current Overview') : Decimal;
    VAR
      PBInstallmentOverviewLine@1100528600 : Record 11072318;
    BEGIN
      PBInstallmentOverviewLine.SETRANGE("Installment Overview No.", "No.");
      PBInstallmentOverviewLine.SETRANGE("Installm. Over. Seq.No. Filter", "Seq. No." -1);
      PBInstallmentOverviewLine.SETRANGE("Line Type", PBInstallmentOverviewLine."Line Type"::Surcharge);
      EXIT(GetTotal(PBInstallmentOverviewLine, IAmountType));
    END;

    PROCEDURE GetTotalAmountByType@1100528619(IAmountKind@1100528600 : 'Price Book,Extra Declarations,Surcharges';IAmountType@1100528601 : 'Previous Overviews,Current,Upto Current Overview') : Decimal;
    VAR
      PBInstallmentOverview@1100528604 : Record 11072317;
      PBInstallmentOverviewLine@1100528603 : Record 11072318;
      TotalAmount@1100528602 : Decimal;
    BEGIN
      IF IAmountType = IAmountType::Current THEN
        PBInstallmentOverview.SETRANGE("No.", "No.")
      ELSE BEGIN
        PBInstallmentOverview.SETCURRENTKEY("Price Book Code");
        PBInstallmentOverview.SETRANGE("Price Book Code", "Price Book Code");
        PBInstallmentOverview.SETRANGE("Price Book Index Date", "Price Book Index Date");
        PBInstallmentOverview.SETRANGE("Service Contract No.", "Service Contract No.");
        PBInstallmentOverview.SETRANGE("Service Order No.", "Service Order No.");
        PBInstallmentOverview.SETRANGE("Customer No.", "Customer No.");
        PBInstallmentOverview.SETRANGE("Bill-to Customer No.", "Bill-to Customer No.");
        IF IAmountType = IAmountType::"Previous Overviews" THEN
          PBInstallmentOverview.SETFILTER("Seq. No.", '<%1', "Seq. No.")
        ELSE
          PBInstallmentOverview.SETRANGE("Seq. No.", 0, "Seq. No.");
        PBInstallmentOverview.SETFILTER(Status, '<>%1', PBInstallmentOverview.Status::Rejected);
      END;
      IF PBInstallmentOverview.FINDSET THEN
        REPEAT
          PBInstallmentOverviewLine.SETRANGE("Installment Overview No.", PBInstallmentOverview."No.");
          PBInstallmentOverviewLine.SETRANGE("Installm. Over. Seq.No. Filter", 0, PBInstallmentOverview."Seq. No." -1);
          CASE IAmountKind OF
            IAmountKind::"Price Book":
              PBInstallmentOverviewLine.SETFILTER("Line Type", '%1|%2|%3',
                PBInstallmentOverviewLine."Line Type"::Initial,
                PBInstallmentOverviewLine."Line Type"::Uncoded,
                PBInstallmentOverviewLine."Line Type"::Administrative);
            IAmountKind::"Extra Declarations":
              PBInstallmentOverviewLine.SETRANGE("Line Type", PBInstallmentOverviewLine."Line Type"::"Extra Declarations");
            IAmountKind::Surcharges:
              PBInstallmentOverviewLine.SETRANGE("Line Type", PBInstallmentOverviewLine."Line Type"::Surcharge);
          END;
          PBInstallmentOverviewLine.CALCSUMS("Overview Amount");
          TotalAmount += PBInstallmentOverviewLine."Overview Amount";
        UNTIL PBInstallmentOverview.NEXT = 0;
      EXIT(TotalAmount);
    END;

    BEGIN
    END.
  }
}

