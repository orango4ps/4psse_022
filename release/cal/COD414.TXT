OBJECT Codeunit 414 Release Sales Document
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.05,4PS14.00,4PSSE;
  }
  PROPERTIES
  {
    TableNo=36;
    OnRun=BEGIN
            SalesHeader.COPY(Rec);
            Code;
            Rec := SalesHeader;
          END;

  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=There is nothing to release for the document of type %1 with the number %2.;NOR=Det finnes ingenting † frigi for dokumentet av typen %1 med nummer %2.;SVE=Det finns ingenting att frisl„ppa f”r dokumentet av typen %1 med numret %2.';
      SalesSetup@1002 : Record 311;
      InvtSetup@1000 : Record 313;
      SalesHeader@1008 : Record 36;
      WhseSalesRelease@1004 : Codeunit 5771;
      Text002@1005 : TextConst 'ENU=This document can only be released when the approval process is complete.;NOR=Dette dokumentet kan f›rst frigis n†r godkjenningsprosessen er fullf›rt.;SVE=Det h„r dokumentet kan inte sl„ppas f”rr„n godk„nnandeprocessen har slutf”rts.';
      Text003@1003 : TextConst 'ENU=The approval process must be cancelled or completed to reopen this document.;NOR=Godkjenningsprosessen m† avbrytes eller fullf›res f›r du kan †pne dette dokumentet p† nytt.;SVE=Godk„nnandeprocessen m†ste avbrytas eller slutf”ras f”r att det h„r dokumentet ska kunna ”ppnas igen.';
      Text005@1006 : TextConst 'ENU=There are unpaid prepayment invoices that are related to the document of type %1 with the number %2.;NOR=Det finnes ubetalte forskuddsfakturaer som er relatert til dokumentet av typen %1 med nummer %2.;SVE=Det finns obetalda f”rskottsfakturor som „r relaterade till dokumentet med typen %1 och numret %2.';
      UnpostedPrepaymentAmountsErr@1007 : TextConst '@@@="%1 - Document Type; %2 - Document No.";ENU=There are unposted prepayment amounts on the document of type %1 with the number %2.;NOR=Det finnes ikke-bokf›rte forskuddsbel›p p† dokumentet av typen %1 med nummer %2.';
      PreviewMode@1009 : Boolean;
      SkipCheckReleaseRestrictions@1010 : Boolean;
      Text11012001@1100525000 : TextConst 'ENU=Not yet fully shipped / invoiced. Close, are you sure?;NOR=Ikke fullstendig levert/fakturert enda. Er du sikker p† † du vil lukke?;SVE=Inte fullst„ndigt levererad/fakturerad „n. Žr du s„ker p† att du vill st„nga?';
      Text11012002@1100525001 : TextConst 'ENU=BOM Item %1 has to be exploded;NOR=Stykklisteartikkel %1 m† utfoldes;SVE=Strukturartikel %1 m†ste expanderas';
      GotSalesSetup@1101285000 : Boolean;

    LOCAL PROCEDURE Code@10() LinesWereModified : Boolean;
    VAR
      SalesLine@1006 : Record 37;
      PrepaymentMgt@1003 : Codeunit 441;
      NotOnlyDropShipment@1002 : Boolean;
      PostingDate@1001 : Date;
      PrintPostedDocuments@1000 : Boolean;
      IsHandled@1004 : Boolean;
    BEGIN
      WITH SalesHeader DO BEGIN
        IF Status = Status::Released THEN
          EXIT;

        OnBeforeReleaseSalesDoc(SalesHeader,PreviewMode);
        IF NOT (PreviewMode OR SkipCheckReleaseRestrictions) THEN
          CheckSalesReleaseRestrictions;

        IsHandled := FALSE;
        OnBeforeCheckCustomerCreated(SalesHeader,IsHandled);
        IF NOT IsHandled THEN
          IF "Document Type" = "Document Type"::Quote THEN
            IF CheckCustomerCreated(TRUE) THEN
              GET("Document Type"::Quote,"No.")
            ELSE
              EXIT;

        IF NOT SalesLine.PlantRelatedInvoice("Job No.",SalesHeader) THEN //**4PS.n
          TESTFIELD("Sell-to Customer No.");

        SalesLine.SETRANGE("Document Type","Document Type");
        SalesLine.SETRANGE("Document No.","No.");
        SalesLine.SETFILTER(Type,'>0');
        SalesLine.SETFILTER(Quantity,'<>0');
        OnBeforeSalesLineFind(SalesLine,SalesHeader);
        IF NOT SalesLine.FIND('-') THEN
          ERROR(Text001,"Document Type","No.");
        InvtSetup.GET;
        IF InvtSetup."Location Mandatory" THEN BEGIN
          SalesLine.SETRANGE(Type,SalesLine.Type::Item);
          IF SalesLine.FINDSET THEN
            REPEAT
              IF SalesLine.IsInventoriableItem THEN
                SalesLine.TESTFIELD("Location Code");
            UNTIL SalesLine.NEXT = 0;
          SalesLine.SETFILTER(Type,'>0');
        END;

        OnCodeOnAfterCheck(SalesHeader,SalesLine,LinesWereModified);

        SalesLine.SETRANGE("Drop Shipment",FALSE);
        NotOnlyDropShipment := SalesLine.FINDFIRST;
        SalesLine.RESET;

        OnBeforeCalcInvDiscount(SalesHeader,PreviewMode);

        SalesSetup.GET;
        IF SalesSetup."Calc. Inv. Discount" THEN BEGIN
          PostingDate := "Posting Date";
          PrintPostedDocuments := "Print Posted Documents";
          CODEUNIT.RUN(CODEUNIT::"Sales-Calc. Discount",SalesLine);
          LinesWereModified := TRUE;
          GET("Document Type","No.");
          "Print Posted Documents" := PrintPostedDocuments;
          IF PostingDate <> "Posting Date" THEN
            VALIDATE("Posting Date",PostingDate);
        END;

        IsHandled := FALSE;
        OnBeforeModifySalesDoc(SalesHeader,PreviewMode,IsHandled);
        IF IsHandled THEN
          EXIT;

        IF PrepaymentMgt.TestSalesPrepayment(SalesHeader) AND ("Document Type" = "Document Type"::Order) THEN BEGIN
          Status := Status::"Pending Prepayment";
          MODIFY(TRUE);
          OnAfterReleaseSalesDoc(SalesHeader,PreviewMode,LinesWereModified);
          EXIT;
        END;
        Status := Status::Released;

        LinesWereModified := LinesWereModified OR CalcAndUpdateVATOnLines(SalesHeader,SalesLine);

        OnAfterUpdateSalesDocLines(SalesHeader,LinesWereModified,PreviewMode);

        ReleaseATOs(SalesHeader);
        OnAfterReleaseATOs(SalesHeader,SalesLine,PreviewMode);

        MODIFY(TRUE);

        IF NotOnlyDropShipment THEN
          IF "Document Type" IN ["Document Type"::Order,"Document Type"::"Return Order"] THEN
            WhseSalesRelease.Release(SalesHeader);

        OnAfterReleaseSalesDoc(SalesHeader,PreviewMode,LinesWereModified);
      END;
    END;

    [External]
    PROCEDURE Reopen@1(VAR SalesHeader@1000 : Record 36);
    VAR
      PreviousStatus@1100528800 : Option;
      SalesLine@1100528801 : Record 37;
    BEGIN
      OnBeforeReopenSalesDoc(SalesHeader,PreviewMode);

      WITH SalesHeader DO BEGIN
        IF Status = Status::Open THEN
          EXIT;
        PreviousStatus := Status; //**4PS.n
        Status := Status::Open;

        IF "Document Type" <> "Document Type"::Order THEN
          ReopenATOs(SalesHeader);

        OnReopenOnBeforeSalesHeaderModify(SalesHeader);
        MODIFY(TRUE);

        //**4PS.sn
        IF (PreviousStatus = Status::Closed) AND ("Document Type" = "Document Type"::Order) THEN BEGIN
          SalesLine.RESET;
          SalesLine.SETRANGE("Document Type", "Document Type");
          SalesLine.SETRANGE("Document No.", "No.");
          SalesLine.SETFILTER(Type, '>0');
          IF SalesLine.FINDSET(TRUE, FALSE) THEN BEGIN
            REPEAT
              SalesLine.InitOutstanding;
              SalesLine.MODIFY;
            UNTIL SalesLine.NEXT = 0;
          END;
        END;
        //**4PS.en

        IF "Document Type" IN ["Document Type"::Order,"Document Type"::"Return Order"] THEN
          WhseSalesRelease.Reopen(SalesHeader);
      END;

      OnAfterReopenSalesDoc(SalesHeader,PreviewMode);
    END;

    [External]
    PROCEDURE PerformManualRelease@2(VAR SalesHeader@1002 : Record 36);
    VAR
      PrepaymentMgt@1001 : Codeunit 441;
    BEGIN
      OnPerformManualReleaseOnBeforeTestSalesPrepayment(SalesHeader,PreviewMode);
      IF PrepaymentMgt.TestSalesPrepayment(SalesHeader) THEN
        ERROR(UnpostedPrepaymentAmountsErr,SalesHeader."Document Type",SalesHeader."No.");

      OnBeforeManualReleaseSalesDoc(SalesHeader,PreviewMode);
      PerformManualCheckAndRelease(SalesHeader);
      OnAfterManualReleaseSalesDoc(SalesHeader,PreviewMode);
    END;

    [External]
    PROCEDURE PerformManualCheckAndRelease@13(VAR SalesHeader@1002 : Record 36);
    VAR
      PrepaymentMgt@1001 : Codeunit 441;
      ApprovalsMgmt@1000 : Codeunit 1535;
      IsHandled@1003 : Boolean;
      SalesLine@1100525001 : Record 37;
      Item@1100525002 : Record 27;
      ROTInformation@1101285000 : Record 11128101;
      SalesSetup@1101285001 : Record 311;
      Text00001@1101285002 : TextConst 'ENU=Extended ROT-info must be entered;SVE=Ut”kad rot-info m†ste anges';
      ExtendedROTRUTInformation@1101285003 : Record 11128102;
    BEGIN
      OnBeforePerformManualCheckAndRelease(SalesHeader,PreviewMode);

      //**4PS.sn
      IF (SalesHeader."Document Type" = SalesHeader."Document Type"::Order) OR
         (SalesHeader."Document Type" = SalesHeader."Document Type"::"Return Order") THEN BEGIN
        SalesLine.SETRANGE("Document Type",SalesHeader."Document Type");
        SalesLine.SETRANGE("Document No.",SalesHeader."No.");
        SalesLine.SETFILTER("Item No.",'<>%1', '');
        SalesLine.SETFILTER(Quantity,'<>0');
        IF SalesLine.FINDSET THEN
          REPEAT
            Item.GET(SalesLine."Item No.");
            IF Item."Logistics on Component Level" THEN
              ERROR(Text11012002,SalesLine."Item No.");
          UNTIL SalesLine.NEXT = 0;
      END;
      SalesLine.RESET;
      //**4PS.en

      WITH SalesHeader DO
        IF ("Document Type" = "Document Type"::Order) AND PrepaymentMgt.TestSalesPayment(SalesHeader) THEN BEGIN
          IF Status <> Status::"Pending Prepayment" THEN BEGIN
            Status := Status::"Pending Prepayment";
            MODIFY;
            COMMIT;
          END;
          ERROR(Text005,"Document Type","No.");
        END;

      IF ApprovalsMgmt.IsSalesHeaderPendingApproval(SalesHeader) THEN
        ERROR(Text002);

      IsHandled := FALSE;
      OnBeforePerformManualRelease(SalesHeader,PreviewMode,IsHandled);
      IF IsHandled THEN
        EXIT;
      //>> 190227 ORANGO.SB Feature #23088 Check rot extensions lines when invoice is released
      IF NOT GotSalesSetup THEN
        GotSalesSetup := SalesSetup.GET;

      // IF (SalesHeader."ROT/RUT reduction") THEN BEGIN
      IF (SalesSetup."ROTRUT Application No."<>0) AND SalesHeader."ROT/RUT reduction" AND SalesSetup."ROTRUT check on release" THEN BEGIN
        ROTInformation.SETRANGE(Type, ROTInformation.Type::Invoice);
        ROTInformation.SETRANGE("Invoice No.",SalesHeader."No.");
      //  IF ROTInformation.FINDSET(true) THEN REPEAT
        IF ROTInformation.FINDSET(FALSE) THEN REPEAT
      //    IF SalesSetup."ROTRUT Application No."<>0 THEN BEGIN
          ROTInformation.CALCFIELDS("Extension Lines");
          ROTInformation.TESTFIELD("Extension Lines");
          ROTInformation.TESTFIELD("Personal No.");
          //>> 200128 #27481 Extended ROT/RUT-lines check
          ExtendedROTRUTInformation.SETRANGE(Type,ROTInformation.Type);
          ExtendedROTRUTInformation.SETRANGE("Document No.",ROTInformation."Document No.");
          ExtendedROTRUTInformation.SETRANGE("Invoice No.",ROTInformation."Invoice No.");
          ExtendedROTRUTInformation.SETRANGE("Personal No.",ROTInformation."Personal No.");
          ExtendedROTRUTInformation.SETRANGE("Application Type",ROTInformation."Application Type");
          ExtendedROTRUTInformation.CALCSUMS("Quantity Hours");
          IF ExtendedROTRUTInformation."Quantity Hours"=0 THEN
            ERROR(Text00001);
          //<<
      //    END;
        UNTIL ROTInformation.NEXT=0;
      END;
      //<<

      CODEUNIT.RUN(CODEUNIT::"Release Sales Document",SalesHeader);
    END;

    [External]
    PROCEDURE PerformManualReopen@3(VAR SalesHeader@1002 : Record 36);
    BEGIN
      IF SalesHeader.Status = SalesHeader.Status::"Pending Approval" THEN
        ERROR(Text003);

      OnBeforeManualReOpenSalesDoc(SalesHeader,PreviewMode);
      Reopen(SalesHeader);
      OnAfterManualReOpenSalesDoc(SalesHeader,PreviewMode);
    END;

    LOCAL PROCEDURE ReleaseATOs@5(SalesHeader@1000 : Record 36);
    VAR
      SalesLine@1001 : Record 37;
      AsmHeader@1002 : Record 900;
    BEGIN
      SalesLine.SETRANGE("Document Type",SalesHeader."Document Type");
      SalesLine.SETRANGE("Document No.",SalesHeader."No.");
      IF SalesLine.FINDSET THEN
        REPEAT
          IF SalesLine.AsmToOrderExists(AsmHeader) THEN
            CODEUNIT.RUN(CODEUNIT::"Release Assembly Document",AsmHeader);
        UNTIL SalesLine.NEXT = 0;
    END;

    LOCAL PROCEDURE ReopenATOs@6(SalesHeader@1000 : Record 36);
    VAR
      SalesLine@1001 : Record 37;
      AsmHeader@1002 : Record 900;
      ReleaseAssemblyDocument@1003 : Codeunit 903;
    BEGIN
      SalesLine.SETRANGE("Document Type",SalesHeader."Document Type");
      SalesLine.SETRANGE("Document No.",SalesHeader."No.");
      IF SalesLine.FINDSET THEN
        REPEAT
          IF SalesLine.AsmToOrderExists(AsmHeader) THEN
            ReleaseAssemblyDocument.Reopen(AsmHeader);
        UNTIL SalesLine.NEXT = 0;
    END;

    [External]
    PROCEDURE ReleaseSalesHeader@11(VAR SalesHdr@1000 : Record 36;Preview@1001 : Boolean) LinesWereModified : Boolean;
    BEGIN
      PreviewMode := Preview;
      SalesHeader.COPY(SalesHdr);
      LinesWereModified := Code;
      SalesHdr := SalesHeader;
    END;

    [External]
    PROCEDURE SetSkipCheckReleaseRestrictions@21();
    BEGIN
      SkipCheckReleaseRestrictions := TRUE;
    END;

    [External]
    PROCEDURE CalcAndUpdateVATOnLines@14(VAR SalesHeader@1003 : Record 36;VAR SalesLine@1002 : Record 37) LinesWereModified : Boolean;
    VAR
      TempVATAmountLine0@1001 : TEMPORARY Record 290;
      TempVATAmountLine1@1000 : TEMPORARY Record 290;
    BEGIN
      SalesLine.SetSalesHeader(SalesHeader);
      SalesLine.CalcVATAmountLines(0,SalesHeader,SalesLine,TempVATAmountLine0);
      SalesLine.CalcVATAmountLines(1,SalesHeader,SalesLine,TempVATAmountLine1);
      LinesWereModified :=
        SalesLine.UpdateVATOnLines(0,SalesHeader,SalesLine,TempVATAmountLine0) OR
        SalesLine.UpdateVATOnLines(1,SalesHeader,SalesLine,TempVATAmountLine1);
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCalcInvDiscount@23(VAR SalesHeader@1000 : Record 36;PreviewMode@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeManualReleaseSalesDoc@15(VAR SalesHeader@1000 : Record 36;PreviewMode@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeReleaseSalesDoc@7(VAR SalesHeader@1000 : Record 36;PreviewMode@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterReleaseSalesDoc@4(VAR SalesHeader@1000 : Record 36;PreviewMode@1001 : Boolean;VAR LinesWereModified@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterManualReleaseSalesDoc@17(VAR SalesHeader@1000 : Record 36;PreviewMode@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeManualReOpenSalesDoc@16(VAR SalesHeader@1000 : Record 36;PreviewMode@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeReopenSalesDoc@8(VAR SalesHeader@1000 : Record 36;PreviewMode@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeModifySalesDoc@22(VAR SalesHeader@1000 : Record 36;PreviewMode@1001 : Boolean;VAR IsHandled@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePerformManualRelease@24(VAR SalesHeader@1000 : Record 36;PreviewMode@1002 : Boolean;VAR IsHandled@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeSalesLineFind@27(VAR SalesLine@1000 : Record 37;SalesHeader@1001 : Record 36);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterReopenSalesDoc@9(VAR SalesHeader@1000 : Record 36;PreviewMode@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterManualReOpenSalesDoc@18(VAR SalesHeader@1000 : Record 36;PreviewMode@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterReleaseATOs@12(VAR SalesHeader@1000 : Record 36;VAR SalesLine@1001 : Record 37;PreviewMode@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterUpdateSalesDocLines@19(VAR SalesHeader@1000 : Record 36;VAR LinesWereModified@1001 : Boolean;PreviewMode@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnCodeOnAfterCheck@20(SalesHeader@1000 : Record 36;VAR SalesLine@1001 : Record 37;VAR LinesWereModified@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCheckCustomerCreated@25(VAR SalesHeader@1000 : Record 36;VAR IsHandled@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePerformManualCheckAndRelease@29(VAR SalesHeader@1000 : Record 36;PreviewMode@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnReopenOnBeforeSalesHeaderModify@26(VAR SalesHeader@1000 : Record 36);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnPerformManualReleaseOnBeforeTestSalesPrepayment@28(VAR SalesHeader@1000 : Record 36;PreviewMode@1001 : Boolean);
    BEGIN
    END;

    PROCEDURE Close@1210190000(VAR SalesHeader@1210190000 : Record 36);
    VAR
      SalesLine@1210190002 : Record 37;
    BEGIN
      //**4PS
      WITH SalesHeader DO BEGIN
        IF Status = Status::Closed THEN
          EXIT;
        IF NOT ("Document Type" IN ["Document Type"::Order, "Document Type"::"Return Order"]) THEN
          EXIT;

        SalesLine.RESET;
        SalesLine.SETRANGE("Document Type", "Document Type");
        SalesLine.SETRANGE("Document No.", "No.");
        IF SalesLine.FINDSET THEN BEGIN
          REPEAT
            IF NOT SalesLine.OrderLineCompletelyShippedAndInvoiced THEN BEGIN
              IF NOT CONFIRM(Text11012001, FALSE) THEN
                EXIT;
              SalesLine.FINDLAST;
            END;
          UNTIL SalesLine.NEXT = 0;
        END;

        Status := Status::Closed;
        MODIFY(TRUE);
        SalesLine.RESET;
        SalesLine.SETRANGE("Document Type","Document Type");
        SalesLine.SETRANGE("Document No.","No.");
        SalesLine.SETFILTER(Type,'>0');
        IF SalesLine.FINDSET(TRUE,FALSE) THEN BEGIN
          REPEAT
            SalesLine."Outstanding Quantity" := 0;
            SalesLine."Outstanding Qty. (Base)" := 0;
            SalesLine."Outstanding Amount" := 0;
            SalesLine."Outstanding Amount (LCY)" := 0;
            SalesLine.MODIFY;
          UNTIL SalesLine.NEXT = 0;
        END;
      END;
    END;

    BEGIN
    {
      190227 ORANGO.SB Feature #23088 Check rot extensions lines when invoice is released
      200128 #27481 Extended ROT/RUT-lines check
    }
    END.
  }
}

