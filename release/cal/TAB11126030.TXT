OBJECT Table 11126030 Waste Flow
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               QASetup@1100528300 : Record 11126001;
               NoSeriesManagement@1100528301 : Codeunit 396;
             BEGIN
               IF ("No." = '') THEN BEGIN
                 QASetup.GET;
                 QASetup.TESTFIELD("Waste Flow Nos.");
                 NoSeriesManagement.InitSeries(QASetup."Waste Flow Nos." ,xRec."No. Series", 0D, "No.", "No. Series");
               END;
             END;

    OnModify=VAR
               WasteFlowLine@1100529500 : Record 11126031;
             BEGIN
               WasteFlowLine.SETRANGE("Waste Flow No.", "No.");
               WasteFlowLine.SETRANGE(Printed, TRUE);
               IF (NOT WasteFlowLine.ISEMPTY) THEN
                 ERROR(Text000);
             END;

    OnDelete=VAR
               WasteFlowLine@1100529500 : Record 11126031;
             BEGIN
               WasteFlowLine.SETRANGE("Waste Flow No.", "No.");
               WasteFlowLine.DELETEALL(TRUE);
             END;

    CaptionML=ENU=Waste Flow;
  }
  FIELDS
  {
    { 10  ;   ;No.                 ;Code20        ;OnValidate=VAR
                                                                QASetup@1100528300 : Record 11126001;
                                                                NoSeriesManagement@1100528301 : Codeunit 396;
                                                              BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  QASetup.GET;
                                                                  NoSeriesManagement.TestManual(QASetup."Waste Flow Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              NOR=Nr.;
                                                              SVE=Nr] }
    { 100 ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 101 ;   ;Nr. of Permits      ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Waste Flow Line" WHERE (Waste Flow No.=FIELD(No.)));
                                                   CaptionML=ENU=Nr. of Permits;
                                                   Editable=No }
    { 102 ;   ;Printed Permits     ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Waste Flow Line" WHERE (Waste Flow No.=FIELD(No.),
                                                                                              Printed=CONST(Yes)));
                                                   CaptionML=ENU=Printed Permits;
                                                   Editable=No }
    { 103 ;   ;Approved Permits    ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Waste Flow Line" WHERE (Waste Flow No.=FIELD(No.),
                                                                                              Approved=CONST(Yes)));
                                                   CaptionML=ENU=Approved Permits;
                                                   Editable=No }
    { 104 ;   ;Processed Permits   ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Waste Flow Line" WHERE (Waste Flow No.=FIELD(No.),
                                                                                              Processed=CONST(Yes)));
                                                   CaptionML=ENU=Processed Permits;
                                                   Editable=No }
    { 110 ;   ;Project No.         ;Code20        ;TableRelation=Job.No.;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Project Description");
                                                              END;

                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr] }
    { 111 ;   ;Project Description ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Job.Description WHERE (No.=FIELD(Project No.)));
                                                   CaptionML=[ENU=Project Description;
                                                              NOR=Prosjektbeskrivelse;
                                                              SVE=Projektbeskrivning];
                                                   Editable=No }
    { 120 ;   ;Price Agreement Type;Option        ;OnValidate=BEGIN
                                                                IF ("Price Agreement Type" <> xRec."Price Agreement Type") THEN
                                                                  VALIDATE("Purchase Order No.", '');
                                                                  VALIDATE("Purchase Order Line No.", 0);
                                                              END;

                                                   CaptionML=ENU=Price Agreement Type;
                                                   OptionCaptionML=ENU=Blanket Order,Project;
                                                   OptionString=BlanketOrder,Project }
    { 130 ;   ;Purchase Order No.  ;Code20        ;TableRelation=IF (Price Agreement Type=CONST(BlanketOrder)) "Purchase Header".No. WHERE (Document Type=CONST(Blanket Order),
                                                                                                                                            Job No.=FIELD(Project No.))
                                                                                                                                            ELSE "Purchase Header".No. WHERE (Document Type=CONST(Order),
                                                                                                                                                                              Job No.=FIELD(Project No.));
                                                   OnValidate=VAR
                                                                PurchaseHeader@1100529500 : Record 38;
                                                              BEGIN
                                                                VALIDATE("Waste Handler No.", '');
                                                                CASE "Price Agreement Type" OF
                                                                  "Price Agreement Type"::BlanketOrder:
                                                                    IF (PurchaseHeader.GET(PurchaseHeader."Document Type"::"Blanket Order", "Purchase Order No.")) THEN
                                                                      VALIDATE("Waste Handler No.", PurchaseHeader."Buy-from Vendor No.");
                                                                  ELSE
                                                                    IF (PurchaseHeader.GET(PurchaseHeader."Document Type"::Order, "Purchase Order No.")) THEN
                                                                      VALIDATE("Waste Handler No.", PurchaseHeader."Buy-from Vendor No.");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Purchase Order No.;
                                                              NOR=Innkj›psordrenr.;
                                                              SVE=Ink”psordernr] }
    { 135 ;   ;Purchase Order Line No.;Integer    ;TableRelation=IF (Price Agreement Type=CONST(BlanketOrder)) "Purchase Line"."Line No." WHERE (Document Type=CONST(Blanket Order),
                                                                                                                                                 Document No.=FIELD(Purchase Order No.),
                                                                                                                                                 Cost Type=CONST(Material),
                                                                                                                                                 Item No.=FILTER(<>''))
                                                                                                                                                 ELSE "Purchase Line"."Line No." WHERE (Document Type=CONST(Order),
                                                                                                                                                                                        Document No.=FIELD(Purchase Order No.),
                                                                                                                                                                                        Cost Type=CONST(Material),
                                                                                                                                                                                        Item No.=FILTER(<>''));
                                                   OnValidate=VAR
                                                                PurchaseLine@1100528300 : Record 39;
                                                              BEGIN
                                                                VALIDATE("Item No.", '');
                                                                UoM := '';
                                                                CASE "Price Agreement Type" OF
                                                                  "Price Agreement Type"::BlanketOrder:
                                                                    IF (PurchaseLine.GET(PurchaseLine."Document Type"::"Blanket Order", "Purchase Order No.", "Purchase Order Line No."))
                                                                    THEN BEGIN
                                                                      VALIDATE("Item No.", PurchaseLine."Item No.");
                                                                      UoM := PurchaseLine."Unit of Measure";
                                                                    END;
                                                                  ELSE
                                                                    IF (PurchaseLine.GET(PurchaseLine."Document Type"::Order, "Purchase Order No.", "Purchase Order Line No.")) THEN BEGIN
                                                                      VALIDATE("Item No.", PurchaseLine."Item No.");
                                                                      UoM := PurchaseLine."Unit of Measure Code";
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Purchase Order Line No.;
                                                              SVE=Ink”psorderradnr] }
    { 140 ;   ;Waste Type Code     ;Code20        ;TableRelation="SH Workplan Waste Type"."Std Waste Type Code" WHERE (Source Type=CONST(Project),
                                                                                                                       Source Code=FIELD(Project No.),
                                                                                                                       Workplan Date=FIELD(Workplan Date),
                                                                                                                       Main Process Code=FIELD(Main Process Code),
                                                                                                                       Sub Process Code=FIELD(Sub Process Code),
                                                                                                                       Location Code=FIELD(Location Code));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Waste Type Description");
                                                              END;

                                                   OnLookup=VAR
                                                              SHWorkplanWasteType@1100528300 : Record 11126028;
                                                            BEGIN
                                                              SHWorkplanWasteType.SETRANGE("Source Type", SHWorkplanWasteType."Source Type"::Project);
                                                              SHWorkplanWasteType.SETRANGE("Source Code", "Project No.");
                                                              SHWorkplanWasteType.SETRANGE("Main Process Code", "Main Process Code");
                                                              SHWorkplanWasteType.SETRANGE("Sub Process Code", "Sub Process Code");
                                                              SHWorkplanWasteType.SETRANGE("Location Code", "Location Code");
                                                              IF (PAGE.RUNMODAL(0, SHWorkplanWasteType) = ACTION::LookupOK) THEN BEGIN
                                                                VALIDATE("Waste Type Code", SHWorkplanWasteType."Std Waste Type Code");
                                                              END;
                                                            END;

                                                   CaptionML=ENU=Waste Type }
    { 141 ;   ;Waste Type Description;Text80      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("SH Code".Description WHERE (Type=CONST(Waste Type),
                                                                                                   Code=FIELD(Waste Type Code)));
                                                   CaptionML=ENU=Waste Type Description;
                                                   Editable=No }
    { 142 ;   ;Product Paper       ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("SH Code"."Product Paper" WHERE (Type=CONST(Waste Type),
                                                                                                       Code=FIELD(Waste Type Code)));
                                                   CaptionML=ENU=Product Paper;
                                                   Editable=No }
    { 150 ;   ;Item No.            ;Code20        ;TableRelation=Item.No.;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Item Description");
                                                              END;

                                                   CaptionML=[ENU=Item No.;
                                                              NOR=Artikkelnr;
                                                              SVE=Artikelnr];
                                                   Editable=No }
    { 151 ;   ;Item Description    ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Item.Description WHERE (No.=FIELD(Item No.)));
                                                   CaptionML=[ENU=Item Description;
                                                              NOR=Artikkelbeskrivelse;
                                                              SVE=Artikelbeskrivning];
                                                   Editable=No }
    { 152 ;   ;UoM                 ;Code10        ;CaptionML=[ENU=UoM;
                                                              SVE=Enhet];
                                                   Editable=No }
    { 160 ;   ;Waste Handler No.   ;Code20        ;TableRelation=Vendor.No.;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Waste Handler Name");
                                                              END;

                                                   CaptionML=ENU=Waste Handler;
                                                   Editable=No }
    { 161 ;   ;Waste Handler Name  ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor.Name WHERE (No.=FIELD(Waste Handler No.)));
                                                   CaptionML=ENU=Waste Handler Name;
                                                   Editable=No }
    { 170 ;   ;Shipping Agent No.  ;Code20        ;TableRelation=Contact.No. WHERE (Type=CONST(Company));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Shipping Agent Name");
                                                              END;

                                                   CaptionML=ENU=Shipping Agent No. }
    { 171 ;   ;Shipping Agent Name ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Shipping Agent No.)));
                                                   CaptionML=[ENU=Shipping Agent Name;
                                                              NOR=Transport›rsnavn;
                                                              SVE=Spedit”rsnamn];
                                                   Editable=No }
    { 180 ;   ;Waste Flow No. Handler;Code50      ;TableRelation="SH Code".Code WHERE (Type=CONST(WasteFlowNo));
                                                   ValidateTableRelation=No;
                                                   CaptionML=ENU=Waste Flow No. (Handler) }
    { 190 ;   ;Estimated Quantity  ;Integer       ;CaptionML=ENU=Estimated Quantity }
    { 200 ;   ;No. Series          ;Code20        ;TableRelation="No. Series".Code;
                                                   CaptionML=[ENU=No. Series;
                                                              NOR=Nr-serie;
                                                              SVE=Nr-serie] }
    { 210 ;   ;Multiple of         ;Integer       ;CaptionML=ENU=Multiple of }
    { 230 ;   ;Workplan Date       ;Date          ;TableRelation="SH Workplan"."Workplan Date" WHERE (Source Type=CONST(Project),
                                                                                                      Source Code=FIELD(Project No.),
                                                                                                      Workplan Date=FILTER(<>''),
                                                                                                      Location Code=FILTER(<>''));
                                                   OnLookup=VAR
                                                              SHWorkplan@1100528300 : Record 11126011;
                                                            BEGIN
                                                              SHWorkplan.SETRANGE("Source Type", SHWorkplan."Source Type"::Project);
                                                              SHWorkplan.SETRANGE("Source Code", "Project No.");
                                                              SHWorkplan.SETFILTER("Location Code", '<>%1', '');
                                                              IF (PAGE.RUNMODAL(0, SHWorkplan) = ACTION::LookupOK) THEN BEGIN
                                                                VALIDATE("Workplan Date", SHWorkplan."Workplan Date");
                                                                VALIDATE("Main Process Code", SHWorkplan."Main Process Code");
                                                                VALIDATE("Sub Process Code", SHWorkplan."Sub Process Code");
                                                                VALIDATE("Location Code", SHWorkplan."Location Code");
                                                              END;
                                                            END;

                                                   CaptionML=ENU=Workplan Date }
    { 240 ;   ;Main Process Code   ;Code20        ;TableRelation="SH Workplan"."Main Process Code" WHERE (Source Type=CONST(Project),
                                                                                                          Source Code=FIELD(Project No.),
                                                                                                          Workplan Date=FIELD(Workplan Date),
                                                                                                          Location Code=FILTER(<>''));
                                                   CaptionML=ENU=Main Process Code;
                                                   NotBlank=Yes }
    { 250 ;   ;Sub Process Code    ;Code20        ;TableRelation="SH Workplan"."Sub Process Code" WHERE (Source Type=CONST(Project),
                                                                                                         Source Code=FIELD(Project No.),
                                                                                                         Workplan Date=FIELD(Workplan Date),
                                                                                                         Main Process Code=FIELD(Main Process Code),
                                                                                                         Location Code=FILTER(<>''));
                                                   CaptionML=ENU=Sub Process Code;
                                                   NotBlank=Yes }
    { 260 ;   ;Location Code       ;Code20        ;TableRelation="SH Workplan"."Location Code" WHERE (Source Type=CONST(Project),
                                                                                                      Source Code=FIELD(Project No.),
                                                                                                      Workplan Date=FIELD(Workplan Date),
                                                                                                      Main Process Code=FIELD(Main Process Code),
                                                                                                      Location Code=FILTER(<>''));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("SH Workplan Description", "Location Description");
                                                              END;

                                                   CaptionML=[ENU=Location Code;
                                                              NOR=Lokasjonskode;
                                                              SVE=Lagerst„llekod] }
    { 261 ;   ;SH Workplan Description;Text80     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("SH Workplan".Description WHERE (Source Type=CONST(Project),
                                                                                                       Source Code=FIELD(Project No.),
                                                                                                       Main Process Code=FIELD(Main Process Code),
                                                                                                       Sub Process Code=FIELD(Sub Process Code),
                                                                                                       Location Code=FIELD(Location Code)));
                                                   CaptionML=ENU=Workplan Description;
                                                   Editable=No }
    { 262 ;   ;Location Description;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("SH Workplan"."Location Description" WHERE (Source Type=CONST(Project),
                                                                                                                  Source Code=FIELD(Project No.),
                                                                                                                  Main Process Code=FIELD(Main Process Code),
                                                                                                                  Sub Process Code=FIELD(Sub Process Code),
                                                                                                                  Location Code=FIELD(Location Code)));
                                                   CaptionML=[ENU=Location Description;
                                                              NOR=Beskrivelse av lokasjon;
                                                              SVE=Beskrivning av lagerst„lle];
                                                   Editable=No }
    { 263 ;   ;Waste Type          ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("SH Code"."Waste Type" WHERE (Type=CONST(Waste Type),
                                                                                                    Code=FIELD(Waste Type Code)));
                                                   CaptionML=ENU=Waste Type;
                                                   OptionCaptionML=ENU=" ,Asbestos";
                                                   OptionString=[ ,Asbestos];
                                                   Editable=No }
    { 264 ;   ;Disposal Method Code;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("SH Code"."Disposal Method Code" WHERE (Type=CONST(Waste Type),
                                                                                                              Code=FIELD(Waste Type Code)));
                                                   CaptionML=ENU=Disposal Method Code;
                                                   Editable=No }
    { 270 ;   ;Route Collection    ;Boolean       ;CaptionML=ENU=Route Collection }
    { 280 ;   ;Collector Scheme    ;Boolean       ;CaptionML=ENU=Collector Scheme }
    { 290 ;   ;Repetetive Loads    ;Boolean       ;CaptionML=ENU=Repetetive loads }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100529500 : TextConst 'ENU=There are already Permits printed.';

    PROCEDURE GenerateWasteFlowLines@1100525000();
    VAR
      WasteFlowLine@1100525000 : Record 11126031;
      MaxWasteFlowPermits@1100525001 : Integer;
      I@1100525002 : Integer;
    BEGIN
      TESTFIELD("Estimated Quantity");
      TESTFIELD("Multiple of");
      MaxWasteFlowPermits := ABS("Estimated Quantity" DIV "Multiple of");
      IF (MaxWasteFlowPermits <= 0) THEN
        EXIT;
      WasteFlowLine.SETRANGE("Waste Flow No.", "No.");
      IF (WasteFlowLine.COUNT >= MaxWasteFlowPermits) THEN
        EXIT;
      MaxWasteFlowPermits := MaxWasteFlowPermits - WasteFlowLine.COUNT;

      FOR I := 1 TO MaxWasteFlowPermits DO
        GenerateWasteFlowLine;
    END;

    PROCEDURE GenerateWasteFlowLine@1100528300();
    VAR
      WasteFlowLine@1100528300 : Record 11126031;
      PurchaseLine@1100528302 : Record 39;
    BEGIN
      TestAllFields;

      WasteFlowLine.INIT;
      WasteFlowLine."Waste Flow No." := "No.";
      WasteFlowLine."Line No." := WasteFlowLine.GetLastLineNo + 10000;
      CASE "Price Agreement Type" OF
        "Price Agreement Type"::BlanketOrder:
          PurchaseLine.GET(PurchaseLine."Document Type"::"Blanket Order", "Purchase Order No.", "Purchase Order Line No.");
        ELSE
          PurchaseLine.GET(PurchaseLine."Document Type"::Order, "Purchase Order No.", "Purchase Order Line No.");
      END;
      WasteFlowLine.UoM := PurchaseLine."Unit of Measure Code";
      WasteFlowLine.INSERT;
    END;

    PROCEDURE PrintLines@1100528301();
    VAR
      WasteFlowLine@1100528300 : Record 11126031;
    BEGIN
      TestAllFields;

      WasteFlowLine.SETRANGE("Waste Flow No.", "No.");
      WasteFlowLine.PrintSelectedLines(WasteFlowLine);
    END;

    PROCEDURE ProcessLines@1100528302();
    VAR
      WasteFlowLine@1100528300 : Record 11126031;
    BEGIN
      TestAllFields;

      WasteFlowLine.SETRANGE("Waste Flow No.", "No.");
      WasteFlowLine.ProcessSelectedLines(WasteFlowLine);
    END;

    LOCAL PROCEDURE TestAllFields@1100529500();
    BEGIN
      TESTFIELD("Project No.");
      TESTFIELD("Main Process Code");
      TESTFIELD("Sub Process Code");
      TESTFIELD("Location Code");
      TESTFIELD("Purchase Order No.");
      TESTFIELD("Purchase Order Line No.");
      TESTFIELD("Item No.");
      TESTFIELD("Waste Flow No. Handler");
    END;

    BEGIN
    END.
  }
}

