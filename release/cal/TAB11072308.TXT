OBJECT Table 11072308 Service Planning Set SUP Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ServicePlanningSet@1100528600 : Record 11072306;
             BEGIN
               ServicePlanningSet.GET("Set Code");
               ServicePlanningSet.TESTFIELD("Price Book Code");
               ServicePlanningSet.TESTFIELD("Price Book Index Date");
             END;

    CaptionML=ENU=Service Planning Set SUP Line;
    LookupPageID=Page11126796;
    DrillDownPageID=Page11126796;
  }
  FIELDS
  {
    { 10  ;   ;Set Code            ;Code10        ;TableRelation="Service Planning Set";
                                                   CaptionML=[ENU=Set Code;
                                                              NOR=Still inn kode;
                                                              SVE=St„ll in kod];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 20  ;   ;Work Order Seq. No. ;Integer       ;TableRelation="Service Planning Set Line"."Work Order Seq. No." WHERE (Set Code=FIELD(Set Code));
                                                   CaptionML=ENU=Work Order Seq. No.;
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 25  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 30  ;   ;Price Book Code     ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Planning Set"."Price Book Code" WHERE (Code=FIELD(Set Code)));
                                                   CaptionML=[ENU=Price Book Code;
                                                              SVE=Enhetspriskod];
                                                   Editable=No }
    { 40  ;   ;Price Book Index Date;Date         ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Planning Set"."Price Book Index Date" WHERE (Code=FIELD(Set Code)));
                                                   CaptionML=[ENU=Price Book Index Date;
                                                              SVE=Indexdatum f”r enhetspris];
                                                   Editable=No }
    { 50  ;   ;Unit Price Type     ;Option        ;OnValidate=BEGIN
                                                                IF "Unit Price Type" <> xRec."Unit Price Type" THEN
                                                                  VALIDATE(Code, '');
                                                              END;

                                                   CaptionML=ENU=Unit Price Set Code;
                                                   OptionCaptionML=ENU=Unit Price Set,Unit Price;
                                                   OptionString=Unit Price Set,Unit Price }
    { 60  ;   ;Code                ;Code20        ;TableRelation=IF (Unit Price Type=CONST(Unit Price Set)) "Service Unit Price Set".Code WHERE (Price Book Code=FIELD(Price Book Code),
                                                                                                                                                 Price Book Index Date=FIELD(Price Book Index Date))
                                                                                                                                                 ELSE IF (Unit Price Type=CONST(Unit Price)) "Service Unit Price".Code WHERE (Price Book Code=FIELD(Price Book Code),
                                                                                                                                                                                                                              Index Date=FIELD(Price Book Index Date));
                                                   OnValidate=BEGIN
                                                                IF Code <> '' THEN BEGIN
                                                                  CalcFieldsPriceBook;
                                                                  UpdateUnitPriceData;
                                                                  IF "Unit Price Type" = "Unit Price Type"::"Unit Price" THEN
                                                                    Quantity := 1;
                                                                END ELSE BEGIN
                                                                  Description := '';
                                                                  "Description 2" := '';
                                                                  "Description 3" := '';
                                                                  "Unit of Measure" := '';
                                                                  "Unit Price" := 0;
                                                                  Quantity := 0;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod] }
    { 70  ;   ;Room Code           ;Code20        ;TableRelation="SUP Room".Code;
                                                   OnValidate=VAR
                                                                ServiceUnitPriceManagement@1100528600 : Codeunit 11012846;
                                                                CustomerNo@1100528601 : Code[20];
                                                                PriceBookType@1100528602 : Option;
                                                              BEGIN
                                                                IF "Room Code" <> '' THEN BEGIN
                                                                  CalcFieldsPriceBook;
                                                                  PriceBookType := ServiceUnitPriceManagement.GetPriceBookType("Price Book Code", "Price Book Index Date");
                                                                  CustomerNo := ServiceUnitPriceManagement.DetermineCustomerNo(PriceBookType, "Price Book Code");
                                                                  ServiceUnitPriceManagement.ValidateRoom(CustomerNo, "Room Code");
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              ServiceUnitPriceManagement@1100528600 : Codeunit 11012846;
                                                              CustomerNo@1100528601 : Code[20];
                                                              PriceBookType@1100528602 : Option;
                                                            BEGIN
                                                              CalcFieldsPriceBook;
                                                              PriceBookType := ServiceUnitPriceManagement.GetPriceBookType("Price Book Code", "Price Book Index Date");
                                                              CustomerNo := ServiceUnitPriceManagement.DetermineCustomerNo(PriceBookType, "Price Book Code");
                                                              IF ServiceUnitPriceManagement.LookupRoom(CustomerNo, '', "Room Code") THEN
                                                                VALIDATE("Room Code");
                                                            END;

                                                   CaptionML=ENU=Room Code }
    { 80  ;   ;Part Code           ;Code20        ;TableRelation="SUP Part".Code;
                                                   OnValidate=VAR
                                                                ServiceUnitPriceManagement@1100528600 : Codeunit 11012846;
                                                                CustomerNo@1100528601 : Code[20];
                                                                PriceBookType@1100528602 : Option;
                                                              BEGIN
                                                                CalcFieldsPriceBook;
                                                                PriceBookType := ServiceUnitPriceManagement.GetPriceBookType("Price Book Code", "Price Book Index Date");
                                                                CustomerNo := ServiceUnitPriceManagement.DetermineCustomerNo(PriceBookType, "Price Book Code");
                                                                ServiceUnitPriceManagement.ValidatePart(CustomerNo, "Part Code");
                                                              END;

                                                   OnLookup=VAR
                                                              ServiceUnitPriceManagement@1100528601 : Codeunit 11012846;
                                                              CustomerNo@1100528600 : Code[20];
                                                              PriceBookType@1100528602 : Option;
                                                            BEGIN
                                                              PriceBookType := ServiceUnitPriceManagement.GetPriceBookType("Price Book Code", "Price Book Index Date");
                                                              CustomerNo := ServiceUnitPriceManagement.DetermineCustomerNo(PriceBookType, "Price Book Code");
                                                              IF ServiceUnitPriceManagement.LookupPart(CustomerNo, "Room Code", "Part Code") THEN
                                                                VALIDATE("Part Code");
                                                            END;

                                                   CaptionML=[ENU=Part;
                                                              NOR=Del;
                                                              SVE=Del] }
    { 90  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning];
                                                   Editable=No }
    { 100 ;   ;Description 2       ;Text50        ;CaptionML=[ENU=Description 2;
                                                              NOR=Beskrivelse 2;
                                                              SVE=Beskrivning 2];
                                                   Editable=No }
    { 110 ;   ;Description 3       ;Text50        ;CaptionML=[ENU=Description 3;
                                                              NOR=Beskrivelse 3;
                                                              SVE=Beskrivning 3];
                                                   Editable=No }
    { 120 ;   ;Unit of Measure     ;Code10        ;CaptionML=[ENU=Unit of Measure;
                                                              NOR=M†leenhet;
                                                              SVE=Enhet];
                                                   Editable=No }
    { 130 ;   ;Unit Price          ;Decimal       ;CaptionML=[ENU=Unit Price;
                                                              NOR=Salgspris;
                                                              SVE=A-pris];
                                                   DecimalPlaces=2:5;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 140 ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                IF Quantity <> 0 THEN
                                                                  TESTFIELD("Unit Price Type", "Unit Price Type"::"Unit Price");
                                                              END;

                                                   CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   DecimalPlaces=0:2;
                                                   BlankZero=Yes }
  }
  KEYS
  {
    {    ;Set Code,Work Order Seq. No.,Line No.   ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE GetBudgetHours@1100528600() BudgetHours : Decimal;
    VAR
      ServiceUnitPriceSet@1100528601 : Record 11072284;
      ServiceUnitPriceManagement@1100528600 : Codeunit 11012846;
    BEGIN
      CalcFieldsPriceBook;
      CASE "Unit Price Type" OF
        "Unit Price Type"::"Unit Price Set":
          IF ServiceUnitPriceSet.GET("Price Book Code", "Price Book Index Date", Code) THEN
            BudgetHours := ServiceUnitPriceSet.GetBudgetHours;
        "Unit Price Type"::"Unit Price":
          BudgetHours := ServiceUnitPriceManagement.GetUnitPriceHours("Price Book Code", "Price Book Index Date", Code) * Quantity;
      END;
    END;

    PROCEDURE CalcFieldsPriceBook@1100528601();
    BEGIN
      CALCFIELDS("Price Book Code", "Price Book Index Date");
    END;

    PROCEDURE UpdateUnitPriceData@1100528604();
    VAR
      ServiceUnitPriceSet@1100528601 : Record 11072284;
      ServiceUnitPrice@1100528600 : Record 11071769;
    BEGIN
      IF Code = '' THEN
        EXIT;
      CalcFieldsPriceBook;
      CASE "Unit Price Type" OF
        "Unit Price Type"::"Unit Price Set":
          IF ServiceUnitPriceSet.GET("Price Book Code", "Price Book Index Date", Code) THEN
            Description := ServiceUnitPriceSet.Description;
        "Unit Price Type"::"Unit Price":
          IF ServiceUnitPrice.GET("Price Book Code", "Price Book Index Date", Code) THEN BEGIN
            Description := ServiceUnitPrice.Description;
            "Description 2" := ServiceUnitPrice."Description 2";
            "Description 3" := ServiceUnitPrice."Description 3";
            "Unit of Measure" := ServiceUnitPrice."Unit of Measure";
            "Unit Price" := ServiceUnitPrice."Unit Price";
          END;
      END;
    END;

    BEGIN
    END.
  }
}

