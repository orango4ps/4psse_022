OBJECT Table 11128571 Sales Header DK info
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PSSEDK;
  }
  PROPERTIES
  {
    LookupPageID=Page11128571;
    DrillDownPageID=Page11128571;
  }
  FIELDS
  {
    { 1   ;   ;tableNo             ;Integer       ;CaptionML=SVE=TableNo }
    { 2   ;   ;Document Type       ;Option        ;CaptionML=[ENU=Document Type;
                                                              NOR=Dokumenttype;
                                                              SVE=Dokumenttyp];
                                                   OptionCaptionML=[ENU=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order,Invoice Proposal;
                                                                    NOR=Tilbud,Ordre,Faktura,Kreditnota,Rammeordre ,Returordre,Fakturaforslag;
                                                                    SVE=Offert,Order,Faktura,Kreditnota,Avropsorder,Returorder,Fakturaf”rslag];
                                                   OptionString=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order,Invoice Proposal }
    { 3   ;   ;No.                 ;Code20        ;TableRelation="Sales Header".No. WHERE (Document Type=FIELD(Document Type));
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=No.;
                                                              NOR=Nr;
                                                              SVE=Nr] }
    { 5047;   ;Version No.         ;Integer       ;CaptionML=[ENU=Version No.;
                                                              NOR=Versjonsnr.;
                                                              SVE=Versionsnr] }
    { 5048;   ;Doc. No. Occurrence ;Integer       ;CaptionML=[ENU=Doc. No. Occurrence;
                                                              NOR=Dokumentnr. Forekomst;
                                                              SVE=Dok.nr f”rekomst] }
    { 11128572;;Account Code DK    ;Text30        ;OnValidate=BEGIN
                                                                IF ("Account Code DK" <> xRec."Account Code DK") AND (xRec.tableNo = 36) THEN BEGIN
                                                                  SalesLines.RESET;
                                                                  SalesLines.SETRANGE("Document Type","Document Type");
                                                                  SalesLines.SETRANGE("Document No.","No.");
                                                                  SalesLines.SETFILTER(Type, '>%1', SalesLines.Type::" ");
                                                                  SalesLines.SETFILTER("Account Code DK", '%1|%2', xRec."Account Code DK", '');
                                                                  SalesLines.MODIFYALL("Account Code DK", "Account Code DK");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Account Code;
                                                              NOR=Kontokode;
                                                              SVE=Kontokod (DK)] }
    { 11128575;;Sell-to Contact Phone No.;Text30  ;ExtendedDatatype=Phone No.;
                                                   CaptionML=[ENU=Sell-to Contact Phone No.;
                                                              NOR=Salg til-telefonnr;
                                                              SVE=K”pares telefonnr] }
    { 11128576;;Sell-to Contact Fax No.;Text30    ;CaptionML=[ENU=Sell-to Contact Fax No.;
                                                              NOR=Salg til-faxnr;
                                                              SVE=K”pares Faxnr] }
    { 11128577;;Sell-to Contact E-Mail;Text80     ;ExtendedDatatype=E-Mail;
                                                   CaptionML=[ENU=Sell-to Contact E-Mail;
                                                              NOR=Salg til-e-post;
                                                              SVE=K”pares e-post] }
  }
  KEYS
  {
    {    ;tableNo,Document Type,No.,Version No.,Doc. No. Occurrence;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      SalesLines@1100285000 : Record 37;

    PROCEDURE StoreSalesHeaderArchiveData@1100530650(VAR SalesHeaderArchive@1100530650 : Record 11072055);
    VAR
      SalesHeaderDKinfo@1100530652 : Record 11128571;
      SalesHeaderDKinfoArchive@1100530651 : Record 11128571;
    BEGIN
      //store DK-data
      SalesHeaderDKinfo.SETRANGE(tableNo, DATABASE::"Sales Header");
      SalesHeaderDKinfo.SETRANGE("Document Type", SalesHeaderArchive."Document Type");
      SalesHeaderDKinfo.SETRANGE("No.", SalesHeaderArchive."No.");
      IF SalesHeaderDKinfo.FINDSET THEN
      REPEAT
        SalesHeaderDKinfoArchive := SalesHeaderDKinfo;
        SalesHeaderDKinfoArchive.tableNo := DATABASE::"Sales Header Archive";
        SalesHeaderDKinfoArchive."Version No." := SalesHeaderArchive."Version No.";
        SalesHeaderDKinfoArchive."Doc. No. Occurrence" := SalesHeaderArchive."Doc. No. Occurrence";
        SalesHeaderDKinfoArchive.INSERT;
      UNTIL SalesHeaderDKinfo.NEXT=0;
    END;

    PROCEDURE RestoreSalesHeaderArchiveData@1100530651(VAR SalesHeaderArchive@1100530650 : Record 11072055);
    VAR
      SalesHeaderDKinfo@1100530652 : Record 11128571;
      SalesHeaderDKinfoArchive@1100530651 : Record 11128571;
    BEGIN
      //restore DK-data
      SalesHeaderDKinfoArchive.SETRANGE(tableNo, DATABASE::"Sales Header Archive");
      SalesHeaderDKinfoArchive.SETRANGE("Document Type", SalesHeaderArchive."Document Type");
      SalesHeaderDKinfoArchive.SETRANGE("No.", SalesHeaderArchive."No.");
      SalesHeaderDKinfoArchive.SETRANGE("Version No.", SalesHeaderArchive."Version No.");
      SalesHeaderDKinfoArchive.SETRANGE("Doc. No. Occurrence", SalesHeaderArchive."Doc. No. Occurrence");
      IF SalesHeaderDKinfoArchive.FINDSET THEN
      REPEAT
        SalesHeaderDKinfo := SalesHeaderDKinfoArchive;
        SalesHeaderDKinfo.tableNo := DATABASE::"Sales Header";
        SalesHeaderDKinfo.INSERT;
      UNTIL SalesHeaderDKinfoArchive.NEXT=0;
    END;

    PROCEDURE CopyFromSalesDocToHeader@9(VAR ToSalesHeader@1002 : Record 36;VAR FromSalesHeader@1003 : Record 36);
    VAR
      ToSalesHeaderDKinfo@1100530651 : Record 11128571;
      FromSalesHeaderDKinfo@1100530650 : Record 11128571;
    BEGIN
      ToSalesHeaderDKinfo.SETRANGE(tableNo, DATABASE::"Sales Header");
      ToSalesHeaderDKinfo.SETRANGE("Document Type", ToSalesHeader."Document Type");
      ToSalesHeaderDKinfo.SETRANGE("No.", ToSalesHeader."No.");
      ToSalesHeaderDKinfo.DELETEALL;
      ToSalesHeaderDKinfo.RESET;

      FromSalesHeaderDKinfo.SETRANGE(tableNo, DATABASE::"Sales Header");
      FromSalesHeaderDKinfo.SETRANGE("Document Type", FromSalesHeader."Document Type");
      FromSalesHeaderDKinfo.SETRANGE("No.", FromSalesHeader."No.");

      IF FromSalesHeaderDKinfo.FINDSET(FALSE) THEN
      REPEAT
        ToSalesHeaderDKinfo := FromSalesHeaderDKinfo;
        ToSalesHeaderDKinfo."Document Type" := ToSalesHeader."Document Type";
        ToSalesHeaderDKinfo."No." := ToSalesHeader."No.";
        ToSalesHeaderDKinfo.INSERT;
      UNTIL FromSalesHeaderDKinfo.NEXT=0;
    END;

    PROCEDURE CopyFromPstdSalesDocToHdr@8(VAR ToSalesHeader@1002 : Record 36;FromDocType@1004 : Option;VAR FromSalesShptHeader@1003 : Record 110;VAR FromSalesInvHeader@1009 : Record 112;VAR FromReturnRcptHeader@1007 : Record 6660;VAR FromSalesCrMemoHeader@1005 : Record 114);
    VAR
      SalesDocType@1100530650 : 'Quote,Blanket Order,Order,Invoice,Return Order,Credit Memo,Posted Shipment,Posted Invoice,Posted Return Receipt,Posted Credit Memo';
      ToSalesHeaderDKinfo@1100530652 : Record 11128571;
      Cust@1100530651 : Record 18;
    BEGIN
      ToSalesHeaderDKinfo.SETRANGE(tableNo, DATABASE::"Sales Header");
      ToSalesHeaderDKinfo.SETRANGE("Document Type", ToSalesHeader."Document Type");
      ToSalesHeaderDKinfo.SETRANGE("No.", ToSalesHeader."No.");
      ToSalesHeaderDKinfo.DELETEALL;
      ToSalesHeaderDKinfo.RESET;

      Cust.GET(ToSalesHeader."Sell-to Customer No.");

      ToSalesHeaderDKinfo.tableNo := DATABASE::"Sales Header";
      ToSalesHeaderDKinfo."Document Type" := ToSalesHeader."Document Type";
      ToSalesHeaderDKinfo."No." := ToSalesHeader."No.";
      CASE FromDocType OF
        SalesDocType::"Posted Shipment":
          BEGIN
            ToSalesHeaderDKinfo."Account Code DK" := Cust."Account Code DK";
            //ToSalesHeaderDKinfo."Sell-to Contact Phone No." :=
            //ToSalesHeaderDKinfo."Sell-to Contact Fax No." :=
            //ToSalesHeaderDKinfo."Sell-to Contact E-Mail" :=
          END;
        SalesDocType::"Posted Invoice":
          BEGIN
            ToSalesHeaderDKinfo."Account Code DK" := Cust."Account Code DK";
            ToSalesHeaderDKinfo."Sell-to Contact Phone No." := FromSalesInvHeader."Sell-to Contact Phone No.";
            ToSalesHeaderDKinfo."Sell-to Contact Fax No." := FromSalesInvHeader."Sell-to Contact Fax No.";
            ToSalesHeaderDKinfo."Sell-to Contact E-Mail" := FromSalesInvHeader."Sell-to Contact E-Mail";
          END;
        SalesDocType::"Posted Return Receipt":
          BEGIN
            ToSalesHeaderDKinfo."Account Code DK" := Cust."Account Code DK";
            //ToSalesHeaderDKinfo."Sell-to Contact Phone No." :=
            //ToSalesHeaderDKinfo."Sell-to Contact Fax No." :=
            //ToSalesHeaderDKinfo."Sell-to Contact E-Mail" :=
          END;
        SalesDocType::"Posted Credit Memo":
          BEGIN
            ToSalesHeaderDKinfo."Account Code DK" := Cust."Account Code DK";
            ToSalesHeaderDKinfo."Sell-to Contact Phone No." := FromSalesCrMemoHeader."Sell-to Contact Phone No.";
            ToSalesHeaderDKinfo."Sell-to Contact Fax No." := FromSalesCrMemoHeader."Sell-to Contact Fax No.";
            ToSalesHeaderDKinfo."Sell-to Contact E-Mail" := FromSalesCrMemoHeader."Sell-to Contact E-Mail";
          END;
      END;
      ToSalesHeaderDKinfo.INSERT;
    END;

    BEGIN
    END.
  }
}

