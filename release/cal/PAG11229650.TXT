OBJECT Page 11229650 DPT Admin Tool Project
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=DPT-Admin-Tool (Projekt);
               ENU=DPT Admin Tool (Project);
               NLD="DPT Admin tool (Project) "];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table11072003;
    PageType=Worksheet;
    OnOpenPage=BEGIN
                 DPTSetup.GET;
                 DPTSetup.TESTFIELD("Admin Tool Password");
                 SETRANGE("Generate Document Portal Site", TRUE);
                 DPTChangeLogEntry.AddLogEntry(RECORDID, CurrPage.OBJECTID, TRUE);
               END;

    OnClosePage=BEGIN
                  DPTChangeLogEntry.AddLogEntry(RECORDID, CurrPage.OBJECTID, FALSE);
                END;

    OnAfterGetRecord=BEGIN
                       CheckPermission;
                     END;

    ActionList=ACTIONS
    {
      { 1100528307;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100528308;1 ;Action    ;
                      CaptionML=[DEU=Projekt bereinigen;
                                 ENU="Cleanup Project ";
                                 NLD=Project opschonen];
                      Image=Restore;
                      OnAction=VAR
                                 Job@1100528300 : Record 11072003;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(Job);
                                 CleanupProjectsite(Job);
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 1100528312;1 ;Action    ;
                      CaptionML=[DEU=ALTE PPT-Felder bereinigen;
                                 ENU=Cleanup OLD PPT fields;
                                 NLD=Oude PPT velden opschonen];
                      Image=Restore;
                      OnAction=BEGIN
                                 CleanupOldPPTFields;
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 1100528309;1 ;Action    ;
                      CaptionML=[DEU=≥Site generiert  auf TRUE setzen;
                                 ENU=Set 'Site Generated' to TRUE;
                                 NLD='Site aangemaakt' op WAAR zetten];
                      Image=Restore;
                      OnAction=VAR
                                 Job@1100528300 : Record 11072003;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(Job);
                                 ToggleGenerateSiteFlag(Job, TRUE);
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 1100528311;1 ;Action    ;
                      CaptionML=[DEU=≥Site generiert  auf FALSE setzen;
                                 ENU=Set 'Site Generated' to FALSE;
                                 NLD='Site aangemaakt' op ONWAAR zetten];
                      Image=Restore;
                      OnAction=VAR
                                 Job@1100528300 : Record 11072003;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(Job);
                                 ToggleGenerateSiteFlag(Job, FALSE);
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100528300;0;Container;
                ContainerType=ContentArea }

    { 1100528310;1;Field  ;
                ExtendedDatatype=Masked;
                CaptionML=[DEU=Kennwort;
                           ENU=Password;
                           NLD=Wachtwoord;
                           NOR=Passord;
                           SVE=Lîsenord];
                SourceExpr=Password;
                OnValidate=BEGIN
                             CheckPassword;
                           END;
                            }

    { 1100528301;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1100528302;2;Field  ;
                SourceExpr="No." }

    { 1100528303;2;Field  ;
                SourceExpr=Description }

    { 1100528304;2;Field  ;
                SourceExpr="Generate Document Portal Site" }

    { 1100528305;2;Field  ;
                SourceExpr="Document Portal Site Generated" }

    { 1100528306;2;Field  ;
                SourceExpr="Document Portal URL" }

  }
  CODE
  {
    VAR
      DPTSetup@1100528303 : Record 11229823;
      DPTChangeLogEntry@1100528308 : Record 11012207;
      Window@1100528300 : Dialog;
      Password@1100528302 : Text[30];
      CLEANPROJECTTEXT@1100528309 : TextConst 'ENU=Cleanup Project';
      CLEANOLDPROJECTTEXT@1100528311 : TextConst 'ENU=Cleanup old PPT project Fields';
      TOGGLEGENERATEFLAGTEXT@1100528310 : TextConst 'ENU=Toggle Flag';
      Text000@1100528306 : TextConst 'DEU=Falsches Passwort.;ENU=Incorrect Password.;NLD=Wachtwoord niet goed.;NOR=Feil passord.;SVE=Ogiltigt lîsenord.';
      Text001@1100528307 : TextConst 'DEU=#1############################;ENU=#1############################;NLD=#1############################;NOR=#1############################;SVE=#1############################';
      Text002@1100528304 : TextConst 'DEU=Sie sind dabei, markierte Projekte zurÅckzusetzen!\Dadurch werden alle verbundenen Belege gelîscht!\\Sind Sie sicher?;ENU=You are about to reset marked Projects!\This will delete all related Documents!!\\Are you sure?;NLD=U gaat alle gemarkeerde projecten resetten!\Hierdoor worden alle gerelateerde documenten verwijderd!!\\Weet u dit zeker?';
      Text003@1100528301 : TextConst 'DEU=Sind Sie ganz sicher?\\Diese Handlung hat rechtliche Konsequenzen!!;ENU=Are you very sure?\\This action does have legal consequences!!;NLD=Weet u het heel zeker?\\Deze actie heeft strafrechterlijke consequenties!!';
      Text005@1100529400 : TextConst 'DEU=Sie haben keine Berechtigung zur AusfÅhrung dieser Seite.;ENU=You have no Permission to run this Page.;NLD=U hebt geen rechten om deze pagina uit te voeren.';

    LOCAL PROCEDURE CheckPermission@1100529400();
    BEGIN
      IF (NOT DPTSetup.WRITEPERMISSION) THEN BEGIN
        MESSAGE(Text005);
        CurrPage.CLOSE;
      END;
    END;

    LOCAL PROCEDURE CheckPassword@1100409010();
    BEGIN
      CheckPermission;
      IF (DPTSetup."Admin Tool Password" = '') OR (Password <> DPTSetup."Admin Tool Password") THEN
        ERROR(Text000);
    END;

    LOCAL PROCEDURE CleanupProjectsite@1100528300(VAR Job@1100528304 : Record 11072003);
    VAR
      DPTSubsiteProject@1100528301 : Record 11072330;
      DPTSubsiteDocTypeProject@1100528300 : Record 11072331;
      DocumentLink@1100528306 : Record 11012747;
      DocumentProperties@1100528307 : Record 11012746;
      ProjectPurchaseAction@1100528309 : Record 11012026;
      PurchActionDocumentRel@1100528308 : Record 11012071;
      BudgetLine@1100528310 : Record 11012001;
      PurchaseHeader@1100528311 : Record 38;
      DPTHeaderText@1100528312 : Record 11229943;
      DPTDocumentVersion@1100529900 : Record 11229944;
      RecRef@1100528305 : RecordRef;
    BEGIN
      CheckPassword;
      IF (NOT Job.FINDSET(TRUE)) THEN
        EXIT;
      IF (NOT CONFIRM(Text002, FALSE)) THEN
        EXIT;
      IF (NOT CONFIRM(Text003, FALSE)) THEN
        EXIT;

      Window.OPEN(Text001);
      REPEAT
        DPTChangeLogEntry.AddLogEntry(Job.RECORDID, CLEANPROJECTTEXT, TRUE);
        Window.UPDATE(1, Job.RECORDID);
        RecRef.GETTABLE(Job);

        DocumentLink.SETCURRENTKEY("Record ID");
        DocumentLink.SETRANGE("Record ID", RecRef.RECORDID);
        DocumentLink.SETRANGE("Table No.", DATABASE::Job);
        IF (DocumentLink.FINDSET(TRUE, TRUE)) THEN BEGIN
          REPEAT
            DocumentLink.DELETE;

            DPTDocumentVersion.SETRANGE("Source Type", DPTDocumentVersion."Source Type"::Project);
            DPTDocumentVersion.SETRANGE("Source Code", DocumentLink."Document No.");
            DPTDocumentVersion.DELETEALL;

            IF (DocumentProperties.GET(DocumentLink."Document No.")) THEN
              DocumentProperties.DELETE;
          UNTIL (DocumentLink.NEXT = 0);
        END;

        PurchActionDocumentRel.SETRANGE(Source, PurchActionDocumentRel.Source::Project);
        PurchActionDocumentRel.SETRANGE("Estimate No.", '');
        PurchActionDocumentRel.SETRANGE("Project No.", Job."No.");
        PurchActionDocumentRel.DELETEALL(TRUE);

        ProjectPurchaseAction.SETRANGE("Project No.", Job."No.");
        IF (ProjectPurchaseAction.FINDSET(TRUE)) THEN BEGIN
          REPEAT
            BudgetLine.SETRANGE("Project No.", ProjectPurchaseAction."Project No.");
            BudgetLine.SETRANGE("Purchase Action", ProjectPurchaseAction.Code);
            BudgetLine.MODIFYALL(Advised, FALSE);
            BudgetLine.MODIFYALL("Purchase Order No.", '');
            BudgetLine.MODIFYALL("Purchase Order Line No.", 0);
            BudgetLine.MODIFYALL(Advised, FALSE);

            PurchaseHeader.SETRANGE("Job No.", ProjectPurchaseAction."Project No.");
            PurchaseHeader.SETRANGE("Purchase Action", ProjectPurchaseAction.Code);
            PurchaseHeader.MODIFYALL("Job No.", '');
            PurchaseHeader.MODIFYALL("Purchase Action", '');
          UNTIL (ProjectPurchaseAction.NEXT = 0);
        END;

        DPTHeaderText.SETRANGE("Table No.", DATABASE::Job);
        DPTHeaderText.SETRANGE("Field No.", Job.FIELDNO("Document Portal Site Generated"));
        DPTHeaderText.SETRANGE("Source No.", Job."No.");
        DPTHeaderText.DELETEALL;

        DPTHeaderText.SETRANGE("Table No.", DATABASE::"DPT Subsite Project");
        DPTHeaderText.SETRANGE("Field No.", DPTSubsiteProject.FIELDNO(Code));
        DPTHeaderText.SETRANGE("Source No.", Job."No.");
        DPTHeaderText.DELETEALL;

        Job."Document Portal Site Generated" := FALSE;
        Job. "Document Portal URL" := '';
        Job.MODIFY;

        DPTSubsiteProject.SETRANGE("Project No.", Job."No.");
        DPTSubsiteProject.MODIFYALL("Synchronized On", 0DT);

        DPTSubsiteDocTypeProject.SETRANGE("Project No.", Job."No.");
        DPTSubsiteDocTypeProject.MODIFYALL("Synchronized On", 0DT);

        DPTChangeLogEntry.AddLogEntry(Job.RECORDID, CLEANPROJECTTEXT, FALSE);
      UNTIL (Job.NEXT = 0);
      Window.CLOSE;
    END;

    LOCAL PROCEDURE CleanupOldPPTFields@1100528303();
    VAR
      Job@1100529400 : Record 11072003;
    BEGIN
      CheckPassword;
      Job.SETRANGE("Project Portal Project", TRUE);
      IF (NOT Job.FINDSET(TRUE)) THEN
        EXIT;

      Window.OPEN(Text001);
      REPEAT
        DPTChangeLogEntry.AddLogEntry(Job.RECORDID, CLEANOLDPROJECTTEXT, TRUE);
        Window.UPDATE(1, Job.RECORDID);
        Job."Project Portal Project" := FALSE;
        Job."SharePoint Site URL" := '';
        Job."SharePoint Site Generated" := FALSE;
        Job."Make SharePoint Site" := FALSE;
        Job."Quote Subsite Generated" := FALSE;
        Job.MODIFY;
        DPTChangeLogEntry.AddLogEntry(Job.RECORDID, CLEANOLDPROJECTTEXT, FALSE);
      UNTIL (Job.NEXT = 0);
      Window.CLOSE;
    END;

    LOCAL PROCEDURE ToggleGenerateSiteFlag@1100528301(VAR Job@1100528301 : Record 11072003;BoolIn@1100528300 : Boolean);
    BEGIN
      CheckPassword;
      IF (NOT Job.FINDSET(TRUE)) THEN
        EXIT;

      Window.OPEN(Text001);
      REPEAT
        DPTChangeLogEntry.AddLogEntry(Job.RECORDID, TOGGLEGENERATEFLAGTEXT, TRUE);
        Window.UPDATE(1, Job.RECORDID);
        Job."Document Portal Site Generated" := BoolIn;
        Job.MODIFY;
        DPTChangeLogEntry.AddLogEntry(Job.RECORDID, TOGGLEGENERATEFLAGTEXT, FALSE);
      UNTIL (Job.NEXT = 0);
      Window.CLOSE;
    END;

    LOCAL PROCEDURE ToggleGenerateQuoteSiteFlag@1100528302(VAR Job@1100528301 : Record 11072003;BoolIn@1100528300 : Boolean);
    BEGIN
      CheckPassword;
      IF (NOT Job.FINDSET(TRUE)) THEN
        EXIT;

      Window.OPEN(Text001);
      REPEAT
        DPTChangeLogEntry.AddLogEntry(Job.RECORDID, TOGGLEGENERATEFLAGTEXT, TRUE);
        Window.UPDATE(1, Job.RECORDID);
        //Job."Document Portal Quote Site Generated" := BoolIn;
        Job.MODIFY;
        DPTChangeLogEntry.AddLogEntry(Job.RECORDID, TOGGLEGENERATEFLAGTEXT, FALSE);
      UNTIL (Job.NEXT = 0);
      Window.CLOSE;
    END;

    BEGIN
    END.
  }
}

