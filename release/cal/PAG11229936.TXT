OBJECT Page 11229936 Bing Maps Service Call
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Bing Maps-Servicemeldung;
               ENU=Bing Maps Service Call;
               NLD=Bing Maps service melding];
    SourceTable=Table11072392;
    OnInit=BEGIN
             ServiceObjectTableIDText := FORMAT(DATABASE::"Service-Object");
             ServiceObjectTableIDInt := DATABASE::"Service-Object";
           END;

    ActionList=ACTIONS
    {
      { 1100525005;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100525006;1 ;Action    ;
                      Name=Update Map;
                      CaptionML=;
                      OnAction=BEGIN
                                 CurrPage.Map.reset;
                                 StartMapWithSettings;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100525000;;Container;
                Name=Container;
                CaptionML=[DEU=Container;
                           ENU=Container;
                           NLD=Container;
                           NOR=Container;
                           SVE=Beh†llare];
                ContainerType=ContentArea }

    { 1100525002;1;Group  ;
                CaptionML=[ENU=Options;
                           NOR=Alternativer;
                           SVE=Alternativ];
                GroupType=Group }

    { 1100525001;1;Field  ;
                SourceExpr=Range }

    { 1100525003;1;Group  ;
                CaptionML=[DEU=Karte;
                           ENU=Map;
                           NLD=Kaart];
                GroupType=Group }

    { 1100525004;2;Field  ;
                Name=Map;
                CaptionML=[DEU=Karte;
                           ENU=Map;
                           NLD=Kaart];
                ControlAddIn=[Fps.Nav.Addin.BingMaps;PublicKeyToken=024bd013665211e9] }

  }
  CODE
  {
    VAR
      Latitude@1100525000 : Code[255];
      Longitude@1100525001 : Code[255];
      Text001@1100525002 : TextConst 'DEU=Meldungsstandort;ENU=Call location;NLD=Melding locatie';
      ServiceObject@1100525003 : Record 11071691;
      ServiceObjectTableIDText@1100525004 : Text[30];
      BingMapsHandler@1100525005 : Codeunit 11012485;
      Text002@1100525006 : TextConst 'DEU=Ausw„hlen;ENU=Choose;NLD=Kies';
      ServiceCall@1100525007 : Record 11012822;
      Text003@1100525008 : TextConst 'DEU=Es ist ein Fehler aufgetreten. Die Seite wird jetzt geschlossen.;ENU=Something went wrong. The page will close now.;NLD=Er ging iets mis. De pagina sluit nu.';
      ServiceObjectTableIDInt@1100525009 : Integer;
      Text004@1100525010 : TextConst 'DEU=Konfigurieren Sie Bing Maps.;ENU=Please configure Bing Maps.;NLD=Stel Bing Maps in.';

    PROCEDURE SetBaseLocation@1100525000(lat@1100525000 : Text;lng@1100525001 : Text);
    BEGIN
      Latitude := lat;
      Longitude := lng;
    END;

    LOCAL PROCEDURE AddBaseLocationPin@1100525002();
    BEGIN
      CurrPage.Map.addPinToMapLatLng(Text001, Text001, Text001, Text001, '', Latitude, Longitude);
    END;

    LOCAL PROCEDURE StartMapWithSettings@1100525007();
    VAR
      titleValue@1100525000 : Text;
      descriptionValue@1100525001 : Text;
      ReturnType@1100525002 : 'NAV,FSA';
    BEGIN
      AddBaseLocationPin;
      ServiceObject.RESET;
      ServiceObject.FINDSET;
      REPEAT
        titleValue := BingMapsHandler.GetValueFromField(ServiceObject.RECORDID, ServiceObjectTableIDInt,
                        BingMapsHandler.GetFieldIDForTitle(ServiceObjectTableIDInt, ReturnType::NAV));
        descriptionValue := BingMapsHandler.BuildDescription(ServiceObject.RECORDID, ServiceObjectTableIDInt, ReturnType::NAV);
        IF (ServiceObject.Latitude <> '') AND (ServiceObject.Longitude <> '') THEN
          IF BingMapsHandler.GetDistanceBetweenPoints(Latitude, Longitude, ServiceObject.Latitude, ServiceObject.Longitude) < Range THEN
          CurrPage.Map.addPinToMapLatLng(ServiceObject."No.", titleValue, ServiceObjectTableIDText, descriptionValue,
                      '', ServiceObject.Latitude, ServiceObject.Longitude);
      UNTIL ServiceObject.NEXT = 0;
    END;

    PROCEDURE SetCallRecord@1100525001(ServiceCallPar@1100525000 : Record 11012822);
    BEGIN
      ServiceCall := ServiceCallPar;
    END;

    EVENT Map@-1100525004::ControlAddInReady@20();
    VAR
      BingMapsSetup@1100525000 : Page 11127140;
    BEGIN
      IF BingMapsHandler.SetupIsDoneAndEnabled THEN
        CurrPage.Map.setApiKey(BingMapsHandler.GetApiKey(CurrPage.OBJECTID(FALSE)))
      ELSE
        BEGIN
          MESSAGE(Text004);
          BingMapsSetup.RUN;
          CurrPage.CLOSE;
        END;
    END;

    EVENT Map@-1100525004::MapLoaded@21();
    BEGIN
      IF BingMapsHandler.DisplayAsPin(DATABASE::"Service Order") THEN
          CurrPage.Map.displayAsPin(Text001);
      CurrPage.Map.setExtraAction(Text002);
      CurrPage.Map.allowActionForType(Text002, ServiceObjectTableIDText);
      StartMapWithSettings;
    END;

    EVENT Map@-1100525004::PinAdded@22(no@1100525001 : Text;type@1100525000 : Text);
    BEGIN
      CurrPage.Map.setBestView;
    END;

    EVENT Map@-1100525004::AddLocationToSelection@23(no@1100525001 : Text;type@1100525000 : Text);
    BEGIN
    END;

    EVENT Map@-1100525004::RemoveLocationFromSelection@24(no@1100525001 : Text;type@1100525000 : Text);
    BEGIN
    END;

    EVENT Map@-1100525004::DirectionsReady@25(distance@1100525001 : Text;time@1100525000 : Text);
    BEGIN
    END;

    EVENT Map@-1100525004::DirectionsError@26();
    BEGIN
    END;

    EVENT Map@-1100525004::LocationUpdated@27(no@1100525003 : Text;type@1100525002 : Text;lat@1100525001 : Text;lng@1100525000 : Text);
    BEGIN
    END;

    EVENT Map@-1100525004::LocationAdded@28(no@1100525003 : Text;type@1100525002 : Text;lat@1100525001 : Text;lng@1100525000 : Text);
    BEGIN
    END;

    EVENT Map@-1100525004::LocationRemoved@29(no@1100525001 : Text;type@1100525000 : Text);
    BEGIN
    END;

    EVENT Map@-1100525004::ExtraAction@30(action@1100525002 : Text;no@1100525001 : Text;type@1100525000 : Text);
    BEGIN
      IF action = Text002 THEN BEGIN
        IF ServiceObject.GET(no) THEN BEGIN
          ServiceCall."Object No." := ServiceObject."No.";
          ServiceCall."Service Location No." := ServiceObject."Service Location No.";
          ServiceCall.MODIFY;
        END
        ELSE
          MESSAGE(Text003);
        CurrPage.CLOSE;
      END;
    END;

    EVENT Map@-1100525004::ZoomLevel@31(level@1100525000 : Text);
    BEGIN
    END;

    EVENT Map@-1100525004::RegisterEvent@32(no@1100528102 : Text;type@1100528101 : Text;eventname@1100528100 : Text);
    BEGIN
      BingMapsHandler.WriteToBingMapsLogUsingQuery(no, CurrPage.OBJECTID(FALSE), eventname, type);
    END;

    BEGIN
    END.
  }
}

