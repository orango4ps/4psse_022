OBJECT Report 77796 Import DotNet Translation File
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=TTool;
  }
  PROPERTIES
  {
    CaptionML=ENU=Import DotNet Translation File;
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  ImportTranslationXMLFile@1100525000 : Codeunit 77792;
                BEGIN
                  IF NOT TTNetProduct.GET(NetProductCode) THEN
                    ERROR(Text003);

                  IF NOT TTNetFile.GET(NetProductCode,NetFileCode) THEN
                    ERROR(Text004);

                  IF NOT TTLanguage.GET(InsertAsLanguageCode) THEN
                    ERROR(Text005);

                  IF TTNetProduct."Source Language ID" <> InsertAsLanguageCode THEN BEGIN
                    IF TTNetProduct."Target Language ID" <> InsertAsLanguageCode THEN
                      ERROR(Text006);
                  END;

                  IF FileName = '' THEN
                    ERROR(Text002);

                  ImportTranslationXMLFile.ImportTranslationData(NetProductCode,NetFileCode,InsertAsLanguageCode,OverwriteExistingCaption,FileName);
                END;

    OnPostReport=BEGIN
                   TTNetFile.GET(NetProductCode,NetFileCode);
                   TTNetFile."Import File Name" := FileName;
                   TTNetFile.MODIFY;
                 END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100409000;;Container;
                  ContainerType=ContentArea }

      { 1100525000;1;Group  ;
                  CaptionML=[ENU=Options;
                             SVE=Alternativ];
                  GroupType=Group }

      { 3   ;2   ;Field     ;
                  CaptionML=ENU=Add Captions to Product Code;
                  SourceExpr=NetProductCode;
                  TableRelation="TT DotNet Product";
                  OnValidate=BEGIN
                               IF TTNetProduct.GET(NetProductCode) THEN
                                 InsertAsLanguageCode := TTNetProduct."Source Language ID";
                             END;
                              }

      { 4   ;2   ;Field     ;
                  CaptionML=ENU=Add Captions to File Code;
                  SourceExpr=NetFileCode;
                  TableRelation="TT DotNet Translation File".Code;
                  OnLookup=BEGIN
                             LookupNetFileCode;
                           END;
                            }

      { 2   ;2   ;Field     ;
                  CaptionML=ENU=Insert Language;
                  SourceExpr=InsertAsLanguageCode;
                  TableRelation="TT Language" }

      { 1   ;2   ;Field     ;
                  CaptionML=ENU=Overwrite existing caption;
                  SourceExpr=OverwriteExistingCaption }

      { 1100409001;2;Field  ;
                  CaptionML=[ENU=File Name;
                             SVE=Filnamn];
                  SourceExpr=FileName;
                  OnAssistEdit=VAR
                                 FileManagement@1100409000 : Codeunit 419;
                               BEGIN
                                 FileName := FileManagement.OpenFileDialog('',FileName,Text001);
                               END;
                                }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1100525002 : TextConst 'ENU=Import Translation Data';
      Text001@1100525003 : TextConst 'ENU=All Files|*.*;SVE=Alla filer|*.*';
      Text002@1100525000 : TextConst 'ENU=Please specify a file name.;SVE=Ange ett filnamn.';
      TTNetProduct@1011 : Record 77791;
      TTNetFile@1010 : Record 77794;
      TTLanguage@1006 : Record 77781;
      FileName@1100525001 : Text;
      NetProductCode@1003 : Code[20];
      NetFileCode@1002 : Code[20];
      InsertAsLanguageCode@1001 : Code[10];
      OverwriteExistingCaption@1000 : Boolean;
      Text003@1007 : TextConst 'ENU=Product Code must be filled and exist as a product.';
      Text004@1004 : TextConst 'ENU=File Code must be filled in and exist at product.';
      Text005@1005 : TextConst 'ENU=Language Code must be filled in.';
      Text006@1008 : TextConst 'ENU=Language Code must be source or target language of product.';

    PROCEDURE LookupNetFileCode@2();
    VAR
      TTDotNetFile@1001 : Record 77794;
      TTDotNetFiles@1000 : Page 77792;
    BEGIN
      TTDotNetFile.SETRANGE("Product Code",NetProductCode);
      IF TTDotNetFile.GET(NetProductCode,NetFileCode) THEN;

      IF PAGE.RUNMODAL(0,TTDotNetFile) = ACTION::LookupOK THEN BEGIN
        NetFileCode := TTDotNetFile.Code;
      END;
    END;

    PROCEDURE SetTTNetFile@1100525004(locNetFile@1100525000 : Record 77794);
    BEGIN
      TTNetProduct.GET(locNetFile."Product Code");
      NetProductCode := TTNetProduct.Code;

      TTNetFile.GET(locNetFile."Product Code", locNetFile.Code);
      NetFileCode := TTNetFile.Code;

      InsertAsLanguageCode := TTNetProduct."Source Language ID";
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

