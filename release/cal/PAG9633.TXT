OBJECT Page 9633 Page Inspection Extensions
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Page Inspection Extensions;
               NOR=Utvidelser for sideinspeksjon;
               SVE=Till„gg f”r sidinspektion];
    SourceTable=Table2000000153;
    PageType=ListPart;
    OnAfterGetRecord=VAR
                       NavAppObjectMetadata@1000 : Record 2000000150;
                     BEGIN
                       Version := STRSUBSTNO('%1.%2.%3',"Version Major","Version Minor","Version Build");
                       PublishedBy := STRSUBSTNO('by %1',Publisher);

                       TypeOfExtension := '';

                       IF NavAppObjectMetadata.READPERMISSION THEN BEGIN
                         NavAppObjectMetadata.RESET;
                         NavAppObjectMetadata.SETFILTER("App Package ID",'%1',"Package ID");
                         // page added by extension
                         NavAppObjectMetadata.SETFILTER("Object ID",'%1',CurrentPageId);
                         NavAppObjectMetadata.SETFILTER("Object Type",'%1',NavAppObjectMetadata."Object Type"::Page);
                         IF NavAppObjectMetadata.FINDFIRST THEN
                           TypeOfExtension := TypeOfExtension + ', ' + NewPageLbl;

                         // table added by extension
                         NavAppObjectMetadata.SETFILTER("Object ID",'%1',CurrentTableId);
                         NavAppObjectMetadata.SETFILTER("Object Type",'%1',NavAppObjectMetadata."Object Type"::Table);
                         IF NavAppObjectMetadata.FINDFIRST THEN
                           TypeOfExtension := TypeOfExtension + ', ' + NewTableLbl;

                         NavAppObjectMetadata.RESET;
                         NavAppObjectMetadata.SETFILTER("App Package ID",'%1',"Package ID");
                         // page extended by extension
                         NavAppObjectMetadata.SETFILTER("Object Subtype",'%1',STRSUBSTNO('%1',CurrentPageId));
                         NavAppObjectMetadata.SETFILTER("Object Type",'%1',NavAppObjectMetadata."Object Type"::PageExtension);
                         IF NavAppObjectMetadata.FINDFIRST THEN
                           TypeOfExtension := TypeOfExtension + ', ' + ExtPageLbl;

                         // table extended by extension
                         NavAppObjectMetadata.SETFILTER("Object Subtype",'%1',STRSUBSTNO('%1',CurrentTableId));
                         NavAppObjectMetadata.SETFILTER("Object Type",'%1',NavAppObjectMetadata."Object Type"::TableExtension);
                         IF NavAppObjectMetadata.FINDFIRST THEN
                           TypeOfExtension := TypeOfExtension + ', ' + ExtTableLbl;

                         TypeOfExtension := DELCHR(TypeOfExtension,'<',',');
                       END;
                     END;

  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                Visible=IsExtensionListVisible;
                GroupType=Repeater }

    { 8   ;2   ;Field     ;
                CaptionML=[ENU=Package ID;
                           NOR=Pakke-ID;
                           SVE=Paket-ID];
                ToolTipML=[ENU=Specifies the ID of the package.;
                           NOR=Angir ID-en for pakken.;
                           SVE=Anger ID f”r paketet.];
                ApplicationArea=#All;
                SourceExpr="Package ID";
                ShowCaption=No }

    { 3   ;2   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=Name;
                           NOR=Navn;
                           SVE=Namn];
                ToolTipML=[ENU=Specifies the name of the extension.;
                           NOR=Angir navnet p† utvidelsen.;
                           SVE=Anger namnet p† till„gget.];
                ApplicationArea=#All;
                SourceExpr=Name;
                ShowCaption=No }

    { 5   ;2   ;Field     ;
                CaptionML=[ENU=Version;
                           NOR=Versjon;
                           SVE=Version];
                ToolTipML=[ENU=Specifies the version of extension.;
                           NOR=Angir versjonen av utvidelsen.;
                           SVE=Anger till„ggets version.];
                ApplicationArea=#All;
                SourceExpr=Version;
                ShowCaption=No }

    { 7   ;2   ;Field     ;
                CaptionML=[ENU=Published by;
                           NOR=Publisert av;
                           SVE=Publicerades av];
                ToolTipML=[ENU=Specifies who published the extension.;
                           NOR=Angir hvem som publiserte utvidelsen.;
                           SVE=Anger vem som publicerade till„gget.];
                ApplicationArea=#All;
                SourceExpr=PublishedBy;
                ShowCaption=No }

    { 4   ;2   ;Field     ;
                CaptionML=[ENU=Type of extension.;
                           NOR=Utvidelsestype.;
                           SVE=Typ av till„gg.];
                ToolTipML=[ENU=Specifies extension type.;
                           NOR=Angir utvidelsestypen.;
                           SVE=Anger typen av till„gg.];
                ApplicationArea=#All;
                SourceExpr=TypeOfExtension;
                ShowCaption=No }

  }
  CODE
  {
    VAR
      Version@1002 : Text;
      PublishedBy@1003 : Text;
      IsExtensionListVisible@1000 : Boolean;
      TypeOfExtension@1004 : Text;
      CurrentPageId@1006 : Integer;
      CurrentTableId@1007 : Integer;
      FilterConditions@1008 : Text;
      NewPageLbl@1009 : TextConst 'ENU=Adds page;NOR=Legger til side;SVE=L„gger till sidan';
      NewTableLbl@1010 : TextConst 'ENU=Adds table;NOR=Legger til tabell;SVE=L„gger till tabell';
      ExtPageLbl@1011 : TextConst 'ENU=Extends page;NOR=Utvider side;SVE=Ut”kar sida';
      ExtTableLbl@1012 : TextConst 'ENU=Extends table;NOR=Utvider tabell;SVE=Ut”kar tabell';

    PROCEDURE FilterForExtAffectingPage@1(PageId@1000 : Integer;TableId@1003 : Integer);
    VAR
      NavAppObjectMetadata@1001 : Record 2000000150;
      TempGuid@1004 : GUID;
    BEGIN
      CurrentPageId := PageId;
      CurrentTableId := TableId;
      FilterConditions := '';

      IF NavAppObjectMetadata.READPERMISSION THEN BEGIN
        // check if this page was added by extension
        NavAppObjectMetadata.RESET;
        NavAppObjectMetadata.SETFILTER("Object Type",'%1',NavAppObjectMetadata."Object Type"::Page);
        NavAppObjectMetadata.SETFILTER("Object ID",'%1',PageId);
        IF NavAppObjectMetadata.FIND('-') THEN
          REPEAT
            FilterConditions := FilterConditions + STRSUBSTNO('%1|',NavAppObjectMetadata."App Package ID");
          UNTIL NavAppObjectMetadata.NEXT = 0;

        // check if page was extended
        NavAppObjectMetadata.RESET;
        NavAppObjectMetadata.SETFILTER("Object Type",'%1',NavAppObjectMetadata."Object Type"::PageExtension);
        NavAppObjectMetadata.SETFILTER("Object Subtype",'%1',STRSUBSTNO('%1',PageId));
        IF NavAppObjectMetadata.FIND('-') THEN
          REPEAT
            FilterConditions := FilterConditions + STRSUBSTNO('%1|',NavAppObjectMetadata."App Package ID");
          UNTIL NavAppObjectMetadata.NEXT = 0;

        // check if source table was added by extension
        NavAppObjectMetadata.RESET;
        NavAppObjectMetadata.SETFILTER("Object Type",'%1',NavAppObjectMetadata."Object Type"::Table);
        NavAppObjectMetadata.SETFILTER("Object ID",'%1',TableId);
        IF NavAppObjectMetadata.FIND('-') THEN
          REPEAT
            FilterConditions := FilterConditions + STRSUBSTNO('%1|',NavAppObjectMetadata."App Package ID");
          UNTIL NavAppObjectMetadata.NEXT = 0;

        // check if source table was extended by extension
        NavAppObjectMetadata.RESET;
        NavAppObjectMetadata.SETFILTER("Object Type",'%1',NavAppObjectMetadata."Object Type"::TableExtension);
        NavAppObjectMetadata.SETFILTER("Object Subtype",'%1',STRSUBSTNO('%1',TableId));
        IF NavAppObjectMetadata.FIND('-') THEN
          REPEAT
            FilterConditions := FilterConditions + STRSUBSTNO('%1|',NavAppObjectMetadata."App Package ID");
          UNTIL NavAppObjectMetadata.NEXT = 0;
      END;

      RESET;
      IF FilterConditions <> '' THEN BEGIN
        FilterConditions := DELCHR(FilterConditions,'>','|');
        SETFILTER("Package ID",FilterConditions);
      END ELSE BEGIN
        TempGuid := CREATEGUID;
        CLEAR(TempGuid);
        SETFILTER("Package ID",'%1',TempGuid);
      END;
      CurrPage.UPDATE(FALSE);
    END;

    PROCEDURE SetExtensionListVisbility@2(NewVisibilityValue@1000 : Boolean);
    BEGIN
      IsExtensionListVisible := NewVisibilityValue;
    END;

    BEGIN
    END.
  }
}

