OBJECT Table 5541 Timeline Event Change
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Timeline Event Change;
               NOR=Endre tidslinjehendelse;
               SVE=Tidslinje f”r h„ndelse„ndring];
  }
  FIELDS
  {
    { 1   ;   ;Reference No.       ;Text200       ;CaptionML=[ENU=Reference No.;
                                                              NOR=Referansenr.;
                                                              SVE=Referensnr];
                                                   Editable=No }
    { 2   ;   ;Changes             ;Integer       ;CaptionML=[ENU=Changes;
                                                              NOR=Endringer;
                                                              SVE=Žndringar];
                                                   Editable=No }
    { 4   ;   ;Original Due Date   ;Date          ;CaptionML=[ENU=Original Due Date;
                                                              NOR=Opprinnelig forfallsdato;
                                                              SVE=Ursprungligt f”rfallodatum];
                                                   Editable=No }
    { 5   ;   ;Due Date            ;Date          ;CaptionML=[ENU=Due Date;
                                                              NOR=Forfallsdato;
                                                              SVE=F”rfallodatum] }
    { 6   ;   ;ChangeRefNo         ;Text250       ;CaptionML=[ENU=ChangeRefNo;
                                                              NOR=ChangeRefNo;
                                                              SVE=ChangeRefNo];
                                                   Editable=No }
    { 20  ;   ;Original Quantity   ;Decimal       ;CaptionML=[ENU=Original Quantity;
                                                              NOR=Opprinnelig antall;
                                                              SVE=Ursprungligt antal];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   Editable=No }
    { 21  ;   ;Quantity            ;Decimal       ;CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 1000;   ;ID                  ;Integer       ;CaptionML=[ENU=ID;
                                                              NOR=ID;
                                                              SVE=ID];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
    {    ;Reference No.                            }
    {    ;Due Date                                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE TransferFromTransactionChangeTable@18(VAR TimelineEventChange@1004 : Record 5541;VAR changeTable@1003 : DotNet "'Microsoft.Dynamics.Framework.UI.WinForms.DataVisualization.Timeline, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Framework.UI.WinForms.DataVisualization.TimelineVisualization.DataModel+TransactionChangesDataTable");
    VAR
      changeRow@1002 : DotNet "'Microsoft.Dynamics.Framework.UI.WinForms.DataVisualization.Timeline, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Framework.UI.WinForms.DataVisualization.TimelineVisualization.DataModel+TransactionChangesRow";
      rowEnum@1000 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.Generic.IEnumerator`1";
      Id@1001 : Integer;
    BEGIN
      TimelineEventChange.RESET;
      TimelineEventChange.DELETEALL;

      rowEnum := changeTable.GetEnumerator;
      Id := 1;

      WHILE rowEnum.MoveNext DO BEGIN
        changeRow := rowEnum.Current;
        InsertTransactionRow(TimelineEventChange,changeRow,Id);
        Id += 1;
      END;

      TimelineEventChange.SETCURRENTKEY("Due Date"); // Restore key to Due Date
    END;

    LOCAL PROCEDURE InsertTransactionRow@1(VAR TimelineEventChange@1001 : Record 5541;changeRow@1002 : DotNet "'Microsoft.Dynamics.Framework.UI.WinForms.DataVisualization.Timeline, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Framework.UI.WinForms.DataVisualization.TimelineVisualization.DataModel+TransactionChangesRow";Id@1000 : Integer);
    BEGIN
      TimelineEventChange.INIT;
      TimelineEventChange.ID := Id;
      TimelineEventChange."Reference No." := changeRow.RefNo;

      IF NOT changeRow.IsNewDateNull THEN
        TimelineEventChange."Due Date" := DT2DATE(changeRow.NewDate);

      IF NOT changeRow.IsOriginalDateNull THEN
        TimelineEventChange."Original Due Date" := DT2DATE(changeRow.OriginalDate);

      IF NOT changeRow.IsNewQuantityNull THEN
        TimelineEventChange.Quantity := changeRow.NewQuantity;

      IF NOT changeRow.IsOriginalQuantityNull THEN
        TimelineEventChange."Original Quantity" := changeRow.OriginalQuantity;

      TimelineEventChange.Changes := changeRow.Changes;
      TimelineEventChange.ChangeRefNo := changeRow.ChangeRefNo;
      TimelineEventChange.INSERT;
    END;

    [External]
    PROCEDURE ActionMessage@7() : Integer;
    VAR
      ActionMsg@1000 : ' ,New,Change Qty.,Reschedule,Resched. & Chg. Qty.,Cancel';
    BEGIN
      IF ("Original Due Date" = 0D) AND ("Original Quantity" = 0) THEN
        EXIT(ActionMsg::New);

      IF Quantity = 0 THEN
        EXIT(ActionMsg::Cancel);

      IF ("Due Date" <> "Original Due Date") AND (Quantity <> "Original Quantity") THEN
        EXIT(ActionMsg::"Resched. & Chg. Qty.");

      IF "Due Date" <> "Original Due Date" THEN
        EXIT(ActionMsg::Reschedule);

      IF Quantity <> "Original Quantity" THEN
        EXIT(ActionMsg::"Change Qty.");

      EXIT(0);
    END;

    [External]
    PROCEDURE NewSupply@1203() : Boolean;
    BEGIN
      EXIT((Changes = 1) AND (ChangeRefNo = ''));
    END;

    BEGIN
    END.
  }
}

