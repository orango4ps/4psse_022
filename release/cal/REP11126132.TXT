OBJECT Report 11126132 GS1 Import Invoices
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=GS1-Rechnung IDen importieren;
               ENU=GS1 Import Invoice IDs;
               NLD=GS1 factuur IDs importeren;
               NOR=Faktura-IDer for GS1-import;
               SVE=Faktura-ID:n f”r GS1-import];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  SalesInConstructSetup.GET;
                END;

    UseRequestPage=No;
  }
  DATASET
  {
    { 2049;    ;DataItem;                    ;
               DataItemTable=Table11126131;
               DataItemTableView=SORTING(Sequence ID)
                                 WHERE(Status=CONST(ID imported));
               PrintOnlyIfDetail=Yes;
               OnPreDataItem=BEGIN
                               SalesInConstructSetup.TESTFIELD(Username);
                               SalesInConstructSetup.TESTFIELD(Password);
                               SalesInConstructSetup.TESTFIELD("Folder Purchase Invoices");
                               SalesInConstructSetup.TESTFIELD("Folder Processed Purch.Inv.");

                               SETFILTER(Status, '<=%1', Status::"ID imported");

                               Window.OPEN(Text001 + FIELDCAPTION("Sequence ID") + '#1##################');
                               GetAndCheckWebServiceURL;
                               SetGlobalVars;
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(1, "Sequence ID");
                                  CreateBasicRequest;

                                  WebServiceMgt.AppendParameter(RequestRootXpath, Namespaces[1,2], Namespaces[1,1] + ':documentPresentation',
                                    'GS1');
                                  WebServiceMgt.AppendParameter(RequestRootXpath, Namespaces[1,2], Namespaces[1,1] + ':sequenceId',
                                    "Sequence ID");

                                  SendRequest;
                                  ProcessResponse;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      SalesInConstructSetup@1100528316 : Record 11072650;
      CompanyInformation@1100528306 : Record 79;
      WebServiceURL@1100528301 : Record 11020659;
      WebServiceMgt@1100528302 : Codeunit 11012266;
      Namespaces@1100528304 : ARRAY [10,2] OF Text[250];
      RequestRootTag@1100528309 : Text[250];
      RequestRootXpath@1100528303 : Text[250];
      ResponseRootTag@1100528314 : Text[250];
      ResponseRootXpath@1100528305 : Text[250];
      Text000@1100528315 : TextConst 'DEU=%1 %2 importiert.;ENU=%1 %2s imported.;NLD=%1 %2en ge‹mporteerd.;NOR=%1 %2 ble importert.;SVE=%1 %2 har importerats.';
      Text001@1100528307 : TextConst 'DEU=Web Service ausfhren fr;ENU="Execute Web Service for ";NLD="Webservice uitvoeren voor ";NOR=Kj›r Webtjeneste for;SVE="K”r webbtj„nst f”r "';
      Window@1100528308 : Dialog;

    LOCAL PROCEDURE GetAndCheckWebServiceURL@1210190008();
    VAR
      PurchasesPayablesSetup@1100528300 : Record 312;
    BEGIN
      PurchasesPayablesSetup.GET;
      PurchasesPayablesSetup.TESTFIELD("Web Service GS1");

      IF (NOT WebServiceURL.GET(WebServiceURL.Type::GS1, PurchasesPayablesSetup."Web Service GS1", '')) THEN
        WebServiceURL.INIT;

      WebServiceURL.TESTFIELD(URL);
      WebServiceURL.TESTFIELD("Target Namespace");
    END;

    LOCAL PROCEDURE SetGlobalVars@1100528300();
    BEGIN
      CompanyInformation.GET;
      CompanyInformation.TESTFIELD(GLN);
      WHILE (STRLEN(CompanyInformation.GLN) < 13) DO
        CompanyInformation.GLN := '0' + CompanyInformation.GLN;

      Namespaces[1,1] := 'sch';
      Namespaces[1,2] := WebServiceURL."Target Namespace";
      Namespaces[2,1] := 'pay';
      Namespaces[2,2] := 'urn:ean.ucc:pay:2';

      RequestRootTag := Namespaces[1,1] + ':documentsRequest';
      RequestRootXpath := './/' + RequestRootTag;
      ResponseRootTag := Namespaces[1,1] + ':documentsResponse';
      ResponseRootXpath := './/' + ResponseRootTag;
    END;

    LOCAL PROCEDURE CreateBasicRequest@1210190006();
    BEGIN
      WebServiceMgt.PrepareSOAPRequest(Namespaces[1,2], RequestRootTag);
      WebServiceMgt.AddRequestSelectionNamespaces(Namespaces);
    END;

    LOCAL PROCEDURE AddRequestParameter@1210190004();
    BEGIN
      WebServiceMgt.AppendParameter(RequestRootXpath, Namespaces[1,2], Namespaces[1,1] + ':sequenceId',
        "GS1 Invoice ID"."Sequence ID");
    END;

    LOCAL PROCEDURE SendRequest@1100528301();
    BEGIN
      WebServiceMgt.SendSOAPRequest2('sendDocumentsRequest', WebServiceURL.URL,
        SalesInConstructSetup.Username, SalesInConstructSetup.Password, TRUE);
      WebServiceMgt.AddResponseSelectionNamespaces(Namespaces);
    END;

    LOCAL PROCEDURE ProcessResponse@1100528305();
    VAR
      XMLExchangeMgt@1100528304 : Codeunit 11072076;
      Counter@1100528300 : Integer;
      ProcessedCounter@1100528305 : Integer;
      FileName@1100528301 : Text[250];
    BEGIN
      IF (NOT WebServiceMgt.ResponseNodeIsPresent('.//' + Namespaces[1,1] + ':documentFiles[1]/' +
        Namespaces[1,1] + ':xmlDocument'))
      THEN
        ERROR(Text000, ProcessedCounter, "GS1 Invoice ID".FIELDCAPTION("Document ID"));

      REPEAT
        Counter += 1;
        WebServiceMgt.DecodeBase64ResponseNode('.//' + Namespaces[1,1] + ':documentFiles[' +
          //FORMAT(Counter) + ']/' + Namespaces[1,1] + ':xmlDocument', Namespaces[1,2], 1);
          FORMAT(Counter) + ']/' + Namespaces[1,1] + ':xmlDocument', '', 0);

        FileName := 'GS1-' + "GS1 Invoice ID"."Sequence ID" + '-' + "GS1 Invoice ID"."Document ID" + '.xml';
        WebServiceMgt.WriteResponseToFile2(SalesInConstructSetup."Folder Processed Purch.Inv." + FileName,
          './/' + Namespaces[1,1] + ':documentFiles[' + FORMAT(Counter) + ']/' + Namespaces[1,1] +
          ':xmlDocument/' +  Namespaces[2,1] + ':invoice');

        IF (XMLExchangeMgt.ProcessXMLFileInvoice2(SalesInConstructSetup."Folder Processed Purch.Inv." +
            FileName) <> '')
        THEN BEGIN
          "GS1 Invoice ID".Status := "GS1 Invoice ID".Status::"GS1 Invoice imported";
          "GS1 Invoice ID".MODIFY(TRUE);

          ProcessedCounter += 1;
        END;

      UNTIL (NOT WebServiceMgt.ResponseNodeIsPresent('.//' + Namespaces[1,1] + ':documentFiles[' +
            FORMAT(Counter+1) + ']/' + Namespaces[1,1] + ':xmlDocument'));

      MESSAGE(Text000, ProcessedCounter, "GS1 Invoice ID".FIELDCAPTION("Document ID"));
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

