OBJECT Page 11126453 SC Budget Progress Bar Chart
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Servicevertragsbudget Fortschritt Balkendiagramm;
               ENU=Service Contract Budget Progress Bar Chart;
               NLD=Staafdiagram voortgang begroting servicecontract];
    SaveValues=Yes;
    UsageCategory=Tasks;
    OnInit=BEGIN
             PeriodType := PeriodType::Month;
             NoOfColumns := 24;

             DetermineMeasurementIndexes;
           END;

    OnOpenPage=BEGIN
                 Date."Period Start" := WORKDATE;
                 PeriodFormManagement.FindDate('=<', Date, PeriodType);
                 BaseDate := Date."Period Start";

                 IF InitContractFilter <> '' THEN
                   ContractFilter := InitContractFilter;
               END;

    ActionList=ACTIONS
    {
      { 1100528611;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100528610;1 ;Action    ;
                      CaptionML=[DEU=ZurÅck;
                                 ENU=Previous;
                                 NLD=Vorige;
                                 NOR=Forrige;
                                 SVE=FîregÜende];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=PreviousRecord;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 PeriodFormManagement.FindDate('<', Date, PeriodType);
                                 BaseDate := Date."Period Start";
                                 UpdateChart;
                               END;
                                }
      { 1100528609;1 ;Action    ;
                      CaptionML=[DEU=Weiter;
                                 ENU=Next;
                                 NLD=Volgende;
                                 NOR=Neste;
                                 SVE=NÑsta];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=NextRecord;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 PeriodFormManagement.FindDate('>', Date, PeriodType);
                                 BaseDate := Date."Period Start";
                                 UpdateChart;
                               END;
                                }
      { 1100528613;1 ;Action    ;
                      CaptionML=[DEU=Erstellen ServiceÅberwachung;
                                 ENU=Create Service Control;
                                 NLD=Servicebewaking aanmaken;
                                 NOR=Opprett servicekontroll;
                                 SVE=Skapa servicekontroll];
                      Promoted=Yes;
                      Image=AddAction;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 REPORT.RUNMODAL(REPORT::"Create Service Control");
                                 BufferedFromDate := 0D;
                                 UpdateChart;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100528600;;Container;
                ContainerType=ContentArea }

    { 1100528601;1;Group  ;
                CaptionML=[DEU=Allgemein;
                           ENU=General;
                           NLD=Algemeen;
                           NOR=Generell;
                           SVE=AllmÑnt];
                GroupType=Group }

    { 1100528617;2;Field  ;
                CaptionML=[DEU=VerkÑuferfilter;
                           ENU=Contract Manager Filter;
                           NLD=Contractbeheerderfilter;
                           NOR=Kontraktlederfilter;
                           SVE=Kontrakthanterarfilter:];
                SourceExpr=ContractManagerFilter;
                TableRelation=Salesperson/Purchaser;
                OnValidate=BEGIN
                             UpdateChart;
                           END;
                            }

    { 1100528607;2;Field  ;
                CaptionML=[DEU=Vertragsfilter;
                           ENU=Contract Filter;
                           NLD=Contractfilter;
                           NOR=Kontraktsfilter;
                           SVE=Kontraktsfilter];
                SourceExpr=ContractFilter;
                TableRelation=Service-Contract;
                OnValidate=BEGIN
                             UpdateChart;
                           END;
                            }

    { 1100528608;2;Field  ;
                CaptionML=[DEU=Vertragsgruppenfilter;
                           ENU=Contract Group Filter;
                           NLD=Contractgroepfilter;
                           NOR=Kontraktgruppefilter;
                           SVE=Kontraktgruppfilter];
                SourceExpr=ContractGroupFilter;
                TableRelation="Service Contract Group";
                OnValidate=BEGIN
                             UpdateChart;
                           END;
                            }

    { 1100528616;2;Field  ;
                CaptionML=[NOR=Avdelingsfilter;
                           SVE=Avdelningsfilter];
                SourceExpr=DepartmentFilter;
                CaptionClass='1,3,1';
                TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                OnValidate=BEGIN
                             UpdateChart;
                           END;
                            }

    { 1100528615;2;Field  ;
                CaptionML=[DEU=Gebietsfilter;
                           ENU=Territory Filter;
                           NLD=Regiofilter;
                           NOR=Distriktsfilter;
                           SVE=Distriktsfilter];
                SourceExpr=TerritoryFilter;
                TableRelation=Territory.Code;
                OnValidate=BEGIN
                             UpdateChart;
                           END;
                            }

    { 1100528614;2;Field  ;
                CaptionML=[DEU=Disziplinfilter;
                           ENU=Discipline Filter;
                           NLD=Disciplinefilter;
                           NOR=Disiplinfilter;
                           SVE=Disciplinfilter];
                SourceExpr=DisciplineFilter;
                TableRelation=Discipline;
                OnValidate=BEGIN
                             UpdateChart;
                           END;
                            }

    { 1100528612;2;Field  ;
                CaptionML=[DEU=Fachgebietfilter;
                           ENU=Trade Speciality Filter;
                           NLD=Vakgebiedfilter;
                           NOR=SNI-nummerfilter;
                           SVE=SNI-nummerfilter];
                SourceExpr=TradeSpecFilter;
                OnValidate=BEGIN
                             UpdateChart;
                           END;

                OnLookup=VAR
                           TradeSpeciality@1100528600 : Record 11020285;
                         BEGIN
                           TradeSpeciality.SETFILTER(Discipline, DisciplineFilter);
                           IF PAGE.RUNMODAL(0, TradeSpeciality) = ACTION::LookupOK THEN BEGIN
                             Text := TradeSpeciality.Code;
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 1100529600;2;Field  ;
                Name=<ServiceLocationFilter>;
                CaptionML=[DEU=Servicestandort Filter;
                           ENU=Service Location Filter;
                           NLD=Servicelocatie Filter];
                SourceExpr=ServiceLocationFilter;
                TableRelation="Service Location" }

    { 1100528602;1;Group  ;
                CaptionML=[DEU=Diagramm;
                           ENU=Diagram;
                           NLD=Diagram];
                GroupType=Group }

    { 1100528603;2;Field  ;
                Name=Chart;
                ControlAddIn=[Microsoft.Dynamics.Nav.Client.BusinessChart;PublicKeyToken=31bf3856ad364e35] }

    { 1100528606;1;Group  ;
                CaptionML=[DEU=Optionen;
                           ENU=Options;
                           NLD=Opties;
                           NOR=Alternativer;
                           SVE=Alternativ];
                GroupType=Group }

    { 1100528605;2;Field  ;
                CaptionML=[DEU=Anzeigen nach;
                           ENU=View by;
                           NLD=Weergeven per;
                           NOR=Vis per;
                           SVE=Visa per];
                OptionCaptionML=[DEU=Tag,Woche,Monat,Quartal,Jahr,Periode;
                                 ENU=Day,Week,Month,Quarter,Year,Period;
                                 NLD=Dag,Week,Maand,Kwartaal,Jaar,Periode;
                                 NOR=Dag,Uke,MÜned,Kvartal,èr,Periode;
                                 SVE=Dag,Vecka,MÜnad,Kvartal,èr,Period];
                SourceExpr=PeriodType;
                OnValidate=BEGIN
                             PeriodFormManagement.FindDate('=<>', Date, PeriodType);
                             UpdateChart;
                           END;
                            }

    { 1100528604;2;Field  ;
                CaptionML=[DEU=Anz. der Perioden;
                           ENU=No. Of Periods;
                           NLD=Aantal perioden];
                SourceExpr=NoOfColumns;
                OnValidate=BEGIN
                             IF NoOfColumns <= 0 THEN
                               ERROR(Text001);
                             UpdateChart;
                           END;
                            }

  }
  CODE
  {
    VAR
      BusinessChartBuffer@1100528600 : Record 485;
      Date@1100528601 : Record 2000000007;
      TmpServiceContractCtrlBudget@1100528612 : TEMPORARY Record 11071759;
      TmpServiceOrderControlBudget@1100528613 : TEMPORARY Record 11071757;
      PeriodFormManagement@1100528602 : Codeunit 359;
      PeriodType@1100528605 : ',Week,Month,Quarter,Year';
      BaseDate@1100528604 : Date;
      NoOfColumns@1100528603 : Integer;
      BufferedFromDate@1100528606 : Date;
      Text000@1100528609 : TextConst 'DEU=Tag,Woche,Monat,Quartal,Jahr,Periode;ENU=Day,Week,Month,Quarter,Year,Period;NLD=Dag,Week,Maand,Kwartaal,Jaar,Periode;NOR=Dag,Uke,MÜned,Kvartal,èr,Periode;SVE=Dag,Vecka,MÜnad,Kvartal,èr,Period';
      Text001@1100528608 : TextConst 'DEU=Die Anz. der Perioden muss mindestens 1 sein;ENU=No. of Periods must be at least 1;NLD=Aantal perioden dient minimaal 1 te zijn';
      Text002@1100528607 : TextConst 'DEU=Fortsetzung: @1@@@@@@@@@@@;ENU=Progress: @1@@@@@@@@@@@;NLD=Voortgang: @1@@@@@@@@@@@;NOR=Fremdrift: @1@@@@@@@@@@@;SVE=Fîrlopp: @1@@@@@@@@@@@';
      ContractManagerFilter@1100528627 : Code[250];
      ContractFilter@1100528610 : Code[250];
      InitContractFilter@1100528616 : Code[250];
      ContractGroupFilter@1100528611 : Code[250];
      DepartmentFilter@1100528621 : Code[250];
      TerritoryFilter@1100528622 : Code[250];
      DisciplineFilter@1100528623 : Code[250];
      TradeSpecFilter@1100528624 : Code[250];
      ServiceLocationFilter@1100529600 : Code[250];
      UnplannedMaintenanceIndex@1100528618 : Integer;
      InProposalMaintenanceIndex@1100528626 : Integer;
      OrdersCreatedMaintenanceIndex@1100528629 : Integer;
      OrdersPlannedMaintenanceIndex@1100528615 : Integer;
      FinishedMaintenanceIndex@1100528617 : Integer;
      TextUnplannedMaintenance@1100528619 : TextConst 'DEU=Wartung (Ungeplant);ENU=Maintenance (Unplanned);NLD=Onderhoud (niet gepland)';
      TextInProposalMaintenance@1100528625 : TextConst 'DEU=Wartung (Angebot);ENU=Maintenance (Proposal);NLD=Onderhoud (voorstel)';
      TextOrdersCreatedMaintenance@1100528614 : TextConst 'DEU=Wartung (AuftrÑge erstellt);ENU=Maintenance (Orders Created);NLD=Onderhoud (orders aangemaakt)';
      TextOrdersPlannedMaintenance@1100528628 : TextConst 'DEU=Wartung (AuftrÑge geplant);ENU=Maintenance (Orders Planned);NLD=Onderhoud (orders gepland)';
      TextFinishedMaintenance@1100528620 : TextConst 'DEU=Wartung (AuftrÑge fertiggestellt);ENU=Maintenance (Orders Finished);NLD=Onderhoud (orders gereed)';

    PROCEDURE SetUnplannedMaintFilter@1100528609(IPeriodStart@1100528600 : Date);
    VAR
      Date@1100528601 : Record 2000000007;
    BEGIN
      Date."Period Start" := IPeriodStart;
      PeriodFormManagement.FindDate('=', Date, PeriodType);
      TmpServiceContractCtrlBudget.RESET;
      TmpServiceContractCtrlBudget.SETRANGE("Reference Date", Date."Period Start", Date."Period End");
      TmpServiceContractCtrlBudget.SETRANGE(Finished, FALSE);
      TmpServiceContractCtrlBudget.SETFILTER("Contract Manager", ContractManagerFilter);
      TmpServiceContractCtrlBudget.SETFILTER("Service Contract No.", ContractFilter);
      TmpServiceContractCtrlBudget.SETFILTER("Contract Group", ContractGroupFilter);
      TmpServiceContractCtrlBudget.SETRANGE("Service Order No.", '');
      TmpServiceContractCtrlBudget.SETRANGE("Maintenance Proposal No.", '');
      TmpServiceContractCtrlBudget.SETFILTER("Global Dimension 1 Code", DepartmentFilter);
      TmpServiceContractCtrlBudget.SETFILTER("Trade Speciality", TradeSpecFilter);
      TmpServiceContractCtrlBudget.SETFILTER("Territory Code", TerritoryFilter);
      TmpServiceContractCtrlBudget.SETFILTER(Discipline, DisciplineFilter);
      TmpServiceContractCtrlBudget.SETFILTER("Service Location No.", ServiceLocationFilter);
    END;

    PROCEDURE SetInProposalMaintFilter@1100528615(IPeriodStart@1100528600 : Date);
    VAR
      Date@1100528601 : Record 2000000007;
    BEGIN
      Date."Period Start" := IPeriodStart;
      PeriodFormManagement.FindDate('=', Date, PeriodType);
      TmpServiceContractCtrlBudget.RESET;
      TmpServiceContractCtrlBudget.SETRANGE("Reference Date", Date."Period Start", Date."Period End");
      TmpServiceContractCtrlBudget.SETRANGE(Finished, FALSE);
      TmpServiceContractCtrlBudget.SETFILTER("Contract Manager", ContractManagerFilter);
      TmpServiceContractCtrlBudget.SETFILTER("Service Contract No.", ContractFilter);
      TmpServiceContractCtrlBudget.SETFILTER("Contract Group", ContractGroupFilter);
      TmpServiceContractCtrlBudget.SETRANGE("Service Order No.", '');
      TmpServiceContractCtrlBudget.SETFILTER("Maintenance Proposal No.", '<>%1', '');
      TmpServiceContractCtrlBudget.SETFILTER("Global Dimension 1 Code", DepartmentFilter);
      TmpServiceContractCtrlBudget.SETFILTER("Trade Speciality", TradeSpecFilter);
      TmpServiceContractCtrlBudget.SETFILTER("Territory Code", TerritoryFilter);
      TmpServiceContractCtrlBudget.SETFILTER(Discipline, DisciplineFilter);
      TmpServiceContractCtrlBudget.SETFILTER("Service Location No.", ServiceLocationFilter);
    END;

    PROCEDURE SetOrdersCreatedMaintFilter@1100528619(IPeriodStart@1100528600 : Date);
    VAR
      Date@1100528601 : Record 2000000007;
    BEGIN
      Date."Period Start" := IPeriodStart;
      PeriodFormManagement.FindDate('=', Date, PeriodType);
      TmpServiceOrderControlBudget.RESET;
      TmpServiceOrderControlBudget.SETRANGE("Service Control Period Date",Date."Period Start", Date."Period End");
      TmpServiceOrderControlBudget.SETRANGE(Finished, FALSE);
      TmpServiceOrderControlBudget.SETFILTER("Contract Manager", ContractManagerFilter);
      TmpServiceOrderControlBudget.SETFILTER("No.", ContractFilter);
      TmpServiceOrderControlBudget.SETFILTER("Contract Group", ContractGroupFilter);
      TmpServiceOrderControlBudget.SETFILTER("Global Dimension 1 Code", DepartmentFilter);
      TmpServiceOrderControlBudget.SETFILTER(Discipline, DisciplineFilter);
      TmpServiceOrderControlBudget.SETFILTER("Trade Speciality", TradeSpecFilter);
      TmpServiceOrderControlBudget.SETFILTER("Territory Code", TerritoryFilter);
      TmpServiceOrderControlBudget.SETRANGE("Resource No.", '');
      TmpServiceOrderControlBudget.SETFILTER("Service Location No.", ServiceLocationFilter);
    END;

    PROCEDURE SetOrdersPlannedMaintFilter@1100528613(IPeriodStart@1100528600 : Date);
    VAR
      Date@1100528601 : Record 2000000007;
    BEGIN
      Date."Period Start" := IPeriodStart;
      PeriodFormManagement.FindDate('=', Date, PeriodType);
      TmpServiceOrderControlBudget.RESET;
      TmpServiceOrderControlBudget.SETRANGE("Service Control Period Date",Date."Period Start", Date."Period End");
      TmpServiceOrderControlBudget.SETRANGE(Finished, FALSE);
      TmpServiceOrderControlBudget.SETFILTER("Contract Manager", ContractManagerFilter);
      TmpServiceOrderControlBudget.SETFILTER("No.", ContractFilter);
      TmpServiceOrderControlBudget.SETFILTER("Contract Group", ContractGroupFilter);
      TmpServiceOrderControlBudget.SETFILTER("Global Dimension 1 Code", DepartmentFilter);
      TmpServiceOrderControlBudget.SETFILTER(Discipline, DisciplineFilter);
      TmpServiceOrderControlBudget.SETFILTER("Trade Speciality", TradeSpecFilter);
      TmpServiceOrderControlBudget.SETFILTER("Territory Code", TerritoryFilter);
      TmpServiceOrderControlBudget.SETFILTER("Resource No.", '<>%1', '');
      TmpServiceOrderControlBudget.SETFILTER("Service Location No.", ServiceLocationFilter);
    END;

    PROCEDURE SetFinishedMaintFilter@1100528614(IPeriodStart@1100528600 : Date);
    VAR
      Date@1100528601 : Record 2000000007;
    BEGIN
      Date."Period Start" := IPeriodStart;
      PeriodFormManagement.FindDate('=', Date, PeriodType);
      TmpServiceOrderControlBudget.RESET;
      TmpServiceOrderControlBudget.SETRANGE("Service Control Period Date",Date."Period Start", Date."Period End");
      TmpServiceOrderControlBudget.SETRANGE(Finished, TRUE);
      TmpServiceOrderControlBudget.SETFILTER("Contract Manager", ContractManagerFilter);
      TmpServiceOrderControlBudget.SETFILTER("No.", ContractFilter);
      TmpServiceOrderControlBudget.SETFILTER("Contract Group", ContractGroupFilter);
      TmpServiceOrderControlBudget.SETFILTER("Global Dimension 1 Code", DepartmentFilter);
      TmpServiceOrderControlBudget.SETFILTER(Discipline, DisciplineFilter);
      TmpServiceOrderControlBudget.SETFILTER("Trade Speciality", TradeSpecFilter);
      TmpServiceOrderControlBudget.SETFILTER("Territory Code", TerritoryFilter);
      TmpServiceOrderControlBudget.SETFILTER("Service Location No.", ServiceLocationFilter);
    END;

    PROCEDURE GetUnplannedMaintenanceHours@1100528603(IPeriodStart@1100528600 : Date) : Decimal;
    VAR
      Date@1100528603 : Record 2000000007;
    BEGIN
      Date."Period Start" := IPeriodStart;
      PeriodFormManagement.FindDate('=', Date, PeriodType);
      SetUnplannedMaintFilter(IPeriodStart);
      TmpServiceContractCtrlBudget.CALCSUMS(Hours);
      EXIT(TmpServiceContractCtrlBudget.Hours);
    END;

    PROCEDURE GetInProposalMaintenanceHours@1100528617(IPeriodStart@1100528600 : Date) : Decimal;
    VAR
      Date@1100528603 : Record 2000000007;
    BEGIN
      Date."Period Start" := IPeriodStart;
      PeriodFormManagement.FindDate('=', Date, PeriodType);
      SetInProposalMaintFilter(IPeriodStart);
      TmpServiceContractCtrlBudget.CALCSUMS(Hours);
      EXIT(TmpServiceContractCtrlBudget.Hours);
    END;

    PROCEDURE GetOrdersCreatedMaintenanceHours@1100528616(IPeriodStart@1100528600 : Date) : Decimal;
    VAR
      Date@1100528603 : Record 2000000007;
    BEGIN
      Date."Period Start" := IPeriodStart;
      PeriodFormManagement.FindDate('=', Date, PeriodType);
      SetOrdersCreatedMaintFilter(IPeriodStart);
      TmpServiceOrderControlBudget.CALCSUMS(Hours);
      EXIT(TmpServiceOrderControlBudget.Hours);
    END;

    PROCEDURE GetOrdersPlannedMaintenanceHours@1100528602(IPeriodStart@1100528600 : Date) : Decimal;
    VAR
      Date@1100528603 : Record 2000000007;
    BEGIN
      Date."Period Start" := IPeriodStart;
      PeriodFormManagement.FindDate('=', Date, PeriodType);
      SetOrdersPlannedMaintFilter(IPeriodStart);
      TmpServiceOrderControlBudget.CALCSUMS(Hours);
      EXIT(TmpServiceOrderControlBudget.Hours);
    END;

    PROCEDURE GetFinishedMaintenanceHours@1100528605(IPeriodStart@1100528600 : Date) : Decimal;
    VAR
      Date@1100528603 : Record 2000000007;
    BEGIN
      Date."Period Start" := IPeriodStart;
      PeriodFormManagement.FindDate('=', Date, PeriodType);
      SetFinishedMaintFilter(IPeriodStart);
      TmpServiceOrderControlBudget.CALCSUMS(Hours);
      EXIT(TmpServiceOrderControlBudget.Hours);
    END;

    PROCEDURE ShowUnplannedMaintenanceHours@1100528608(IOffset@1100528600 : Integer);
    VAR
      Date@1100528601 : Record 2000000007;
    BEGIN
      GetPeriod(IOffset, Date);
      SetUnplannedMaintFilter(Date."Period Start");
      PAGE.RUNMODAL(0, TmpServiceContractCtrlBudget);
    END;

    PROCEDURE ShowInProposalMaintenanceHours@1100528618(IOffset@1100528600 : Integer);
    VAR
      Date@1100528601 : Record 2000000007;
    BEGIN
      GetPeriod(IOffset, Date);
      SetInProposalMaintFilter(Date."Period Start");
      PAGE.RUNMODAL(0, TmpServiceContractCtrlBudget);
    END;

    PROCEDURE ShowOrdersCreatedMaintenanceHours@1100528620(IOffset@1100528600 : Integer);
    VAR
      Date@1100528601 : Record 2000000007;
    BEGIN
      GetPeriod(IOffset, Date);
      SetOrdersCreatedMaintFilter(Date."Period Start");
      PAGE.RUNMODAL(0, TmpServiceOrderControlBudget);
    END;

    PROCEDURE ShowOrdersPlannedMaintenanceHours@1100528606(IOffset@1100528600 : Integer);
    VAR
      Date@1100528601 : Record 2000000007;
    BEGIN
      GetPeriod(IOffset, Date);
      SetOrdersPlannedMaintFilter(Date."Period Start");
      PAGE.RUNMODAL(0, TmpServiceOrderControlBudget);
    END;

    PROCEDURE ShowFinishedMaintenanceHours@1100528607(IOffset@1100528600 : Integer;IFinished@1100528602 : Boolean);
    VAR
      Date@1100528601 : Record 2000000007;
    BEGIN
      GetPeriod(IOffset, Date);
      SetFinishedMaintFilter(Date."Period Start");
      PAGE.RUNMODAL(0, TmpServiceOrderControlBudget);
    END;

    PROCEDURE UpdateChart@1100528601();
    VAR
      Date@1100528604 : Record 2000000007;
      Offset@1100528602 : Integer;
      ColumnName@1100528601 : Text[30];
      PeriodDate@1100528600 : Date;
      UnplannedMaintenanceHours@1100528606 : Decimal;
      InProposalMaintenanceHours@1100528608 : Decimal;
      OrdersCreatedMaintenanceHours@1100528609 : Decimal;
      OrdersPlannedMaintenanceHours@1100528603 : Decimal;
      FinishedMaintenanceHours@1100528607 : Decimal;
    BEGIN
      BuildTmpTables;

      WITH BusinessChartBuffer DO BEGIN
        Initialize;
        AddMeasure(TextUnplannedMaintenance, UnplannedMaintenanceIndex, "Data Type"::Decimal, "Chart Type"::StackedColumn);
        AddMeasure(TextInProposalMaintenance, InProposalMaintenanceIndex, "Data Type"::Decimal, "Chart Type"::StackedColumn);
        AddMeasure(TextOrdersCreatedMaintenance, OrdersCreatedMaintenanceIndex, "Data Type"::Decimal, "Chart Type"::StackedColumn);
        AddMeasure(TextOrdersPlannedMaintenance, OrdersPlannedMaintenanceIndex, "Data Type"::Decimal, "Chart Type"::StackedColumn);
        AddMeasure(TextFinishedMaintenance, FinishedMaintenanceIndex, "Data Type"::Decimal, "Chart Type"::StackedColumn);
        SetXAxis(GetPeriodTypeText(PeriodType), "Data Type"::String);

        Date."Period Start" := BaseDate;
        PeriodFormManagement.FindDate('=<>', Date, PeriodType);

        FOR Offset := 0 TO NoOfColumns -1 DO BEGIN
          IF Offset > 0 THEN
            PeriodFormManagement.NextDate(1, Date, PeriodType);
          PeriodDate := Date."Period Start";
          ColumnName := PeriodFormManagement.CreatePeriodFormat(PeriodType, Date."Period Start");

          AddColumn(ColumnName);

          UnplannedMaintenanceHours := GetUnplannedMaintenanceHours(PeriodDate);
          InProposalMaintenanceHours := GetInProposalMaintenanceHours(PeriodDate);
          OrdersCreatedMaintenanceHours := GetOrdersCreatedMaintenanceHours(PeriodDate);
          OrdersPlannedMaintenanceHours := GetOrdersPlannedMaintenanceHours(PeriodDate);
          FinishedMaintenanceHours := GetFinishedMaintenanceHours(PeriodDate);

          BusinessChartBuffer.SetValueByIndex(UnplannedMaintenanceIndex -1, Offset, UnplannedMaintenanceHours);
          BusinessChartBuffer.SetValueByIndex(InProposalMaintenanceIndex -1, Offset, InProposalMaintenanceHours);
          BusinessChartBuffer.SetValueByIndex(OrdersCreatedMaintenanceIndex -1, Offset, OrdersCreatedMaintenanceHours);
          BusinessChartBuffer.SetValueByIndex(OrdersPlannedMaintenanceIndex -1, Offset, OrdersPlannedMaintenanceHours);
          BusinessChartBuffer.SetValueByIndex(FinishedMaintenanceIndex -1, Offset, FinishedMaintenanceHours);
        END;
        Update(CurrPage.Chart);
      END;
    END;

    PROCEDURE GetPeriod@1100528604(IOffset@1100528600 : Integer;VAR ODate@1100528601 : Record 2000000007);
    BEGIN
      ODate."Period Start" := BaseDate;
      PeriodFormManagement.FindDate('=<>', ODate, PeriodType);
      IF IOffset <> 0 THEN
        PeriodFormManagement.NextDate(IOffset, ODate, PeriodType);
    END;

    PROCEDURE GetPeriodTypeText@1100528610(IPeriodType@1100528600 : Option) PeriodTypeText : Text;
    VAR
      OptionFieldTools@1100528601 : Codeunit 11012271;
    BEGIN
      OptionFieldTools.OptionValueToReadableOption(Text000, IPeriodType, PeriodTypeText);
    END;

    PROCEDURE BuildTmpTables@1100528612();
    VAR
      ServiceSetup@1100528600 : Record 11012800;
      ServiceContract@1100528602 : Record 11012812;
      ServiceContractCtrlBudget@1100528603 : Record 11071759;
      ServiceOrderControlBudget@1100528608 : Record 11071757;
      Dialog@1100528604 : Dialog;
      FromDate@1100528607 : Date;
      ToDate@1100528601 : Date;
      RecordCount@1100528605 : Integer;
      RecordCounter@1100528606 : Integer;
    BEGIN
      Dialog.OPEN(Text002);
      FromDate := DMY2DATE(1, 1, DATE2DMY(BaseDate, 3));
      IF (BufferedFromDate <> 0D) AND (FromDate >= BufferedFromDate) THEN
        EXIT;
      BufferedFromDate := FromDate;

      TmpServiceContractCtrlBudget.RESET;
      TmpServiceContractCtrlBudget.DELETEALL;

      TmpServiceOrderControlBudget.RESET;
      TmpServiceOrderControlBudget.DELETEALL;

      ToDate := 99991231D;
      ServiceSetup.GET;

      ServiceContract.SETRANGE("No. of Control Messages", 0);
      IF ServiceContract.FINDSET THEN BEGIN
        RecordCount := ServiceContract.COUNT;
        REPEAT
          IF ServiceSetup."Service Control Method" = ServiceSetup."Service Control Method"::Snapshot THEN
            ServiceContractCtrlBudget.SETRANGE("Reporting Date", ServiceContract."Control Build Date")
          ELSE
            ServiceContractCtrlBudget.SETCURRENTKEY("Service Control Method");
          ServiceContractCtrlBudget.SETRANGE("Service Control Method", ServiceSetup."Service Control Method");
          ServiceContractCtrlBudget.SETRANGE("Service Contract No.", ServiceContract."No.");
          ServiceContractCtrlBudget.SETRANGE("Reference Date", BufferedFromDate, ToDate);
          ServiceContractCtrlBudget.SETRANGE("Budget Type", ServiceContractCtrlBudget."Budget Type"::Budget);
          IF ServiceSetup."Service Control Method" <> ServiceSetup."Service Control Method"::Snapshot THEN
            ServiceContractCtrlBudget.SETCURRENTKEY("Service Control Method");
          ServiceContractCtrlBudget.SETRANGE("Service Control Method", ServiceSetup."Service Control Method");
          IF ServiceContractCtrlBudget.FINDSET THEN
            REPEAT
              TmpServiceContractCtrlBudget := ServiceContractCtrlBudget;
              TmpServiceContractCtrlBudget.INSERT;
            UNTIL ServiceContractCtrlBudget.NEXT = 0;

          IF ServiceSetup."Service Control Method" = ServiceSetup."Service Control Method"::Snapshot THEN
            ServiceOrderControlBudget.SETRANGE("Reporting Date", ServiceContract."Control Build Date")
          ELSE
            ServiceOrderControlBudget.SETCURRENTKEY("Service Control Method");
          ServiceOrderControlBudget.SETRANGE("Service Control Method", ServiceSetup."Service Control Method");
          ServiceOrderControlBudget.SETRANGE(Work, ServiceOrderControlBudget.Work::Contract);
          ServiceOrderControlBudget.SETRANGE("No.", ServiceContract."No.");
          ServiceOrderControlBudget.SETRANGE("Service Control Period Date", BufferedFromDate, ToDate);
          ServiceOrderControlBudget.SETRANGE("Budget Type", ServiceOrderControlBudget."Budget Type"::Budget);
          ServiceOrderControlBudget.SETRANGE("Source Type", ServiceOrderControlBudget."Source Type"::Contract);
          IF ServiceOrderControlBudget.FINDSET THEN
            REPEAT
              TmpServiceOrderControlBudget := ServiceOrderControlBudget;
              TmpServiceOrderControlBudget.INSERT;
            UNTIL ServiceOrderControlBudget.NEXT = 0;

          RecordCounter += 1;
          Dialog.UPDATE(1, ROUND(RecordCounter / RecordCount * 9999, 1));
        UNTIL ServiceContract.NEXT = 0;
      END;
      Dialog.CLOSE;
    END;

    PROCEDURE DetermineMeasurementIndexes@1100528611();
    BEGIN
      UnplannedMaintenanceIndex := 1;
      InProposalMaintenanceIndex := 2;
      OrdersCreatedMaintenanceIndex := 3;
      OrdersPlannedMaintenanceIndex := 4;
      FinishedMaintenanceIndex := 5;
    END;

    PROCEDURE SetContractFilter@1100528600(IContractFilter@1100528600 : Code[250]);
    BEGIN
      InitContractFilter := IContractFilter;
    END;

    LOCAL PROCEDURE DetermineReportingDate@1100528621(IServiceContractNo@1100528600 : Code[20]) : Date;
    VAR
      ServiceSetup@1100528602 : Record 11012800;
      ServiceContract@1100528601 : Record 11012812;
      SCControlbyPeriod@1100528603 : Record 11072311;
    BEGIN
      ServiceSetup.GET;
      ServiceContract.GET(IServiceContractNo);
      CASE ServiceSetup."Service Control Method" OF
        ServiceSetup."Service Control Method"::Snapshot:
          EXIT(ServiceContract."Control Build Date");
        ServiceSetup."Service Control Method"::"Real-Time":
          BEGIN
            SCControlbyPeriod.SETCURRENTKEY("Service Contract No.");
            SCControlbyPeriod.SETRANGE("Service Contract No.", IServiceContractNo);
            IF SCControlbyPeriod.FINDFIRST THEN
              EXIT(DT2DATE(SCControlbyPeriod."Last Updated at"))
            ELSE
              EXIT(TODAY);
          END;
      END;
    END;

    EVENT Chart@-1100528603::DataPointClicked@1(point@1100525000 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartDataPoint");
    VAR
      DrillDownIndex@1100525001 : Integer;
    BEGIN
      BusinessChartBuffer.SetDrillDownIndexes(point);
      DrillDownIndex := BusinessChartBuffer."Drill-Down X Index";
      CASE BusinessChartBuffer."Drill-Down Measure Index" +1 OF
        UnplannedMaintenanceIndex:
          ShowUnplannedMaintenanceHours(DrillDownIndex);
        InProposalMaintenanceIndex:
          ShowInProposalMaintenanceHours(DrillDownIndex);
        OrdersCreatedMaintenanceIndex:
          ShowOrdersCreatedMaintenanceHours(DrillDownIndex);
        OrdersPlannedMaintenanceIndex:
          ShowOrdersPlannedMaintenanceHours(DrillDownIndex);
        FinishedMaintenanceIndex:
          ShowFinishedMaintenanceHours(DrillDownIndex, TRUE);
      END;
    END;

    EVENT Chart@-1100528603::DataPointDoubleClicked@2(point@1100525000 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartDataPoint");
    BEGIN
    END;

    EVENT Chart@-1100528603::AddInReady@3();
    BEGIN
      UpdateChart;
    END;

    EVENT Chart@-1100528603::Refresh@4();
    BEGIN
    END;

    BEGIN
    END.
  }
}

