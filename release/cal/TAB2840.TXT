OBJECT Table 2840 Native - Gen. Settings Buffer
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.01;
  }
  PROPERTIES
  {
    ReplicateData=No;
    CaptionML=[ENU=Native - Gen. Settings Buffer;
               NOR=Opprinnelig - Buffer for generelle innstillinger];
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Code10        ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Primary Key;
                                                              NOR=Prim‘rn›kkel;
                                                              SVE=Prim„rnyckel] }
    { 2   ;   ;Currency Symbol     ;Text10        ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Currency Symbol;
                                                              NOR=Valutasymbol;
                                                              SVE=Valutasymbol] }
    { 3   ;   ;Paypal Email Address;Text250       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Paypal Email Address;
                                                              NOR=E-postadresse for Paypal] }
    { 4   ;   ;Country/Region Code ;Code10        ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Country/Region Code;
                                                              NOR=Lands-/omr†dekode;
                                                              SVE=Lands-/regionkod] }
    { 5   ;   ;Language Locale ID  ;Integer       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Language Locale ID;
                                                              NOR=ID for nasjonal innstilling for spr†k] }
    { 6   ;   ;Language Code       ;Text50        ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Language Code;
                                                              NOR=Spr†kkode;
                                                              SVE=Spr†kkod] }
    { 7   ;   ;Language Display Name;Text80       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Language Display Name;
                                                              NOR=Visningsnavn for spr†k] }
    { 50  ;   ;Default Tax ID      ;GUID          ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Default Tax ID;
                                                              NOR=Standard avgifts-ID];
                                                   Editable=No }
    { 51  ;   ;Defauilt Tax Description;Text100   ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Defauilt Tax Description;
                                                              NOR=Beskrivelse av standardavgift];
                                                   Editable=No }
    { 52  ;   ;Default Payment Terms ID;GUID      ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Default Payment Terms ID;
                                                              NOR=ID for standard betalingsbetingelser];
                                                   Editable=No }
    { 53  ;   ;Def. Pmt. Term Description;Text50  ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Def. Pmt. Term Description;
                                                              NOR=Beskrivelse av standard betalingsbetingelse];
                                                   Editable=No }
    { 54  ;   ;Default Payment Method ID;GUID     ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Default Payment Method ID;
                                                              NOR=ID for standard betalingsm†te];
                                                   Editable=No }
    { 55  ;   ;Def. Pmt. Method Description;Text50;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Def. Pmt. Method Description;
                                                              NOR=Beskrivelse av standard betalingsm†te];
                                                   Editable=No }
    { 56  ;   ;Amount Rounding Precision;Decimal  ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Amount Rounding Precision;
                                                              NOR=Avrundingspresisjon, bel›p;
                                                              SVE=Belopp avrundning];
                                                   Editable=No }
    { 57  ;   ;Unit-Amount Rounding Precision;Decimal;
                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Unit-Amount Rounding Precision;
                                                              NOR=Avrundingspresisjon, pris;
                                                              SVE=A-pris avrundning];
                                                   Editable=No }
    { 58  ;   ;VAT/Tax Rounding Precision;Decimal ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=VAT/Tax Rounding Precision;
                                                              NOR=Avrund.presisjon, mva/avgift];
                                                   Editable=No }
    { 59  ;   ;Quantity Rounding Precision;Decimal;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Quantity Rounding Precision;
                                                              NOR=Antallavrundingspresisjon];
                                                   Editable=No }
    { 60  ;   ;EnableSync          ;Boolean       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=EnableSync;
                                                              NOR=EnableSync] }
    { 61  ;   ;EnableSyncCoupons   ;Boolean       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=EnableSyncCoupons;
                                                              NOR=EnableSyncCoupons] }
  }
  KEYS
  {
    {    ;Primary Key                             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      RecordMustBeTemporaryErr@1000 : TextConst 'ENU=General Settings Buffer must be used as a temporary record.;NOR=Buffer for generelle innstillinger m† brukes som en midlertidig post.';
      CannotEnableCouponsSyncErr@1001 : TextConst 'ENU=Cannot enable coupons synchronization while Microsoft Graph synchronization is turned off.;NOR=Kan ikke aktivere synkronisering av kuponger mens synkronisering av Microsoft Graph er deaktivert.';
      CannotSetWebhookSubscriptionUserErr@1007 : TextConst 'ENU=Cannot set the webhook subscription user.;NOR=Kan ikke angi brukeren av webhook-abonnement.';
      CannotGetCompanyInformationErr@1003 : TextConst 'ENU=Cannot get the company information.;NOR=Kan ikke hente firmainformasjonen.';
      SyncOnlyAllowedInSaasErr@1004 : TextConst 'ENU=Microsoft Graph synchronization is only allowed in SaaS.;NOR=Synkronisering av Microsoft Graph er bare tillatt i SaaS.';
      SyncNotAllowedInDemoCompanyErr@1005 : TextConst 'ENU=Microsoft Graph synchronization is not allowed in a demo company.;NOR=Synkronisering av Microsoft Graph er ikke tillatt i et demonstrasjonsselskap.';
      SyncNotAllowedErr@1006 : TextConst 'ENU=Microsoft Graph synchronization is not allowed.;NOR=Synkronisering av Microsoft Graph er ikke tillatt.';

    [External]
    PROCEDURE LoadRecord@2();
    VAR
      CompanyInformation@1001 : Record 79;
      O365SalesInitialSetup@1003 : Record 2110;
      GeneralLedgerSetup@1004 : Record 98;
      TempNativeAPITaxSetup@1007 : TEMPORARY Record 2850;
      MarketingSetup@1008 : Record 5079;
      PaypalAccountProxy@1002 : Codeunit 1060;
    BEGIN
      IF NOT ISTEMPORARY THEN
        ERROR(RecordMustBeTemporaryErr);

      DELETEALL;
      CLEAR(Rec);

      CompanyInformation.GET;
      "Country/Region Code" := CompanyInformation.GetCompanyCountryRegionCode;

      GeneralLedgerSetup.GET;
      "Currency Symbol" := GeneralLedgerSetup.GetCurrencySymbol;
      "Amount Rounding Precision" := GetNumberOfDecimals(GeneralLedgerSetup."Amount Rounding Precision");
      "Unit-Amount Rounding Precision" := GetNumberOfDecimals(GeneralLedgerSetup."Unit-Amount Rounding Precision");
      "Quantity Rounding Precision" := 5;
      // We hardcode these to 2/3 as they are like that in business center.
      IF GeneralLedgerSetup.UseVat THEN
        "VAT/Tax Rounding Precision" := 2
      ELSE
        "VAT/Tax Rounding Precision" := 3;

      PaypalAccountProxy.GetPaypalAccount("Paypal Email Address");

      TempNativeAPITaxSetup.LoadSetupRecords;
      TempNativeAPITaxSetup.SETRANGE(Default,TRUE);
      IF TempNativeAPITaxSetup.FINDFIRST THEN BEGIN
        "Default Tax ID" := TempNativeAPITaxSetup.Id;
        "Defauilt Tax Description" := TempNativeAPITaxSetup.Description;
      END;

      GetLanguageInfo;
      GetPaymentInfo;

      IF MarketingSetup.GET THEN BEGIN
        EnableSync := MarketingSetup."Sync with Microsoft Graph";
        IF EnableSync THEN BEGIN
          O365SalesInitialSetup.GET;
          EnableSyncCoupons := O365SalesInitialSetup."Coupons Integration Enabled";
        END;
      END;

      INSERT(TRUE);
    END;

    [Internal]
    PROCEDURE SaveRecord@1();
    VAR
      PaypalAccountProxy@1000 : Codeunit 1060;
    BEGIN
      IF xRec."Currency Symbol" <> "Currency Symbol" THEN
        UpdateCurrencySymbol;

      IF xRec."Paypal Email Address" <> "Paypal Email Address" THEN
        PaypalAccountProxy.SetPaypalAccount("Paypal Email Address",TRUE);

      IF xRec."Country/Region Code" <> "Country/Region Code" THEN
        UpdateCountryRegionCode;

      IF xRec."Language Locale ID" <> "Language Locale ID" THEN
        UpdateLanguageId;

      IF xRec.EnableSync <> EnableSync THEN
        UpdateSync;

      IF xRec.EnableSyncCoupons <> EnableSyncCoupons THEN
        UpdateCouponsSync;
    END;

    LOCAL PROCEDURE UpdateCurrencySymbol@3();
    VAR
      GeneralLedgerSetup@1000 : Record 98;
    BEGIN
      GeneralLedgerSetup.GET;
      IF "Currency Symbol" <> GeneralLedgerSetup."Local Currency Symbol" THEN BEGIN
        GeneralLedgerSetup.VALIDATE("Local Currency Symbol","Currency Symbol");
        GeneralLedgerSetup.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE UpdateCountryRegionCode@6();
    VAR
      CompanyInformation@1000 : Record 79;
    BEGIN
      CompanyInformation.GET;
      IF CompanyInformation."Country/Region Code" <> "Country/Region Code" THEN BEGIN
        CompanyInformation.VALIDATE("Country/Region Code","Country/Region Code");
        CompanyInformation.MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE UpdateLanguageId@9();
    VAR
      UserPersonalization@1000 : Record 2000000073;
    BEGIN
      UserPersonalization.GET(USERSECURITYID);
      IF "Language Locale ID" <> UserPersonalization."Language ID" THEN BEGIN
        UserPersonalization.VALIDATE("Language ID","Language Locale ID");
        UserPersonalization.MODIFY(TRUE);
        GetLanguageInfo;
        GetPaymentInfo;
      END;
    END;

    LOCAL PROCEDURE UpdateSync@7();
    VAR
      MarketingSetup@1001 : Record 5079;
    BEGIN
      CheckSyncAllowed;

      MarketingSetup.GET;
      EnableSync := MarketingSetup."Sync with Microsoft Graph";

      IF NOT EnableSync THEN
        EnableSyncCoupons := FALSE;
    END;

    LOCAL PROCEDURE UpdateCouponsSync@10();
    VAR
      MarketingSetup@1000 : Record 5079;
      O365SalesInitialSetup@1001 : Record 2110;
    BEGIN
      IF EnableSyncCoupons THEN BEGIN
        CheckSyncAllowed;
        MarketingSetup.GET;
        IF NOT MarketingSetup."Sync with Microsoft Graph" THEN
          ERROR(CannotEnableCouponsSyncErr);
        IF MarketingSetup.TrySetWebhookSubscriptionUser(USERSECURITYID) THEN
          ERROR(CannotSetWebhookSubscriptionUserErr);
      END;
      O365SalesInitialSetup.GET;
      O365SalesInitialSetup."Coupons Integration Enabled" := EnableSyncCoupons;
      O365SalesInitialSetup.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE GetNumberOfDecimals@4(AmountRoundingPrecision@1000 : Decimal) : Integer;
    VAR
      Count@1001 : Integer;
    BEGIN
      Count := 0;

      IF AmountRoundingPrecision >= 1 THEN
        EXIT(Count);

      REPEAT
        Count += 1;
        AmountRoundingPrecision := AmountRoundingPrecision * 10;
      UNTIL AmountRoundingPrecision >= 1;

      EXIT(Count);
    END;

    [External]
    PROCEDURE GetPaymentInfo@12();
    VAR
      O365SalesInitialSetup@1001 : Record 2110;
      PaymentMethod@1005 : Record 289;
      PaymentTerms@1006 : Record 3;
    BEGIN
      O365SalesInitialSetup.GET;

      IF PaymentTerms.GET(O365SalesInitialSetup."Default Payment Terms Code") THEN BEGIN
        "Default Payment Terms ID" := PaymentTerms.Id;
        "Def. Pmt. Term Description" := PaymentTerms.GetDescriptionInCurrentLanguage;
      END;

      IF PaymentMethod.GET(O365SalesInitialSetup."Default Payment Method Code") THEN BEGIN
        "Default Payment Method ID" := PaymentMethod.Id;
        "Def. Pmt. Method Description" :=
          COPYSTR(PaymentMethod.GetDescriptionInCurrentLanguage,1,MAXSTRLEN("Def. Pmt. Method Description"));
      END;
    END;

    LOCAL PROCEDURE GetLanguageInfo@5();
    VAR
      UserPersonalization@1001 : Record 2000000073;
      WindowsLanguage@1000 : Record 2000000045;
      LanguageManagement@1003 : Codeunit 43;
    BEGIN
      "Language Locale ID" := LanguageManagement.ApplicationLanguage;

      IF UserPersonalization.GET(USERSECURITYID) THEN
        IF UserPersonalization."Language ID" > 0 THEN
          "Language Locale ID" := UserPersonalization."Language ID";

      IF LanguageManagement.TryGetCultureName("Language Locale ID","Language Code") THEN;

      IF WindowsLanguage.GET("Language Locale ID") THEN
        "Language Display Name" := WindowsLanguage.Name;
    END;

    [External]
    PROCEDURE CheckSyncAllowed@8();
    VAR
      CompanyInformation@1003 : Record 79;
      CompanyInformationMgt@1002 : Codeunit 1306;
      PermissionManager@1001 : Codeunit 9002;
      WebhookManagement@1000 : Codeunit 5377;
    BEGIN
      IF WebhookManagement.IsSyncAllowed THEN
        EXIT;

      IF NOT CompanyInformation.GET THEN
        ERROR(CannotGetCompanyInformationErr);

      IF NOT PermissionManager.SoftwareAsAService THEN
        ERROR(SyncOnlyAllowedInSaasErr);

      IF CompanyInformationMgt.IsDemoCompany THEN
        ERROR(SyncNotAllowedInDemoCompanyErr);

      ERROR(SyncNotAllowedErr);
    END;

    BEGIN
    END.
  }
}

