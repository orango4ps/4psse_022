OBJECT Page 6085595 Document Header Fields ListPar
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW16.00.00.5.00.07;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Belegkopffelder ListPar;
               ENU=Document Header Fields ListPar;
               NLD=Document Header Fields ListPar];
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table6085595;
    SourceTableView=SORTING(Document No.,Sort Order);
    PageType=ListPart;
    SourceTableTemporary=Yes;
    OnOpenPage=BEGIN
                 IF CurrPage.CAPTION <> 'Document Header Fields ListPar' THEN
                   ERROR(STRSUBSTNO(Text001,'Document Header Fields ListPar'));
               END;

    OnFindRecord=VAR
                   Doc@1000000003 : Record 6085590;
                 BEGIN
                   FILTERGROUP(4);
                   IF GETFILTER("Document No.") = '2..1' THEN BEGIN // This fixes a MS bug in RTC
                     FILTERGROUP(0);
                     EXIT;
                   END;

                   IF NOT Doc.GET(GETFILTER("Document No.")) THEN
                     CLEAR(Doc);

                   IF (Doc."No." <> Document."No.") OR (Doc."Template No." <> Document."Template No.") THEN BEGIN
                     Document := Doc;
                     InitFields;
                     IF FINDFIRST THEN;
                     CurrPage.UPDATE(FALSE);
                   END;

                   FILTERGROUP(0);
                   EXIT(FIND(Which));
                 END;

    OnAfterGetRecord=VAR
                       Field@161024012 : Record 6085580;
                     BEGIN
                       AllowEdit := Document.Status = Document.Status::Open;
                       IF AllowEdit THEN BEGIN
                         IF Field.GET(Document."Template No.",Field.Type::Header,"Field Code") THEN
                           AllowEdit := Field.Formula = '';
                       END;
                     END;

  }
  CONTROLS
  {
    { 1000000000;;Container;
                Name=ContentArea;
                ContainerType=ContentArea }

    { 1000000001;1;Group  ;
                Name=Repeater;
                IndentationColumnName=1;
                GroupType=Repeater }

    { 1000000002;2;Field  ;
                Name=Description;
                CaptionML=[DEU=Feld;
                           ENU=Field;
                           NLD=Veld;
                           SVE=F„ltnr];
                SourceExpr=Description;
                Enabled=FALSE;
                Editable=FALSE }

    { 1000000003;2;Field  ;
                Name=Value;
                SourceExpr=Value;
                Editable=AllowEdit;
                OnValidate=VAR
                             Field@1000000001 : Record 6085580;
                           BEGIN
                             Document.FIND('=');
                             Document.TESTFIELD(Status,Document.Status::Open);
                             Document.TESTFIELD("Template No.");
                             Field.GET(Document."Template No.",Field.Type::Header,"Field Code");
                             CaptureMgt.UpdateFieldValue("Document No.","Page No.",0,Field,Value,TRUE,TRUE);
                             Value := CaptureMgt.GetValueAsText(Document."No.",0,Field);
                             "Is Valid" := CaptureMgt.IsValidValue(Field,Document."No.",0);
                             Document.FIND('=');
                             Document.ValidateDocument;
                             UpdatePage;
                             GET(xRec."Document No.",xRec."Field Code");
                           END;

                OnLookup=VAR
                           Field@1000000001 : Record 6085580;
                           NewText@1000000000 : Text[1024];
                         BEGIN
                           Field.GET(Document."Template No.",Field.Type::Header,"Field Code");
                           NewText := CaptureMgt.LookupValue("Document No.",Field,0,Text);
                           IF NewText <> '' THEN BEGIN
                             Text := NewText;
                             EXIT(TRUE);
                           END;
                         END;

                OnAssistEdit=VAR
                               Field@1000000000 : Record 6085580;
                             BEGIN
                               Field.GET(Document."Template No.",Field.Type::Header,"Field Code");
                               Field.SETRECFILTER;
                               PAGE.RUNMODAL(PAGE::"Template Field Card",Field);
                             END;
                              }

    { 1000000004;2;Field  ;
                Name=Is Valid;
                CaptionML=[DEU=OK;
                           ENU=OK;
                           NLD=OK;
                           SVE=OK];
                SourceExpr="Is Valid";
                Editable=FALSE }

  }
  CODE
  {
    VAR
      Document@1000000000 : Record 6085590;
      TempDocField@1000000001 : Record 6085595;
      CaptureMgt@1000000002 : Codeunit 6085576;
      EventNotifier@1000000003 : BigText;
      AllowEdit@1000000004 : Boolean INDATASET;
      Text001@1000 : TextConst 'ENU=Page 6085595 must have the Caption: %1. Please contact your system administrator.';

    PROCEDURE InitFields@1000000003();
    VAR
      Field@1000000000 : Record 6085580;
      DocValue@1000000001 : Record 6085593;
    BEGIN
      DELETEALL;
      Field.SETCURRENTKEY("Template No.",Type,"Sort Order");
      Field.SETRANGE("Template No.",Document."Template No.");
      Field.SETRANGE(Type,Field.Type::Header);
      IF Field.FINDSET THEN
        REPEAT
          "Document No." := Document."No.";
          "Field Code" := Field.Code;
          "Sort Order" := Field."Sort Order";
          Description := Field."Field Name";
          Value := CaptureMgt.GetValueAsText(Document."No.",0,Field);
          IF DocValue.GET(Document."No.",TRUE,Field.Code,0) THEN
            "Page No." := DocValue."Page No.";
          "Is Valid" := CaptureMgt.IsValidValue(Field,Document."No.",0);
          INSERT;
        UNTIL Field.NEXT = 0;
    END;

    PROCEDURE UpdatePage@1000000000();
    BEGIN
      InitFields;
    END;

    BEGIN
    END.
  }
}

