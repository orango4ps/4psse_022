OBJECT Table 7354 Bin
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.05,4PS14.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Location Code,Zone Code,Code;
    OnInsert=BEGIN
               TESTFIELD("Location Code");
               GetLocation("Location Code");
               IF Location."Directed Put-away and Pick" THEN BEGIN
                 TESTFIELD("Zone Code");
                 TESTFIELD("Bin Type Code");
               END ELSE BEGIN
                 TESTFIELD("Zone Code",'');
                 TESTFIELD("Bin Type Code",'');
               END;
             END;

    OnModify=BEGIN
               GetLocation("Location Code");
               IF Location."Directed Put-away and Pick" THEN BEGIN
                 TESTFIELD("Zone Code");
                 TESTFIELD("Bin Type Code");
               END ELSE BEGIN
                 TESTFIELD("Zone Code",'');
                 TESTFIELD("Bin Type Code",'');
               END;
             END;

    OnDelete=VAR
               Location@1003 : Record 14;
               ItemJnlLine@1004 : Record 83;
             BEGIN
               CheckEmptyBin(Text008);

               Location.GET("Location Code");
               IF Location."Adjustment Bin Code" = Code THEN BEGIN
                 ItemJnlLine.SETCURRENTKEY("Entry Type","Item No.","Variant Code","Location Code");
                 ItemJnlLine.SETFILTER("Entry Type",'%1|%2|%3|%4',
                   ItemJnlLine."Entry Type"::"Negative Adjmt.",ItemJnlLine."Entry Type"::Sale,
                   ItemJnlLine."Entry Type"::"Positive Adjmt.",ItemJnlLine."Entry Type"::Purchase);
                 ItemJnlLine.SETRANGE("Location Code","Location Code");
                 IF ItemJnlLine.FINDFIRST THEN
                   IF NOT CONFIRM(
                        Text002,FALSE,STRSUBSTNO(ItemJnlLine.TABLECAPTION,TABLECAPTION))
                   THEN
                     ERROR(Text003);
               END;

               BinContent.RESET;
               BinContent.SETRANGE("Location Code","Location Code");
               BinContent.SETRANGE("Bin Code",Code);
               BinContent.DELETEALL;
             END;

    CaptionML=[ENU=Bin;
               NOR=Hylle];
    LookupPageID=Page7303;
  }
  FIELDS
  {
    { 1   ;   ;Location Code       ;Code10        ;TableRelation=Location;
                                                   CaptionML=[ENU=Location Code;
                                                              NOR=Lokasjonskode;
                                                              SVE=Lagerst„llekod];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 2   ;   ;Code                ;Code20        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 3   ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 4   ;   ;Zone Code           ;Code10        ;TableRelation=Zone.Code WHERE (Location Code=FIELD(Location Code));
                                                   OnValidate=BEGIN
                                                                IF "Zone Code" <> xRec."Zone Code" THEN BEGIN
                                                                  CheckEmptyBin(Text007);
                                                                  IF Code = '' THEN
                                                                    SetUpNewLine;
                                                                  BinContent.RESET;
                                                                  BinContent.SETRANGE("Location Code","Location Code");
                                                                  BinContent.SETRANGE("Bin Code",Code);
                                                                  BinContent.MODIFYALL("Zone Code","Zone Code");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Zone Code;
                                                              NOR=Sonekode;
                                                              SVE=Zonkod];
                                                   Editable=No }
    { 5   ;   ;Adjustment Bin      ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist(Location WHERE (Code=FIELD(Location Code),
                                                                                     Adjustment Bin Code=FIELD(Code)));
                                                   CaptionML=[ENU=Adjustment Bin;
                                                              NOR=Justeringshylle];
                                                   Editable=No }
    { 10  ;   ;Bin Type Code       ;Code10        ;TableRelation="Bin Type";
                                                   OnValidate=BEGIN
                                                                IF "Bin Type Code" <> xRec."Bin Type Code" THEN BEGIN
                                                                  CheckEmptyBin(Text007);
                                                                  BinContent.RESET;
                                                                  BinContent.SETRANGE("Location Code","Location Code");
                                                                  BinContent.SETRANGE("Bin Code",Code);
                                                                  BinContent.MODIFYALL("Bin Type Code","Bin Type Code");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Bin Type Code;
                                                              NOR=Hylletypekode] }
    { 11  ;   ;Warehouse Class Code;Code10        ;TableRelation="Warehouse Class";
                                                   OnValidate=BEGIN
                                                                IF "Warehouse Class Code" <> xRec."Warehouse Class Code" THEN BEGIN
                                                                  CheckEmptyBin(Text007);
                                                                  BinContent.RESET;
                                                                  BinContent.SETRANGE("Location Code","Location Code");
                                                                  BinContent.SETRANGE("Bin Code",Code);
                                                                  BinContent.MODIFYALL("Warehouse Class Code","Warehouse Class Code");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Warehouse Class Code;
                                                              NOR=Lagerklassekode;
                                                              SVE=Dist.lager klasskod] }
    { 12  ;   ;Block Movement      ;Option        ;OnValidate=BEGIN
                                                                IF "Block Movement" <> xRec."Block Movement" THEN BEGIN
                                                                  BinContent.RESET;
                                                                  BinContent.SETRANGE("Location Code","Location Code");
                                                                  BinContent.SETRANGE("Bin Code",Code);
                                                                  BinContent.MODIFYALL("Block Movement","Block Movement");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Block Movement;
                                                              NOR=Sperr for flytting];
                                                   OptionCaptionML=[ENU=" ,Inbound,Outbound,All";
                                                                    NOR=" ,Inng†ende,Utg†ende,Alle"];
                                                   OptionString=[ ,Inbound,Outbound,All] }
    { 20  ;   ;Special Equipment Code;Code10      ;TableRelation="Special Equipment";
                                                   CaptionML=[ENU=Special Equipment Code;
                                                              NOR=Spesialutstyrskode;
                                                              SVE=Specialutrustning kod] }
    { 21  ;   ;Bin Ranking         ;Integer       ;OnValidate=VAR
                                                                WhseActivLine@1000 : Record 5767;
                                                              BEGIN
                                                                IF "Bin Ranking" <> xRec."Bin Ranking" THEN BEGIN
                                                                  BinContent.RESET;
                                                                  BinContent.SETRANGE("Location Code","Location Code");
                                                                  BinContent.SETRANGE("Bin Code",Code);
                                                                  BinContent.MODIFYALL("Bin Ranking","Bin Ranking");

                                                                  WhseActivLine.SETCURRENTKEY("Bin Code","Location Code");
                                                                  WhseActivLine.SETRANGE("Bin Code",Code);
                                                                  WhseActivLine.SETRANGE("Location Code","Location Code");
                                                                  WhseActivLine.MODIFYALL("Bin Ranking","Bin Ranking");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Bin Ranking;
                                                              NOR=Hylleprioritering] }
    { 22  ;   ;Maximum Cubage      ;Decimal       ;OnValidate=BEGIN
                                                                CheckMaxQtyBinContent(FALSE);
                                                              END;

                                                   CaptionML=[ENU=Maximum Cubage;
                                                              NOR=Maksimalt kubikkinnhold];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 23  ;   ;Maximum Weight      ;Decimal       ;OnValidate=BEGIN
                                                                CheckMaxQtyBinContent(TRUE);
                                                              END;

                                                   CaptionML=[ENU=Maximum Weight;
                                                              NOR=Maksimumsvekt];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 30  ;   ;Empty               ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[ENU=Empty;
                                                              NOR=Tom;
                                                              SVE=Tom];
                                                   Editable=No }
    { 32  ;   ;Item Filter         ;Code20        ;FieldClass=FlowFilter;
                                                   TableRelation=Item;
                                                   CaptionML=[ENU=Item Filter;
                                                              NOR=Varefilter;
                                                              SVE=Artikelfilter] }
    { 33  ;   ;Variant Filter      ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation="Stockkeeping Unit"."Variant Code" WHERE (Location Code=FIELD(Location Code),
                                                                                                           Item No.=FIELD(Item Filter));
                                                   CaptionML=[ENU=Variant Filter;
                                                              NOR=Variantfilter;
                                                              SVE=Variantfilter] }
    { 34  ;   ;Default Bin (Item)  ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Bin Content" WHERE (Location Code=FIELD(Location Code),
                                                                                          Bin Code=FIELD(Code),
                                                                                          Item No.=FIELD(Item Filter),
                                                                                          Variant Code=FIELD(Variant Filter),
                                                                                          Default=CONST(Yes)));
                                                   CaptionML=[ENU=Default Bin (Item);
                                                              NOR=Standard];
                                                   Description=4ps: caption change;
                                                   Editable=No }
    { 40  ;   ;Cross-Dock Bin      ;Boolean       ;OnValidate=BEGIN
                                                                IF "Cross-Dock Bin" <> xRec."Cross-Dock Bin" THEN BEGIN
                                                                  BinContent.RESET;
                                                                  BinContent.SETRANGE("Location Code","Location Code");
                                                                  BinContent.SETRANGE("Bin Code",Code);
                                                                  BinContent.MODIFYALL("Cross-Dock Bin","Cross-Dock Bin");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Cross-Dock Bin;
                                                              NOR=Kryssoverf›ringshylle] }
    { 41  ;   ;Dedicated           ;Boolean       ;OnValidate=BEGIN
                                                                IF Dedicated <> xRec.Dedicated THEN BEGIN
                                                                  CheckEmptyBin(Text007);
                                                                  BinContent.RESET;
                                                                  BinContent.SETRANGE("Location Code","Location Code");
                                                                  BinContent.SETRANGE("Bin Code",Code);
                                                                  BinContent.MODIFYALL(Dedicated,Dedicated);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Dedicated;
                                                              NOR=Dedikert] }
    { 11012000;;Default Bin (Location);Boolean    ;OnValidate=BEGIN
                                                                //**4PS
                                                                IF (xRec."Default Bin (Location)" <> "Default Bin (Location)") AND "Default Bin (Location)" THEN
                                                                  IF CheckDefaultBin THEN
                                                                    ERROR(Text11012000,"Location Code");
                                                              END;

                                                   CaptionML=ENU=Default Bin (Location) }
    { 11012010;;Job Filter         ;Code20        ;FieldClass=FlowFilter;
                                                   TableRelation=Job;
                                                   CaptionML=[ENU=Job Filter;
                                                              SVE=Projektfilter] }
    { 11012011;;Job Bin            ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Preference Bin Location" WHERE (Location Code=FIELD(Location Code),
                                                                                                      Bin Code=FIELD(Code),
                                                                                                      Type=CONST(Job),
                                                                                                      No.=FIELD(Job Filter)));
                                                   CaptionML=ENU=Job Bin (Item);
                                                   Editable=No }
    { 11012012;;Default Bin (Job)  ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Preference Bin Location" WHERE (Location Code=FIELD(Location Code),
                                                                                                      Bin Code=FIELD(Code),
                                                                                                      Type=CONST(Job),
                                                                                                      No.=FIELD(Job Filter),
                                                                                                      Default Bin=CONST(Yes)));
                                                   CaptionML=ENU=Default Bin (Job);
                                                   Editable=No }
    { 11012020;;Service Order Filter;Code20       ;FieldClass=FlowFilter;
                                                   TableRelation="Service Order";
                                                   CaptionML=[ENU=Service Order Filter;
                                                              SVE=Serviceorderfilter] }
    { 11012021;;Service Order Bin  ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Preference Bin Location" WHERE (Location Code=FIELD(Location Code),
                                                                                                      Bin Code=FIELD(Code),
                                                                                                      Type=CONST(Service Order),
                                                                                                      No.=FIELD(Service Order Filter)));
                                                   CaptionML=ENU=Service Order Bin (Item);
                                                   Editable=No }
    { 11012022;;Default Bin (Service Order);Boolean;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Exist("Preference Bin Location" WHERE (Location Code=FIELD(Location Code),
                                                                                                      Bin Code=FIELD(Code),
                                                                                                      Type=CONST(Service Order),
                                                                                                      No.=FIELD(Service Order Filter),
                                                                                                      Default Bin=CONST(Yes)));
                                                   CaptionML=ENU=Default Bin (Service Order);
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Location Code,Code                      ;Clustered=Yes }
    {    ;Bin Type Code                            }
    {    ;Location Code,Warehouse Class Code,Bin Ranking }
    {    ;Location Code,Zone Code,Code             }
    {    ;Code                                     }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Location@1009 : Record 14;
      Zone@1000 : Record 7300;
      Text000@1001 : TextConst 'ENU="You cannot %1 the %2 with %3 = %4, %5 = %6, because the %2 contains items.";NOR="Du kan ikke %1 %2 med %3 = %4, %5 = %6, fordi %2 inneholder varer."';
      Text001@1002 : TextConst 'ENU="You cannot %1 the %2 with %3 = %4, %5 = %6, because one or more %7 exists for this %2.";NOR="Du kan ikke %1 %2 med %3 = %4, %5 = %6, fordi det finnes ‚n eller flere %7 for denne %2."';
      Text002@1003 : TextConst 'ENU=One or more %1 exists for this bin. Do you still want to delete this %2?;NOR=Det finnes ‚n eller flere %1 for denne hyllen. Vil du fortsatt slette %2?';
      Text003@1004 : TextConst 'ENU=Cancelled.;NOR=Avbrutt.;SVE=Avbryten';
      BinContent@1010 : Record 7302;
      Item@1012 : Record 27;
      WMSMgt@1005 : Codeunit 7302;
      Text005@1006 : TextConst 'ENU=The total cubage %1 of the %2 in the bin contents exceeds the entered %3 %4.\Do you still want to enter this %3?;NOR=Det totale kubikkinnholdet %1 i %2 i hylleinnholdet overskrider angitt %3 %4.\Vil du fortsatt angi %3?';
      Text006@1007 : TextConst 'ENU=The total weight %1 of the %2 in the bin contents exceeds the entered %3 %4.\Do you still want to enter this %3?;NOR=Den samlede vekten %1 i %2 i hylleinnholdet overskrider angitt %3 %4.\Vil du likevel angi %3?';
      Text007@1008 : TextConst 'ENU=modify;NOR=endre;SVE=Redigera';
      Text008@1011 : TextConst 'ENU=delete;NOR=slett;SVE=Ta bort';
      Text11012000@1100526200 : TextConst 'ENU=There is already a default bin for location code %1.;SVE=Det finns redan ett standardlagerst„lle f”r platskod %1.';

    [External]
    PROCEDURE SetUpNewLine@8();
    BEGIN
      GetLocation("Location Code");
      IF Location."Directed Put-away and Pick" THEN
        IF GetZone("Location Code","Zone Code") THEN BEGIN
          "Bin Type Code" := Zone."Bin Type Code";
          "Warehouse Class Code" := Zone."Warehouse Class Code";
          "Special Equipment Code" := Zone."Special Equipment Code";
          "Bin Ranking" := Zone."Zone Ranking";
          "Cross-Dock Bin" := Zone."Cross-Dock Bin Zone";
        END;

      OnAfterSetUpNewLine(Rec);
    END;

    LOCAL PROCEDURE GetZone@1(LocationCode@1000 : Code[10];ZoneCode@1001 : Code[10]) : Boolean;
    BEGIN
      IF (LocationCode = '') OR (ZoneCode = '') THEN
        EXIT(FALSE);
      IF (Zone."Location Code" <> LocationCode) OR
         (Zone.Code <> ZoneCode)
      THEN
        IF NOT Zone.GET("Location Code","Zone Code") THEN
          EXIT(FALSE);

      EXIT(TRUE);
    END;

    [External]
    PROCEDURE CalcCubageAndWeight@3(VAR Cubage@1000 : Decimal;VAR Weight@1007 : Decimal;CalledbyPosting@1012 : Boolean);
    VAR
      PostedCubage@1001 : Decimal;
      PostedWeight@1002 : Decimal;
      WhseActivityWeight@1006 : Decimal;
      WhseActivityCubage@1005 : Decimal;
      JournalWeight@1004 : Decimal;
      JournalCubage@1003 : Decimal;
      WhseRcptWeight@1009 : Decimal;
      WhseRcptCubage@1008 : Decimal;
      WhseShptWeight@1011 : Decimal;
      WhseShptCubage@1010 : Decimal;
      WhseIntPickWeight@1014 : Decimal;
      WhseIntPickCubage@1013 : Decimal;
    BEGIN
      IF ("Maximum Cubage" <> 0) OR ("Maximum Weight" <> 0) THEN BEGIN
        CalcPostedCubageAndWeight(PostedCubage,PostedWeight);
        IF NOT CalledbyPosting THEN BEGIN
          CalcPutAwayOnWhseActivity(WhseActivityWeight,WhseActivityCubage);
          CalcPutAwayOnWhseJnl(JournalWeight,JournalCubage);
          CalcPutAwayOnWhseRcpt(WhseRcptWeight,WhseRcptCubage);
          CalcPutAwayOnWhseShpt(WhseShptWeight,WhseShptCubage);
          CalcPutAwayOnWhseIntPick(WhseIntPickWeight,WhseIntPickCubage);
        END;
        IF "Maximum Cubage" <> 0 THEN
          Cubage :=
            "Maximum Cubage" -
            (PostedCubage + WhseActivityCubage + JournalCubage +
             WhseRcptCubage + WhseShptCubage + WhseIntPickCubage);

        IF "Maximum Weight" <> 0 THEN
          Weight :=
            "Maximum Weight" -
            (PostedWeight + WhseActivityWeight + JournalWeight +
             WhseRcptWeight + WhseShptWeight + WhseIntPickWeight);
      END;
    END;

    LOCAL PROCEDURE CalcPostedCubageAndWeight@5(VAR PostedCubage@1000 : Decimal;VAR PostedWeight@1002 : Decimal);
    VAR
      WhseEntry@1001 : Record 7312;
    BEGIN
      WhseEntry.SETCURRENTKEY("Bin Code","Location Code");
      WhseEntry.SETRANGE("Bin Code",Code);
      WhseEntry.SETRANGE("Location Code","Location Code");
      WhseEntry.CALCSUMS(Cubage,Weight);
      PostedCubage := WhseEntry.Cubage;
      PostedWeight := WhseEntry.Weight;
    END;

    LOCAL PROCEDURE CalcPutAwayOnWhseActivity@21(VAR WhseActivWeight@1002 : Decimal;VAR WhseActivCubage@1000 : Decimal);
    VAR
      WhseActivLine@1001 : Record 5767;
    BEGIN
      WhseActivLine.SETCURRENTKEY("Bin Code","Location Code","Action Type");
      WhseActivLine.SETRANGE("Bin Code",Code);
      WhseActivLine.SETRANGE("Location Code","Location Code");
      WhseActivLine.SETRANGE("Action Type",WhseActivLine."Action Type"::Place);
      WhseActivLine.CALCSUMS(Cubage,Weight);
      WhseActivCubage := WhseActivLine.Cubage;
      WhseActivWeight := WhseActivLine.Weight;

      WhseActivLine.SETRANGE("Action Type",WhseActivLine."Action Type"::Take);
      WhseActivLine.SETFILTER("Breakbulk No.",'<>0');
      WhseActivLine.CALCSUMS(Cubage,Weight);
      WhseActivCubage := WhseActivCubage - WhseActivLine.Cubage;
      WhseActivWeight := WhseActivWeight - WhseActivLine.Weight;
    END;

    LOCAL PROCEDURE CalcPutAwayOnWhseJnl@22(VAR JournalWeight@1002 : Decimal;VAR JournalCubage@1001 : Decimal);
    VAR
      WhseJnlLine@1000 : Record 7311;
    BEGIN
      WhseJnlLine.SETCURRENTKEY("To Bin Code","Location Code");
      WhseJnlLine.SETRANGE("To Bin Code",Code);
      WhseJnlLine.SETRANGE("Location Code","Location Code");
      WhseJnlLine.CALCSUMS(Cubage,Weight);
      JournalCubage := WhseJnlLine.Cubage;
      JournalWeight := WhseJnlLine.Weight;
    END;

    LOCAL PROCEDURE CalcPutAwayOnWhseRcpt@11(VAR WhseRcptWeight@1002 : Decimal;VAR WhseRcptCubage@1000 : Decimal);
    VAR
      WhseRcptLine@1001 : Record 7317;
    BEGIN
      WhseRcptLine.SETCURRENTKEY("Bin Code","Location Code");
      WhseRcptLine.SETRANGE("Bin Code",Code);
      WhseRcptLine.SETRANGE("Location Code","Location Code");
      WhseRcptLine.CALCSUMS(Cubage,Weight);
      WhseRcptCubage := WhseRcptLine.Cubage;
      WhseRcptWeight := WhseRcptLine.Weight;
    END;

    LOCAL PROCEDURE CalcPutAwayOnWhseShpt@13(VAR WhseShptWeight@1002 : Decimal;VAR WhseShptCubage@1000 : Decimal);
    VAR
      WhseShptLine@1001 : Record 7321;
    BEGIN
      WhseShptLine.SETCURRENTKEY("Bin Code","Location Code");
      WhseShptLine.SETRANGE("Bin Code",Code);
      WhseShptLine.SETRANGE("Location Code","Location Code");
      WhseShptLine.CALCSUMS(Cubage,Weight);
      WhseShptCubage := WhseShptLine.Cubage;
      WhseShptWeight := WhseShptLine.Weight;
    END;

    LOCAL PROCEDURE CalcPutAwayOnWhseIntPick@12(VAR WhseIntPickWeight@1002 : Decimal;VAR WhseIntPickCubage@1000 : Decimal);
    VAR
      WhseIntPickLine@1001 : Record 7334;
    BEGIN
      WhseIntPickLine.SETCURRENTKEY("To Bin Code","Location Code");
      WhseIntPickLine.SETRANGE("To Bin Code",Code);
      WhseIntPickLine.SETRANGE("Location Code","Location Code");
      WhseIntPickLine.CALCSUMS(Cubage,Weight);
      WhseIntPickCubage := WhseIntPickLine.Cubage;
      WhseIntPickWeight := WhseIntPickLine.Weight;
    END;

    LOCAL PROCEDURE GetItem@2(ItemNo@1000 : Code[20]);
    BEGIN
      IF ItemNo <> Item."No." THEN
        Item.GET(ItemNo);
    END;

    LOCAL PROCEDURE CheckMaxQtyBinContent@4(CheckWeight@1000 : Boolean);
    VAR
      TotalCubage@1002 : Decimal;
      TotalWeight@1003 : Decimal;
      Cubage@1004 : Decimal;
      Weight@1005 : Decimal;
    BEGIN
      IF ("Maximum Cubage" <> 0) OR ("Maximum Weight" <> 0) THEN BEGIN
        BinContent.RESET;
        BinContent.SETRANGE("Location Code","Location Code");
        BinContent.SETRANGE("Bin Code",Code);
        IF BinContent.FIND('-') THEN
          REPEAT
            WMSMgt.CalcCubageAndWeight(
              BinContent."Item No.",BinContent."Unit of Measure Code",
              BinContent."Max. Qty.",Cubage,Weight);
            TotalCubage := TotalCubage + Cubage;
            TotalWeight := TotalWeight + Weight;
          UNTIL BinContent.NEXT = 0;

        IF (NOT CheckWeight) AND ("Maximum Cubage" > 0) AND ("Maximum Cubage" - TotalCubage < 0) THEN
          IF NOT CONFIRM(Text005,FALSE,
               TotalCubage,BinContent.FIELDCAPTION("Max. Qty."),
               FIELDCAPTION("Maximum Cubage"),"Maximum Cubage")
          THEN
            ERROR(Text003);
        IF CheckWeight AND ("Maximum Weight" > 0) AND ("Maximum Weight" - TotalWeight < 0) THEN
          IF NOT CONFIRM(Text006,FALSE,
               TotalWeight,BinContent.FIELDCAPTION("Max. Qty."),
               FIELDCAPTION("Maximum Weight"),"Maximum Weight")
          THEN
            ERROR(Text003);
      END;
    END;

    [External]
    PROCEDURE CheckWhseClass@6(ItemNo@1000 : Code[20];IgnoreError@1001 : Boolean) : Boolean;
    BEGIN
      GetItem(ItemNo);
      IF IgnoreError THEN
        EXIT("Warehouse Class Code" = Item."Warehouse Class Code");
      TESTFIELD("Warehouse Class Code",Item."Warehouse Class Code");
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE CheckEmptyBin@9(ErrorText@1003 : Text[250]);
    VAR
      WarehouseEntry@1002 : Record 7312;
      WarehouseJnl@1001 : Record 7311;
      WhseActivLine@1000 : Record 5767;
      WhseRcptLine@1004 : Record 7317;
      WhseShptLine@1005 : Record 7321;
    BEGIN
      WarehouseEntry.SETCURRENTKEY("Bin Code","Location Code");
      WarehouseEntry.SETRANGE("Bin Code",Code);
      WarehouseEntry.SETRANGE("Location Code","Location Code");
      WarehouseEntry.CALCSUMS("Qty. (Base)");
      IF WarehouseEntry."Qty. (Base)" <> 0 THEN
        ERROR(
          Text000,
          ErrorText,TABLECAPTION,FIELDCAPTION("Location Code"),
          "Location Code",FIELDCAPTION(Code),Code);

      WhseActivLine.SETRANGE("Bin Code",Code);
      WhseActivLine.SETRANGE("Location Code","Location Code");
      WhseActivLine.SETRANGE("Activity Type",WhseActivLine."Activity Type"::Movement);
      IF NOT WhseActivLine.ISEMPTY THEN
        ERROR(
          Text001,
          ErrorText,TABLECAPTION,FIELDCAPTION("Location Code"),"Location Code",
          FIELDCAPTION(Code),Code,WhseActivLine.TABLECAPTION);

      WarehouseJnl.SETRANGE("Location Code","Location Code");
      WarehouseJnl.SETRANGE("From Bin Code",Code);
      IF NOT WarehouseJnl.ISEMPTY THEN
        ERROR(
          Text001,
          ErrorText,TABLECAPTION,FIELDCAPTION("Location Code"),"Location Code",
          FIELDCAPTION(Code),Code,WarehouseJnl.TABLECAPTION);

      WarehouseJnl.RESET;
      WarehouseJnl.SETRANGE("To Bin Code",Code);
      WarehouseJnl.SETRANGE("Location Code","Location Code");
      IF NOT WarehouseJnl.ISEMPTY THEN
        ERROR(
          Text001,
          ErrorText,TABLECAPTION,FIELDCAPTION("Location Code"),"Location Code",
          FIELDCAPTION(Code),Code,WarehouseJnl.TABLECAPTION);

      WhseRcptLine.SETRANGE("Bin Code",Code);
      WhseRcptLine.SETRANGE("Location Code","Location Code");
      IF NOT WhseRcptLine.ISEMPTY THEN
        ERROR(
          Text001,
          ErrorText,TABLECAPTION,FIELDCAPTION("Location Code"),"Location Code",
          FIELDCAPTION(Code),Code,WhseRcptLine.TABLECAPTION);

      WhseShptLine.SETRANGE("Bin Code",Code);
      WhseShptLine.SETRANGE("Location Code","Location Code");
      IF NOT WhseShptLine.ISEMPTY THEN
        ERROR(
          Text001,
          ErrorText,TABLECAPTION,FIELDCAPTION("Location Code"),"Location Code",
          FIELDCAPTION(Code),Code,WhseShptLine.TABLECAPTION);

      OnAfterCheckEmptyBin(Rec);
    END;

    [External]
    PROCEDURE CheckIncreaseBin@7(BinCode@1009 : Code[20];ItemNo@1006 : Code[20];Qty@1005 : Decimal;DeductCubage@1013 : Decimal;DeductWeight@1012 : Decimal;PutawayCubage@1008 : Decimal;PutawayWeight@1007 : Decimal;CalledbyPosting@1010 : Boolean;IgnoreError@1000 : Boolean) : Boolean;
    VAR
      WhseActivLine@1001 : Record 5767;
      WMSMgt@1002 : Codeunit 7302;
      AvailableWeight@1003 : Decimal;
      AvailableCubage@1004 : Decimal;
    BEGIN
      IF "Block Movement" IN ["Block Movement"::Inbound,"Block Movement"::All] THEN
        FIELDERROR("Block Movement");

      GetLocation("Location Code");
      IF Code = Location."Adjustment Bin Code" THEN
        EXIT;

      IF ItemNo <> '' THEN
        IF NOT CheckWhseClass(ItemNo,IgnoreError) THEN
          EXIT(FALSE);

      IF (Qty <> 0) AND (("Maximum Cubage" <> 0) OR ("Maximum Weight" <> 0)) THEN
        IF Location."Bin Capacity Policy" IN
           [Location."Bin Capacity Policy"::"Allow More Than Max. Capacity",
            Location."Bin Capacity Policy"::"Prohibit More Than Max. Cap."]
        THEN BEGIN
          CalcCubageAndWeight(AvailableCubage,AvailableWeight,CalledbyPosting);
          IF NOT CalledbyPosting THEN BEGIN
            AvailableCubage := AvailableCubage + DeductCubage;
            AvailableWeight := AvailableWeight + DeductWeight;
          END;

          IF ("Maximum Cubage" <> 0) AND (PutawayCubage > AvailableCubage) THEN
            WMSMgt.CheckPutAwayAvailability(
              BinCode,WhseActivLine.FIELDCAPTION(Cubage),TABLECAPTION,PutawayCubage,AvailableCubage,
              (Location."Bin Capacity Policy" =
               Location."Bin Capacity Policy"::"Prohibit More Than Max. Cap.") AND CalledbyPosting);

          IF ("Maximum Weight" <> 0) AND (PutawayWeight > AvailableWeight) THEN
            WMSMgt.CheckPutAwayAvailability(
              BinCode,WhseActivLine.FIELDCAPTION(Weight),TABLECAPTION,PutawayWeight,AvailableWeight,
              (Location."Bin Capacity Policy" =
               Location."Bin Capacity Policy"::"Prohibit More Than Max. Cap.") AND CalledbyPosting);
        END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE GetLocation@10(LocationCode@1000 : Code[10]);
    BEGIN
      IF Location.Code <> LocationCode THEN
        Location.GET(LocationCode);
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCheckEmptyBin@15(VAR Bin@1000 : Record 7354);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSetUpNewLine@14(VAR Bin@1000 : Record 7354);
    BEGIN
    END;

    PROCEDURE CheckDefaultBin@1100526200() : Boolean;
    VAR
      BinRec@1100526200 : Record 7354;
    BEGIN
      //**4PS
      BinRec.SETRANGE("Default Bin (Location)",TRUE);
      BinRec.SETRANGE("Location Code","Location Code");
      BinRec.SETFILTER(Code,'<>%1',Code);
      IF BinRec.FIND('-') THEN
        EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

