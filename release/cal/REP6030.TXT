OBJECT Report 6030 Create Contract Invoices
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03,4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Servicevertragsrechnungen erstellen;
               ENU=Create Service Contract Invoices;
               NLD=Servicecontractfacturen maken;
               NOR=Opprett servicekontraktfakturaer;
               SVE=Skapa servicekontraktsfakturor];
    ApplicationArea=#Service;
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   IF NOT SetOptionsCalled THEN
                     PostingDate := WORKDATE;
                   NoOfInvoices := 0;
                 END;

  }
  DATASET
  {
    { 9952;    ;DataItem;                    ;
               DataItemTable=Table5965;
               DataItemTableView=SORTING(Bill-to Customer No.,Contract Type,Combine Invoices,Next Invoice Date)
                                 WHERE(Contract Type=CONST(Contract),
                                       Status=CONST(Signed),
                                       Change Status=CONST(Locked));
               OnPreDataItem=VAR
                               ConfirmManagement@1000 : Codeunit 27;
                             BEGIN
                               IF CreateInvoices = CreateInvoices::"Print Only" THEN BEGIN
                                 CLEAR(ContractInvoicingTest);
                                 ContractInvoicingTest.InitVariables(PostingDate,InvoiceToDate);
                                 ContractInvoicingTest.SETTABLEVIEW("Service Contract Header");
                                 ContractInvoicingTest.RUNMODAL;
                                 CurrReport.BREAK;
                               END;

                               IF PostingDate = 0D THEN
                                 ERROR(Text000);

                               IF NOT HideDialog THEN
                                 IF PostingDate > WORKDATE THEN
                                   IF NOT ConfirmManagement.ConfirmProcess(Text001,TRUE) THEN
                                     ERROR(Text002);

                               IF InvoiceToDate = 0D THEN
                                 ERROR(Text003);

                               IF NOT HideDialog THEN
                                 IF InvoiceToDate > WORKDATE THEN
                                   IF NOT ConfirmManagement.ConfirmProcess(Text004,TRUE) THEN
                                     ERROR(Text002);

                               LastCustomer := '';
                               LastContractCombined := FALSE;
                               SETFILTER("Next Invoice Date",'<>%1&<=%2',0D,InvoiceToDate);
                               IF GETFILTER("Invoice Period") <> '' THEN
                                 SETFILTER("Invoice Period",GETFILTER("Invoice Period") + '&<>%1',"Invoice Period"::None)
                               ELSE
                                 SETFILTER("Invoice Period",'<>%1',"Invoice Period"::None);
                               ServContractMgt.CheckMultipleCurrenciesForCustomers("Service Contract Header");
                               Window.OPEN(
                                 Text005 +
                                 '@1@@@@@@@@@@@@@@@@@@@@@@@@@@@@@');

                               CounterTotal := COUNT;
                               Counter1 := 0;
                               Counter2 := 0;
                               CounterBreak := ROUND(CounterTotal / 100,1,'>');
                               Currency.InitRoundingPrecision;

                               OnAfterServiceContractHeaderOnPreDataItem("Service Contract Header",PostingDate,InvoiceToDate);
                             END;

               OnAfterGetRecord=BEGIN
                                  OnBeforeServiceContractHeaderOnAfterGetRecord("Service Contract Header");

                                  Counter1 := Counter1 + 1;
                                  Counter2 := Counter2 + 1;
                                  IF Counter2 >= CounterBreak THEN BEGIN
                                    Counter2 := 0;
                                    Window.UPDATE(1,ROUND(Counter1 / CounterTotal * 10000,1));
                                  END;
                                  CLEAR(ServContractMgt);
                                  ServContractMgt.InitCodeUnit;
                                  ServContractHeader := "Service Contract Header";
                                  WITH ServContractHeader DO BEGIN
                                    TESTFIELD("Serv. Contract Acc. Gr. Code");
                                    ServContractAccGr.GET("Serv. Contract Acc. Gr. Code");
                                    ServContractAccGr.TESTFIELD("Non-Prepaid Contract Acc.");
                                    IF Prepaid THEN
                                      ServContractAccGr.TESTFIELD("Prepaid Contract Acc.");
                                    Cust.GET("Customer No.");
                                    ResultDescription := '';
                                    ServContractMgt.GetNextInvoicePeriod(ServContractHeader,InvoiceFrom,InvoiceTo);
                                    ContractExist := CheckIfCombinationExists("Service Contract Header");
                                    IF "Amount per Period" > 0 THEN BEGIN
                                      IF NOT ServContractMgt.CheckIfServiceExist(ServContractHeader) THEN
                                        ResultDescription := Text006;
                                      IF ResultDescription = '' THEN BEGIN
                                        InvoicedAmount := ROUND(
                                            ServContractMgt.CalcContractAmount(ServContractHeader,InvoiceFrom,InvoiceTo),
                                            Currency."Amount Rounding Precision");
                                        IF InvoicedAmount = 0 THEN
                                          CurrReport.SKIP;
                                        IF NOT "Combine Invoices" OR (LastCustomer <> "Bill-to Customer No.") OR NOT LastContractCombined
                                        THEN BEGIN
                                          InvoiceNo := ServContractMgt.CreateServHeader(ServContractHeader,PostingDate,ContractExist);
                                          NoOfInvoices := NoOfInvoices + 1;
                                        END;
                                        ResultDescription := InvoiceNo;
                                        ServContractMgt.CreateAllServLines(InvoiceNo,ServContractHeader);
                                        LastCustomer := "Bill-to Customer No.";
                                        LastContractCombined := "Combine Invoices";
                                      END;
                                    END ELSE
                                      IF "Annual Amount" = 0 THEN
                                        ResultDescription := STRSUBSTNO(Text009,FIELDCAPTION("Annual Amount"))
                                      ELSE
                                        ResultDescription := '';
                                  END;
                                  ServContractMgt.FinishCodeunit;

                                  OnAfterServiceContractHeaderOnAfterGetRecord("Service Contract Header",InvoiceNo);
                                END;

               OnPostDataItem=BEGIN
                                IF NOT HideDialog THEN BEGIN
                                  IF CreateInvoices = CreateInvoices::"Create Invoices" THEN
                                    IF NoOfInvoices > 1 THEN
                                      MESSAGE(Text010,NoOfInvoices)
                                    ELSE
                                      MESSAGE(Text011,NoOfInvoices);
                                END;

                                OnAfterServiceContractHeaderOnPostDataItem;
                              END;

               ReqFilterFields=Bill-to Customer No.,Contract No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 3   ;2   ;Field     ;
                  Name=PostingDate;
                  CaptionML=[DEU=Buchungsdatum;
                             ENU=Posting Date;
                             NLD=Boekingsdatum;
                             NOR=Bokfõringsdato;
                             SVE=Bokfîringsdatum];
                  ToolTipML=[DEU=Gibt Datum an, das als Buchungsdatum in den erstellten Servicerechnungen verwendet werden soll.;
                             ENU=Specifies the date that you want to use as the posting date on the service invoices created.;
                             NLD=Hiermee wordt de datum opgegeven die u wilt gebruiken als de boekingsdatum voor de gemaakte servicefacturen.;
                             NOR=Angir datoen som du vil bruke som bokfõringsdato pÜ servicefakturaene som er opprettet.];
                  ApplicationArea=#Service;
                  SourceExpr=PostingDate }

      { 2   ;2   ;Field     ;
                  Name=InvoiceToDate;
                  CaptionML=[DEU=Fakturierung bis heute;
                             ENU=Invoice to Date;
                             NLD=Factureren tot datum;
                             NOR=Fakturer t.o.m. dato];
                  ToolTipML=[DEU=Gibt das Datum an, bis zu welchem Sie die VertrÑge fakturieren mîchten. Die Stapelverarbeitung umfasst VertrÑge mit den nÑchsten Fakturierungsterminen zu oder vor diesem Datum.;
                             ENU=Specifies the date up to which you want to invoice contracts. The batch job includes contracts with next invoice dates on or before this date.;
                             NLD=Hiermee wordt de datum opgegeven tot wanneer u contracten wilt factureren. De batchverwerking bevat contracten met de volgende factuurdatums op of v¢¢r deze datum.;
                             NOR=Angir datoen som du vil fakturere kontrakter frem til. Kjõrselen inkluderer kontrakter med de neste fakturadatoene pÜ eller fõr denne datoen.];
                  ApplicationArea=#Service;
                  SourceExpr=InvoiceToDate }

      { 5   ;2   ;Field     ;
                  Name=CreateInvoices;
                  CaptionML=[DEU=Aktion;
                             ENU=Action;
                             NLD=Actie;
                             NOR=Handling;
                             SVE=ètgÑrd];
                  ToolTipML=[DEU=Gibt die gewÅnschte Aktion fÅr zur Fakturierung fÑllige ServicevertrÑge an.;
                             ENU=Specifies the desired action for service contracts that are due for invoicing.;
                             NLD=Hiermee wordt de gewenste actie opgegeven voor servicecontracten die moeten worden gefactureerd.;
                             NOR=Angir den õnskede handlingen for servicekontrakter som forfaller til fakturering.];
                  OptionCaptionML=[DEU=Rechnungen erstellen,Nur Drucken;
                                   ENU=Create Invoices,Print Only;
                                   NLD=Facturen maken,Alleen afdrukken;
                                   NOR=Opprett fakturaer,Kun utskrift];
                  ApplicationArea=#Service;
                  SourceExpr=CreateInvoices }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Sie haben kein Buchungsdatum angegeben.;ENU=You have not filled in the posting date.;NLD=U hebt de boekingsdatum niet ingevuld.;NOR=Du har ikke fylt ut bokfõringsdato.';
      Text001@1001 : TextConst 'DEU=Das Buchungsdatum liegt nach dem Arbeitsdatum.\\BestÑtigen Sie, dass das Datum korrekt ist.;ENU=The posting date is later than the work date.\\Confirm that this is the correct date.;NLD=De boekingsdatum valt na de werkdatum.\\Bevestig of dit de correcte datum is.;NOR=Bokfõringsdatoen er senere enn arbeidsdatoen.\\Bekreft at dette er riktig dato.';
      Text002@1002 : TextConst 'DEU=Das Programm hat die Stapelverarbeitung auf Ihren Befehl angehalten.;ENU=The program has stopped the batch job at your request.;NLD=Het programma is gestopt met de verzochte batchverwerking.;NOR=Programmet har stoppet kjõrselen etter anmodning fra brukeren.';
      Text003@1003 : TextConst 'DEU=Sie mÅssen das Feld Fakturierung bis heute ausfÅllen.;ENU=You must fill in the Invoice-to Date field.;NLD=U moet het veld Factureren tot datum invullen.;NOR=Du mÜ fylle ut feltet Fakturer t.o.m. dato.';
      Text004@1004 : TextConst 'DEU=Das Datum fÅr Fakturierung bis heute liegt nach dem Arbeitsdatum.\\BestÑtigen Sie, dass das Datum korrekt ist.;ENU=The Invoice-to Date is later than the work date.\\Confirm that this is the correct date.;NLD=De datum in het veld Factureren tot datum valt na de werkdatum.\\Bevestig of dit de correcte datum is.;NOR=Datoen i feltet Fakturer t.o.m. dato er senere enn arbeidsdatoen.\\Bekreft at dette er riktig dato..';
      Text005@1005 : TextConst 'DEU=Serv.-Rech. zu Vertr. werden erstellt ...\\;ENU=Creating contract invoices...\\;NLD=Maken contractfacturen...\\;NOR=Oppretter kontraktfakturaer...\\';
      Text006@1006 : TextConst 'DEU=Serviceauftrag fehlt.;ENU=Service Order is missing.;NLD=Serviceorder ontbreekt.;NOR=Serviceordre mangler.';
      Text009@1008 : TextConst 'DEU=%1 fehlt.;ENU=%1 is missing.;NLD=%1 ontbreekt.;NOR=%1 mangler.';
      Text010@1009 : TextConst 'DEU=%1 Rechnungen wurden erstellt.;ENU=%1 invoices were created.;NLD=%1 facturen werden gemaakt.;NOR=%1 fakturaer ble opprettet.';
      Text011@1010 : TextConst 'DEU=%1 Rechnung wurde erstellt.;ENU=%1 invoice was created.;NLD=%1 factuur werd gemaakt.;NOR=%1 faktura ble opprettet.';
      Cust@1011 : Record 18;
      ServContractHeader@1015 : Record 5965;
      ServContractAccGr@1037 : Record 5973;
      Currency@1007 : Record 4;
      ContractInvoicingTest@1012 : Report 5984;
      ServContractMgt@1016 : Codeunit 5940;
      Window@1018 : Dialog;
      InvoicedAmount@1019 : Decimal;
      NoOfInvoices@1020 : Integer;
      CounterTotal@1022 : Integer;
      Counter1@1023 : Integer;
      Counter2@1024 : Integer;
      CounterBreak@1025 : Integer;
      ResultDescription@1026 : Text[80];
      InvoiceNo@1027 : Code[20];
      LastCustomer@1028 : Code[20];
      InvoiceFrom@1029 : Date;
      InvoiceTo@1030 : Date;
      PostingDate@1031 : Date;
      InvoiceToDate@1032 : Date;
      LastContractCombined@1034 : Boolean;
      CreateInvoices@1035 : 'Create Invoices,Print Only';
      ContractExist@1036 : Boolean;
      HideDialog@1038 : Boolean;
      SetOptionsCalled@1039 : Boolean;

    LOCAL PROCEDURE CheckIfCombinationExists@1(FromServContract@1001 : Record 5965) : Boolean;
    VAR
      ServContract2@1000 : Record 5965;
    BEGIN
      ServContract2.SETCURRENTKEY("Customer No.","Bill-to Customer No.");
      ServContract2.SETFILTER("Contract No.",'<>%1',FromServContract."Contract No.");
      ServContract2.SETRANGE("Customer No.",FromServContract."Customer No.");
      ServContract2.SETRANGE("Bill-to Customer No.",FromServContract."Bill-to Customer No.");
      EXIT(ServContract2.FINDFIRST);
    END;

    [External]
    PROCEDURE SetOptions@2(NewPostingDate@1000 : Date;NewInvoiceToDate@1001 : Date;NewCreateInvoices@1002 : 'Create Invoices,Print Only');
    BEGIN
      SetOptionsCalled := TRUE;
      PostingDate := NewPostingDate;
      InvoiceToDate := NewInvoiceToDate;
      CreateInvoices := NewCreateInvoices;
    END;

    [External]
    PROCEDURE SetHideDialog@3(NewHideDialog@1000 : Boolean);
    BEGIN
      HideDialog := NewHideDialog;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterServiceContractHeaderOnPreDataItem@4(VAR ServiceContractHeader@1000 : Record 5965;PostingDate@1001 : Date;InvoiceToDate@1002 : Date);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterServiceContractHeaderOnAfterGetRecord@6(VAR ServiceContractHeader@1000 : Record 5965;InvoiceNo@1001 : Code[20]);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterServiceContractHeaderOnPostDataItem@7();
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeServiceContractHeaderOnAfterGetRecord@5(VAR ServiceContractHeader@1000 : Record 5965);
    BEGIN
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

