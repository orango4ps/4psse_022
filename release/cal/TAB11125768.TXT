OBJECT Table 11125768 Glazing Activity
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Code,Description;
    OnDelete=VAR
               GlazingActivityLine@1100525000 : Record 11125769;
             BEGIN
               GlazingActivityLine.SETRANGE("Glazing Activity Code", Code);
               GlazingActivityLine.DELETEALL;
             END;

    CaptionML=[ENU=Glazing Activity;
               NOR=Innglassingsaktivitet;
               SVE=Inglasningsaktivitet];
    LookupPageID=Page11125769;
    DrillDownPageID=Page11125769;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 10  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 20  ;   ;Rate Code           ;Code10        ;TableRelation="Budget Rate".Code;
                                                   OnValidate=BEGIN
                                                                UpdateGlazingActivityLines(FIELDNO("Rate Code"));
                                                              END;

                                                   CaptionML=[ENU=Rate Code;
                                                              NOR=Avgiftskode;
                                                              SVE=Avgiftskod] }
    { 30  ;   ;Price Reference Date;Date          ;OnValidate=BEGIN
                                                                UpdateGlazingActivityLines(FIELDNO("Price Reference Date"));
                                                              END;

                                                   CaptionML=[ENU=Price Reference Date;
                                                              NOR=Referansedato for pris;
                                                              SVE=Priss„ttningsdatum] }
    { 40  ;   ;Default Surcharge % ;Decimal       ;OnValidate=BEGIN
                                                                UpdateGlazingActivityLines(FIELDNO("Default Surcharge %"));
                                                              END;

                                                   CaptionML=[ENU=Default Surcharge %;
                                                              NOR=Standard tilleggsgebyr %;
                                                              SVE=Std till„ggsavg.proc.] }
    { 50  ;   ;Default Profit/Risk %;Decimal      ;OnValidate=BEGIN
                                                                UpdateGlazingActivityLines(FIELDNO("Default Profit/Risk %"));
                                                              END;

                                                   CaptionML=[ENU=Default Profit/Risk %;
                                                              NOR=Standard fortjeneste/risiko %;
                                                              SVE=Standard procent vinst/risk] }
    { 100 ;   ;Surface Area        ;Decimal       ;OnValidate=BEGIN
                                                                UpdateGlazingActivityLines(FIELDNO("Surface Area"));
                                                                "Avg. Surface Area per Pane" := SafeDivision("Surface Area", Quantity);
                                                              END;

                                                   CaptionML=[ENU=Surface Area;
                                                              NOR=Overflateomr†de;
                                                              SVE=Ytarea] }
    { 110 ;   ;Linear Meters       ;Decimal       ;OnValidate=BEGIN
                                                                UpdateGlazingActivityLines(FIELDNO("Linear Meters"));
                                                              END;

                                                   CaptionML=[ENU=Linear Meters;
                                                              NOR=L›pemeter;
                                                              SVE=L”pmeter] }
    { 120 ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                UpdateGlazingActivityLines(FIELDNO(Quantity));
                                                                "Avg. Surface Area per Pane" := SafeDivision("Surface Area", Quantity);
                                                              END;

                                                   CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal] }
    { 130 ;   ;Height Surcharge    ;Decimal       ;OnValidate=BEGIN
                                                                UpdateGlazingActivityLines(FIELDNO("Height Surcharge"));
                                                              END;

                                                   CaptionML=[ENU=Height Surcharge;
                                                              NOR=St›rrelse p† tilleggsgebyr;
                                                              SVE=H”jd, extra avgift] }
    { 140 ;   ;No. of Glaziers     ;Decimal       ;OnValidate=BEGIN
                                                                UpdateGlazingActivityLines(FIELDNO("No. of Glaziers"));
                                                              END;

                                                   CaptionML=[ENU=No. of Glaziers;
                                                              NOR=Antall glassmestere;
                                                              SVE=Antal glasm„stare] }
    { 200 ;   ;Total Cost          ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Glazing Activity Line"."Total Cost Amount" WHERE (Glazing Activity Code=FIELD(Code)));
                                                   CaptionML=[ENU=Total Cost;
                                                              NOR=Kostbel›p;
                                                              SVE=Totalkostnad];
                                                   DecimalPlaces=2:2;
                                                   Editable=No }
    { 210 ;   ;Total Hours         ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Glazing Activity Line"."Total Hours" WHERE (Glazing Activity Code=FIELD(Code)));
                                                   CaptionML=[ENU=Total Hours;
                                                              NOR=Timer totalt;
                                                              SVE=Timmar totalt];
                                                   Editable=No }
    { 220 ;   ;Total Surcharge     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Glazing Activity Line"."Total Surcharge" WHERE (Glazing Activity Code=FIELD(Code)));
                                                   CaptionML=[ENU=Total Surcharge;
                                                              NOR=Total tilleggsavgift;
                                                              SVE=Till„ggsavgifter totalt];
                                                   Editable=No }
    { 230 ;   ;Total Profit/Risk   ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Glazing Activity Line"."Total Profit/Risk" WHERE (Glazing Activity Code=FIELD(Code)));
                                                   CaptionML=[ENU=Total Profit/Risk;
                                                              NOR=Total fortjeneste/risiko;
                                                              SVE=Totalt vinst/risk];
                                                   Editable=No }
    { 240 ;   ;Total Sales Price   ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Glazing Activity Line"."Total Sales Amount" WHERE (Glazing Activity Code=FIELD(Code)));
                                                   CaptionML=[ENU=Total Sales Price;
                                                              NOR=Total salgspris;
                                                              SVE=Totalt f”rs„ljningspris];
                                                   Editable=No }
    { 300 ;   ;Avg. Surface Area per Pane;Decimal ;CaptionML=[ENU=Avg. Surface Area per Pane;
                                                              NOR=Gjennomsnittlig overflate per rute;
                                                              SVE=Gen. Ytarea per panel];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE InitRecord@1100525001();
    VAR
      GlazingCallMgtSetup@1100525000 : Record 11125762;
    BEGIN
      GlazingCallMgtSetup.GET; // jhoek.281010

      "Price Reference Date" := WORKDATE;
      "Rate Code" := GlazingCallMgtSetup."Default Rate Code"; // jhoek.281010
    END;

    PROCEDURE UpdateGlazingActivityLines@1100525000(FieldNo@1100525001 : Integer);
    VAR
      GlazingActivityLine@1100525000 : Record 11125769;
    BEGIN
      GlazingActivityLine.SETRANGE("Glazing Activity Code", Code);

      IF GlazingActivityLine.FINDSET(TRUE, FALSE) THEN
        REPEAT
          CASE FieldNo OF
            Rec.FIELDNO("Surface Area"): GlazingActivityLine.VALIDATE("Surface Area", "Surface Area");
            Rec.FIELDNO("Linear Meters"): GlazingActivityLine.VALIDATE("Linear Meters", "Linear Meters");
            Rec.FIELDNO(Quantity): GlazingActivityLine.VALIDATE("Activity Quantity", Quantity);
            Rec.FIELDNO("Height Surcharge"): GlazingActivityLine.VALIDATE("Height Surcharge", "Height Surcharge");
            Rec.FIELDNO("No. of Glaziers"): GlazingActivityLine.VALIDATE("No. of Glaziers", "No. of Glaziers");
            Rec.FIELDNO("Rate Code"): GlazingActivityLine.VALIDATE("Rate Code", "Rate Code");
            Rec.FIELDNO("Price Reference Date"): GlazingActivityLine.VALIDATE("Price Reference Date", "Price Reference Date");
            Rec.FIELDNO("Default Surcharge %"): GlazingActivityLine.VALIDATE("Surcharge %", "Default Surcharge %");
            Rec.FIELDNO("Default Profit/Risk %"): GlazingActivityLine.VALIDATE("Profit/Risk %", "Default Profit/Risk %");
          END;
          GlazingActivityLine.MODIFY;
        UNTIL GlazingActivityLine.NEXT = 0;
    END;

    PROCEDURE AvgHoursPerM2@1100525008() : Decimal;
    BEGIN
      CALCFIELDS("Total Hours");
      EXIT(SafeDivision("Total Hours", "Surface Area"));
    END;

    PROCEDURE AvgCostPerM2@1100525009() : Decimal;
    BEGIN
      CALCFIELDS("Total Cost");
      EXIT(SafeDivision("Total Cost", "Surface Area"));
    END;

    PROCEDURE AvgSalesPricePerM2@1100525010() : Decimal;
    BEGIN
      CALCFIELDS("Total Sales Price");
      EXIT(SafeDivision("Total Sales Price", "Surface Area"));
    END;

    PROCEDURE SafeDivision@1100525014(Numerator@1100525000 : Decimal;Denominator@1100525001 : Decimal) : Decimal;
    BEGIN
      IF Denominator = 0 THEN
        EXIT(0);

      EXIT(Numerator/Denominator);
    END;

    BEGIN
    END.
  }
}

