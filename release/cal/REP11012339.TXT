OBJECT Report 11012339 Copy Part
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Kopieren Kalkulationsteil;
               ENU=Copy Estimate Part;
               NLD=Calculatie-onderdeel kopiâren;
               NOR=Kopier kalkyledel;
               SVE=Kopiera kalkyldel];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 4767;    ;DataItem;Origin              ;
               DataItemTable=Table11072073;
               DataItemTableView=SORTING(Estimate No.,Sub-Estimate No.,Part Group,Part);
               OnPreDataItem=BEGIN
                               //db.sn, 09-07-15: C024532
                               SETRANGE("Estimate No.", EstFrom);
                               SETRANGE("Sub-Estimate No.", SubEstFrom);
                               SETRANGE("Part Group", PartGrpFrom);
                               SETRANGE(Part, EstPartFrom);
                               CheckSelection;
                               FormatMgt.FormatElem(EstPartTo,1,'');  //db, 16-06-16: C032655
                               CheckLevel(1);
                               //db.en, 09-07-15: C024532
                             END;

               OnAfterGetRecord=BEGIN
                                  EstFrom := "Estimate No.";
                                  SubEstFrom := "Sub-Estimate No.";
                                  PartGrpFrom := "Part Group";
                                  EstPartFrom := Part;
                                  StartUpdate;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100528909;;Container;
                  ContainerType=ContentArea }

      { 1100528908;1;Group  ;
                  GroupType=Group }

      { 1100525011;2;Field  ;
                  CaptionClass=Text998;
                  Style=Strong;
                  StyleExpr=TRUE }

      { 1100525010;2;Field  ;
                  Name=EstFrom;
                  CaptionML=[DEU=Kalkulation;
                             ENU=Estimate;
                             NLD=Calculatie;
                             NOR=Kalkyle;
                             SVE=Kalkyl];
                  SourceExpr=EstFrom;
                  OnValidate=BEGIN
                               OnValidateEstFrom;
                             END;

                  OnLookup=BEGIN
                             OnLookupEstFrom;
                           END;
                            }

      { 1100525009;2;Field  ;
                  Name=SubEstFrom;
                  CaptionML=[DEU=Teilkalkulation;
                             ENU=Sub-Estimate;
                             NLD=Deelcalculatie;
                             NOR=Delkalkyle;
                             SVE=Delkalkyl];
                  SourceExpr=SubEstFrom;
                  OnValidate=BEGIN
                               OnValidateSubEstFrom;
                             END;

                  OnLookup=BEGIN
                             OnLookupSubEstFrom;
                           END;
                            }

      { 1100525008;2;Field  ;
                  Name=EstPartFrom;
                  CaptionML=[DEU=Teil;
                             ENU=Part;
                             NLD=Onderdeel;
                             NOR=Del;
                             SVE=Del];
                  SourceExpr=EstPartFrom;
                  OnValidate=BEGIN
                               OnValidateEstPartFrom;
                             END;

                  OnLookup=BEGIN
                             OnLookupEstPartFrom;
                           END;
                            }

      { 1100525007;2;Field  ;
                  CaptionClass=Text999;
                  Style=Strong;
                  StyleExpr=TRUE }

      { 1100525006;2;Field  ;
                  Name=EstTo;
                  CaptionML=[DEU=Kalkulation;
                             ENU=Estimate;
                             NLD=Calculatie;
                             NOR=Kalkyle;
                             SVE=Kalkyl];
                  SourceExpr=EstTo;
                  OnValidate=BEGIN
                               OnValidateEstTo;
                             END;

                  OnLookup=BEGIN
                             OnLookupEstTo;
                           END;
                            }

      { 1100525005;2;Field  ;
                  Name=SubEstTo;
                  CaptionML=[DEU=Teilkalkulation;
                             ENU=Sub-Estimate;
                             NLD=Deelcalculatie;
                             NOR=Delkalkyle;
                             SVE=Delkalkyl];
                  SourceExpr=SubEstTo;
                  OnValidate=BEGIN
                               OnValidateSubEstTo;
                             END;

                  OnLookup=BEGIN
                             OnLookupSubEstTo;
                           END;
                            }

      { 1100525004;2;Field  ;
                  Name=EstPartTo;
                  CaptionML=[DEU=Teil;
                             ENU=Part;
                             NLD=Onderdeel;
                             NOR=Del;
                             SVE=Del];
                  SourceExpr=EstPartTo;
                  OnValidate=BEGIN
                               OnValidateEstPartTo;
                             END;

                  OnLookup=BEGIN
                             OnLookupEstPartTo;
                           END;
                            }

      { 1100525003;2;Field  ;
                  CaptionML=[DEU=Teilbeschreibung;
                             ENU=Part Description;
                             NLD=Omschrijving onderdeel;
                             NOR=Delbeskrivelse;
                             SVE=Delbeskrivning];
                  SourceExpr=EstPartToTxt }

      { 1100409001;2;Field  ;
                  CaptionML=[DEU=Teilbeschreibung 2;
                             ENU=Part Description 2;
                             NLD=Omschrijving onderdeel 2];
                  SourceExpr=EstPartToTxt2 }

      { 1100525002;2;Field  ;
                  CaptionML=[DEU=Nur Struktur kopieren;
                             ENU=Skip Lines;
                             NLD=Alleen structuur kopiâren;
                             NOR=Hopp over rader;
                             SVE=Hoppa îver rader];
                  SourceExpr=SkipLines }

      { 1100525001;2;Field  ;
                  CaptionML=[DEU=Preisvereinbarung kopieren;
                             ENU=Copy Price Agreement;
                             NLD=Prijsafspraak kopiâren;
                             NOR=Kopier prisavtale;
                             SVE=Kopiera prisavtal];
                  SourceExpr=CopyPriceAgreement }

      { 1100525000;2;Field  ;
                  CaptionML=[DEU=öbertragen Bemerkungen;
                             ENU=Transfer Comment Lines;
                             NLD=Overzetten opmerkingen;
                             NOR=Overfõr kommentarrader;
                             SVE=ôverfîr kommentarsrader];
                  SourceExpr=TransferCommentLines }

      { 1100409002;2;Field  ;
                  CaptionML=[DEU=Verkauf von Kosten initialisieren;
                             ENU=Iintialize Sales From Cost;
                             NLD=Initialiseren verkoop vanuit kostprijs];
                  SourceExpr=InitSalesFromCost }

      { 1100409000;2;Field  ;
                  CaptionML=[DEU=Kopieren Auszugsblatt;
                             ENU=Copy Estimate Quantity Sheet;
                             NLD=Kopieer uittrekstaten;
                             NOR=Kopier kalkulert antallsspesifikasjon;
                             SVE=Kopiera kalkylerad antalsspecifikation];
                  SourceExpr=CopyEstQuanSheet }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      EstFrom@1100528920 : Code[20];
      EstTo@1100528919 : Code[20];
      SubEstFrom@1100528918 : Code[20];
      SubEstTo@1100528917 : Code[20];
      PartGrpFrom@1100528916 : Code[20];
      PartGrpTo@1100528915 : Code[20];
      EstPartFrom@1100528914 : Code[20];
      EstPartTo@1100528913 : Code[20];
      EstFromTxt@1100528912 : Text[50];
      EstToTxt@1100528911 : Text[50];
      SubEstFromTxt@1100528910 : Text[50];
      SubEstToTxt@1100528909 : Text[50];
      EstPartFromTxt@1100528908 : Text[50];
      EstPartFromTxt2@1100409002 : Text[50];
      EstPartToTxt@1100528907 : Text[50];
      EstPartToTxt2@1100409001 : Text[50];
      EstRec@1100528906 : Record 11012151;
      SubEstRec@1100528905 : Record 11072074;
      EstPartRec@1100528904 : Record 11072073;
      FormatMgt@1100528903 : Codeunit 11012022;
      SkipLines@1100528902 : Boolean;
      CopyPriceAgreement@1100528901 : Boolean;
      TransferCommentLines@1100528900 : Boolean;
      Text000@1100528921 : TextConst 'DEU=Fehlende öbereinstimmung Ursprung-Bestimmung;ENU=Mismatch selection origin-destination;NLD=Ongeldige selectie bron-bestemming';
      Text998@1100525001 : TextConst 'DEU=Kopieren von;ENU=Copy From;NLD=Kopieer van;NOR=Kopier fra;SVE=Kopiera frÜn';
      Text999@1100528922 : TextConst 'DEU=Kopieren in;ENU=Copy To;NLD=Kopieer naar;NOR=Kopier til;SVE=Kopiera till';
      CopyEstQuanSheet@1100409000 : Boolean;
      InitSalesFromCost@1100409004 : Boolean;
      EstSetup@1100409003 : Record 11012150;
      NormGroupFrom@1100525008 : Code[20];
      NormGroupTo@1100525007 : Code[20];
      NormGroupFromTxt@1100525006 : Text[50];
      NormGroupToTxt@1100525005 : Text[50];
      NormGroupSalesFrom@1100525012 : Code[20];
      NormGroupSalesTo@1100525011 : Code[20];
      NormGroupSalesFromTxt@1100525010 : Text[50];
      NormGroupSalesToTxt@1100525009 : Text[50];
      NormGroupRec@1100525004 : Record 11012164;

    PROCEDURE FillDefaults@1(lEstFrom@11012000 : Code[20];lSubEstFrom@11012001 : Code[20];lPartGrpFrom@11012002 : Code[20];lEstPartFrom@11012003 : Code[20]);
    BEGIN
      EstFrom  := lEstFrom;
      SubEstFrom := lSubEstFrom;
      PartGrpFrom := lPartGrpFrom;
      EstPartFrom := lEstPartFrom;

      EstSetup.GET;
      InitSalesFromCost := EstSetup."Init Sales From Cost";  //db, 03-02-15: C020959

      IF EstRec.GET(EstFrom) THEN BEGIN
        EstFromTxt := EstRec.Description;
        //db.sn, 10-02-15: C009772
        NormGroupFrom := EstRec."Norm Group";
        IF NormGroupRec.GET(NormGroupFrom) THEN
          NormGroupFromTxt := COPYSTR(NormGroupRec.Description, 1, 50);
        NormGroupSalesFrom := EstRec."Norm Group (Sales)";
        IF NormGroupRec.GET(NormGroupFrom) THEN
          NormGroupSalesFromTxt := COPYSTR(NormGroupRec.Description, 1, 50);
        //db.en, 10-02-15: C009772
      END;

      IF SubEstRec.GET(EstFrom,SubEstFrom) THEN BEGIN
        SubEstFromTxt := SubEstRec.Description;
        //db.sn, 10-02-15: C009772
        IF SubEstRec."Norm Group" <> '' THEN BEGIN
          NormGroupFrom := SubEstRec."Norm Group";
          IF NormGroupRec.GET(NormGroupFrom) THEN
            NormGroupFromTxt := COPYSTR(NormGroupRec.Description, 1, 50);
        END;
        IF SubEstRec."Norm Group (Sales)" <> '' THEN BEGIN
          NormGroupSalesFrom := SubEstRec."Norm Group (Sales)";
          IF NormGroupRec.GET(NormGroupFrom) THEN
            NormGroupSalesFromTxt := COPYSTR(NormGroupRec.Description, 1, 50);
        END;
        //db.en, 10-02-15: C009772
      END;

      IF EstPartRec.GET(EstFrom,SubEstFrom,PartGrpFrom,EstPartFrom) THEN BEGIN
        EstPartFromTxt := EstPartRec.Description;
        EstPartFromTxt2 := EstPartRec."Description 2";  //DP00686
        //db.sn, 10-02-15: C009772
        IF EstPartRec."Norm Group" <> '' THEN BEGIN
          NormGroupFrom := EstPartRec."Norm Group";
          IF NormGroupRec.GET(NormGroupFrom) THEN
            NormGroupFromTxt := COPYSTR(NormGroupRec.Description, 1, 50);
        END;
        IF EstPartRec."Norm Group (Sales)" <> '' THEN BEGIN
          NormGroupSalesFrom := EstPartRec."Norm Group (Sales)";
          IF NormGroupRec.GET(NormGroupFrom) THEN
            NormGroupSalesFromTxt := COPYSTR(NormGroupRec.Description, 1, 50);
        END;
        //db.en, 10-02-15: C009772
      END;

      EstTo := EstFrom;
      SubEstTo := SubEstFrom;
      PartGrpTo:= PartGrpFrom;
      EstPartTo := EstPartFrom;
      EstToTxt := EstFromTxt;
      SubEstToTxt := SubEstFromTxt;
      EstPartToTxt := EstPartFromTxt;
      EstPartToTxt2 := EstPartFromTxt2;  //DP00686

      CheckDefaultNormGroup;  //db, 10-02-15: C009772
    END;

    PROCEDURE ValidateEstimate@1210190001();
    VAR
      EstLineRec@1210190000 : Record 11072072;
      lvText000@1210190001 : TextConst 'DEU=Es gibt bereits Kalkulationszeilen in angegebener Bestimmung.;ENU=Estimate Lines present in Destination.;NLD=Er zijn reeds calculatieregels aanwezig in opgegeven bestemming.;NOR=Det finnes kalkylerader i Destinasjonen.;SVE=Det finns kalkylrader i destinationen.';
      lvChapter@1210190003 : Code[20];
      lvParagraph@1210190004 : Code[20];
      lvLevel@1210190005 : Integer;
      LineExist@1100525000 : Boolean;
      lvText001@1100525001 : TextConst 'DEU=Es gibt bereits Kalkulationszeilen in angegebener Bestimmung. Fortfahren?;ENU="Estimate Lines present in Destination; Continue ?";NLD="Er zijn reeds calculatieregels aanwezig in opgegeven bestemming; doorgaan ?";NOR=Det finnes kalkylerader i Destinasjonen. Vil du fortsette?;SVE=Det finns kalkylrader i destinationen. Vill du fortsÑtta?';
      sp1@1100528500 : Integer;
      sp2@1100528501 : Integer;
      CurrLevel@1100528502 : Integer;
      FormatManagement@1100528900 : Codeunit 11012022;
    BEGIN
      FormatMgt.FormatElemExtended(1,EstPartTo,lvChapter,lvParagraph,lvLevel);

      //C054075.sn
      FormatManagement.CheckFormat(1,sp1,sp2);  //C063045
      CurrLevel := lvLevel;
      IF (sp1 = 0) AND (CurrLevel = 1) THEN CurrLevel := CurrLevel + 1;
      IF (sp2 = 0) AND (CurrLevel = 2) THEN CurrLevel := CurrLevel + 1;
      //C054075.en

      EstLineRec.RESET;
      EstLineRec.SETRANGE("Estimate No.", EstTo);
      EstLineRec.SETRANGE("Sub-Estimate No.", SubEstTo);
      EstLineRec.SETRANGE("Part Group", PartGrpTo);
      CASE CurrLevel OF  //C054075
        1: EstLineRec.SETRANGE(Chapter, lvChapter);
        2: EstLineRec.SETRANGE(Paragraph, lvParagraph);
        3: EstLineRec.SETRANGE(Part, EstPartTo);
      END;
      IF EstLineRec.FINDFIRST THEN BEGIN
        LineExist := TRUE;
        IF NOT SkipLines THEN
          ERROR(lvText000);
      END;

      IF LineExist THEN BEGIN
        IF NOT CONFIRM(lvText001) THEN
          ERROR('');
      END;
    END;

    PROCEDURE StartUpdate@1210190000();
    VAR
      CopyMgt@1210190000 : Codeunit 11012227;
    BEGIN
      EstRec.GET(EstTo);
      EstRec.TESTFIELD(Fixed, FALSE);
      ValidateEstimate;
      CopyMgt.SetSelectionSubLevel(NormGroupFrom, NormGroupTo, NormGroupSalesFrom, NormGroupSalesTo);  //db, 10-02-15: C009772
      CopyMgt.SetTransferComments(TransferCommentLines);
      CopyMgt.SetCopyQuantitySheet(CopyEstQuanSheet);  //DP00686
      CopyMgt.SetInitSalesFromCost(InitSalesFromCost);  //db, 03-02-15: C020959
      CopyMgt.CopyEstPart(EstFrom,SubEstFrom,EstTo,SubEstTo,PartGrpFrom,EstPartFrom,EstPartTo,
        EstPartToTxt,EstPartToTxt2,SkipLines,CopyPriceAgreement);  //DP00686
    END;

    PROCEDURE CheckLevel@1100485000(iAction@1100525000 : Integer);
    VAR
      lvText000@1100485000 : TextConst 'DEU=Herkunft und Bestimmungsteil mÅssen von der gleichen Ebene sein.;ENU=Source and Target Part should be of the same level;NLD=Herkomst en bestemmingsonderdeel moeten van hetzelfde niveau zijn;NOR=Kilde- og MÜldelen mÜ ha samme nivÜ;SVE=Ursprungs- och mÜldelen mÜste ha samma nivÜ';
      lvPartRec@1100485001 : TEMPORARY Record 11072073;
    BEGIN
      //db, 09-07-15: C024532
      WITH lvPartRec DO BEGIN
        FormatMgt.DetermineLevel(EstPartTo,Chapter,Paragraph,Level);
        EstPartRec.GET(EstFrom, SubEstFrom, PartGrpFrom, EstPartFrom);
        IF Level <> EstPartRec.Level THEN BEGIN
          IF iAction = 1 THEN  //db, 21-09-15: C024532
            ERROR(lvText000);
          IF iAction = 0 THEN
            EstPartTo := EstPartFrom;
        END;
      END;
    END;

    PROCEDURE CheckSelection@1100528900();
    BEGIN
      IF ((EstFrom = '') AND (EstTo <> '')) OR
         ((EstFrom <> '') AND (EstTo = '')) OR
         ((SubEstFrom = '') AND (SubEstTo <> '')) OR
         ((SubEstFrom <> '') AND (SubEstTo = '')) OR
         ((EstPartFrom = '') AND (EstPartTo <> '')) THEN
        ERROR(Text000);

      IF (EstFrom = EstTo) AND  (SubEstFrom = SubEstTo) AND  (EstPartFrom = EstPartTo) THEN
        ERROR(Text000);  //db, 21-09-15: C024532
    END;

    PROCEDURE "-CopyFrom-"@1100525012();
    BEGIN
    END;

    PROCEDURE OnValidateEstFrom@1100525011();
    BEGIN
      EstRec."No." := EstFrom;
      EstRec.DPA_FindRecWithDPAFilterAppld;

      EstFromTxt := EstRec.Description;
    END;

    PROCEDURE OnLookupEstFrom@1100525010();
    BEGIN
      EstRec.RESET;
      IF PAGE.RUNMODAL(0,EstRec) = ACTION::LookupOK THEN BEGIN
        EstFrom := EstRec."No.";
        EstFromTxt := EstRec.Description;
      END;
    END;

    PROCEDURE OnValidateSubEstFrom@1100525009();
    VAR
      Chapter@1100525002 : Code[20];
      Paragraph@1100525001 : Code[20];
      Level@1100525000 : Integer;
    BEGIN
      FormatMgt.FormatSubEstimate(SubEstFrom,Chapter,Paragraph,Level,EstFrom,FALSE);
      SubEstRec.GET(EstFrom,SubEstFrom);
      SubEstFromTxt := SubEstRec.Description;
      PartGrpFrom := SubEstRec."Part Group";
    END;

    PROCEDURE OnLookupSubEstFrom@1100525008();
    BEGIN
      SubEstRec.RESET;
      SubEstRec.SETRANGE("Estimate No.",EstFrom);
      IF PAGE.RUNMODAL(0,SubEstRec) = ACTION::LookupOK THEN BEGIN
        EstRec.GET(SubEstRec."Estimate No.");
        EstFrom := EstRec."No.";
        EstFromTxt := EstRec.Description;
        SubEstFrom := SubEstRec."Sub-Estimate No.";
        SubEstFromTxt := SubEstRec.Description;
        PartGrpFrom := SubEstRec."Part Group";
      END;
    END;

    PROCEDURE OnValidateEstPartFrom@1100525007();
    BEGIN
      FormatMgt.FormatElem(EstPartFrom,1,'');
      CheckLevel(0);
      //DP00686.sn
      EstPartRec.GET(EstFrom, SubEstFrom, PartGrpFrom, EstPartFrom);
      EstPartFromTxt := EstPartRec.Description;
      EstPartFromTxt2 := EstPartRec."Description 2";
      EstPartToTxt := EstPartFromTxt;
      EstPartToTxt2 := EstPartFromTxt2;
      //DP00686.en
    END;

    PROCEDURE OnLookupEstPartFrom@1100525006();
    BEGIN
      EstPartRec.RESET;
      EstPartRec.SETRANGE("Estimate No.",EstFrom);
      EstPartRec.SETRANGE("Sub-Estimate No.",SubEstFrom);
      IF PAGE.RUNMODAL(0,EstPartRec) = ACTION::LookupOK THEN BEGIN
        PartGrpFrom := EstPartRec."Part Group";
        EstPartFrom := EstPartRec.Part;
        //DP00686.sn
        EstPartFromTxt := EstPartRec.Description;
        EstPartFromTxt2 := EstPartRec."Description 2";
        EstPartToTxt := EstPartFromTxt;
        EstPartToTxt2 := EstPartFromTxt2;
        //DP00686.en
      END;
    END;

    PROCEDURE "-CopyTo-"@1100525013();
    BEGIN
    END;

    PROCEDURE OnValidateEstTo@1100525000();
    BEGIN
      EstRec."No." := EstTo;
      EstRec.DPA_FindRecWithDPAFilterAppld;

      EstToTxt := EstRec.Description;
      CheckDefaultNormGroup;  //db, 10-02-15: C009772
    END;

    PROCEDURE OnLookupEstTo@1100525001();
    BEGIN
      EstRec.RESET;
      IF PAGE.RUNMODAL(0,EstRec) = ACTION::LookupOK THEN BEGIN
        EstTo := EstRec."No.";
        OnValidateEstTo;  //db, 10-02-15: C009772
      END;
    END;

    PROCEDURE OnValidateSubEstTo@1100525003();
    VAR
      Chapter@1100525002 : Code[20];
      Paragraph@1100525001 : Code[20];
      Level@1100525000 : Integer;
    BEGIN
      FormatMgt.FormatSubEstimate(SubEstTo,Chapter,Paragraph,Level,EstTo,FALSE);
      SubEstRec.GET(EstTo,SubEstTo);
      SubEstToTxt := SubEstRec.Description;
      PartGrpTo := SubEstRec."Part Group";

      CheckDefaultNormGroup;  //db, 10-02-15: C009772
    END;

    PROCEDURE OnLookupSubEstTo@1100525002();
    BEGIN
      SubEstRec.RESET;
      SubEstRec.SETRANGE("Estimate No.",EstTo);
      IF PAGE.RUNMODAL(0,SubEstRec) = ACTION::LookupOK THEN BEGIN
         SubEstTo := SubEstRec."Sub-Estimate No.";
         OnValidateSubEstTo;  //db, 10-02-15: C009772
      END;
    END;

    PROCEDURE OnValidateEstPartTo@1100525004();
    BEGIN
      FormatMgt.FormatElem(EstPartTo,1,'');
      CheckLevel(0);
      //DP00686.sn
      IF EstPartRec.GET(EstTo, SubEstTo, PartGrpTo, EstPartTo) THEN BEGIN
        EstPartToTxt := EstPartRec.Description;
        EstPartToTxt2 := EstPartRec."Description 2";
      END ELSE BEGIN
        EstPartToTxt := EstPartFromTxt;
        EstPartToTxt2 := EstPartFromTxt2;
      END;
      //DP00686.en
    END;

    PROCEDURE OnLookupEstPartTo@1100525005();
    BEGIN
      EstPartRec.RESET;
      EstPartRec.SETRANGE("Estimate No.",EstTo);
      EstPartRec.SETRANGE("Sub-Estimate No.",SubEstTo);
      IF PAGE.RUNMODAL(0,EstPartRec) = ACTION::LookupOK THEN BEGIN
         PartGrpTo := EstPartRec."Part Group";
         EstPartTo := EstPartRec.Part;
         OnValidateEstPartTo;  //db, 10-02-15: C009772
      END;
    END;

    PROCEDURE OnValidateNormGroupTo@1100525016();
    BEGIN
      IF NOT NormGroupRec.GET(NormGroupTo) THEN BEGIN
        NormGroupTo := NormGroupFrom;
        NormGroupToTxt := NormGroupFromTxt;
      END ELSE BEGIN
        NormGroupToTxt := COPYSTR(NormGroupRec.Description, 1, 50);
      END;
    END;

    PROCEDURE OnValidateNormGroupSalesTo@1100525015();
    BEGIN
      IF NOT NormGroupRec.GET(NormGroupSalesTo) THEN BEGIN
        NormGroupSalesTo := NormGroupSalesFrom;
        NormGroupSalesToTxt := NormGroupSalesFromTxt;
      END ELSE BEGIN
        NormGroupSalesToTxt := COPYSTR(NormGroupRec.Description, 1, 50);
      END;
    END;

    PROCEDURE CheckDefaultNormGroup@1100525014();
    VAR
      EstRecTo@1100525001 : Record 11012151;
      SubEstRecTo@1100525000 : Record 11072074;
    BEGIN
      //db, 10-02-15: C009772
      EstRecTo.GET(EstTo);
      IF NOT SubEstRecTo.GET(EstTo,SubEstTo) THEN EXIT;  //db, 30-11-2017: C041907

      IF SubEstRecTo."Norm Group" <> '' THEN
        NormGroupTo := SubEstRecTo."Norm Group"
      ELSE
        NormGroupTo := EstRecTo."Norm Group";
      IF SubEstRecTo."Norm Group (Sales)" <> '' THEN
        NormGroupSalesTo := SubEstRecTo."Norm Group (Sales)"
      ELSE
        NormGroupSalesTo := EstRecTo."Norm Group (Sales)";

      OnValidateNormGroupTo;
      OnValidateNormGroupSalesTo;
    END;

    BEGIN
    {
      db, 10-02-15: From-Selection editable; overrule tablefilter by DataItemTableView to disable selection/filter DataItem of this report
    }
    END.
  }
  RDLDATA
  {
  }
}

