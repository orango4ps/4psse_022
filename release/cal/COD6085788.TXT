OBJECT Codeunit 6085788 DC Client Add-In Management
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW19.00.00.5.00;
  }
  PROPERTIES
  {
    OnRun=VAR
            Window@6085573 : Dialog;
          BEGIN
            CreateClientAddInEntries;
            COMMIT;
            IF NOT HasImportedClientAddIns THEN BEGIN
              Window.OPEN(Text004);
              ImportWebClientAddIns(FALSE);
              CODEUNIT.RUN(CODEUNIT::"Online Client Resource Mgt.");
              Window.CLOSE;
            END;
          END;

  }
  CODE
  {
    VAR
      Text001@1002 : TextConst 'DEU=Es ist nicht m”glich, die Continia-Web-Client-Add-ins von Continia Online herunterzuladen.\\M”chten Sie einen Ordner ausw„hlen, wo sich die Continia-Web-Client-Add-Ins befinden?;ENU=It was not possible to download the Continia Web Client Add-ins from Continia Online.\\Do you want to select a local folder where the Continia Web Client Add-ins are located?;NLD=Niet mogelijk om de Continia Web Client Add-ins te downloaden van Continia Online.\\Wilt u een map selecteren waar de Continia Web Client Add-ins zich bevinden?';
      Text002@1000 : TextConst 'DEU=W„hlen Sie den Ordner aus, wo sich die Cortinia-Web-Client-Add-ins befinden.;ENU=Please select the folder where the Continia Web Client Add-ins are located.;NLD=Selecteer de map waar de Continia Web Client Add-ins zich bevinden.';
      Text003@1001 : TextConst 'DEU=Die notwendigen Komponenten wurden nicht im ausgew„hlten Ordner gefunden.\\M”chten Sie einen anderen Ordner ausw„hlen?\\Sie k”nnen auch sp„ter von der Document Capture-Einrichtung oder Expense Management-Einrichtung eine Wahl treffen.;ENU=The necessary components were not found in the selected folder.\\Do you want to select another folder?\\You can do this from the Document Capture or Expense Management Setup card later.;NLD=De noodzakelijke componenten zijn niet gevonden in de geselecteerde map.\\Wilt u een andere map selecteren?\\U kunt dit ook later doen vanuit de Document Capture-instellingen of de Expense Management-instellingen.';
      Text004@6085573 : TextConst 'DEU=Add-ins von Continia Online herunterladen...;ENU=Downloading Add-ins from Continia Online...;NLD=Add-ins downloaden van Continia Online...';

    PROCEDURE CreateClientAddInEntries@161024012();
    VAR
      ClientAddIn@161024012 : Record 2000000069;
    BEGIN
      CreateClientAddInEntry(
        'Akeyto.DocumentCapture.Dynamics.Nav.EventNotifier','0c53e51c0afe7001','5.0.0.0',ClientAddIn.Category::"DotNet Control Add-in");
      CreateClientAddInEntry(
        'Continia.DocumentCapture.Dynamics.Nav.ClientEventNotifier','0c53e51c0afe7001','5.0.0.0',ClientAddIn.Category::"DotNet Control Add-in");
      CreateClientAddInEntry(
        'Continia.DocumentCapture.Dynamics.Nav.Page.EventListener','0c53e51c0afe7001','5.0.0.0',ClientAddIn.Category::"DotNet Control Add-in");
      CreateClientAddInEntry(
        'Continia.DocumentCapture.Dynamics.Nav.Web.UI','0c53e51c0afe7001','5.0.0.0',ClientAddIn.Category::"JavaScript Control Add-in");
      CreateClientAddInEntry(
        'Continia.DocumentCapture.Dynamics.Nav.Web.DragAndDrop','0c53e51c0afe7001','5.0.0.0',ClientAddIn.Category::"JavaScript Control Add-in");
      CreateClientAddInEntry(
        'Akeyto.DocumentCapture.Dynamics.Nav.Scanner','0c53e51c0afe7001','5.0.0.0',ClientAddIn.Category::"DotNet Control Add-in");
      CreateClientAddInEntry(
        'Akeyto.DocumentCapture.Dynamics.Nav.UI','0c53e51c0afe7001','5.0.0.0',ClientAddIn.Category::"DotNet Control Add-in");
      CreateClientAddInEntry(
        'Akeyto.DocumentCapture.Dynamics.Nav.MatchInfoBar','0c53e51c0afe7001','5.0.0.0',ClientAddIn.Category::"DotNet Control Add-in");
      CreateClientAddInEntry(
        'Continia.DocumentCapture.Dynamics.Nav.DocDragAndDrop110','0c53e51c0afe7001','5.0.0.0',ClientAddIn.Category::"DotNet Control Add-in");
      CreateClientAddInEntry(
        'Continia.DocumentCapture.Dynamics.Nav.DocDragAndDrop220','0c53e51c0afe7001','5.0.0.0',ClientAddIn.Category::"DotNet Control Add-in");
      CreateClientAddInEntry(
        'Continia.DocumentCapture.Dynamics.Nav.Page.Controls.RadioButton','0c53e51c0afe7001','5.0.0.0',ClientAddIn.Category::"DotNet Control Add-in");
      CreateClientAddInEntry(
        'Akeyto.DocumentCapture.Dynamics.Nav.Shared','0c53e51c0afe7001','5.0.0.0',ClientAddIn.Category::"DotNet Interoperability");
      CreateClientAddInEntry(
        'Continia.DocumentCapture.Dynamics.Nav.MessageBus','0c53e51c0afe7001','5.0.0.0',ClientAddIn.Category::"DotNet Interoperability");
    END;

    LOCAL PROCEDURE CreateClientAddInEntry@2(AddInName@1000 : Text[220];PublicKeyToken@1001 : Text[20];Version@1002 : Text[25];Category@1003 : Integer);
    VAR
      ClientAddIn@1004 : Record 2000000069;
    BEGIN
      IF ClientAddIn.GET(AddInName,PublicKeyToken,Version) THEN
        EXIT;

      ClientAddIn.SETRANGE("Add-in Name",AddInName);
      IF ClientAddIn.FINDSET THEN
        REPEAT
          ClientAddIn.DELETE(TRUE);
        UNTIL ClientAddIn.NEXT = 0;

      ClientAddIn."Add-in Name" := AddInName;
      ClientAddIn."Public Key Token" := PublicKeyToken;
      ClientAddIn.Version := Version;
      ClientAddIn.Category := Category;
      ClientAddIn.INSERT;
    END;

    PROCEDURE ImportWebClientAddIns@1(OverwriteExisting@1008 : Boolean) : Boolean;
    VAR
      TempNameValueBuffer@1003 : TEMPORARY Record 823;
      ClientAddIn@1005 : Record 2000000069;
      FileSysMgt@1001 : Codeunit 6085584;
      WebClientMgt@1000 : Codeunit 6085662;
      FolderPath@1002 : Text[250];
      ResourceFilePath@1004 : Text[250];
      ServerFilePath@1006 : Text[1024];
      AddInsFound@1007 : Boolean;
    BEGIN
      ClientAddIn.SETFILTER("Add-in Name",'Continia.DocumentCapture.Dynamics.Nav.Web.*');
      ClientAddIn.SETRANGE(Category,ClientAddIn.Category::"JavaScript Control Add-in");
      IF ClientAddIn.ISEMPTY THEN
        EXIT;

      IF NOT OverwriteExisting THEN
        IF ClientAddIn.FINDSET THEN
          REPEAT
            IF ClientAddIn.Resource.HASVALUE THEN
              EXIT;
          UNTIL ClientAddIn.NEXT = 0;

      IF NOT WebClientMgt.RUN THEN BEGIN
        IF NOT CONFIRM(Text001,TRUE) THEN
          EXIT(FALSE);

        WHILE NOT AddInsFound DO BEGIN
          FileSysMgt.SelectFolderDialog(Text002,FolderPath);

          IF FolderPath = '' THEN
            EXIT;

          FileSysMgt.GetClientDirectoryFilesList(TempNameValueBuffer,FolderPath);

          AddInsFound := TRUE;
          IF ClientAddIn.FINDSET THEN
            REPEAT
              IF TempNameValueBuffer.FINDSET THEN
                REPEAT
                  ResourceFilePath := STRSUBSTNO('%1\%2.zip',FolderPath,ClientAddIn."Add-in Name");
                  TempNameValueBuffer.SETFILTER(Name,ResourceFilePath);
                  IF TempNameValueBuffer.ISEMPTY THEN
                    AddInsFound := FALSE;
                UNTIL (TempNameValueBuffer.NEXT = 0) OR NOT AddInsFound;
            UNTIL (ClientAddIn.NEXT = 0) OR NOT AddInsFound;

          IF NOT AddInsFound THEN
            IF NOT CONFIRM(Text003,TRUE) THEN
              EXIT(FALSE)
            ELSE
              FolderPath := '';
        END;

        IF ClientAddIn.FINDSET(TRUE,FALSE) THEN
          REPEAT
            ResourceFilePath := STRSUBSTNO('%1\%2.zip',FolderPath,ClientAddIn."Add-in Name");
            TempNameValueBuffer.SETFILTER(Name,ResourceFilePath);
            IF TempNameValueBuffer.FINDSET THEN
              REPEAT
                ServerFilePath := FileSysMgt.UploadFileSilent(TempNameValueBuffer.Name);
                ClientAddIn.Resource.IMPORT(ServerFilePath);
                ClientAddIn.MODIFY;
              UNTIL TempNameValueBuffer.NEXT = 0;
        UNTIL ClientAddIn.NEXT = 0;

        EXIT(TRUE)
      END ELSE
        EXIT(TRUE);
    END;

    LOCAL PROCEDURE HasImportedClientAddIns@6085573() : Boolean;
    VAR
      ClientAddIn@6085573 : Record 2000000069;
      VersionMgt@6085574 : Codeunit 6192775;
    BEGIN
      IF VersionMgt.NAVVersion >= 100000 THEN
        ClientAddIn.SETFILTER("Add-in Name",'Continia.DocumentCapture.*|Akeyto.DocumentCapture.*')
      ELSE
        ClientAddIn.SETFILTER("Add-in Name",'Continia.DocumentCapture.Dynamics.Nav.Web.*');

      IF ClientAddIn.FINDSET THEN
        REPEAT
          ClientAddIn.CALCFIELDS(Resource);
          IF NOT ClientAddIn.Resource.HASVALUE THEN
            EXIT(FALSE);
        UNTIL ClientAddIn.NEXT = 0;

      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

