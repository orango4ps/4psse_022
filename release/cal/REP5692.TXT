OBJECT Report 5692 Calculate Depreciation
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04,4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=AfA berechnen;
               ENU=Calculate Depreciation;
               NLD=Afschrijving berekenen;
               NOR=Beregn avskrivninger;
               SVE=BerÑkna avskrivning];
    ApplicationArea=#FixedAssets;
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  FPSLicenseManagement@1100527400 : Codeunit 11229289;
                BEGIN
                  DeprBook.GET(DeprBookCode);
                  IF DeprUntilDate = 0D THEN
                    ERROR(Text000,FAJnlLine.FIELDCAPTION("FA Posting Date"));
                  IF PostingDate = 0D THEN
                    PostingDate := DeprUntilDate;
                  IF UseForceNoOfDays AND (DaysInPeriod = 0) THEN
                    ERROR(Text001);

                  IF DeprBook."Use Same FA+G/L Posting Dates" AND (DeprUntilDate <> PostingDate) THEN
                    ERROR(
                      Text002,
                      FAJnlLine.FIELDCAPTION("FA Posting Date"),
                      FAJnlLine.FIELDCAPTION("Posting Date"),
                      DeprBook.FIELDCAPTION("Use Same FA+G/L Posting Dates"),
                      FALSE,
                      DeprBook.TABLECAPTION,
                      DeprBook.FIELDCAPTION(Code),
                      DeprBook.Code);

                  //**4PS.sn
                  gBlnPlantIntegration := FALSE;
                  IF (FPSLicenseManagement.LicenseAndReadPermissionForTable(gRecPlantSetup)) THEN BEGIN
                    IF gRecPlantSetup.GET THEN BEGIN
                      gBlnPlantIntegration := gRecPlantSetup."Integration Fixed Assets";
                      CheckNotPostedPlantSalvage();
                    END;
                  END;
                  //**4PS.en

                  Window.OPEN(
                    Text003 +
                    Text004 +
                    Text005);
                END;

    OnPostReport=VAR
                   PageGenJnlLine@1000 : Record 81;
                   PageFAJnlLine@1001 : Record 5621;
                 BEGIN
                   Window.CLOSE;
                   IF (FAJnlLineCreatedCount = 0) AND (GenJnlLineCreatedCount = 0) THEN BEGIN
                     MESSAGE(CompletionStatsMsg);
                     EXIT;
                   END;

                   IF FAJnlLineCreatedCount > 0 THEN
                     IF CONFIRM(CompletionStatsFAJnlQst,TRUE,FAJnlLineCreatedCount)
                     THEN BEGIN
                       PageFAJnlLine.SETRANGE("Journal Template Name",FAJnlLine."Journal Template Name");
                       PageFAJnlLine.SETRANGE("Journal Batch Name",FAJnlLine."Journal Batch Name");
                       PageFAJnlLine.FINDFIRST;
                       PAGE.RUN(PAGE::"Fixed Asset Journal",PageFAJnlLine);
                     END;

                   IF GenJnlLineCreatedCount > 0 THEN
                     IF CONFIRM(CompletionStatsGenJnlQst,TRUE,GenJnlLineCreatedCount)
                     THEN BEGIN
                       PageGenJnlLine.SETRANGE("Journal Template Name",GenJnlLine."Journal Template Name");
                       PageGenJnlLine.SETRANGE("Journal Batch Name",GenJnlLine."Journal Batch Name");
                       PageGenJnlLine.FINDFIRST;
                       PAGE.RUN(PAGE::"Fixed Asset G/L Journal",PageGenJnlLine);
                     END;
                 END;

    UsageCategory=Tasks;
    AdditionalSearchTermsML=[DEU=Wert Anlagen teilweise entfernen;
                             ENU=write down fixed asset;
                             NLD=waarde van vast activum verminderen;
                             NOR=aktivanedskriving];
  }
  DATASET
  {
    { 3794;    ;DataItem;                    ;
               DataItemTable=Table5600;
               OnAfterGetRecord=BEGIN
                                  IF Inactive OR Blocked THEN
                                    CurrReport.SKIP;

                                  CalculateDepr.Calculate(
                                    DeprAmount,Custom1Amount,NumberOfDays,Custom1NumberOfDays,
                                    "No.",DeprBookCode,DeprUntilDate,EntryAmounts,0D,DaysInPeriod);
                                  IF (DeprAmount <> 0) OR (Custom1Amount <> 0) THEN
                                    Window.UPDATE(1,"No.")
                                  ELSE
                                    Window.UPDATE(2,"No.");

                                  OnAfterCalculateDepreciation(
                                    "No.",TempGenJnlLine,TempFAJnlLine,DeprAmount,NumberOfDays,DeprBookCode,DeprUntilDate,EntryAmounts,DaysInPeriod);

                                  IF Custom1Amount <> 0 THEN
                                    IF NOT DeprBook."G/L Integration - Custom 1" OR "Budgeted Asset" THEN BEGIN
                                      TempFAJnlLine."FA No." := "No.";
                                      TempFAJnlLine."FA Posting Type" := TempFAJnlLine."FA Posting Type"::"Custom 1";
                                      TempFAJnlLine.Amount := Custom1Amount;
                                      TempFAJnlLine."No. of Depreciation Days" := Custom1NumberOfDays;
                                      TempFAJnlLine."FA Error Entry No." := Custom1ErrorNo;
                                      TempFAJnlLine."Line No." := TempFAJnlLine."Line No." + 1;
                                      TempFAJnlLine.INSERT;
                                    END ELSE BEGIN
                                      TempGenJnlLine."Account No." := "No.";
                                      TempGenJnlLine."FA Posting Type" := TempGenJnlLine."FA Posting Type"::"Custom 1";
                                      TempGenJnlLine.Amount := Custom1Amount;
                                      TempGenJnlLine."No. of Depreciation Days" := Custom1NumberOfDays;
                                      TempGenJnlLine."FA Error Entry No." := Custom1ErrorNo;
                                      TempGenJnlLine."Line No." := TempGenJnlLine."Line No." + 1;
                                      TempGenJnlLine.INSERT;
                                    END;

                                  IF DeprAmount <> 0 THEN
                                    IF NOT DeprBook."G/L Integration - Depreciation" OR "Budgeted Asset" THEN BEGIN
                                      TempFAJnlLine."FA No." := "No.";
                                      TempFAJnlLine."FA Posting Type" := TempFAJnlLine."FA Posting Type"::Depreciation;
                                      TempFAJnlLine.Amount := DeprAmount;
                                      TempFAJnlLine."No. of Depreciation Days" := NumberOfDays;
                                      TempFAJnlLine."FA Error Entry No." := ErrorNo;
                                      TempFAJnlLine."Line No." := TempFAJnlLine."Line No." + 1;
                                      TempFAJnlLine.INSERT;
                                    END ELSE BEGIN
                                      TempGenJnlLine."Account No." := "No.";
                                      TempGenJnlLine."FA Posting Type" := TempGenJnlLine."FA Posting Type"::Depreciation;
                                      TempGenJnlLine.Amount := DeprAmount;
                                      TempGenJnlLine."No. of Depreciation Days" := NumberOfDays;
                                      TempGenJnlLine."FA Error Entry No." := ErrorNo;
                                      TempGenJnlLine."Line No." := TempGenJnlLine."Line No." + 1;
                                      TempGenJnlLine.INSERT;
                                    END;
                                END;

               OnPostDataItem=VAR
                                FAPlantRec@1100525000 : Record 5600;
                              BEGIN
                                WITH FAJnlLine DO BEGIN
                                  IF TempFAJnlLine.FIND('-') THEN BEGIN
                                    LOCKTABLE;
                                    FAJnlSetup.FAJnlName(DeprBook,FAJnlLine,FAJnlNextLineNo);
                                    NoSeries := FAJnlSetup.GetFANoSeries(FAJnlLine);
                                    IF DocumentNo = '' THEN
                                      DocumentNo2 := FAJnlSetup.GetFAJnlDocumentNo(FAJnlLine,DeprUntilDate,TRUE)
                                    ELSE
                                      DocumentNo2 := DocumentNo;
                                  END;
                                  IF TempFAJnlLine.FIND('-') THEN
                                    REPEAT
                                      INIT;
                                      "Line No." := 0;
                                      FAJnlSetup.SetFAJnlTrailCodes(FAJnlLine);
                                      LineNo := LineNo + 1;
                                      Window.UPDATE(3,LineNo);
                                      "Posting Date" := PostingDate;
                                      "FA Posting Date" := DeprUntilDate;
                                      IF "Posting Date" = "FA Posting Date" THEN
                                        "Posting Date" := 0D;
                                      "FA Posting Type" := TempFAJnlLine."FA Posting Type";
                                      VALIDATE("FA No.",TempFAJnlLine."FA No.");
                                      "Document No." := DocumentNo2;
                                      "Posting No. Series" := NoSeries;
                                      Description := PostingDescription;
                                      VALIDATE("Depreciation Book Code",DeprBookCode);
                                      VALIDATE(Amount,TempFAJnlLine.Amount);
                                      "No. of Depreciation Days" := TempFAJnlLine."No. of Depreciation Days";
                                      "FA Error Entry No." := TempFAJnlLine."FA Error Entry No.";
                                      FAJnlNextLineNo := FAJnlNextLineNo + 10000;
                                      "Line No." := FAJnlNextLineNo;
                                      OnBeforeFAJnlLineInsert(TempFAJnlLine,FAJnlLine);
                                      INSERT(TRUE);
                                      FAJnlLineCreatedCount += 1;
                                    UNTIL TempFAJnlLine.NEXT = 0;
                                END;

                                WITH GenJnlLine DO BEGIN
                                  IF TempGenJnlLine.FIND('-') THEN BEGIN
                                    LOCKTABLE;
                                    FAJnlSetup.GenJnlName(DeprBook,GenJnlLine,GenJnlNextLineNo);
                                    NoSeries := FAJnlSetup.GetGenNoSeries(GenJnlLine);
                                    IF DocumentNo = '' THEN
                                      DocumentNo2 := FAJnlSetup.GetGenJnlDocumentNo(GenJnlLine,DeprUntilDate,TRUE)
                                    ELSE
                                      DocumentNo2 := DocumentNo;
                                  END;
                                  IF TempGenJnlLine.FIND('-') THEN
                                    REPEAT
                                      INIT;
                                      "Line No." := 0;
                                      FAJnlSetup.SetGenJnlTrailCodes(GenJnlLine);
                                      LineNo := LineNo + 1;
                                      Window.UPDATE(3,LineNo);
                                      "System-Created Entry" := TRUE;  //**4PS.n
                                      "Posting Date" := PostingDate;
                                      "FA Posting Date" := DeprUntilDate;
                                      IF "Posting Date" = "FA Posting Date" THEN
                                        "FA Posting Date" := 0D;
                                      "FA Posting Type" := TempGenJnlLine."FA Posting Type";
                                      "Account Type" := "Account Type"::"Fixed Asset";
                                      VALIDATE("Account No.",TempGenJnlLine."Account No.");
                                      Description := PostingDescription;
                                      "Document No." := DocumentNo2;
                                      "Posting No. Series" := NoSeries;
                                      VALIDATE("Depreciation Book Code",DeprBookCode);
                                      VALIDATE(Amount,TempGenJnlLine.Amount);
                                      "No. of Depreciation Days" := TempGenJnlLine."No. of Depreciation Days";
                                      "FA Error Entry No." := TempGenJnlLine."FA Error Entry No.";
                                      GenJnlNextLineNo := GenJnlNextLineNo + 1000;
                                      "Line No." := GenJnlNextLineNo;

                                      //**4PS.sn
                                      IF (gBlnPlantIntegration) THEN BEGIN
                                        FAPlantRec."No." := TempGenJnlLine."Account No.";
                                        FAPlantRec.CALCFIELDS("Plant No.");
                                        IF FAPlantRec."Plant No." <> '' THEN
                                          "Cost Component Plant" := gRecPlantSetup."Cost Component Depreciation"
                                        ELSE BEGIN
                                          IF FAPlantRec.CalcFields_PlantMainAsset(FAPlantRec, FAPlantRec.FIELDNO("Plant No. (Main Asset)")) <> '' THEN
                                            "Cost Component Plant" := gRecPlantSetup."Cost Component Depreciation";
                                        END;
                                      END;
                                      //**4PS.en

                                      OnBeforeGenJnlLineInsert(TempGenJnlLine,GenJnlLine);
                                      INSERT(TRUE);
                                      GenJnlLineCreatedCount += 1;
                                      IF BalAccount THEN
                                        FAInsertGLAcc.GetBalAcc2(GenJnlLine,GenJnlNextLineNo);
                                    UNTIL TempGenJnlLine.NEXT = 0;
                                END;
                              END;

               ReqFilterFields=No.,FA Class Code,FA Subclass Code,Budgeted Asset }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   BalAccount := TRUE;
                   PostingDate := WORKDATE;
                   DeprUntilDate := WORKDATE;
                   IF DeprBookCode = '' THEN BEGIN
                     FASetup.GET;
                     DeprBookCode := FASetup."Default Depr. Book";
                   END;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 15  ;2   ;Field     ;
                  Name=DepreciationBook;
                  CaptionML=[DEU=AfA-Buch;
                             ENU=Depreciation Book;
                             NLD=Afschrijvingsboek;
                             NOR=AvskrivningstablÜ;
                             SVE=Avskrivningsregel];
                  ToolTipML=[DEU=Gibt den Code des AfA-Buchs an, das im Bericht oder in der Stapelverarbeitung berÅcksichtigt werden soll.;
                             ENU=Specifies the code for the depreciation book to be included in the report or batch job.;
                             NLD=Hiermee wordt de code opgegeven van het afschrijvingsboek dat in het rapport of de batchverwerking moet worden opgenomen.;
                             NOR=Angir koden for avskrivningstablÜet som skal vëre med i rapporten eller kjõrselen.;
                             SVE=Anger koden fîr den avskrivningsregel som ska tas med i rapporten eller batch-jobbet.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=DeprBookCode;
                  TableRelation="Depreciation Book" }

      { 9   ;2   ;Field     ;
                  Name=FAPostingDate;
                  CaptionML=[DEU=Anlagedatum;
                             ENU=FA Posting Date;
                             NLD=VA-boekingsdatum;
                             NOR=Aktivabokfõringsdato;
                             SVE=Anl. bokfîringsdatum];
                  ToolTipML=[DEU=Gibt das Anlagedatum an, das von der Stapelverarbeitung verwendet werden soll. Die Stapelverarbeitung umfasst Posten bis zu diesem Datum. Dieses Datum erscheint in den resultierenden Buch.-Blattzeilen im Feld "Anlagedatum". Wenn in dem fÅr die Stapelverarbeitung verwendeten AfA-Buch das Feld "Anl.+Fibu-Buchungsdat. gleich" aktiviert ist, muss dieses Datum dasselbe sein wie das Datum in dem Feld "Buchungsdatum".;
                             ENU=Specifies the fixed asset posting date to be used by the batch job. The batch job includes ledger entries up to this date. This date appears in the FA Posting Date field in the resulting journal lines. If the Use Same FA+G/L Posting Dates field has been activated in the depreciation book that is used in the batch job, then this date must be the same as the posting date entered in the Posting Date field.;
                             NLD=Hiermee wordt de VA-boekingsdatum opgegeven die door de batchverwerking moet worden gebruikt. De batchverwerking bevat posten tot deze datum. Deze datum verschijnt in het veld VA-boekingsdatum op de resulterende dagboekregels. Als het veld Zelfde VA/GB-boekingsdatum is geactiveerd in het afschrijvingsboek dat in deze batchverwerking wordt gebruikt, moet deze datum hetzelfde zijn als de boekingsdatum die is ingevoerd in het veld Boekingsdatum.;
                             NOR=Angir aktivabokfõringsdatoen som skal brukes av kjõrselen. Kjõrselen tar med poster frem til denne datoen. Denne datoen vises i feltet Aktivabokf.dato pÜ kladdelinjene. Hvis feltet Bruk lik akt.+finansbokf.dato er aktivert i avskrivningstablÜet som brukes i kjõrselen, mÜ denne datoen vëre den samme som bokfõringsdatoen som er angitt i feltet Bokfõringsdato.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=DeprUntilDate;
                  Importance=Additional;
                  OnValidate=BEGIN
                               DeprUntilDateModified := TRUE;
                             END;
                              }

      { 8   ;2   ;Field     ;
                  Name=UseForceNoOfDays;
                  CaptionML=[DEU=Feste Anzahl Tage verwenden;
                             ENU=Use Force No. of Days;
                             NLD=Vast aantal dagen gebruiken;
                             NOR=Bruk tvunget antall dager];
                  ToolTipML=[DEU=Gibt an, ob die Anwendung die Anzahl der Tage, die Sie in dem nÑchsten Feld eingegeben haben, fÅr die Abschreibungsberechnung verwenden soll.;
                             ENU=Specifies if you want the program to use the number of days, as specified in the field below, in the depreciation calculation.;
                             NLD=Hiermee wordt opgegeven of u wilt dat het programma het aantal dagen dat in het onderstaande veld is opgegeven, gebruikt bij het berekenen van de afschrijving.;
                             NOR=Angir om du vil at programmet skal bruke antall dager som angitt i feltet nedenfor, i beregningen av avskrivning.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=UseForceNoOfDays;
                  Importance=Additional;
                  OnValidate=BEGIN
                               IF NOT UseForceNoOfDays THEN
                                 DaysInPeriod := 0;
                             END;
                              }

      { 5   ;2   ;Field     ;
                  Name=ForceNoOfDays;
                  CaptionML=[DEU=Feste Anzahl Tage;
                             ENU=Force No. of Days;
                             NLD=Vast aantal dagen;
                             NOR=Tvunget antall dager];
                  ToolTipML=[DEU=Gibt an, ob die Anwendung die Anzahl der Tage, die Sie in dem nÑchsten Feld eingegeben haben, fÅr die Abschreibungsberechnung verwenden soll.;
                             ENU=Specifies if you want the program to use the number of days, as specified in the field below, in the depreciation calculation.;
                             NLD=Hiermee wordt opgegeven of u wilt dat het programma het aantal dagen dat in het onderstaande veld is opgegeven, gebruikt bij het berekenen van de afschrijving.;
                             NOR=Angir om du vil at programmet skal bruke antall dager som angitt i feltet nedenfor, i beregningen av avskrivning.];
                  ApplicationArea=#FixedAssets;
                  BlankZero=Yes;
                  SourceExpr=DaysInPeriod;
                  MinValue=0;
                  Importance=Additional;
                  OnValidate=BEGIN
                               IF NOT UseForceNoOfDays AND (DaysInPeriod <> 0) THEN
                                 ERROR(Text006);
                             END;
                              }

      { 11  ;2   ;Field     ;
                  Name=PostingDate;
                  CaptionML=[DEU=Buchungsdatum;
                             ENU=Posting Date;
                             NLD=Boekingsdatum;
                             NOR=Bokfõringsdato;
                             SVE=Bokfîringsdatum];
                  ToolTipML=[DEU=Gibt das von der Stapelverarbeitung zu verwendende Buchungsdatum an.;
                             ENU=Specifies the posting date to be used by the batch job.;
                             NLD=Hiermee wordt de boekingsdatum opgegeven die door de batchverwerking moet worden gebruikt.;
                             NOR=Angir bokfõringsdatoen som skal brukes av kjõrselen.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=PostingDate;
                  OnValidate=BEGIN
                               IF NOT DeprUntilDateModified THEN
                                 DeprUntilDate := PostingDate;
                             END;
                              }

      { 1   ;2   ;Field     ;
                  Name=DocumentNo;
                  CaptionML=[DEU=Belegnr.;
                             ENU=Document No.;
                             NLD=Documentnr.;
                             NOR=Bilagsnr.;
                             SVE=Dokumentnr];
                  ToolTipML=[DEU=Gibt, wenn das Feld leer ist, die nÑchste verfÅgbare Nummer in der resultierenden Buch.-Blattzeile an. Wenn keine Nummernserie eingerichtet wurde, geben Sie die Belegnummer ein, die der resultierenden Buch.-Blattzeile zugewiesen werden soll.;
                             ENU=Specifies, if you leave the field empty, the next available number on the resulting journal line. If a number series is not set up, enter the document number that you want assigned to the resulting journal line.;
                             NLD=Hiermee wordt, als u het veld leeg laat, het volgende beschikbare nummer op de resulterende dagboekregel opgegeven. Als geen nummerreeks is ingesteld, voert u het documentnummer in dat u wilt toewijzen aan de resulterende dagboekregel.;
                             NOR=Angir, hvis du lar feltet vëre tomt, det neste tilgjengelige nummeret for den resulterende kladdelinjen. Hvis en nummer serie ikke er definert, angir du bilagsnummeret som du vil tilordne til den resulterende kladdelinjen.;
                             SVE=Om du inte anger nÜgot i fÑltet anges nÑsta lediga nummer pÜ journalraden. Om ingen nummerserie har konfigurerats anger du det dokumentnummer som du vill tilldela journalraden.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=DocumentNo }

      { 13  ;2   ;Field     ;
                  Name=PostingDescription;
                  CaptionML=[DEU=Buchungsbeschreibung;
                             ENU=Posting Description;
                             NLD=Boekingsomschrijving;
                             NOR=Bokfõringsbeskrivelse;
                             SVE=Bokfîringsbeskrivning];
                  ToolTipML=[DEU=Gibt das Buchungsdatum an, das von der Stapelverarbeitung als Filter verwendet werden soll.;
                             ENU=Specifies the posting date to be used by the batch job as a filter.;
                             NLD=Hiermee wordt de boekingsdatum opgegeven die door de batchverwerking als filter moet worden gebruikt.;
                             NOR=Angir bokfõringsdatoen som skal brukes av kjõrselen som et filter.;
                             SVE=Anger bokfîringsdatumet som ska anvÑndas i batch-jobbet som ett filter.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=PostingDescription }

      { 3   ;2   ;Field     ;
                  Name=InsertBalAccount;
                  CaptionML=[DEU=Gegenkonto einfÅgen;
                             ENU=Insert Bal. Account;
                             NLD=Tegenrekening invoegen;
                             NOR=Sett inn motkonto;
                             SVE=Infoga motkonto];
                  ToolTipML=[DEU=Gibt an, ob die Stapelverarbeitung automatisch Gegenkonten in die Anlagenposten einfÅgen soll.;
                             ENU=Specifies if you want the batch job to automatically insert fixed asset entries with balancing accounts.;
                             NLD=Hiermee wordt opgegeven of u wilt dat tijdens de batchverwerking automatisch VA-posten met tegenrekeningen worden ingevoegd.;
                             NOR=Angir om du vil at kjõrselen automatisk skal sette inn aktivaposter med motkonti.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=BalAccount;
                  Importance=Additional }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Sie mÅssen %1 angeben.;ENU=You must specify %1.;NLD=U moet %1 opgeven.;NOR=Du mÜ angi %1.;SVE=Du mÜste specificera %1.';
      Text001@1001 : TextConst 'DEU=Feste Anzahl Tage muss aktiviert sein.;ENU=Force No. of Days must be activated.;NLD=Verplicht aantal dagen moet geactiveerd zijn.;NOR=Tvunget antall dager mÜ aktiveres.';
      Text002@1002 : TextConst 'DEU="%1 und %2 mÅssen identisch sein. %3 muss %4 sein in %5 %6 = %7.";ENU="%1 and %2 must be identical. %3 must be %4 in %5 %6 = %7.";NLD="%1 en %2 moeten gelijk zijn. %3 moet %4 zijn in %5 %6 = %7.";NOR="%1 og %2 mÜ vëre like. %3 mÜ vëre %4 i %5 %6 = %7."';
      Text003@1003 : TextConst 'DEU=AfA Anlage                 #1##########\;ENU=Depreciating fixed asset      #1##########\;NLD=Afschrijven VA                #1##########\;NOR=Avskriver aktiva              #1##########\';
      Text004@1004 : TextConst 'DEU=Keine AfA von Anlage       #2##########\;ENU=Not depreciating fixed asset  #2##########\;NLD=Niet afschrijven VA           #2##########\;NOR=Avskriver ikke aktiva         #2##########\';
      Text005@1005 : TextConst 'DEU=Buch.-Bl.-Zeilen einfÅgen  #3##########;ENU=Inserting journal lines       #3##########;NLD=Invoegen dagboekregels        #3##########;NOR=Setter inn kladdelinjer       #3##########';
      Text006@1006 : TextConst 'DEU=Feste Anzahl Tage verwenden muss aktiviert sein.;ENU=Use Force No. of Days must be activated.;NLD=Verpl. aantal dagen gebruiken moet geactiveerd zijn.;NOR=Bruk tvunget antall dager mÜ aktiveres.';
      GenJnlLine@1007 : Record 81;
      TempGenJnlLine@1008 : TEMPORARY Record 81;
      FASetup@1009 : Record 5603;
      FAJnlLine@1010 : Record 5621;
      TempFAJnlLine@1011 : TEMPORARY Record 5621;
      DeprBook@1012 : Record 5611;
      FAJnlSetup@1013 : Record 5605;
      CalculateDepr@1014 : Codeunit 5610;
      FAInsertGLAcc@1015 : Codeunit 5601;
      Window@1016 : Dialog;
      DeprAmount@1017 : Decimal;
      Custom1Amount@1018 : Decimal;
      NumberOfDays@1019 : Integer;
      Custom1NumberOfDays@1020 : Integer;
      DeprUntilDate@1021 : Date;
      UseForceNoOfDays@1022 : Boolean;
      DaysInPeriod@1023 : Integer;
      PostingDate@1024 : Date;
      DocumentNo@1025 : Code[20];
      DocumentNo2@1026 : Code[20];
      NoSeries@1027 : Code[20];
      PostingDescription@1028 : Text[100];
      DeprBookCode@1029 : Code[10];
      BalAccount@1030 : Boolean;
      ErrorNo@1031 : Integer;
      Custom1ErrorNo@1032 : Integer;
      FAJnlNextLineNo@1033 : Integer;
      GenJnlNextLineNo@1034 : Integer;
      EntryAmounts@1035 : ARRAY [4] OF Decimal;
      LineNo@1036 : Integer;
      CompletionStatsMsg@1037 : TextConst 'DEU=Die Abschreibung wurde berechnet.\\Keine Buch.-Blattzeilen wurden erstellt.;ENU=The depreciation has been calculated.\\No journal lines were created.;NLD=De afschrijving is berekend.\\Er zijn geen dagboekregels gemaakt.;NOR=Avskrivningen er beregnet.\\Ingen kladdelinjer er opprettet.';
      FAJnlLineCreatedCount@1038 : Integer;
      GenJnlLineCreatedCount@1039 : Integer;
      CompletionStatsFAJnlQst@1042 : TextConst '@@@=The depreciation has been calculated.\\5 fixed asset journal lines were created.\\Do you want to open the Fixed Asset Journal window?;DEU=Die Abschreibung wurde berechnet.\\%1 Anl. Buch.-Blattzeilen wurden erstellt.\\Mîchten Sie das Anlagen Buch.-Blatt-Fenster îffnen?;ENU=The depreciation has been calculated.\\%1 fixed asset journal lines were created.\\Do you want to open the Fixed Asset Journal window?;NLD=De afschrijving is berekend.\\Er zijn %1 VA-dagboekregels gemaakt.\\Wilt u het venster VA-dagboek openen?;NOR=Avskrivningen er beregnet.\\%1 aktivakladdelinjer er opprettet.\\Vil du Üpne vinduet for aktivakladd?';
      CompletionStatsGenJnlQst@1043 : TextConst '@@@=The depreciation has been calculated.\\2 fixed asset G/L  journal lines were created.\\Do you want to open the Fixed Asset G/L Journal window?;DEU=Die Abschreibung wurde berechnet.\\%1 Anlagen-Fibu Buch.-Blattzeilen wurden erstellt.\\Mîchten Sie das Anlagen-Fibu Buch.-Blatt-Fenster îffnen?;ENU=The depreciation has been calculated.\\%1 fixed asset G/L journal lines were created.\\Do you want to open the Fixed Asset G/L Journal window?;NLD=De afschrijving is berekend.\\Er zijn %1 VA-grootboekregels gemaakt.\\Wilt u het venster VA-grootboek openen?;NOR=Avskrivningen er beregnet.\\%1 linjer i aktivafinanskladden er opprettet.\\Vil du Üpne vinduet for aktivafinanskladden?';
      DeprUntilDateModified@1040 : Boolean;
      gRecPlantSetup@1100525000 : Record 11012550;
      gBlnPlantIntegration@1100525001 : Boolean;
      Text11012001@1100525002 : TextConst 'DEU=''%1'' Restwert Werkzeug wurde noch nicht gebucht;ENU=''%1'' Salvage Value Plant not posted yet;NLD=''%1'' restwaarde materieel nog niet geboekt';

    [External]
    PROCEDURE InitializeRequest@1(DeprBookCodeFrom@1007 : Code[10];DeprUntilDateFrom@1000 : Date;UseForceNoOfDaysFrom@1001 : Boolean;DaysInPeriodFrom@1002 : Integer;PostingDateFrom@1003 : Date;DocumentNoFrom@1004 : Code[20];PostingDescriptionFrom@1005 : Text[100];BalAccountFrom@1006 : Boolean);
    BEGIN
      DeprBookCode := DeprBookCodeFrom;
      DeprUntilDate := DeprUntilDateFrom;
      UseForceNoOfDays := UseForceNoOfDaysFrom;
      DaysInPeriod := DaysInPeriodFrom;
      PostingDate := PostingDateFrom;
      DocumentNo := DocumentNoFrom;
      PostingDescription := PostingDescriptionFrom;
      BalAccount := BalAccountFrom;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCalculateDepreciation@3(FANo@1000 : Code[20];VAR TempGenJournalLine@1001 : TEMPORARY Record 81;VAR TempFAJournalLine@1002 : TEMPORARY Record 5621;VAR DeprAmount@1008 : Decimal;VAR NumberOfDays@1007 : Integer;DeprBookCode@1004 : Code[10];DeprUntilDate@1006 : Date;EntryAmounts@1003 : ARRAY [4] OF Decimal;DaysInPeriod@1005 : Integer);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeFAJnlLineInsert@2(VAR TempFAJournalLine@1000 : TEMPORARY Record 5621;VAR FAJournalLine@1001 : Record 5621);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeGenJnlLineInsert@4(VAR TempGenJournalLine@1000 : TEMPORARY Record 81;VAR GenJournalLine@1001 : Record 81);
    BEGIN
    END;

    PROCEDURE CheckNotPostedPlantSalvage@1100525000();
    VAR
      FA@1100525000 : Record 5600;
      FAJnlLine@1100525001 : Record 5621;
    BEGIN
      FA.COPY("Fixed Asset");
      FA.SETRANGE(Inactive, FALSE);  //* In 'OnAfterGetRecord' of "Fixed Asset"
      FA.SETRANGE(Blocked, FALSE);   //* Inactive and Blocked are skiped
      IF FA.FINDSET THEN BEGIN
        REPEAT
          FA.CALCFIELDS("Plant No.");
          IF FA."Plant No." <> '' THEN BEGIN
            FAJnlLine.SETRANGE("FA No.", FA."No.");
            FAJnlLine.SETRANGE("Depreciation Book Code", DeprBookCode);
            FAJnlLine.SETRANGE("FA Posting Type", FAJnlLine."FA Posting Type"::"Salvage Value");
            IF FAJnlLine.FINDFIRST THEN
              FAJnlLine.FIELDERROR("FA No.", STRSUBSTNO(Text11012001, FAJnlLine."FA No."));
          END;
        UNTIL FA.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

