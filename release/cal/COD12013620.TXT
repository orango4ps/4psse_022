OBJECT Codeunit 12013620 ExFlow Copy Ex Links Mgt
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF500000;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE CopyEXLinks@1100285000(ExPurchDocHeader@1100285000 : Record 12013587;ToExDocHead@1100285001 : Record 12013608);
    VAR
      ExFlowSetup@1100285002 : Record 12013601;
    BEGIN
      ExFlowSetup.GET;

      IF ExFlowSetup."Copy Purchas Order Attachments" THEN
        CopyPOLinks(ExPurchDocHeader,ToExDocHead);

      CopyContractLinks(ExPurchDocHeader,ToExDocHead);
    END;

    PROCEDURE CopyPOLinks@1100285002(ExPurchDocHeader@1100285002 : Record 12013587;ToExDocHead@1100285000 : Record 12013608);
    VAR
      PurchaseHeader@1100285001 : Record 38;
      ExDocPurchLine@1100285003 : Record 12013588;
      TempPurchHeader@1100285004 : TEMPORARY Record 38;
      TempDocumentType@1100285005 : Integer;
    BEGIN
      ExDocPurchLine.RESET;
      ExDocPurchLine.SETRANGE("Inbound Document No.", ExPurchDocHeader."Inbound Document No.");
      ExDocPurchLine.SETFILTER("Order No.", '<>%1', '');
      IF ExDocPurchLine.FINDSET THEN
        REPEAT
          CASE ExPurchDocHeader."ExFlow Document Type" OF
            ExPurchDocHeader."ExFlow Document Type"::Invoice:
              TempDocumentType := TempPurchHeader."Document Type"::Order;
            ExPurchDocHeader."ExFlow Document Type"::"Credit Memo":
              TempDocumentType := TempPurchHeader."Document Type"::"Return Order";
          END;
          IF NOT TempPurchHeader.GET(TempDocumentType,ExDocPurchLine."Order No.") THEN BEGIN
            TempPurchHeader."Document Type" := TempDocumentType;
            TempPurchHeader."No." := ExDocPurchLine."Order No.";
            TempPurchHeader.INSERT;
          END;
        UNTIL ExDocPurchLine.NEXT = 0;

      TempPurchHeader.RESET;
      IF TempPurchHeader.FINDSET THEN
        REPEAT
          IF PurchaseHeader.GET(TempPurchHeader."Document Type",TempPurchHeader."No.") THEN
            InsertPOLinks(PurchaseHeader,ToExDocHead);
        UNTIL TempPurchHeader.NEXT = 0;
    END;

    LOCAL PROCEDURE InsertPOLinks@1100285004(PurchaseHeader@1100285001 : Record 38;ToExDocHead@1100285004 : Record 12013608);
    VAR
      ExDoc2@1100285002 : Record 12013612;
      ExAttachment@1100285003 : Record 12013643;
      TempBLob@1100285000 : Record 99008535;
    BEGIN
      IF ExDoc2.RetrieveRecord(ExDoc2,PurchaseHeader."Document Type",PurchaseHeader."No.") THEN BEGIN
        IF ExDoc2."Image Blob".HASVALUE THEN BEGIN
          ExDoc2.CALCFIELDS("Image Blob");
          TempBLob.Blob := ExDoc2."Image Blob";
          AddDocumentAttachment(ToExDocHead,ExDoc2."Image Name",TempBLob);
        END;
      END;

      ExAttachment.RESET;
      ExAttachment.SETRANGE("Document Type",PurchaseHeader."Document Type");
      ExAttachment.SETRANGE("Document No.",PurchaseHeader."No.");
      IF ExAttachment.FINDSET THEN
        REPEAT
          IF ExAttachment."Attachment Blob".HASVALUE THEN BEGIN
            ExDoc2.CALCFIELDS("Image Blob");
            TempBLob.Blob := ExDoc2."Image Blob";
            AddDocumentAttachment(ToExDocHead,ExDoc2."Image Name",TempBLob);
          END;
        UNTIL ExAttachment.NEXT = 0;
    END;

    PROCEDURE CopyContractLinks@1100285005(ExPurchDocHeader@1100285002 : Record 12013587;ToExDocHead@1100285000 : Record 12013608);
    VAR
      ExFlowContract@1100285001 : Record 12013633;
      TempBlob@1100285003 : Record 99008535;
    BEGIN
      IF ExPurchDocHeader."Contract No." = 0 THEN
        EXIT;

      IF NOT ExFlowContract.GET(ExPurchDocHeader."Contract No.") THEN
        EXIT;

      IF NOT ExFlowContract."Copy Attached File To Invoice" THEN
        EXIT;

      IF NOT ExFlowContract."Attachment Blob".HASVALUE THEN
        EXIT;

      ExFlowContract.CALCFIELDS("Attachment Blob");
      TempBlob.Blob := ExFlowContract."Attachment Blob";
      AddDocumentAttachment(ToExDocHead,ExFlowContract."Attached File",TempBlob);
    END;

    LOCAL PROCEDURE AddDocumentAttachment@1100285040(ToExDocHead@1100285009 : Record 12013608;fileName@1100285001 : Text[250];TempBlob@1100285000 : Record 99008535);
    VAR
      ExDoc@1100285003 : Record 12013608;
      ExAttachment@1100285004 : Record 12013643;
      ExFlow@1100285002 : Codeunit 12013601;
      ExUserCompany@1100285005 : Record 12013641;
      ExUserID@1100285007 : Text[100];
    BEGIN
      IF ExFlow.GetUserComp(ExUserCompany,USERID) THEN
        ExUserID := ExUserCompany."User ID"
      ELSE
        ExUserID := USERID;

      ExAttachment.INIT;
      ExAttachment."Entry No." := 0;
      ExAttachment."Attached File" := fileName;
      ExAttachment."Attachment Blob" := TempBlob.Blob;
      ExAttachment."Document Type" := ToExDocHead."Document Type";
      ExAttachment."Document No." := ToExDocHead."Document No.";
      ExAttachment.Attached := TRUE;
      ExAttachment."Document ID" := ToExDocHead.ID;
      ExAttachment."Web ID" := ToExDocHead."Web ID";
      ExAttachment."Created By" := ExUserID;
      ExAttachment."Created Date" := CREATEDATETIME(TODAY,TIME);
      ExAttachment.Source := ExAttachment.Source::NAV;
      ExAttachment.INSERT;
    END;

    BEGIN
    END.
  }
}

