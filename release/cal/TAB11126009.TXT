OBJECT Table 11126009 Accident - Selected Option
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Accident - Selected Option;
               NOR=ulykke - valgt alternativ;
               SVE=Olycksfall Õ valt alternativ];
  }
  FIELDS
  {
    { 10  ;   ;Accident No.        ;Code20        ;TableRelation="Accident QA";
                                                   CaptionML=[ENU=Accident No.;
                                                              NOR=ulykke nummer;
                                                              SVE=Olycksfallsnr] }
    { 20  ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Type;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=[ENU=Accident ReportedTo,Place Injury,Injury Type,Absence Type,Circumstances,Action,Organisation;
                                                                    NOR=ulykke rapportert til,Üsted,type skade,type fravër,omstendigheter,handling,organisering;
                                                                    SVE=Olycksfallet har rapporterats till,Platsskada,Skadetyp,FrÜnvarotyp,OmstÑndigheter,ètgÑrd,Organisation];
                                                   OptionString=AccReportedTo,PlaceInjury,InjuryType,AbsenceType,Circumstances,Action,Organisation;
                                                   Description=NOTE: Options must be the same as in T11126008 }
    { 30  ;   ;Option Code         ;Code10        ;TableRelation="Accident - Multi Option Value".Code WHERE (Type=FIELD(Type));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS(Description);
                                                              END;

                                                   CaptionML=[ENU=Option Code;
                                                              NOR=Alternativkode;
                                                              SVE=Alternativkod] }
    { 40  ;   ;Description         ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Accident - Multi Option Value"."Option Value" WHERE (Type=FIELD(Type),
                                                                                                                            Code=FIELD(Option Code)));
                                                   OnValidate=BEGIN
                                                                LookupOptionCode(FALSE);
                                                              END;

                                                   CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Accident No.,Type,Option Code           ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100525000 : TextConst 'ENU=First select line(s).;NOR=Velg rad(er) fõrst.;SVE=VÑlj fîrst rad(er).';

    PROCEDURE RunFormSelectOptions@1100525006(AccidentNo@1100525003 : Code[20];OptionType@1100525000 : Option);
    VAR
      AccSelectedOption@1100525005 : Record 11126009;
    BEGIN
      IF AccidentNo = '' THEN
        EXIT;
      AccSelectedOption.FILTERGROUP(2);
      AccSelectedOption.SETRANGE("Accident No.", AccidentNo);
      AccSelectedOption.SETRANGE(Type, OptionType);
      AccSelectedOption.FILTERGROUP(0);
      PAGE.RUNMODAL(PAGE::"Accident - Selected Options", AccSelectedOption);
    END;

    PROCEDURE RunFormMultiSelectOptions@1100525002(AccidentNo@1100525003 : Code[20];OptionType@1100525000 : Option);
    VAR
      Accident@1100525004 : Record 11126005;
      MultiOptionSelection@1100525001 : Page 11126012;
      EditableMode@1100525002 : Boolean;
    BEGIN
      IF AccidentNo = '' THEN
        EXIT;
      IF Accident.GET(AccidentNo) THEN BEGIN
        IF Accident.Status < Accident.Status::Finished THEN
          EditableMode := TRUE;
      END;
      MultiOptionSelection.SetSelections(AccidentNo,OptionType,EditableMode);
      MultiOptionSelection.RUNMODAL;
    END;

    PROCEDURE GetSelectedOptionsText@1100525005(AccidentNo@1100525000 : Code[20];OptionType@1100525002 : Option;MaxLen@1100525003 : Integer) SelectedOptions : Text[1024];
    VAR
      AccSelectedOption@1100525004 : Record 11126009;
      Separator@1100525005 : Text[3];
    BEGIN
      IF AccidentNo = '' THEN
        EXIT('');
      SelectedOptions := '';
      Separator := '';
      IF (MaxLen = 0) OR (MaxLen > MAXSTRLEN(SelectedOptions)) THEN
        MaxLen := MAXSTRLEN(SelectedOptions);

      AccSelectedOption.SETRANGE("Accident No.", AccidentNo);
      AccSelectedOption.SETRANGE(Type, OptionType);
      IF AccSelectedOption.FINDSET THEN BEGIN
        REPEAT
          AccSelectedOption.CALCFIELDS(Description);
          IF STRLEN(SelectedOptions) + STRLEN(Separator) + STRLEN(AccSelectedOption.Description) <= MaxLen THEN
            SelectedOptions := SelectedOptions + Separator + AccSelectedOption.Description
          ELSE BEGIN
            IF STRLEN(SelectedOptions) <= (MaxLen - 3) THEN
              SelectedOptions := SelectedOptions + '...'
            ELSE
              SelectedOptions := COPYSTR(SelectedOptions, 1, MaxLen - 3) + '...';
            EXIT;
          END;
          Separator := ' / ';
        UNTIL AccSelectedOption.NEXT = 0;
      END;
    END;

    PROCEDURE LookupOptionCode@1100525000(MultiCopy@1100525000 : Boolean);
    VAR
      Accident@1100525003 : Record 11126005;
      MultiOptionValue@1100525001 : Record 11126008;
      MultiOptionValueList@1100525002 : Page 11126010;
      EditableMode@1100525004 : Boolean;
    BEGIN
      IF ("Accident No." <> '') AND Accident.GET("Accident No.") THEN BEGIN
        IF Accident.Status < Accident.Status::Finished THEN
          EditableMode := TRUE;
      END;

      MultiOptionValue.FILTERGROUP(2);
      MultiOptionValue.SETRANGE(Type, Type);
      MultiOptionValue.FILTERGROUP(0);
      MultiOptionValue.Type := Type;
      MultiOptionValue.Code := "Option Code";
      MultiOptionValueList.SETTABLEVIEW(MultiOptionValue);
      MultiOptionValueList.SETRECORD(MultiOptionValue);
      IF MultiCopy AND EditableMode THEN
        MultiOptionValueList.SetMultiCopy("Accident No.");
      IF NOT EditableMode THEN
        MultiOptionValueList.RUNMODAL
      ELSE BEGIN
        MultiOptionValueList.LOOKUPMODE(TRUE);
        IF MultiOptionValueList.RUNMODAL = ACTION::LookupOK THEN BEGIN
          MultiOptionValueList.GETRECORD(MultiOptionValue);
          IF MultiOptionValue.Code <> '' THEN
            VALIDATE("Option Code", MultiOptionValue.Code);
        END;
      END;
    END;

    PROCEDURE CopyMarkedOptionsToAccident@1100525001(VAR MultiOptionValue@1100525001 : Record 11126008;AccidentNo@1100525000 : Code[20]) : Boolean;
    VAR
      MarkedMultiOptionValue@1100525002 : Record 11126008;
      AccSelectedOption@1100525003 : Record 11126009;
    BEGIN
      MarkedMultiOptionValue.COPY(MultiOptionValue);
      IF NOT MarkedMultiOptionValue.FINDSET THEN BEGIN
        MESSAGE(Text000);
        EXIT(FALSE);
      END ELSE BEGIN
        REPEAT
          IF NOT AccSelectedOption.GET(AccidentNo, MarkedMultiOptionValue.Type, MarkedMultiOptionValue.Code) THEN BEGIN
            AccSelectedOption.INIT;
            AccSelectedOption."Accident No." := AccidentNo;
            AccSelectedOption.Type := MarkedMultiOptionValue.Type;
            AccSelectedOption."Option Code" := MarkedMultiOptionValue.Code;
            AccSelectedOption.INSERT;
          END;
        UNTIL MarkedMultiOptionValue.NEXT = 0;
      END;
      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

