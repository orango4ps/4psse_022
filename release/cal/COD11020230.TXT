OBJECT Codeunit 11020230 Update Signhost Data
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            UpdateDataOfSignhostDocuments;
          END;

  }
  CODE
  {

    PROCEDURE UpdateDataOfSignhostDocumentsOfRecID@1100528401(RecordID@1100528400 : RecordID);
    VAR
      SignhostDocument@1100528401 : Record 11229468;
    BEGIN
      SignhostDocument.SETRANGE("Record ID", RecordID);
      IF SignhostDocument.FINDSET THEN
        REPEAT
          UpdateDataOfSignhostDocument(SignhostDocument, TRUE);
        UNTIL SignhostDocument.NEXT = 0;
    END;

    PROCEDURE UpdateDataOfSignhostDocumentOfRecIDAndWordDoc@1100528400(RecordID@1100528400 : RecordID;WordDocumentNo@1100528402 : Code[20]);
    VAR
      SignhostDocument@1100528401 : Record 11229468;
    BEGIN
      SignhostDocument.SETRANGE("Record ID", RecordID);
      SignhostDocument.SETRANGE("Sent Word Document No.", WordDocumentNo);
      IF SignhostDocument.FINDFIRST THEN
        UpdateDataOfSignhostDocument(SignhostDocument, TRUE);
    END;

    LOCAL PROCEDURE UpdateDataOfSignhostDocuments@1100528404();
    VAR
      SignhostDocument@1100528401 : Record 11229468;
    BEGIN
      SignhostDocument.SETRANGE("Signing Process Finished", FALSE);
      SignhostDocument.SETRANGE(Ignore, FALSE);
      IF SignhostDocument.FINDSET THEN
        REPEAT
          UpdateDataOfSignhostDocument(SignhostDocument, FALSE);
        UNTIL SignhostDocument.NEXT = 0;
    END;

    LOCAL PROCEDURE UpdateDataOfSignhostDocument@1100528405(SignhostDocument@1100528400 : Record 11229468;ThrowErrorMessage@1100528402 : Boolean);
    VAR
      SignhostManagement@1100528401 : Codeunit 11012637;
      ErrorMessage@1100528403 : Text;
    BEGIN
      IF NOT SignhostManagement.RUN(SignhostDocument) THEN BEGIN
        SignhostDocument.SETRANGE("Record ID", SignhostDocument."Record ID");
        SignhostDocument.SETRANGE("Sent Word Document No.", SignhostDocument."Sent Word Document No.");
        IF SignhostDocument.FINDFIRST THEN BEGIN
          ErrorMessage := GETLASTERRORTEXT;
          SignhostDocument."Error Message" := COPYSTR(
            ErrorMessage, 1, MAXSTRLEN(SignhostDocument."Error Message"));
          SignhostDocument.MODIFY;
          IF ThrowErrorMessage THEN BEGIN
            COMMIT;
            ERROR(ErrorMessage);
          END;
        END;
      END ELSE
        IF SignhostDocument."Error Message" <> '' THEN BEGIN
          SignhostDocument.SETRANGE("Record ID", SignhostDocument."Record ID");
          SignhostDocument.SETRANGE("Sent Word Document No.", SignhostDocument."Sent Word Document No.");
          IF SignhostDocument.FINDFIRST THEN BEGIN
            SignhostDocument."Error Message" := '';
            SignhostDocument.MODIFY;
          END;
        END;
      COMMIT;
    END;

    BEGIN
    END.
  }
}

