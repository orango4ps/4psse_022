OBJECT Table 5104 Segment Interaction Language
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               Cont@1003 : Record 5050;
               SegHeader@1002 : Record 11072039;
               SegLine@1001 : Record 11072040;
               SegInteractLanguage@1000 : Record 5104;
               FirstSegIntLanguage@1004 : Boolean;
             BEGIN
               IF "Segment Line No." = 0 THEN BEGIN
                 SegInteractLanguage.SETRANGE("Segment No.","Segment No.");
                 SegInteractLanguage.SETRANGE("Segment Line No.",0);
                 FirstSegIntLanguage := NOT SegInteractLanguage.FINDFIRST;

                 SegLine.SETRANGE("Segment No.","Segment No.");
                 SegHeader.GET("Segment No.");
                 SegLine.SETRANGE("Interaction Template Code",SegHeader."Interaction Template Code");
                 IF SegLine.FIND('-') THEN
                   REPEAT
                     IF FirstSegIntLanguage THEN BEGIN
                       IF SegLine.AttachmentInherited OR
                          (SegLine."Attachment No." = 0)
                       THEN BEGIN
                         SegLine."Language Code" := "Language Code";
                         SegLine.Subject := Subject;
                         SegLine."Attachment No." := "Attachment No.";
                         SegLine.MODIFY;
                       END
                     END ELSE
                       IF SegLine.AttachmentInherited OR (SegLine."Attachment No." = 0) THEN
                         IF Cont.GET(SegLine."Contact No.") THEN
                           IF Cont."Language Code" = "Language Code" THEN BEGIN
                             SegLine."Language Code" := "Language Code";
                             SegLine.Subject := Subject;
                             SegLine."Attachment No." := "Attachment No.";
                             SegLine.MODIFY;
                           END;
                   UNTIL SegLine.NEXT = 0;
               END ELSE BEGIN
                 SegLine.GET("Segment No.","Segment Line No.");
                 SegLine."Language Code" := "Language Code";
                 SegLine.Subject := Subject;
                 SegLine."Attachment No." := "Attachment No.";
                 SegLine.MODIFY;
               END;
             END;

    OnDelete=VAR
               SegHeader@1000 : Record 11072039;
               SegLine@1001 : Record 11072040;
               Cont@1002 : Record 5050;
               SegInteractLanguage@1003 : Record 5104;
             BEGIN
               SegHeader.GET("Segment No.");
               IF ("Segment Line No." = 0) AND
                  ("Language Code" = SegHeader."Language Code (Default)")
               THEN BEGIN
                 SegInteractLanguage.SETRANGE("Segment No.","Segment No.");
                 SegInteractLanguage.SETRANGE("Segment Line No.",0);
                 SegInteractLanguage.SETFILTER("Language Code",'<>%1',"Language Code");
                 IF SegInteractLanguage.FINDFIRST THEN BEGIN
                   SegHeader."Language Code (Default)" := SegInteractLanguage."Language Code";
                   SegHeader."Subject (Default)" := SegInteractLanguage.Subject;
                 END ELSE BEGIN
                   SegHeader."Language Code (Default)" := '';
                   SegHeader."Subject (Default)" := '';
                 END;
                 SegHeader.MODIFY;
               END;
               SegHeader.CALCFIELDS("Attachment No.");

               IF "Segment Line No." = 0 THEN BEGIN
                 SegLine.SETRANGE("Segment No.","Segment No.");
                 SegLine.SETRANGE("Attachment No.","Attachment No.");
                 IF SegLine.FIND('-') THEN
                   REPEAT
                     IF Cont.GET(SegLine."Contact No.") THEN BEGIN
                       IF (Cont."Language Code" <> "Language Code") AND
                          SegInteractLanguage.GET("Segment No.",0,Cont."Language Code")
                       THEN BEGIN
                         SegLine."Language Code" := Cont."Language Code";
                         SegLine.Subject := SegInteractLanguage.Subject;
                         SegLine."Attachment No." := SegInteractLanguage."Attachment No.";
                       END ELSE BEGIN
                         SegLine."Language Code" := SegHeader."Language Code (Default)";
                         SegLine.Subject := SegHeader."Subject (Default)";
                         SegLine."Attachment No." := SegHeader."Attachment No.";
                       END;
                       SegLine.MODIFY;
                     END;
                   UNTIL SegLine.NEXT = 0;
               END ELSE BEGIN // UNIQUE Attachment
                 SegLine.GET("Segment No.","Segment Line No.");
                 IF SegLine."Attachment No." = "Attachment No." THEN BEGIN
                   SegInteractLanguage.SETRANGE("Segment No.","Segment No.");
                   SegInteractLanguage.SETRANGE("Segment Line No.","Segment Line No.");
                   SegInteractLanguage.SETFILTER("Language Code",'<>%1',"Language Code");
                   IF SegInteractLanguage.FINDFIRST THEN BEGIN
                     SegLine."Language Code" := SegInteractLanguage."Language Code";
                     SegLine.Subject := SegInteractLanguage.Subject;
                     SegLine."Attachment No." := SegInteractLanguage."Attachment No.";
                   END ELSE
                     IF SegLine."Interaction Template Code" = SegHeader."Interaction Template Code" THEN BEGIN
                       Cont.GET(SegLine."Contact No.");
                       IF SegInteractLanguage.GET("Segment No.",0,Cont."Language Code") THEN BEGIN
                         SegLine."Language Code" := Cont."Language Code";
                         SegLine.Subject := SegInteractLanguage.Subject;
                         SegLine."Attachment No." := SegInteractLanguage."Attachment No.";
                       END ELSE BEGIN
                         SegLine."Language Code" := SegHeader."Language Code (Default)";
                         SegLine.Subject := SegHeader."Subject (Default)";
                         SegLine."Attachment No." := SegHeader."Attachment No.";
                       END;
                     END ELSE BEGIN
                       SegLine."Language Code" := '';
                       SegLine.Subject := '';
                       SegLine."Attachment No." := 0;
                     END;
                   SegLine.MODIFY;
                 END;
               END;

               RemoveAttachment(FALSE);
             END;

    OnRename=VAR
               SegHeader@1001 : Record 11072039;
               SegLine@1000 : Record 11072040;
               Cont@1002 : Record 5050;
             BEGIN
               SegHeader.GET("Segment No.");
               SegHeader.CALCFIELDS("Attachment No.");
               IF ("Segment Line No." = 0) AND
                  (SegHeader."Language Code (Default)" = xRec."Language Code")
               THEN BEGIN
                 SegHeader."Language Code (Default)" := "Language Code";
                 SegHeader.MODIFY;
               END;

               IF "Segment Line No." = 0 THEN BEGIN
                 SegLine.SETRANGE("Segment No.","Segment No.");
                 SegLine.SETRANGE("Attachment No.","Attachment No.");
                 IF SegLine.FIND('-') THEN
                   REPEAT
                     SegLine."Language Code" := SegHeader."Language Code (Default)";
                     SegLine."Attachment No." := SegHeader."Attachment No.";
                     SegLine.MODIFY;
                   UNTIL SegLine.NEXT = 0;

                 SegLine.RESET;
                 SegLine.SETRANGE("Segment No.","Segment No.");
                 SegLine.SETRANGE("Interaction Template Code",SegHeader."Interaction Template Code");
                 IF SegLine.FIND('-') THEN
                   REPEAT
                     IF SegLine.AttachmentInherited OR (SegLine."Attachment No." = 0) THEN
                       IF Cont.GET(SegLine."Contact No.") THEN
                         IF Cont."Language Code" = "Language Code" THEN BEGIN
                           SegLine."Language Code" := "Language Code";
                           SegLine.Subject := Subject;
                           SegLine."Attachment No." := "Attachment No.";
                           SegLine.MODIFY;
                         END;
                   UNTIL SegLine.NEXT = 0;
               END ELSE BEGIN
                 SegLine.GET("Segment No.","Segment Line No.");
                 IF SegLine."Language Code" = xRec."Language Code" THEN BEGIN
                   SegLine."Language Code" := "Language Code";
                   SegLine."Attachment No." := "Attachment No.";
                   SegLine.MODIFY;
                 END;
               END;
             END;

    CaptionML=[ENU=Segment Interaction Language;
               NOR=Samhandlingsspr†k for segment];
    LookupPageID=Page5155;
  }
  FIELDS
  {
    { 1   ;   ;Segment No.         ;Code20        ;TableRelation="Segment Header";
                                                   CaptionML=[ENU=Segment No.;
                                                              NOR=Segmentnr.;
                                                              SVE=Segmentnr] }
    { 2   ;   ;Segment Line No.    ;Integer       ;TableRelation="Segment Line"."Line No." WHERE (Segment No.=FIELD(Segment No.));
                                                   CaptionML=[ENU=Segment Line No.;
                                                              NOR=Segmentlinjenr.;
                                                              SVE=Segmentradnr] }
    { 3   ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   CaptionML=[ENU=Language Code;
                                                              NOR=Spr†kkode;
                                                              SVE=Spr†kkod] }
    { 4   ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 5   ;   ;Attachment No.      ;Integer       ;CaptionML=[ENU=Attachment No.;
                                                              NOR=Vedleggsnr.;
                                                              SVE=Bilagenr] }
    { 6   ;   ;Subject             ;Text100       ;OnValidate=VAR
                                                                SegHeader@1000 : Record 11072039;
                                                                SegLine@1001 : Record 11072040;
                                                                UpdateLines@1002 : Boolean;
                                                              BEGIN
                                                                SegHeader.GET("Segment No.");
                                                                IF "Segment Line No." = 0 THEN
                                                                  IF SegHeader.SegLinesExist('') THEN
                                                                    UpdateLines := CONFIRM(STRSUBSTNO(Text005,FIELDCAPTION(Subject)),TRUE);

                                                                IF SegHeader."Language Code (Default)" = "Language Code" THEN
                                                                  IF SegHeader."Subject (Default)" = xRec.Subject THEN BEGIN
                                                                    SegHeader."Subject (Default)" := Subject;
                                                                    SegHeader.MODIFY;
                                                                    MODIFY;
                                                                  END;

                                                                IF NOT UpdateLines THEN
                                                                  EXIT;

                                                                SegLine.SETRANGE("Segment No.","Segment No.");
                                                                IF "Segment Line No." = 0 THEN
                                                                  SegLine.SETRANGE("Interaction Template Code",SegHeader."Interaction Template Code")
                                                                ELSE BEGIN
                                                                  SegLine.GET("Segment No.","Segment Line No.");
                                                                  SegLine.SETRANGE("Interaction Template Code",SegLine."Interaction Template Code");
                                                                END;
                                                                SegLine.SETRANGE("Language Code","Language Code");
                                                                SegLine.SETRANGE(Subject,xRec.Subject);
                                                                SegLine.MODIFYALL(Subject,Subject);
                                                              END;

                                                   CaptionML=[ENU=Subject;
                                                              NOR=Emne;
                                                              SVE=Ang†ende] }
  }
  KEYS
  {
    {    ;Segment No.,Segment Line No.,Language Code;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You have canceled the create process.;NOR=Du har avbrutt opprettingen.';
      Text001@1004 : TextConst 'ENU=Replace existing attachment?;NOR=Erstatte eksisterende vedlegg?';
      Text002@1003 : TextConst 'ENU=The import was canceled or the specified file could not be accessed. The import failed.;NOR=Importen ble avbrutt, eller den angitte filen var ikke tilgjengelig. Importen mislyktes.';
      Text003@1002 : TextConst 'ENU=Inherited;NOR=Arvet;SVE=Žrvd';
      Text004@1001 : TextConst 'ENU=Unique;NOR=Unik;SVE=Unik';
      Text005@1005 : TextConst 'ENU=You have modified %1.\\Do you want to update the segment lines with the same Interaction Template Code and Language Code?;NOR=Du har endret %1.\\Vil du oppdatere segmentlinjene med samme kode for samhandlingsmal og spr†k?';

    [Internal]
    PROCEDURE CreateAttachment@1();
    VAR
      SegInteractLanguage@1000 : Record 5104;
      Attachment@1001 : Record 11072030;
      WordManagement@1002 : Codeunit 11072023;
      ClientTypeManagement@1004 : Codeunit 4030;
      NewAttachmentNo@1003 : Integer;
    BEGIN
      IF "Attachment No." <> 0 THEN BEGIN
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
        RemoveAttachment(FALSE);
      END;

      IF ClientTypeManagement.GetCurrentClientType IN [CLIENTTYPE::Web,CLIENTTYPE::Tablet,CLIENTTYPE::Phone] THEN BEGIN
        IF Attachment.ImportAttachmentFromClientFile('',FALSE,TRUE) THEN
          NewAttachmentNo := Attachment."No.";
      END ELSE
        NewAttachmentNo := WordManagement.CreateWordAttachment("Segment No." + ' ' + Description,"Language Code");

      IF NewAttachmentNo <> 0 THEN BEGIN
        "Attachment No." := NewAttachmentNo;
        IF SegInteractLanguage.GET("Segment No.","Segment Line No.","Language Code") THEN
          MODIFY
        ELSE
          INSERT(TRUE);
        UpdateSegLineAttachment(0);
      END ELSE
        ERROR(Text000);
    END;

    [Internal]
    PROCEDURE OpenAttachment@2();
    VAR
      Attachment@1000 : Record 11072030;
    BEGIN
      IF "Attachment No." = 0 THEN
        EXIT;
      Attachment.GET("Attachment No.");
      Attachment.SetTemplateCode(GetTemplateCode); //**4PS.n
      Attachment.OpenAttachment("Segment No." + ' ' + Description,FALSE,"Language Code");
    END;

    [Internal]
    PROCEDURE CopyFromAttachment@3();
    VAR
      SegInteractLanguage@1004 : Record 5104;
      AttachmentManagement@1002 : Codeunit 11072021;
      NewAttachNo@1001 : Integer;
    BEGIN
      IF "Attachment No." <> 0 THEN BEGIN
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
        RemoveAttachment(FALSE);
        COMMIT;
      END;

      SegInteractLanguage.SETRANGE("Segment No.","Segment No.");
      SegInteractLanguage.SETFILTER("Attachment No.",'<>%1&<>%2',0,"Attachment No.");
      IF PAGE.RUNMODAL(0,SegInteractLanguage) = ACTION::LookupOK THEN BEGIN
        NewAttachNo := AttachmentManagement.InsertAttachment(SegInteractLanguage."Attachment No.");
        IF NewAttachNo <> 0 THEN BEGIN
          "Attachment No." := NewAttachNo;
          MODIFY;
          UpdateSegLineAttachment(0);
        END;
      END;
    END;

    [Internal]
    PROCEDURE ImportAttachment@4();
    VAR
      Attachment@1003 : Record 11072030;
      InterActionTemplateRec@1100485004 : Record 11072032;
      SegHeaderrec@1100485003 : Record 11072039;
      SegLineRec@1100485002 : Record 11072040;
      InteractionTemplateCode@1100485001 : Code[20];
      SecureDirectory@1100485000 : Text[250];
    BEGIN
      IF "Attachment No." <> 0 THEN BEGIN
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
        RemoveAttachment(FALSE);
      END;

      //**4PS.sn
      InteractionTemplateCode := '';
      SecureDirectory := '';
      IF "Segment Line No." = 0 THEN BEGIN
        SegHeaderrec.GET("Segment No.");
        InteractionTemplateCode := SegHeaderrec."Interaction Template Code";
      END ELSE BEGIN
        SegLineRec.GET("Segment No.","Segment Line No.");
        InteractionTemplateCode := SegLineRec."Interaction Template Code";
      END;
      IF InteractionTemplateCode <> '' THEN BEGIN
        InterActionTemplateRec.GET(InteractionTemplateCode);
        IF InterActionTemplateRec."Secured Document" = InterActionTemplateRec."Secured Document"::Always THEN BEGIN
          SecureDirectory := InterActionTemplateRec."Secured Document Directory";
        END;
      END;
      IF SecureDirectory <> '' THEN
        Attachment."Interaction Template Code" := InteractionTemplateCode;
      //**4PS.en
      IF Attachment.ImportAttachmentFromClientFile('',FALSE,FALSE) THEN BEGIN
        "Attachment No." := Attachment."No.";
        MODIFY;
        UpdateSegLineAttachment(0);
      END ELSE
        ERROR(Text002);
    END;

    [Internal]
    PROCEDURE ExportAttachment@5();
    VAR
      Attachment@1000 : Record 11072030;
      FileName@1001 : Text[1024];
    BEGIN
      IF Attachment.GET("Attachment No.") THEN BEGIN
        IF Attachment."Storage Type" = Attachment."Storage Type"::Embedded THEN
          Attachment.CALCFIELDS("Attachment File");
        Attachment.ExportAttachmentToClientFile(FileName);
      END;
    END;

    [Internal]
    PROCEDURE RemoveAttachment@6(Prompt@1000 : Boolean);
    VAR
      Attachment@1002 : Record 11072030;
      OldAttachmentNo@1001 : Integer;
    BEGIN
      OldAttachmentNo := "Attachment No.";
      IF Attachment.GET("Attachment No.") AND (NOT Attachment."Read Only") THEN
        IF Attachment.RemoveAttachment(Prompt) THEN BEGIN
          "Attachment No." := 0;
          MODIFY;
        END;
      UpdateSegLineAttachment(OldAttachmentNo);
    END;

    LOCAL PROCEDURE UpdateSegLineAttachment@7(OldAttachmentNo@1000 : Integer);
    VAR
      SegLine@1001 : Record 11072040;
      SegHeader@1002 : Record 11072039;
    BEGIN
      IF "Segment Line No." = 0 THEN BEGIN
        SegHeader.GET("Segment No.");
        SegLine.SETRANGE("Segment No.","Segment No.");
        SegLine.SETRANGE("Interaction Template Code",SegHeader."Interaction Template Code");
        SegLine.SETRANGE("Language Code","Language Code");
        SegLine.SETRANGE("Attachment No.",OldAttachmentNo);
        SegLine.MODIFYALL("Attachment No.","Attachment No.");
      END ELSE BEGIN
        SegLine.SETRANGE("Segment No.","Segment No.");
        SegLine.SETRANGE("Line No.","Segment Line No.");
        SegLine.SETRANGE("Attachment No.",OldAttachmentNo);
        IF SegLine.FINDFIRST THEN BEGIN
          SegLine."Attachment No." := "Attachment No.";
          SegLine.MODIFY;
        END;
      END;
    END;

    [External]
    PROCEDURE AttachmentText@9() : Text[30];
    BEGIN
      IF "Attachment No." = 0 THEN
        EXIT('');

      IF "Segment Line No." = 0 THEN
        EXIT(Text003);

      EXIT(Text004);
    END;

    [External]
    PROCEDURE Caption@8() CaptionText@1002 : Text[260];
    VAR
      SegHeader@1003 : Record 11072039;
      SegLine@1000 : Record 11072040;
      Cont@1001 : Record 5050;
    BEGIN
      SegHeader.GET("Segment No.");
      CaptionText := FORMAT("Segment No.") + ' ' + SegHeader.Description;
      IF "Segment Line No." <> 0 THEN BEGIN
        SegLine.GET("Segment No.","Segment Line No.");
        IF Cont.GET(SegLine."Contact No.") THEN;
        CaptionText := CaptionText + ' ' + FORMAT(SegLine."Contact No.") + ' ' + Cont.Name;
      END;
    END;

    PROCEDURE GetTemplateCode@1210190000() : Code[10];
    VAR
      SegHeader@1210190001 : Record 11072039;
      SegLine@1210190000 : Record 11072040;
    BEGIN
      //**4PS
      IF "Segment Line No." <> 0 THEN BEGIN
        SegLine.GET("Segment No.","Segment Line No.");
        EXIT(SegLine."Interaction Template Code");
      END ELSE BEGIN
        SegHeader.GET("Segment No.");
        EXIT(SegHeader."Interaction Template Code");
      END;
    END;

    BEGIN
    END.
  }
}

