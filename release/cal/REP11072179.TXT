OBJECT Report 11072179 Close Total Serv. Contr. Res.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Abschliessen Gesamtergebnis Servicevertrag;
               ENU=Close Total Service Contract Result;
               NLD=Afsluiten totaal resultaat service contract];
    ProcessingOnly=Yes;
    OnPostReport=BEGIN
                   DisplayError;
                 END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 4178;    ;DataItem;                    ;
               DataItemTable=Table11012812;
               DataItemTableView=SORTING(No.)
                                 WHERE(Result Calc. on Total Contract=CONST(Yes));
               OnAfterGetRecord=VAR
                                  LocServiceContractCtrlPeriod@1100529600 : Record 11071746;
                                BEGIN
                                  TESTFIELD("Result Calc. on Total Contract", TRUE);
                                  ContractErrorsExist := FALSE;

                                  TmpServiceContractCtrlPeriod.RESET;
                                  TmpServiceContractCtrlPeriod.DELETEALL;

                                  LocServiceContractCtrlPeriod.SETRANGE("Service Contract No.", "No.");
                                  IF LocServiceContractCtrlPeriod.FINDSET THEN BEGIN
                                    REPEAT
                                      TmpServiceContractCtrlPeriod := LocServiceContractCtrlPeriod;
                                      TmpServiceContractCtrlPeriod.INSERT;
                                    UNTIL LocServiceContractCtrlPeriod.NEXT = 0;

                                    TmpServiceContractCtrlPeriod.Code := INCSTR(TmpServiceContractCtrlPeriod.Code);
                                    TmpServiceContractCtrlPeriod."Starting Date" := LocServiceContractCtrlPeriod."Ending Date" + 1;
                                    TmpServiceContractCtrlPeriod."Ending Date" := "Ending Date";
                                    TmpServiceContractCtrlPeriod.INSERT;
                                  END ELSE BEGIN
                                    CLEAR(TmpServiceContractCtrlPeriod);
                                    TmpServiceContractCtrlPeriod."Service Contract No." := "No.";
                                    TmpServiceContractCtrlPeriod."Starting Date" := "Starting Date";
                                    TmpServiceContractCtrlPeriod."Ending Date" := "Ending Date";
                                    TmpServiceContractCtrlPeriod.INSERT;
                                  END;
                                END;

               ReqFilterFields=No.,Contract Group,Contract Manager,Contract Form }

    { 9302;1   ;DataItem;ServiceContractCtrlPeriod;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number);
               OnPreDataItem=BEGIN
                               SETRANGE(Number, 1, TmpServiceContractCtrlPeriod.COUNT);
                             END;

               OnAfterGetRecord=VAR
                                  ServiceOrder@1100528601 : Record 11012823;
                                BEGIN
                                  IF Number = 1 THEN
                                    TmpServiceContractCtrlPeriod.FINDSET
                                  ELSE
                                    TmpServiceContractCtrlPeriod.NEXT;
                                  IF Number = TmpServiceContractCtrlPeriod.COUNT THEN
                                    TmpServiceContractCtrlPeriod.Code := '';

                                  TmpServiceOrder.RESET;
                                  TmpServiceOrder.DELETEALL;

                                  // Service Orders (Calls and Direct)
                                  ServiceOrder.SETCURRENTKEY("Service Contract No.");
                                  ServiceOrder.SETRANGE("Service Contract No.", TmpServiceContractCtrlPeriod."Service Contract No.");
                                  ServiceOrder.SETRANGE("Service Control Period Date", TmpServiceContractCtrlPeriod."Starting Date", TmpServiceContractCtrlPeriod."Ending Date");
                                  ServiceOrder.SETFILTER("Source Type", '%1|%2', ServiceOrder."Source Type"::Call, ServiceOrder."Source Type"::Direct);
                                  ServiceOrder.SETFILTER(Status, '<%1', ServiceOrder.Status::Cancelled);
                                  IF ServiceOrder.FINDSET THEN
                                    REPEAT
                                      InsertSOtoTmp(ServiceOrder, '', TmpServiceContractCtrlPeriod."Service Contract No.", TmpServiceContractCtrlPeriod.Code);
                                    UNTIL ServiceOrder.NEXT = 0;

                                  // Service Orders (Contract) without Contract Arrangement link (due to conversion)
                                  ServiceOrder.SETCURRENTKEY("Service Contract No.");
                                  ServiceOrder.SETRANGE("Service Contract No.", TmpServiceContractCtrlPeriod."Service Contract No.");
                                  ServiceOrder.SETRANGE("Service Control Period Date", TmpServiceContractCtrlPeriod."Starting Date", TmpServiceContractCtrlPeriod."Ending Date");
                                  ServiceOrder.SETRANGE("Source Type", ServiceOrder."Source Type"::Contract);
                                  ServiceOrder.SETFILTER(Status, '<%1', ServiceOrder.Status::Cancelled);
                                  IF ServiceOrder.FINDSET THEN
                                    REPEAT
                                      IF NOT ErrorFoundOnContractObjectMaintenance(ServiceOrder, TmpServiceContractCtrlPeriod.Code) THEN
                                        InsertSOtoTmp(ServiceOrder, '', TmpServiceContractCtrlPeriod."Service Contract No.", TmpServiceContractCtrlPeriod.Code);
                                    UNTIL ServiceOrder.NEXT = 0;

                                  CheckTmpServiceOrders(TmpServiceContractCtrlPeriod);
                                  CheckServiceContractPeriod(TmpServiceContractCtrlPeriod);

                                  IF Finalize THEN
                                    IF NOT ErrorsExist(TmpServiceContractCtrlPeriod."Service Contract No.", TmpServiceContractCtrlPeriod.Code) THEN BEGIN
                                      FinalizeTmpServiceOrders(TmpServiceContractCtrlPeriod);
                                      FinalizeServiceContractPeriod(TmpServiceContractCtrlPeriod);
                                    END ELSE
                                      ContractErrorsExist := TRUE;
                                END;

               OnPostDataItem=VAR
                                ServiceContract@1100529600 : Record 11012812;
                              BEGIN
                                IF (NOT ContractErrorsExist) AND Finalize THEN BEGIN
                                  ServiceContract.GET("Service-Contract"."No.");
                                  ServiceContract.Status := ServiceContract.Status::Closed;
                                  ServiceContract.Blocked := TRUE;
                                  ServiceContract.MODIFY;
                                  COMMIT;
                                END;
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525001;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100525003;2;Field  ;
                  Name=Posting Date;
                  CaptionML=[DEU=Buchungsdatum;
                             ENU=Posting Date;
                             NLD=Boekingsdatum;
                             SVE=Bokf”ringsdatum];
                  SourceExpr=PostingDate }

      { 1100525004;2;Field  ;
                  Name=Ingnore Sales Control;
                  CaptionML=[DEU=Ignorieren Verkaufskontrolle;
                             ENU=Ingnore Sales Control;
                             NLD=Negeer verkoopcontrole;
                             SVE=Ignorera f”rs„ljningskontroll];
                  SourceExpr=IgnoreSales;
                  OnValidate=BEGIN
                               IF IgnoreSales THEN
                                 MESSAGE(Text000 + Text001);
                             END;
                              }

      { 1100525005;2;Field  ;
                  Name=Ignore Purchase Control;
                  CaptionML=[DEU=Ignorieren Einkaufskontrolle;
                             ENU=Ignore Purchase Control;
                             NLD=Negeer inkoopcontrole;
                             SVE=Ignorera ink”pskontroll];
                  SourceExpr=IgnorePurchase;
                  OnValidate=BEGIN
                               IF IgnorePurchase THEN
                                 MESSAGE(Text002 + Text003);
                             END;
                              }

      { 1100525006;2;Field  ;
                  Name=Ignore Future Posting Control;
                  CaptionML=[DEU=Ignorieren Buchungen nach Buch.-Datumberpr.;
                             ENU=Ignore Future Posting Control;
                             NLD=Negeer boekingen na boekingsdatum controle;
                             SVE=Ignorera kontroll av framtida bokf”ring];
                  SourceExpr=IgnoreFuturePostings }

      { 1100528600;2;Field  ;
                  CaptionML=[DEU=Arbeitsauftr„ge ignorieren;
                             ENU=Ignore Work Orders;
                             NLD=Negeer werkorders];
                  SourceExpr=IgnoreWorkOrders }

      { 1100528200;2;Field  ;
                  CaptionML=[DEU=Ignoriere Artikeljournalkontrolle;
                             ENU=Ignore Item Journal Check;
                             NLD=Negeer artikeljournaalcontrole];
                  SourceExpr=IgnoreItemJournal }

      { 1100528201;2;Field  ;
                  CaptionML=[DEU=Ignoriere andere Betriebe;
                             ENU=Ignore Other Companies;
                             NLD=Negeer andere bedrijven];
                  SourceExpr=IgnoreInterCompany }

      { 1100525007;2;Field  ;
                  Name=Post;
                  CaptionML=[DEU=Buchen;
                             ENU=Post;
                             NLD=Boeken;
                             SVE=Bokf”r];
                  SourceExpr=Finalize }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TmpServiceOrder@1100528602 : TEMPORARY Record 11012823;
      TmpServiceContractCtrlPeriod@1100529601 : TEMPORARY Record 11071746;
      PostingDate@1100528604 : Date;
      IgnorePurchase@1100528605 : Boolean;
      IgnoreSales@1100528606 : Boolean;
      IgnoreFuturePostings@1100528607 : Boolean;
      IgnoreWorkOrders@1100528600 : Boolean;
      Finalize@1100528603 : Boolean;
      Text000@1100528609 : TextConst 'DEU=Das Ignorieren der Einkaufskontrolle kann zur Folge haben, dass Serviceavertr„ge zu frh fertiggemeldet werden.\;ENU=Ignoring the Sales Check could result in evaluating Service Contracts too early.\;NLD=Het negeren van de verkoopcontrole kan tot gevolg hebben dat servicecontracten te vroeg worden gereedgemeld.\;SVE=Om f”rs„ljningskontrollen ignoreras kan servicekontrakt avslutas f”r tidigt.\';
      Text001@1100528608 : TextConst 'DEU=Es k”nnen doch noch nicht fakturierte Regieeintrag-Posten oder Raten vorhanden sein.;ENU=There could be not Invoiced Cost Plus Entries or Installemtens present.;NLD=Er kunnen immers nog ongefactureerde regiemutaties of termijnen aanwezig zijn.;SVE=Det kan finnas redovisade sj„lvkostnader eller avbetalningar.';
      Text002@1100528611 : TextConst 'DEU=Das Ignorieren der Einkaufskontrolle kann zur Folge haben, dass Serviceauftr„ge zu frh fertiggemeldet werden.\;ENU=Ignoring the Purchase Check could result in closing Service Orders too early.\;NLD=Het negeren van de inkoopcontrole kan tot gevolg hebben dat serviceorders te vroeg worden gereedgemeld.\;SVE=Om k”pkontrollen ignoreras kan serviceorder avslutas f”r tidigt.\';
      Text003@1100528610 : TextConst 'DEU=Es k”nnen doch noch Einkaufsbestellungen vorhanden sein.;ENU=There could still be Purchase Orders present.;NLD=Er kunnen immers nog inkooporders aanwezig zijn.;SVE=Det kan fortfarande finnas ink”psorder.';
      Text004@1100528612 : TextConst 'DEU=Serviceauftrag nicht administrativ abgeschlossen;ENU=Service Order not administrative finished;NLD=Serviceorder heeft niet de status administratief gereed;SVE=Serviceorder inte administrativt klar';
      Text005@1100528500 : TextConst 'DEU=Keine "%1" fr Objekt %2 angegeben, Wartungskonzept %3;ENU=No "%1" specified for object %2, maintenance scheme %3;NLD=Geen "%1" gespecificeerd voor object %2, onderhoudsschema %3';
      ContractErrorsExist@1100529600 : Boolean;
      IgnoreItemJournal@1100528201 : Boolean;
      IgnoreInterCompany@1100528200 : Boolean;

    PROCEDURE CheckTmpServiceOrders@1100528603(IServiceContractCtrlPeriod@1100525000 : Record 11071746);
    VAR
      CloseProjectServiceCU@1100528600 : Codeunit 11012046;
      Type@1100528602 : 'Project,Service Contract,Service Order';
    BEGIN
      IF NOT TmpServiceOrder.FINDSET THEN
        EXIT;
      REPEAT
        CLEAR(CloseProjectServiceCU);
        CloseProjectServiceCU.SetSelection(
          Type::"Service Order", '', TmpServiceOrder."Service Contract No.", TmpServiceOrder."No.",
          PostingDate, IgnorePurchase, IgnoreSales, IgnoreFuturePostings, FALSE, IgnoreWorkOrders, '', FALSE,
          IgnoreItemJournal, IgnoreInterCompany);
        IF IServiceContractCtrlPeriod.Code = '' THEN
          CloseProjectServiceCU.SetSelectionCloseServiceRestOfContract(0, TmpServiceContractCtrlPeriod)
        ELSE
          CloseProjectServiceCU.SetSelectionCloseService(0, IServiceContractCtrlPeriod.Code);
        CloseProjectServiceCU.SetServOrderExtraTest(FALSE, TRUE);
        CloseProjectServiceCU.Check;
      UNTIL TmpServiceOrder.NEXT = 0;
    END;

    PROCEDURE FinalizeTmpServiceOrders@1100528604(IServiceContractCtrlPeriod@1100525000 : Record 11071746);
    VAR
      CloseProjectServiceCU@1100528601 : Codeunit 11012046;
      Type@1100528600 : 'Project,Service Contract,Service Order';
    BEGIN
      IF NOT TmpServiceOrder.FINDSET THEN
        EXIT;
      REPEAT
        CLEAR(CloseProjectServiceCU);
        CloseProjectServiceCU.SetSelection(
          Type::"Service Order", '', TmpServiceOrder."Service Contract No.", TmpServiceOrder."No.",
          PostingDate, IgnorePurchase, IgnoreSales, IgnoreFuturePostings, FALSE, IgnoreWorkOrders, '', FALSE,
          IgnoreItemJournal, IgnoreInterCompany);
        IF IServiceContractCtrlPeriod.Code = '' THEN
          CloseProjectServiceCU.SetSelectionCloseServiceRestOfContract(0, TmpServiceContractCtrlPeriod)
        ELSE
          CloseProjectServiceCU.SetSelectionCloseService(0, IServiceContractCtrlPeriod.Code);
        CloseProjectServiceCU.SetServOrderExtraTest(FALSE, TRUE);
        CloseProjectServiceCU.SetCloseContract(TRUE);
        CloseProjectServiceCU.Check;
        CloseProjectServiceCU.Post;
      UNTIL TmpServiceOrder.NEXT = 0;
    END;

    PROCEDURE CheckServiceContractPeriod@1100528600(IServiceContractCtrlPeriod@1100528601 : Record 11071746);
    VAR
      CloseProjectServiceCU@1100528602 : Codeunit 11012046;
      Type@1100528600 : 'Project,Service Contract,Service Order';
    BEGIN
      CloseProjectServiceCU.SetSelection(
        Type::"Service Contract", '', IServiceContractCtrlPeriod."Service Contract No.", '',
        PostingDate, IgnorePurchase, IgnoreSales, IgnoreFuturePostings, FALSE, FALSE, '', FALSE,
        IgnoreItemJournal, IgnoreInterCompany);
      IF IServiceContractCtrlPeriod.Code = '' THEN
        CloseProjectServiceCU.SetSelectionCloseServiceRestOfContract(0, TmpServiceContractCtrlPeriod)
      ELSE
        CloseProjectServiceCU.SetSelectionCloseService(0, IServiceContractCtrlPeriod.Code);
      CloseProjectServiceCU.Check;
    END;

    PROCEDURE FinalizeServiceContractPeriod@1100528605(IServiceContractCtrlPeriod@1100528602 : Record 11071746);
    VAR
      CloseProjectServiceCU@1100528601 : Codeunit 11012046;
      Type@1100528600 : 'Project,Service Contract,Service Order';
    BEGIN
      CloseProjectServiceCU.SetSelection(
        Type::"Service Contract", '', IServiceContractCtrlPeriod."Service Contract No.", '',
        PostingDate, IgnorePurchase, IgnoreSales, IgnoreFuturePostings, FALSE, FALSE, '', FALSE,
        IgnoreItemJournal, IgnoreInterCompany);
      IF IServiceContractCtrlPeriod.Code = '' THEN
        CloseProjectServiceCU.SetSelectionCloseServiceRestOfContract(0, TmpServiceContractCtrlPeriod)
      ELSE
        CloseProjectServiceCU.SetSelectionCloseService(0, IServiceContractCtrlPeriod.Code);
      CloseProjectServiceCU.SetCloseContract(TRUE);
      CloseProjectServiceCU.Check;
      CloseProjectServiceCU.Post;
    END;

    PROCEDURE InsertSOtoTmp@1100528607(IServiceOrder@1100528600 : Record 11012823;IBaseServiceOrderNo@1100528601 : Code[20];IServiceContractNo@1100525001 : Code[20];IServContrControlPeriodCode@1100525000 : Code[10]);
    BEGIN
      IF IServiceOrder.Status < IServiceOrder.Status::"Administrative Finished" THEN
        AddError(Text004, IServiceOrder."No.", IServiceContractNo, IServContrControlPeriodCode)
      ELSE BEGIN
        TmpServiceOrder := IServiceOrder;
        IF TmpServiceOrder.INSERT THEN;
      END;
    END;

    PROCEDURE AddError@7(IMessTxt@11012000 : Text[250];IServiceOrder@1100525001 : Code[20];IServiceContractNo@1100525002 : Code[20];IServContrControlPeriodCode@1100525003 : Code[10]);
    VAR
      ErrorRec@1100525000 : Record 11012051;
      NewLine@1100485000 : Integer;
    BEGIN
      WITH ErrorRec DO BEGIN
        SETRANGE("User ID", USERID);
        SETRANGE("Source Type", "Source Type"::"Finish Service-Project");
        IF FINDLAST THEN
          NewLine := "Line No." + 1
        ELSE
          NewLine := 1;

        INIT;
        "User ID" := USERID;
        "Source Type" := "Source Type"::"Finish Service-Project";
        "Line No." := NewLine;
        "Error message" := IMessTxt;
        "Service Order No." := IServiceOrder;
        "Service Contract No." := IServiceContractNo;
        "Serv. Contr. Ctrl. Period Code" := IServContrControlPeriodCode;
        INSERT;
      END;
    END;

    PROCEDURE DisplayError@1210190000();
    VAR
      ErrorRec@1100525000 : Record 11012051;
    BEGIN
      ErrorRec.SETRANGE("User ID", USERID);
      ErrorRec.SETRANGE("Source Type", ErrorRec."Source Type"::"Finish Service-Project");
      IF ErrorRec.FINDSET THEN BEGIN
        COMMIT;
        PAGE.RUNMODAL(PAGE::Errors, ErrorRec);
      END;
    END;

    PROCEDURE ErrorsExist@1100528606(IServiceContractNo@1100525000 : Code[20];IServContrControlPeriodCode@1100525001 : Code[10]) : Boolean;
    VAR
      ErrorRec@1100528600 : Record 11012051;
    BEGIN
      ErrorRec.SETRANGE("User ID", USERID);
      ErrorRec.SETRANGE("Source Type", ErrorRec."Source Type"::"Finish Service-Project");
      ErrorRec.SETRANGE("Service Contract No.", IServiceContractNo);
      ErrorRec.SETRANGE("Serv. Contr. Ctrl. Period Code", IServContrControlPeriodCode);
      EXIT(NOT ErrorRec.ISEMPTY)
    END;

    PROCEDURE ErrorFoundOnContractObjectMaintenance@1100525014(ServiceOrder@1100528500 : Record 11012823;ServiceContractControlPeriodCode@1100528501 : Code[10]) ErrorFound : Boolean;
    VAR
      ServiceOrderObject@1100525000 : Record 11071724;
      ContractObjectMaintenance@1100525001 : Record 11071692;
      ErrorMessage@1100528502 : Text;
    BEGIN
      ErrorFound := FALSE;
      ServiceOrderObject.SETRANGE("Service Order No.", ServiceOrder."No.");
      ServiceOrderObject.SETFILTER("Maintenance Scheme", '<>%1', '');
      IF ServiceOrderObject.FINDSET THEN
        REPEAT
          IF ContractObjectMaintenance.GET(
            ServiceOrderObject."Service Contract No.", ServiceOrderObject."Object No.", ServiceOrderObject."Maintenance Scheme")
          THEN BEGIN
            IF (FORMAT(ContractObjectMaintenance."Maintenance Interval") = '') AND (ContractObjectMaintenance."Maintenance Interval (Counter)" = 0) THEN BEGIN
              ErrorMessage := STRSUBSTNO(Text005,ContractObjectMaintenance.FIELDCAPTION("Maintenance Interval"), ContractObjectMaintenance."Object No.", ContractObjectMaintenance."Maintenance Scheme");
              ErrorFound := TRUE;
              AddError(ErrorMessage, ServiceOrder."No.", ServiceOrder."Service Contract No.", ServiceContractControlPeriodCode);
            END;
            IF (ContractObjectMaintenance."Maintenance Interval (Counter)" <> 0) AND (ContractObjectMaintenance."Consumption Component" = '') THEN BEGIN
              ErrorMessage := STRSUBSTNO(Text005,ContractObjectMaintenance.FIELDCAPTION("Consumption Component"), ContractObjectMaintenance."Object No.", ContractObjectMaintenance."Maintenance Scheme");
              ErrorFound := TRUE;
              AddError(ErrorMessage, ServiceOrder."No.", ServiceOrder."Service Contract No.", ServiceContractControlPeriodCode);
            END;
            IF (ContractObjectMaintenance."Consumption Component" <> '') AND (ContractObjectMaintenance."Maintenance Interval (Counter)" = 0) THEN BEGIN
              ErrorMessage := STRSUBSTNO(Text005, ContractObjectMaintenance.FIELDCAPTION("Maintenance Interval (Counter)"), ContractObjectMaintenance."Object No.", ContractObjectMaintenance."Maintenance Scheme");
              ErrorFound := TRUE;
              AddError(ErrorMessage, ServiceOrder."No.", ServiceOrder."Service Contract No.", ServiceContractControlPeriodCode);
            END;
            IF (ContractObjectMaintenance."Consumption Component" = '') AND (FORMAT(ContractObjectMaintenance."Maintenance Interval") = '') THEN BEGIN
              ErrorMessage := STRSUBSTNO(Text005, ContractObjectMaintenance.FIELDCAPTION("Maintenance Interval"), ContractObjectMaintenance."Object No.", ContractObjectMaintenance."Maintenance Scheme");
              ErrorFound := TRUE;
              AddError(ErrorMessage, ServiceOrder."No.", ServiceOrder."Service Contract No.", ServiceContractControlPeriodCode);
            END;
          END;
        UNTIL ServiceOrderObject.NEXT = 0;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

