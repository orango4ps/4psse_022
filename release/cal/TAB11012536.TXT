OBJECT Table 11012536 Order No. Contact (Plant)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Contact No.");
             END;

    CaptionML=[ENU=Order No. Contact;
               NOR=Ordrenr, Kontakt;
               SVE=Ordernr, kontakt];
  }
  FIELDS
  {
    { 5   ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Type;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=ENU=Plant Order - From Location,Plant Order - To Location,Transport Order - From Location,Transport Order - To Location,Plant Request - From Location,Plant Request - To Location,Exit Order,Returned Loss Order,Plant Rental Free Order;
                                                   OptionString=PO From,PO To,TO From,TO To,Req From,Req To,ExitOrder,ReturnedLossOrder,RentalFreeOrder }
    { 10  ;   ;Order No.           ;Code20        ;TableRelation=IF (Type=FILTER(PO From|PO To)) "Plant Order"
                                                                 ELSE IF (Type=FILTER(Req From|Req To)) "Plant Request"
                                                                 ELSE IF (Type=CONST(ExitOrder)) "Exit Order"
                                                                 ELSE IF (Type=CONST(ReturnedLossOrder)) "Returned Loss Order"
                                                                 ELSE IF (Type=CONST(RentalFreeOrder)) "Plant Rental Free Order";
                                                   CaptionML=[ENU=Order No.;
                                                              NOR=Ordrenr.;
                                                              SVE=Ordernr];
                                                   NotBlank=Yes }
    { 15  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 20  ;   ;Contact No.         ;Code20        ;TableRelation=Contact;
                                                   OnValidate=BEGIN
                                                                IF "Contact No." <> '' THEN BEGIN
                                                                  TESTFIELD("Order No.");
                                                                  OrderContRec.RESET;
                                                                  OrderContRec.SETCURRENTKEY(Type, "Order No.", "Contact No.");
                                                                  OrderContRec.SETRANGE(Type, Type);
                                                                  OrderContRec.SETRANGE("Order No.", "Order No.");
                                                                  OrderContRec.SETRANGE("Contact No.", "Contact No.");
                                                                  OrderContRec.SETFILTER("Line No.", '<>%1', "Line No.");
                                                                  IF OrderContRec.FIND('-') THEN
                                                                    OrderContRec.FIELDERROR("Contact No.", STRSUBSTNO(Text001, "Contact No."));

                                                                  IF NOT ExitOrderWithoutLocation() THEN BEGIN  //*C020580.n
                                                                    OrderLocation := GetLocationCode();
                                                                    IF NOT LocContRec.GET(OrderLocation, "Contact No.") THEN
                                                                      ERROR(Text000, FIELDCAPTION("Contact No."), "Contact No.", OrderLocation);
                                                                  END;
                                                                END;

                                                                CALCFIELDS(
                                                                  Name, "Name 2",
                                                                  Address, "Address 2", City, "Post Code",
                                                                  "Phone No.", "Phone No. 2", "Mobile Phone No.", "Mobile Phone No. 2", "Phone No. Work", "Phone No. Work 2",
                                                                  "Fax No.", "E-Mail", "E-Mail 2"
                                                                  );
                                                              END;

                                                   CaptionML=[ENU=Contact No.;
                                                              NOR=Kontaktnr.;
                                                              SVE=Kontaktnr];
                                                   NotBlank=Yes }
    { 21  ;   ;Name                ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=[ENU=Name;
                                                              NOR=Navn;
                                                              SVE=Namn];
                                                   Editable=No }
    { 22  ;   ;Name 2              ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact."Name 2" WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=[ENU=Name 2;
                                                              NOR=Navn 2;
                                                              SVE=Namn 2];
                                                   Editable=No }
    { 30  ;   ;Address             ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Address WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=[ENU=Address;
                                                              NOR=Adresse;
                                                              SVE=Adress];
                                                   Editable=No }
    { 31  ;   ;Address 2           ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact."Address 2" WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=[ENU=Address 2;
                                                              NOR=Adresse 2;
                                                              SVE=Adress 2];
                                                   Editable=No }
    { 32  ;   ;City                ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.City WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=[ENU=City;
                                                              NOR=Poststed;
                                                              SVE=Ort];
                                                   Editable=No }
    { 33  ;   ;Post Code           ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact."Post Code" WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=[ENU=Post Code;
                                                              NOR=Postnr.;
                                                              SVE=Postnr];
                                                   Editable=No }
    { 40  ;   ;Phone No.           ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact."Phone No." WHERE (No.=FIELD(Contact No.)));
                                                   ExtendedDatatype=Phone No.;
                                                   CaptionML=[ENU=Phone No.;
                                                              NOR=Telefonnr.;
                                                              SVE=Telefonnr];
                                                   Editable=No }
    { 41  ;   ;Phone No. 2         ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact."Phone No." WHERE (No.=FIELD(Contact No.)));
                                                   ExtendedDatatype=Phone No.;
                                                   CaptionML=[ENU=Phone No. 2;
                                                              NOR=Telefonnr 2;
                                                              SVE=Telefonnr 2];
                                                   Editable=No }
    { 42  ;   ;Mobile Phone No.    ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact."Mobile Phone No." WHERE (No.=FIELD(Contact No.)));
                                                   ExtendedDatatype=Phone No.;
                                                   CaptionML=[ENU=Mobile Phone No.;
                                                              NOR=Mobiltelefonnr;
                                                              SVE=Mobiltelefonnr];
                                                   Editable=No }
    { 43  ;   ;Mobile Phone No. 2  ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact."Mobile Phone No. 2" WHERE (No.=FIELD(Contact No.)));
                                                   ExtendedDatatype=Phone No.;
                                                   CaptionML=[ENU=Mobile Phone No. 2;
                                                              NOR=Mobiltelefonnr 2;
                                                              SVE=Mobiltelefonnr 2];
                                                   Editable=No }
    { 44  ;   ;Phone No. Work      ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact."Phone No." WHERE (No.=FIELD(Contact No.)));
                                                   ExtendedDatatype=Phone No.;
                                                   CaptionML=[ENU=Phone No. Work;
                                                              NOR=Telefonnr, arbeid;
                                                              SVE=Telefonnr, arbete];
                                                   Editable=No }
    { 45  ;   ;Phone No. Work 2    ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact."Phone No." WHERE (No.=FIELD(Contact No.)));
                                                   ExtendedDatatype=Phone No.;
                                                   CaptionML=[ENU=Phone No. Work 2;
                                                              NOR=Telefonnr, Arbeid 2;
                                                              SVE=Telefonnr, arbete 2];
                                                   Editable=No }
    { 49  ;   ;Fax No.             ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact."Fax No." WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=[ENU=Fax No.;
                                                              NOR=Telefaxnr;
                                                              SVE=Telefaxnr];
                                                   Editable=No }
    { 50  ;   ;E-Mail              ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.E-Mail WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=[ENU=Email;
                                                              NOR=E-post;
                                                              SVE=E-post];
                                                   Editable=No }
    { 51  ;   ;E-Mail 2            ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact."E-Mail 2" WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=[ENU=Email 2;
                                                              NOR=E-post 2;
                                                              SVE=E-post 2];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Type,Order No.,Line No.                 ;Clustered=Yes }
    {    ;Type,Order No.,Contact No.               }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      LocContRec@1100485001 : Record 11012594;
      OrderContRec@1100485003 : Record 11012536;
      OrderLocation@1100485000 : Code[20];
      Text000@1100485002 : TextConst 'ENU=%1 ''%2'' not present on location ''%3'';NOR=%1 ''%2'' finnes ikke p† lokasjon ''%3'';SVE=%1 ''%2'' finns inte p† lagerst„lle ''%3''';
      Text001@1100485004 : TextConst 'ENU=''%1'' is already present;NOR=''%1'' finnes allerede;SVE=''%1'' finns redan';

    PROCEDURE LookupLocContact@1100485000();
    VAR
      Contact@1100409000 : Record 5050;
    BEGIN
      IF NOT ExitOrderWithoutLocation() THEN BEGIN  //*C020580.n
        OrderLocation := GetLocationCode();
        IF OrderLocation = '' THEN
          EXIT;

        LocContRec.RESET;
        LocContRec.SETRANGE(Location, OrderLocation);
        LocContRec.Location := OrderLocation;
        IF PAGE.RUNMODAL(0, LocContRec) = ACTION::LookupOK THEN
          VALIDATE("Contact No.", LocContRec."Contact No.");
      //*C020580.sn
      END ELSE BEGIN
        Contact."No." := "Contact No.";
        IF PAGE.RUNMODAL(0, Contact) = ACTION::LookupOK THEN
          VALIDATE("Contact No.", Contact."No.");
      END;
      //*C020580.en
    END;

    PROCEDURE GetLocationCode@1100485001() : Code[20];
    VAR
      lvPlantOrderRec@1100485002 : Record 11012556;
      lvTransOrderRec@1100485001 : Record 11020505;
      lvPostedTransOrderRec@1100485000 : Record 11020507;
      lvPlantReqRec@1100485003 : Record 11020520;
      ExitOrder@1100409000 : Record 11012559;
      ReturnedLossOrder@1100527400 : Record 11012655;
      PlantRentalFreeOrder@1100527401 : Record 11020552;
    BEGIN
      IF "Order No." = '' THEN
        EXIT('');

      CASE Type OF
        Type::"PO From":
          IF lvPlantOrderRec.GET("Order No.") THEN
            EXIT(lvPlantOrderRec."From Location");
        Type::"PO To":
          IF lvPlantOrderRec.GET("Order No.") THEN
            EXIT(lvPlantOrderRec."To Location");
        Type::"TO From":
          IF lvTransOrderRec.GET("Order No.") THEN
            EXIT(lvTransOrderRec."From Location")
          ELSE BEGIN
            IF lvPostedTransOrderRec.GET("Order No.") THEN
              EXIT(lvPostedTransOrderRec."From Location");
          END;
        Type::"TO To":
          IF lvTransOrderRec.GET("Order No.") THEN
            EXIT(lvTransOrderRec."To Location")
          ELSE BEGIN
            IF lvPostedTransOrderRec.GET("Order No.") THEN
              EXIT(lvPostedTransOrderRec."To Location");
          END;
        Type::"Req From":
          IF lvPlantReqRec.GET("Order No.") THEN
            EXIT(lvPlantReqRec."From Location");
        Type::"Req To":
          IF lvPlantReqRec.GET("Order No.") THEN
            EXIT(lvPlantReqRec."To Location");
        Type::ExitOrder:  //*C020580
          IF ExitOrder.GET("Order No.") THEN
            EXIT(ExitOrder.Location);
        Type::ReturnedLossOrder:
          IF ReturnedLossOrder.GET("Order No.") THEN
            EXIT(ReturnedLossOrder.Location);
        Type::RentalFreeOrder:
          IF PlantRentalFreeOrder.GET("Order No.") THEN
            EXIT(PlantRentalFreeOrder."Plant Location");
      END;
      EXIT('');
    END;

    PROCEDURE ShowOrderContacts@1100525008(ContactType@1100525001 : Option;OrderNo@1100525002 : Code[20];PlantLocation@1100525003 : Code[20]);
    VAR
      OrderCont@1100525000 : Record 11012536;
    BEGIN
      IF (OrderNo = '') OR ((PlantLocation = '') AND (ContactType <> Type::ExitOrder)) THEN  //*C020580.c
        EXIT;

      OrderCont.FILTERGROUP(9);
      OrderCont.SETRANGE(Type, ContactType);
      OrderCont.SETRANGE("Order No.", OrderNo);
      OrderCont.FILTERGROUP(0);
      PAGE.RUNMODAL(PAGE::"Order No. Contacts (Plant)", OrderCont);
    END;

    PROCEDURE GetFirstOrderContact@1100485003(ContactType@1100525000 : Option;OrderNo@1100525001 : Code[20];VAR OrderCont@1100525002 : Record 11012536);
    BEGIN
      OrderCont.RESET;
      OrderCont.SETRANGE(Type, ContactType);
      OrderCont.SETRANGE("Order No.", OrderNo);
      IF OrderCont.FINDFIRST THEN
        OrderCont.CALCFIELDS(
          Name,  "Name 2",  Address,  "Address 2",  "Post Code",  City, "Fax No.", "E-Mail", "E-Mail 2",
          "Phone No.", "Phone No. 2", "Mobile Phone No.", "Mobile Phone No. 2", "Phone No. Work", "Phone No. Work 2")
      ELSE
        OrderCont.INIT;
    END;

    PROCEDURE ExitOrderWithoutLocation@1100409000() : Boolean;
    BEGIN
      //*C020580
      IF ((Type <> Type::ExitOrder) AND (Type <> Type::ReturnedLossOrder)) OR ("Order No." = '') THEN
        EXIT(FALSE);
      EXIT(GetLocationCode() = '');
    END;

    PROCEDURE ReceiveEmail@1100409001() : Boolean;
    VAR
      PlantLocationContact@1100409000 : Record 11012594;
      Location@1100409001 : Code[20];
    BEGIN
      //*C020580
      IF "Contact No." = '' THEN
        EXIT(FALSE);
      CALCFIELDS("E-Mail");
      IF "E-Mail" = '' THEN
        EXIT(FALSE);
      Location := GetLocationCode();
      IF Location = '' THEN
        EXIT(Type = Type::ExitOrder);
      IF PlantLocationContact.GET(Location, "Contact No.") THEN
        EXIT(PlantLocationContact."Receive E-Mail");
      EXIT(FALSE);
    END;

    PROCEDURE CopyOrderContacts@1100525000(FromType@1100525000 : Option;FromOrderNo@1100525001 : Code[20];ToType@1100525002 : Option;ToOrderNo@1100525003 : Code[20]);
    VAR
      OrderContactPlant1@1100525004 : Record 11012536;
      OrderContactPlant2@1100525005 : Record 11012536;
      NextLineNo@1100525006 : Integer;
    BEGIN
      OrderContactPlant1.SETRANGE(Type, FromType);
      OrderContactPlant1.SETRANGE("Order No.", FromOrderNo);
      IF OrderContactPlant1.FINDSET THEN BEGIN
        OrderContactPlant2.SETRANGE(Type, ToType);
        OrderContactPlant2.SETRANGE("Order No.", ToOrderNo);
        REPEAT
          OrderContactPlant2.SETRANGE("Contact No.", OrderContactPlant1."Contact No.");
          IF OrderContactPlant2.ISEMPTY THEN BEGIN
            OrderContactPlant2.SETRANGE("Contact No.");  //Reset filter
            NextLineNo := 10000;
            IF OrderContactPlant2.FINDLAST THEN
              NextLineNo := NextLineNo + OrderContactPlant2."Line No.";
            OrderContactPlant2.Type := ToType;
            OrderContactPlant2."Order No." := ToOrderNo;
            OrderContactPlant2."Line No." := NextLineNo;
            OrderContactPlant2.VALIDATE("Contact No.", OrderContactPlant1."Contact No.");
            OrderContactPlant2.INSERT(TRUE);
          END;
        UNTIL OrderContactPlant1.NEXT = 0;
      END;
    END;

    PROCEDURE GetContactPerson@1100528503(PlantOrderNo@1100528504 : Code[20];VAR ContactPerson@1100528500 : Text[100];VAR ContactPhoneNo@1100528501 : Text[30];Location@1100528503 : 'ToLocation,FromLocation';TransportOrderNo@1100527353 : Code[20]);
    VAR
      PlantOrderContact@1100528502 : Record 11012536;
      PlantOrder@1100527350 : Record 11012556;
      PlantTransportOrder@1100527352 : Record 11020505;
    BEGIN
      PlantOrder.GET(PlantOrderNo);

      ContactPerson := '';
      ContactPhoneNo := '';

      IF Location = Location::ToLocation THEN
        PlantOrderContact.SETRANGE(PlantOrderContact.Type, PlantOrderContact.Type::"PO To")
      ELSE
        PlantOrderContact.SETRANGE(PlantOrderContact.Type, PlantOrderContact.Type::"PO From");

      PlantOrderContact.SETRANGE("Order No.", PlantOrder."No.");
      IF PlantOrderContact.FINDFIRST THEN BEGIN
        PlantOrderContact.CALCFIELDS(Name, "Mobile Phone No.", "Phone No.");
        ContactPerson := PlantOrderContact.Name;
        ContactPhoneNo := PlantOrderContact."Mobile Phone No.";
        IF ContactPhoneNo = '' THEN
          ContactPhoneNo := PlantOrderContact."Phone No.";
      END ELSE BEGIN
        IF TransportOrderNo <> '' THEN BEGIN
          IF PlantTransportOrder.GET(TransportOrderNo) THEN BEGIN
            ContactPerson := PlantTransportOrder."To Location Contact";
            ContactPhoneNo := PlantOrder."To Location Contact Phone No"
          END;
        END ELSE BEGIN
          IF Location = Location::ToLocation THEN BEGIN
            ContactPerson := PlantOrder."To Location Contact";
            ContactPhoneNo := PlantOrder."To Location Contact Phone No";
          END ELSE BEGIN
            ContactPerson  := PlantOrder."From Location Contact";
            ContactPhoneNo := PlantOrder."From Location Contact Phone No";
          END;
        END;
      END;
    END;

    BEGIN
    {
      LocContRec."Receive E-Mail"
    }
    END.
  }
}

