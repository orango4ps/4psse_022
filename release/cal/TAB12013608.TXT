OBJECT Table 12013608 EX Document
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF513000;
  }
  PROPERTIES
  {
    Permissions=TableData 12013608=rimd,
                TableData 12013609=rimd,
                TableData 12013610=rimd,
                TableData 12013611=rimd;
    OnInsert=BEGIN
               "Change Date" := CREATEDATETIME(TODAY,TIME);
               "Changed By" := USERID;
             END;

    OnModify=BEGIN
               "Change Date" := CREATEDATETIME(TODAY,TIME);
               "Changed By" := USERID;
             END;

    OnDelete=BEGIN
               DeleteExDoc;
             END;

    CaptionML=[ENU=Document;
               SVE=Dokument];
    LookupPageID=Page12013633;
    DrillDownPageID=Page12013633;
  }
  FIELDS
  {
    { 2   ;   ;Document Type       ;Option        ;CaptionML=[ENU=Document Type;
                                                              SVE=Dokumenttyp];
                                                   OptionCaptionML=[ENU=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order,Posted Invoice,Posted Credit Memo,Requisition;
                                                                    SVE=Offert,Order,Faktura,Kreditnota,Avropsorder,Returorder,Bokfîrd faktura,Bokfîrd kreditnota,Rekvisition];
                                                   OptionString=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order,Posted Invoice,Posted Credit Memo,Requisition }
    { 3   ;   ;Document No.        ;Code20        ;CaptionML=[ENU=Document No.;
                                                              SVE=Dokumentnr] }
    { 10  ;   ;Initiator           ;Code50        ;TableRelation="EX User Group".Code WHERE (Blocked=CONST(No));
                                                   CaptionML=[ENU=Assigned Purchasing Group;
                                                              SVE=Tilldelad inkîpargrupp] }
    { 11  ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor;
                                                   CaptionML=[ENU=Vendor No.;
                                                              SVE=Leverantîrsnr] }
    { 12  ;   ;Admin Comment       ;Text180       ;CaptionML=[ENU=Comments;
                                                              SVE=Kommentar] }
    { 13  ;   ;Create Date         ;Date          ;CaptionML=[ENU=Create Date;
                                                              SVE=UpplÑggningsdatum] }
    { 14  ;   ;Due Date            ;Date          ;CaptionML=[ENU=Due Date;
                                                              SVE=Fîrfallodatum] }
    { 15  ;   ;Posting Date        ;Date          ;CaptionML=[ENU=Posting Date;
                                                              SVE=Bokfîringsdatum] }
    { 16  ;   ;Document Date       ;Date          ;CaptionML=[ENU=Document Date;
                                                              SVE=Dokumentdatum] }
    { 17  ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=[ENU=Inactive,Active,Approved,Ready for posting,On hold,Received,Invoiced,Not released;
                                                                    SVE=Inaktiv,Aktiv,Slutattesterad,Klar att bokfîras,Stoppad,Inlevererad,Fakturerad,Ej slÑppt];
                                                   OptionString=Inactive,Active,Approved,Ready for posting,On hold,Received,Invoiced,Not released }
    { 18  ;   ;Predefind Posting No.;Code20       ;CaptionML=[ENU=Predefind Posting No.;
                                                              SVE=Fîrhandstilldelat nr] }
    { 19  ;   ;Vendor Document No. ;Code50        ;CaptionML=[ENU=Vendor Document No.;
                                                              SVE=Leverantîrens dokumentnr] }
    { 20  ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=[ENU=Currency Code;
                                                              SVE=Valutakod] }
    { 21  ;   ;Vendor Name         ;Text50        ;CaptionML=[ENU=Vendor Name;
                                                              SVE=Leverantîrsnamn] }
    { 22  ;   ;Image Name          ;Text250       ;CaptionML=[ENU=Image Name;
                                                              SVE=Bildnamn] }
    { 23  ;   ;Rejected            ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("EX Document Line" WHERE (Document Type=FIELD(Document Type),
                                                                                               Document No.=FIELD(Document No.),
                                                                                               Flowstatus=CONST(Rejected)));
                                                   CaptionML=[ENU=Rejected;
                                                              SVE=Avvisad];
                                                   Editable=No }
    { 24  ;   ;Net Amount          ;Decimal       ;CaptionML=[ENU=Net Amount;
                                                              SVE=Summa Netto];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 25  ;   ;VAT Amount          ;Decimal       ;CaptionML=[ENU=VAT Amount;
                                                              SVE=Moms summa];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 26  ;   ;ID                  ;BigInteger    ;AutoIncrement=Yes;
                                                   CaptionML=[ENU=ID;
                                                              SVE=ID];
                                                   Description=PK }
    { 27  ;   ;Expected Receipt Date;Date         ;CaptionML=[ENU=Expected Receipt Date;
                                                              SVE=FîrvÑntat inleveransdatum] }
    { 28  ;   ;Hold by Approver    ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("EX Document Line Approver" WHERE (Document Type=FIELD(Document Type),
                                                                                                        Document No.=FIELD(Document No.),
                                                                                                        Flowstatus=CONST(On hold)));
                                                   CaptionML=[ENU=On Hold;
                                                              SVE=PÜ bevakning];
                                                   Editable=No }
    { 29  ;   ;Batch No.           ;Code20        ;CaptionML=[ENU=Batch No.;
                                                              SVE=Batchnr];
                                                   Editable=No }
    { 40  ;   ;Gross Amount        ;Decimal       ;CaptionML=[ENU=Gross Amount;
                                                              SVE=Summa Brutto];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 50  ;   ;Assigned Receipt Group;Code50      ;TableRelation="EX User Group".Code WHERE (Blocked=CONST(No));
                                                   CaptionML=[ENU=Assigned Receipt Group;
                                                              SVE=Tilldelad inleveransgrupp] }
    { 60  ;   ;Ex Location ID      ;Integer       ;TableRelation="EX Location".ID;
                                                   OnValidate=VAR
                                                                ExLocation@1100285000 : Record 12013613;
                                                              BEGIN
                                                                IF "Ex Location ID" = 0 THEN
                                                                  "Text Field 10" := ''
                                                                ELSE BEGIN
                                                                  ExLocation.GET("Ex Location ID");
                                                                  "Text Field 10" := ExLocation.Name;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=ExFlow Delivery Address ID;
                                                              SVE=ExFlow Leveransaddress ID] }
    { 70  ;   ;Requisition Status  ;Option        ;CaptionML=[ENU=Requisition Status;
                                                              SVE=Rekvisitionsstatus];
                                                   OptionCaptionML=[ENU=Unsubmitted,Submitted to Vendor;
                                                                    SVE=Ej skickad,Skickad till leverantîr];
                                                   OptionString=Unsubmitted,Submitted to Vendor }
    { 71  ;   ;Requisition Archive ID;BigInteger  ;CaptionML=[ENU=Requisition Archive ID;
                                                              SVE=Rekvistionsarkivsid];
                                                   Editable=No }
    { 75  ;   ;Requisition Subtype ;Integer       ;TableRelation="Requisition subtype";
                                                   CaptionML=[ENU=Requisition Subtype;
                                                              SVE=Rekvisition subtyp] }
    { 76  ;   ;Requisition No. Series;Code10      ;TableRelation="No. Series";
                                                   CaptionML=[ENU=Requisition No. Series;
                                                              SVE=Rekvisition Nr-serie];
                                                   Editable=No }
    { 80  ;   ;Web Company ID      ;BigInteger    ;CaptionML=[ENU=Web Company ID;
                                                              SVE=Webb fîretagsID] }
    { 81  ;   ;Web ID              ;BigInteger    ;CaptionML=[ENU=Web ID;
                                                              SVE=Webb ID];
                                                   Description=ID+Web Company ID }
    { 500 ;   ;Project No.         ;Code20        ;CaptionML=[ENU=Project No.;
                                                              SVE=Projektnr] }
    { 501 ;   ;Error Message       ;Text250       ;CaptionML=[ENU=Error Message;
                                                              SVE=Felmeddelande] }
    { 502 ;   ;Text Field 1        ;Text100       ;CaptionML=[ENU=Reference No.;
                                                              SVE=Referensnummer] }
    { 503 ;   ;Text Field 2        ;Text100       ;CaptionML=[ENU=Text Field 2;
                                                              SVE=TextfÑlt 2] }
    { 504 ;   ;Date Field 1        ;Date          ;CaptionML=[ENU=Date Field 1;
                                                              SVE=DatumfÑlt 1] }
    { 505 ;   ;Date Field 2        ;Date          ;CaptionML=[ENU=Date Field 2;
                                                              SVE=DatumfÑlt 2] }
    { 510 ;   ;Text Field 3        ;Text100       ;CaptionML=[ENU=Text Field 3;
                                                              SVE=TextfÑlt 3] }
    { 511 ;   ;Text Field 4        ;Text100       ;CaptionML=[ENU=Text Field 4;
                                                              SVE=TextfÑlt 4] }
    { 512 ;   ;Text Field 5        ;Text100       ;CaptionML=[ENU=Text Field 5;
                                                              SVE=TextfÑlt 5] }
    { 513 ;   ;Text Field 6        ;Text100       ;CaptionML=[ENU=Text Field 6;
                                                              SVE=TextfÑlt 6] }
    { 514 ;   ;Text Field 7        ;Text100       ;CaptionML=[ENU=Text Field 7;
                                                              SVE=TextfÑlt 7] }
    { 515 ;   ;Text Field 8        ;Text100       ;CaptionML=[ENU=Text Field 8;
                                                              SVE=TextfÑlt 8] }
    { 516 ;   ;Text Field 9        ;Text100       ;CaptionML=[ENU=Text Field 9;
                                                              SVE=TextfÑlt 9] }
    { 517 ;   ;Text Field 10       ;Text100       ;CaptionML=[ENU=Text Field 10;
                                                              SVE=TextfÑlt 10] }
    { 550 ;   ;Date Field 3        ;DateTime      ;CaptionML=[ENU=Date Field 3;
                                                              SVE=DatumfÑlt 3] }
    { 551 ;   ;Date Field 4        ;DateTime      ;CaptionML=[ENU=Date Field 4;
                                                              SVE=DatumfÑlt 4] }
    { 560 ;   ;Decimal Field 1     ;Decimal       ;CaptionML=[ENU=Decimal Field 1;
                                                              SVE=DecimalfÑlt 1] }
    { 561 ;   ;Decimal Field 2     ;Decimal       ;CaptionML=[ENU=Decimal Field 2;
                                                              SVE=DecimalfÑlt 2] }
    { 562 ;   ;Decimal Field 3     ;Decimal       ;CaptionML=[ENU=Decimal Field 3;
                                                              SVE=DecimalfÑlt 3] }
    { 563 ;   ;Decimal Field 4     ;Decimal       ;CaptionML=[ENU=Decimal Field 4;
                                                              SVE=DecimalfÑlt 4] }
    { 600 ;   ;Approver Comments   ;BLOB          ;Compressed=No;
                                                   CaptionML=[ENU=Approver Header Comments;
                                                              SVE=Attestant huvud kommentarer] }
    { 601 ;   ;Changed By          ;Code50        ;CaptionML=[ENU=Changed By;
                                                              SVE=éndrad av];
                                                   Editable=No }
    { 602 ;   ;Change Date         ;DateTime      ;CaptionML=[ENU=Change Date;
                                                              SVE=éndrad datum];
                                                   Editable=No }
    { 603 ;   ;Web Approver Comments;BLOB         ;Compressed=No;
                                                   CaptionML=[ENU=Web Approver Header Comments;
                                                              SVE=Web attestant huvud kommentarer];
                                                   Description=Temp. Solution. Delete }
    { 650 ;   ;Created By          ;Code50        ;CaptionML=[ENU=Created By;
                                                              SVE=Skapad av];
                                                   Editable=No }
    { 651 ;   ;Created Date        ;DateTime      ;CaptionML=[ENU=Created Date;
                                                              SVE=Skapad datum];
                                                   Editable=No }
    { 700 ;   ;Change Jnl Processed PO;Boolean    ;CaptionML=[ENU=Change Jnl Processed Purchase Order;
                                                              SVE=éndringsjournal processad Inkîpsorder] }
    { 1500;   ;Has Attachments     ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("EX Attached File" WHERE (Document Type=FIELD(Document Type),
                                                                                               Document No.=FIELD(Document No.)));
                                                   CaptionML=[ENU=Has Attachments;
                                                              SVE=Har bilagor];
                                                   Editable=No }
    { 1501;   ;Has Approver Comments;Boolean      ;FieldClass=FlowField;
                                                   CalcFormula=Exist("EX Document Line Approver" WHERE (Document Type=FIELD(Document Type),
                                                                                                        Document No.=FIELD(Document No.),
                                                                                                        Comment=FILTER(<>''),
                                                                                                        Auto Approved=CONST(No)));
                                                   CaptionML=[ENU=Has Approver Line Comments;
                                                              SVE=Har attestant rad kommentarer];
                                                   Editable=No }
    { 1502;   ;Financial On Hold   ;Code3         ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purchase Header"."On Hold" WHERE (Document Type=FIELD(Document Type),
                                                                                                         No.=FIELD(Document No.)));
                                                   CaptionML=[ENU=Payment/Refund On Hold;
                                                              SVE=Betalning/èterbet stoppad];
                                                   Editable=No }
    { 1505;   ;Shortcut Dimension 1 Code;Code20   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purchase Header"."Shortcut Dimension 1 Code" WHERE (Document Type=FIELD(Document Type),
                                                                                                                           No.=FIELD(Document No.)));
                                                   TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   CaptionML=[ENU=Shortcut Dimension 1 Code;
                                                              SVE=GenvÑg dimension 1 kod];
                                                   Editable=No;
                                                   CaptionClass='1,2,1' }
    { 1506;   ;Shortcut Dimension 2 Code;Code20   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Purchase Header"."Shortcut Dimension 2 Code" WHERE (Document Type=FIELD(Document Type),
                                                                                                                           No.=FIELD(Document No.)));
                                                   TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   CaptionML=[ENU=Shortcut Dimension 2 Code;
                                                              SVE=GenvÑg dimension 2 kod];
                                                   Editable=No;
                                                   CaptionClass='1,2,2' }
    { 1510;   ;Line 1 Current Approver;Code50     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("EX Document Line Approver".Approver WHERE (Document ID=FIELD(ID),
                                                                                                                  Flowstatus=CONST(Current)));
                                                   CaptionML=[ENU=Current Approver (First line);
                                                              SVE=Nuvarande attestant (Fîrsta raden)];
                                                   Editable=No }
    { 1515;   ;Connected Order No. ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("EX Document Line"."Order No." WHERE (Document ID=FIELD(ID),
                                                                                                            Order No.=FILTER(<>'')));
                                                   CaptionML=[ENU=Connected Order No. (first);
                                                              SVE=Kopplat Ordernumer (Fîrsta)];
                                                   Editable=No }
    { 3000;   ;Temp Company Name   ;Text30        ;CaptionML=[ENU=Company Name;
                                                              SVE=Fîretagsnamn];
                                                   Description=Used temporarily;
                                                   Editable=No }
    { 4000;   ;Temp Source Table   ;Integer       ;CaptionML=[ENU=Temp Source Table;
                                                              SVE=Temp kÑlltabell];
                                                   Description=Used temporarily;
                                                   Editable=No }
    { 5000;   ;Temp ID             ;BigInteger    ;CaptionML=[ENU=Temp ID;
                                                              SVE=Temp ID];
                                                   Description=Used temporarily;
                                                   Editable=No }
    { 6000;   ;Temp Approval Date  ;DateTime      ;CaptionML=[ENU=Temp Approval Date;
                                                              SVE=Temp Attestdatum];
                                                   Description=Used temporarily;
                                                   Editable=No }
    { 12013595;;Import Document No.;Integer       ;CaptionML=[ENU=Import Document No.;
                                                              SVE=Import dokumentnr] }
    { 12013650;;Vendor Document No. 2;Code50      ;CaptionML=[ENU=Vendor Document No. 2;
                                                              SVE=Leverantîrens dokumentnr 2] }
    { 12013668;;Contract No.       ;Integer       ;TableRelation="ExFlow Contract Matching" WHERE (Vendor No.=FIELD(Vendor No.));
                                                   CaptionML=[ENU=Contract No.;
                                                              SVE=Avtalsnr.] }
    { 12013721;;Chang Journal Line Exists;Boolean ;FieldClass=FlowField;
                                                   CalcFormula=Exist("EX Change Journal Line" WHERE (Document Type=FIELD(Document Type),
                                                                                                     Document No.=FIELD(Document No.)));
                                                   CaptionML=[ENU=Change Journal Line Exists;
                                                              SVE=éndring Journal Rader finns] }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
    {    ;Web ID                                   }
    {    ;Document No.                             }
    {    ;Status,Document Type                     }
    {    ;Status,Document Type,Document No.        }
    {    ;Document Type,Document No.               }
    {    ;Due Date                                 }
    {    ;Posting Date                             }
    {    ;Initiator                                }
    {    ;Assigned Receipt Group                   }
    {    ;Create Date                              }
    {    ;Vendor Name                              }
    {    ;Created By                               }
    {    ;Vendor Document No.                      }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      EXF117@1100285010 : TextConst 'ENU=Detailed Import information does not exist.;SVE=Detaljerad Importinformation finns ej.';
      EXF118@1100285000 : TextConst 'ENU=No lines with the specified filter.;SVE=Rader saknas med angivet filter.';
      EXF120@1100285001 : TextConst 'ENU=Function is not supported by Document Type: %1.;SVE=Funktionen Ñr ej supporterad av dokumenttyp: %1';
      EXF121@1100285002 : TextConst 'ENU=Document is synchonized;SVE=Dokumentet Ñr synkroniserat';
      EXF122@1100285003 : TextConst 'ENU=You dont have permission to perform this action;SVE=Du saknar behîrigheter att utfîra ÜtgÑrden';

    PROCEDURE ShowApproverComments@1100285001();
    VAR
      ExFileMgt@1100285001 : Codeunit 12013602;
      ExFlowBlob@1100285003 : Record 12013591;
      TempFileName@1100285000 : Text[1024];
    BEGIN
      CALCFIELDS("Approver Comments");
      CALCFIELDS("Web Approver Comments");

      IF NOT ("Approver Comments".HASVALUE OR "Web Approver Comments".HASVALUE) THEN
        EXIT;

      TempFileName := ExFileMgt.ClientTempFileName(CREATEGUID,'.txt');

      IF "Approver Comments".HASVALUE THEN
         ExFlowBlob.BLOB := "Approver Comments"
      ELSE
        ExFlowBlob.BLOB := "Web Approver Comments";

      ExFileMgt.ShowBLOB(ExFlowBlob,TempFileName);

      HYPERLINK(TempFileName);
    END;

    PROCEDURE Navigate@1100285002();
    VAR
      NavigateForm@1100285000 : Page 344;
    BEGIN
      NavigateForm.SetDoc("Posting Date", "Document No.");
      NavigateForm.RUN;
    END;

    PROCEDURE ShowCard@1100285003();
    BEGIN
      Card;
    END;

    LOCAL PROCEDURE Card@1000000000();
    VAR
      PurchHeader@1100285000 : Record 38;
      PurchInvHeader@1100285001 : Record 122;
      PurchCrMemoHeader@1100285002 : Record 124;
    BEGIN
      PurchHeader.RESET;
      PurchHeader.SETRANGE("No.","Document No.");

      CASE "Document Type" OF
        "Document Type"::Invoice :
          BEGIN
            PurchHeader.SETRANGE("Document Type",PurchHeader."Document Type"::Invoice);
            PurchHeader.FINDFIRST;
            PAGE.RUNMODAL(51,PurchHeader);
          END;
        "Document Type"::"Credit Memo" :
          BEGIN
            PurchHeader.SETRANGE("Document Type",PurchHeader."Document Type"::"Credit Memo");
            PurchHeader.FINDFIRST;
            PAGE.RUNMODAL(52,PurchHeader);
          END;
        "Document Type"::Order :
          BEGIN
            PurchHeader.SETRANGE("Document Type",PurchHeader."Document Type"::Order);
            PurchHeader.FINDFIRST;
            PAGE.RUNMODAL(50,PurchHeader);
          END;
        "Document Type"::Quote :
          BEGIN
            PurchHeader.SETRANGE("Document Type",PurchHeader."Document Type"::Quote);
            PurchHeader.FINDFIRST;
            PAGE.RUNMODAL(49,PurchHeader);
          END;
        "Document Type"::"Posted Invoice" :
          BEGIN
            PurchInvHeader.SETRANGE("No.", "Document No.");
            PurchInvHeader.FINDFIRST;
            PAGE.RUNMODAL(138,PurchInvHeader);
          END;
        "Document Type"::"Posted Credit Memo" :
          BEGIN
            PurchCrMemoHeader.SETRANGE("No.", "Document No.");
            PurchCrMemoHeader.FINDFIRST;
            PAGE.RUNMODAL(140,PurchCrMemoHeader);
          END;
      END;
    END;

    PROCEDURE RetrieveRecord@1100285013(VAR ExDoc@1100285004 : Record 12013608;DocType@1100285001 : Integer;DocNo@1100285002 : Code[20]) : Boolean;
    BEGIN
      WITH ExDoc DO BEGIN
        RESET;
        CLEAR(ExDoc);
        SETCURRENTKEY("Document No.");
        SETRANGE("Document Type", DocType);
        SETRANGE("Document No.", DocNo);
        EXIT(FINDFIRST);
      END;
    END;

    PROCEDURE ShowImportCard@1100285004();
    VAR
      ExfImportedPurchHead@1100285000 : Record 12013650;
    BEGIN
      IF ExfImportedPurchHead.GET("Import Document No.") THEN
        PAGE.RUNMODAL(12013635,ExfImportedPurchHead)
      ELSE
        MESSAGE(EXF117);
    END;

    PROCEDURE DeleteExDoc@1100285005();
    VAR
      ExDeletedDoc@1100285003 : Record 12013622;
      ExDocLine@1100285000 : Record 12013609;
      PurchLine@1100285005 : Record 39;
      ExfWorkflowMgt@1100285001 : Codeunit 12013593;
    BEGIN
      ExDeletedDoc.INIT;
      ExDeletedDoc.TRANSFERFIELDS(Rec);
      ExDeletedDoc.ID := 0;
      ExDeletedDoc."Deleted By" := USERID;
      ExDeletedDoc."Deleted Date" := CREATEDATETIME(TODAY,TIME);
      ExDeletedDoc."Old ID" := ID;
      ExDeletedDoc.INSERT;

      PurchLine.RESET;
      IF PurchLine.SETCURRENTKEY("Document No.") THEN;
      PurchLine.SETRANGE("Document Type", "Document Type");
      PurchLine.SETRANGE("Document No.", "Document No.");
      IF NOT PurchLine.ISEMPTY THEN BEGIN
        PurchLine.MODIFYALL(Approve, FALSE);
        PurchLine.MODIFYALL("Applied Template Rule",'');
      END;

      ExDocLine.RESET;
      ExDocLine.SETCURRENTKEY("Document ID");
      ExDocLine.SETRANGE("Document ID", ID);
      IF ExDocLine.FINDSET(TRUE) THEN BEGIN
        REPEAT
          ExDocLine.DeleteLine(ExDeletedDoc.ID,Rec);
        UNTIL ExDocLine.NEXT = 0;

        ExDocLine.DELETEALL;
      END;

      ExfWorkflowMgt.RestoreDelExDocPDFandXML(Rec);
    END;

    PROCEDURE FilterOnApprover@1100285000(VAR ExDoc@1100285007 : Record 12013608);
    VAR
      lEXUserGroup@1100285006 : Record 12013606;
      lEXDocumentLineApprover@1100285004 : Record 12013610;
      lSelectApprover@1100285003 : Page 12013647;
      lApprover@1100285002 : Code[50];
      lStatus@1100285001 : Integer;
      TempPos@1100285009 : Integer;
      lMarked@1100285000 : Boolean;
    BEGIN
      CLEAR(lMarked);
      CLEAR(lSelectApprover);

      IF NOT (lSelectApprover.RUNMODAL = ACTION::OK) THEN
        EXIT;

      lApprover := lSelectApprover.GetApprover;
      lStatus := lSelectApprover.GetStatus;
      IF NOT lEXUserGroup.GET(lApprover) THEN BEGIN
        TempPos := STRPOS(lApprover,'\');
        IF TempPos <> 0 THEN BEGIN
          lApprover := COPYSTR(lApprover,TempPos + 1);
          IF NOT lEXUserGroup.GET(lApprover) THEN
            EXIT;
        END;
      END;

      lEXDocumentLineApprover.RESET;
      lEXDocumentLineApprover.SETCURRENTKEY(Approver,"Days to Due Date");

      CASE lStatus OF
        0 : lEXDocumentLineApprover.SETRANGE(Flowstatus,lEXDocumentLineApprover.Flowstatus::Current);
        1 : lEXDocumentLineApprover.SETRANGE(Flowstatus,lEXDocumentLineApprover.Flowstatus::"Not processed");
        2 : ;
      END;

      lEXDocumentLineApprover.SETRANGE(Approver,lApprover);
      ExDoc.ID := 0;

      IF lEXDocumentLineApprover.FINDSET THEN
        REPEAT
          IF lEXDocumentLineApprover."Document ID" <> ExDoc.ID THEN BEGIN
            IF ExDoc.GET(lEXDocumentLineApprover."Document ID") THEN BEGIN
              ExDoc.MARK(TRUE);
              lMarked := TRUE;
            END;
          END;
        UNTIL lEXDocumentLineApprover.NEXT = 0;

      IF lMarked THEN
        ExDoc.MARKEDONLY(TRUE)
      ELSE
        MESSAGE(EXF118);
    END;

    PROCEDURE GetApproverDocs@1100285007(VAR TempExDoc@1100285007 : TEMPORARY Record 12013608;lApprover@1100285000 : Code[50]);
    VAR
      AppSetup@1100285008 : Record 12013601;
      lEXUserGroup@1100285006 : Record 12013606;
      lEXDocument@1100285005 : Record 12013608;
      lEXDocumentLineApprover@1100285004 : Record 12013610;
      TempPos@1100285009 : Integer;
    BEGIN
      AppSetup.GET;

      TempExDoc.RESET;
      TempExDoc.DELETEALL;

      IF NOT lEXUserGroup.GET(lApprover) THEN BEGIN
        TempPos := STRPOS(lApprover,'\');
        IF TempPos <> 0 THEN BEGIN
          lApprover := COPYSTR(lApprover,TempPos + 1);
          IF NOT lEXUserGroup.GET(lApprover) THEN
            EXIT;
        END;
      END;

      lEXDocumentLineApprover.RESET;
      lEXDocumentLineApprover.SETCURRENTKEY(Approver,"Days to Due Date");
      lEXDocumentLineApprover.SETRANGE("Document Type", 0, 5); // No posted documents
      lEXDocumentLineApprover.SETRANGE(Approver,lApprover);
      lEXDocumentLineApprover.SETRANGE(Flowstatus,lEXDocumentLineApprover.Flowstatus::Current);
      IF lEXDocumentLineApprover.FINDSET THEN
        REPEAT
          IF NOT TempExDoc.GET(lEXDocumentLineApprover."Document ID") THEN BEGIN
            lEXDocument.GET(lEXDocumentLineApprover."Document ID");
            IF NOT lEXDocument."Change Jnl Processed PO" THEN
              IF lEXDocument.Status = lEXDocument.Status::Active THEN BEGIN
                TempExDoc.INIT;
                TempExDoc.TRANSFERFIELDS(lEXDocument);
                TempExDoc.INSERT;
              END;
          END;
        UNTIL lEXDocumentLineApprover.NEXT = 0;

      TempExDoc.SETCURRENTKEY("Document Type","Document No.");
      IF TempExDoc.FINDFIRST THEN;
    END;

    PROCEDURE ApproveDoc@1100285006(VAR TempExDoc@1100285001 : TEMPORARY Record 12013608;lApprover@1100285000 : Code[50]);
    VAR
      ExDocLineApprover@1100285003 : Record 12013610;
      DocLine@1100285002 : Record 12013609;
      ExFlow@1100285004 : Codeunit 12013601;
    BEGIN
      lApprover := ExFlow.GetUserGroup(lApprover);
      IF lApprover = '' THEN
        EXIT;

      ExDocLineApprover.RESET;
      ExDocLineApprover.SETCURRENTKEY("Document No.");
      ExDocLineApprover.SETRANGE("Document Type", "Document Type");
      ExDocLineApprover.SETRANGE("Document No.", "Document No.");
      ExDocLineApprover.SETRANGE(Approver,lApprover);
      IF ExDocLineApprover.FINDSET THEN
        REPEAT
          DocLine.GET(ExDocLineApprover."Document Line ID");
          DocLine.FinishOneLine(DocLine,ExDocLineApprover);
        UNTIL ExDocLineApprover.NEXT = 0;

      GetApproverDocs(TempExDoc,USERID);
    END;

    PROCEDURE RejectDoc@1100285009(VAR TempExDoc@1100285001 : TEMPORARY Record 12013608;lApprover@1100285000 : Code[50]);
    VAR
      ExDocLineApprover@1100285003 : Record 12013610;
      DocLine@1100285002 : Record 12013609;
      ExFlow@1100285004 : Codeunit 12013601;
      FinishComment@1100285005 : Text[180];
    BEGIN
      lApprover := ExFlow.GetUserGroup(lApprover);
      IF lApprover = '' THEN
        EXIT;

      ExDocLineApprover.RESET;
      ExDocLineApprover.SETCURRENTKEY("Document No.");
      ExDocLineApprover.SETRANGE("Document Type", "Document Type");
      ExDocLineApprover.SETRANGE("Document No.", "Document No.");
      ExDocLineApprover.SETRANGE(Approver,lApprover);
      IF ExDocLineApprover.FINDSET THEN
        REPEAT
          DocLine.GET(ExDocLineApprover."Document Line ID");
          DocLine.RejectOneLine(DocLine,ExDocLineApprover,FinishComment);
        UNTIL ExDocLineApprover.NEXT = 0;

      GetApproverDocs(TempExDoc,USERID);
    END;

    PROCEDURE OpenOrderNo@1100285010() : Code[20];
    VAR
      PurchHeader@1100285000 : Record 38;
    BEGIN
      CALCFIELDS("Connected Order No.");
      IF "Connected Order No." <> '' THEN BEGIN
        IF "Document Type" = "Document Type"::Invoice THEN BEGIN
          IF PurchHeader.GET(PurchHeader."Document Type"::Order,"Connected Order No.") THEN
            PAGE.RUNMODAL(50,PurchHeader);
        END
        ELSE
          IF "Document Type" = "Document Type"::"Credit Memo" THEN BEGIN
            IF PurchHeader.GET(PurchHeader."Document Type"::"Return Order","Connected Order No.") THEN
              PAGE.RUNMODAL(6640,PurchHeader);
          END;
      END;
    END;

    PROCEDURE GetPostingDesc@1100285008() : Text[50];
    VAR
      PurchHeader@1100285000 : Record 38;
      PurchInvHeader@1100285002 : Record 122;
      PurchCrMemoHeader@1100285001 : Record 124;
    BEGIN
      CASE "Document Type" OF
        "Document Type"::Invoice :
          BEGIN
            PurchHeader.RESET;
            PurchHeader.SETCURRENTKEY("No.");
            PurchHeader.SETRANGE("No.","Document No.");
            PurchHeader.SETRANGE("Document Type",PurchHeader."Document Type"::Invoice);
            IF PurchHeader.FINDFIRST THEN
                EXIT(PurchHeader."Posting Description");
          END;
        "Document Type"::"Credit Memo" :
          BEGIN
            PurchHeader.RESET;
            PurchHeader.SETCURRENTKEY("No.");
            PurchHeader.SETRANGE("No.","Document No.");
            PurchHeader.SETRANGE("Document Type",PurchHeader."Document Type"::"Credit Memo");
            IF PurchHeader.FINDFIRST THEN
                EXIT(PurchHeader."Posting Description");
          END;
        "Document Type"::"Posted Invoice" :
          BEGIN
            PurchInvHeader.RESET;
            PurchInvHeader.SETRANGE("No.", "Document No.");
            IF PurchInvHeader.FINDFIRST THEN
              EXIT(PurchInvHeader."Posting Description");
          END;
        "Document Type"::"Posted Credit Memo" :
          BEGIN
            PurchCrMemoHeader.RESET;
            PurchCrMemoHeader.SETRANGE("No.", "Document No.");
            IF PurchCrMemoHeader.FINDFIRST THEN
              EXIT(PurchCrMemoHeader."Posting Description");
          END;
      END;

      EXIT('');
    END;

    PROCEDURE GetCaption@1100285011() : Text[250];
    BEGIN
      EXIT(STRSUBSTNO('%1 %2',FORMAT("Document Type",0,0),"Document No."));
    END;

    PROCEDURE GetApproverDocsGroup@1100285012(VAR TempExDoc@1100285007 : TEMPORARY Record 12013608;lApprover@1100285000 : Code[50]);
    VAR
      AppSetup@1100285008 : Record 12013601;
      lEXUserGroupLine@1100285001 : Record 12013607;
      lEXDocument@1100285005 : Record 12013608;
      lEXDocumentLineApprover@1100285004 : Record 12013610;
      TempPos@1100285009 : Integer;
    BEGIN

      AppSetup.GET;

      TempPos := STRPOS(lApprover,'\');
      IF TempPos <> 0 THEN
        lApprover := COPYSTR(lApprover,TempPos + 1);

      TempExDoc.RESET;
      TempExDoc.DELETEALL;

      lEXUserGroupLine.RESET;
      lEXUserGroupLine.SETRANGE("User ID",lApprover);
      IF NOT lEXUserGroupLine.FINDSET THEN EXIT;

      REPEAT
        lEXDocumentLineApprover.RESET;
        lEXDocumentLineApprover.SETCURRENTKEY(Approver,"Days to Due Date");
        lEXDocumentLineApprover.SETRANGE("Document Type", 0, 5); // No posted documents
        lEXDocumentLineApprover.SETRANGE(Approver,lEXUserGroupLine.Code);
        lEXDocumentLineApprover.SETRANGE(Flowstatus,lEXDocumentLineApprover.Flowstatus::Current);
        IF lEXDocumentLineApprover.FINDSET THEN
          REPEAT
            IF NOT TempExDoc.GET(lEXDocumentLineApprover."Document ID") THEN BEGIN
              lEXDocument.GET(lEXDocumentLineApprover."Document ID");
              IF NOT lEXDocument."Change Jnl Processed PO" THEN
                IF lEXDocument.Status = lEXDocument.Status::Active THEN BEGIN
                  TempExDoc.INIT;
                  TempExDoc.TRANSFERFIELDS(lEXDocument);
                  TempExDoc.INSERT;
                END;
            END;
          UNTIL lEXDocumentLineApprover.NEXT = 0;
      UNTIL lEXUserGroupLine.NEXT = 0;

      TempExDoc.SETCURRENTKEY("Document Type","Document No.");
      IF TempExDoc.FINDFIRST THEN;
    END;

    PROCEDURE ApproveDocGroup@1100285014(VAR TempExDoc@1100285001 : TEMPORARY Record 12013608;lApprover@1100285000 : Code[50]);
    VAR
      ExDocLineApprover@1100285003 : Record 12013610;
      DocLine@1100285002 : Record 12013609;
      ExFlow@1100285004 : Codeunit 12013601;
      lEXUserGroupLine@1100285005 : Record 12013607;
    BEGIN

      lApprover := ExFlow.GetUserGroup(lApprover);
      IF lApprover = '' THEN EXIT;

      ExDocLineApprover.RESET;
      ExDocLineApprover.SETCURRENTKEY("Document No.");
      ExDocLineApprover.SETRANGE("Document Type", "Document Type");
      ExDocLineApprover.SETRANGE("Document No.", "Document No.");
      IF ExDocLineApprover.FINDSET THEN REPEAT
        lEXUserGroupLine.RESET;
        lEXUserGroupLine.SETRANGE(Code,ExDocLineApprover.Approver);
        lEXUserGroupLine.SETRANGE("User ID",lApprover);
        IF lEXUserGroupLine.FINDSET THEN BEGIN
          DocLine.GET(ExDocLineApprover."Document Line ID");
          DocLine.FinishOneLine(DocLine,ExDocLineApprover);
        END;
      UNTIL ExDocLineApprover.NEXT = 0;

      GetApproverDocsGroup(TempExDoc,USERID);
    END;

    PROCEDURE RejectDocGroup@1100285016(VAR TempExDoc@1100285001 : TEMPORARY Record 12013608;lApprover@1100285000 : Code[50]);
    VAR
      ExDocLineApprover@1100285003 : Record 12013610;
      DocLine@1100285002 : Record 12013609;
      ExFlow@1100285004 : Codeunit 12013601;
      lEXUserGroupLine@1100285005 : Record 12013607;
      FinishComment@1100285006 : Text[180];
      FinishApprovalOneLineOption@1100285008 : Report 12013636;
      Work@1100285009 : Text[1024];
    BEGIN

      lApprover := ExFlow.GetUserGroup(lApprover);
      IF lApprover = '' THEN EXIT;

      CLEAR(FinishComment);
      ExDocLineApprover.RESET;
      ExDocLineApprover.SETCURRENTKEY("Document No.");
      ExDocLineApprover.SETRANGE("Document Type", "Document Type");
      ExDocLineApprover.SETRANGE("Document No.", "Document No.");
      IF ExDocLineApprover.FINDSET THEN BEGIN
        Work := FinishApprovalOneLineOption.RUNREQUESTPAGE;
        IF STRPOS(Work,'"FinishComment">') = 0 THEN
          Work := ''
        ELSE BEGIN
          Work := COPYSTR(Work,STRPOS(Work,'"FinishComment">') + 16);
          Work := COPYSTR(Work,1,STRPOS(Work,'</Field>')-1);
        END;
        FinishComment := Work;
        IF FinishComment <> '' THEN BEGIN
          FinishComment := COPYSTR(Work,1,MAXSTRLEN(FinishComment));
          REPEAT
            lEXUserGroupLine.RESET;
            lEXUserGroupLine.SETRANGE(Code,ExDocLineApprover.Approver);
            lEXUserGroupLine.SETRANGE("User ID",lApprover);
            IF lEXUserGroupLine.FINDSET THEN BEGIN
              DocLine.GET(ExDocLineApprover."Document Line ID");
              DocLine.RejectOneLine(DocLine,ExDocLineApprover,FinishComment);
            END;
          UNTIL (ExDocLineApprover.NEXT = 0) OR (FinishComment = '');
        END;
        GetApproverDocsGroup(TempExDoc,USERID);
      END;
    END;

    PROCEDURE SaveAdminCommentToApproverComment@1100285015();
    VAR
      CR@1100285000 : Char;
      LF@1100285006 : Char;
      TempText@1100285002 : Text[1024];
      Ostream@1100285003 : OutStream;
      InStream@1100285004 : InStream;
      ExFlow@1100285001 : Codeunit 12013601;
      ExUserID@1100285005 : Code[100];
      BigTxt@1100285007 : BigText;
      LineBreak@1100285009 : Text;
    BEGIN
      CR := 13;
      LF := 10;
      LineBreak := FORMAT(LF) + FORMAT(CR);
      TESTFIELD("Admin Comment");
      CALCFIELDS("Approver Comments");

      ExUserID := USERID;
      ExUserID := ExFlow.GetUserGroup(ExUserID);
      IF ExUserID = '' THEN
        ExUserID := USERID;

      IF "Approver Comments".HASVALUE THEN BEGIN
        "Approver Comments".CREATEINSTREAM(InStream);
        BigTxt.READ(InStream);
      END;

      TempText := STRSUBSTNO('%1%4%2%4%3%4%4',FORMAT(TODAY,0,9),"Admin Comment",FORMAT(ExUserID),LineBreak);
      BigTxt.ADDTEXT(TempText,1);

      "Admin Comment" := '';
      "Approver Comments".CREATEOUTSTREAM(Ostream);
      BigTxt.WRITE(Ostream);
      MODIFY;
    END;

    PROCEDURE Changes@1100285017() : Boolean;
    VAR
      ChangeJnlLine@1100285000 : Record 12013618;
      cu609@1100285001 : Codeunit 12013609;
      lEXEscalationManagement@1100285002 : Codeunit 12013625;
      AppSetup@1100285003 : Record 12013601;
      ExDoc@1100285005 : Record 12013608;
      TempExDoc@1100285006 : TEMPORARY Record 12013608;
    BEGIN
      AppSetup.GET;

      IF AppSetup.Requisition THEN
        REPORT.RUNMODAL(12013610,FALSE,FALSE,Rec);

      IF AppSetup."Purchase Order" THEN
        REPORT.RUNMODAL(12013609,FALSE,FALSE,Rec);

      cu609.CreateJournalLine(FALSE,Rec);
      COMMIT;

      ChangeJnlLine.RESET;
      IF ChangeJnlLine.FINDSET THEN BEGIN
        TempExDoc.RESET;
        TempExDoc.DELETEALL;
        REPEAT
          IF ExDoc.RetrieveRecord(ExDoc,ChangeJnlLine."Document Type",ChangeJnlLine."Document No.") THEN BEGIN
            TempExDoc.INIT;
            TempExDoc.TRANSFERFIELDS(ExDoc);
            IF TempExDoc.INSERT THEN;
          END;
        UNTIL ChangeJnlLine.NEXT = 0;
        PAGE.RUNMODAL(12013622,TempExDoc)
      END;

      IF AppSetup."Change Journal Escalate" THEN
        lEXEscalationManagement.Escalate;
    END;

    PROCEDURE ChangesForceSync@1100285018() : Boolean;
    VAR
      cu609@1100285001 : Codeunit 12013609;
      AppSetup@1100285003 : Record 12013601;
      TempExDoc@1100285004 : TEMPORARY Record 12013608;
    BEGIN
      AppSetup.GET;

      IF NOT ("Document Type" IN ["Document Type"::Invoice,"Document Type"::"Credit Memo"]) THEN
       ERROR(EXF120,"Document Type");

      cu609.CreateJournalLine(TRUE,Rec);
      COMMIT;
      CALCFIELDS("Chang Journal Line Exists");
      IF "Chang Journal Line Exists" THEN BEGIN
        TempExDoc.RESET;
        TempExDoc.DELETEALL;
        TempExDoc.INIT;
        TempExDoc.TRANSFERFIELDS(Rec);
        IF TempExDoc.INSERT THEN;
        PAGE.RUNMODAL(12013622,TempExDoc)
      END ELSE
        MESSAGE(EXF121);
    END;

    PROCEDURE ReplaceDocImage@1100285019();
    VAR
      PurchHeader@1100285000 : Record 38;
      ExFlow@1100285001 : Codeunit 12013601;
    BEGIN
      IF (Status = Status::"Ready for posting") OR (Status = Status::Approved) THEN
        IF NOT ExFlow.IsUserSuperior(USERID) THEN
          ERROR(EXF122);

      PurchHeader.GET("Document Type","Document No.");
      "Image Name" := ExFlow.GetImage(PurchHeader,TRUE);
      MODIFY(TRUE);
    END;

    PROCEDURE WebApproversCommentExist@1100285020() : Boolean;
    BEGIN
      CALCFIELDS("Web Approver Comments");
      CALCFIELDS("Approver Comments");

      IF "Web Approver Comments".HASVALUE OR "Approver Comments".HASVALUE THEN
        EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

