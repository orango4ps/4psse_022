OBJECT Codeunit 759 Job Chart Mgt
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      XAxisStringTxt@1001 : TextConst 'ENU=Job;NOR=Prosjekt;SVE=Projekt';
      Job@1000 : Record 11072003;
      TotalRevenueTxt@1002 : TextConst 'ENU=Total Revenue;NOR=Sum salgsinntekter;SVE=R”relsens int„kter totalt';
      TotalCostTxt@1003 : TextConst 'ENU=Total Cost;NOR=Kostbel›p;SVE=Totalkostnad';
      ProfitMarginTxt@1004 : TextConst 'ENU=Profit Margin;NOR=Fortjenestemargin;SVE=Vinstmarginal';
      JobCalcStatistics@1009 : Codeunit 11072019;
      CL@1008 : ARRAY [16] OF Decimal;
      PL@1007 : ARRAY [16] OF Decimal;
      ActualTotalCostTxt@1010 : TextConst 'ENU=Actual Total Cost;NOR=Faktisk kostbel›p;SVE=Faktisk total kostnad';
      BudgetTotalCostTxt@1011 : TextConst 'ENU=Budget Total Cost;NOR=Budsjettkostbel›p;SVE=Budgeterad total kostnad';
      CostVarianceTxt@1012 : TextConst 'ENU=Cost Variance;NOR=Kostavvik;SVE=Kostnadsvarians';
      ActualTotalPriceTxt@1015 : TextConst 'ENU=Actual Total Price;NOR=Faktisk salgsbel›p;SVE=Faktiskt totalpris';
      BudgetTotalPriceTxt@1014 : TextConst 'ENU=Budget Total Price;NOR=Budsjettsalgsbel›p;SVE=Budgeterat totalpris';
      PriceVarianceTxt@1013 : TextConst 'ENU=Price Variance;NOR=Prisavvik;SVE=Prisvarians';

    PROCEDURE CreateJobChart@1(VAR BusChartBuf@1000 : Record 485;VAR TempJob@1001 : TEMPORARY Record 11072003;ChartType@1002 : 'Point,,Bubble,Line,,StepLine,,,,,Column,StackedColumn,StackedColumn100,Area,,StackedArea,StackedArea100,Pie,Doughnut,,,Range,,,,Radar,,,,,,,,Funnel';JobChartType@1003 : 'Profitability,Actual to Budget Cost,Actual to Budget Price');
    BEGIN
      InitializeBusinessChart(BusChartBuf);
      SetJobRangeByMyJob(TempJob);
      IF NothingToShow(TempJob) THEN
        EXIT;

      AddMeasures(BusChartBuf,ChartType,JobChartType);
      SetXAxis(BusChartBuf);
      SetJobChartValues(BusChartBuf,TempJob,JobChartType);
    END;

    [External]
    PROCEDURE SetJobRangeByMyJob@10(VAR TempRangeJob@1000 : TEMPORARY Record 11072003);
    VAR
      MyJob@1001 : Record 9154;
    BEGIN
      TempRangeJob.DELETEALL;

      WITH MyJob DO BEGIN
        SETRANGE("User ID",USERID);
        SETRANGE("Exclude from Business Chart",FALSE);
        IF FINDSET THEN
          REPEAT
            IF Job.GET("Job No.") THEN BEGIN
              TempRangeJob := Job;
              TempRangeJob.INSERT;
            END;
          UNTIL NEXT = 0;
      END;
    END;

    [External]
    PROCEDURE DataPointClicked@7(VAR BusChartBuf@1000 : Record 485;VAR RangeJob@1001 : Record 11072003);
    BEGIN
      WITH BusChartBuf DO BEGIN
        FindCurrentJob(BusChartBuf,RangeJob);
        DrillDownJobValue(RangeJob);
      END;
    END;

    LOCAL PROCEDURE FindCurrentJob@11(VAR BusChartBuf@1001 : Record 485;VAR RangeJob@1000 : Record 11072003);
    BEGIN
      WITH RangeJob DO BEGIN
        FINDSET;
        NEXT(BusChartBuf."Drill-Down X Index");
      END;
    END;

    LOCAL PROCEDURE DrillDownJobValue@12(VAR RangeJob@1000 : Record 11072003);
    BEGIN
      PAGE.RUN(PAGE::"Job Card",RangeJob);
    END;

    LOCAL PROCEDURE NothingToShow@2(VAR RangeJob@1000 : Record 11072003) : Boolean;
    BEGIN
      WITH RangeJob DO
        EXIT(ISEMPTY);
    END;

    LOCAL PROCEDURE InitializeBusinessChart@3(VAR BusChartBuf@1000 : Record 485);
    BEGIN
      WITH BusChartBuf DO
        Initialize;
    END;

    LOCAL PROCEDURE AddMeasures@4(VAR BusChartBuf@1000 : Record 485;ChartType@1002 : 'Point,,Bubble,Line,,StepLine,,,,,Column,StackedColumn,StackedColumn100,Area,,StackedArea,StackedArea100,Pie,Doughnut,,,Range,,,,Radar,,,,,,,,Funnel';JobChartType@1003 : 'Profitability,Actual to Budget Cost,Actual to Budget Price');
    BEGIN
      WITH BusChartBuf DO
        CASE JobChartType OF
          JobChartType::Profitability:
            BEGIN
              AddMeasure(TotalRevenueTxt,1,"Data Type"::Decimal,ChartType);
              AddMeasure(TotalCostTxt,2,"Data Type"::Decimal,ChartType);
              AddMeasure(ProfitMarginTxt,3,"Data Type"::Decimal,ChartType);
            END;
          JobChartType::"Actual to Budget Cost":
            BEGIN
              AddMeasure(ActualTotalCostTxt,1,"Data Type"::Decimal,ChartType);
              AddMeasure(BudgetTotalCostTxt,2,"Data Type"::Decimal,ChartType);
              AddMeasure(CostVarianceTxt,3,"Data Type"::Decimal,ChartType);
            END;
          JobChartType::"Actual to Budget Price":
            BEGIN
              AddMeasure(ActualTotalPriceTxt,1,"Data Type"::Decimal,ChartType);
              AddMeasure(BudgetTotalPriceTxt,2,"Data Type"::Decimal,ChartType);
              AddMeasure(PriceVarianceTxt,3,"Data Type"::Decimal,ChartType);
            END;
        END;
    END;

    LOCAL PROCEDURE SetXAxis@5(VAR BusChartBuf@1000 : Record 485);
    BEGIN
      WITH BusChartBuf DO
        SetXAxis(XAxisStringTxt,"Data Type"::String);
    END;

    LOCAL PROCEDURE SetJobChartValues@6(VAR BusChartBuf@1000 : Record 485;VAR RangeJob@1001 : Record 11072003;JobChartType@1005 : 'Profitability,Actual to Budget Cost,Actual to Budget Price');
    VAR
      Index@1002 : Integer;
      JobRevenue@1003 : Decimal;
      JobCost@1004 : Decimal;
      ActualCost@1006 : Decimal;
      BudgetCost@1007 : Decimal;
      ActualPrice@1008 : Decimal;
      BudgetPrice@1009 : Decimal;
    BEGIN
      WITH RangeJob DO
        IF FINDSET THEN
          REPEAT
            CASE JobChartType OF
              JobChartType::Profitability:
                BEGIN
                  CalculateJobRevenueAndCost(RangeJob,JobRevenue,JobCost);
                  SetJobChartValue(BusChartBuf,RangeJob,Index,JobRevenue,JobCost,JobChartType);
                END;
              JobChartType::"Actual to Budget Cost":
                BEGIN
                  CalculateJobCosts(RangeJob,ActualCost,BudgetCost);
                  SetJobChartValue(BusChartBuf,RangeJob,Index,ActualCost,BudgetCost,JobChartType);
                END;
              JobChartType::"Actual to Budget Price":
                BEGIN
                  CalculateJobPrices(RangeJob,ActualPrice,BudgetPrice);
                  SetJobChartValue(BusChartBuf,RangeJob,Index,ActualPrice,BudgetPrice,JobChartType);
                END;
            END;
          UNTIL NEXT = 0;
    END;

    LOCAL PROCEDURE SetJobChartValue@8(VAR BusChartBuf@1000 : Record 485;VAR RangeJob@1001 : Record 11072003;VAR Index@1002 : Integer;Value1@1003 : Decimal;Value2@1004 : Decimal;JobChartType@1005 : 'Profitability,Actual to Budget Cost,Actual to Budget Price');
    BEGIN
      WITH BusChartBuf DO BEGIN
        AddColumn(RangeJob."No.");
        SetValueByIndex(0,Index,Value1);
        SetValueByIndex(1,Index,Value2);
        IF JobChartType = JobChartType::Profitability THEN
          SetValueByIndex(2,Index,(Value1 - Value2));
        IF (JobChartType = JobChartType::"Actual to Budget Cost") OR (JobChartType = JobChartType::"Actual to Budget Price") THEN
          SetValueByIndex(2,Index,(Value2 - Value1));
        Index += 1;
      END;
    END;

    [External]
    PROCEDURE CalculateJobRevenueAndCost@16(VAR RangeJob@1005 : Record 11072003;VAR JobRevenue@1001 : Decimal;VAR JobCost@1000 : Decimal);
    BEGIN
      CLEAR(JobCalcStatistics);
      JobCalcStatistics.JobCalculateCommonFilters(RangeJob);
      JobCalcStatistics.CalculateAmounts;
      JobCalcStatistics.GetLCYCostAmounts(CL);
      JobCalcStatistics.GetLCYPriceAmounts(PL);
      JobRevenue := PL[16];
      JobCost := CL[8];
    END;

    [External]
    PROCEDURE CalculateJobCosts@25(VAR RangeJob@1005 : Record 11072003;VAR ActualCost@1001 : Decimal;VAR BudgetCost@1000 : Decimal);
    BEGIN
      CLEAR(JobCalcStatistics);
      JobCalcStatistics.JobCalculateCommonFilters(RangeJob);
      JobCalcStatistics.CalculateAmounts;
      JobCalcStatistics.GetLCYCostAmounts(CL);
      JobCalcStatistics.GetLCYPriceAmounts(PL);
      ActualCost := CL[8];
      BudgetCost := CL[4];
    END;

    [External]
    PROCEDURE CalculateJobPrices@26(VAR RangeJob@1005 : Record 11072003;VAR ActualPrice@1001 : Decimal;VAR BudgetPrice@1000 : Decimal);
    BEGIN
      CLEAR(JobCalcStatistics);
      JobCalcStatistics.JobCalculateCommonFilters(RangeJob);
      JobCalcStatistics.CalculateAmounts;
      JobCalcStatistics.GetLCYCostAmounts(CL);
      JobCalcStatistics.GetLCYPriceAmounts(PL);
      ActualPrice := PL[16];
      BudgetPrice := PL[4];
    END;

    BEGIN
    END.
  }
}

