OBJECT Report 11012545 Close Purchase Orders
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Bestellung abschliessen;
               ENU=Close Purchase Orders;
               NLD=Inkooporders afsluiten;
               NOR=Lukk Innkj›psordre;
               SVE=St„ng ink”psorder];
    ProcessingOnly=Yes;
    UsageCategory=Tasks;
  }
  DATASET
  {
    { 4458;    ;DataItem;                    ;
               DataItemTable=Table38;
               DataItemTableView=SORTING(Document Type,No.)
                                 WHERE(Document Type=CONST(Order),
                                       Status=FILTER(<Closed));
               OnPreDataItem=BEGIN
                               IF NOT CONFIRM(Text001) THEN
                                 CurrReport.BREAK;

                               //ProjSetup.GET;  Project can be in another company
                               Window.OPEN(Text000);
                             END;

               OnAfterGetRecord=VAR
                                  ClosePurchaseOrder@1100525000 : Codeunit 11012246;
                                BEGIN
                                  Window.UPDATE(1,"No.");

                                  IF CheckCloseOrderAllowed THEN
                                    IF ClosePurchaseOrder.RUN("Purchase Header") THEN
                                      NoOrdersClosed := NoOrdersClosed + 1;
                                  //In ClosePurchaseOrder more checks are done and errors can be given.
                                  //To keep the process going in case one order can not be closed it is put in a separate codeunit.run
                                END;

               OnPostDataItem=BEGIN
                                MESSAGE(Text002, NoOrdersClosed);
                              END;

               ReqFilterFields=No.,Order Date,Posting Date,Status }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100409000;;Container;
                  ContainerType=ContentArea }

      { 1100409002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100409001;2;Field  ;
                  CaptionML=[DEU=Auch nicht vollst„ndig eingegangene/fakturierte Auftr„ge schlieáen;
                             ENU=Also close non-completely received/invoiced orders;
                             NLD=Ook niet volledig ontvangen/gefactureerde orders afsluiten];
                  SourceExpr=ForceClosing }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1100525000 : TextConst 'DEU=Verarbeiten Einkaufsbestellungen   #1##########;ENU=Processing purchase orders #1##########;NLD=Verwerken inkooporders   #1##########;NOR=Behandler innkj›psordre #1##########;SVE=Behandlar ink”psorder #1##########';
      Window@1100525001 : Dialog;
      Text001@1100525002 : TextConst 'DEU=M”chten Sie die Einkaufsbestellungen abschliessen?;ENU=Do you want to close the purchase orders?;NLD=Wilt u de inkooporders afsluiten?;NOR=Vil du lukke kj›psordrene?;SVE=Vill du st„nga ink”psorderna?';
      NoOrdersClosed@1100525004 : Integer;
      Text002@1100525005 : TextConst 'DEU=%1 Auftr„ge abgeschlossen.;ENU=%1 orders closed.;NLD=%1 orders afgesloten.;NOR=%1 ordre er lukket.;SVE=%1 order „r st„ngda.';
      ForceClosing@1100409000 : Boolean;

    PROCEDURE CheckCloseOrderAllowed@1100525000() CloseOrderAllowed : Boolean;
    VAR
      PurchLine@1100525000 : Record 39;
      CloseLineAllowed@1100525002 : Boolean;
    BEGIN
      CloseOrderAllowed := FALSE;

      //C015768.sn
      IF ForceClosing THEN
        CloseOrderAllowed := TRUE;

      IF NOT CloseOrderAllowed THEN
      //C015768.en
        WITH PurchLine DO BEGIN
          SETRANGE("Document Type", "Purchase Header"."Document Type");
          SETRANGE("Document No.", "Purchase Header"."No.");
          IF FINDSET THEN BEGIN
            REPEAT
              CloseLineAllowed := CheckCloseLineAllowed(PurchLine);
            UNTIL NOT CloseLineAllowed OR (NEXT = 0);
            CloseOrderAllowed := CloseLineAllowed;
          END;
        END;
    END;

    PROCEDURE CheckCloseLineAllowed@1100525001(PurchLine@1100525000 : Record 39) : Boolean;
    VAR
      ProjSetup@1210190000 : Record 315;
      Project@1100525001 : Record 11072003;
      ServiceOrder@1100525002 : Record 11012823;
    BEGIN
      WITH PurchLine DO BEGIN
        IF Type = 0 THEN
          EXIT(TRUE);

        IF "Job No." <> '' THEN BEGIN
          IF "Receiving Company" <> '' THEN BEGIN
            ProjSetup.CHANGECOMPANY("Receiving Company");
            Project.CHANGECOMPANY("Receiving Company");
          END;
          ProjSetup.GET;
          IF Project.GET("Job No.") THEN BEGIN
            IF ProjSetup."Provisions at Closure" THEN BEGIN
              IF Project."Project Status" >= Project."Project Status"::Archive THEN
                EXIT(TRUE);
            END ELSE
              IF Project."Project Status" >= Project."Project Status"::Finished THEN
                EXIT(TRUE);
          END ELSE
            EXIT(TRUE);
        END;

        IF "Service Order No." <> '' THEN BEGIN
          IF "Receiving Company" <> '' THEN
            ServiceOrder.CHANGECOMPANY("Receiving Company");
          //IF ServiceOrder.GET("Job No.") THEN BEGIN
          IF ServiceOrder.GET("Service Order No.") THEN BEGIN
            IF ServiceOrder.Status >= ServiceOrder.Status::Finished THEN
              EXIT(TRUE);
          END ELSE
            EXIT(TRUE);
        END;

        IF "Purchase Header"."Amounts only" THEN BEGIN
          IF ("Amnt. Invoiced" = 0) OR
             ("Outstanding Amount" <> 0) OR
             ("Amt. Rcd. Not Invoiced" <> 0) THEN
            EXIT(FALSE);
        END ELSE BEGIN
          IF ("Quantity Invoiced" = 0) OR
             ("Outstanding Quantity" <> 0) OR
             ("Qty. Rcd. Not Invoiced" <> 0) THEN
            EXIT(FALSE);
          IF PurchLine.Type = PurchLine.Type::"Charge (Item)" THEN
            IF (PurchLine."Qty. Assigned" <> PurchLine."Quantity Invoiced") OR
               (PurchLine."Qty. Assigned" = 0) THEN
              EXIT(FALSE);
        END;
        EXIT(TRUE);
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

