OBJECT Codeunit 6086387 Mileage - Complete
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.3.00.01;
  }
  PROPERTIES
  {
    TableNo=6086338;
    OnRun=BEGIN
            ShowError := TRUE;
            Code(Rec);
          END;

  }
  CODE
  {
    VAR
      ShowError@1160040001 : Boolean;
      NewerVersionExists@1160040002 : TextConst 'DEU=Eine neuere Version ist verfgbar in Continia Online. M”chten Sie synchronisieren und nochmals versenden?;ENU=A newer version might exist in Continia Online. Do you want to synchronize and send again?;NLD=Een nieuwere versie is beschikbaar in Continia Online. Wilt u synchroniseren en opnieuw verzenden?';
      SendingDocCompleted@1160040000 : TextConst 'DEU=Das Versenden des Belegs ist vollendet...;ENU=Reopening Mileage...;NLD=Het verzenden van document voltooid...';

    LOCAL PROCEDURE Code@1160040020(VAR Mileage@1160040000 : Record 6086338);
    VAR
      EMOnlineMgt@1160040002 : Codeunit 6086305;
      MilValidate@1160040005 : Codeunit 6086345;
      EMAppBridge@1160040001 : Codeunit 6086369;
      Window@1160040003 : Dialog;
    BEGIN
      IF Mileage.Status = Mileage.Status::Open THEN
        EXIT;

      Mileage.CheckInboxAndThrowError;

      IF GUIALLOWED AND (Mileage."Settlement No." = '') THEN
        Window.OPEN(STRSUBSTNO(SendingDocCompleted,FORMAT(Mileage."Entry No.")));

      IF Mileage."Settlement No." = '' THEN BEGIN
        EMAppBridge.CancelApprovalRequest(DATABASE::Mileage,FORMAT(Mileage."Entry No."));
        Mileage.GET(Mileage."Entry No.");
      END;

      IF Mileage.Status = Mileage.Status::"Pending Expense User" THEN
        IF HasPermissionToSync AND (Mileage."Settlement No." = '') AND GUIALLOWED THEN BEGIN
          IF NOT EMOnlineMgt.DeprecateMileage(Mileage."Mileage GUID",Mileage."Continia Online Version No.",FALSE) THEN BEGIN
            IF CONFIRM(NewerVersionExists) THEN BEGIN
              CODEUNIT.RUN(CODEUNIT::"EM Online Mgt.");
              Mileage.GET(Mileage."Entry No.");
            END;

            EMOnlineMgt.DeprecateMileage(Mileage."Mileage GUID",Mileage."Continia Online Version No.",TRUE);
          END;
        END ELSE
          EMOnlineMgt.DeprecateMileage(Mileage."Mileage GUID",Mileage."Continia Online Version No.",ShowError);

      //A SHADOW EXIST FOR HISTORY PURPOSE. MAKE SURE THAT ONE IS FORGOTTEN (NO ERROR)
      IF Mileage.Status > Mileage.Status::"Pending Expense User" THEN
        EMOnlineMgt.DeprecateMileage(Mileage."Mileage GUID",'',FALSE);

      Mileage.Status := Mileage.Status::Open;
      Mileage."Mileage Completed" := TRUE;
      Mileage.SetSkipSendToExpUser(TRUE);
      Mileage.MODIFY(TRUE);

      MilValidate.RUN(Mileage);

      IF GUIALLOWED AND (Mileage."Settlement No." = '') THEN
        Window.CLOSE;
    END;

    LOCAL PROCEDURE HasPermissionToSync@1160040002() : Boolean;
    VAR
      ExpNavVersionMgt@1160040000 : Codeunit 6086319;
    BEGIN
      IF ExpNavVersionMgt.HasPermission('EM-SUPER') THEN
        EXIT(TRUE);

      IF ExpNavVersionMgt.HasPermission('SUPER') THEN
        EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

