OBJECT Table 11229341 Customer Webservice URL
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ApplicationSystemConstants@1100525000 : Codeunit 9015;
             BEGIN
               "Application Id" := COPYSTR(ApplicationSystemConstants.ApplicationVersion,1,MAXSTRLEN("Application Id" ));
               IF (Type <> 0) THEN
                 FillDefaults;

               InsertConsumedWebService;
             END;

    OnModify=BEGIN
               ModifyConsumedWebService;
             END;

    OnDelete=BEGIN
               DeleteConsumedWebService;
             END;

    CaptionML=ENU=Customer Webservice URL;
  }
  FIELDS
  {
    { 10  ;   ;Customer No.        ;Code20        ;TableRelation=Customer;
                                                   CaptionML=[ENU=Customer No.;
                                                              NOR=Kundenr;
                                                              SVE=Kundnr];
                                                   NotBlank=Yes }
    { 20  ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Type;
                                                              SVE=Typ];
                                                   OptionCaptionML=ENU=" ,Invoice (INSBOU),Maintenance Status (INSBOU)";
                                                   OptionString=[ ,INVOIC,MTNSTA] }
    { 40  ;   ;URL                 ;Text250       ;ExtendedDatatype=URL;
                                                   CaptionML=[ENU=URL;
                                                              NOR=URL;
                                                              SVE=URL] }
    { 50  ;   ;User Domain         ;Text30        ;CaptionML=ENU=User Domain }
    { 60  ;   ;User Name           ;Text30        ;CaptionML=[ENU=User Name;
                                                              NOR=Brukernavn;
                                                              SVE=Anv„ndarnamn] }
    { 70  ;   ;Password            ;Text30        ;ExtendedDatatype=Masked;
                                                   CaptionML=[ENU=Password;
                                                              NOR=Passord;
                                                              SVE=L”senord] }
    { 90  ;   ;Relation Id         ;Code30        ;CaptionML=ENU=Relation Id }
    { 100 ;   ;Consumed Web Service Code;Code20   ;TableRelation="Consumed Web Service";
                                                   CaptionML=ENU=Consumed Web Service Code;
                                                   Editable=No }
    { 110 ;   ;Application Id      ;Code50        ;CaptionML=[ENU=Application Id;
                                                              SVE=Program-ID] }
    { 120 ;   ;Version OLD         ;Code20        ;CaptionML=[ENU=Version;
                                                              NOR=Versjon;
                                                              SVE=Version];
                                                   Editable=No }
    { 130 ;   ;MessageService Version;Option      ;CaptionML=ENU=MessageService Version;
                                                   OptionCaptionML=ENU=" ,2.4,3.0,3.1";
                                                   OptionString=[ ,2.4,3.0,3.1] }
    { 140 ;   ;Message Version     ;Option        ;OnValidate=BEGIN
                                                                CheckTypeAndVersion;
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Message version;
                                                   OptionCaptionML=ENU=,INSBOU003,INSBOU004,SALES005Beta,SALES005;
                                                   OptionString=,003,004,005Beta,005 }
  }
  KEYS
  {
    {    ;Customer No.,Type                       ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text002@1100529900 : TextConst 'ENU=Version: %1 not allowed for message: %2.';

    LOCAL PROCEDURE InsertConsumedWebService@1100525001();
    VAR
      ConsumedWebService@1100525000 : Record 11229797;
    BEGIN
      IF ("Consumed Web Service Code" <> '') THEN
        FIELDERROR("Consumed Web Service Code");

      ConsumedWebService.INIT;
      ConsumedWebService.Code := ConsumedWebService.GetFreeConsumedWebServiceCode;
      ConsumedWebService.Description := STRSUBSTNO('INSBOU %1 %2', "Customer No.", FORMAT(Type));
      FillFieldsConsumedWebService(ConsumedWebService);
      ConsumedWebService.INSERT(TRUE);

      "Consumed Web Service Code" := ConsumedWebService.Code;
      ConsumedWebService.InsertConsumedWebServiceLines(ConsumedWebService,"MessageService Version");
    END;

    LOCAL PROCEDURE ModifyConsumedWebService@1100525002();
    VAR
      ConsumedWebService@1100525000 : Record 11229797;
      JustInserted@1100525001 : Boolean;
      MessageVersion@1100529900 : Text;
    BEGIN
      JustInserted := FALSE;
      IF ("Consumed Web Service Code" = '') THEN BEGIN
        InsertConsumedWebService;
        JustInserted := TRUE;
      END;

      IF (NOT ConsumedWebService.GET("Consumed Web Service Code")) THEN BEGIN
        InsertConsumedWebService;
        JustInserted := TRUE;
        ConsumedWebService.GET("Consumed Web Service Code");
      END;

      IF ( NOT JustInserted AND (Rec."MessageService Version" <> xRec."MessageService Version")) THEN BEGIN
        ConsumedWebService.DeleteConsumedWebServiceLines("Consumed Web Service Code");
        ConsumedWebService.InsertConsumedWebServiceLines(ConsumedWebService,"MessageService Version");
      END;

      CASE "Message Version" OF
        "Message Version"::"003":
          MessageVersion := '003';
        "Message Version"::"004":
          MessageVersion := '004';
        "Message Version"::"005",
        "Message Version"::"005Beta":
          MessageVersion := '005';
      END;
      FillFieldsConsumedWebService(ConsumedWebService);
      ConsumedWebService.FillHeaderXML(ConsumedWebService,"MessageService Version", MessageVersion, "Application Id", "Relation Id");
      ConsumedWebService.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE DeleteConsumedWebService@1100525003();
    VAR
      ConsumedWebService@1100525000 : Record 11229797;
    BEGIN
      IF ("Consumed Web Service Code" = '') THEN
        EXIT;

      ConsumedWebService.DeleteConsumedWebServiceLines("Consumed Web Service Code");
      ConsumedWebService.DeleteConsumedWebService("Consumed Web Service Code");
    END;

    LOCAL PROCEDURE FillFieldsConsumedWebService@1100525005(VAR ConsumedWebService@1100525000 : Record 11229797);
    VAR
      StringFunctions@1100528300 : Codeunit 11012273;
      StartPosition2@1100528502 : Integer;
      EndPosition1@1100528501 : Integer;
      EndPosition2@1100528500 : Integer;
    BEGIN
      ConsumedWebService.VALIDATE(Login, "User Name");
      IF ("User Domain" <> '') THEN
        ConsumedWebService.VALIDATE(Login, "User Domain" + '\' + "User Name")
      ELSE
        ConsumedWebService.VALIDATE(Login, "User Name");
      ConsumedWebService.VALIDATE(Password, Password);
      ConsumedWebService.VALIDATE("Web Service URL", URL);
      ConsumedWebService.Active := TRUE;
      ConsumedWebService."Log Communication" := TRUE;
      ConsumedWebService."Content Type" := 'text/xml; charset=utf-8';
      IF (URL <> '') THEN BEGIN
        EndPosition1 := STRPOS(URL, '//') + 1;
        StartPosition2 := EndPosition1 + 1;
        EndPosition2 := StringFunctions.RSTRPOS(URL, '/')- EndPosition1;
        IF (EndPosition1 > 0) AND (EndPosition2 > 0) THEN
          ConsumedWebService."SOAP Action" := LOWERCASE(COPYSTR(URL, 1, EndPosition1) + 'www.' + COPYSTR(URL, StartPosition2, EndPosition2));
      END;
    END;

    LOCAL PROCEDURE FillDefaults@1100528300();
    VAR
      CustomerINSBOULogin@1100528301 : Record 11229341;
    BEGIN
      IF (NOT CustomerINSBOULogin.GET("Customer No.", 0)) THEN
        EXIT;

      URL := CustomerINSBOULogin.URL;
      "User Domain" := CustomerINSBOULogin."User Domain";
      "User Name" := CustomerINSBOULogin."User Name";
      Password := CustomerINSBOULogin.Password;
      "Message Version" := CustomerINSBOULogin."Message Version";
      "MessageService Version" := CustomerINSBOULogin."MessageService Version";
      IF CustomerINSBOULogin."MessageService Version" = "MessageService Version"::" " THEN
        "MessageService Version" := "MessageService Version"::"2.4";
    END;

    LOCAL PROCEDURE CheckTypeAndVersion@1100529902();
    BEGIN
      IF Type = Type::INVOIC THEN
        IF NOT ("Message Version" IN ["Message Version"::"003","Message Version"::"004","Message Version"::"005"]) THEN
          ERROR(Text002, STRSUBSTNO('%1',"Message Version"), STRSUBSTNO('%1',Type));
      IF Type = Type::MTNSTA THEN
        IF NOT ("Message Version" IN ["Message Version"::"005Beta","Message Version"::"005"]) THEN
          ERROR(Text002, STRSUBSTNO('%1',"Message Version"), STRSUBSTNO('%1',Type));
    END;

    BEGIN
    END.
  }
}

