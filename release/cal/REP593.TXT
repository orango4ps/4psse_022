OBJECT Report 593 Intrastat - Make Disk Tax Auth
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PSSE;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Intrastat - Make Disk Tax Auth;
               NOR=Intrastat - ikke i Norge;
               SVE=Intrastat - skapa fil];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   GLSetup.GET;
                   Finnishlocalizationactive := GLSetup."Finnish localization active";
                   DKinfoEnable := GLSetup."Danish Localization Active";
                 END;

    OnPreReport=BEGIN
                  FileName := FileMgt.ServerTempFileName('');

                  IntrastatJnlLine4.COPYFILTERS("Intrastat Jnl. Line");
                  IF FileName = '' THEN
                    ERROR(Text000);
                  IntraFile.TEXTMODE := TRUE;
                  IntraFile.WRITEMODE := TRUE;
                  IntraFile.CREATE(FileName);


                  IF NOT Finnishlocalizationactive THEN BEGIN
                    IF ("Intrastat Jnl. Line".GETFILTER(Type) <> FORMAT("Intrastat Jnl. Line".Type::Receipt)) AND
                     ("Intrastat Jnl. Line".GETFILTER(Type) <> FORMAT("Intrastat Jnl. Line".Type::Shipment))
                    THEN
                      ERROR(Text11200,"Intrastat Jnl. Line".FIELDCAPTION(Type));
                  END;
                END;

  }
  DATASET
  {
    { 2880;    ;DataItem;                    ;
               DataItemTable=Table262;
               DataItemTableView=SORTING(Journal Template Name,Name);
               OnPreDataItem=BEGIN
                               IntrastatJnlLine4.COPYFILTER("Journal Template Name","Journal Template Name");
                               IntrastatJnlLine4.COPYFILTER("Journal Batch Name",Name);
                             END;

               OnAfterGetRecord=BEGIN
                                  IF NOT Finnishlocalizationactive THEN
                                    TESTFIELD(Reported,FALSE);
                                  IntraReferenceNo := "Statistics Period" + '000000';
                                END;

               ReqFilterFields=Journal Template Name,Name }

    { 9905;1   ;DataItem;                    ;
               DataItemTable=Table263;
               DataItemTableView=SORTING(Type,Country/Region Code,Tariff No.,Transaction Type,Transport Method);
               OnPreDataItem=BEGIN
                               IF Finnishlocalizationactive THEN
                                 "Intrastat Jnl. Line".SETRANGE(Type,IntraJnlLineType);
                             END;

               OnAfterGetRecord=BEGIN
                                  IF Finnishlocalizationactive THEN BEGIN
                                          TESTFIELD("Transaction Type");
                                          IF IntraJnlLineType = 0 THEN
                                            "Intrastat Jnl. Batch".TESTFIELD("Reported Receipt",FALSE)
                                          ELSE
                                            IF IntraJnlLineType = 1 THEN
                                              "Intrastat Jnl. Batch".TESTFIELD("Reported Shipment",FALSE);
                                  END;

                                  IF ("Tariff No." = '') AND
                                     ("Country/Region Code" = '') AND
                                     ("Transaction Type" = '') AND
                                     //("Transport Method" = '') AND
                                     ("Statistical Value" = 0) AND
                                     ("Total Weight" = 0)
                                  THEN
                                    CurrReport.SKIP;

                                  TESTFIELD("Tariff No.");
                                  TESTFIELD("Country/Region Code");
                                  TESTFIELD("Transaction Type");
                                  TESTFIELD("Total Weight");

                                  TESTFIELD("Statistical Value");
                                  IF STRLEN(FORMAT(ROUND(Quantity,1),0,'<Sign><Integer><Decimals>')) > 11 THEN
                                    ERROR(Text11201,FIELDCAPTION(Quantity));
                                  IF STRLEN(FORMAT(ROUND("Total Weight",1),0,'<Sign><Integer><Decimals>')) > 11 THEN
                                    ERROR(Text11201,FIELDCAPTION("Total Weight"));
                                  IF STRLEN(FORMAT(ROUND("Statistical Value",1),0,'<Sign><Integer><Decimals>')) > 11 THEN
                                    ERROR(Text11201,FIELDCAPTION("Statistical Value"));

                                  IF "Supplementary Units" THEN
                                    TESTFIELD(Quantity);

                                  //NAVDK
                                  TransMeth := "Transport Method";
                                  IF TransMeth = '' THEN
                                    TransMeth := '0';

                                  CompoundField :=
                                    FORMAT("Country/Region Code",10) + FORMAT(DELCHR("Tariff No."),10) +
                                    FORMAT("Transaction Type",10) + FORMAT("Transport Method",10);

                                  IF (TempType <> Type) OR (STRLEN(TempCompoundField) = 0) THEN BEGIN
                                    TempType := Type;
                                    TempCompoundField := CompoundField;
                                    IntraReferenceNo := COPYSTR(IntraReferenceNo,1,4) + FORMAT(Type,1,2) + '01001';
                                  END ELSE
                                    IF (TempCompoundField <> CompoundField) THEN BEGIN
                                      TempCompoundField := CompoundField;
                                      IF COPYSTR(IntraReferenceNo,8,3) = '999' THEN
                                        IntraReferenceNo := INCSTR(COPYSTR(IntraReferenceNo,1,7)) + '001'
                                      ELSE
                                        IntraReferenceNo := INCSTR(IntraReferenceNo);
                                    END;

                                  "Internal Ref. No." := IntraReferenceNo;
                                  MODIFY;
                                END;

               ReqFilterFields=Type;
               DataItemLink=Journal Template Name=FIELD(Journal Template Name),
                            Journal Batch Name=FIELD(Name) }

    { 2177;1   ;DataItem;IntrastatJnlLine2   ;
               DataItemTable=Table263;
               DataItemTableView=SORTING(Internal Ref. No.);
               OnPreDataItem=BEGIN
                               IF Finnishlocalizationactive THEN
                               BEGIN
                                 CompanyInfo.GET;
                                 VATRegNo := CONVERTSTR(CompanyInfo."VAT Registration No.",Text001,'    ');

                                 IntrastatJnlLine2.COPYFILTERS("Intrastat Jnl. Line");

                                 SETRANGE("Internal Ref. No.",COPYSTR(IntraReferenceNo,1,4),COPYSTR(IntraReferenceNo,1,4)+'9');
                                 CurrReport.CREATETOTALS(Quantity,"Statistical Value","Total Weight");

                                 IntrastatJnlLine3.SETCURRENTKEY("Internal Ref. No.");
                               END
                               ELSE
                                 IF DKinfoEnable THEN BEGIN
                                   CompanyInfo.GET;
                                   VATRegNo := CONVERTSTR(CompanyInfo."VAT Registration No.",Text001,'    ');
                                   WriteToFile(FORMAT('00' + FORMAT(VATRegNo,8) + Text002,80));
                                   WriteToFile(FORMAT('0100004',80));

                                   SETRANGE("Internal Ref. No.",COPYSTR(IntraReferenceNo,1,4),COPYSTR(IntraReferenceNo,1,4) + '9');

                                   IntrastatJnlLine3.SETCURRENTKEY("Internal Ref. No.");
                                 END
                                 ELSE
                                               BEGIN
                                                 IntrastatJnlLine2.SETRANGE(Type,IntraJnlLineType);
                                                 CompanyInfo.GET;
                                                 IntraSetup.GET;
                                                 BusinessIdCode := CompanyInfo."Business Identity Code";
                                                 CompanyCode := IntraSetup."Company Serial No.";
                                                 FileNo := IntraSetup."File No.";
                                                 StatPeriod := "Intrastat Jnl. Batch"."Statistics Period";
                                                 StatCustChamber := IntraSetup."Custom Code";
                                                 LinePos := STRPOS(BusinessIdCode,'-');
                                                 LineNo := '00000';

                                                 IF "Intrastat Jnl. Line".Type = 0 THEN
                                                   FileType := 'A'
                                                 ELSE
                                                   FileType := 'D';

                                                 IF LinePos <> 0 THEN
                                                   BusinessIdCode := DELSTR(BusinessIdCode,LinePos,1);

                                                 IF IntraSetup."Last Transfer Date" = TODAY THEN
                                                   IntraSetup."File No." := INCSTR(IntraSetup."File No.")
                                                 ELSE BEGIN
                                                   IntraSetup."Last Transfer Date" := TODAY;
                                                   IntraSetup."File No." := '001';
                                                 END;

                                                 WriteToFile(FORMAT('KON0037' + FORMAT(BusinessIdCode,8),20));

                                                 EVALUATE(TmpDate,'0101' + FORMAT(TODAY,0,'<Year,2>'));
                                                 WriteToFile(FORMAT('OTS' + FORMAT(TODAY,0,'<Year,2>') + StatCustChamber +
                                                                 TextZeroFormat(FORMAT(TODAY - TmpDate+1),3) + CompanyCode +
                                                                 IntraSetup."File No." + FileType + StatPeriod + 'T  ' + '             ' +
                                                                 'FI' + BusinessIdCode + '       ' +'                           ' + StatCustChamber +
                                                                 '               ' + 'EUR',101));

                                                 SETRANGE("Internal Ref. No.",COPYSTR(IntraReferenceNo,1,4),COPYSTR(IntraReferenceNo,1,4) + '9');
                                                 CurrReport.CREATETOTALS(Quantity,"Statistical Value","Total Weight", Amount,"Quantity 2");

                                                 IntrastatJnlLine3.SETCURRENTKEY("Internal Ref. No.");
                                               END;
                             END;

               OnAfterGetRecord=BEGIN
                                  IF ("Tariff No." = '') AND
                                     ("Country/Region Code" = '') AND
                                     ("Transaction Type" = '') AND
                                     //("Transport Method" = '') AND
                                     ("Statistical Value" = 0) AND
                                     ("Total Weight" = 0)
                                  THEN
                                    CurrReport.SKIP;
                                  "Tariff No." := DELCHR("Tariff No.");

                                  TotalWeightAmt += "Total Weight";
                                  IF Finnishlocalizationactive THEN
                                    QuantityAmt += "Quantity 2"
                                  ELSE
                                    QuantityAmt += Quantity;
                                  StatisticalValueAmt += "Statistical Value";
                                  GrTotalAmt += Amount;

                                  IntrastatJnlLine5.COPY(IntrastatJnlLine2);
                                  IF IntrastatJnlLine5.NEXT = 1 THEN BEGIN
                                    IF (DELCHR(IntrastatJnlLine5."Tariff No.") = "Tariff No.") AND
                                       (IntrastatJnlLine5."Country/Region Code" = "Country/Region Code") AND
                                       (IntrastatJnlLine5."Transaction Type" = "Transaction Type") AND
                                       (IntrastatJnlLine5."Transport Method" = "Transport Method")
                                    THEN
                                      GroupTotal := FALSE
                                    ELSE
                                      GroupTotal := TRUE;
                                  END ELSE
                                    GroupTotal := TRUE;

                                  IF GroupTotal THEN BEGIN
                                    WriteGrTotalsToFile(TotalWeightAmt,QuantityAmt,StatisticalValueAmt);
                                    StatisticalValueTotalAmt += StatisticalValueAmt;
                                    TotalWeightAmt := 0;
                                    QuantityAmt := 0;
                                    StatisticalValueAmt := 0;
                                    GrTotalAmt := 0;
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                //NAVDK
                                IF DKinfoEnable THEN BEGIN
                                  IF NOT Receipt THEN
                                    WriteToFile(
                                      FORMAT(
                                        '02000' + FORMAT(IntraReferenceNo,4) + '100000' +
                                        FORMAT(VATRegNo,8) + '1' + FORMAT(IntraReferenceNo,4),
                                        80));
                                  IF NOT Shipment THEN
                                    WriteToFile(
                                      FORMAT(
                                        '02000' + FORMAT(IntraReferenceNo,4) + '200000' +
                                        FORMAT(VATRegNo,8) + '2' + FORMAT(IntraReferenceNo,4),
                                        80));
                                  IntraFile.WRITE(FORMAT('10' + DecimalNumeralZeroFormat(StatisticalValueTotalAmt,16),80));
                                END;

                                IF NOT Finnishlocalizationactive THEN BEGIN
                                  "Intrastat Jnl. Batch".Reported := TRUE;
                                  "Intrastat Jnl. Batch".MODIFY;

                                  IntraFile.CLOSE;
                                  //NAVDK
                                  IF DKinfoEnable THEN BEGIN
                                    IntraFile.OPEN(FileName);
                                    IntraFile.SEEK(IntraFile.LEN-2);
                                    IntraFile.TRUNC;
                                    IntraFile.CLOSE;
                                    "Intrastat Jnl. Batch".Reported := TRUE;
                                    "Intrastat Jnl. Batch".MODIFY;
                                  END;
                                END
                                ELSE BEGIN
                                  //NAVFI
                                  WriteToFile(
                                    FORMAT(
                                    'SUM' + TextZeroFormat(COPYSTR("Internal Ref. No.",8,3),18)+
                                    DecimalNumeralZeroFormat(ROUND(TotalAmount,1,'>'),18),39));

                                  IntraFile.CLOSE;

                                  IF IntraJnlLineType = 0 THEN
                                    BEGIN
                                      "Intrastat Jnl. Batch"."Reported Receipt" := TRUE;
                                  "Intrastat Jnl. Batch".MODIFY;
                                      IntraSetup.MODIFY;
                                    END
                                  ELSE IF IntraJnlLineType = 1 THEN
                                    BEGIN
                                      "Intrastat Jnl. Batch"."Reported Shipment" := TRUE;
                                      "Intrastat Jnl. Batch".MODIFY;
                                      IntraSetup.MODIFY;
                                    END;
                                END;

                                IF ServerFileName = '' THEN
                                  FileMgt.DownloadHandler(FileName,'','',FileMgt.GetToFilterText('',DefaultFilenameTxt),DefaultFilenameTxt)
                                ELSE
                                  FileMgt.CopyServerFile(FileName,ServerFileName,TRUE);
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=VAR
                   IntrastatSetup@1000 : Record 247;
                 BEGIN
                   IF NOT IntrastatSetup.GET THEN
                     EXIT;

                   IF IntrastatSetup."Report Receipts" AND IntrastatSetup."Report Shipments" THEN
                     EXIT;

                   IF IntrastatSetup."Report Receipts" THEN
                     "Intrastat Jnl. Line".SETRANGE(Type,"Intrastat Jnl. Line".Type::Receipt)
                   ELSE
                     IF IntrastatSetup."Report Shipments" THEN
                       "Intrastat Jnl. Line".SETRANGE(Type,"Intrastat Jnl. Line".Type::Shipment)
                 END;

    }
    CONTROLS
    {
      { 1   ;0   ;Container ;
                  ContainerType=ContentArea }

      { 2   ;1   ;Group     ;
                  CaptionML=[ENU=Options;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  Visible=Finnishlocalizationactive;
                  GroupType=Group }

      { 1090000;2;Field     ;
                  Name=IntrastatJnlLineType;
                  CaptionML=ENU=Intrastat Journal Line Type;
                  OptionCaptionML=[ENU=Receipt,Shipment;
                                   NOR=Mottak,Levering;
                                   SVE=Inleverans,Utleverans];
                  SourceExpr=IntraJnlLineType }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Enter the file name.;NOR=Angi filnavn.;SVE=Ange filnamnet.';
      Text001@1001 : TextConst 'ENU=WwWw;NOR=WwWw;SVE=WwWw';
      Text002@1002 : TextConst 'ENU=INTRASTAT;NOR=INTRASTAT;SVE=INTRASTAT';
      Text003@1003 : TextConst 'ENU=It is not possible to display %1 in a field with a length of %2.;NOR=Det er ikke mulig † vise %1 i et felt med en lengde p† %2.;SVE=Det „r inte m”jligt att visa %1 i ett f„lt med l„ngden %2.';
      IntrastatJnlLine3@1004 : Record 263;
      IntrastatJnlLine4@1005 : Record 263;
      IntrastatJnlLine5@1026 : Record 263;
      CompanyInfo@1006 : Record 79;
      Country@1007 : Record 9;
      IntraSetup@1090002 : Record 11126170;
      FileMgt@1030 : Codeunit 419;
      IntraFile@1008 : File;
      QuantityAmt@1023 : Decimal;
      StatisticalValueAmt@1024 : Decimal;
      StatisticalValueTotalAmt@1028 : Decimal;
      TotalWeightAmt@1025 : Decimal;
      FileName@1009 : Text;
      IntraReferenceNo@1010 : Text[10];
      CompoundField@1011 : Text[40];
      TempCompoundField@1012 : Text[40];
      ServerFileName@1020 : Text;
      TempType@1013 : Integer;
      NoOfEntries@1014 : Text[3];
      Receipt@1015 : Boolean;
      Shipment@1016 : Boolean;
      BusinessIdCode@10117 : Code[20];
      VATRegNo@1017 : Code[20];
      ImportExport@1018 : Code[1];
      OK@1019 : Boolean;
      DefaultFilenameTxt@1022 : TextConst '@@@={Locked};ENU=Default.txt;NOR=Default.txt;SVE=Default.txt';
      GroupTotal@1027 : Boolean;
      TransMeth@1000000 : Code[10];
      Text11200@1070000 : TextConst 'ENU=Please enter either Receipt or Shipment for the %1 field.;SVE=Ange antingen inleverans eller utleverans f”r f„ltet %1.';
      Text11201@1070001 : TextConst 'ENU=%1 exceeds the maximum value to be imported into IDEP.;SVE=%1 ”verskrider det maximala v„rdet som kan importeras till IDEP.';
      LinePos@1090000 : Integer;
      TmpDate@1090001 : Date;
      CompanyCode@1090003 : Code[3];
      FileNo@1090004 : Code[3];
      StatPeriod@1090005 : Code[4];
      StatCustChamber@1090006 : Text[17];
      CountryFormat@1090007 : Text[6];
      Quantity2Code@1090008 : Text[3];
      TotalAmount@1090009 : Decimal;
      GrTotalAmt@1090015 : Decimal;
      FileType@1090010 : Text[1];
      Text1090000@1090012 : TextConst 'ENU=must be either Receipt or Shipment;NOR=m† v‘re enten mottak eller levering;SVE=m†ste antingen vara inleverans eller utleverans';
      IntraJnlLineType@1090013 : 'Receipt,Shipment';
      LineNo@1090011 : Text[5];
      InternalReference@1090014 : Text[15];
      GLSetup@1100285500 : Record 98;
      Finnishlocalizationactive@1100285501 : Boolean INDATASET;
      DKinfoEnable@1100285502 : Boolean INDATASET;

    LOCAL PROCEDURE DecimalNumeralZeroFormat@1(DecimalNumeral@1000 : Decimal;Length@1001 : Integer) : Text[250];
    BEGIN
      EXIT(TextZeroFormat(DELCHR(FORMAT(ROUND(ABS(DecimalNumeral),1,'<'),0,1)),Length));
    END;

    LOCAL PROCEDURE TextZeroFormat@2(Text@1000 : Text[250];Length@1001 : Integer) : Text[250];
    BEGIN
      IF STRLEN(Text) > Length THEN
        ERROR(
          Text003,
          Text,Length);
      EXIT(PADSTR('',Length - STRLEN(Text),'0') + Text);
    END;

    [External]
    PROCEDURE InitializeRequest@4(newServerFileName@1000 : Text);
    BEGIN
      ServerFileName := newServerFileName;
    END;

    [Internal]
    PROCEDURE WriteGrTotalsToFile@3(TotalWeightAmt@1000 : Decimal;QuantityAmt@1001 : Decimal;StatisticalValueAmt@1002 : Decimal);
    BEGIN
      WITH IntrastatJnlLine2 DO BEGIN
        OK := COPYSTR("Internal Ref. No.",8,3) = '001';
        IF OK THEN BEGIN
          IntrastatJnlLine3.SETRANGE(
            "Internal Ref. No.",
            COPYSTR("Internal Ref. No.",1,7) + '000',
            COPYSTR("Internal Ref. No.",1,7) + '999');
          IntrastatJnlLine3.FINDLAST;
          NoOfEntries := COPYSTR(IntrastatJnlLine3."Internal Ref. No.",8,3);
        END;
        ImportExport := INCSTR(FORMAT(Type,1,2));
        //NAVFI
        IF "Quantity 2" <> 0 THEN BEGIN
          Quantity2Code := 'AAE';
          TESTFIELD("Unit of Measure");
        END
        ELSE BEGIN
          Quantity2Code := '';
          "Unit of Measure" := '';
        END;
        //NAVFI

        IF Type = Type::Receipt THEN BEGIN
          Receipt := TRUE;
          CountryFormat := FORMAT("Country/Region of Origin Code",2) + FORMAT("Country/Region Code",2) + '  ';
        END
        ELSE BEGIN
          Shipment := TRUE;
          CountryFormat := '    '+ FORMAT("Country/Region Code",2)
        END;
        IF Finnishlocalizationactive THEN BEGIN
                LineNo := INCSTR(LineNo);
                InternalReference := FORMAT("Internal Ref. No.",15);
                WriteToFile(
                  FORMAT('NIM' + LineNo +
                  PADSTR("Tariff No.",8,'0') + FORMAT("Transaction Type",2) +
                  CountryFormat + FORMAT("Transport Method",1) +
                  DecimalNumeralZeroFormat(ROUND(StatisticalValueAmt,1,'>'),10) +
                  InternalReference + 'WT ' + 'KGM' +
                  DecimalNumeralZeroFormat(ROUND(TotalWeightAmt,1,'>'),10) +
                  FORMAT(Quantity2Code,3) + FORMAT("Unit of Measure",3) +
                  DecimalNumeralZeroFormat(ROUND(QuantityAmt,1,'>'),10) +
                  DecimalNumeralZeroFormat(ROUND(GrTotalAmt,1,'>'),10),92));

          TotalAmount := TotalAmount + GrTotalAmt;
        END
        ELSE BEGIN
          Country.GET("Country/Region Code");
          Country.TESTFIELD("Intrastat Code");

                IF OK THEN
                  WriteToFile(
                    FORMAT(
                      '02' +
                      TextZeroFormat(DELCHR(NoOfEntries),3) +
                      FORMAT(COPYSTR(IntrastatJnlLine3."Internal Ref. No.",1,7) + '000',10) +
                      FORMAT(VATRegNo,8) + FORMAT(ImportExport,1) + FORMAT(IntraReferenceNo,4),
                      80));

          IF DKinfoEnable THEN
            WriteToFile(
              FORMAT(
                '03' +
                TextZeroFormat(COPYSTR("Internal Ref. No.",8,3),3) +
                FORMAT("Internal Ref. No.",10) + FORMAT(Country."Intrastat Code",3) + FORMAT("Transaction Type",2) +
                '0' + FORMAT(TransMeth,1) + PADSTR("Tariff No.",9,'0') +
                DecimalNumeralZeroFormat(ROUND(TotalWeightAmt,1,'>'),15) +
                DecimalNumeralZeroFormat(QuantityAmt,10) +
                DecimalNumeralZeroFormat(StatisticalValueAmt,15),
                80))
          ELSE
          WriteToFile(
            FORMAT(Country."Intrastat Code",2)  + ';' +
            //FORMAT("Transaction Type",1) + ';' +
            FORMAT(TransMeth,1) + ';' +
            PADSTR("Tariff No.",8,'0') + ';' +
            FORMAT(QuantityAmt,0,'<Sign><Integer><Decimals>') + ';' +
            FORMAT(ROUND(TotalWeightAmt,1),0,'<Sign><Integer><Decimals>') + ';' +
            FORMAT(StatisticalValueAmt,0,'<Sign><Integer><Decimals>'));

        END;
      END;
    END;

    LOCAL PROCEDURE WriteToFile@1060000(Text@1060000 : Text[80]);
    BEGIN
      IntraFile.WRITE(Text);
      IF DKinfoEnable THEN
        IntraFile.SEEK(IntraFile.POS-2);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

