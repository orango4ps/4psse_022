OBJECT Codeunit 11128510 E-Invoice Check Serv. Document
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVNO9.00 (10623);
  }
  PROPERTIES
  {
    TableNo=5900;
    OnRun=VAR
            ResponsibilityCenter@1080001 : Record 5714;
            Customer@1080000 : Record 18;
          BEGIN
            IF NOT "E-Invoice" THEN
              EXIT;

            ReadCompanyInfo;
            ReadGLSetup;

            EInvoiceDocumentEncode.GetEInvoiceCurrencyCode("Currency Code");

            IF "Responsibility Center" <> '' THEN BEGIN
              ResponsibilityCenter.GET("Responsibility Center");
              ResponsibilityCenter.TESTFIELD(Name);
              ResponsibilityCenter.TESTFIELD(Address);
              ResponsibilityCenter.TESTFIELD(City);
              ResponsibilityCenter.TESTFIELD("Post Code");
              ResponsibilityCenter.TESTFIELD("Country/Region Code");
            END ELSE BEGIN
              CompanyInfo.TESTFIELD(Name);
              CompanyInfo.TESTFIELD(Address);
              CompanyInfo.TESTFIELD(City);
              CompanyInfo.TESTFIELD("Post Code");
            END;
            CompanyInfo.TESTFIELD("Country/Region Code");
            EInvoiceDocumentEncode.GetEInvoiceCountryRegionCode(CompanyInfo."Country/Region Code");

            CompanyInfo.TESTFIELD("VAT Registration No.");

            TESTFIELD("Bill-to Name");
            TESTFIELD("Bill-to Address");
            TESTFIELD("Bill-to City");
            TESTFIELD("Bill-to Post Code");
            TESTFIELD("Bill-to Country/Region Code");
            EInvoiceDocumentEncode.GetEInvoiceCountryRegionCode("Bill-to Country/Region Code");
            IF ("Document Type" IN ["Document Type"::Invoice,"Document Type"::Order,"Document Type"::"Credit Memo"]) AND
               Customer.GET("Bill-to Customer No.")
            THEN
              Customer.TESTFIELD("VAT Registration No.");

            IF "Document Type" = "Document Type"::"Credit Memo" THEN BEGIN
              IF "Applies-to Doc. Type" = "Applies-to Doc. Type"::Invoice THEN
                TESTFIELD("Applies-to Doc. No.")
              ELSE
                IF "External Document No." = '' THEN
                  ERROR(MissingExternalDocNumberErr,"Document Type");
            END;

            TESTFIELD("VAT Registration No.");
            TESTFIELD("Your Reference");
            TESTFIELD("Ship-to Address");
            TESTFIELD("Ship-to City");
            TESTFIELD("Ship-to Post Code");
            TESTFIELD("Ship-to Country/Region Code");
            EInvoiceDocumentEncode.GetEInvoiceCountryRegionCode("Ship-to Country/Region Code");
            TESTFIELD("Due Date");

            IF CompanyInfo.IBAN = '' THEN
              CompanyInfo.TESTFIELD("Bank Account No.");
            CompanyInfo.TESTFIELD("Bank Branch No.");

            CheckServiceLines(Rec);
          END;

  }
  CODE
  {
    VAR
      GLSetup@1080000 : Record 98;
      CompanyInfo@1080001 : Record 79;
      EInvoiceDocumentEncode@1080002 : Codeunit 11128501;
      GLSetupRead@1080003 : Boolean;
      CompanyInfoRead@1080004 : Boolean;
      EmptyFieldsQst@1080007 : TextConst '@@@=Parameter 1 - document type (Quote,Order,Invoice,Credit Memo), 2 - document number;ENU=The %1 %2 contains lines in which either the Type, the No. or the Description is empty. Lines that contain these empty fields are not taken into account when you create an E-Invoice document. Do you want to continue?;NOR=%1 %2 inneholder linjer der typen, nummeret eller beskrivelsen er tom. Linjer som inneholder disse tomme feltene, tas ikke i betraktning n†r du oppretter et e-fakturadokument. Vil du fortsette?';
      EmptyUnitOfMeasureErr@1080009 : TextConst '@@@=Parameter 1 - document type (Quote,Order,Invoice,Credit Memo), 2 - document number;ENU=The %1 %2 contains lines in which the Unit of Measure is empty. This is not allowed for an E-Invoice document which might be created from the posted document.;NOR=%1 %2 inneholder linjer der m†leenheten er tom. Dette er ikke tillatt for et e-fakturadokument som kan opprettes fra det bokf›rte dokumentet.';
      MissingDescriptionErr@1080006 : TextConst '@@@=Parameter 1 - document type (Quote,Order,Invoice,Credit Memo), 2 - document number;ENU=The %1 %2 contains lines in which the Type and the No. are specified, but the Description is empty. This is not allowed for an E-Invoice document which might be created from the posted document.;NOR=%1 %2 inneholder linjer der typen og nummeret er angitt, men der beskrivelsen er tom. Dette er ikke tillatt for et e-fakturadokument som kan opprettes fra det bokf›rte dokumentet.';
      MissingExternalDocNumberErr@1080008 : TextConst 'ENU="You must specify an External document number in Document Type  = %1.  If you use E-Invoice, this field is required regardless of the value in the External Document No. field of the Service Mgt. Setup table.";NOR="Du m† angi et eksterndokumentnummer i dokumenttype  = %1. Hvis du bruker e-faktura, er dette feltet obligatorisk, uansett verdi i feltet Eksterndokumentnr. i tabellen Salgsoppsett."';
      MissingUnitOfMeasureCodeErr@1080005 : TextConst 'ENU=You must specify a valid %2 for the %3 for %1.;NOR=Du m† angi gyldig %2 for %3 for %1.';
      PostingWasInterruptedErr@1080017 : TextConst 'ENU=The posting has been interrupted.;NOR=Bokf›ringen er avbrutt.';

    LOCAL PROCEDURE ReadCompanyInfo@1080021();
    BEGIN
      IF NOT CompanyInfoRead THEN BEGIN
        CompanyInfo.GET;
        CompanyInfoRead := TRUE;
      END;
    END;

    LOCAL PROCEDURE ReadGLSetup@1080022();
    BEGIN
      IF NOT GLSetupRead THEN BEGIN
        GLSetup.GET;
        GLSetupRead := TRUE;
      END;
    END;

    LOCAL PROCEDURE CheckServiceLines@1080023(ServiceHeader@1060000 : Record 5900);
    VAR
      ServiceLine@1060001 : Record 5902;
      UnitOfMeasure@1080000 : Record 204;
      EmptyLineFound@1060002 : Boolean;
    BEGIN
      EmptyLineFound := FALSE;
      WITH ServiceLine DO BEGIN
        SETRANGE("Document Type",ServiceHeader."Document Type");
        SETRANGE("Document No.",ServiceHeader."No.");
        IF FINDSET THEN
          REPEAT
            IF (Type <> Type::" ") AND ("No." <> '') AND ("Unit of Measure" = '') THEN
              ERROR(EmptyUnitOfMeasureErr,"Document Type","Document No.");
            IF Description = '' THEN
              IF (Type <> Type::" ") AND ("No." <> '') THEN
                ERROR(MissingDescriptionErr,"Document Type","Document No.");
            IF (Type = Type::" ") OR ("No." = '') THEN
              EmptyLineFound := TRUE;
            IF "Document Type" IN ["Document Type"::Invoice,"Document Type"::Order,"Document Type"::"Credit Memo"] THEN BEGIN
              TESTFIELD("Unit of Measure Code");
              UnitOfMeasure.GET("Unit of Measure Code");
              IF UnitOfMeasure."International Standard Code" = '' THEN
                ERROR(
                  MissingUnitOfMeasureCodeErr,"Unit of Measure Code",UnitOfMeasure.FIELDCAPTION("International Standard Code"),
                  UnitOfMeasure.TABLECAPTION);
            END;
          UNTIL (NEXT = 0);

        IF EmptyLineFound THEN
          IF NOT CONFIRM(EmptyFieldsQst,TRUE,"Document Type","Document No.") THEN
            ERROR(PostingWasInterruptedErr);
      END;
    END;

    BEGIN
    END.
  }
}

