OBJECT Table 11229811 Subsite-Doc Type (PPT)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CheckIfDocsAreLinkedToPPT;
             END;

    OnModify=BEGIN
               IF (xRec."Default Quote Subsite" <> "Default Quote Subsite") THEN
                 CheckIfDocsAreLinkedToPPT;
             END;

    OnDelete=VAR
               RespPersPermPPT@1100528303 : Record 11229812;
               AuthorityPermPPT@1100528302 : Record 11229813;
               DefaultRespPersPermPPT@1100528301 : Record 11229814;
               DefaultAuthorityPermPPT@1100528300 : Record 11229815;
             BEGIN
               CheckIfDocsAreLinkedToPPT;

               RespPersPermPPT.SETRANGE("Project Portal Subsite", "Project Portal Subsite");
               RespPersPermPPT.SETRANGE("Document Type", "Document Type");
               RespPersPermPPT.DELETEALL(TRUE);

               AuthorityPermPPT.SETRANGE("Project Portal Subsite", "Project Portal Subsite");
               AuthorityPermPPT.SETRANGE("Document Type", "Document Type");
               AuthorityPermPPT.DELETEALL(TRUE);

               DefaultRespPersPermPPT.SETRANGE("Project Portal Subsite", "Project Portal Subsite");
               DefaultRespPersPermPPT.SETRANGE("Document Type", "Document Type");
               DefaultRespPersPermPPT.DELETEALL(TRUE);

               DefaultAuthorityPermPPT.SETRANGE("Project Portal Subsite", "Project Portal Subsite");
               DefaultAuthorityPermPPT.SETRANGE("Document Type", "Document Type");
               DefaultAuthorityPermPPT.DELETEALL(TRUE);
             END;

    CaptionML=ENU=Document Portal Document Type per Subsite;
    LookupPageID=Page11072410;
  }
  FIELDS
  {
    { 5   ;   ;Subsite Type        ;Option        ;CaptionML=ENU=Document Portal Subsite Type;
                                                   OptionCaptionML=ENU=Project,Service Location;
                                                   OptionString=Project,ServLoc }
    { 10  ;   ;Project Portal Subsite;Code20      ;TableRelation="Project Portal Subsite".Code WHERE (Quote Request Subsite=CONST(No),
                                                                                                      Type=FIELD(Subsite Type));
                                                   CaptionML=ENU=Document Portal Subsite;
                                                   NotBlank=Yes }
    { 11  ;   ;Project Portal Subsite Descr;Text30;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Portal Subsite".Description WHERE (Code=FIELD(Project Portal Subsite)));
                                                   CaptionML=ENU=Document Portal Subsite Descr;
                                                   Editable=No }
    { 12  ;   ;High Security Subsite;Boolean      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Portal Subsite"."High Security Subsite" WHERE (Code=FIELD(Project Portal Subsite)));
                                                   CaptionML=ENU=High Security Subsite;
                                                   Editable=No }
    { 20  ;   ;Document Type       ;Code10        ;TableRelation="Document Type";
                                                   CaptionML=[ENU=Document Type;
                                                              NOR=Dokumenttype;
                                                              SVE=Dokumenttyp];
                                                   NotBlank=Yes }
    { 21  ;   ;Document Type Description;Text30   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Document Type".Description WHERE (Type=FIELD(Document Type)));
                                                   CaptionML=[ENU=Document Type Description;
                                                              NOR=Dokumenttype, Beskrivelse;
                                                              SVE=Dokumenttyp, beskrivning];
                                                   Editable=No }
    { 30  ;   ;Default Quote Subsite;Boolean      ;OnValidate=BEGIN
                                                                CheckDefaultQuoteSubsite;
                                                              END;

                                                   CaptionML=ENU=Default Quote Subsite }
  }
  KEYS
  {
    {    ;Subsite Type,Project Portal Subsite,Document Type;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100525000 : TextConst 'ENU=Change of %1 is not allowed.\There are already Documents linked to it.';
      Text001@1100525001 : TextConst 'ENU=There can be only 1 %1.';
      Text002@1100525002 : TextConst 'ENU=%1 cann''t be equal to %2, because this is already the Quote Request %1.';

    PROCEDURE CreateAllLinks@1100528301();
    VAR
      ProjectPortalSite@1100528300 : Record 11229810;
      CreateSubsiteDocLinksPPT@1100528301 : Report 11012912;
    BEGIN
      ProjectPortalSite.SETRANGE(Type, "Subsite Type");
      IF (NOT ProjectPortalSite.FINDSET) THEN
        EXIT;

      IF ("Project Portal Subsite" <> '') THEN
        ProjectPortalSite.SETRANGE(Code, "Project Portal Subsite");
      CreateSubsiteDocLinksPPT.SetSubsiteType("Subsite Type");
      CreateSubsiteDocLinksPPT.SETTABLEVIEW(ProjectPortalSite);
      CreateSubsiteDocLinksPPT.RUNMODAL
    END;

    PROCEDURE UpdateProjectPortal@1100528302();
    VAR
      DocumentPortalProject@1100528300 : Codeunit 11012948;
    BEGIN
      DocumentPortalProject.UpdateStaticContentAllProjects;
    END;

    PROCEDURE CheckIfDocsAreLinkedToPPT@1100525001();
    VAR
      DocumentProperties@1100528300 : Record 11012746;
      DocumentLink@1100528301 : Record 11012747;
      Job@1100528302 : Record 11072003;
      RecRef@1100528303 : RecordRef;
    BEGIN
      DocumentProperties.SETRANGE("Internal Company", COMPANYNAME);
      DocumentProperties.SETRANGE("Project Portal Subsite", "Project Portal Subsite");
      DocumentProperties.SETRANGE("Document Type", "Document Type");
      DocumentProperties.SETFILTER("Web link", '<>%1', '');
      IF (NOT DocumentProperties.FINDSET) THEN
        EXIT;

      REPEAT
        DocumentLink.SETRANGE("Document No.", DocumentProperties."No.");
        DocumentLink.SETRANGE("Table No.", DATABASE::Job);
        IF (DocumentLink.FINDSET) THEN BEGIN
          REPEAT
            IF (RecRef.GET(DocumentLink."Record ID")) THEN BEGIN
              RecRef.SETTABLE(Job);
              IF (Job."SharePoint Site Generated") AND
                 (Job."SharePoint Site URL" <> '') AND
                 (Job."Project Portal Project")
              THEN
                ERROR(Text000, TABLECAPTION);
            END;
          UNTIL (DocumentLink.NEXT = 0);
        END;
      UNTIL (DocumentProperties.NEXT = 0);
    END;

    PROCEDURE CheckDefaultQuoteSubsite@1100528300();
    VAR
      SubsiteDocTypePPT@1100528301 : Record 11229811;
      DocumentTemplate@1100525000 : Record 11012401;
    BEGIN
      TESTFIELD("Subsite Type", "Subsite Type"::Project);

      SubsiteDocTypePPT.SETRANGE("Subsite Type", "Subsite Type");
      SubsiteDocTypePPT.SETFILTER("Document Type", '<>%1', "Document Type");
      SubsiteDocTypePPT.SETRANGE("Default Quote Subsite", TRUE);
      IF (NOT SubsiteDocTypePPT.ISEMPTY) THEN
        ERROR(Text001, FIELDCAPTION("Default Quote Subsite"));

      DocumentTemplate.SETRANGE(Type, DocumentTemplate.Type::"Purchase Quote");
      DocumentTemplate.SETRANGE("Document Type", "Document Type");
      IF (NOT DocumentTemplate.ISEMPTY) THEN
        ERROR(Text002, FIELDCAPTION("Document Type"), "Document Type");
    END;

    BEGIN
    END.
  }
}

