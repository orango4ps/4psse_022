OBJECT Table 11072658 INSBOU Purchase Order Response
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               INSBOUPurchOrderRespLine@1100528300 : Record 11072659;
             BEGIN
               IF ("Process Status" = "Process Status"::Processing) THEN
                 FIELDERROR("Process Status");

               INSBOUPurchOrderRespLine.SETRANGE("Vendor No.", "Vendor No.");
               INSBOUPurchOrderRespLine.SETRANGE(OrderResponsNumber, OrderResponsNumber);
               INSBOUPurchOrderRespLine.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               IF ("Process Status" > "Process Status"::Processing) THEN
                 FIELDERROR("Process Status");
             END;

    CaptionML=ENU=INSBOU Purchase Order Response;
  }
  FIELDS
  {
    { 5   ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor;
                                                   CaptionML=[ENU=Vendor No.;
                                                              NOR=Leverand›rnr.;
                                                              SVE=Leverant”rsnr];
                                                   NotBlank=Yes }
    { 6   ;   ;Vendor Name         ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor.Name WHERE (No.=FIELD(Vendor No.)));
                                                   CaptionML=[ENU=Vendor Name;
                                                              NOR=Leverand›rsnavn;
                                                              SVE=Leverant”rsnamn];
                                                   Editable=No }
    { 10  ;   ;OrderResponsNumber  ;Code17        ;CaptionML=ENU=Order Response Number;
                                                   NotBlank=Yes }
    { 20  ;   ;OrderResponseDate   ;Date          ;CaptionML=ENU=Order Response Date }
    { 30  ;   ;OrderResponseTime   ;Time          ;CaptionML=ENU=Order Response Time }
    { 40  ;   ;StatusCode          ;Code3         ;CaptionML=[ENU=Status Code;
                                                              NOR=Statuskode;
                                                              SVE=Statuskod] }
    { 50  ;   ;TotalAmount         ;Decimal       ;CaptionML=[ENU=Total Amount;
                                                              NOR=Totalt bel›p;
                                                              SVE=Totalt belopp] }
    { 60  ;   ;BuyersOrderNumber   ;Code17        ;CaptionML=ENU=Buyers Order Number }
    { 70  ;   ;ProjectNumber       ;Code17        ;CaptionML=ENU=Project Number }
    { 80  ;   ;FreeText            ;Text250       ;CaptionML=ENU=Free Text }
    { 90  ;   ;DeliveryDateTimeInformation;Text30 ;CaptionML=ENU=Delivery Date Time Information }
    { 100 ;   ;ScheduledDeliveryDate;Date         ;CaptionML=ENU=Scheduled Delivery Date }
    { 110 ;   ;ScheduledDeliveryTime;Time         ;CaptionML=ENU=Scheduled Delivery Time }
    { 120 ;   ;DeliveryTimeFrame   ;Text30        ;CaptionML=ENU=Delivery Time Frame }
    { 130 ;   ;DeliveryDateEarliest;Date          ;CaptionML=ENU=Delivery Date Earliest }
    { 140 ;   ;DeliveryTimeEarliest;Time          ;CaptionML=ENU=Delivery Time Earliest }
    { 150 ;   ;DeliveryDateLatest  ;Date          ;CaptionML=ENU=Delivery Date Latest }
    { 160 ;   ;DeliveryTimeLatest  ;Time          ;CaptionML=ENU=Delivery Time Latest }
    { 170 ;   ;Buyer               ;Text30        ;CaptionML=[ENU=Buyer;
                                                              NOR=Kj›per;
                                                              SVE=K”pare] }
    { 180 ;   ;BuyerGLN            ;Code13        ;CaptionML=[ENU=GLN;
                                                              NOR=GLN;
                                                              SVE=GLN] }
    { 190 ;   ;BuyerName           ;Text50        ;CaptionML=[ENU=Name;
                                                              NOR=Navn;
                                                              SVE=Namn] }
    { 200 ;   ;BuyerName2          ;Text50        ;CaptionML=ENU=Name2 }
    { 210 ;   ;BuyerStreetAndNumber;Text35        ;CaptionML=ENU=Street And Number }
    { 220 ;   ;BuyerCity           ;Text35        ;CaptionML=[ENU=City;
                                                              NOR=Poststed;
                                                              SVE=Ort] }
    { 230 ;   ;BuyerPostalCode     ;Code9         ;CaptionML=[ENU=Postal Code;
                                                              NOR=Postnummer;
                                                              SVE=Postnummer] }
    { 240 ;   ;BuyerCountry        ;Code2         ;CaptionML=ENU=Buyer Country }
    { 250 ;   ;Supplier            ;Text30        ;CaptionML=ENU=Supplier }
    { 260 ;   ;SupplierGLN         ;Code13        ;CaptionML=[ENU=GLN;
                                                              NOR=GLN;
                                                              SVE=GLN] }
    { 270 ;   ;SupplierName        ;Text50        ;CaptionML=[ENU=Name;
                                                              NOR=Navn;
                                                              SVE=Namn] }
    { 280 ;   ;SupplierName2       ;Text50        ;CaptionML=ENU=Name2 }
    { 290 ;   ;SupplierStreetAndNumber;Text35     ;CaptionML=ENU=Street And Number }
    { 300 ;   ;SupplierCity        ;Text35        ;CaptionML=[ENU=City;
                                                              NOR=Poststed;
                                                              SVE=Ort] }
    { 310 ;   ;SupplierPostalCode  ;Code9         ;CaptionML=[ENU=PostalCode;
                                                              NOR=PostalCode;
                                                              SVE=PostalCode] }
    { 320 ;   ;SupplierCountry     ;Code2         ;CaptionML=[ENU=Country;
                                                              NOR=Land;
                                                              SVE=Land] }
    { 330 ;   ;SupplierContactInformation;Text30  ;CaptionML=[ENU=Contact Information;
                                                              NOR=Kontaktinformasjon;
                                                              SVE=Kontaktinformation] }
    { 340 ;   ;SupplierContactPersonName;Text35   ;CaptionML=[ENU=Contact Person Name;
                                                              NOR=Kontaktpersonens navn;
                                                              SVE=Kontaktpersonens namn] }
    { 350 ;   ;SupplierPhoneNumber ;Code20        ;CaptionML=[ENU=Phone Number;
                                                              NOR=Telefon;
                                                              SVE=Telefonnummer] }
    { 360 ;   ;EmailAddress        ;Text250       ;CaptionML=[ENU=Email Address;
                                                              NOR=E-postadresse;
                                                              SVE=E-postadress] }
    { 370 ;   ;DeliveryParty       ;Text30        ;CaptionML=ENU=DeliveryParty }
    { 380 ;   ;DeliveryPartyGLN    ;Code13        ;CaptionML=[ENU=GLN;
                                                              NOR=GLN;
                                                              SVE=GLN] }
    { 390 ;   ;DeliveryPartyName   ;Text50        ;CaptionML=[ENU=Name;
                                                              NOR=Navn;
                                                              SVE=Namn] }
    { 400 ;   ;DeliveryPartyName2  ;Text50        ;CaptionML=ENU=Name2 }
    { 410 ;   ;DeliveryPartyStreetAndNumber;Text35;CaptionML=ENU=StreetAndNumber }
    { 420 ;   ;DeliveryPartyCity   ;Text35        ;CaptionML=[ENU=City;
                                                              NOR=Poststed;
                                                              SVE=Ort] }
    { 430 ;   ;DeliveryPartyPostalCode;Code9      ;CaptionML=[ENU=PostalCode;
                                                              NOR=PostalCode;
                                                              SVE=PostalCode] }
    { 440 ;   ;DeliveryPartyCountry;Code2         ;CaptionML=[ENU=Country;
                                                              NOR=Land;
                                                              SVE=Land] }
    { 450 ;   ;DeliveryPartyLocationDescripti;Text70;
                                                   CaptionML=[ENU=Location Description;
                                                              NOR=Beskrivelse av lokasjon;
                                                              SVE=Beskrivning av lagerst„lle] }
    { 460 ;   ;DeliveryPartyContactgegevens;Text30;CaptionML=ENU=Contact gegevens }
    { 470 ;   ;DeliveryPartyContactPersonName;Text35;
                                                   CaptionML=[ENU=Contact Person Name;
                                                              NOR=Kontaktpersonens navn;
                                                              SVE=Kontaktpersonens namn] }
    { 490 ;   ;DeliveryPartyPhoneNumber;Code20    ;CaptionML=[ENU=Phone Number;
                                                              NOR=Telefon;
                                                              SVE=Telefonnummer] }
    { 500 ;   ;ShipFrom            ;Text30        ;CaptionML=ENU=ShipFrom }
    { 510 ;   ;ShipFromGLN         ;Code13        ;CaptionML=[ENU=GLN;
                                                              NOR=GLN;
                                                              SVE=GLN] }
    { 520 ;   ;ShipFromName        ;Text50        ;CaptionML=[ENU=Name;
                                                              NOR=Navn;
                                                              SVE=Name] }
    { 530 ;   ;ShipFromName2       ;Text50        ;CaptionML=ENU=Name2 }
    { 540 ;   ;ShipFromStreetAndNumber;Text35     ;CaptionML=ENU=Street And Number }
    { 550 ;   ;ShipFromCity        ;Text35        ;CaptionML=[ENU=City;
                                                              NOR=Poststed;
                                                              SVE=Ort] }
    { 560 ;   ;ShipFromPostalCode  ;Code9         ;CaptionML=[ENU=Postal Code;
                                                              NOR=Postnummer;
                                                              SVE=Postnummer] }
    { 570 ;   ;ShipFromCountry     ;Code2         ;CaptionML=[ENU=Country;
                                                              NOR=Land;
                                                              SVE=Land] }
    { 580 ;   ;UltimateConsignee   ;Text30        ;CaptionML=ENU=UltimateConsignee }
    { 590 ;   ;UltimateConsigneeGLN;Code13        ;CaptionML=[ENU=GLN;
                                                              NOR=GLN;
                                                              SVE=GLN] }
    { 600 ;   ;UltimateConsigneeName;Text50       ;CaptionML=[ENU=Name;
                                                              NOR=Navn;
                                                              SVE=Namn] }
    { 610 ;   ;UltimateConsigneeName2;Text50      ;CaptionML=ENU=Name2 }
    { 620 ;   ;UltimateConsigneeStreetAndNumb;Text35;
                                                   CaptionML=ENU=Street And Number }
    { 630 ;   ;UltimateConsigneeCity;Text35       ;CaptionML=[ENU=City;
                                                              NOR=Poststed;
                                                              SVE=Ort] }
    { 640 ;   ;UltimateConsigneePostalCode;Code9  ;CaptionML=[ENU=Postal Code;
                                                              NOR=Postnummer;
                                                              SVE=Postnummer] }
    { 650 ;   ;UltimateConsigneeCountry;Code2     ;CaptionML=[ENU=Country;
                                                              NOR=Land;
                                                              SVE=Land] }
    { 660 ;   ;Charge              ;Text30        ;CaptionML=[ENU=Charge;
                                                              NOR=Gebyr;
                                                              SVE=Avgift] }
    { 670 ;   ;ChargeTypeCode      ;Code3         ;CaptionML=ENU=TypeCode }
    { 680 ;   ;ChargeAmount        ;Decimal       ;CaptionML=[ENU=Amount;
                                                              NOR=Bel›p;
                                                              SVE=Belopp] }
    { 690 ;   ;ChargeVatInformation;Text30        ;CaptionML=ENU=VatInformation }
    { 700 ;   ;ChargeVATRate       ;Code3         ;CaptionML=ENU=VATRate }
    { 710 ;   ;ChargeVATPercentage ;Decimal       ;CaptionML=[ENU=VATPercentage;
                                                              SVE=vatPercentage] }
    { 720 ;   ;Allowance           ;Text30        ;CaptionML=[ENU=Allowance;
                                                              SVE=Traktamente] }
    { 730 ;   ;AllowanceTypeCode   ;Code3         ;CaptionML=ENU=TypeCode }
    { 740 ;   ;AllowanceAmount     ;Decimal       ;CaptionML=[ENU=Amount;
                                                              NOR=Bel›p;
                                                              SVE=Belopp] }
    { 750 ;   ;AlowanceVATRate     ;Code3         ;CaptionML=ENU=VATRate }
    { 760 ;   ;AllowanceVATPercentage;Decimal     ;CaptionML=[ENU=VATPercentage;
                                                              SVE=vatPercentage] }
    { 1000;   ;Process Status      ;Option        ;CaptionML=[ENU=Process Status;
                                                              NOR=Prosess status;
                                                              SVE=Processtatus];
                                                   OptionCaptionML=ENU=Received,,,Processing,,,Processed;
                                                   OptionString=Received,,,Processing,,,Processed;
                                                   Editable=No }
    { 1010;   ;Message Id          ;Text50        ;CaptionML=[ENU=Message Id;
                                                              SVE=Meddelande-ID];
                                                   Editable=No }
    { 1020;   ;Error Message       ;Text250       ;CaptionML=[ENU=Error Message;
                                                              NOR=Feilmelding;
                                                              SVE=Felmeddelande];
                                                   Editable=No }
    { 1030;   ;Allow Prices out of Margin;Boolean ;CaptionML=ENU=Allow Prices out of Margin }
  }
  KEYS
  {
    {    ;Vendor No.,OrderResponsNumber           ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528300 : TextConst 'ENU=%1 %2 already exsists.';
      Text001@1100528301 : TextConst 'ENU=There are no %1s or more than 1 %1s found for %2 %3.';
      Text002@1100525000 : TextConst 'ENU=%3.%1 %2 does not exist as Job or Service Order.';
      Text003@1100529900 : TextConst 'ENU=The VAT Product Posting Group could not be determined for all lines.';

    PROCEDURE ProcessOrderResponses@1100528300(VAR INSBOUPurchaseOrderResponse@1100528300 : Record 11072658);
    BEGIN
      INSBOUPurchaseOrderResponse.SETFILTER("Process Status", '<%1', "Process Status"::Processed);
      IF INSBOUPurchaseOrderResponse.FINDSET(TRUE) THEN
        REPEAT
          ProcessOrderResponse(INSBOUPurchaseOrderResponse);
        UNTIL INSBOUPurchaseOrderResponse.NEXT = 0;
    END;

    PROCEDURE ProcessOrderResponse@1100528401(INSBOUPurchaseOrderResponse@1100528400 : Record 11072658);
    VAR
      INSBOUProcessOrderResponse@1100528401 : Codeunit 11072078;
    BEGIN
      CLEARLASTERROR;
      COMMIT;
      INSBOUPurchaseOrderResponse.SETRECFILTER;
      IF NOT INSBOUPurchaseOrderResponse.DetermineVatProdPostGroup(INSBOUPurchaseOrderResponse."Vendor No.", INSBOUPurchaseOrderResponse.OrderResponsNumber) THEN BEGIN
        INSBOUPurchaseOrderResponse.GET(INSBOUPurchaseOrderResponse."Vendor No.", INSBOUPurchaseOrderResponse.OrderResponsNumber);
        INSBOUPurchaseOrderResponse."Error Message" := Text003;
        INSBOUPurchaseOrderResponse.MODIFY(TRUE);
      END ELSE BEGIN
        COMMIT;
        IF (NOT INSBOUProcessOrderResponse.RUN(INSBOUPurchaseOrderResponse)) THEN BEGIN
          INSBOUPurchaseOrderResponse.GET(INSBOUPurchaseOrderResponse."Vendor No.", INSBOUPurchaseOrderResponse.OrderResponsNumber);
          INSBOUPurchaseOrderResponse."Error Message" := COPYSTR(GETLASTERRORTEXT, 1, MAXSTRLEN(INSBOUPurchaseOrderResponse."Error Message"));
          INSBOUPurchaseOrderResponse.MODIFY(TRUE);
        END;
      END;
    END;

    PROCEDURE CreatePurchaseOrder@1100528301(VAR INSBOUPurchaseOrderResponse@1100528301 : Record 11072658);
    VAR
      PurchaseHeader@1100528300 : Record 38;
      Job@1100528401 : Record 11072003;
      ServiceOrder@1100528402 : Record 11012823;
      NoSeriesManagement@1100525000 : Codeunit 396;
      OrigBuyersOrderNumber@1100528400 : Code[17];
      SiBSetup@1100525002 : Record 11072650;
      PurchaseHeaderExtension@1100528100 : Record 11020398;
    BEGIN
      INSBOUPurchaseOrderResponse.SETFILTER(BuyersOrderNumber, '%1|%2','','X');
      IF (NOT INSBOUPurchaseOrderResponse.FINDSET) THEN
        EXIT;

      REPEAT
        SiBSetup.GET;
        OrigBuyersOrderNumber := INSBOUPurchaseOrderResponse.BuyersOrderNumber;
        INSBOUPurchaseOrderResponse.BuyersOrderNumber := NoSeriesManagement.GetNextNo(SiBSetup."Order Nos. (Dispatch Notes)", TODAY, TRUE);
        INSBOUPurchaseOrderResponse.MODIFY;
        INSBOUPurchaseOrderResponse.TESTFIELD(SupplierGLN);
        IF (INSBOUPurchaseOrderResponse.StatusCode = '7') THEN
          INSBOUPurchaseOrderResponse.FIELDERROR(StatusCode);
        IF (PurchaseHeader.GET(INSBOUPurchaseOrderResponse.DetermineDocumentType,
          INSBOUPurchaseOrderResponse.BuyersOrderNumber))
        THEN
          ERROR(Text000, PurchaseHeader.TABLECAPTION, INSBOUPurchaseOrderResponse.BuyersOrderNumber);

        PurchaseHeader.INIT;
        PurchaseHeader."Document Type" := INSBOUPurchaseOrderResponse.DetermineDocumentType;
        PurchaseHeader."No." := INSBOUPurchaseOrderResponse.BuyersOrderNumber;
        PurchaseHeader.InitRecord;
        PurchaseHeader."Reference (Vendor)" := OrigBuyersOrderNumber;
        IF INSBOUPurchaseOrderResponse."Vendor No." = '' THEN
          PurchaseHeader.VALIDATE("Buy-from Vendor No.", PurchaseHeader.DetermineBuyFromVendorNoWithGLN(INSBOUPurchaseOrderResponse.SupplierGLN))
        ELSE
          PurchaseHeader.VALIDATE("Buy-from Vendor No.", INSBOUPurchaseOrderResponse."Vendor No.");
        PurchaseHeader.TESTFIELD("Buy-from Vendor No.");

        PurchaseHeader.INSERT(TRUE);

        IF INSBOUPurchaseOrderResponse.ProjectNumber <> '' THEN
          IF Job.GET(INSBOUPurchaseOrderResponse.ProjectNumber) THEN
            PurchaseHeader."Job No." := INSBOUPurchaseOrderResponse.ProjectNumber
          ELSE
            IF ServiceOrder.GET(INSBOUPurchaseOrderResponse.ProjectNumber) THEN
              PurchaseHeader."Service Order No." := INSBOUPurchaseOrderResponse.ProjectNumber
            ELSE
              ERROR(Text002,INSBOUPurchaseOrderResponse.FIELDCAPTION(ProjectNumber),INSBOUPurchaseOrderResponse.ProjectNumber,INSBOUPurchaseOrderResponse.TABLECAPTION);

        PurchaseHeader."Vendor Order No." := INSBOUPurchaseOrderResponse.OrderResponsNumber;
        PurchaseHeader."Vendor Shipment No." := INSBOUPurchaseOrderResponse.OrderResponsNumber;

        PurchaseHeader."Ship-to Name" := INSBOUPurchaseOrderResponse.DeliveryPartyName;
        PurchaseHeader."Ship-to Name 2" := INSBOUPurchaseOrderResponse.DeliveryPartyName2;
        PurchaseHeader."Ship-to Address" := INSBOUPurchaseOrderResponse.DeliveryPartyStreetAndNumber;
        PurchaseHeader."Ship-to City" := INSBOUPurchaseOrderResponse.DeliveryPartyCity;

        PurchaseHeader."Order Date" := INSBOUPurchaseOrderResponse.OrderResponseDate;
        PurchaseHeader."Expected Receipt Date" := INSBOUPurchaseOrderResponse.OrderResponseDate;
        PurchaseHeader."Due Date" := INSBOUPurchaseOrderResponse.OrderResponseDate;
        PurchaseHeader."Document Date" := INSBOUPurchaseOrderResponse.OrderResponseDate;
        PurchaseHeader."Posting Date" := INSBOUPurchaseOrderResponse.OrderResponseDate;

        PurchaseHeader.MODIFY(TRUE);

        PurchaseHeaderExtension.GetPurchHeadExtension(PurchaseHeader."Document Type", PurchaseHeader."No.");
        PurchaseHeaderExtension."Processing Type" := PurchaseHeaderExtension."Processing Type"::INSBOU;
        PurchaseHeaderExtension.UpdatePurchHeadExtension(PurchaseHeader."Document Type", PurchaseHeader."No.");

        IF (INSBOUPurchaseOrderResponse.FreeText <> '') THEN
          CreateTextLines(INSBOUPurchaseOrderResponse);

        CreatePurchaseLines(INSBOUPurchaseOrderResponse, PurchaseHeader);
        CODEUNIT.RUN(CODEUNIT::"Release Purchase Document",PurchaseHeader);
      UNTIL (INSBOUPurchaseOrderResponse.NEXT = 0);
    END;

    LOCAL PROCEDURE CreatePurchaseLines@1100528306(VAR INSBOUPurchaseOrderResponse@1100528306 : Record 11072658;PurchaseHeader@1100528300 : Record 38);
    VAR
      INSBOUPurchOrderRespLine@1100528302 : Record 11072659;
      PurchaseLine@1100528301 : Record 39;
      Vendor@1100528304 : Record 23;
      GeneralLedgerSetup@1100528305 : Record 98;
    BEGIN
      INSBOUPurchOrderRespLine.SETRANGE("Vendor No.", INSBOUPurchaseOrderResponse."Vendor No.");
      INSBOUPurchOrderRespLine.SETRANGE(OrderResponsNumber, INSBOUPurchaseOrderResponse.OrderResponsNumber);
      IF (NOT INSBOUPurchOrderRespLine.FINDSET) THEN
        EXIT;

      Vendor.GET(PurchaseHeader."Buy-from Vendor No.");
      GeneralLedgerSetup.GET;
      REPEAT
        INSBOUPurchOrderRespLine.TESTFIELD(OrderLineIdentification);
        INSBOUPurchOrderRespLine.TESTFIELD(SuppliersTradeItemIdentificati);
        INSBOUPurchOrderRespLine.CALCFIELDS("Partial Delivery");

        PurchaseLine.INIT;
        PurchaseLine."Document Type" := PurchaseHeader."Document Type";
        PurchaseLine."Document No." := PurchaseHeader."No.";
        PurchaseLine."Line No." := INSBOUPurchOrderRespLine.OrderLineIdentification;
        PurchaseLine.INSERT(TRUE);

        IF PurchaseHeader."Job No." <> '' THEN
          PurchaseLine.VALIDATE("Job No.", PurchaseHeader."Job No.");
        IF PurchaseHeader."Service Order No." <> '' THEN
          PurchaseLine.VALIDATE("Service Order No.", PurchaseHeader."Service Order No.");

        PurchaseLine.VALIDATE("Vendor (Trade Item)", GetVendorTradeItemByGLN(INSBOUPurchaseOrderResponse.SupplierGLN));
        PurchaseLine.VALIDATE("Trade Item", GetTradeItem(PurchaseLine."Vendor (Trade Item)", INSBOUPurchOrderRespLine));
        PurchaseLine.VALIDATE(Quantity, INSBOUPurchOrderRespLine.PlannedDeliveryQuantity);
        PurchaseLine.VALIDATE("Unit of Measure", INSBOUPurchOrderRespLine.QuantityMeasureUnitCode);
        PurchaseLine."Unit of Measure" := INSBOUPurchOrderRespLine.QuantityMeasureUnitCode;
        IF Vendor."INSBOU Price Meth. Order Resp." = Vendor."INSBOU Price Meth. Order Resp."::"Order Response" THEN
          IF (INSBOUPurchOrderRespLine.NetLineAmount <> 0) THEN BEGIN
            PurchaseLine.VALIDATE(Amount, INSBOUPurchOrderRespLine.NetLineAmount);
            PurchaseLine.VALIDATE("Direct Unit Cost",
              ROUND(PurchaseLine.Amount / PurchaseLine.Quantity, GeneralLedgerSetup."Unit-Amount Rounding Precision"));
          END;
        IF (INSBOUPurchOrderRespLine."Partial Delivery") THEN
          PurchaseLine.VALIDATE(Quantity, 0);

        PurchaseLine.VALIDATE("VAT Prod. Posting Group", INSBOUPurchOrderRespLine."VAT Prod. Posting Group");

        PurchaseLine.MODIFY(TRUE);
      UNTIL (INSBOUPurchOrderRespLine.NEXT = 0);
    END;

    LOCAL PROCEDURE CreateTextLines@1100528308(INSBOUPurchaseOrderResponse@1100528301 : Record 11072658);
    VAR
      PurchaseText@1100528300 : Record 11012067;
      LastLineNo@1100528302 : Integer;
    BEGIN
      LastLineNo := 0;
      PurchaseText.SETRANGE("Line Type", PurchaseText."Line Type"::"Purchase Order");
      PurchaseText.SETRANGE("No.", INSBOUPurchaseOrderResponse.BuyersOrderNumber);
      PurchaseText.SETRANGE("Purchase Action", '');
      IF (PurchaseText.FINDLAST) THEN
        LastLineNo := PurchaseText."Line No.";

      PurchaseText.RESET;
      PurchaseText.INIT;
      PurchaseText."Line Type" := PurchaseText."Line Type"::"Purchase Order";
      PurchaseText."No." := INSBOUPurchaseOrderResponse.BuyersOrderNumber;
      PurchaseText."Line No." := LastLineNo + 10000;
      PurchaseText.Text := COPYSTR(INSBOUPurchaseOrderResponse.FreeText, 1, MAXSTRLEN(PurchaseText.Text));
      PurchaseText."Line Break" := TRUE;
      PurchaseText.INSERT(TRUE);
    END;

    LOCAL PROCEDURE GetVendorNo@1100528302(GLN@1100528300 : Code[13]) : Code[20];
    VAR
      ETIMVendorCentralMgt@1100528301 : Record 11012304;
    BEGIN
      EXIT(ETIMVendorCentralMgt.GetVendorPurchaseFromGLN(GLN));
    END;

    LOCAL PROCEDURE GetTradeItem@1100528309(VendorNo@1100528302 : Code[20];INSBOUPurchOrderRespLine@1100528300 : Record 11072659) : Code[20];
    VAR
      TradeItem@1100528301 : Record 11012317;
    BEGIN
      TradeItem.SETRANGE(Vendor, VendorNo);
      TradeItem.SETRANGE("Item Code",INSBOUPurchOrderRespLine.SuppliersTradeItemIdentificati);
      //TradeItem.SETRANGE("GTIN Code (Item)", INSBOUPurchOrderRespLine.GTIN);
      IF (TradeItem.COUNT = 0) THEN
        CreateTradeItem(VendorNo, INSBOUPurchOrderRespLine);
      IF (TradeItem.COUNT <> 1) THEN
        ERROR(Text001, TradeItem.TABLECAPTION, TradeItem."Item Code", INSBOUPurchOrderRespLine.SuppliersTradeItemIdentificati);
      TradeItem.FINDFIRST;
      EXIT(TradeItem."Item Code");
    END;

    PROCEDURE PurchaseHeaderPresent@1100528303() : Boolean;
    VAR
      PurchaseHeader@1100528300 : Record 38;
    BEGIN
      EXIT(PurchaseHeader.GET(DetermineDocumentType, BuyersOrderNumber));
    END;

    PROCEDURE DetermineDocumentType@1100528305() : Integer;
    VAR
      PurchaseHeader@1100528300 : Record 38;
    BEGIN
      IF (BuyersOrderNumber = '') THEN
        EXIT(-1);
      PurchaseHeader.SETRANGE("No.", BuyersOrderNumber);
      IF (PurchaseHeader.COUNT <> 1) THEN BEGIN
        EXIT(PurchaseHeader."Document Type"::Order);
      END;
      PurchaseHeader.FINDFIRST;
      EXIT(PurchaseHeader."Document Type");
    END;

    LOCAL PROCEDURE CreateTradeItem@1100525000(VendorNo@1100525001 : Code[20];INSBOUPurchOrderRespLine@1100525002 : Record 11072659);
    VAR
      ItemSetup@1100525003 : Record 11012300;
      TradeItem@1100525000 : Record 11012317;
      WebshopItemMgt@1100525004 : Codeunit 11012306;
    BEGIN
      IF (TradeItem.GET(VendorNo, INSBOUPurchOrderRespLine.SuppliersTradeItemIdentificati)) THEN
        EXIT;

      ItemSetup.GET;
      TradeItem.INIT;
      TradeItem.Vendor := VendorNo;
      TradeItem."Item Code" := INSBOUPurchOrderRespLine.SuppliersTradeItemIdentificati;
      TradeItem.INSERT;

      TradeItem."GTIN Code (Item)" := INSBOUPurchOrderRespLine.GTIN;
      TradeItem."Item Description" := COPYSTR(INSBOUPurchOrderRespLine.TradeItemDescription, 1, MAXSTRLEN(TradeItem."Item Description"));
      CASE ItemSetup."Adjust Item Description" OF
        ItemSetup."Adjust Item Description"::ToUpper: BEGIN
          TradeItem."Item Description" := UPPERCASE(TradeItem."Item Description");
        END;
        ItemSetup."Adjust Item Description"::ToLower: BEGIN
          TradeItem."Item Description" := LOWERCASE(TradeItem."Item Description");
        END;
      END;
      IF TradeItem."Search Argument" = '' THEN
        TradeItem."Search Argument" := COPYSTR(TradeItem."Item Description", 1, MAXSTRLEN(TradeItem."Search Argument"));

      TradeItem."Application Unit Quantity" := 1;
      TradeItem."Starting Date Price" := TODAY;
      TradeItem."Gross Price" := INSBOUPurchOrderRespLine.GrossPrice;
      TradeItem."Quantity Per Price" := 1;

      TradeItem.Width := INSBOUPurchOrderRespLine.Width;
      TradeItem.Length := INSBOUPurchOrderRespLine.Length;
      TradeItem.Height := INSBOUPurchOrderRespLine.Height;
      TradeItem."Size Unit" := INSBOUPurchOrderRespLine.MeasurementUnitCode;
      TradeItem."Gross Price Operation Surch." := INSBOUPurchOrderRespLine.GrossPriceProcessingCharge;
      TradeItem."Packaging Unit" := INSBOUPurchOrderRespLine.MeasureUnitPriceBasis;
      IF INSBOUPurchOrderRespLine.NumberOfUnitsInPriceBasis <> 0 THEN
        TradeItem."Quantity Per Price" := INSBOUPurchOrderRespLine.NumberOfUnitsInPriceBasis;
      TradeItem."Net Price" := INSBOUPurchOrderRespLine.NetPrice;

      IF (TradeItem."Cost Object" = '') OR (NOT TradeItem."Cost Object changed Manually") THEN
        TradeItem."Cost Object" := TradeItem.GetDefaultCostObject(FALSE);
      TradeItem.Expired := FALSE;
      TradeItem.MODIFY;

      WebshopItemMgt.UpdatePriceHistory(TradeItem);
    END;

    PROCEDURE CheckBuyerGLN@1100528400();
    VAR
      CompanyInformation@1100528400 : Record 79;
    BEGIN
      CompanyInformation.GET;
      TESTFIELD(BuyerGLN, CompanyInformation.GLN);
    END;

    PROCEDURE DetermineVatProdPostGroup@1100528500(VendorNo@1100528500 : Code[20];OrderResponseNo@1100528501 : Code[17]) : Boolean;
    VAR
      INSBOUPurchOrderRespLine@1100528502 : Record 11072659;
      PriceHistoryTradeItem@1100529901 : Record 11012315;
      Vendor@1100529902 : Record 23;
      TradeItemVatPercentage@1100529900 : Decimal;
      VATPostingSetup@1100529903 : Record 325;
      VendorVatPercentage@1100529904 : Decimal;
      LineWithoutVatExists@1100529905 : Boolean;
      VendorVatCalculationType@1100529906 : Integer;
      VendorVATPostingSetup@1100529907 : Record 325;
    BEGIN
      LineWithoutVatExists := FALSE;
      Vendor.GET(VendorNo);
      VendorVATPostingSetup.GET(Vendor."VAT Bus. Posting Group", Vendor."VAT Prod. Posting Group");
      VendorVatPercentage := VendorVATPostingSetup."VAT %";
      VendorVatCalculationType := VendorVATPostingSetup."VAT Calculation Type";

      INSBOUPurchOrderRespLine.SETRANGE("Vendor No.", VendorNo);
      INSBOUPurchOrderRespLine.SETRANGE(OrderResponsNumber, OrderResponseNo);
      IF INSBOUPurchOrderRespLine.FINDSET THEN
        REPEAT
          IF INSBOUPurchOrderRespLine."VAT Prod. Posting Group" = '' THEN BEGIN
            PriceHistoryTradeItem.RESET;
            PriceHistoryTradeItem.SETCURRENTKEY(Vendor, "Item Code", "Starting Date");
            PriceHistoryTradeItem.SETRANGE(Vendor, Vendor."Vendor (Trade Item)");
            PriceHistoryTradeItem.SETRANGE("Item Code", INSBOUPurchOrderRespLine.SuppliersTradeItemIdentificati);
            PriceHistoryTradeItem.SETFILTER("Starting Date", '..%1', TODAY);
            IF PriceHistoryTradeItem.FINDLAST THEN
              TradeItemVatPercentage := PriceHistoryTradeItem."VAT %";

            IF (TradeItemVatPercentage = 0) OR (TradeItemVatPercentage = VendorVatPercentage) THEN
              INSBOUPurchOrderRespLine."VAT Prod. Posting Group" := Vendor."VAT Prod. Posting Group"
            ELSE BEGIN
              VATPostingSetup.RESET;
              VATPostingSetup.SETRANGE("VAT Bus. Posting Group", Vendor."VAT Bus. Posting Group");
              VATPostingSetup.SETRANGE("VAT Calculation Type", VendorVatCalculationType);
              VATPostingSetup.SETRANGE("VAT %", TradeItemVatPercentage);
              IF VATPostingSetup.COUNT = 1 THEN BEGIN
                IF VATPostingSetup.FINDFIRST THEN
                  INSBOUPurchOrderRespLine."VAT Prod. Posting Group" := VATPostingSetup."VAT Prod. Posting Group";
              END ELSE BEGIN
                INSBOUPurchOrderRespLine."VAT Prod. Posting Group" := '';
                LineWithoutVatExists := TRUE;
              END;
            END;
            INSBOUPurchOrderRespLine.MODIFY;
          END;
        UNTIL INSBOUPurchOrderRespLine.NEXT = 0;
      EXIT(NOT LineWithoutVatExists);
    END;

    LOCAL PROCEDURE GetVendorTradeItemByGLN@1100528502(GLN@1100528500 : Code[13]) VendorTradeItem : Code[15];
    VAR
      Vendor@1100528501 : Record 23;
      ETIMVendorCentralMgt@1100528502 : Record 11012304;
    BEGIN
      Vendor.RESET;
      Vendor.SETRANGE(GLN, GLN);
      IF Vendor.COUNT = 1 THEN
        IF Vendor.FINDFIRST THEN
          IF Vendor."Vendor (Trade Item)" <> '' THEN
            EXIT(Vendor."Vendor (Trade Item)");

      EXIT(ETIMVendorCentralMgt.GetVendorFromGLN(GLN));
    END;

    BEGIN
    END.
  }
}

