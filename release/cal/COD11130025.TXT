OBJECT Codeunit 11130025 EdControls Interface WS
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text001@1100529100 : TextConst 'DEU=Beleg %1 besteht nicht.;ENU=Document %1 does not exist.;NLD=Document %1 bestaat niet';
      Text002@1100529101 : TextConst 'DEU=Keine JobNo;ENU=No JobNo;NLD=Geen JobNo';
      Text003@1100529102 : TextConst 'DEU=Kein Content;ENU=No Content;NLD=Geen Content';
      Text004@1100529103 : TextConst 'DEU=Kein FileName;ENU=No FileName;NLD=Geen FileName';
      Text005@1100529104 : TextConst 'DEU=JobNo ungÅltig;ENU=JobNo not valid;NLD=JobNo bestaat niet';

    [External]
    PROCEDURE DownloadFile@1100528105(DocumentNo@1100528102 : Code[20];VAR Base64EncodedContents@1100528101 : BigText;VAR ErrorMessage@1100528100 : Text;VAR FileName@1100529101 : Text;VAR ModifyDateTime@1100529105 : DateTime;VAR FileLength@1100529103 : BigInteger) : Boolean;
    VAR
      DocumentProperties@1100528103 : Record 11012746;
      DocumentManagementWebservice@1100529100 : Codeunit 11012405;
    BEGIN
      IF NOT DocumentProperties.GET(DocumentNo) THEN BEGIN
        ErrorMessage := STRSUBSTNO(Text001, DocumentNo);
        EXIT(FALSE);
      END;
      DocumentManagementWebservice.GetPropertiesFromFile(DocumentNo,FileName,ModifyDateTime,FileLength);
      EXIT(DocumentManagementWebservice.GetDocumentBase64Encoded(DocumentNo,Base64EncodedContents,ErrorMessage));
    END;

    [External]
    PROCEDURE UploadFile@1100529100(JobNo@1100529107 : Code[20];VAR DocumentNo@1100529103 : Code[20];Base64EncodedContents@1100529102 : BigText;VAR ErrorMessage@1100529101 : Text;VAR FileName@1100529104 : Text;VAR ModifyDateTime@1100529105 : DateTime) : Boolean;
    VAR
      DocumentManagementWebservice@1100529100 : Codeunit 11012405;
      Job@1100529108 : Record 11072003;
      EdControlsSetup@1100529109 : Record 11130005;
      ExternalDocumentManagement@1100529110 : Codeunit 11012403;
      FileLenght@1100529111 : Integer;
      EntityKey@1100525000 : ARRAY [5] OF Code[20];
    BEGIN
      IF JobNo = '' THEN BEGIN
        ErrorMessage := Text002;
        EXIT(FALSE);
      END;
      IF Base64EncodedContents.LENGTH = 0 THEN BEGIN
        ErrorMessage := Text003;
        EXIT(FALSE);
      END;
      IF FileName = '' THEN BEGIN
        ErrorMessage := Text004;
        EXIT(FALSE);
      END;
      IF NOT EdControlsSetup.GET THEN BEGIN
        ErrorMessage := '';
        EXIT(FALSE);
      END;
      IF NOT Job.GET(JobNo) THEN BEGIN
        ErrorMessage := Text005;
        EXIT(FALSE);
      END;

      EntityKey[1] := Job."No.";
      EXIT(DocumentManagementWebservice.SaveDocumentBase64Encoded(DATABASE::Job, EntityKey, DocumentNo,
        Base64EncodedContents, ErrorMessage, FileName, ModifyDateTime, EdControlsSetup."Document Type Audit"));
    END;

    BEGIN
    END.
  }
}

