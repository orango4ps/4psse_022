OBJECT Codeunit 11229306 Generic Process Job Queue
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    TableNo=472;
    OnRun=VAR
            ParameterString@1100528301 : Text;
            Pos@1100528302 : Integer;
          BEGIN
            GenericProcessSetup.GET;
            ParameterString := "Parameter String";

            REPEAT
              Pos := STRPOS(ParameterString, ',');
              IF (Pos > 0) THEN
                Parameter := UPPERCASE(COPYSTR(ParameterString, 1, Pos - 1))
              ELSE
                Parameter := UPPERCASE(ParameterString);
              JobQueueHandler;
              ParameterString := COPYSTR(ParameterString, Pos + 1);
            UNTIL (Pos = 0);
          END;

  }
  CODE
  {
    VAR
      GenericProcessSetup@1100528300 : Record 11229409;
      GenericProcessManagement@1100528302 : Codeunit 11229305;
      Parameter@1100528301 : Text;

    LOCAL PROCEDURE JobQueueHandler@1100525010();
    BEGIN
      ProcessQueueEntries;
      GenericProcessManagement.DeleteCompletedQueueEntries;
    END;

    LOCAL PROCEDURE "---Locals---"@1100528300();
    BEGIN
    END;

    LOCAL PROCEDURE ProcessQueueEntries@1100525006();
    VAR
      GenericProcessQueue@1100525000 : Record 11229430;
      GenericProcessStep@1100528302 : Record 11229408;
      GenericTempTable@1100528300 : TEMPORARY Record 11020579;
    BEGIN
      IF (Parameter = '') THEN
        EXIT;

      GenericTempTable.DELETEALL;
      GenericProcessQueue.SETFILTER(Status, '%1|%2', GenericProcessQueue.Status::Open,
        GenericProcessQueue.Status::Failed);
      GenericProcessQueue.SETRANGE("Company Name", COMPANYNAME);
      GenericProcessQueue.SETRANGE("Step Type", Parameter);
      IF (GenericProcessSetup."Max No. Of Retries" > 0) THEN
        GenericProcessQueue.SETRANGE("Try No.", 0, GenericProcessSetup."Max No. Of Retries" - 1);
      IF (NOT GenericProcessQueue.FINDSET) THEN
        EXIT;

      REPEAT
        GenericProcessQueue.CALCFIELDS(Priority);
        GenericProcessStep.GET(GenericProcessQueue."Step Name");
        IF (NOT GenericProcessStep.Manual) THEN BEGIN
          CLEAR(GenericTempTable);
          GenericTempTable."Key Code1" := FORMAT(GenericProcessQueue.Priority);
          GenericTempTable."Key Code2" := FORMAT(GenericProcessQueue."Entry No.");
          GenericTempTable.BigInt1 := GenericProcessQueue."Entry No.";
          GenericTempTable.INSERT;
        END;
      UNTIL (GenericProcessQueue.NEXT= 0);

      IF (GenericTempTable.FINDSET) THEN
        REPEAT
          GenericProcessManagement.ProcessQueueEntry(GenericTempTable.BigInt1);
        UNTIL (GenericTempTable.NEXT= 0);
    END;

    BEGIN
    END.
  }
}

