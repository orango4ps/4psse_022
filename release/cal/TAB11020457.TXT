OBJECT Table 11020457 Estimate Merge Field
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Estimate Merge Field;
               NOR=Flettefelt for Kalkyle;
               SVE=Kalkylkopplingsf„lt];
    LookupPageID=Page11125314;
  }
  FIELDS
  {
    { 10  ;   ;Code                ;Code20        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod] }
    { 100 ;   ;Table No.           ;Option        ;CaptionML=[ENU=Table;
                                                              NOR=Tabell;
                                                              SVE=Tabell];
                                                   OptionCaptionML=[ENU=" ,Company Information,,,,Try-Out Quote,Estimate,Sub-Estimate,Part";
                                                                    NOR=" ,Selskapsinformasjon,,,,Tilbudsforesp›rsel,Kalkyle,Delkalkyle,Del";
                                                                    SVE=" ,F”retagsinformation,,,,Anbudsoffert,Kalkyl,Delkalkyl,Del"];
                                                   OptionString=[ ,CompInfo,,,,TryOutQuote,Estimate,SubEstimate,Part] }
    { 101 ;   ;Field No.           ;Integer       ;OnValidate=VAR
                                                                FieldTableManagement@1100529200 : Codeunit 11012372;
                                                              BEGIN
                                                                FieldTableManagement.GetFieldCaption(GetTableID,"Field No.","Field Name");
                                                              END;

                                                   OnLookup=VAR
                                                              FieldTableManagement@1100529200 : Codeunit 11012372;
                                                            BEGIN
                                                              VALIDATE("Field No.",FieldTableManagement.LookupFieldNo(GetTableID,"Field No."));
                                                            END;

                                                   CaptionML=[ENU=Field No.;
                                                              NOR=Feltnr.;
                                                              SVE=F„ltnr] }
    { 102 ;   ;Field Name          ;Text30        ;CaptionML=[ENU=Field Name;
                                                              NOR=Feltnavn;
                                                              SVE=F„ltnamn];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      RecRef@1210190000 : RecordRef;
      Text002@1210190003 : TextConst 'ENU=******************************;NOR=******************************;SVE=******************************';
      EstTotal@1210190005 : Decimal;
      SubEstTotal@1210190006 : Decimal;
      PartTotal@1210190007 : Decimal;

    PROCEDURE GetTableID@4() TableID : Integer;
    BEGIN
      CASE "Table No." OF
        "Table No."::CompInfo : TableID := DATABASE::"Company Information";
        "Table No."::TryOutQuote: TableID := DATABASE::"Try-out Quote";
        "Table No."::Estimate: TableID := DATABASE::Estimate;
        "Table No."::SubEstimate: TableID := DATABASE::"Sub-Estimate";
        "Table No."::Part: TableID := DATABASE::"Estimate Part";
      END;
    END;

    PROCEDURE GetSubstitute@1210190004(DocNo@1100526201 : Code[20];SubDoc1@1210190003 : Code[20];SubDoc2@1210190004 : Code[20];SubDoc3@1210190005 : Code[20]) rTxtContent@11012000 : Text[250];
    VAR
      lvCompInfoRec@1210190002 : Record 79;
      lvTryOutRec@1210190000 : Record 11012758;
      lvEstRec@1210190006 : Record 11012151;
      lvSubEstRec@1210190001 : Record 11072074;
      lvEstPartRec@1210190007 : Record 11072073;
    BEGIN
      CASE "Table No." OF
        "Table No."::CompInfo:
          BEGIN
            lvCompInfoRec.GET;
            RecRef.GETTABLE(lvCompInfoRec);
            rTxtContent := AnyTableFieldValue(DATABASE::"Company Information", "Field No.");
          END;
        "Table No."::TryOutQuote:
          BEGIN
            lvEstRec.GET(DocNo);
            DocNo := lvEstRec.GetTryOut(2);
            IF NOT lvTryOutRec.GET(DocNo) THEN EXIT('');
            RecRef.GETTABLE(lvTryOutRec);
            rTxtContent := AnyTableFieldValue(DATABASE::"Try-out Quote", "Field No.");
          END;
        "Table No."::Estimate:
          BEGIN
            lvEstRec.GET(DocNo);
            RecRef.GETTABLE(lvEstRec);
            rTxtContent := AnyTableFieldValue(DATABASE::Estimate, "Field No.");
          END;
        "Table No."::SubEstimate:
          BEGIN
            lvSubEstRec.GET(DocNo, SubDoc1);
            RecRef.GETTABLE(lvSubEstRec);
            rTxtContent := AnyTableFieldValue(DATABASE::"Sub-Estimate", "Field No.");
          END;
        "Table No."::Part:
          BEGIN
            lvEstPartRec.GET(DocNo, SubDoc1, SubDoc2, SubDoc3);
            RecRef.GETTABLE(lvEstPartRec);
            rTxtContent := AnyTableFieldValue(DATABASE::"Estimate Part", "Field No.");
          END;
      END;
    END;

    PROCEDURE AnyTableFieldValue@1210190010(TableNo@1210190000 : Integer;FieldNo@1210190001 : Integer) : Text[250];
    VAR
      DecimalValue@1210190002 : Decimal;
      FldRef@1100526200 : FieldRef;
      FieldRec@1100526202 : Record 2000000041;
    BEGIN
      //copied from codeunit 110120401
      FldRef := RecRef.FIELD(FieldNo);
      IF NOT FieldRec.GET(TableNo,FieldNo) THEN
        EXIT(Text002);

      IF FieldRec.Class = FieldRec.Class::FlowField THEN
        FldRef.CALCFIELD;

      IF FieldRec.Type = FieldRec.Type::Decimal THEN BEGIN
        EVALUATE(DecimalValue,FORMAT(FldRef.VALUE));
        EXIT(FormatDecimal(DecimalValue));
      END;

      EXIT(FORMAT(FldRef));
    END;

    PROCEDURE FormatDecimal@1210190009(DecimalValue@1210190000 : Decimal) : Text[30];
    BEGIN
      //copied from codeunit 110120401
      EXIT(FORMAT(DecimalValue,0,'<Precision,2:><Standard format,0>'));
    END;

    PROCEDURE SetTotals@1210190000(iLevel@1210190003 : Integer;iEstTotal@1210190000 : Decimal;iSubEstTotal@1210190001 : Decimal;iPartTotal@1210190002 : Decimal);
    BEGIN
      IF iLevel >= 1 THEN
        EstTotal := iEstTotal;
      IF iLevel >= 2 THEN
        SubEstTotal := iSubEstTotal;
      IF iLevel >= 3 THEN
        PartTotal := iPartTotal;
    END;

    BEGIN
    END.
  }
}

