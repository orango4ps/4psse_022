OBJECT Table 207 Res. Journal Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03,4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               LOCKTABLE;
               ResJnlTemplate.GET("Journal Template Name");
               ResJnlBatch.GET("Journal Template Name","Journal Batch Name");

               ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
               ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");
             END;

    CaptionML=[ENU=Res. Journal Line;
               NOR=Res.kladdelinje;
               SVE=Resursjournalrad];
  }
  FIELDS
  {
    { 1   ;   ;Journal Template Name;Code10       ;TableRelation="Res. Journal Template";
                                                   CaptionML=[ENU=Journal Template Name;
                                                              NOR=Kladdemalnavn;
                                                              SVE=Mallnamn fîr journal] }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 3   ;   ;Entry Type          ;Option        ;CaptionML=[ENU=Entry Type;
                                                              NOR=Posttype;
                                                              SVE=Transaktionstyp];
                                                   OptionCaptionML=[ENU=Usage,Sale;
                                                                    NOR=Forbruk,Salg;
                                                                    SVE=Fîrbruk.kost.,Fîrs.];
                                                   OptionString=Usage,Sale }
    { 4   ;   ;Document No.        ;Code20        ;CaptionML=[ENU=Document No.;
                                                              NOR=Bilagsnr.;
                                                              SVE=Dokumentnr] }
    { 5   ;   ;Posting Date        ;Date          ;OnValidate=BEGIN
                                                                TESTFIELD("Posting Date");
                                                                VALIDATE("Document Date","Posting Date");
                                                              END;

                                                   CaptionML=[ENU=Posting Date;
                                                              NOR=Bokfõringsdato;
                                                              SVE=Bokfîringsdatum] }
    { 6   ;   ;Resource No.        ;Code20        ;TableRelation=Resource;
                                                   OnValidate=BEGIN
                                                                IF "Resource No." = '' THEN BEGIN
                                                                  CreateDim(
                                                                    DATABASE::Resource,"Resource No.",
                                                                    DATABASE::"Resource Group","Resource Group No.",
                                                                    DATABASE::Job,"Job No.");
                                                                  EXIT;
                                                                END;

                                                                Res.GET("Resource No.");
                                                                Res.CheckResourcePrivacyBlocked(FALSE);
                                                                Res.TESTFIELD(Blocked,FALSE);
                                                                Description := Res.Name;
                                                                "Direct Unit Cost" := Res."Direct Unit Cost";
                                                                "Unit Cost" := Res."Unit Cost";
                                                                "Unit Price" := Res."Unit Price";
                                                                "Resource Group No." := Res."Resource Group No.";
                                                                "Work Type Code" := '';
                                                                "Gen. Prod. Posting Group" := Res."Gen. Prod. Posting Group";
                                                                VALIDATE("Unit of Measure Code",Res."Base Unit of Measure");

                                                                IF NOT "System-Created Entry" THEN
                                                                  IF "Time Sheet No." = '' THEN
                                                                    Res.TESTFIELD("Use Time Sheet",FALSE);

                                                                CreateDim(
                                                                  DATABASE::Resource,"Resource No.",
                                                                  DATABASE::"Resource Group","Resource Group No.",
                                                                  DATABASE::Job,"Job No.");
                                                              END;

                                                   CaptionML=[ENU=Resource No.;
                                                              NOR=Ressursnr.;
                                                              SVE=Resursnr] }
    { 7   ;   ;Resource Group No.  ;Code20        ;TableRelation="Resource Group";
                                                   OnValidate=BEGIN
                                                                CreateDim(
                                                                  DATABASE::"Resource Group","Resource Group No.",
                                                                  DATABASE::Resource,"Resource No.",
                                                                  DATABASE::Job,"Job No.");
                                                              END;

                                                   CaptionML=[ENU=Resource Group No.;
                                                              NOR=Ressursgruppenr.;
                                                              SVE=Gruppnr];
                                                   Editable=No }
    { 8   ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 9   ;   ;Work Type Code      ;Code10        ;TableRelation="Work Type";
                                                   OnValidate=VAR
                                                                ResourceUnitOfMeasure@1000 : Record 205;
                                                              BEGIN
                                                                IF "Resource No." <> '' THEN BEGIN
                                                                  IF WorkType.GET("Work Type Code") THEN
                                                                    "Unit of Measure Code" := WorkType."Unit of Measure Code"
                                                                  ELSE BEGIN
                                                                    Res.GET("Resource No.");
                                                                    "Unit of Measure Code" := Res."Base Unit of Measure";
                                                                  END;

                                                                  IF "Unit of Measure Code" = '' THEN BEGIN
                                                                    Res.GET("Resource No.");
                                                                    "Unit of Measure Code" := Res."Base Unit of Measure"
                                                                  END;
                                                                  ResourceUnitOfMeasure.GET("Resource No.","Unit of Measure Code");
                                                                  "Qty. per Unit of Measure" := ResourceUnitOfMeasure."Qty. per Unit of Measure";

                                                                  FindResUnitCost;
                                                                  FindResPrice;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Work Type Code;
                                                              NOR=Arbeidstypekode;
                                                              SVE=Arbetstypkod] }
    { 10  ;   ;Job No.             ;Code20        ;TableRelation=Job;
                                                   OnValidate=BEGIN
                                                                FindResPrice;

                                                                CreateDim(
                                                                  DATABASE::Job,"Job No.",
                                                                  DATABASE::Resource,"Resource No.",
                                                                  DATABASE::"Resource Group","Resource Group No.");
                                                              END;

                                                   CaptionML=[ENU=Job No.;
                                                              NOR=Prosjektnr.;
                                                              SVE=Projektnr] }
    { 11  ;   ;Unit of Measure Code;Code10        ;TableRelation="Resource Unit of Measure".Code WHERE (Resource No.=FIELD(Resource No.));
                                                   OnValidate=VAR
                                                                ResourceUnitOfMeasure@1000 : Record 205;
                                                              BEGIN
                                                                IF CurrFieldNo <> FIELDNO("Work Type Code") THEN
                                                                  TESTFIELD("Work Type Code",'');

                                                                IF "Unit of Measure Code" = '' THEN BEGIN
                                                                  Res.GET("Resource No.");
                                                                  "Unit of Measure Code" := Res."Base Unit of Measure"
                                                                END;
                                                                ResourceUnitOfMeasure.GET("Resource No.","Unit of Measure Code");
                                                                "Qty. per Unit of Measure" := ResourceUnitOfMeasure."Qty. per Unit of Measure";

                                                                FindResUnitCost;
                                                                FindResPrice;

                                                                VALIDATE(Quantity);
                                                              END;

                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              NOR=Enhetskode;
                                                              SVE=Enhetskod] }
    { 12  ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Unit Cost");
                                                                VALIDATE("Unit Price");
                                                              END;

                                                   CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   DecimalPlaces=0:5 }
    { 13  ;   ;Direct Unit Cost    ;Decimal       ;CaptionML=[ENU=Direct Unit Cost;
                                                              NOR=Direkte enhetskost;
                                                              SVE=Inkîpspris];
                                                   MinValue=0;
                                                   AutoFormatType=2 }
    { 14  ;   ;Unit Cost           ;Decimal       ;OnValidate=BEGIN
                                                                "Total Cost" := Quantity * "Unit Cost";
                                                              END;

                                                   CaptionML=[ENU=Unit Cost;
                                                              NOR=Enhetskost;
                                                              SVE=Styckkostnad];
                                                   MinValue=0;
                                                   AutoFormatType=2 }
    { 15  ;   ;Total Cost          ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD(Quantity);
                                                                GetGLSetup;
                                                                "Unit Cost" := ROUND("Total Cost" / Quantity,GLSetup."Unit-Amount Rounding Precision");
                                                              END;

                                                   CaptionML=[ENU=Total Cost;
                                                              NOR=Kostbelõp;
                                                              SVE=Totalkostnad];
                                                   AutoFormatType=1 }
    { 16  ;   ;Unit Price          ;Decimal       ;OnValidate=BEGIN
                                                                "Total Price" := Quantity * "Unit Price";
                                                              END;

                                                   CaptionML=[ENU=Unit Price;
                                                              NOR=Salgspris;
                                                              SVE=A-pris];
                                                   MinValue=0;
                                                   AutoFormatType=2 }
    { 17  ;   ;Total Price         ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD(Quantity);
                                                                GetGLSetup;
                                                                "Unit Price" := ROUND("Total Price" / Quantity,GLSetup."Unit-Amount Rounding Precision");
                                                              END;

                                                   CaptionML=[ENU=Total Price;
                                                              NOR=Salgsbelõp;
                                                              SVE=Totalpris];
                                                   AutoFormatType=1 }
    { 18  ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
                                                              END;

                                                   CaptionML=[ENU=Shortcut Dimension 1 Code;
                                                              NOR=Snarveisdimensjon 1 - kode;
                                                              SVE=GenvÑg dimension 1 kod];
                                                   CaptionClass='1,2,1' }
    { 19  ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");
                                                              END;

                                                   CaptionML=[ENU=Shortcut Dimension 2 Code;
                                                              NOR=Snarveisdimensjon 2 - kode;
                                                              SVE=GenvÑg dimension 2 kod];
                                                   CaptionClass='1,2,2' }
    { 21  ;   ;Source Code         ;Code10        ;TableRelation="Source Code";
                                                   CaptionML=[ENU=Source Code;
                                                              NOR=Kildespor;
                                                              SVE=Ursprungskod];
                                                   Editable=No }
    { 23  ;   ;Journal Batch Name  ;Code10        ;TableRelation="Res. Journal Batch".Name WHERE (Journal Template Name=FIELD(Journal Template Name));
                                                   CaptionML=[ENU=Journal Batch Name;
                                                              NOR=Kladdenavn;
                                                              SVE=Journalnamn] }
    { 24  ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   CaptionML=[ENU=Reason Code;
                                                              NOR=èrsaksspor;
                                                              SVE=Uppfîljningskod] }
    { 25  ;   ;Recurring Method    ;Option        ;CaptionML=[ENU=Recurring Method;
                                                              NOR=Gjentakelsesprinsipp;
                                                              SVE=Metod fîr Üterkommande trans.];
                                                   OptionCaptionML=[ENU=,Fixed,Variable;
                                                                    NOR=,Fast,Variabel;
                                                                    SVE=,Fast,Variabel];
                                                   OptionString=,Fixed,Variable;
                                                   BlankZero=Yes }
    { 26  ;   ;Expiration Date     ;Date          ;CaptionML=[ENU=Expiration Date;
                                                              NOR=UtgÜr den;
                                                              SVE=UtgÜngsdatum] }
    { 27  ;   ;Recurring Frequency ;DateFormula   ;CaptionML=[ENU=Recurring Frequency;
                                                              NOR=Gjentakelsesintervall;
                                                              SVE=èterkommande frekvens] }
    { 28  ;   ;Gen. Bus. Posting Group;Code20     ;TableRelation="Gen. Business Posting Group";
                                                   CaptionML=[ENU=Gen. Bus. Posting Group;
                                                              NOR=Bokfõringsgruppe - firma;
                                                              SVE=Gen. rîrelsebokfîringsmall] }
    { 29  ;   ;Gen. Prod. Posting Group;Code20    ;TableRelation="Gen. Product Posting Group";
                                                   CaptionML=[ENU=Gen. Prod. Posting Group;
                                                              NOR=Bokfõringsgruppe - vare;
                                                              SVE=Produktbokfîringsmall] }
    { 30  ;   ;Document Date       ;Date          ;CaptionML=[ENU=Document Date;
                                                              NOR=Bilagsdato;
                                                              SVE=Dokumentdatum] }
    { 31  ;   ;External Document No.;Code35       ;CaptionML=[ENU=External Document No.;
                                                              NOR=Eksterndokumentnr.;
                                                              SVE=Externt dokumentnr] }
    { 32  ;   ;Posting No. Series  ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=Posting No. Series;
                                                              NOR=Bokfõringsnr.serie;
                                                              SVE=Bokfîringsnr-serie] }
    { 33  ;   ;Source Type         ;Option        ;CaptionML=[ENU=Source Type;
                                                              NOR=Kildetype;
                                                              SVE=Ursprungstyp];
                                                   OptionCaptionML=[ENU=" ,Customer";
                                                                    NOR=" ,Kunde";
                                                                    SVE=" ,Kund"];
                                                   OptionString=[ ,Customer] }
    { 34  ;   ;Source No.          ;Code20        ;TableRelation=IF (Source Type=CONST(Customer)) Customer.No.;
                                                   CaptionML=[ENU=Source No.;
                                                              NOR=Kildenr.;
                                                              SVE=Ursprungsnr] }
    { 35  ;   ;Qty. per Unit of Measure;Decimal   ;CaptionML=[ENU=Qty. per Unit of Measure;
                                                              NOR=Antall per enhet;
                                                              SVE=Antal per enhet] }
    { 90  ;   ;Order Type          ;Option        ;CaptionML=[ENU=Order Type;
                                                              NOR=Ordretype;
                                                              SVE=Ordertyp];
                                                   OptionCaptionML=[ENU=" ,Production,Transfer,Service,Assembly";
                                                                    NOR=" ,Produksjon,Overfõring,Service,Montering";
                                                                    SVE=" ,Produktion,ôverfîring,Service,Montering"];
                                                   OptionString=[ ,Production,Transfer,Service,Assembly];
                                                   Editable=No }
    { 91  ;   ;Order No.           ;Code20        ;CaptionML=[ENU=Order No.;
                                                              NOR=Ordrenr.;
                                                              SVE=Ordernr];
                                                   Editable=No }
    { 92  ;   ;Order Line No.      ;Integer       ;CaptionML=[ENU=Order Line No.;
                                                              NOR=Ordrelinjenr.;
                                                              SVE=Orderradnr];
                                                   Editable=No }
    { 480 ;   ;Dimension Set ID    ;Integer       ;TableRelation="Dimension Set Entry";
                                                   OnValidate=BEGIN
                                                                DimMgt.UpdateGlobalDimFromDimSetID("Dimension Set ID","Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              ShowDimensions;
                                                            END;

                                                   CaptionML=[ENU=Dimension Set ID;
                                                              NOR=Dimensjonssett-ID;
                                                              SVE=DimensionsuppsÑttnings-ID];
                                                   Editable=No }
    { 950 ;   ;Time Sheet No.      ;Code20        ;TableRelation="Time Sheet Header";
                                                   CaptionML=[ENU=Time Sheet No.;
                                                              NOR=Timelistenummer;
                                                              SVE=Tidrapportsnr] }
    { 951 ;   ;Time Sheet Line No. ;Integer       ;TableRelation="Time Sheet Line"."Line No." WHERE (Time Sheet No.=FIELD(Time Sheet No.));
                                                   CaptionML=[ENU=Time Sheet Line No.;
                                                              NOR=Linjenr. for timeliste;
                                                              SVE=Radnummer pÜ tidrapport] }
    { 952 ;   ;Time Sheet Date     ;Date          ;TableRelation="Time Sheet Detail".Date WHERE (Time Sheet No.=FIELD(Time Sheet No.),
                                                                                                 Time Sheet Line No.=FIELD(Time Sheet Line No.));
                                                   CaptionML=[ENU=Time Sheet Date;
                                                              NOR=Dato for timeliste;
                                                              SVE=Tidrapportdatum] }
    { 959 ;   ;System-Created Entry;Boolean       ;CaptionML=[ENU=System-Created Entry;
                                                              NOR=Opprettet automatisk;
                                                              SVE=Systemkreerad trans.];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Journal Template Name,Journal Batch Name,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ResJnlTemplate@1000 : Record 206;
      ResJnlBatch@1001 : Record 236;
      ResJnlLine@1002 : Record 207;
      Res@1003 : Record 156;
      ResCost@1004 : Record 202;
      ResPrice@1005 : Record 201;
      WorkType@1006 : Record 200;
      GLSetup@1012 : Record 98;
      NoSeriesMgt@1009 : Codeunit 396;
      DimMgt@1010 : Codeunit 408;
      GLSetupRead@1011 : Boolean;
      DPA_Management@1100000999 : Codeunit 11012783;

    LOCAL PROCEDURE FindResUnitCost@1();
    BEGIN
      ResCost.INIT;
      ResCost.Code := "Resource No.";
      ResCost."Work Type Code" := "Work Type Code";
      CODEUNIT.RUN(CODEUNIT::"Resource-Find Cost",ResCost);
      "Direct Unit Cost" := ResCost."Direct Unit Cost" * "Qty. per Unit of Measure";
      "Unit Cost" := ResCost."Unit Cost" * "Qty. per Unit of Measure";
      VALIDATE("Unit Cost");
    END;

    LOCAL PROCEDURE FindResPrice@2();
    BEGIN
      ResPrice.INIT;
      ResPrice.Code := "Resource No.";
      ResPrice."Work Type Code" := "Work Type Code";
      CODEUNIT.RUN(CODEUNIT::"Resource-Find Price",ResPrice);
      "Unit Price" := ResPrice."Unit Price" * "Qty. per Unit of Measure";
      VALIDATE("Unit Price");
    END;

    [External]
    PROCEDURE EmptyLine@5() : Boolean;
    BEGIN
      EXIT(("Resource No." = '') AND (Quantity = 0));
    END;

    [External]
    PROCEDURE SetUpNewLine@8(LastResJnlLine@1000 : Record 207);
    BEGIN
      ResJnlTemplate.GET("Journal Template Name");
      ResJnlBatch.GET("Journal Template Name","Journal Batch Name");
      ResJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
      ResJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");
      IF ResJnlLine.FINDFIRST THEN BEGIN
        "Posting Date" := LastResJnlLine."Posting Date";
        "Document Date" := LastResJnlLine."Posting Date";
        "Document No." := LastResJnlLine."Document No.";
      END ELSE BEGIN
        "Posting Date" := WORKDATE;
        "Document Date" := WORKDATE;
        IF ResJnlBatch."No. Series" <> '' THEN BEGIN
          CLEAR(NoSeriesMgt);
          "Document No." := NoSeriesMgt.TryGetNextNo(ResJnlBatch."No. Series","Posting Date");
        END;
      END;
      "Recurring Method" := LastResJnlLine."Recurring Method";
      "Source Code" := ResJnlTemplate."Source Code";
      "Reason Code" := ResJnlBatch."Reason Code";
      "Posting No. Series" := ResJnlBatch."Posting No. Series";
    END;

    [External]
    PROCEDURE CreateDim@13(Type1@1000 : Integer;No1@1001 : Code[20];Type2@1002 : Integer;No2@1003 : Code[20];Type3@1004 : Integer;No3@1005 : Code[20]);
    VAR
      TableID@1006 : ARRAY [10] OF Integer;
      No@1007 : ARRAY [10] OF Code[20];
    BEGIN
      TableID[1] := Type1;
      No[1] := No1;
      TableID[2] := Type2;
      No[2] := No2;
      TableID[3] := Type3;
      No[3] := No3;
      OnAfterCreateDimTableIDs(Rec,CurrFieldNo,TableID,No);

      "Shortcut Dimension 1 Code" := '';
      "Shortcut Dimension 2 Code" := '';
      "Dimension Set ID" :=
        DimMgt.GetRecDefaultDimID(
          Rec,CurrFieldNo,TableID,No,"Source Code","Shortcut Dimension 1 Code","Shortcut Dimension 2 Code",0,0);
    END;

    [External]
    PROCEDURE ValidateShortcutDimCode@14(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateShortcutDimValues(FieldNumber,ShortcutDimCode,"Dimension Set ID");
    END;

    [External]
    PROCEDURE LookupShortcutDimCode@18(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.LookupDimValueCode(FieldNumber,ShortcutDimCode);
      DimMgt.ValidateShortcutDimValues(FieldNumber,ShortcutDimCode,"Dimension Set ID");
    END;

    [External]
    PROCEDURE ShowShortcutDimCode@15(VAR ShortcutDimCode@1000 : ARRAY [8] OF Code[20]);
    BEGIN
      DimMgt.GetShortcutDimensions("Dimension Set ID",ShortcutDimCode);
    END;

    [External]
    PROCEDURE CopyDocumentFields@129(DocNo@1003 : Code[20];ExtDocNo@1002 : Text[35];SourceCode@1001 : Code[10];NoSeriesCode@1000 : Code[20]);
    BEGIN
      "Document No." := DocNo;
      "External Document No." := ExtDocNo;
      "Source Code" := SourceCode;
      IF NoSeriesCode <> '' THEN
        "Posting No. Series" := NoSeriesCode;
    END;

    [External]
    PROCEDURE CopyFromSalesHeader@24(SalesHeader@1000 : Record 36);
    BEGIN
      "Posting Date" := SalesHeader."Posting Date";
      "Document Date" := SalesHeader."Document Date";
      "Reason Code" := SalesHeader."Reason Code";

      OnAfterCopyResJnlLineFromSalesHeader(SalesHeader,Rec);
    END;

    [External]
    PROCEDURE CopyFromSalesLine@6(SalesLine@1000 : Record 37);
    BEGIN
      "Resource No." := SalesLine."No.";
      Description := SalesLine.Description;
      "Source Type" := "Source Type"::Customer;
      "Source No." := SalesLine."Sell-to Customer No.";
      "Work Type Code" := SalesLine."Work Type Code";
      "Job No." := SalesLine."Job No.";
      "Unit of Measure Code" := SalesLine."Unit of Measure Code";
      "Shortcut Dimension 1 Code" := SalesLine."Shortcut Dimension 1 Code";
      "Shortcut Dimension 2 Code" := SalesLine."Shortcut Dimension 2 Code";
      "Dimension Set ID" := SalesLine."Dimension Set ID";
      "Gen. Bus. Posting Group" := SalesLine."Gen. Bus. Posting Group";
      "Gen. Prod. Posting Group" := SalesLine."Gen. Prod. Posting Group";
      "Entry Type" := "Entry Type"::Sale;
      "Qty. per Unit of Measure" := SalesLine."Qty. per Unit of Measure";
      Quantity := -SalesLine."Qty. to Invoice";
      "Unit Cost" := SalesLine."Unit Cost (LCY)";
      "Total Cost" := SalesLine."Unit Cost (LCY)" * Quantity;
      "Unit Price" := SalesLine."Unit Price";
      "Total Price" := -SalesLine.Amount;

      OnAfterCopyResJnlLineFromSalesLine(SalesLine,Rec);
    END;

    [External]
    PROCEDURE CopyFromServHeader@11(ServiceHeader@1000 : Record 5900);
    BEGIN
      "Document Date" := ServiceHeader."Document Date";
      "Reason Code" := ServiceHeader."Reason Code";
      "Order No." := ServiceHeader."No.";

      OnAfterCopyResJnlLineFromServHeader(ServiceHeader,Rec);
    END;

    [External]
    PROCEDURE CopyFromServLine@7(ServiceLine@1000 : Record 5902);
    BEGIN
      "Posting Date" := ServiceLine."Posting Date";
      "Order Type" := "Order Type"::Service;
      "Order Line No." := ServiceLine."Line No.";
      "Resource No." := ServiceLine."No.";
      Description := ServiceLine.Description;
      "Work Type Code" := ServiceLine."Work Type Code";
      "Shortcut Dimension 1 Code" := ServiceLine."Shortcut Dimension 1 Code";
      "Shortcut Dimension 2 Code" := ServiceLine."Shortcut Dimension 2 Code";
      "Dimension Set ID" := ServiceLine."Dimension Set ID";
      "Unit of Measure Code" := ServiceLine."Unit of Measure Code";
      "Qty. per Unit of Measure" := ServiceLine."Qty. per Unit of Measure";
      "Gen. Bus. Posting Group" := ServiceLine."Gen. Bus. Posting Group";
      "Gen. Prod. Posting Group" := ServiceLine."Gen. Prod. Posting Group";
      "Source Type" := "Source Type"::Customer;
      "Source No." := ServiceLine."Customer No.";
      "Time Sheet No." := ServiceLine."Time Sheet No.";
      "Time Sheet Line No." := ServiceLine."Time Sheet Line No.";
      "Time Sheet Date" := ServiceLine."Time Sheet Date";
      "Job No." := ServiceLine."Job No.";

      OnAfterCopyResJnlLineFromServLine(ServiceLine,Rec);
    END;

    [External]
    PROCEDURE CopyFromServShptHeader@10(ServShptHeader@1000 : Record 5990);
    BEGIN
      "Document Date" := ServShptHeader."Document Date";
      "Reason Code" := ServShptHeader."Reason Code";
      "Source Type" := "Source Type"::Customer;
      "Source No." := ServShptHeader."Customer No.";

      OnAfterCopyResJnlLineFromServShptHeader(ServShptHeader,Rec);
    END;

    [External]
    PROCEDURE CopyFromServShptLine@9(ServShptLine@1000 : Record 5991);
    BEGIN
      "Posting Date" := ServShptLine."Posting Date";
      "Resource No." := ServShptLine."No.";
      Description := ServShptLine.Description;
      "Work Type Code" := ServShptLine."Work Type Code";
      "Unit of Measure Code" := ServShptLine."Unit of Measure Code";
      "Qty. per Unit of Measure" := ServShptLine."Qty. per Unit of Measure";
      "Shortcut Dimension 1 Code" := ServShptLine."Shortcut Dimension 1 Code";
      "Shortcut Dimension 2 Code" := ServShptLine."Shortcut Dimension 2 Code";
      "Dimension Set ID" := ServShptLine."Dimension Set ID";
      "Gen. Bus. Posting Group" := ServShptLine."Gen. Bus. Posting Group";
      "Gen. Prod. Posting Group" := ServShptLine."Gen. Prod. Posting Group";
      "Entry Type" := "Entry Type"::Usage;

      OnAfterCopyResJnlLineFromServShptLine(ServShptLine,Rec);
    END;

    [External]
    PROCEDURE CopyFromJobJnlLine@22(JobJnlLine@1000 : Record 11072008);
    BEGIN
      "Entry Type" := JobJnlLine."Entry Type";
      "Document No." := JobJnlLine."Document No.";
      "External Document No." := JobJnlLine."External Document No.";
      "Posting Date" := JobJnlLine."Posting Date";
      "Document Date" := JobJnlLine."Document Date";
      "Resource No." := JobJnlLine."No.";
      Description := JobJnlLine.Description;
      "Work Type Code" := JobJnlLine."Work Type Code";
      "Job No." := JobJnlLine."Job No.";
      "Shortcut Dimension 1 Code" := JobJnlLine."Shortcut Dimension 1 Code";
      "Shortcut Dimension 2 Code" := JobJnlLine."Shortcut Dimension 2 Code";
      "Dimension Set ID" := JobJnlLine."Dimension Set ID";
      "Unit of Measure Code" := JobJnlLine."Unit of Measure Code";
      "Source Code" := JobJnlLine."Source Code";
      "Gen. Bus. Posting Group" := JobJnlLine."Gen. Bus. Posting Group";
      "Gen. Prod. Posting Group" := JobJnlLine."Gen. Prod. Posting Group";
      "Posting No. Series" := JobJnlLine."Posting No. Series";
      "Reason Code" := JobJnlLine."Reason Code";
      "Resource Group No." := JobJnlLine."Resource Group No.";
      "Recurring Method" := JobJnlLine."Recurring Method";
      "Expiration Date" := JobJnlLine."Expiration Date";
      "Recurring Frequency" := JobJnlLine."Recurring Frequency";
      Quantity := JobJnlLine.Quantity;
      "Qty. per Unit of Measure" := JobJnlLine."Qty. per Unit of Measure";
      "Direct Unit Cost" := JobJnlLine."Direct Unit Cost (LCY)";
      "Unit Cost" := JobJnlLine."Unit Cost (LCY)";
      "Total Cost" := JobJnlLine."Total Cost (LCY)";
      "Unit Price" := JobJnlLine."Unit Price (LCY)";
      "Total Price" := JobJnlLine."Line Amount (LCY)";
      "Time Sheet No." := JobJnlLine."Time Sheet No.";
      "Time Sheet Line No." := JobJnlLine."Time Sheet Line No.";
      "Time Sheet Date" := JobJnlLine."Time Sheet Date";

      OnAfterCopyResJnlLineFromJobJnlLine(Rec,JobJnlLine);
    END;

    LOCAL PROCEDURE GetGLSetup@3();
    BEGIN
      IF NOT GLSetupRead THEN
        GLSetup.GET;
      GLSetupRead := TRUE;
    END;

    [External]
    PROCEDURE ShowDimensions@4();
    BEGIN
      DimMgt.SetTableID(DATABASE::"Res. Journal Line"); //**4PS.n
      "Dimension Set ID" :=
        DimMgt.EditDimensionSet("Dimension Set ID",STRSUBSTNO('%1 %2 %3',"Journal Template Name","Journal Batch Name","Line No."));
      DimMgt.UpdateGlobalDimFromDimSetID("Dimension Set ID","Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
    END;

    [External]
    PROCEDURE IsOpenedFromBatch@42() : Boolean;
    VAR
      ResJournalBatch@1002 : Record 236;
      TemplateFilter@1001 : Text;
      BatchFilter@1000 : Text;
    BEGIN
      BatchFilter := GETFILTER("Journal Batch Name");
      IF BatchFilter <> '' THEN BEGIN
        TemplateFilter := GETFILTER("Journal Template Name");
        IF TemplateFilter <> '' THEN
          ResJournalBatch.SETFILTER("Journal Template Name",TemplateFilter);
        ResJournalBatch.SETFILTER(Name,BatchFilter);
        ResJournalBatch.FINDFIRST;
      END;

      EXIT((("Journal Batch Name" <> '') AND ("Journal Template Name" = '')) OR (BatchFilter <> ''));
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyResJnlLineFromSalesHeader@25(VAR SalesHeader@1000 : Record 36;VAR ResJournalLine@1001 : Record 207);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyResJnlLineFromSalesLine@12(VAR SalesLine@1000 : Record 37;VAR ResJnlLine@1001 : Record 207);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyResJnlLineFromServHeader@23(VAR ServiceHeader@1000 : Record 5900;VAR ResJournalLine@1001 : Record 207);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyResJnlLineFromServLine@16(VAR ServLine@1000 : Record 5902;VAR ResJnlLine@1001 : Record 207);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyResJnlLineFromServShptHeader@21(VAR ServiceShipmentHeader@1000 : Record 5990;VAR ResJournalLine@1001 : Record 207);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyResJnlLineFromServShptLine@17(VAR ServShptLine@1000 : Record 5991;VAR ResJnlLine@1001 : Record 207);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyResJnlLineFromJobJnlLine@19(VAR ResJnlLine@1000 : Record 207;VAR JobJnlLine@1001 : Record 11072008);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCreateDimTableIDs@164(VAR ResJournalLine@1000 : Record 207;VAR FieldNo@1001 : Integer;VAR TableID@1003 : ARRAY [10] OF Integer;VAR No@1002 : ARRAY [10] OF Code[20]);
    BEGIN
    END;

    PROCEDURE DPA_GetFieldNoDepartmentFilter@1100000993() : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      EXIT(0);
    END;

    PROCEDURE DPA_GetFiltersFltrGrp@1100000997(FilterGroupNo@1100525000 : Integer) FilterText : Text[250];
    BEGIN
      //**4PS Generated Code - Do Not Change
      FILTERGROUP(FilterGroupNo);
      FilterText := COPYSTR(GETFILTERS, 1, MAXSTRLEN(FilterText));
      FILTERGROUP(0);
    END;

    PROCEDURE DPA_SetFilters@1100000998(Context@1100525000 : Text[30]) : Boolean;
    VAR
      RecRef@1100525003 : RecordRef;
      i@1100525004 : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN
        EXIT;

      RecRef.GETTABLE(Rec);
      DPA_Management.SetDPAFilterFromContext(
        RecRef,
        Context,
        DPA_GetFieldNoDepartmentCode,
        DPA_GetFieldNoDepartmentFilter );

      FOR i := 10 DOWNTO 0 DO BEGIN
        RecRef.FILTERGROUP(i);
        FILTERGROUP(i);
        SETVIEW(RecRef.GETVIEW(FALSE));
      END;

      EXIT(DPA_Management.IsHardFilter);
    END;

    LOCAL PROCEDURE DPA_IsActive@1100000999() : Boolean;
    VAR
      DepartmentAuthorizObject@1100526000 : Record 11012466;
      FPSLicenseManagement@1100528500 : Codeunit 11229289;
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT FPSLicenseManagement.LicenseAndReadPermissionForTable(DepartmentAuthorizObject) THEN
        EXIT(FALSE);

      IF NOT DPA_Management.AuthorizationActive THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    PROCEDURE DPA_FindRecWithDPAFilterAppld@1100000989();
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN BEGIN
        FIND;
        EXIT;
      END;

      IF NOT DPA_Management.HasTableHardFilter(207) THEN BEGIN
        FIND;
        EXIT;
      END;

      DPA_SetFilters('Table 207');
      IF NOT FIND THEN
        DPA_Management.RaiseDPAErrorForTable(TABLECAPTION);
    END;

    PROCEDURE DPA_ChkDepartmentCodeAllowed@1100000990(IsHardFilter@1100526000 : Boolean);
    VAR
      DimensionManagement@1100526001 : Codeunit 408;
    BEGIN
      //**4PS Generated Code - Do Not Change
      DimensionManagement.CheckDepartmAuthWithFilterType(
        DATABASE::"Res. Journal Line", "Shortcut Dimension 1 Code", IsHardFilter);
    END;

    PROCEDURE DPA_GetFieldNoDepartmentCode@1100000994() : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      EXIT(FIELDNO("Shortcut Dimension 1 Code"));
    END;

    PROCEDURE DPA_SetDefaultDepartment@1100000996();
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN
        EXIT;

      DPA_Management.SetDepartmentCodeUser(TABLENAME, "Shortcut Dimension 1 Code");
    END;

    BEGIN
    END.
  }
}

