OBJECT Report 11071715 Upgrade MMR Recording Qty.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Aufnahmemenge von Kalt-Aufnahmemenge aktualisieren;
               ENU=Upgrade Recording Quantity from Cold Recording Quantity;
               NLD=Upgrade opname aantal vanuit koude opname];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   ProjSetup.GET;
                 END;

    OnPreReport=BEGIN
                  Window.OPEN(Text000 + Text001);
                  IF ProjCde = '' THEN
                    ProjCde := Job.GETFILTER("No.");
                END;

    OnPostReport=BEGIN
                   Window.CLOSE;
                   IF CounterTotal = 0 THEN
                     MESSAGE(Text007)
                   ELSE
                     MESSAGE(Text008,CounterTotal);
                 END;

  }
  DATASET
  {
    { 8019;    ;DataItem;                    ;
               DataItemTable=Table11072003;
               DataItemTableView=SORTING(No.)
                                 WHERE(No.=FILTER(<>''),
                                       Maintenance And Renovation=CONST(Yes));
               OnAfterGetRecord=BEGIN
                                  IF ("No." = '') THEN
                                    CurrReport.SKIP;
                                  ProjCde := "No.";
                                  //
                                  IF MMRConversionLog.GET("No.") THEN BEGIN
                                    MESSAGE(Text009, "No.");
                                    CurrReport.SKIP;
                                  END ELSE BEGIN
                                    MMRConversionLog.INIT;
                                    MMRConversionLog."Job No." := "No.";
                                    MMRConversionLog."Conversion required" := TRUE;
                                    MMRConversionLog."Date Converted" := TODAY;
                                    MMRConversionLog.INSERT;
                                  END;
                                  IF Job."Budget Level for Settl. Q." = Job."Budget Level for Settl. Q."::Detailed THEN BEGIN
                                   Job."Budget Level for Settl. Q." := Job."Budget Level for Settl. Q."::"SQ-Cost Object";
                                   Job.MODIFY;
                                  END;
                                  //
                                END;

               ReqFilterFields=No. }

    { 1100528205;1;DataItem;                 ;
               DataItemTable=Table11072251;
               DataItemTableView=SORTING(Project No.,Plot No.,Phase,Recording Date,Seq. No.)
                                 ORDER(Descending)
                                 WHERE(Phase=CONST(Cold Recording),
                                       Plot No.=FILTER(''));
               MaxIteration=1;
               OnPreDataItem=BEGIN
                               IF NOT Job."Maintenance And Renovation" THEN
                                 CurrReport.BREAK;
                             END;

               OnAfterGetRecord=BEGIN
                                  PlotSQRecording.SETRANGE("Project No.", "Plot SQ Recording"."Project No.");
                                  PlotSQRecording.SETRANGE("Plot No.", '');
                                  PlotSQRecording.SETRANGE(Phase, PlotSQRecording.Phase::"Warm Recording",PlotSQRecording.Phase::Production);
                                  PlotSQRecording.SETCURRENTKEY("Project No.","Plot No.",Phase,"Recording Date","Seq. No.");
                                  IF NOT PlotSQRecording.FINDFIRST THEN BEGIN
                                    MMRConversionLog."Conversion required" := FALSE;
                                    MMRConversionLog."Date Converted" := 0D;
                                    MMRConversionLog.MODIFY;
                                    CurrReport.BREAK;
                                  END;
                                END;

               DataItemLink=Project No.=FIELD(No.) }

    { 1100528204;2;DataItem;PlotDataItem     ;
               DataItemTable=Table11012500;
               DataItemTableView=SORTING(Project No.,Plot No.);
               OnPreDataItem=BEGIN
                               IF Job."No." <> '' THEN; //For Debugging this line added.
                             END;

               OnAfterGetRecord=BEGIN
                                  IF PlotDataItem."Project No." = Job."No." THEN; //For Debugging this line added.

                                  ReferencePlotSQRecordingLine.SETCURRENTKEY("Project No.","Plot No.","Recording Seq. No.","House Model",Unit,"Line No.");
                                  ReferencePlotSQRecordingLine.SETRANGE("Project No.",  PlotDataItem."Project No.");
                                  ReferencePlotSQRecordingLine.SETRANGE("Plot No.", PlotDataItem."Plot No.");
                                  ReferencePlotSQRecordingLine.SETRANGE(Phase, ReferencePlotSQRecordingLine.Phase::"Warm Recording",ReferencePlotSQRecordingLine.Phase::Production);
                                  IF NOT ReferencePlotSQRecordingLine.FINDFIRST THEN
                                    CurrReport.BREAK;

                                  PlotSQRecording.SETRANGE("Project No.", PlotDataItem."Project No.");
                                  PlotSQRecording.SETRANGE("Plot No.", '');
                                  PlotSQRecording.SETRANGE(Phase, ReferencePlotSQRecordingLine.Phase);
                                  PlotSQRecording.SETRANGE("Recording Date",ReferencePlotSQRecordingLine."Recording Date");
                                  PlotSQRecording.SETRANGE("Seq. No.", ReferencePlotSQRecordingLine."Recording Seq. No.");
                                  IF PlotSQRecording.FINDFIRST THEN;
                                END;

               ReqFilterFields=Plot No.;
               DataItemLink=Project No.=FIELD(Project No.) }

    { 1100528203;3;DataItem;SettlementQuantityCrosslist;
               DataItemTable=Table11072253;
               DataItemTableView=SORTING(Project No.,House Model,Unit,Type,Main Group,Group,Sub Group,Code,Line No.)
                                 ORDER(Ascending);
               OnAfterGetRecord=VAR
                                  PlotSQRecordingLine@1100528200 : Record 11072252;
                                  PlotSQConversionLine@1100528201 : Record 11072255;
                                BEGIN
                                  PlotSQRecordingLine.SETCURRENTKEY("Project No.","Plot No.","Recording Seq. No.","House Model",Unit,"Line No.");
                                  PlotSQRecordingLine.SETRANGE("Project No.", SettlementQuantityCrosslist."Project No.");
                                  PlotSQRecordingLine.SETRANGE("Plot No.", PlotDataItem."Plot No.");
                                  PlotSQRecordingLine.SETRANGE("House Model", SettlementQuantityCrosslist."House Model");
                                  PlotSQRecordingLine.SETRANGE(Unit, SettlementQuantityCrosslist.Unit);
                                  PlotSQRecordingLine.SETRANGE("Line No.", SettlementQuantityCrosslist."Line No.");

                                  IF PlotSQRecordingLine.FINDLAST THEN BEGIN
                                    PlotSQRecordingLine.SETFILTER("Date Filter", '..%1', PlotSQRecording."Recording Date");
                                    PlotSQRecordingLine.CALCFIELDS("Cold Recording Quantity", "Production Recording Quantity");
                                    IF PlotSQRecordingLine."Cold Recording Quantity" <> 0 THEN BEGIN
                                      SettlementQuantityManagement.SetConversionStatus(TRUE);
                                      SettlementQuantityManagement.Recording_UpdateQuantity(SettlementQuantityCrosslist,PlotDataItem."Plot No.",
                                                   PlotSQRecording."Recording Date",
                                                   PlotSQRecording.Phase,
                                                   PlotSQRecordingLine."Cold Recording Quantity" + PlotSQRecordingLine."Production Recording Quantity");
                                      SettlementQuantityManagement.SetConversionStatus(FALSE);
                                      IF PlotSQConversionLine.GET(PlotSQRecordingLine."Project No.",PlotSQRecordingLine."Plot No.",PlotSQRecordingLine."Recording Seq. No.",
                                                                      PlotSQRecordingLine."House Model",PlotSQRecordingLine.Unit,PlotSQRecordingLine."Line No.") THEN BEGIN
                                        PlotSQConversionLine."Cold Recording Quantity" := PlotSQRecordingLine."Cold Recording Quantity";
                                        PlotSQConversionLine."Production Recording Quantity" := PlotSQRecordingLine."Production Recording Quantity";
                                        PlotSQConversionLine."Conversion Date" := TODAY;
                                        PlotSQConversionLine."Recording Date" := PlotSQRecordingLine."Recording Date";
                                        PlotSQConversionLine.MODIFY;
                                      END ELSE BEGIN
                                        PlotSQConversionLine."Project No." := PlotSQRecordingLine."Project No.";
                                        PlotSQConversionLine."Plot No." := PlotSQRecordingLine."Plot No.";
                                        PlotSQConversionLine."Recording Seq. No." := PlotSQRecordingLine."Recording Seq. No.";
                                        PlotSQConversionLine."House Model" := PlotSQRecordingLine."House Model";
                                        PlotSQConversionLine.Unit := PlotSQRecordingLine.Unit;
                                        PlotSQConversionLine."Line No." := PlotSQRecordingLine."Line No.";
                                        PlotSQConversionLine."Cold Recording Quantity" := PlotSQRecordingLine."Cold Recording Quantity";
                                        PlotSQConversionLine."Production Recording Quantity" := PlotSQRecordingLine."Production Recording Quantity";
                                        PlotSQConversionLine."Conversion Date" := TODAY;
                                        PlotSQConversionLine."Recording Date" := PlotSQRecordingLine."Recording Date";
                                        PlotSQConversionLine.INSERT;
                                      END;

                                      CounterTotal := CounterTotal + 1;
                                    END;
                                  END;
                                END;

               DataItemLink=Project No.=FIELD(Project No.) }

    { 1100525000;1;DataItem;                 ;
               DataItemTable=Table11012001;
               OnPreDataItem=BEGIN
                               SETFILTER("Budget Level for Settl. Q.", '<>%1', Job."Budget Level for Settl. Q.");
                             END;

               OnAfterGetRecord=BEGIN
                                  "Budget Level for Settl. Q." := Job."Budget Level for Settl. Q.";
                                  "Budget Line".MODIFY;
                                END;

               DataItemLink=Project No.=FIELD(No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF gvSelection = FALSE THEN BEGIN
                     ProjCde := Job.GETFILTER("No.");
                     CustCde := '';
                   END ELSE BEGIN  //db, 18-07-08
                     CustCde := gvCust;
                   END;
                 END;

    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@11012011 : TextConst 'DEU=Aufnahmen werden konvertiert...\\;ENU=Convert Recordings....\\;NLD=Opnames worden geconverteerd...\\';
      Text001@11012012 : TextConst 'DEU=Prozentsatz fertig     @1@@@@@@@@@@@@@\;ENU=Percentage Finished  @1@@@@@@@@@@@@@\;NLD=Percentage gereed  @1@@@@@@@@@@@@@\;NOR=Prosent ferdig  @1@@@@@@@@@@@@@\;SVE=Procent f„rdigt  @1@@@@@@@@@@@@@\';
      Text007@11012018 : TextConst 'DEU=Es wurden keine Aufnahmezeilen konvertiert.;ENU=There are no Recording Lines converted.;NLD=Er zijn geen opnameregels geconverteerd.';
      Text008@11012019 : TextConst 'DEU=%1 Aufnahmezeilen wurden konvertiert.;ENU=%1 Recording Lines have been converted.;NLD=%1 Opnameregels zijn geconverteerd.';
      ProjSetup@1210190004 : Record 315;
      SettlementQuantityManagement@1100485013 : Codeunit 11071690;
      Window@1210190013 : Dialog;
      CounterTotal@1210190009 : Integer;
      CustCde@1210190008 : Code[20];
      ProjCde@1210190002 : Code[20];
      gvSelection@1100485016 : Boolean;
      gvCust@1100485019 : Code[20];
      Text009@1100528202 : TextConst 'DEU=Projekt %1 wurde vorher konvertiert.;ENU=Project %1 iis converted before.;NLD=Project %1 is al eerder geconverteerd.';
      MMRConversionLog@1100528205 : Record 11072246;
      ReferencePlotSQRecordingLine@1100525000 : Record 11072252;
      PlotSQRecording@1100525001 : Record 11072251;

    PROCEDURE SetSelection@1100485000(lvProj@1210190000 : Code[20]);
    BEGIN
      ProjCde := lvProj;
      gvSelection := TRUE;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

