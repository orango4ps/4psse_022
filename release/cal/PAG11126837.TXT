OBJECT Page 11126837 Service Order Status Text
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Statustext Serviceauftrag;
               ENU=Service Order Status Text;
               NLD=Serviceorder status tekst];
    SourceTable=Table11072550;
    DelayedInsert=Yes;
    PageType=List;
    AutoSplitKey=Yes;
    ActionList=ACTIONS
    {
      { 1100525009;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100525008;1 ;Action    ;
                      CaptionML=[DEU=&Textverarbeiter;
                                 ENU=&Text Editor;
                                 NLD=&Tekstverwerker;
                                 NOR=&Tekstredigering;
                                 SVE=&Texteditor];
                      Promoted=Yes;
                      Image=SNInfo;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 TextEditor@1210190001 : Codeunit 11012001;
                               BEGIN
                                 IF CurrPage.EDITABLE THEN
                                   TextEditor.ServiceOrderStatusText(Rec);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100525000;0;Container;
                ContainerType=ContentArea }

    { 1100525001;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1100525002;2;Field  ;
                SourceExpr=Date }

    { 1100525003;2;Field  ;
                SourceExpr="Standard Text Code";
                OnValidate=BEGIN
                             CodeOnAfterValidate;
                           END;

                OnLookup=VAR
                           StandardText@1100525000 : Record 7;
                         BEGIN
                           StandardText.Code := "Standard Text Code";
                           IF PAGE.RUNMODAL(PAGE::"Standard Text Codes", StandardText) = ACTION::LookupOK THEN BEGIN
                             IF  (StandardText.Code <> "Standard Text Code") AND (StandardText.Code <> '') THEN BEGIN
                               "Standard Text Code" := StandardText.Code;
                               GetStandardText;
                             END;
                           END;
                         END;
                          }

    { 1100525004;2;Field  ;
                SourceExpr=Comment }

    { 1100525005;2;Field  ;
                SourceExpr="Maintenance Status Code" }

    { 1100525006;2;Field  ;
                SourceExpr="Created by" }

    { 1100525007;2;Field  ;
                SourceExpr="Time Created" }

  }
  CODE
  {
    VAR
      Text001@1100525000 : TextConst 'DEU="Nicht genÅgend Raum zum ZwischenfÅgen von Textzeilen; nicht alle Textzeilen wurden hinzugefÅgt.";ENU=Not enough space to insert text lines, not all text lines are added.;NLD=Niet voldoende ruimte om tekstregels tussen te voegen, niet alle tekstregels zijn toegevoegd.;NOR=Det finnes ikke tilstrekkelig med plass for Ü sette inn tekstrader. ikke alle tekstrader legges til.;SVE=Det finns inte tillrÑckligt med utrymme fîr att infoga textrader. Inte alla textrader lÑggs till.';

    PROCEDURE GetStandardText@1210190000();
    VAR
      StandardText@1210190003 : Record 7;
      ExtendedTextLine@1210190002 : Record 280;
      ServiceOrderStatusText@1210190001 : Record 11072550;
      CurrLino@1210190000 : Integer;
    BEGIN
      IF "Standard Text Code" = '' THEN
        EXIT;

      CurrPage.SAVERECORD;
      CurrLino := "Line No.";
      IF StandardText.GET("Standard Text Code") THEN BEGIN
        IF (StandardText.Description <> '') THEN BEGIN
          Comment := COPYSTR(StandardText.Description, 1, MAXSTRLEN(Comment));
          MODIFY;
        END;
        ExtendedTextLine.SETRANGE("Table Name", ExtendedTextLine."Table Name"::"Standard Text");
        ExtendedTextLine.SETRANGE("No.", "Standard Text Code");
        IF ExtendedTextLine.FIND('-') THEN BEGIN
          REPEAT
            ServiceOrderStatusText := Rec;
            ServiceOrderStatusText.Comment := COPYSTR(ExtendedTextLine.Text, 1, MAXSTRLEN(ServiceOrderStatusText.Comment));
            ServiceOrderStatusText."Line No." := CurrLino + 1;
            ServiceOrderStatusText."Standard Text Code" := '';

            IF NOT ServiceOrderStatusText.INSERT THEN BEGIN
              MESSAGE(Text001);
              EXIT;
            END;
            CurrLino := ServiceOrderStatusText."Line No.";
          UNTIL ExtendedTextLine.NEXT = 0;
        END;
      END;
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE CodeOnAfterValidate@19050293();
    BEGIN
      IF "Standard Text Code" <> xRec."Standard Text Code" THEN
        GetStandardText;
    END;

    BEGIN
    END.
  }
}

