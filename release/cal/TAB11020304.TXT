OBJECT Table 11020304 Print PDF Document
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Print PDF Document;
               NOR=Skriv ut PDF-dokument;
               SVE=Skriv ut PDF-dokument];
  }
  FIELDS
  {
    { 1   ;   ;Document Code       ;Code3         ;CaptionML=[ENU=Document Code;
                                                              NOR=Dokumentkode;
                                                              SVE=Dokumentkod] }
    { 2   ;   ;Document No         ;Code20        ;CaptionML=[ENU=Document No;
                                                              NOR=Verifiseringsnr.;
                                                              SVE=Dokumentnr.] }
    { 10  ;   ;Printed             ;Boolean       ;CaptionML=[ENU=Printed;
                                                              NOR=Skrevet ut;
                                                              SVE=Utskrivet] }
  }
  KEYS
  {
    {    ;Document Code,Document No               ;Clustered=Yes }
    {    ;Printed                                  }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PrintDoc@1107100000 : Record 11020304;
      PostedInvHeader@1107100001 : Record 112;
      PostedCMHeader@1100485000 : Record 114;
      DocsPrinted@1107100002 : Boolean;

    PROCEDURE PrintDocuments@2();
    BEGIN
      SELECTLATESTVERSION;
      DocsPrinted := FALSE;
      PrintDoc.RESET;
      PrintDoc.SETCURRENTKEY(Printed);
      PrintDoc.SETRANGE(Printed,FALSE);
      IF PrintDoc.FIND('-') THEN
        REPEAT
          CASE PrintDoc."Document Code" OF
            'INV':
              BEGIN
                IF PostedInvHeader.GET(PrintDoc."Document No") THEN
                  BEGIN
                    PostedInvHeader.PDFPrint;
                    PrintDoc.Printed := TRUE;
                    PrintDoc.MODIFY;
                    DocsPrinted := TRUE
                  END;
              END;
            'SCM':
              BEGIN
                IF PostedCMHeader.GET(PrintDoc."Document No") THEN
                  BEGIN
                    PostedCMHeader.PDFPrint;
                    PrintDoc.Printed := TRUE;
                    PrintDoc.MODIFY;
                    DocsPrinted := TRUE
                  END;
              END;
          END; //CASE
        UNTIL PrintDoc.NEXT=0;

      IF DocsPrinted THEN
        BEGIN
          PostedInvHeader.RESET;
          PostedCMHeader.RESET;

          PrintDoc.RESET;
          PrintDoc.SETCURRENTKEY(Printed);
          PrintDoc.SETRANGE(Printed,TRUE);
          PrintDoc.DELETEALL;
        END;
    END;

    PROCEDURE PDFCreateEmbeddedCode@1(DocumentNumber@1107100000 : Code[20];Attachement@1100485000 : Boolean) EmbCode : Text[250];
    VAR
      SalesSetup@1107100001 : Record 311;
    BEGIN
      IF DocumentNumber <> '' THEN
        BEGIN
          SalesSetup.GET;
          EmbCode := '~0% '; //Startcode
          EmbCode := EmbCode + STRSUBSTNO('OutputDir "%1" ',SalesSetup."PDF Export Path");
          EmbCode := EmbCode + STRSUBSTNO('{ Outputfilename "%1" ',DocumentNumber);
          IF Attachement THEN
            EmbCode := EmbCode + '{ Append "1" '
          ELSE
            EmbCode := EmbCode + '{ Overwrite "1" ';
          EmbCode := EmbCode + '{ Prompt "0" ';
          EmbCode := EmbCode + '}?';
        END ELSE
          EmbCode := '';
    END;

    BEGIN
    END.
  }
}

