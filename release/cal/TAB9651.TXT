OBJECT Table 9651 Report Layout Selection
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnInsert=BEGIN
               TESTFIELD("Report ID");
               IF "Company Name" = '' THEN
                 "Company Name" := COMPANYNAME;
             END;

    CaptionML=[ENU=Report Layout Selection;
               NOR=Rapportoppsettsvalg;
               SVE=Val av rapportlayout];
  }
  FIELDS
  {
    { 1   ;   ;Report ID           ;Integer       ;CaptionML=[ENU=Report ID;
                                                              NOR=Rapport-ID;
                                                              SVE=Rapport-ID] }
    { 2   ;   ;Report Name         ;Text80        ;CaptionML=[ENU=Report Name;
                                                              NOR=Rapportnavn;
                                                              SVE=Rapportnamn];
                                                   Editable=No }
    { 3   ;   ;Company Name        ;Text30        ;TableRelation=Company;
                                                   CaptionML=[ENU=Company Name;
                                                              NOR=Selskapsnavn;
                                                              SVE=F”retagsnamn] }
    { 4   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                TESTFIELD("Report ID");
                                                                CALCFIELDS("Report Caption");
                                                                "Report Name" := "Report Caption";
                                                                CASE Type OF
                                                                  Type::"RDLC (built-in)":
                                                                    BEGIN
                                                                      IF NOT HasRdlcLayout("Report ID") THEN
                                                                        ERROR(NoRdlcLayoutErr,"Report Name");
                                                                      "Custom Report Layout Code" := '';
                                                                    END;
                                                                  Type::"Word (built-in)":
                                                                    BEGIN
                                                                      IF NOT HasWordLayout("Report ID") THEN
                                                                        ERROR(NoWordLayoutErr,"Report Name");
                                                                      "Custom Report Layout Code" := '';
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              NOR=Type;
                                                              SVE=Typ];
                                                   OptionCaptionML=[ENU=RDLC (built-in),Word (built-in),Custom Layout;
                                                                    NOR=RDLC (innebygd),Word (innebygd),Egendefinert oppsett];
                                                   OptionString=RDLC (built-in),Word (built-in),Custom Layout }
    { 6   ;   ;Custom Report Layout Code;Code20   ;TableRelation="Custom Report Layout" WHERE (Report ID=FIELD(Report ID));
                                                   OnValidate=BEGIN
                                                                IF "Custom Report Layout Code" = '' THEN
                                                                  Type := GetDefaultType("Report ID")
                                                                ELSE
                                                                  Type := Type::"Custom Layout";
                                                              END;

                                                   CaptionML=[ENU=Custom Report Layout Code;
                                                              NOR=Kode for egendefinert rapportoppsett;
                                                              SVE=Anpassad rapportlayoutkod] }
    { 7   ;   ;Report Layout Description;Text250  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Custom Report Layout".Description WHERE (Code=FIELD(Custom Report Layout Code)));
                                                   CaptionML=[ENU=Report Layout Description;
                                                              NOR=Beskrivelse av rapportoppsett] }
    { 8   ;   ;Report Caption      ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Report),
                                                                                                                Object ID=FIELD(Report ID)));
                                                   CaptionML=[ENU=Report Caption;
                                                              NOR=Rapportoverskrift;
                                                              SVE=Rapportrubrik];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Report ID,Company Name                  ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      NoRdlcLayoutErr@1000 : TextConst '@@@="%1=a report name";ENU=Report ''%1'' has no built-in RDLC layout.;NOR=Rapporten %1 har ikke et innebygd RDLC-oppsett.';
      NoWordLayoutErr@1001 : TextConst '@@@="%1=a report name";ENU=Report ''%1'' has no built-in Word layout.;NOR=Rapporten %1 har ikke et innebygd Word-oppsett.';

    [External]
    PROCEDURE GetDefaultType@2(ReportID@1000 : Integer) : Integer;
    BEGIN
      IF REPORT.DEFAULTLAYOUT(ReportID) = DEFAULTLAYOUT::Word THEN
        EXIT(Type::"Word (built-in)");
      EXIT(Type::"RDLC (built-in)");
    END;

    [External]
    PROCEDURE IsProcessingOnly@20(ReportID@1000 : Integer) : Boolean;
    BEGIN
      EXIT(REPORT.DEFAULTLAYOUT(ReportID) = DEFAULTLAYOUT::None);
    END;

    LOCAL PROCEDURE HasRdlcLayout@21(ReportID@1001 : Integer) : Boolean;
    VAR
      InStr@1000 : InStream;
    BEGIN
      IF REPORT.RDLCLAYOUT(ReportID,InStr) THEN
        EXIT(NOT InStr.EOS);
      EXIT(FALSE);
    END;

    [External]
    PROCEDURE HasWordLayout@23(ReportID@1001 : Integer) : Boolean;
    VAR
      InStr@1000 : InStream;
    BEGIN
      IF REPORT.WORDLAYOUT(ReportID,InStr) THEN
        EXIT(NOT InStr.EOS);
      EXIT(FALSE);
    END;

    [External]
    PROCEDURE HasCustomLayout@1(ReportID@1000 : Integer) : Integer;
    VAR
      CustomReportLayout@1001 : Record 9650;
    BEGIN
      // Temporarily selected layout for Design-time report execution?
      IF GetTempLayoutSelected <> '' THEN
        IF CustomReportLayout.GET(GetTempLayoutSelected) THEN BEGIN
          IF CustomReportLayout.Type = CustomReportLayout.Type::RDLC THEN
            EXIT(1);
          EXIT(2);
        END;

      // Normal selection
      IF NOT GET(ReportID,COMPANYNAME) THEN
        EXIT(0);
      CASE Type OF
        Type::"RDLC (built-in)":
          BEGIN
            IF REPORT.DEFAULTLAYOUT(ReportID) = DEFAULTLAYOUT::RDLC THEN
              EXIT(0);
            EXIT(1);
          END;
        Type::"Word (built-in)":
          EXIT(2);
        Type::"Custom Layout":
          BEGIN
            IF NOT CustomReportLayout.GET("Custom Report Layout Code") THEN
              EXIT(0);
            IF CustomReportLayout.Type = CustomReportLayout.Type::RDLC THEN
              EXIT(1);
            EXIT(2);
          END;
      END;
    END;

    [External]
    PROCEDURE GetTempLayoutSelected@3() : Code[20];
    VAR
      DesignTimeReportSelection@1000 : Codeunit 9654;
    BEGIN
      EXIT(DesignTimeReportSelection.GetSelectedCustomLayout);
    END;

    [External]
    PROCEDURE SetTempLayoutSelected@4(NewTempSelectedLayoutCode@1001 : Code[20]);
    VAR
      DesignTimeReportSelection@1000 : Codeunit 9654;
    BEGIN
      DesignTimeReportSelection.SetSelectedCustomLayout(NewTempSelectedLayoutCode);
    END;

    BEGIN
    END.
  }
}

