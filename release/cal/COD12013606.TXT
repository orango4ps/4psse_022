OBJECT Codeunit 12013606 Update Quote
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF410000;
  }
  PROPERTIES
  {
    TableNo=12013608;
    OnRun=VAR
            DocHead@1100285016 : Record 12013608;
            PurchHeader@1100285015 : Record 38;
            PurchLine@1100285014 : Record 39;
            PurchLine2@1100285013 : Record 39;
            DocLine@1100285012 : Record 12013609;
            DocLineApp@1100285010 : Record 12013610;
            DocLineDim@1100285008 : Record 12013611;
            Account@1100285006 : Record 15;
            AppSetup@1100285005 : Record 12013601;
            Vend@1100285004 : Record 23;
            UpdatePurchLine@1100285000 : Codeunit 12013609;
            ReqToQuote@1100285003 : Codeunit 12013614;
            ExFlow@1100285017 : Codeunit 12013601;
            NewLineNo@1100285001 : Integer;
            TempStrLen@1100285002 : Integer;
            TempDate@1100285007 : Date;
            TempDecimal@1100285009 : Decimal;
            TempBoolean@1100285011 : Boolean;
          BEGIN
            DocHead := Rec;
            AppSetup.GET;

            IF CheckQuote(DocHead) THEN BEGIN
              UpdatePurchLine.UpdatePurchHeader(DocHead);

              PurchHeader.GET(DocHead."Document Type",DocHead."Document No.");

              PurchLine2.RESET;
              PurchLine2.LOCKTABLE;
              PurchLine2.SETRANGE("Document Type", DocHead."Document Type");
              PurchLine2.SETRANGE("Document No.", DocHead."Document No.");
              IF PurchLine2.FINDLAST THEN
                NewLineNo := PurchLine2."Line No." + 10000
              ELSE
                NewLineNo := 10000;

              // Renumber DocLineApp, DocLineDim and DocLine for New Lines
              DocLine.RESET;
              DocLine.SETCURRENTKEY("Document ID");
              DocLine.SETRANGE("Document ID", DocHead.ID);
              DocLine.SETRANGE("Connected Line ID", 0);
              DocLine.SETRANGE("New Line", TRUE);
              IF DocLine.FINDSET(TRUE) THEN
                REPEAT
                  DocLineApp.RESET;
                  DocLineApp.SETCURRENTKEY("Document Line ID");
                  DocLineApp.SETRANGE("Document Line ID", DocLine.ID);
                  IF DocLineApp.FINDSET(TRUE) THEN
                    REPEAT
                      DocLineApp."Line No." := NewLineNo;
                      DocLineApp.MODIFY;
                    UNTIL DocLineApp.NEXT = 0;

                  DocLineDim.RESET;
                  DocLineDim.SETCURRENTKEY("Document Line ID");
                  DocLineDim.SETRANGE("Document Line ID", DocLine.ID);
                  IF DocLineDim.FINDSET(TRUE) THEN
                    REPEAT
                      DocLineDim."Line No." := NewLineNo;
                      DocLineDim.MODIFY;
                    UNTIL DocLineDim.NEXT = 0;

                  DocLine."Line No." := NewLineNo;
                  DocLine.MODIFY;

                  NewLineNo := NewLineNo + 10000;
                UNTIL DocLine.NEXT = 0;

              // Update existing and create New PurchLines
              DocLine.RESET;
              DocLine.SETCURRENTKEY("Document ID");
              DocLine.SETRANGE("Document ID", DocHead.ID);
              DocLine.SETRANGE("Connected Line ID", 0);
              IF DocLine.FINDSET(TRUE) THEN
                REPEAT
                  IF DocLine."New Line" THEN BEGIN
                    PurchLine.INIT;
                    PurchLine.SuspendStatusCheck(TRUE);
                    PurchLine."Document Type" := PurchHeader."Document Type";
                    PurchLine."Document No." := PurchHeader."No.";
                    PurchLine."Line No." := DocLine."Line No.";
                    PurchLine."Buy-from Vendor No." := PurchHeader."Buy-from Vendor No.";
                    PurchLine.VALIDATE(Type, DocLine."Line Type");
                    PurchLine.Approve := TRUE;
                    PurchLine."Order Date" := DocHead."Create Date";
                    PurchLine."Expected Receipt Date" := DocHead."Expected Receipt Date";
                    PurchLine.INSERT;

                    DocLine.VALIDATE(Quantity, DocLine.Quantity);
                    DocLine.VALIDATE("Direct Unit Cost", DocLine."Direct Unit Cost");
                  END
                  ELSE BEGIN
                    PurchLine.GET(DocLine."Document Type", DocLine."Document No.", DocLine."Line No.");
                    PurchLine.SuspendStatusCheck(TRUE);
                  END;

                  IF PurchLine."Quantity Received" = 0 THEN BEGIN
                    IF PurchLine."No." <> DocLine."No." THEN
                      PurchLine.VALIDATE("No.", DocLine."No.");

                    IF (PurchLine."No." <> '') AND (PurchLine.Type = PurchLine.Type::"G/L Account") THEN
                      BEGIN
                        IF Account.GET(PurchLine."No.") THEN
                          BEGIN
                            IF PurchLine."VAT Prod. Posting Group" = '' THEN
                              PurchLine."VAT Prod. Posting Group" := Account."VAT Prod. Posting Group";

                            IF PurchLine."Gen. Prod. Posting Group" = '' THEN
                             PurchLine."Gen. Prod. Posting Group" := Account."Gen. Prod. Posting Group";
                          END;
                      END;

                    IF PurchLine.Description <> DocLine.Description THEN
                      IF DocLine.Description <> '' THEN BEGIN
                        TempStrLen := MAXSTRLEN(PurchLine.Description);

                        IF STRLEN(DocLine.Description) > TempStrLen THEN BEGIN
                          IF FORMAT(DocLine.Description[TempStrLen]) <> ' ' THEN
                            REPEAT
                              TempStrLen := TempStrLen - 1;
                            UNTIL (FORMAT(DocLine.Description[TempStrLen]) = ' ') OR (TempStrLen = 1);

                          IF TempStrLen = 1 THEN
                            TempStrLen := MAXSTRLEN(PurchLine.Description);

                          PurchLine.Description := COPYSTR(DocLine.Description, 1, TempStrLen);
                          PurchLine."Description 2" := COPYSTR(DocLine.Description,TempStrLen + 1,
                                                                                   MAXSTRLEN(PurchLine.Description));
                        END
                        ELSE
                          PurchLine.Description := DocLine.Description;
                      END;

                    IF PurchLine.Quantity <> DocLine.Quantity THEN
                      PurchLine.VALIDATE(Quantity, DocLine.Quantity);

                    IF PurchLine."Direct Unit Cost" <> DocLine."Direct Unit Cost" THEN
                      PurchLine.VALIDATE("Direct Unit Cost", DocLine."Direct Unit Cost");

                    IF PurchLine."Line Discount %" <> DocLine."Line Discount %" THEN
                      PurchLine.VALIDATE("Line Discount %", DocLine."Line Discount %");

                    // Separate VAT line is never used for Quote/Order documents

                    IF PurchLine."Gen. Bus. Posting Group" = '' THEN
                      IF Vend.GET(PurchHeader."Buy-from Vendor No.") THEN
                        BEGIN
                          PurchLine."Gen. Bus. Posting Group" := Vend."Gen. Bus. Posting Group";
                          PurchLine."VAT Bus. Posting Group" := Vend."VAT Bus. Posting Group";
                        END;

                    ExDimMgt.UpdateDocLineDimQuote(PurchLine,PurchHeader,AppSetup);

                    DocLineDim.RESET;
                    DocLineDim.SETCURRENTKEY("Document Line ID");
                    DocLineDim.SETRANGE("Document Line ID", DocLine.ID);
                    IF DocLineDim.FINDSET THEN
                      REPEAT
                        CASE DocLineDim."Dimension Code" OF
                          'JOB': PurchLine.VALIDATE("Job No.", DocLineDim."Dimension Value");
                          'JOBTASK': PurchLine.VALIDATE("Job Task No.", DocLineDim."Dimension Value");
                          'CUSTOMERINV':
                            IF EVALUATE(TempBoolean, DocLineDim."Dimension Value") THEN
                              IF TempBoolean <> PurchLine."Invoice to Customer" THEN
                                PurchLine.VALIDATE("Invoice to Customer", TempBoolean);
                          'AMOUNTINV':
                            BEGIN
                              IF EVALUATE(TempDecimal, DocLineDim."Dimension Value") THEN
                                IF TempDecimal <> PurchLine."Invoicing Amount" THEN
                                  PurchLine.VALIDATE("Invoicing Amount", TempDecimal);
                            END;
                          'INVDATE':
                            BEGIN
                              TempDate := ReqToQuote.DateField(DocLineDim."Dimension Value");
                              IF TempDate <> PurchLine."Start Date for Invoicing" THEN
                                PurchLine.VALIDATE("Start Date for Invoicing", TempDate);
                            END;
                          'INV_CODE':
                            BEGIN
                              IF DocLineDim."Dimension Value" <> PurchLine."Invoicing Code" THEN
                                PurchLine.VALIDATE("Invoicing Code", DocLineDim."Dimension Value");
                            END;
                        END;
                      UNTIL DocLineDim.NEXT = 0;

                    PurchLine.Difference := PurchLine.Difference::Netto;
                    PurchLine.MODIFY(FALSE);

                    DocLine."New Line" := FALSE;
                    DocLine.MODIFY(FALSE);
                  END;
                UNTIL DocLine.NEXT = 0;

              PurchLine.RESET;
              PurchLine.SETRANGE("Document Type", DocHead."Document Type");
              PurchLine.SETRANGE("Document No.", DocHead."Document No.");
              IF PurchLine.FINDFIRST THEN
                ExFlow.AmountToDocHead(PurchLine,FALSE,PurchHeader);
            END;
          END;

  }
  CODE
  {
    VAR
      EXF10@1100285005 : TextConst 'ENU=Direct posting is not allowed for Account %1!;SVE=Konto %1 f†r inte direktbokf”ras!';
      EXF11@1100285004 : TextConst 'ENU=Account %1 is blocked!;SVE=Konto %1 „r stoppat!';
      EXF36@1100285003 : TextConst 'ENU=General Posting setup %1 , %2 missing!;SVE="Bokf”ringsinst„llning %1 , %2 saknas! "';
      EXF38@1100285001 : TextConst 'ENU=Product Posting Group does not exist!;SVE="Produktbokf”ringsmall %1 finns inte!  "';
      EXF39@1100285000 : TextConst 'ENU=VAT Product Posting Group %1 does not exist.;SVE=Moms produktbokf”ringsmall %1 finns inte!';
      EXF100@1100285006 : TextConst 'ENU=Updating Purchase Order #1##################;SVE=Uppdaterar ink”psorder #1##############';
      EXF101@1100285026 : TextConst 'ENU=Valid G/L Account must be supplied for Quote %1 Line %2.;SVE=Ogiltigt konto angivet p† offert %1 och rad %2.';
      ExDimMgt@1100285007 : Codeunit 12013605;

    PROCEDURE CheckQuote@1100285003(VAR DocHead@1100285000 : Record 12013608) : Boolean;
    VAR
      PurchHeader@1100285009 : Record 38;
      PurchLine@1100285001 : Record 39;
      Account@1100285007 : Record 15;
      ErrText@1100285006 : Text[1024];
      VATPostingSetup@1100285005 : Record 325;
      PostingSetup@1100285004 : Record 252;
      GenProductPostingGroup@1100285003 : Record 251;
      VATProductPostingGroup@1100285002 : Record 324;
      AppSetup@1100285008 : Record 12013601;
    BEGIN
      AppSetup.GET;
      DocHead."Error Message" := '';
      DocHead.MODIFY(TRUE);

      PurchLine.RESET;
      PurchLine.SETRANGE("Document Type", DocHead."Document Type");
      PurchLine.SETRANGE("Document No.", DocHead."Document No.");
      PurchLine.SETRANGE(Type, PurchLine.Type::"G/L Account");
      PurchLine.SETFILTER("No.", '<>%1','');
      IF PurchLine.FINDSET THEN
        REPEAT
          IF NOT Account.GET(PurchLine."No.") THEN
            BEGIN
              ErrText := STRSUBSTNO(EXF101, PurchLine."Document No.", PurchLine."No.");
              ErrorText(DocHead, ErrText);
            END
            ELSE BEGIN
              IF NOT Account."Direct Posting" THEN
                BEGIN
                  ErrText := STRSUBSTNO(EXF10, PurchLine."No.");
                  ErrorText(DocHead, ErrText);
                END
              ELSE
                IF Account.Blocked THEN
                  BEGIN
                    ErrText := STRSUBSTNO(EXF11, PurchLine."No.");
                    ErrorText(DocHead, ErrText);
                  END;

              // Check "Gen. Prod. Posting Group" and "VAT Prod. Posting Group"
              IF NOT GenProductPostingGroup.GET(PurchLine."Gen. Prod. Posting Group") THEN
                BEGIN
                  ErrText := STRSUBSTNO(EXF38, PurchLine."Gen. Prod. Posting Group");
                  ErrorText(DocHead, ErrText);
                END;

              //342555
              IF PurchLine."VAT Prod. Posting Group" <> '' THEN
              //342555
                IF NOT VATProductPostingGroup.GET(PurchLine."VAT Prod. Posting Group") THEN
                  BEGIN
                    ErrText := STRSUBSTNO(EXF39, PurchLine."VAT Prod. Posting Group");
                    ErrorText(DocHead, ErrText);
                  END;

              // Check Posting Setup
              IF AppSetup."Check Posting Setup" THEN
                BEGIN
                  IF NOT PostingSetup.GET(PurchLine."Gen. Bus. Posting Group",PurchLine."Gen. Prod. Posting Group") THEN
                    BEGIN
                      ErrText := STRSUBSTNO(EXF36, PurchLine."Gen. Bus. Posting Group",PurchLine."Gen. Prod. Posting Group");
                      ErrorText(DocHead, ErrText);
                    END;

                  IF NOT VATPostingSetup.GET(PurchLine."VAT Bus. Posting Group",PurchLine."VAT Prod. Posting Group") THEN
                    BEGIN
                      ErrText := STRSUBSTNO(EXF36, PurchLine."VAT Bus. Posting Group",PurchLine."VAT Prod. Posting Group");
                      ErrorText(DocHead, ErrText);
                    END;
                END;
            END;
        UNTIL PurchLine.NEXT = 0;

      PurchHeader.GET(DocHead."Document Type",DocHead."Document No.");

      IF AppSetup."Check Dimensions" THEN
        BEGIN
          ExDimMgt.CopyAndCheckDocDimToTempDocDim(PurchHeader, ErrText);
          IF ErrText <> '' THEN
            ErrorText(DocHead, ErrText);
        END;

      IF DocHead."Error Message" = '' THEN
        EXIT(TRUE)
      ELSE
        BEGIN
          DocHead.MODIFY(TRUE);
          EXIT(FALSE);
        END;
    END;

    PROCEDURE ErrorText@1100285014(VAR DocHead@1100285000 : Record 12013608;NewError@1100285001 : Text[1024]);
    BEGIN
      IF STRLEN(DocHead."Error Message") + STRLEN(NewError) > 249 THEN
        EXIT;

      DocHead."Error Message" := DocHead."Error Message" + ' ' + NewError;
    END;

    PROCEDURE CheckQuoteBatch@1100285001(VAR ExDoc@1100285000 : Record 12013608);
    VAR
      ExDoc2@1100285001 : Record 12013608;
      PurchLine@1100285003 : Record 39;
      PurchHeader@1100285005 : Record 38;
      ExFlow@1100285004 : Codeunit 12013601;
      Window@1100285002 : Dialog;
    BEGIN
      IF GUIALLOWED THEN
        Window.OPEN(EXF100);

      // Filters are set where this function is being called
      IF ExDoc.FINDSET(TRUE,FALSE) THEN
        REPEAT
          IF GUIALLOWED THEN
            Window.UPDATE(1,ExDoc."Document No.");

          ExDoc2.GET(ExDoc.ID);
          CheckQuote(ExDoc2);
          PurchLine.RESET;
          PurchLine.SETRANGE("Document Type", ExDoc."Document Type");
          PurchLine.SETRANGE("Document No.", ExDoc."Document No.");
          PurchLine.SETFILTER("No.", '<>%1','');
          IF PurchLine.FINDFIRST THEN BEGIN
            PurchHeader.GET(PurchLine."Document Type",PurchLine."Document No.");
            ExFlow.AmountToDocHead(PurchLine,FALSE,PurchHeader);
          END;
        UNTIL ExDoc.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

