OBJECT Page 11012030 Project Image
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Projektbild;
               ENU=Project Image;
               NLD=Projectafbeelding;
               NOR=Prosjektbilde;
               SVE=Projektbild];
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table11072003;
    PageType=CardPart;
    OnOpenPage=BEGIN
                 //**4PS Generated Code. Do Not Change.sn01
                 DPA_IsHardFilter := DPA_SetFilters(CurrPage.OBJECTID(FALSE));
                 //**4PS Generated Code.en01
               END;

    OnNewRecord=BEGIN
                  //**4PS Generated Code. Do Not Change.sn02
                  DPA_SetDefaultDepartment;
                  //**4PS Generated Code.en02
                END;

    OnInsertRecord=VAR
                     xRecRef@1100000999 : RecordRef;
                   BEGIN
                     //**4PS Generated Code. Do Not Change.sn05
                     xRecRef.GETTABLE(xRec);
                     DPA_SetDefaultDpmtFromNoSeries(xRecRef);
                     //**4PS Generated Code.en05
                   END;

    OnAfterGetCurrRecord=BEGIN
                           SetEditableOnImageActions;
                         END;

    ActionList=ACTIONS
    {
      { 1100527400;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100527403;1 ;Action    ;
                      Name=ImportPicture;
                      CaptionML=[DEU=Importieren;
                                 ENU=Import;
                                 NLD=Importeren];
                      ToolTipML=[DEU=Importiert eine Bilddatei.;
                                 ENU=Import a picture file.;
                                 NLD=Een afbeeldingsbestand importeren.];
                      ApplicationArea=#All;
                      Image=Import;
                      OnAction=VAR
                                 FileManagement@1001 : Codeunit 419;
                                 FileName@1000 : Text;
                                 ClientFileName@1002 : Text;
                               BEGIN
                                 IF Image.HASVALUE THEN
                                   IF NOT CONFIRM(OverrideImageQst) THEN
                                     EXIT;

                                 FileName := FileManagement.UploadFile(SelectPictureTxt,ClientFileName);
                                 IF FileName = '' THEN
                                   EXIT;

                                 CLEAR(Image);
                                 Image.IMPORTFILE(FileName,ClientFileName);
                                 MODIFY(TRUE);
                                 IF FileManagement.DeleteServerFile(FileName) THEN;
                               END;
                                }
      { 1100527402;1 ;Action    ;
                      Name=ExportFile;
                      CaptionML=[DEU=Exportieren;
                                 ENU=Export;
                                 NLD=Exporteren];
                      ToolTipML=[DEU=Exportiert das Bild in eine Datei.;
                                 ENU=Export the picture to a file.;
                                 NLD=De afbeelding exporteren naar een bestand.];
                      ApplicationArea=#All;
                      Enabled=DeleteExportEnabled;
                      Image=Export;
                      OnAction=VAR
                                 NameValueBuffer@1002 : Record 823;
                                 TempNameValueBuffer@1004 : TEMPORARY Record 823;
                                 FileManagement@1001 : Codeunit 419;
                                 ToFile@1003 : Text;
                                 ExportPath@1005 : Text;
                               BEGIN
                                 NameValueBuffer.DELETEALL;
                                 ExportPath := TEMPORARYPATH + "No." + FORMAT(Image.MEDIAID);
                                 Image.EXPORTFILE(ExportPath);
                                 FileManagement.GetServerDirectoryFilesList(TempNameValueBuffer,TEMPORARYPATH);
                                 TempNameValueBuffer.SETFILTER(Name,STRSUBSTNO('%1*',ExportPath));
                                 TempNameValueBuffer.FINDFIRST;
                                 ToFile := STRSUBSTNO('%1 %2.jpg',"No.",Description);
                                 DOWNLOAD(TempNameValueBuffer.Name,DownloadImageTxt,'','',ToFile);
                                 IF FileManagement.DeleteServerFile(TempNameValueBuffer.Name) THEN;
                               END;
                                }
      { 1100527401;1 ;Action    ;
                      Name=DeletePicture;
                      CaptionML=[DEU=L”schen;
                                 ENU=Delete;
                                 NLD=Verwijderen];
                      ToolTipML=[DEU=L”scht den Datensatz.;
                                 ENU=Delete the record.;
                                 NLD=De record verwijderen.];
                      ApplicationArea=#All;
                      Enabled=DeleteExportEnabled;
                      Image=Delete;
                      OnAction=BEGIN
                                 IF NOT CONFIRM(DeleteImageQst) THEN
                                   EXIT;

                                 CLEAR(Image);
                                 MODIFY(TRUE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Field     ;
                ToolTipML=[DEU=Hiermit wird die Abbildung des Projekts angegeben, z.B. ein Logo.;
                           ENU=Specifies the picture of the project, for example, a logo.;
                           NLD=Hiermee wordt de afbeelding van het project opgegeven, bijvoorbeeld een logo.];
                ApplicationArea=#All;
                SourceExpr=Image;
                ShowCaption=No }

  }
  CODE
  {
    VAR
      OverrideImageQst@1100527403 : TextConst 'DEU=Das vorhandene Bild wird ersetzt. M”chten Sie fortfahren?;ENU=The existing picture will be replaced. Do you want to continue?;NLD=De bestaande afbeelding wordt vervangen. Wilt u doorgaan?';
      DeleteImageQst@1100527402 : TextConst 'DEU=M”chten Sie das Bild wirklich l”schen?;ENU=Are you sure you want to delete the picture?;NLD=Weet u zeker dat u de afbeelding wilt verwijderen?';
      SelectPictureTxt@1100527401 : TextConst 'DEU=Ein Bild zum Hochladen ausw„hlen;ENU=Select a picture to upload;NLD=Een afbeelding selecteren om te uploaden';
      DownloadImageTxt@1100527400 : TextConst 'DEU=Bild herunterladen;ENU=Download image;NLD=Afbeelding downloaden';
      DeleteExportEnabled@1100527404 : Boolean;
      DPA_IsHardFilter@1100000999 : Boolean;

    LOCAL PROCEDURE SetEditableOnImageActions@1100527405();
    BEGIN
      DeleteExportEnabled := Image.HASVALUE;
    END;

    BEGIN
    END.
  }
}

