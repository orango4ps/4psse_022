OBJECT Report 193 Issue Finance Charge Memos
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PSSE,NAVDK.NO;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Issue Finance Charge Memos;
               NOR=Utsted rentenotaer;
               SVE=Utskick r„ntefakturor];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   GLSetup.GET;
                 END;

  }
  DATASET
  {
    { 8733;    ;DataItem;Check Fin Charge Memo Header;
               DataItemTable=Table302;
               DataItemTableView=SORTING(No.);
               ReqFilterHeadingML=[ENU=Finance Charge Memo;
                                   NOR=Rentenota;
                                   SVE=R„ntefaktura];
               OnAfterGetRecord=BEGIN
                                  //>>NAVNO
                                  IF GLSetup."Norwegian Localization Active" THEN
                                    IF "E-Invoice" THEN
                                      CODEUNIT.RUN(CODEUNIT::"E-Invoice Check Fin. Chrg.Memo","Check Fin Charge Memo Header");
                                  //<<NAVNO
                                END;

               ReqFilterFields=No. }

    { 1080000; ;DataItem;                    ;
               DataItemTable=Table302;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=BEGIN
                               //>>NAVNO
                               IF GLSetup."Norwegian Localization Active" THEN BEGIN
                                 SETVIEW("Check Fin Charge Memo Header".GETVIEW);
                                 COPYFILTERS("Check Fin Charge Memo Header");
                               END;
                               //<<NAVNO

                               IF ReplacePostingDate AND (PostingDateReq = 0D) THEN
                                 ERROR(Text000);
                               NoOfRecords := COUNT;
                               IF NoOfRecords = 1 THEN
                                 Window.OPEN(Text001)
                               ELSE BEGIN
                                 Window.OPEN(Text002);
                                 OldDateTime := CURRENTDATETIME;
                               END;
                             END;

               OnAfterGetRecord=BEGIN
                                  RecordNo := RecordNo + 1;
                                  CLEAR(FinChrgMemoIssue);
                                  FinChrgMemoIssue.Set("Finance Charge Memo Header",ReplacePostingDate,PostingDateReq);
                                  IF NoOfRecords = 1 THEN BEGIN
                                    IF GLSetup."Danish Localization Active" THEN  //NAVDK
                                      OIOXMLCheckFinChargeMemo.RUN("Finance Charge Memo Header");  //NAVDK
                                  //>>NAVNO
                                    IF GLSetup."Norwegian Localization Active" THEN
                                      EInvoiceCheckFinChargeMemo.CheckFinChrgMemo("Finance Charge Memo Header");
                                  //<<NAVNO
                                    FinChrgMemoIssue.RUN;
                                    MARK := FALSE;
                                  END ELSE BEGIN
                                    NewDateTime := CURRENTDATETIME;
                                    IF (NewDateTime - OldDateTime > 100) OR (NewDateTime < OldDateTime) THEN BEGIN
                                      NewProgress := ROUND(RecordNo / NoOfRecords * 100,1);
                                      IF NewProgress <> OldProgress THEN BEGIN
                                        Window.UPDATE(1,NewProgress * 100);
                                        OldProgress := NewProgress;
                                      END;
                                      OldDateTime := CURRENTDATETIME;
                                    END;
                                    MARK := FALSE;
                                    //>>NAVDK
                                    IF GLSetup."Danish Localization Active" THEN
                                      MARK := NOT OIOXMLCheckFinChargeMemo.RUN("Finance Charge Memo Header");
                                    //<<NAVDK
                                    IF NOT "Finance Charge Memo Header".MARK THEN
                                      MARK := NOT FinChrgMemoIssue.RUN;
                                  END;

                                  IF ((PrintDoc <> PrintDoc::" ") OR SentElectronically) AND NOT MARK THEN BEGIN // RFC-035
                                  //IF (PrintDoc <> PrintDoc::" ") AND NOT MARK THEN BEGIN
                                    FinChrgMemoIssue.GetIssuedFinChrgMemo(IssuedFinChrgMemoHeader);
                                    TempIssuedFinChrgMemoHeader := IssuedFinChrgMemoHeader;
                                    TempIssuedFinChrgMemoHeader.INSERT;
                                  END;
                                END;

               OnPostDataItem=VAR
                                ConfirmManagement@1001 : Codeunit 27;
                                IsHandled@1000 : Boolean;
                              BEGIN
                                Window.CLOSE;
                                COMMIT;
                                IF PrintDoc <> PrintDoc::" " THEN
                                  IF TempIssuedFinChrgMemoHeader.FINDSET THEN
                                    REPEAT
                                      IssuedFinChrgMemoHeader := TempIssuedFinChrgMemoHeader;
                                      IssuedFinChrgMemoHeader.SETRECFILTER;
                                      IssuedFinChrgMemoHeader.PrintRecords(FALSE,PrintDoc = PrintDoc::Email,HideDialog);
                                    UNTIL TempIssuedFinChrgMemoHeader.NEXT = 0;

                                //*RFC-035 151104 ITERO.MH <<
                                IF (SentElectronically) THEN BEGIN
                                   //>> 160122 ITERO.AC RFC035 bugfix
                                   //TempIssuedFinChrgMemoHeader.SendFinChargeMemoElectronically;
                                   FinChrgMemoIssue.SendFinChargeMemoElectronically(TempIssuedFinChrgMemoHeader);
                                   //<< 160122 ITERO.AC RFC035 bugfix
                                END;
                                //*RFC-035 151104 ITERO.MH >>

                                MARKEDONLY(TRUE);
                                IF FINDFIRST THEN
                                  IF CONFIRM(Text003,TRUE) THEN
                                    PAGE.RUNMODAL(0,"Finance Charge Memo Header");
                              END;

               ReqFilterFields=No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 3   ;2   ;Field     ;
                  Name=PrintDoc;
                  CaptionML=[ENU=Print;
                             NOR=Skriv ut;
                             SVE=Skriv ut];
                  ToolTipML=[ENU=Specifies if you want the program to print the finance charge memos when they are issued.;
                             NOR=Angir om du vil at programmet skal skrive ut rentenotaene n†r de er utstedt.;
                             SVE=Anger om du vill att programmet ska skriva ut r„ntefakturorna n„r de utf„rdas.];
                  ApplicationArea=#Suite;
                  SourceExpr=PrintDoc }

      { 2   ;2   ;Field     ;
                  CaptionML=[ENU=Replace Posting Date;
                             NOR=Erstatt bokf›ringsdato;
                             SVE=Ers„tt bokf”ringsdatum];
                  ToolTipML=[ENU=Specifies if you want to replace the finance charge memos' posting date with the date entered in the field below.;
                             NOR=Angir om du vil erstatte rentenotaenes bokf›ringsdato med datoen du angir i feltet nedenfor.;
                             SVE=Anger om du vill ers„tta r„ntefakturans bokf”ringsdatum med det bokf”ringsdatum som anges i f„ltet nedan.];
                  ApplicationArea=#Suite;
                  SourceExpr=ReplacePostingDate }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Posting Date;
                             NOR=Bokf›ringsdato;
                             SVE=Bokf”ringsdatum];
                  ToolTipML=[ENU=Specifies the posting date. If you place a check mark in the check box above, the program will use this date on all finance charge memos when you post.;
                             NOR=Angir bokf›ringsdatoen. Hvis du setter en hake i avmerkingsboksen ovenfor, brukes denne datoen p† alle rentenotaene n†r du bokf›rer.;
                             SVE=Anger bokf”ringsdatumet. Om du markerar kryssrutan ovan, anv„nds detta datum f”r alla r„ntefakturor n„r du bokf”r.];
                  ApplicationArea=#Suite;
                  SourceExpr=PostingDateReq }

      { 4   ;2   ;Field     ;
                  Name=HideEmailDialog;
                  CaptionML=[ENU=Hide Email Dialog;
                             NOR=Skjul dialogboksen E-post;
                             SVE=D”lj e-postdialog];
                  ToolTipML=[ENU=Specifies if you want to hide email dialog.;
                             NOR=Angir om du vil skjule dialogboksen E-post.;
                             SVE=Anger om du vill d”lja dialogrutan f”r e-post.];
                  ApplicationArea=#Suite;
                  SourceExpr=HideDialog }

      { 1100285500;2;Field  ;
                  CaptionML=[ENU=Send Electronically;
                             NOR=Send elektronisk;
                             SVE=Skicka elektroniskt];
                  SourceExpr=SentElectronically }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Enter the posting date.;NOR=Angi bokf›ringsdatoen.;SVE=Ange bokf”ringsdatum.';
      Text001@1001 : TextConst 'ENU=Issuing finance charge memo...;NOR=Utsteder rentenota...;SVE=Utskick r„ntefaktura...';
      Text002@1002 : TextConst 'ENU=Issuing finance charge memos @1@@@@@@@@@@@@@;NOR=Utsteder rentenotaer         @1@@@@@@@@@@@@@;SVE=Utskick r„ntefakturor        @1@@@@@@@@@@@@@';
      Text003@1003 : TextConst 'ENU=It was not possible to issue some of the selected finance charge memos.\Do you want to see these finance charge memos?;NOR=Det var ikke mulig † utstede en del av de valgte rentenotaene.\Vil du vise disse rentenotaene?;SVE=Det gick inte att skicka ut n†gra av de valda r„ntefakturorna.\Vill du visa dem?';
      IssuedFinChrgMemoHeader@1005 : Record 304;
      TempIssuedFinChrgMemoHeader@1004 : TEMPORARY Record 304;
      FinChrgMemoIssue@1006 : Codeunit 395;
      EInvoiceCheckFinChargeMemo@1080000 : Codeunit 11128504;
      Window@1007 : Dialog;
      NoOfRecords@1008 : Integer;
      RecordNo@1009 : Integer;
      NewProgress@1010 : Integer;
      OldProgress@1011 : Integer;
      NewDateTime@1012 : DateTime;
      OldDateTime@1013 : DateTime;
      PostingDateReq@1014 : Date;
      ReplacePostingDate@1015 : Boolean;
      PrintDoc@1016 : ' ,Print,Email';
      OIOXMLCheckFinChargeMemo@1101100000 : Codeunit 11128573;
      GLSetup@111285700 : Record 98;
      SentElectronically@1100285500 : Boolean;
      HideDialog@1017 : Boolean;

    [Integration]
    LOCAL PROCEDURE OnBeforePrintRecords@1(VAR IssuedFinChargeMemoHeader@1000 : Record 304;VAR IsHandled@1001 : Boolean);
    BEGIN
    END;

    BEGIN
    {
      151104 RFC035 ITERO.MH Added possibility to send fin charge memo electronically
      160122 RFC035 ITERO.AC Bugfix Moved function SendFinChargeMemoElectronically() from Table 304 to Code Unit 395 due to License error
    }
    END.
  }
  RDLDATA
  {
  }
}

