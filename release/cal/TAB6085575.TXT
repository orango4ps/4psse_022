OBJECT Table 6085575 DC Document Category
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.5.00.07,4PS14.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Code,Description;
    OnInsert=BEGIN
               "Export PDF File" := "Export PDF File"::"Yes - Keep Existing Or Create New";
               "Document Category GUID" := CREATEGUID;
               IF "Drag and Drop Category" THEN
                 "Allow Deleting Documents" := TRUE;
             END;

    OnDelete=VAR
               Document@1160040004 : Record 6085590;
               Template@1000000001 : Record 6085579;
               SourceExcl@1160040002 : Record 6085598;
               IdentField@1160040001 : Record 6085759;
               GroupLang@1000000000 : Record 6085576;
               RecIDMgt@1160040000 : Codeunit 6085604;
               CODC@1160040003 : Codeunit 6085617;
             BEGIN
               Document.SETRANGE("Document Category Code",Code);
               IF NOT Document.ISEMPTY THEN
                 ERROR(Text008,TABLECAPTION,Code);

               Template.SETRANGE("Category Code",Code);
               IF NOT Template.ISEMPTY THEN
                 ERROR(Text007,TABLECAPTION,Code);

               GroupLang.SETRANGE("Document Category Code",Code);
               GroupLang.DELETEALL(TRUE);

               SourceExcl.SETRANGE("Document Category Code",Code);
               SourceExcl.DELETEALL(TRUE);

               IdentField.SETRANGE("Document Category Code",Code);
               IdentField.DELETEALL(TRUE);

               RecIDMgt.DeleteTableFilter("Document Category GUID");

               IF NOT "Drag and Drop Category" AND "Exported to Cloud OCR" THEN
                 CODC.DeleteCategory(Rec);
             END;

    OnRename=BEGIN
               ERROR(Text003,TABLECAPTION);
             END;

    CaptionML=[DEU=Beleg Kategorie;
               ENU=Document Category;
               NLD=Documentcategorie;
               NOR=Dokumentkategori;
               SVE=Dokumentkategori];
    LookupPageID=Page6085582;
    DrillDownPageID=Page6085582;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=[DEU=Code;
                                                              ENU=Code;
                                                              NLD=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text50        ;CaptionML=[DEU=Beschreibung;
                                                              ENU=Description;
                                                              NLD=Omschrijving;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 3   ;   ;Source Table No.    ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   OnValidate=BEGIN
                                                                CheckSourceTable;
                                                                IF "Split on Sep. Fields" AND ("Source Table No." > 0) THEN
                                                                  "Split on Source ID" := TRUE;
                                                              END;

                                                   CaptionML=[DEU=Herkunftstabellennr.;
                                                              ENU=Source Table No.;
                                                              NLD=Brontabelnr.];
                                                   BlankZero=Yes }
    { 4   ;   ;Source Table Name   ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Source Table No.)));
                                                   CaptionML=[DEU=Herkunftstabellenname;
                                                              ENU=Source Table Name;
                                                              NLD=Brontabelnaam];
                                                   Editable=No }
    { 5   ;   ;Source Type         ;Option        ;CaptionML=[DEU=Herkunftsart;
                                                              ENU=Source Type;
                                                              NLD=Bronsoort;
                                                              NOR=Kildetype;
                                                              SVE=Ursprungstyp];
                                                   OptionCaptionML=[DEU=" ,Kreditor,Kontakt,Debitor,Projekt,Artikel,Anlage,Mitarbeiter";
                                                                    ENU=" ,Vendor,Contact,Customer,Job,Item,Fixed Asset,Employee";
                                                                    NLD=" ,Leverancier,Contact,Klant,Taak,Artikel,Vast activum,Werknemer"];
                                                   OptionString=[ ,Vendor,Contact,Customer,Job,Item,Fixed Asset,Employee] }
    { 6   ;   ;Documents to Register;Integer      ;CaptionML=[DEU=Anz. der offenen Belege;
                                                              ENU=Documents to Register;
                                                              NLD=Te registreren documenten];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 10  ;   ;Registered Documents;Integer       ;CaptionML=[DEU=Anz. registrierte Belege;
                                                              ENU=Registered Documents;
                                                              NLD=Geregistreerde documenten;
                                                              SVE=Registrerade dokument];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 11  ;   ;Rejected Documents  ;Integer       ;CaptionML=[DEU=Anzahl abgelehnte Belege;
                                                              ENU=Rejected Documents;
                                                              NLD=Afgewezen documenten];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 12  ;   ;Export PDF File     ;Option        ;InitValue=Yes - Keep Existing Or Create New;
                                                   CaptionML=[DEU=PDF-Datei exportieren;
                                                              ENU=Export PDF File;
                                                              NLD=Exporteerd PDF bestand];
                                                   OptionCaptionML=[DEU=Nein,Ja - Vorhandene behalten oder neue erstellen,Ja - Immer neue erstellen;
                                                                    ENU=No,Yes - Keep Existing Or Create New,Yes - Always Create New;
                                                                    NLD=Nee,Ja - behoud bestaande of creeer nieuw,Ja - Altijd nieuwe creeren];
                                                   OptionString=No,Yes - Keep Existing Or Create New,Yes - Always Create New }
    { 13  ;   ;Process Scanned Files;Boolean      ;InitValue=Yes;
                                                   CaptionML=[DEU=Gescannte Dateien verarbeiten;
                                                              ENU=Process Scanned Files;
                                                              NLD=Verwerk gescande bestanden] }
    { 14  ;   ;Process PDF Files   ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[DEU=PDF-Dateien verarbeiten;
                                                              ENU=Process PDF Files;
                                                              NLD=Verwerk PDF bestanden] }
    { 15  ;   ;PDF Format          ;Option        ;InitValue=PDF/A;
                                                   CaptionML=[DEU=PDF-Format;
                                                              ENU=PDF Format;
                                                              NLD=PDF formaat];
                                                   OptionCaptionML=[DEU=PDF,PDF/A;
                                                                    ENU=PDF,PDF/A;
                                                                    NLD=PDF,PDF/A];
                                                   OptionString=PDF,PDF/A }
    { 16  ;   ;TIFF Image Resolution;Integer      ;InitValue=300;
                                                   CaptionML=[DEU=TIFF-Bildaufl”sung;
                                                              ENU=TIFF Image Resolution;
                                                              NLD=TIFF afbeelding Resolutie];
                                                   MinValue=150 }
    { 18  ;   ;Allow Deleting Documents;Boolean   ;CaptionML=[DEU=Distributionsgruppe;
                                                              ENU=Allow Deleting Documents;
                                                              NLD=Sta verwijderen document toe] }
    { 20  ;   ;E-Mail Connection Endpoint;Code10  ;TableRelation="Connection Endpoint";
                                                   OnValidate=VAR
                                                                ConnEndpoint@161024012 : Record 6085620;
                                                                DocCat@1160040000 : Record 6085575;
                                                              BEGIN
                                                                IF "E-Mail Connection Endpoint" = '' THEN
                                                                  EXIT;

                                                                DocCat.SETRANGE("E-Mail Connection Endpoint","E-Mail Connection Endpoint");
                                                                DocCat.SETFILTER(Code,'<>%1',Code);
                                                                IF NOT DocCat.ISEMPTY THEN
                                                                  ERROR(Text016,"E-Mail Connection Endpoint",FIELDCAPTION("E-Mail Connection Endpoint"),TABLECAPTION);

                                                                ConnEndpoint.GET("E-Mail Connection Endpoint");
                                                                IF ConnEndpoint.Protocol = ConnEndpoint.Protocol::IMAP THEN BEGIN
                                                                  ConnEndpoint.TESTFIELD("Protocol Port");
                                                                  ConnEndpoint.TESTFIELD("Server Address");
                                                                  ConnEndpoint.TESTFIELD(Username);
                                                                  ConnEndpoint.TESTFIELD(Password);
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=E-Mail-Verbindung Endpunkt;
                                                              ENU=E-Mail Connection Endpoint;
                                                              NLD=E-mail verbinding eindpunt] }
    { 21  ;   ;Document with UIC   ;Option        ;OnValidate=BEGIN
                                                                TESTFIELD("Auto Move to Company",TRUE);
                                                              END;

                                                   CaptionML=[DEU=E-Mail-Absender-Filter;
                                                              ENU=Doc. with Unidentified Company;
                                                              NLD=Doc. met onge‹dentificeerd bedrijf];
                                                   OptionCaptionML=[DEU=Importiere in aktives Unternehmen,Importiere als UIC-Beleg;
                                                                    ENU=Import in active Company,Import as UIC document;
                                                                    NLD=Importeer in actief bedrijf,Importeer als UIC-document];
                                                   OptionString=Import in active Company,Import as UIC document }
    { 22  ;   ;No. of Documents with UIC;Integer  ;FieldClass=FlowField;
                                                   CalcFormula=Count("DC Document (UIC)" WHERE (Document Category Code=FIELD(Code),
                                                                                                Status=CONST(Open)));
                                                   CaptionML=[DEU=E-Mail-Empf„nger-Filter;
                                                              ENU=Open UIC Documents;
                                                              NLD=Open UIC-documenten];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 25  ;   ;Drag and Drop Category;Boolean     ;CaptionML=[DEU=Drag-and-drop zulassen;
                                                              ENU=Drag and Drop Category;
                                                              NLD=Drag & dropcategorie] }
    { 26  ;   ;Source Field No.    ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Source Table No.));
                                                   OnValidate=VAR
                                                                RecIDMgt@1160040000 : Codeunit 6085604;
                                                              BEGIN
                                                                IF "Source Field No." = xRec."Source Field No." THEN
                                                                  EXIT;

                                                                RecIDMgt.CheckDocument(Code,FIELDCAPTION("Source Field No."));

                                                                IF NOT RecIDMgt.PartOfKey("Source Table No.","Source Field No.") THEN
                                                                  ERROR(Text013,FIELDCAPTION("Source Field No."),FIELDCAPTION("Source Table No."));
                                                              END;

                                                   CaptionML=[DEU=Quellfeldnr.;
                                                              ENU=Source Field No.;
                                                              NLD=Bronveldnr.;
                                                              NOR=Kildefeltnr.;
                                                              SVE=K„llf„ltsnr];
                                                   BlankZero=Yes }
    { 27  ;   ;Source Field No. (Name);Integer    ;TableRelation=Field.No. WHERE (TableNo=FIELD(Source Table No.));
                                                   OnValidate=VAR
                                                                RecIDMgt@1160040000 : Codeunit 6085604;
                                                              BEGIN
                                                                IF "Source Field No. (Name)" = 0 THEN
                                                                  "Source Field No. (Name)" := "Source Field No.";
                                                              END;

                                                   CaptionML=[DEU=Quellfeldnr. (Name);
                                                              ENU=Source Field No. (Name);
                                                              NLD=Bronveldnr. (naam)];
                                                   BlankZero=Yes }
    { 29  ;   ;Destination Header Table No.;Integer;
                                                   TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   OnValidate=VAR
                                                                Template@161024013 : Record 6085579;
                                                                TemplField@161024012 : Record 6085580;
                                                                RecIDMgt@1160040000 : Codeunit 6085604;
                                                              BEGIN
                                                                IF "Destination Header Table No." = xRec."Destination Header Table No." THEN
                                                                  EXIT;

                                                                Template.SETRANGE("Category Code",Code);
                                                                IF Template.FINDSET THEN
                                                                  REPEAT
                                                                    TemplField.SETRANGE("Template No.",Template."No.");
                                                                    TemplField.SETFILTER("Destination Header Field No.",'<>%1',0);
                                                                    IF NOT TemplField.ISEMPTY THEN
                                                                      ERROR(Text015,FIELDCAPTION("Destination Header Table No."),
                                                                        TemplField.TABLECAPTION,TemplField.FIELDCAPTION(TemplField."Destination Header Field No."));
                                                                  UNTIL Template.NEXT = 0;
                                                              END;

                                                   CaptionML=[DEU=Zielkopftabellennr.;
                                                              ENU=Destination Header Table No.;
                                                              NLD=Doeltabelnr. (kop)];
                                                   BlankZero=Yes }
    { 30  ;   ;Destination Line Table No.;Integer ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   OnValidate=VAR
                                                                Template@161024013 : Record 6085579;
                                                                TemplField@161024012 : Record 6085580;
                                                                RecIDMgt@1160040000 : Codeunit 6085604;
                                                              BEGIN
                                                                IF "Destination Line Table No." = xRec."Destination Line Table No." THEN
                                                                  EXIT;

                                                                Template.SETRANGE("Category Code",Code);
                                                                IF Template.FINDSET THEN
                                                                  REPEAT
                                                                    TemplField.SETRANGE("Template No.",Template."No.");
                                                                    TemplField.SETFILTER("Destination Line Field No.",'<>%1',0);
                                                                    IF NOT TemplField.ISEMPTY THEN
                                                                      ERROR(Text015,FIELDCAPTION("Destination Line Table No."),
                                                                        TemplField.TABLECAPTION,TemplField.FIELDCAPTION(TemplField."Destination Line Field No."));
                                                                  UNTIL Template.NEXT = 0;
                                                              END;

                                                   CaptionML=[DEU=Zielzeilentabellennr.;
                                                              ENU=Destination Line Table No.;
                                                              NLD=Doeltabelnr. (regel)];
                                                   BlankZero=Yes }
    { 31  ;   ;Archive E-mails on Document;Boolean;OnValidate=VAR
                                                                Template@6085573 : Record 6085579;
                                                              BEGIN
                                                                // "Merge from same E-mail" only works when "Archive E-mails on Document" is populated
                                                                // so makes no sence to have it set when "Archive E-mails on Document" = No
                                                                IF NOT "Archive E-mails on Document" THEN BEGIN
                                                                  Template.SETRANGE("Category Code",Code);
                                                                  Template.SETRANGE("Merge from same E-mail",TRUE);
                                                                  IF Template.FINDFIRST THEN
                                                                    ERROR(TemplateExistWithMergeEmailErr,FIELDCAPTION("Archive E-mails on Document"),"Archive E-mails on Document",
                                                                      Template.FIELDCAPTION("Merge from same E-mail"),Template."Merge from same E-mail");
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=E-Mails auf Beleg archivieren;
                                                              ENU=Archive E-mails on Document;
                                                              NLD=E-mails archiveren op document] }
    { 32  ;   ;Source Search Field No.;Integer    ;TableRelation=Field.No. WHERE (TableNo=FIELD(Source Table No.));
                                                   OnValidate=VAR
                                                                RecIDMgt@1160040000 : Codeunit 6085604;
                                                              BEGIN
                                                                IF "Source Field No." = xRec."Source Field No." THEN
                                                                  EXIT;
                                                              END;

                                                   CaptionML=[DEU=Quell-Suchfeldnr.;
                                                              ENU=Source Search Field No.;
                                                              NLD=Bronzoekveldnr.];
                                                   BlankZero=Yes }
    { 33  ;   ;TIFF Image Colour Mode;Option      ;CaptionML=[DEU=TIFF-Bild Farbmodus;
                                                              ENU=TIFF Image Colour Mode;
                                                              NLD=TIFF afbeelding Kleurinstelling];
                                                   OptionCaptionML=[DEU=Scharz-Weiá, Grau, Farbe;
                                                                    ENU=Black & White,Gray,Colour;
                                                                    NLD=Zwart & Wit,Grijs,Kleur];
                                                   OptionString=Black & White,Gray,Colour }
    { 50  ;   ;Codeunit ID: Reopen ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Codeunit));
                                                   CaptionML=[DEU=Codeunit-ID: Erneut ”ffnen;
                                                              ENU=Codeunit ID: Reopen;
                                                              NLD=Codeunit-id: Heropenen];
                                                   BlankZero=Yes }
    { 53  ;   ;Document Category GUID;GUID        ;CaptionML=[DEU=Belegkategorie-GUID;
                                                              ENU=Document Category GUID;
                                                              NLD=Documentcategorie GUID] }
    { 55  ;   ;Register Documents Automatic.;Boolean;
                                                   CaptionML=[DEU=Belege automatisch registrieren;
                                                              ENU=Register Documents Automatically;
                                                              NLD=Documenten automatisch registreren] }
    { 56  ;   ;Codeunit ID: Show Reg. Doc.;Integer;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Codeunit));
                                                   CaptionML=[DEU=Codeunit-ID: Reg.-Bel. anzeigen;
                                                              ENU=Codeunit ID: Show Reg. Doc.;
                                                              NLD=Codeunit-id: Toon gereg. doc.];
                                                   BlankZero=Yes }
    { 58  ;   ;Codeunit ID: Get Doc. Status;Integer;
                                                   TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Codeunit));
                                                   CaptionML=[DEU=Codeunit-ID: Bel.-Status abrufen;
                                                              ENU=Codeunit ID: Get Doc. Status;
                                                              NLD=Codeunit-id: Ophalen doc. status];
                                                   BlankZero=Yes }
    { 60  ;   ;Documents for OCR   ;Integer       ;CaptionML=[DEU=Anz. Dateien fr OCR;
                                                              ENU=Documents for OCR;
                                                              NLD=Documenten voor OCR;
                                                              SVE=Dokument f”r OCR];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 61  ;   ;Documents for Import;Integer       ;CaptionML=[DEU=Anz. Dateien fr Import;
                                                              ENU=Documents for Import;
                                                              NLD=Te importeren documenten];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 62  ;   ;Documents with Error;Integer       ;CaptionML=[DEU=Anz. der PDF-Dateien mit Fehlern;
                                                              ENU=Documents with Error;
                                                              NLD=Documenten met fout];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 64  ;   ;Split on Barcode    ;Boolean       ;CaptionML=[DEU=Teilen auf Barcode;
                                                              ENU=Split on Barcode;
                                                              NLD=Splitsen op Barcode] }
    { 65  ;   ;Split Barcode Text  ;Text250       ;CaptionML=[DEU=Teilen auf Barcodetext;
                                                              ENU=Split on Barcode Text;
                                                              NLD=Splitsen op Barcodetekst] }
    { 66  ;   ;Split on Blank Page ;Boolean       ;CaptionML=[DEU=Teilen auf leerer Seite;
                                                              ENU=Split on Blank Page;
                                                              NLD=Splitsen op blanco pagina] }
    { 67  ;   ;Split on Source ID  ;Boolean       ;OnValidate=BEGIN
                                                                IF "Split on Sep. Fields" AND ("Source Table No." > 0) THEN
                                                                  TESTFIELD("Split on Source ID",TRUE);
                                                              END;

                                                   CaptionML=[DEU=Teilen auf Herkunfts-ID;
                                                              ENU=Split on Source ID;
                                                              NLD=Splits op bron-id] }
    { 68  ;   ;Auto Move to Company;Boolean       ;CaptionML=[DEU=Automatisch zu Unternehmen verschieben;
                                                              ENU=Auto Move to Company;
                                                              NLD=Automatisch verplaatsen naar bedrijf] }
    { 70  ;   ;Codeunit ID: Get File Count;Integer;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Codeunit));
                                                   CaptionML=[DEU=Codeunit-ID: Dateianzahl abrufen;
                                                              ENU=Codeunit ID: Get File Count;
                                                              NLD=Codeunit-id: Bepaal aantal bestanden];
                                                   BlankZero=Yes }
    { 72  ;   ;Codeunit ID: Doc. or File;Integer  ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Codeunit));
                                                   CaptionML=[DEU=Codeunit-ID: Belegjournal;
                                                              ENU=Codeunit ID: Show Document or File;
                                                              NLD=Codeunit-id: Documentdagboek];
                                                   BlankZero=Yes }
    { 74  ;   ;Codeunit ID: Import Files;Integer  ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Codeunit));
                                                   CaptionML=[DEU=Codeunit-ID: Dateien importieren;
                                                              ENU=Codeunit ID: Import Files;
                                                              NLD=Codeunit-id: Importeer bestanden];
                                                   BlankZero=Yes }
    { 76  ;   ;Split on Sep. Fields;Boolean       ;OnValidate=BEGIN
                                                                IF "Source Table No." > 0 THEN
                                                                  "Split on Source ID" := TRUE;
                                                              END;

                                                   CaptionML=[DEU=Teilen auf Trennerfeldern;
                                                              ENU=Split on Separator Fields;
                                                              NLD=Splits op scheidingsvelden] }
    { 77  ;   ;Archive Documents in Cloud OCR;Option;
                                                   CaptionML=[DEU=Belege in Cloud OCR archivieren;
                                                              ENU=Archive Documents in Cloud OCR;
                                                              NLD=Archiveer documenten in Cloud OCR];
                                                   OptionCaptionML=[DEU=Nein,Ein Tag,Eine Woche,Ein Monat,Ein Jahr,Permanent;
                                                                    ENU=No,One Day,One Week,One Month,One Year,Permanent;
                                                                    NLD=Nee,Een dag,Een week,Een maand,Een jaar,Permanent];
                                                   OptionString=No,One Day,One Week,One Month,One Year,Permanent }
    { 78  ;   ;Max no. of Pages to Process;Integer;InitValue=200;
                                                   CaptionML=[DEU=Max. Anzahl der Seiten, die pro Datie  verarbeiten werden sollen;
                                                              ENU=Max. number of pages to process per file;
                                                              NLD=Max. aantal pagina's om per bestand te verwerken] }
    { 79  ;   ;OCR Export Codepage ;Integer       ;InitValue=1252;
                                                   CaptionML=[DEU=OCR Export Codepage;
                                                              ENU=OCR Export Codepage;
                                                              NLD=OCR Export Codepage] }
    { 90  ;   ;Exported to Cloud OCR;Boolean      ;CaptionML=[DEU=Exportiert in Cloud OCR;
                                                              ENU=Exported to Cloud OCR;
                                                              NLD=Ge‰xporteerd naar Cloud OCR] }
    { 11012001;;Company Identification Fields;Integer;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Count("Category Company Caption" WHERE (Document Category=FIELD(Code)));
                                                   CaptionML=[DEU=Unternehmensidentifikationstexte;
                                                              ENU=Company Identification Fields;
                                                              NLD=Bedrijf identificatievelden];
                                                   BlankZero=Yes;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text002@1000000001 : TextConst 'DEU=In dieser Gruppe befinden sich keine offenen Belege.;ENU=There are no open documents in this group.;NLD=Er zijn geen open documenten in deze groep.';
      Text003@1160040005 : TextConst 'DEU=Sie k”nnen eine %1 nicht umbenennen.;ENU=You cannot rename a %1.;NLD=U kunt een %1 niet hernoemen.;NOR=Du kan ikke gi nytt navn til en %1.;SVE=Du kan inte byta namn p† %1.';
      Text006@1000000005 : TextConst 'DEU=Es k”nnen keine Belegtrenner auf einem %1 erstellt werden\\ In einem %1 platzierte Belege werden automatisch auf die korrekten Kategorien verteilt, basierend auf den Belegtrennermustern der einzelnen Kategorien.;ENU=No Document Separators can be created on a %1.\\Documents placed in a %1 will automatically be distributed to the correct categories, based on the Document Separator patterns of the individual categories.;NLD=Er kunnen geen document scheidingstekens worden gemaakt op een %1.\\Documenten in een %1 worden automatisch aan de juiste categorie toegewezen.';
      Text007@1000000006 : TextConst 'DEU=Sie k”nnen %1 %2 nicht l”schen, da noch mindestens eine Vorlage fr diese Kategorie vorhanden ist.;ENU=You cannot delete %1 %2 because there is at least one template for this category.;NLD=U kunt %1 %2 niet verwijderen om dat er tenminste een sjabloon voor deze categorie is.';
      Text008@1000000007 : TextConst 'DEU=Sie k”nnen %1 %2 nicht l”schen, da noch mindestens ein Beleg fr diese Kategorie vorhanden ist.;ENU=You cannot delete %1 %2 because there is at least one document for this category.;NLD=U kunt %1 %2 niet verwijderen om dat er tenminste een sjabloon voor deze categorie is.';
      Text010@161024013 : TextConst 'DEU=%1 - %2;ENU=%1 - %2;NLD=%1 - %2;NOR=%1 - %2;SVE=%1 - %2';
      Text012@161024015 : TextConst 'DEU=L”schen Sie alle Identifikationsfelder, bevor Sie %1 „ndern.;ENU=Please delete all identification fields before you change %1.;NLD=Verwijder alle kenmerkvelden voordat u %1 wijzigt.';
      Text013@1160040001 : TextConst 'DEU=%1 ist nicht Teil des Prim„rschlssels fr von %2.;ENU=The %1 is not part of the primary key of the %2.;NLD=%1 is geen onderdeel van de primaire sleutel van %2.';
      Text014@1160040002 : TextConst 'DEU=%1 kann nicht ge„ndert werden, da mindestens eine Vorlage mit einem Wert fr dieses Unternehmen vorhanden ist.;ENU=The %1 cannot be changed because there is at least one template with a value for this category.;NLD=%1 kan niet worden gewijzigd omdat er tenminste ‚‚n sjabloon is met een waarde voor deze categorie.';
      Text015@161024016 : TextConst 'DEU=%1 kann nicht ge„ndert werden, da mindestens ein oder mehrere %2 mit %3 zugeordnet vorhanden ist.;ENU=The %1 cannot be changed because it has one or more %2 with %3 assigned.;NLD=%1 kan niet worden gewijzigd omdat er ‚‚n of meer %2-records met %3 aan toegewezen zijn.';
      Text016@1160040003 : TextConst 'DEU=%1 wird bereits als %2 in einem anderen %3 verwendet.;ENU=%1 is already used as %2 in another %3.;NLD=%1 is reeds gebruikt als %2 in een andere %3.';
      ForceUpdate@1160040004 : Boolean;
      Status@1160040000 : 'Files for OCR,Files for Import,Files with Error,Open Documents,Registered Documents,Rejected Documents,UIC Documents';
      TemplateExistWithMergeEmailErr@6085573 : TextConst 'DEU="Sie k”nnen %1 = %2 nicht einrichten, wenn es Vorlagen mit %3 = %4 gibt, weil %3 nur funktioniert, wenn %1 aktiv ist.";ENU="You cannot set %1 = %2 when templates exist with %3 = %4 as %3 only works when %1 is active.";NLD="U kunt %1 = %2 niet instellen als er sjablonen zijn met %3 = %4 omdat %3 alleen werkt wanneer %1 actief is."';

    PROCEDURE RegisterOpenDoc@1000000002();
    VAR
      Document@1000000000 : Record 6085590;
    BEGIN
      Document.SETCURRENTKEY(Status);
      Document.SETRANGE(Status,Document.Status::Open);
      Document.SETRANGE("Document Category Code",Code);
      IF Document.ISEMPTY THEN
        ERROR(Text002);

      IF Document.FINDSET THEN
        REPEAT
          Document.MARK(TRUE);
        UNTIL Document.NEXT = 0;

      Document.FINDFIRST;
      Document.SETRANGE("Document Category Code");
      Document.MARKEDONLY(TRUE);
      PAGE.RUN(PAGE::"Document Card",Document);
    END;

    PROCEDURE CheckSourceTable@1160040000();
    VAR
      Template@1160040004 : Record 6085579;
      IdentField@1160040006 : Record 6085759;
      TableFilterField@1160040001 : Record 6085762;
      DCMgt@1160040003 : Codeunit 6085603;
      RecIDMgt@1160040000 : Codeunit 6085604;
    BEGIN
      IF "Source Table No." > 0 THEN
        DCMgt.IsLogActive("Source Table No.",TRUE);

      IF "Source Table No." = xRec."Source Table No." THEN
        EXIT;

      RecIDMgt.CheckDocument(Code,FIELDCAPTION("Source Table No."));

      Template.SETRANGE("Category Code",Code);
      Template.SETFILTER("Source Record ID Tree ID",'<>%1',0);
      IF NOT Template.ISEMPTY THEN
        ERROR(Text014,FIELDCAPTION("Source Table No."));

      IdentField.SETRANGE("Document Category Code",Code);
      IF NOT IdentField.ISEMPTY THEN
        ERROR(Text012);

      RecIDMgt.DeleteTableFilter("Document Category GUID");

      "Source Field No." := RecIDMgt.GetFirstKeyField("Source Table No.");
      "Source Field No. (Name)" := "Source Field No.";
      MODIFY;
    END;

    PROCEDURE UpdateCounters@1160040001();
    BEGIN
      IF "Codeunit ID: Get File Count" = 0 THEN
        CODEUNIT.RUN(CODEUNIT::"Update DC Doc. Counters",Rec)
      ELSE
        CODEUNIT.RUN("Codeunit ID: Get File Count",Rec)
    END;

    PROCEDURE GetCategoryPath@1160040003(FileType@1160040003 : 'Obsolete,PDFForOCR,ImportFiles,ErrorFiles') Path : Text[1024];
    VAR
      ContiniaCompanySetup@6085573 : Record 6192773;
      DCSetup@1160040000 : Record 6085573;
      FileSysMgt@1160040001 : Codeunit 6085584;
      VersMgt@1160040002 : Codeunit 6192775;
    BEGIN
      DCSetup.GET;
      ContiniaCompanySetup.GET;
      ContiniaCompanySetup.TESTFIELD("Company Code");
      Path := ContiniaCompanySetup."Company Code" + ' - ' + Code;

      CASE FileType OF
        FileType::Obsolete:
          ERROR('This option is obsolete');
        FileType::PDFForOCR:
          Path :=  DCSetup."PDF File Path for OCR" + Path;
        FileType::ImportFiles:
          IF DCSetup."Use Cloud OCR" AND (VersMgt.NAVVersion > 60100) THEN
            Path := Path
          ELSE
            IF DCSetup."Use Cloud OCR" THEN
              Path := FileSysMgt.GetTempFolderPath + Path // Use in-memory storage
            ELSE
              Path := DCSetup."File Path for OCR-proc. files" + Path;
        FileType::ErrorFiles:
          Path := DCSetup."PDF File Path for OCR" + Path + '\ERROR';
      END;

      IF NOT DCSetup."Use Cloud OCR" THEN
        IF (NOT FileSysMgt.DirectoryExists(Path)) THEN
          FileSysMgt.CreateDirectory(Path);
    END;

    PROCEDURE UploadFilesForOCR@1160040004();
    VAR
      CODC@1160040000 : Codeunit 6085617;
      DocCat@1160040001 : Record 6085575;
    BEGIN
      IF CODC.IsCloudActive(FALSE) THEN BEGIN
        DocCat := Rec;
        CODC.UploadPdfDocsInCategory(DocCat);
      END;
    END;

    PROCEDURE UploadFilesInFolderForOCR@6085574(FolderPath@6085573 : Text[1024]);
    VAR
      CODC@1160040000 : Codeunit 6085617;
      DocCat@1160040001 : Record 6085575;
    BEGIN
      IF CODC.IsCloudActive(FALSE) THEN BEGIN
        DocCat := Rec;
        CODC.UploadPdfDocsInFolder(DocCat,FolderPath);
      END;
    END;

    LOCAL PROCEDURE OpenDocumentOrFile@1160040006(_Status@1160040000 : 'Files for OCR,Files for Import,Files with Error,Open Documents,Registered Documents,Rejected Documents,UIC Documents');
    VAR
      ShowDocAndFilesArgsTemp@1160040001 : Record 6085636;
    BEGIN
      ShowDocAndFilesArgsTemp."Document Category Code" := Code;
      ShowDocAndFilesArgsTemp.Status := _Status;

      IF "Codeunit ID: Doc. or File" = 0 THEN
        CODEUNIT.RUN(CODEUNIT::"Open Document or File",ShowDocAndFilesArgsTemp)
      ELSE
        CODEUNIT.RUN("Codeunit ID: Doc. or File",ShowDocAndFilesArgsTemp);
    END;

    PROCEDURE OpenFilesForOCR@1160040002();
    BEGIN
      OpenDocumentOrFile(Status::"Files for OCR");
    END;

    PROCEDURE OpenFilesForImport@1160040005();
    BEGIN
      OpenDocumentOrFile(Status::"Files for Import");
    END;

    PROCEDURE OpenFilesWithError@1160040007();
    BEGIN
      OpenDocumentOrFile(Status::"Files with Error");
    END;

    PROCEDURE OpenOpenDocuments@1160040008();
    BEGIN
      OpenDocumentOrFile(Status::"Open Documents");
    END;

    PROCEDURE OpenRegisteredDocuments@1160040009();
    BEGIN
      OpenDocumentOrFile(Status::"Registered Documents");
    END;

    PROCEDURE OpenRejectedDocuments@1160040010();
    BEGIN
      OpenDocumentOrFile(Status::"Rejected Documents");
    END;

    PROCEDURE OpenUICDocuments@1160040011();
    BEGIN
      OpenDocumentOrFile(Status::"UIC Documents");
    END;

    PROCEDURE HasMasterTemplate@6085593() : Boolean;
    VAR
      Template@6085573 : Record 6085579;
    BEGIN
      Template.SETCURRENTKEY("Category Code",Type);
      Template.SETRANGE("Category Code",Code);
      Template.SETRANGE(Type,Template.Type::Master);
      EXIT(NOT Template.ISEMPTY);
    END;

    PROCEDURE IsPurchInvCrMemoCategory@6085573() : Boolean;
    BEGIN
      EXIT(("Source Table No." = DATABASE::Vendor) AND ("Destination Header Table No." = DATABASE::"Purchase Header"));
    END;

    PROCEDURE GetEmail@6085575() : Text[250];
    VAR
      DCSetup@6085575 : Record 6085573;
      ConnEndpoint@6085574 : Record 6085620;
      CODC@6085573 : Codeunit 6085617;
    BEGIN
      IF "E-Mail Connection Endpoint" <> '' THEN
        IF ConnEndpoint.GET("E-Mail Connection Endpoint") THEN
          IF DCSetup.GET THEN
            IF DCSetup."Use Cloud OCR" THEN
              EXIT(CODC.GetOneCategoryEmail(ConnEndpoint));
      EXIT('');
    END;

    BEGIN
    END.
  }
}

