OBJECT Codeunit 6085703 Purch. Doc. - Full Capture
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.5.00.06,4PS14.00;
  }
  PROPERTIES
  {
    TableNo=6085590;
    Permissions=TableData 6085700=rimd;
    OnRun=VAR
            GLSetup@1000000015 : Record 98;
            DCSetup@1160040001 : Record 6085573;
            Field@161024020 : Record 6085580;
            TemplField@161024019 : Record 6085580;
            TemplField2@1000000006 : Record 6085580;
            Value@1000000002 : Record 6085593;
            Value2@1000000007 : Record 6085593;
            FieldRule@1000000000 : Record 6085583;
            PurchDocMatch@161024013 : Record 6085700;
            DocumentComment@1000000005 : Record 6085594;
            Vendor@1000000003 : Record 23;
            Purchaser@1002 : Record 13;
            PaymentTerms@1000000004 : Record 3;
            TemplFieldCaption@1000000008 : Record 6085581;
            PurchHeader@161024022 : Record 38;
            CaptionStartWord@1000000011 : ARRAY [100] OF Record 6085592;
            CaptionEndWord@1000000012 : ARRAY [100] OF Record 6085592;
            DataTransl@161024018 : Record 6085584;
            CaptureEngine@1000000010 : Codeunit 6085575;
            CaptionFound@1000000013 : Boolean;
            OrderNo@161024021 : Code[20];
            CodePart@161024014 : Code[250];
            TopMostCodePart@161024017 : Code[250];
            TopMostY@161024016 : Integer;
            DocType@161024023 : Integer;
            OurContact@1001 : Text[50];
            CaptionPart@1000 : Text[250];
            VendorNo@6085575 : Code[20];
            Template@6085574 : Record 6085579;
            TemplSearchText@6085573 : Text[200];
            GLAccountNo@1100528200 : Text[30];
            lPaymentTermsCode@1100528201 : Code[20];
          BEGIN
            DeleteComments(DocumentComment.Area::Capture);

            PurchDocMatch.SETRANGE("Document No.","No.");
            PurchDocMatch.DELETEALL(TRUE);

            GLSetup.GET;
            DCSetup.GET;

            VendorNo := GetSourceID;
            IF NOT Vendor.GET(VendorNo) THEN BEGIN
              Template.GET("Template No.");
              TemplSearchText := Template."Search Text";
              ERROR(VendorNotFountErr,VendorNo,TemplSearchText,"Template No.");
            END;

            // *********************************************************************************************************************************
            // IF CURRENCY CODE HAS NOT BEEN RECOGNIZED, THEN GO GET IT FROM THE VENDOR
            // *********************************************************************************************************************************
            IF Field.GET("Template No.",Field.Type::Header,'CURRCODE') THEN
              IF (NOT CaptureMgt.GetFieldValue(Rec,Field,0,Value)) OR (Value."Value (Text)" = '') THEN
                IF Vendor."Currency Code" <> '' THEN BEGIN
                  CaptureMgt.UpdateFieldValue("No.",0,0,Field,Vendor."Currency Code",TRUE,FALSE);
                  IF Field."Search for Value" THEN
                    DocumentComment.Add(Rec,Field,0,DocumentComment.Area::Capture,DocumentComment."Comment Type"::Warning,
                      STRSUBSTNO(Text001,Field."Field Name",LOWERCASE(Vendor.TABLECAPTION)));
                END ELSE
                  IF DCSetup."Fill-out LCY" THEN
                    CaptureMgt.UpdateFieldValue("No.",0,0,Field,GLSetup."LCY Code",FALSE,FALSE);

            // *********************************************************************************************************************************
            // FIND THE DOCUMENT TYPE
            // *********************************************************************************************************************************
            Field.GET("Template No.",Field.Type::Header,'DOCTYPE');
            IF CaptureMgt.GetText(Rec,Field.Type::Header,Field.Code,0) = '' THEN BEGIN
              TemplFieldCaption.SETRANGE("Template No.","Template No.");
              TemplFieldCaption.SETRANGE(Type,Field.Type);
              TemplFieldCaption.SETRANGE(Code,Field.Code);
              IF TemplFieldCaption.FINDSET(FALSE,FALSE) THEN
                REPEAT
                  IF STRPOS(TemplFieldCaption.Caption,':') <> 0 THEN BEGIN
                    CodePart := DELCHR(COPYSTR(TemplFieldCaption.Caption,1,STRPOS(TemplFieldCaption.Caption,':')-1),'<>',' ');
                    CaptionPart := DELCHR(COPYSTR(TemplFieldCaption.Caption,STRPOS(TemplFieldCaption.Caption,':')+1),'<>',' ');
                    IF (CodePart <> '') AND (CaptionPart <> '') THEN BEGIN
                      TemplFieldCaption.Caption := CaptionPart;
                      IF CaptureEngine.FindCaption("No.",1,Field,TemplFieldCaption,CaptionStartWord,CaptionEndWord) THEN BEGIN
                        IF (CaptionStartWord[1].Top < TopMostY) OR (TopMostY = 0) THEN BEGIN
                          TopMostY := CaptionStartWord[1].Top;
                          TopMostCodePart := CodePart;
                        END;
                      END;
                    END;
                  END;
                UNTIL TemplFieldCaption.NEXT = 0;

              CaptureMgt.UpdateFieldValue("No.",0,0,Field,TopMostCodePart,TRUE,FALSE);
            END;

            //**4PS
            // *********************************************************************************************************************************
            // GET PAYMENT TERMS FROM THE RELATED ORDER, THE MAY DIFFER FROM PAYMENT TERMS FROM THE VENDOR
            // *********************************************************************************************************************************
            lPaymentTermsCode := Vendor."Payment Terms Code";
            IF Field.GET("Template No.",Field.Type::Header,'DUEDATE') THEN BEGIN
              OrderNo := COPYSTR(PurchDocMgt.GetOurDocumentNo(Rec),1,MAXSTRLEN(OrderNo));
              IF (OrderNo <> '') THEN BEGIN
                IF PurchDocMgt.GetIsInvoice(Rec) THEN
                  DocType := PurchHeader."Document Type"::Order
                ELSE
                  DocType := PurchHeader."Document Type"::"Return Order";

                IF PurchHeader.GET(DocType,OrderNo) THEN
                  IF PurchHeader."Payment Terms Code" <> '' THEN
                    IF PaymentTerms.GET(PurchHeader."Payment Terms Code") THEN
                      lPaymentTermsCode := PurchHeader."Payment Terms Code";

              END;
            END;
            //**4PS.en
            // *********************************************************************************************************************************
            // IF DUE DATE HAS NOT BEEN RECOGNIZED, THEN CALCULATE IT BASED ON THE DOCUMENT DATE AND VENDOR PAYMENT TERMS
            // *********************************************************************************************************************************
            IF Field.GET("Template No.",Field.Type::Header,'DUEDATE') THEN BEGIN
              IF (NOT CaptureMgt.GetFieldValue(Rec,Field,0,Value)) OR (Value."Value (Date)" = 0D) THEN BEGIN
                IF PurchDocMgt.GetIsInvoice(Rec) THEN BEGIN
                  TemplField.GET("Template No.",Field.Type::Header,'DOCDATE');
                  IF (CaptureMgt.GetFieldValue(Rec,TemplField,0,Value2)) AND (Value2."Value (Date)" <> 0D) THEN BEGIN
                    IF PaymentTerms.GET(Vendor."Payment Terms Code") AND (FORMAT(PaymentTerms."Due Date Calculation") <> '') THEN BEGIN
                    //**4PS.sn
                      Template.GET("Template No.");
                      IF Template."Use E-mail Date for Doc. Date" AND ("E-Mail Date" <> 0DT) THEN
                        CaptureMgt.UpdateFieldValue("No.",0,0,Field,
                          FORMAT(CALCDATE(PaymentTerms."Due Date Calculation",DT2DATE("E-Mail Date"))),TRUE,FALSE)
                      ELSE
                         CaptureMgt.UpdateFieldValue("No.",0,0,Field,
                           FORMAT(CALCDATE(PaymentTerms."Due Date Calculation",Value2."Value (Date)")),TRUE,FALSE);
                      IF Template."Use E-mail Date for Doc. Date" AND ("E-Mail Date" <> 0DT) THEN
                        DocumentComment.Add(Rec, Field, 0, DocumentComment.Area::Capture, DocumentComment."Comment Type"::Information,
                          STRSUBSTNO(Text002, Field."Field Name", LOWERCASE(FIELDCAPTION("E-Mail Date"))))
                      ELSE
                        DocumentComment.Add(Rec, Field, 0, DocumentComment.Area::Capture, DocumentComment."Comment Type"::Information,
                          STRSUBSTNO(Text002,Field."Field Name",TemplField."Field Name"));
                    END;
                    //**4PS.en
                    //META001
                  END;
                END ELSE BEGIN
                  //**4PS.sn
                  Template.GET("Template No.");
                  IF Template."Use E-mail Date for Doc. Date" AND ("E-Mail Date" <> 0DT) THEN
                    CaptureMgt.UpdateFieldValue("No.",0,0,Field,
                      FORMAT(CALCDATE(PaymentTerms."Due Date Calculation",DT2DATE("E-Mail Date"))),TRUE,FALSE)
                  ELSE
                  //**4PS.en
                  CaptureMgt.UpdateFieldValue("No.",0,0,Field,FORMAT(TODAY),TRUE,FALSE);
                  DocumentComment.Add(Rec,Field,0,DocumentComment.Area::Capture,DocumentComment."Comment Type"::Information,
                    STRSUBSTNO(Text003,Field."Field Name"));
                END;
              //**4PS.sn
              END ELSE BEGIN
                IF PaymentTerms.GET(lPaymentTermsCode) AND (FORMAT(PaymentTerms."Due Date Calculation") <> '') THEN BEGIN
                  Template.GET("Template No.");
                  IF Template."Use E-mail Date for Doc. Date" AND ("E-Mail Date" <> 0DT) THEN BEGIN
                    CaptureMgt.UpdateFieldValue("No.",0,0,Field,
                      FORMAT(CALCDATE(PaymentTerms."Due Date Calculation",DT2DATE("E-Mail Date"))),TRUE,FALSE);
                    DocumentComment.Add(Rec, Field, 0, DocumentComment.Area::Capture, DocumentComment."Comment Type"::Warning,
                                STRSUBSTNO(Text002, Field."Field Name", LOWERCASE(FIELDCAPTION("E-Mail Date"))));
                  END;
                END;
              END;
              //**4PS.en
            END;

            // *********************************************************************************************************************************
            // IF PURCHASER HAS NOT BEEN FOUND THEN GET IT FROM FROM NAME, ORDER OR VENDOR
            // *********************************************************************************************************************************
            IF Field.GET("Template No.",Field.Type::Header,'OURCONTACT') THEN BEGIN
              OurContact := COPYSTR(PurchDocMgt.GetOurContact(Rec),1,MAXSTRLEN(OurContact));
              IF OurContact <> '' THEN BEGIN
                IF NOT Purchaser.GET(COPYSTR(OurContact,1,MAXSTRLEN(Purchaser.Code))) THEN BEGIN
                  Purchaser.SETFILTER(Name,'@' + CaptureMgt.ReplaceIllegalFilterCharacters(OurContact,FALSE,0));
                  IF Purchaser.FINDFIRST THEN
                    CaptureMgt.UpdateFieldValue("No.",0,0,Field,Purchaser.Code,TRUE,FALSE);
                END;
              END ELSE BEGIN
                OrderNo := COPYSTR(PurchDocMgt.GetOurDocumentNo(Rec),1,MAXSTRLEN(OrderNo));
                IF OrderNo <> '' THEN BEGIN
                  IF PurchDocMgt.GetIsInvoice(Rec) THEN
                    DocType := PurchHeader."Document Type"::Order
                  ELSE
                    DocType := PurchHeader."Document Type"::"Return Order";

                  IF PurchHeader.GET(DocType,OrderNo) THEN
                    CaptureMgt.UpdateFieldValue("No.",0,0,Field,PurchHeader."Purchaser Code",TRUE,FALSE);
                END ELSE
                  IF Vendor."Purchaser Code" <> '' THEN
                    CaptureMgt.UpdateFieldValue("No.",0,0,Field,Vendor."Purchaser Code",TRUE,FALSE);
              END;
            END;

            // *********************************************************************************************************************************
            // UPDATE POSTING ACCOUNTS FROM DATA TRANSLATION TABLE
            // *********************************************************************************************************************************
            Field.SETRANGE("Template No.","Template No.");
            Field.SETRANGE(Type,Field.Type::Header);
            Field.SETFILTER("G/L Account Field Code",'<>%1','');
            IF Field.FINDSET THEN
              REPEAT
                IF TemplField2.GET(Field."Template No.",Field.Type,Field."G/L Account Field Code") THEN BEGIN
                  IF (NOT CaptureMgt.GetFieldValue(Rec,TemplField2,0,Value)) OR (Value."Value (Text)" = '') THEN BEGIN
                    DataTransl.SETRANGE("Template No.","Template No.");
                    DataTransl.SETRANGE(Type,Field.Type);
                    DataTransl.SETRANGE("Field Code",Field.Code);

                    IF DataTransl.FINDFIRST AND (DataTransl."Translate to Type" = DataTransl."Translate to Type"::"G/L Account") AND
                      (DataTransl."Translate to No." <> '')
                    THEN
                      CaptureMgt.UpdateFieldValue("No.",0,0,TemplField2,DataTransl."Translate to No.",TRUE,FALSE);
                  END;
                END;
              UNTIL Field.NEXT = 0;

            //**4PS
            // *********************************************************************************************************************************
            // UPDATE Account No from the vendor table only when no project is involved, and the field is notfound on the document
            // Field 'GLACCOUNTNO' should be included in the header fields, otherwise it will not be handled
            // *********************************************************************************************************************************
            IF Vendor."Preference Account No." <> '' THEN BEGIN
              IF (PurchDocMgt.GetJobNoHeader(Rec) = '') THEN BEGIN
                GLAccountNo := PurchDocMgt.GetGLAccountNo(Rec, '');
                IF GLAccountNo = '' THEN BEGIN
                  IF Field.GET("Template No.", Field.Type::Header,'GLACCOUNTNO') THEN
                    CaptureMgt.UpdateFieldValue("No.", 0, 0, Field, Vendor."Preference Account No.", TRUE,FALSE);
                END;
              END;
            END;

            IF NOT "Amounts Only" THEN
            //**4PS.en
              AdjustMissingQty(Rec);

            IF PurchDocMgt.AutoMatchEnabled(Rec) THEN
              PurchDocMgt.AutoMatch(Rec);
          END;

  }
  CODE
  {
    VAR
      Text001@1000000000 : TextConst 'DEU=%1 wurde aus %2 Åbernommen.;ENU=WARNING: %1 was taken from the %2.;NLD=WAARSCHUWING: %1 komt van %2.';
      Text002@1000000001 : TextConst 'DEU=%1 wurde berechnet auf Grundlage von %2.;ENU=%1 was calculated based on %2.;NLD=%1 is berekend op basis van %2.';
      PurchDocMgt@1000000007 : Codeunit 6085709;
      CaptureMgt@1000000008 : Codeunit 6085576;
      Text003@161024015 : TextConst 'DEU=%1 wurde auf heute eingestellt.;ENU=%1 was set to today.;NLD=%1 is ingesteld op vandaag.';
      VendorNotFountErr@6085573 : TextConst 'ENU=Vendor %1 doesn''t exist. Please, remove search text %2 from template %3.';

    PROCEDURE AdjustMissingQty@1000000000(VAR Document@1000000000 : Record 6085590);
    VAR
      DocumentLine@1000000001 : TEMPORARY Record 6085596;
      Template@1000000007 : Record 6085579;
      Field@1000000006 : Record 6085580;
      UnitCost@1000000002 : Decimal;
      DiscPct@1000000003 : Decimal;
      DiscAmount@1000000004 : Decimal;
      LineAmount@1000000005 : Decimal;
    BEGIN
      Document.BuildTempLinesTable(DocumentLine);

      IF DocumentLine.ISEMPTY THEN
        EXIT;

      Template.GET(Document."Template No.");

      DocumentLine.FINDSET;
      REPEAT
        IF PurchDocMgt.GetLineQuantity(Document,DocumentLine."Line No.") = 0 THEN BEGIN
          UnitCost := PurchDocMgt.GetLineUnitCost(Document,DocumentLine."Line No.");
          IF UnitCost <> 0 THEN BEGIN
            DiscPct := PurchDocMgt.GetLineDiscPct(Document,DocumentLine."Line No.");
            DiscAmount := PurchDocMgt.GetLineDiscAmount(Document,DocumentLine."Line No.");
            LineAmount := PurchDocMgt.GetLineAmount(Document,DocumentLine."Line No.");

            IF DiscAmount <> 0 THEN
              UnitCost := UnitCost - DiscAmount
            ELSE IF DiscPct <> 0 THEN
              UnitCost := UnitCost * ((100-DiscPct) / 100);

            IF UnitCost = LineAmount THEN BEGIN
              Field.GET(Document."Template No.",Field.Type::Line,'QUANTITY');
              CaptureMgt.UpdateFieldValue(Document."No.",DocumentLine."Page No.",DocumentLine."Line No.",Field,FORMAT(1),TRUE,FALSE);
            END;
          END;
        END;
      UNTIL DocumentLine.NEXT = 0;
    END;

    BEGIN
    {
      This codeunit performs post-capture actions on purchase documents
    }
    END.
  }
}

