OBJECT Codeunit 270 ResJnlManagement
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03;
  }
  PROPERTIES
  {
    Permissions=TableData 206=imd,
                TableData 236=imd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=RESOURCES;NOR=RESSURSER;SVE=RESURSER';
      Text001@1001 : TextConst 'ENU=Resource Journals;NOR=Ressurskladder;SVE=Resursjournaler';
      Text002@1002 : TextConst 'ENU=RECURRING;NOR=GJENTAK.;SVE=èTERKOMMER';
      Text003@1003 : TextConst 'ENU=Recurring Resource Journal;NOR=Gjentakelse ressurskladd;SVE=èterkommande resursjournal';
      Text004@1004 : TextConst 'ENU=DEFAULT;NOR=STANDARD;SVE=STANDARD';
      Text005@1005 : TextConst 'ENU=Default Journal;NOR=Standardkladd;SVE=Standardjournal';
      OldResNo@1006 : Code[20];
      OpenFromBatch@1007 : Boolean;
      ResJnlBatchEmptyErr@1500 : TextConst 'ENU=The Resource Journal batch job is empty.;NOR=Kjõrselen for ressurskladd er tom.';

    [External]
    PROCEDURE TemplateSelection@1(PageID@1004 : Integer;RecurringJnl@1000 : Boolean;VAR ResJnlLine@1003 : Record 207;VAR JnlSelected@1002 : Boolean);
    VAR
      ResJnlTemplate@1001 : Record 206;
    BEGIN
      JnlSelected := TRUE;

      ResJnlTemplate.RESET;
      ResJnlTemplate.SETRANGE("Page ID",PageID);
      ResJnlTemplate.SETRANGE(Recurring,RecurringJnl);

      CASE ResJnlTemplate.COUNT OF
        0:
          BEGIN
            ResJnlTemplate.INIT;
            ResJnlTemplate.Recurring := RecurringJnl;
            IF NOT RecurringJnl THEN BEGIN
              ResJnlTemplate.Name := Text000;
              ResJnlTemplate.Description := Text001;
            END ELSE BEGIN
              ResJnlTemplate.Name := Text002;
              ResJnlTemplate.Description := Text003;
            END;
            ResJnlTemplate.VALIDATE("Page ID");
            ResJnlTemplate.INSERT;
            COMMIT;
          END;
        1:
          ResJnlTemplate.FINDFIRST;
        ELSE
          JnlSelected := PAGE.RUNMODAL(0,ResJnlTemplate) = ACTION::LookupOK;
      END;
      IF JnlSelected THEN BEGIN
        ResJnlLine.FILTERGROUP := 2;
        ResJnlLine.SETRANGE("Journal Template Name",ResJnlTemplate.Name);
        ResJnlLine.FILTERGROUP := 0;
        IF OpenFromBatch THEN BEGIN
          ResJnlLine."Journal Template Name" := '';
          PAGE.RUN(ResJnlTemplate."Page ID",ResJnlLine);
        END;
      END;
    END;

    [External]
    PROCEDURE TemplateSelectionFromBatch@10(VAR ResJnlBatch@1000 : Record 236);
    VAR
      ResJnlLine@1002 : Record 207;
      ResJnlTemplate@1003 : Record 206;
    BEGIN
      OpenFromBatch := TRUE;
      ResJnlTemplate.GET(ResJnlBatch."Journal Template Name");
      ResJnlTemplate.TESTFIELD("Page ID");
      ResJnlBatch.TESTFIELD(Name);

      ResJnlLine.FILTERGROUP := 2;
      ResJnlLine.SETRANGE("Journal Template Name",ResJnlTemplate.Name);
      ResJnlLine.FILTERGROUP := 0;

      ResJnlLine."Journal Template Name" := '';
      ResJnlLine."Journal Batch Name" := ResJnlBatch.Name;
      PAGE.RUN(ResJnlTemplate."Page ID",ResJnlLine);
    END;

    [External]
    PROCEDURE OpenJnl@2(VAR CurrentJnlBatchName@1000 : Code[10];VAR ResJnlLine@1001 : Record 207);
    BEGIN
      CheckTemplateName(ResJnlLine.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName);
      ResJnlLine.FILTERGROUP := 2;
      ResJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      ResJnlLine.FILTERGROUP := 0;
    END;

    [External]
    PROCEDURE OpenJnlBatch@12(VAR ResJnlBatch@1000 : Record 236);
    VAR
      ResJnlTemplate@1002 : Record 206;
      ResJnlLine@1004 : Record 207;
      JnlSelected@1003 : Boolean;
    BEGIN
      IF ResJnlBatch.GETFILTER("Journal Template Name") <> '' THEN
        EXIT;
      ResJnlBatch.FILTERGROUP(2);
      IF ResJnlBatch.GETFILTER("Journal Template Name") <> '' THEN BEGIN
        ResJnlBatch.FILTERGROUP(0);
        EXIT;
      END;
      ResJnlBatch.FILTERGROUP(0);

      IF NOT ResJnlBatch.FIND('-') THEN BEGIN
        IF NOT ResJnlTemplate.FINDFIRST THEN
          TemplateSelection(0,FALSE,ResJnlLine,JnlSelected);
        IF ResJnlTemplate.FINDFIRST THEN
          CheckTemplateName(ResJnlTemplate.Name,ResJnlBatch.Name);
        ResJnlTemplate.SETRANGE(Recurring,TRUE);
        IF NOT ResJnlTemplate.FINDFIRST THEN
          TemplateSelection(0,TRUE,ResJnlLine,JnlSelected);
        IF ResJnlTemplate.FINDFIRST THEN
          CheckTemplateName(ResJnlTemplate.Name,ResJnlBatch.Name);
        ResJnlTemplate.SETRANGE(Recurring);
      END;
      IF NOT ResJnlBatch.FIND('-') THEN
        ERROR(ResJnlBatchEmptyErr);

      JnlSelected := TRUE;
      ResJnlBatch.CALCFIELDS(Recurring);
      ResJnlTemplate.SETRANGE(Recurring,ResJnlBatch.Recurring);
      IF ResJnlBatch.GETFILTER("Journal Template Name") <> '' THEN
        ResJnlTemplate.SETRANGE(Name,ResJnlBatch.GETFILTER("Journal Template Name"));
      CASE ResJnlTemplate.COUNT OF
        1:
          ResJnlTemplate.FINDFIRST;
        ELSE
          JnlSelected := PAGE.RUNMODAL(0,ResJnlTemplate) = ACTION::LookupOK;
      END;
      IF NOT JnlSelected THEN
        ERROR('');

      ResJnlBatch.FILTERGROUP(0);
      ResJnlBatch.SETRANGE("Journal Template Name",ResJnlTemplate.Name);
      ResJnlBatch.FILTERGROUP(2);
    END;

    LOCAL PROCEDURE CheckTemplateName@3(CurrentJnlTemplateName@1000 : Code[10];VAR CurrentJnlBatchName@1001 : Code[10]);
    VAR
      ResJnlBatch@1002 : Record 236;
    BEGIN
      ResJnlBatch.SETRANGE("Journal Template Name",CurrentJnlTemplateName);
      IF NOT ResJnlBatch.GET(CurrentJnlTemplateName,CurrentJnlBatchName) THEN BEGIN
        IF NOT ResJnlBatch.FINDFIRST THEN BEGIN
          ResJnlBatch.INIT;
          ResJnlBatch."Journal Template Name" := CurrentJnlTemplateName;
          ResJnlBatch.SetupNewBatch;
          ResJnlBatch.Name := Text004;
          ResJnlBatch.Description := Text005;
          ResJnlBatch.INSERT(TRUE);
          COMMIT;
        END;
        CurrentJnlBatchName := ResJnlBatch.Name;
      END;
    END;

    [External]
    PROCEDURE CheckName@4(CurrentJnlBatchName@1000 : Code[10];VAR ResJnlLine@1001 : Record 207);
    VAR
      ResJnlBatch@1002 : Record 236;
    BEGIN
      ResJnlBatch.GET(ResJnlLine.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName);
    END;

    [External]
    PROCEDURE SetName@5(CurrentJnlBatchName@1000 : Code[10];VAR ResJnlLine@1001 : Record 207);
    BEGIN
      ResJnlLine.FILTERGROUP := 2;
      ResJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      ResJnlLine.FILTERGROUP := 0;
      IF ResJnlLine.FIND('-') THEN;
    END;

    [External]
    PROCEDURE LookupName@6(VAR CurrentJnlBatchName@1000 : Code[10];VAR ResJnlLine@1001 : Record 207);
    VAR
      ResJnlBatch@1002 : Record 236;
    BEGIN
      COMMIT;
      ResJnlBatch."Journal Template Name" := ResJnlLine.GETRANGEMAX("Journal Template Name");
      ResJnlBatch.Name := ResJnlLine.GETRANGEMAX("Journal Batch Name");
      ResJnlBatch.FILTERGROUP(2);
      ResJnlBatch.SETRANGE("Journal Template Name",ResJnlBatch."Journal Template Name");
      ResJnlBatch.FILTERGROUP(0);
      IF PAGE.RUNMODAL(0,ResJnlBatch) = ACTION::LookupOK THEN BEGIN
        CurrentJnlBatchName := ResJnlBatch.Name;
        SetName(CurrentJnlBatchName,ResJnlLine);
      END;
    END;

    [External]
    PROCEDURE GetRes@7(ResNo@1000 : Code[20];VAR ResName@1001 : Text[100]);
    VAR
      Res@1002 : Record 156;
    BEGIN
      IF ResNo <> OldResNo THEN BEGIN
        ResName := '';
        IF ResNo <> '' THEN
          IF Res.GET(ResNo) THEN
            ResName := Res.Name;
        OldResNo := ResNo;
      END;
    END;

    BEGIN
    END.
  }
}

