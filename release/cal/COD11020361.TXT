OBJECT Codeunit 11020361 SAF-T XML Import
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVNO14.04 (10671),4PSSE;
  }
  PROPERTIES
  {
    TableNo=11072496;
    OnRun=VAR
            Window@1100285100 : Dialog;
          BEGIN
            IF GUIALLOWED() THEN
              Window.OPEN(ImportingSourceForMappingLbl);
            ImportMappingData(Rec);
            ImportStandardVATCodes();
            IF GUIALLOWED() THEN
              Window.CLOSE();
          END;

  }
  CODE
  {
    VAR
      ImportingSourceForMappingLbl@1100285100 : TextConst 'ENU=Importing source for mapping...;NOR=Importerer kilde for tilordning...';
      NotPossibleToFindXMLFilesForMappingTypeErr@1100285101 : TextConst '@@@="%1 = a mapping type, one of the following - SAF-T Standard Account, Income Statement";ENU=Not possible to find the XML files for mapping type %1;NOR=Det er ikke mulig † finne XML-filene for tilordningstypen %1';
      NotPossibleToParseMappingXMLFileErr@1100285102 : TextConst '@@@="%1 = a mapping type, one of the following - SAF-T Standard Account, Income Statement";ENU=Not possible to parse XML file with %1 for mapping;NOR=Ikke mulig † analysere XML-filen med %1 for tilordning';
      MappingFileNotLoadedErr@1100285103 : TextConst 'ENU=Mapping source file %1 was not loaded. Choose the Import the Source Files For Mapping action and import one or more mapping source files.;NOR=Tilordningskildefilen %1 er ikke lastet inn. Velg handlingen Importer kildefilene for tilordning, og importer ‚n eller flere tilordningskildefiler.';
      NoMappingSourceIdentifiedErr@1100285104 : TextConst 'ENU=Not possible to identify mapping source type %1. Open a SAF-T Mapping Source page and import source file with such type;NOR=Det er ikke mulig † identifisere tilordningskildetype %1. pne siden SAF-T-tilordningskilde, og importer kildefil med denne typen';
      SAFTStandardAccountsTxt@1100285105 : TextConst 'ENU=SAF-T Standard Accounts;NOR=SAF-T-standardkontoer';
      SAFTGroupingCodesTxt@1100285106 : TextConst 'ENU=SAF-T Grouping Codes;NOR=SAF-T-grupperinger';
      SAFTTaxCodeTxt@1100285107 : TextConst 'ENU=SAF-T Tax Codes;NOR=SAF-T-mva-koder';
      SelectMappingTxt@1100285108 : TextConst 'ENU=Select an XML file with SAF-T codes for mapping;NOR=Velg en XML-fil med SAF-T-koder for tilordning';
      NATxt@1100285109 : TextConst 'ENU=NA;NOR=NA';
      NotApplicableTxt@1100285110 : TextConst 'ENU=Not applicable;NOR=Ikke i bruk;SVE=Ej till„mpbart';
      AssetsLbl@1100285111 : TextConst 'ENU=Assets;NOR=Aktiva;SVE=TILLGNGAR';
      LiabilityAndCapitalLbl@1100285112 : TextConst 'ENU=Liability and capital;NOR=Gjeld og kapital';
      RevenueLbl@1100285113 : TextConst 'ENU=Revenue;NOR=Inntekt;SVE=Int„kter';
      PurchaseLbl@1100285114 : TextConst 'ENU=Purchase;NOR=Kj›p;SVE=Ink”p';
      SaleriesAndHRCostsLbl@1100285115 : TextConst 'ENU=Salaries and HR Costs;NOR=L›nns- og personalkostnader';
      OtherCostsLbl@1100285116 : TextConst 'ENU=Other costs;NOR=Andre kostnader;SVE="™vriga kostnader "';
      FinanceLbl@1100285117 : TextConst 'ENU=Finance;NOR=Finans;SVE=Finans';
      CannotFindCategotyForStdAccErr@1100285118 : TextConst '@@@=%1 - standard account no.;ENU=Not possible to find category for standard account %1;NOR=Det er ikke mulig † finne kategori for standardkonto %1';
      MediaResourceNoContentErr@1100285119 : TextConst '@@@=File name, like GeneralLedgerAccounts.xml;ENU=Media resource %1 has not content. Open the SAF-T Mapping Source page and choose the Update action on selected media resource code.;NOR=Medieressurs %1 har ikke innhold. pne siden SAF-T-tilordningskilde, og velg Oppdater-handlingen p† valgt medieressurskode.';

    LOCAL PROCEDURE ImportMappingData@1100285103(SAFTMappingRange@1100285100 : Record 11072496);
    VAR
      TempMediaResources@1100285101 : TEMPORARY Record 2000000182;
      TempXMLBuffer@1100285102 : TEMPORARY Record 1235;
    BEGIN
      CopyMediaResourceToTempFromMappingSources(TempMediaResources, SAFTMappingRange.GetSAFTMappingSourceTypeByMappingType(), FALSE);
      IF NOT TempMediaResources.FINDSET() THEN
        ERROR(NotPossibleToFindXMLFilesForMappingTypeErr, FORMAT(SAFTMappingRange."Mapping Type"));
      REPEAT
        FillXMLBufferFromMediaResource(TempXMLBuffer, TempMediaResources);
        CASE SAFTMappingRange."Mapping Type" OF
            SAFTMappingRange."Mapping Type"::"Two Digit Standard Account", SAFTMappingRange."Mapping Type"::"Four Digit Standard Account":
                ImportStandardAccountsFromXMLBuffer(TempXMLBuffer, SAFTMappingRange."Mapping Type");
            SAFTMappingRange."Mapping Type"::"Income Statement":
                ImportGroupingCodesFromXMLBuffer(TempXMLBuffer);
        END;
      UNTIL TempMediaResources.NEXT() = 0;
    END;

    PROCEDURE ImportFromMappingSource@1100285104(SAFTMappingSource@1100285100 : Record 11072497);
    VAR
      TempMediaResources@1100285102 : TEMPORARY Record 2000000182;
      TempXMLBuffer@1100285101 : TEMPORARY Record 1235;
    BEGIN
      CopyMediaResourceToTempFromMappingSource(TempMediaResources, SAFTMappingSource);
      FillXMLBufferFromMediaResource(TempXMLBuffer, TempMediaResources);
      CASE SAFTMappingSource."Source Type" OF
        SAFTMappingSource."Source Type"::"Two Digit Standard Account", SAFTMappingSource."Source Type"::"Four Digit Standard Account":
            ImportStandardAccountsFromXMLBuffer(TempXMLBuffer, GetMappingTypeBySourceType(SAFTMappingSource."Source Type"));
        SAFTMappingSource."Source Type"::"Income Statement":
            ImportGroupingCodesFromXMLBuffer(TempXMLBuffer);
        SAFTMappingSource."Source Type"::"Standard Tax Code":
            ImportStandardVATCodesFromXMLBuffer(TempXMLBuffer);
      END;
    END;

    PROCEDURE ImportStandardVATCodes@1100285105();
    VAR
      TempMediaResources@1100285101 : TEMPORARY Record 2000000182;
      TempXMLBuffer@1100285100 : TEMPORARY Record 1235;
      SAFTSetup@1100285102 : Record 11072490;
      SAFTMappingSourceType@1100285103 : 'None,Two Digit Standard Account,Four Digit Standard Account,Income Statement,Standard Tax Code';
    BEGIN
      CopyMediaResourceToTempFromMappingSources(TempMediaResources, SAFTMappingSourceType::"Standard Tax Code", FALSE);
      FillXMLBufferFromMediaResource(TempXMLBuffer, TempMediaResources);
      ImportStandardVATCodesFromXMLBuffer(TempXMLBuffer);
      SAFTSetup.GET();
      SAFTSetup.VALIDATE("Not Applicable VAT Code", InsertNotApplicableVATCode());
      SAFTSetup.MODIFY(TRUE);
    END;

    PROCEDURE MappingSourceLoaded@1100285106(SAFTMappingRange@1100285100 : Record 11072496) : Boolean;
    VAR
      TempMediaResources@1100285102 : TEMPORARY Record 2000000182;
      SAFTMappingSourceType@1100285104 : 'None,Two Digit Standard Account,Four Digit Standard Account,Income Statement,Standard Tax Code';
      MappingType@1100285103 : Integer;
    BEGIN
      EXIT(
          CopyMediaResourceToTempFromMappingSources(TempMediaResources, SAFTMappingRange.GetSAFTMappingSourceTypeByMappingType, TRUE) AND
          CopyMediaResourceToTempFromMappingSources(TempMediaResources, SAFTMappingSourceType::"Standard Tax Code", TRUE));

      // FOR MappingType := SAFTMappingRange."Mapping Type"::"Two Digit Standard Account" TO SAFTMappingRange."Mapping Type"::"Income Statement" DO
      //    CopyMediaResourceToTempFromMappingSources(TempMediaResources, MappingType, TRUE);
      // CopyMediaResourceToTempFromMappingSources(TempMediaResources, SAFTMappingSourceType::"Standard Tax Code", TRUE);
      // EXIT(NOT TempMediaResources.ISEMPTY());
    END;

    LOCAL PROCEDURE ImportStandardVATCodesFromXMLBuffer@1100285107(VAR TempXMLBuffer@1100285100 : Record 1235);
    VAR
      TempChildXMLBuffer@1100285101 : TEMPORARY Record 1235;
      VATCode@1100285102 : Record 11128501;
    BEGIN
      IF NOT TempXMLBuffer.FindNodesByXPath(TempXMLBuffer, '/StandardTaxCodes/TaxCode') THEN
        ERROR(NotPossibleToParseMappingXMLFileErr, SAFTTaxCodeTxt);
      REPEAT
        IF NOT TempXMLBuffer.HasChildNodes() THEN
            ERROR(NotPossibleToParseMappingXMLFileErr, SAFTTaxCodeTxt);
        TempXMLBuffer.FindChildElements(TempChildXMLBuffer);
        VATCode.INIT();
        VATCode.Code := COPYSTR(TempChildXMLBuffer.Value, 1, MAXSTRLEN(VATCode.Code));
        TempChildXMLBuffer.NEXT();
        VATCode.Description := COPYSTR(TempChildXMLBuffer.Value, 1, MAXSTRLEN(VATCode.Description));
        TempChildXMLBuffer.NEXT(); // skip eng description
        TempChildXMLBuffer.NEXT();
        IF TempChildXMLBuffer.Name = 'TaxRate' THEN
            TempChildXMLBuffer.NEXT();
        IF TempChildXMLBuffer.Name = 'Compensation' THEN
            EVALUATE(VATCode.Compensation, TempChildXMLBuffer.Value);
        IF VATCode.INSERT() THEN;
      UNTIL TempXMLBuffer.NEXT() = 0;
    END;

    LOCAL PROCEDURE ImportStandardAccountsFromXMLBuffer@1100285108(VAR TempXMLBuffer@1100285100 : TEMPORARY Record 1235;MappingType@1100285101 : Integer);
    VAR
      TempChildXMLBuffer@1100285102 : TEMPORARY Record 1235;
      SAFTMapping@1100285103 : Record 11072492;
      StdAccNo@1100285104 : Code[20];
    BEGIN
      IF NOT TempXMLBuffer.FindNodesByXPath(TempXMLBuffer, '/StandardAccounts/Account') THEN
        ERROR(NotPossibleToParseMappingXMLFileErr, SAFTStandardAccountsTxt);
      REPEAT
        IF NOT TempXMLBuffer.HasChildNodes() THEN
            ERROR(NotPossibleToParseMappingXMLFileErr, SAFTStandardAccountsTxt);
        TempXMLBuffer.FindChildElements(TempChildXMLBuffer);
        IF (STRLEN(TempChildXMLBuffer.Value) > 4) THEN
            ERROR('Standard account id must not has length more than 4 chars');
        StdAccNo := COPYSTR(TempChildXMLBuffer.Value, 1, MAXSTRLEN(SAFTMapping."No."));
        SAFTMapping.INIT();
        SAFTMapping."Mapping Type" := MappingType;
        SAFTMapping."Category No." := TryInsertCategoryForStandardAccount(SAFTMapping."Mapping Type", StdAccNo);
        SAFTMapping."No." := StdAccNo;
        TempChildXMLBuffer.NEXT();
        SAFTMapping.Description := COPYSTR(TempChildXMLBuffer.Value, 1, MAXSTRLEN(SAFTMapping.Description));
        IF SAFTMapping.INSERT() THEN;
      UNTIL TempXMLBuffer.NEXT() = 0;
      InsertNotApplicationMappingCode(MappingType);
    END;

    LOCAL PROCEDURE InsertNotApplicationMappingCode@1100285109(MappingType@1100285100 : Integer);
    VAR
      SAFTMapping@1100285101 : Record 11072492;
    BEGIN
      SAFTMapping.INIT();
      SAFTMapping."Mapping Type" := MappingType;
      SAFTMapping."Category No." := TryInsertSAFTMappingCategory(MappingType, NATxt, NotApplicableTxt);
      SAFTMapping."No." := NATxt;
      SAFTMapping.Description := NotApplicableTxt;
      IF SAFTMapping.INSERT() THEN;
    END;

    LOCAL PROCEDURE InsertNotApplicableVATCode@1100285110() : Code[10];
    VAR
      VATCode@1100285100 : Record 11128501;
    BEGIN
      VATCode.INIT();
      VATCode.Code := NATxt;
      VATCode.Description := NotApplicableTxt;
      IF NOT VATCode.INSERT() THEN;
      EXIT(VATCode.Code)
    END;

    LOCAL PROCEDURE ImportGroupingCodesFromXMLBuffer@1100285111(VAR TempXMLBuffer@1100285100 : TEMPORARY Record 1235);
    VAR
      SAFTMappingCategory@1100285101 : Record 11072491;
      SAFTMapping@1100285102 : Record 11072492;
      TempChildXMLBuffer@1100285103 : TEMPORARY Record 1235;
      CategoryCode@1100285104 : Code[20];
    BEGIN
      IF NOT TempXMLBuffer.FindNodesByXPath(TempXMLBuffer, '/GroupingCategoryCode/Account') THEN
        ERROR(NotPossibleToParseMappingXMLFileErr, SAFTGroupingCodesTxt);
      REPEAT
        IF NOT TempXMLBuffer.HasChildNodes() THEN
            ERROR(NotPossibleToParseMappingXMLFileErr, SAFTGroupingCodesTxt);
        TempXMLBuffer.FindChildElements(TempChildXMLBuffer);
        CategoryCode := COPYSTR(TempChildXMLBuffer.Value, 1, MAXSTRLEN(CategoryCode));
        IF CategoryCode <> SAFTMappingCategory."No." THEN BEGIN
          SAFTMappingCategory.INIT();
          SAFTMappingCategory."Mapping Type" := SAFTMappingCategory."Mapping Type"::"Income Statement";
          SAFTMappingCategory."No." := CategoryCode;
          TempChildXMLBuffer.NEXT();
          SAFTMappingCategory.Description := COPYSTR(TempChildXMLBuffer.Value, 1, MAXSTRLEN(SAFTMappingCategory.Description));
          IF NOT SAFTMappingCategory.INSERT() THEN
              SAFTMappingCategory.MODIFY();
        END ELSE
          TempChildXMLBuffer.NEXT();

        SAFTMapping.INIT();
        SAFTMapping."Mapping Type" := SAFTMapping."Mapping Type"::"Income Statement";
        SAFTMapping."Category No." := SAFTMappingCategory."No.";
        TempChildXMLBuffer.NEXT();
        IF TempChildXMLBuffer.Name = 'CategoryDescription' THEN
            TempChildXMLBuffer.NEXT();
        SAFTMapping."No." := COPYSTR(TempChildXMLBuffer.Value, 1, MAXSTRLEN(SAFTMapping."No."));
        TempChildXMLBuffer.NEXT();
        SAFTMapping.Description := COPYSTR(TempChildXMLBuffer.Value, 1, MAXSTRLEN(SAFTMapping.Description));
        IF NOT SAFTMapping.INSERT() THEN
            SAFTMapping.MODIFY();
      UNTIL TempXMLBuffer.NEXT() = 0;
      InsertNotApplicationMappingCode(SAFTMapping."Mapping Type");
    END;

    LOCAL PROCEDURE CopyMediaResourceToTempFromMappingSources@1100285112(VAR TempMediaResource@1100285100 : TEMPORARY Record 2000000182;SAFTMappingSourceType@1100285101 : Integer;CheckOnly@1100285102 : Boolean) MappingSourceFileLoaded : Boolean;
    VAR
      MediaResources@1100285103 : Record 2000000182;
      SAFTMappingSource@1100285104 : Record 11072497;
    BEGIN
      SAFTMappingSource.SETRANGE("Source Type", SAFTMappingSourceType);
      IF NOT SAFTMappingSource.FINDSET() THEN BEGIN
        IF CheckOnly THEN
          EXIT(FALSE);
        ERROR(NoMappingSourceIdentifiedErr, FORMAT(SAFTMappingSourceType));
      END;
      REPEAT
        MappingSourceFileLoaded := FALSE;
        IF MediaResources.GET(SAFTMappingSource."Source No.") THEN BEGIN
          MediaResources.CALCFIELDS(Blob);
          IF MediaResources.Blob.HASVALUE THEN BEGIN
            TempMediaResource.INIT();
            TempMediaResource := MediaResources;
            IF NOT TempMediaResource.INSERT THEN TempMediaResource.MODIFY;
            MappingSourceFileLoaded := TRUE;
          END;
        END;
        IF (NOT MappingSourceFileLoaded) AND (NOT CheckOnly) THEN
          ERROR(MappingFileNotLoadedErr, SAFTMappingSource."Source No.");
      UNTIL SAFTMappingSource.NEXT() = 0;
      EXIT(MappingSourceFileLoaded);
    END;

    LOCAL PROCEDURE CopyMediaResourceToTempFromMappingSource@1100285113(VAR TempMediaResource@1100285100 : TEMPORARY Record 2000000182;SAFTMappingSource@1100285101 : Record 11072497);
    VAR
      MediaResources@1100285102 : Record 2000000182;
    BEGIN
      IF NOT MediaResources.GET(SAFTMappingSource."Source No.") THEN
        ERROR(MappingFileNotLoadedErr, SAFTMappingSource."Source No.");
      MediaResources.CALCFIELDS(Blob);
      TempMediaResource.INIT();
      TempMediaResource := MediaResources;
      TempMediaResource.INSERT();
    END;

    LOCAL PROCEDURE FillXMLBufferFromMediaResource@1100285114(VAR TempXMLBuffer@1100285100 : TEMPORARY Record 1235;VAR TempMediaResources@1100285101 : TEMPORARY Record 2000000182);
    VAR
      MediaResources@1100285102 : Record 2000000182;
      InStream@1100285103 : InStream;
    BEGIN
      MediaResources.GET(TempMediaResources.Code);
      MediaResources.CALCFIELDS(Blob);
      IF NOT MediaResources.Blob.HASVALUE() THEN
        ERROR(MediaResourceNoContentErr);
      MediaResources.Blob.CREATEINSTREAM(InStream);
      TempXMLBuffer.RESET();
      TempXMLBuffer.DELETEALL();
      TempXMLBuffer.LoadFromStream(InStream);
    END;

    PROCEDURE ImportXmlFileIntoMediaResources@1100285115(VAR MediaResources@1100285100 : Record 2000000182);
    VAR
      TempBlob@1100285101 : Record 99008535;
      FileManagement@1100285102 : Codeunit 419;
      ImportFileInStream@1100285103 : InStream;
      ImportFileOutStream@1100285104 : OutStream;
      ClientFileName@1100285105 : Text;
    BEGIN
      ClientFileName := FileManagement.BLOBImportWithFilter(TempBlob, SelectMappingTxt, '', 'XML file (*.xml)|*.xml', 'xml');
      IF ClientFileName = '' THEN
        EXIT;

      MediaResources.INIT();
      MediaResources.Code :=
        COPYSTR(FileManagement.GetFileName(ClientFileName), 1, MAXSTRLEN(MediaResources.Code));
      IF MediaResources.FIND() THEN
        MediaResources.DELETE();

      TempBlob.Blob.CREATEINSTREAM(ImportFileInStream);
      MediaResources.Blob.CREATEOUTSTREAM(ImportFileOutStream);
      COPYSTREAM(ImportFileOutStream, ImportFileInStream);
      MediaResources.INSERT();
    END;

    LOCAL PROCEDURE GetMappingTypeBySourceType@1100285116(SourceType@1100285100 : 'None,Two Digit Standard Account,Four Digit Standard Account,Income Statement') MappingType : Integer;
    BEGIN
      CASE SourceType OF
        SourceType::"Two Digit Standard Account":
            EXIT(1);
        SourceType::"Four Digit Standard Account":
            EXIT(2);
      END;
    END;

    LOCAL PROCEDURE TryInsertCategoryForStandardAccount@1100285117(MappingType@1100285100 : Integer;StdAccNo@1100285101 : Code[20]) : Code[20];
    VAR
      CategoryNo@1100285102 : Code[20];
    BEGIN
      CategoryNo := COPYSTR(StdAccNo, 1, 1);
      CASE CategoryNo OF
        '1':
            EXIT(TryInsertSAFTMappingCategory(MappingType, CategoryNo, AssetsLbl));
        '2':
            EXIT(TryInsertSAFTMappingCategory(MappingType, CategoryNo, LiabilityAndCapitalLbl));
        '3':
            EXIT(TryInsertSAFTMappingCategory(MappingType, CategoryNo, RevenueLbl));
        '4':
            EXIT(TryInsertSAFTMappingCategory(MappingType, CategoryNo, PurchaseLbl));
        '5':
            EXIT(TryInsertSAFTMappingCategory(MappingType, CategoryNo, SaleriesAndHRCostsLbl));
        '6', '7':
            EXIT(TryInsertSAFTMappingCategory(MappingType, CategoryNo, OtherCostsLbl));
        '8':
            EXIT(TryInsertSAFTMappingCategory(MappingType, CategoryNo, FinanceLbl));
        ELSE
            ERROR(CannotFindCategotyForStdAccErr, StdAccNo);
      END;
    END;

    LOCAL PROCEDURE TryInsertSAFTMappingCategory@1100285118(MappingType@1100285100 : Integer;CategoryNo@1100285101 : Code[20];Description@1100285102 : Text[100]) : Code[20];
    VAR
      SAFTMappingCategory@1100285103 : Record 11072491;
    BEGIN
      SAFTMappingCategory.INIT();
      SAFTMappingCategory."Mapping Type" := MappingType;
      SAFTMappingCategory."No." := CategoryNo;
      SAFTMappingCategory.Description := Description;
      IF NOT SAFTMappingCategory.INSERT() THEN
        SAFTMappingCategory.MODIFY();
      EXIT(SAFTMappingCategory."No.");
    END;

    BEGIN
    {
      191003 ORANGO.DL RFC194 implement SAF-T from BC14 CU04 (NAVNO14.04) NorwegianSAFT-extension
      191112 ORANGO.DL updates from BC14.CU06
    }
    END.
  }
}

