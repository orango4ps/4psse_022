OBJECT Codeunit 11012593 External Transport Planning WS
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      ExtTransportPlanningMgt@1100529000 : Codeunit 11012592;

    [External]
    PROCEDURE ConfirmTransportRequest@1100525002(ShippingAgent@1100525002 : Code[10];IdentificationCode@1100525001 : Code[50];PlantOrderNo@1100525003 : Code[20];ReferenceExternalTransport@1100525004 : Text[30]) : Boolean;
    VAR
      PlantOrder@1100525005 : Record 11012556;
      ActionBy@1100529000 : Text;
    BEGIN
      WITH PlantOrder DO BEGIN
        CheckCommon(ShippingAgent, IdentificationCode, PlantOrderNo, TRUE, ActionBy);
        GET(PlantOrderNo);
        TESTFIELD("Transport Status (Ext.)", "Transport Status (Ext.)"::Request);
        "Transport Status (Ext.)" := "Transport Status (Ext.)"::Requested;
        "Reference External Transport" := ReferenceExternalTransport;
        TESTFIELD("Reference External Transport");
        MODIFY;
        ExtTransportPlanningMgt.UpdateLogbook("No.", "Transport Status (Ext.)", ActionBy, FALSE);
        EXIT(TRUE);
      END;
    END;

    [External]
    PROCEDURE ConfirmTransportUpdate@1100525003(ShippingAgent@1100525002 : Code[10];IdentificationCode@1100525001 : Code[50];PlantOrderNo@1100525000 : Code[20];ReferenceExternalTransport@1100525006 : Text[30]) : Boolean;
    VAR
      PlantOrder@1100525004 : Record 11012556;
      ActionBy@1100529000 : Text;
    BEGIN
      WITH PlantOrder DO BEGIN
        CheckCommon(ShippingAgent, IdentificationCode, PlantOrderNo, TRUE, ActionBy);
        GET(PlantOrderNo);
        TESTFIELD("Transport Status (Ext.)", "Transport Status (Ext.)"::Update);
        "Transport Status (Ext.)" := "Transport Status (Ext.)"::Updated;
        "Reference External Transport" := ReferenceExternalTransport;
        TESTFIELD("Reference External Transport");
        MODIFY;
        ExtTransportPlanningMgt.UpdateLogbook("No.", "Transport Status (Ext.)", ActionBy, FALSE);
        EXIT(TRUE);
      END;
    END;

    [External]
    PROCEDURE ConfirmTransportCancel@1100525004(ShippingAgent@1100525002 : Code[10];IdentificationCode@1100525001 : Code[50];PlantOrderNo@1100525003 : Code[20]) : Boolean;
    VAR
      PlantOrder@1100525005 : Record 11012556;
      ActionBy@1100529000 : Text;
    BEGIN
      WITH PlantOrder DO BEGIN
        CheckCommon(ShippingAgent, IdentificationCode, PlantOrderNo, TRUE, ActionBy);
        GET(PlantOrderNo);
        TESTFIELD("Transport Status (Ext.)", "Transport Status (Ext.)"::Cancel);
        "Transport Status (Ext.)" := "Transport Status (Ext.)"::Canceled;
        MODIFY;
        ExtTransportPlanningMgt.UpdateLogbook("No.", "Transport Status (Ext.)", ActionBy, FALSE);
        EXIT(TRUE);
      END;
    END;

    [External]
    PROCEDURE ConfirmTransportFinished@1100529002(ShippingAgent@1100525002 : Code[10];IdentificationCode@1100525001 : Code[50];PlantOrderNo@1100525003 : Code[20]) : Boolean;
    VAR
      PlantOrder@1100525005 : Record 11012556;
      ActionBy@1100529000 : Text;
    BEGIN
      WITH PlantOrder DO BEGIN
        CheckCommon(ShippingAgent, IdentificationCode, PlantOrderNo, FALSE, ActionBy);
        GET(PlantOrderNo);
        IF NOT ("Transport Status (Ext.)" IN
          ["Transport Status (Ext.)"::Requested, "Transport Status (Ext.)"::Updated, "Transport Status (Ext.)"::Finished])
        THEN
          FIELDERROR("Transport Status (Ext.)");
        IF "Transport Status (Ext.)" <> "Transport Status (Ext.)"::Finished THEN BEGIN
          "Transport Status (Ext.)" := "Transport Status (Ext.)"::Finished;
          MODIFY;
        END;
        ExtTransportPlanningMgt.UpdateLogbook("No.", "Transport Status (Ext.)", ActionBy, FALSE);
        EXIT(TRUE);
      END;
    END;

    LOCAL PROCEDURE CheckCommon@1100529003(ShippingAgent@1100529003 : Code[10];IdentificationCode@1100529006 : Code[50];PlantOrderNo@1100529000 : Code[20];CheckNotPosted@1100529001 : Boolean;VAR ActionBy@1100529005 : Text);
    VAR
      PlantOrder@1100529002 : Record 11012556;
      ShipAgent@1100529004 : Record 291;
    BEGIN
      ActionBy := '';
      ExtTransportPlanningMgt.CheckIdentificationCodeWS(ShippingAgent, IdentificationCode);
      WITH PlantOrder DO BEGIN
        GET(PlantOrderNo);
        TESTFIELD("No.");
        IF CheckNotPosted THEN
          TESTFIELD(Posted, FALSE);
        TESTFIELD("Shipping Agent", ShippingAgent);
        ShipAgent.GET(ShippingAgent);
        ActionBy := ShipAgent.Name; //Use name in logbook, so not UserID of webservice
      END;
    END;

    BEGIN
    END.
  }
}

