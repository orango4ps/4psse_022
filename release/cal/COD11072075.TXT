OBJECT Codeunit 11072075 XML File Import Mgt (SiB)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      SiBSetup@1100525000 : Record 11072650;
      FileManagement@1100525005 : Codeunit 419;
      ProcessType@1100525008 : Integer;
      Text000@1100525002 : TextConst 'DEU=Art     #1##################\Datei  #2##################\Fortschritt #3# von#4#;ENU=Type     #1##################\File     #2##################\Progress #3# of#4#;NLD=Soort     #1##################\Bestand   #2##################\Voortgang #3# van#4#;NOR=Skriv     #1##################\Fil     #2##################\Fremdrift #3# av#4#;SVE=Typ     #1##################\Fil     #2##################\F”rlopp #3# av#4#';
      Text001@1100525006 : TextConst 'DEU=Fr ''%1'' ist keine zu importierende Datei (%2) vorhanden. šberprfen Sie %3 bei %4.;ENU=For ''%1'', no import file (%2) present. Check %3 for %4.;NLD=Voor ''%1'', geen te importeren bestand (%2) aanwezig.Controleer %3 bij %4.;NOR=For ''%1'' finnes ingen importfil (%2). Kontroller %3 for %4.;SVE=F”r ''%1'' finns ingen importfil (%2). Kontrollera %3 f”r %4.';
      ProcessName@1100525001 : Text[100];
      FileExtension@1100525003 : Text[5];
      FirstNewDocNo@1100525004 : Code[20];

    PROCEDURE ProcessImportTradeItems@1100525001(iProcessName@1100525000 : Text[100]);
    VAR
      lvTypeNo@1100525001 : Integer;
    BEGIN
      ProcessType := DATABASE::"Import Trade Item";  //DP00065
      ProcessName := iProcessName;
      FileExtension := 'xml';

      SiBSetup.GET;
      SiBSetup.TESTFIELD("Folder Trade Items");
      SiBSetup.TESTFIELD("Folder Processed Trade Items");
      SiBSetup.CheckDirectory(
        SiBSetup.FIELDCAPTION("Folder Trade Items"), SiBSetup."Folder Trade Items");
      SiBSetup.CheckDirectory(
        SiBSetup.FIELDCAPTION("Folder Processed Trade Items"), SiBSetup."Folder Processed Trade Items");

      CASE SiBSetup."File Type Trade Items" OF
        SiBSetup."File Type Trade Items"::CSV:
          FileExtension := 'csv';
        SiBSetup."File Type Trade Items"::Both:
          BEGIN
            lvTypeNo := STRMENU('XML,CSV');
            IF lvTypeNo = 0 THEN
              ERROR('');
            IF lvTypeNo = 2 THEN
              FileExtension := 'csv';
          END;
      END;

      IF NOT GUIALLOWED THEN BEGIN
        IF FileManagement.ClientFolderIsEmpty(SiBSetup."Folder Trade Items", '*.' + FileExtension) THEN
          EXIT;
      END;

      ProcessImportXMLFiles(SiBSetup."Folder Trade Items", SiBSetup."Folder Processed Trade Items");
    END;

    PROCEDURE ProcessImportDispatchNotes@1100525007(iProcessName@1100525000 : Text[100]);
    BEGIN
      ProcessType := DATABASE::"Dispatch Note SiB";
      ProcessName := iProcessName;
      FileExtension := 'xml';

      SiBSetup.GET;
      SiBSetup.TESTFIELD("Folder Dispatch Notes");
      SiBSetup.TESTFIELD("Folder Processed Disp.Notes");
      SiBSetup.CheckDirectory(
        SiBSetup.FIELDCAPTION("Folder Dispatch Notes"), SiBSetup."Folder Dispatch Notes");
      SiBSetup.CheckDirectory(
        SiBSetup.FIELDCAPTION("Folder Processed Disp.Notes"), SiBSetup."Folder Processed Disp.Notes");

      ProcessImportXMLFiles(SiBSetup."Folder Dispatch Notes", SiBSetup."Folder Processed Disp.Notes");
    END;

    PROCEDURE ProcessImportPurchInvoices@1100525008(iProcessName@1100525000 : Text[100]);
    BEGIN
      ProcessType := DATABASE::"Invoice SiB";
      ProcessName := iProcessName;
      FileExtension := 'xml';

      SiBSetup.GET;
      SiBSetup.TESTFIELD("Folder Purchase Invoices");
      SiBSetup.TESTFIELD("Folder Processed Purch.Inv.");
      SiBSetup.CheckDirectory(
        SiBSetup.FIELDCAPTION("Folder Purchase Invoices"), SiBSetup."Folder Purchase Invoices");
      SiBSetup.CheckDirectory(
        SiBSetup.FIELDCAPTION("Folder Processed Purch.Inv."), SiBSetup."Folder Processed Purch.Inv.");

      ProcessImportXMLFiles(SiBSetup."Folder Purchase Invoices", SiBSetup."Folder Processed Purch.Inv.");
    END;

    PROCEDURE ProcessImportXMLFiles@1100485010(iPathImport@1100525001 : Text[250];iPathProcessed@1100525000 : Text[250]);
    VAR
      lvExchangeMgtCU@1100525002 : Codeunit 11072076;
      lvWindow@1100525003 : Dialog;
      lvCounter@1100525005 : Integer;
      lvGLNVendDocNo@1100525006 : Code[50];
      lvOldFileName@1100409000 : Text[1024];
      lvNewFileName@1100525007 : Text[1024];
      FileNames@1100525010 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Array";
      FilterString@1100525009 : Text;
      I@1100525008 : Integer;
    BEGIN
      FirstNewDocNo := '';

      IF FileManagement.ClientFolderIsEmpty(iPathImport, '*.' + FileExtension) THEN
        ERROR(Text001, ProcessName, FileExtension,
          SiBSetup.FIELDCAPTION("Folder Trade Items"), SiBSetup.TABLECAPTION);  //DP00065

      FilterString := FileManagement.GetToFilterText('', '.' + FileExtension);
      IF NOT FileManagement.OpenMultiselectFileDialog('', iPathImport, FilterString, FileNames) THEN
        ERROR('');

      IF FileNames.Length > 0 THEN BEGIN
        lvWindow.OPEN(Text000, ProcessName);
        lvWindow.UPDATE(4, FileNames.Length);
        IF ProcessType = DATABASE::"Import Trade Item" THEN
          CheckImportTradeItem;  //DP00065
        FOR I := 0 TO FileNames.Length - 1 DO BEGIN
          lvCounter := lvCounter + 1;
          lvWindow.UPDATE(2, FileManagement.GetFileName(FileNames.GetValue(I)));
          lvWindow.UPDATE(3, lvCounter);
          IF FileManagement.ClientFileExists(FileNames.GetValue(I)) THEN BEGIN
            CASE ProcessType OF
              DATABASE::"Import Trade Item":
                IF FileExtension <> 'csv' THEN  //db, 15-06-15: C023991
                  lvGLNVendDocNo := lvExchangeMgtCU.ProcessXMLFileTradeItems(FileNames.GetValue(I))
                ELSE
                  lvGLNVendDocNo := lvExchangeMgtCU.ProcessCSVFileTradeItems(FileNames.GetValue(I));
              DATABASE::"Dispatch Note SiB":
                lvGLNVendDocNo := lvExchangeMgtCU.ProcessXMLFileDispatchNote(FileNames.GetValue(I));
              DATABASE::"Invoice SiB":
                lvGLNVendDocNo := lvExchangeMgtCU.ProcessXMLFileInvoice(FileNames.GetValue(I));
            END;
            IF FirstNewDocNo = '' THEN
              FirstNewDocNo := lvExchangeMgtCU.GetNewDocNo;
            COMMIT;
            lvNewFileName := iPathProcessed + lvGLNVendDocNo + '-' + CurrDateTime2Text() + '.' + FileExtension;
            IF FileManagement.ClientFileExists(lvNewFileName) THEN BEGIN
              SLEEP(1010);
              lvNewFileName := iPathProcessed + lvGLNVendDocNo + '-' + CurrDateTime2Text() + '.' + FileExtension;
            END;
            FileManagement.RenameClientFile(FileNames.GetValue(I), lvNewFileName, TRUE);

            //db, 16-09-14
            IF ProcessType = DATABASE::"Invoice SiB" THEN BEGIN
              //lvOldFileName := lvFileRec.Path + COPYSTR(lvFileRec.Name, 1, STRLEN(lvFileRec.Name)-4) + '.pdf';
              lvOldFileName := COPYSTR(FileNames.GetValue(I), 1, STRLEN(FileNames.GetValue(I))-4) + '.pdf';
              IF FileManagement.ClientFileExists(lvOldFileName) THEN BEGIN
                lvNewFileName := iPathProcessed + lvGLNVendDocNo + '.pdf';
                FileManagement.RenameClientFile(lvOldFileName, lvNewFileName, TRUE);
              END;
            END;
            //db, 16-09-14
          END;
        END;
      END;
    END;

    LOCAL PROCEDURE CurrDateTime2Text@1100525000() : Text[30];
    BEGIN
      EXIT(FORMAT(CURRENTDATETIME, 0, '<Year4><Month,2><Day,2>-<Hours24,2><Minutes,2><Seconds,2>'));
    END;

    PROCEDURE GetFirstNewDocNo@1100525003() : Code[20];
    BEGIN
      EXIT(FirstNewDocNo);
    END;

    PROCEDURE CheckImportTradeItem@1100528900();
    VAR
      ImportSetup@1100528900 : Record 11012301;
      ImportTradeItemRec@1100528904 : Record 11012333;
      ImportTradeItemChargeRec@1100528902 : Record 11012740;
      TradeErrorRec@1100528901 : Record 11012337;
    BEGIN
      //DP00065
      ImportSetup.GET;
      IF ImportSetup."Delete Existing Import Data" THEN BEGIN
        ImportTradeItemRec.SETFILTER("Import Type", '<>%1', ImportTradeItemRec."Import Type"::"Web Service");
        ImportTradeItemRec.DELETEALL;
        ImportTradeItemRec.SETRANGE("Import Type");
        ImportTradeItemChargeRec.DELETEALL;
        TradeErrorRec.SETFILTER("Import Type", '<>%1', TradeErrorRec."Import Type"::"Web Service");
        TradeErrorRec.DELETEALL;
        TradeErrorRec.SETRANGE("Import Type");
      END;
    END;

    BEGIN
    {
      db, 15-06-15: C023991
      - rename of table 'Import Trade Item' (11012333) to 'Trade Item Import Log' resulted in check on table 'Import Trade Item Charge'
    }
    END.
  }
}

