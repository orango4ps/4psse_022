OBJECT Codeunit 7300 Whse. Jnl.-B.Register+Print
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04;
  }
  PROPERTIES
  {
    TableNo=7310;
    OnRun=BEGIN
            WhseJnlBatch.COPY(Rec);
            Code;
            COPY(WhseJnlBatch);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to register the journals?;NOR=Vil du registrere kladdene?;SVE=Vill du registrera journalerna?';
      Text001@1001 : TextConst 'ENU=The journals were successfully registered.;NOR=Kladdene ble registrert.;SVE=Det gick bra att registrera journalerna.';
      Text002@1002 : TextConst 'ENU="It was not possible to register all of the journals. ";NOR="Det var ikke mulig  registrere alle kladdene. ";SVE="Det var inte mjligt att registrera alla journaler. "';
      Text003@1003 : TextConst 'ENU=The journals that were not successfully registered are now marked.;NOR=Kladdene som ikke ble riktig registrert, er n merket.;SVE=Journalerna som inte gick att registrera r nu markerade.';
      WhseJnlTemplate@1004 : Record 7309;
      WhseJnlBatch@1005 : Record 7310;
      WhseJnlLine@1006 : Record 7311;
      WhseReg@1009 : Record 7313;
      WhseJnlRegisterBatch@1007 : Codeunit 7304;
      JnlWithErrors@1008 : Boolean;

    LOCAL PROCEDURE Code@1();
    BEGIN
      WITH WhseJnlBatch DO BEGIN
        WhseJnlTemplate.GET("Journal Template Name");
        WhseJnlTemplate.TESTFIELD("Registering Report ID");

        IF NOT CONFIRM(Text000,FALSE) THEN
          EXIT;

        FIND('-');
        REPEAT
          WhseJnlLine."Journal Template Name" := "Journal Template Name";
          WhseJnlLine."Journal Batch Name" := Name;
          WhseJnlLine."Location Code" := "Location Code";
          WhseJnlLine."Line No." := 10000;
          CLEAR(WhseJnlRegisterBatch);
          IF WhseJnlRegisterBatch.RUN(WhseJnlLine) THEN BEGIN
            OnAfterRegisterBatch(WhseJnlLine);
            MARK(FALSE);
            IF WhseReg.GET(WhseJnlLine."Line No.") THEN BEGIN
              WhseReg.SETRECFILTER;
              REPORT.RUN(WhseJnlTemplate."Registering Report ID",FALSE,FALSE,WhseReg);
            END;
          END ELSE BEGIN
            MARK(TRUE);
            JnlWithErrors := TRUE;
          END;
        UNTIL NEXT = 0;

        IF NOT JnlWithErrors THEN
          MESSAGE(Text001)
        ELSE
          MESSAGE(
            Text002 +
            Text003);

        IF NOT FIND('=><') THEN BEGIN
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Location Code","Location Code");
          FILTERGROUP(0);
          Name := '';
        END;
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterRegisterBatch@2(VAR WarehouseJournalLine@1000 : Record 7311);
    BEGIN
    END;

    BEGIN
    END.
  }
}

