OBJECT Codeunit 1173 Document Attachment Mgmt
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.01,4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    LOCAL PROCEDURE DeleteAttachedDocuments@2(RecRef@1005 : RecordRef);
    VAR
      DocumentAttachment@1002 : Record 1173;
      FieldRef@1000 : FieldRef;
      RecNo@1004 : Code[20];
      DocType@1003 : 'Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order';
      LineNo@1001 : Integer;
    BEGIN
      IF RecRef.ISTEMPORARY THEN
        EXIT;
      IF DocumentAttachment.ISEMPTY THEN
        EXIT;
      DocumentAttachment.SETRANGE("Table ID",RecRef.NUMBER);
      CASE RecRef.NUMBER OF
        DATABASE::Customer,
        DATABASE::Vendor,
        DATABASE::Item,
        DATABASE::Employee,
        DATABASE::"Fixed Asset",
        DATABASE::Resource,
        DATABASE::Job:
          BEGIN
            FieldRef := RecRef.FIELD(1);
            RecNo := FieldRef.VALUE;
            DocumentAttachment.SETRANGE("No.",RecNo);
          END;
      END;
      CASE RecRef.NUMBER OF
        DATABASE::"Sales Header",
        DATABASE::"Purchase Header",
        DATABASE::"Sales Line",
        DATABASE::"Purchase Line":
          BEGIN
            FieldRef := RecRef.FIELD(1);
            DocType := FieldRef.VALUE;
            DocumentAttachment.SETRANGE("Document Type",DocType);

            FieldRef := RecRef.FIELD(3);
            RecNo := FieldRef.VALUE;
            DocumentAttachment.SETRANGE("No.",RecNo);
          END;
      END;
      CASE RecRef.NUMBER OF
        DATABASE::"Sales Line",
        DATABASE::"Purchase Line":
          BEGIN
            FieldRef := RecRef.FIELD(4);
            LineNo := FieldRef.VALUE;
            DocumentAttachment.SETRANGE("Line No.",LineNo);
          END;
      END;
      CASE RecRef.NUMBER OF
        DATABASE::"Sales Invoice Header",
        DATABASE::"Sales Invoice Line",
        DATABASE::"Sales Cr.Memo Header",
        DATABASE::"Sales Cr.Memo Line",
        DATABASE::"Purch. Inv. Header",
        DATABASE::"Purch. Inv. Line",
        DATABASE::"Purch. Cr. Memo Hdr.",
        DATABASE::"Purch. Cr. Memo Line":
          BEGIN
            FieldRef := RecRef.FIELD(3);
            RecNo := FieldRef.VALUE;
            DocumentAttachment.SETRANGE("No.",RecNo);
          END;
      END;
      DocumentAttachment.DELETEALL;
    END;

    [EventSubscriber(Table,18,OnAfterDeleteEvent)]
    LOCAL PROCEDURE DeleteAttachedDocumentsOnAfterDeleteCustomer@4(VAR Rec@1000 : Record 18;RunTrigger@1001 : Boolean);
    VAR
      RecRef@1002 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      DeleteAttachedDocuments(RecRef);
    END;

    [EventSubscriber(Table,36,OnAfterDeleteEvent)]
    LOCAL PROCEDURE DeleteAttachedDocumentsOnAfterDeleteSalesHeader@10(VAR Rec@1000 : Record 36;RunTrigger@1001 : Boolean);
    VAR
      RecRef@1002 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      DeleteAttachedDocuments(RecRef);
    END;

    [EventSubscriber(Table,37,OnAfterDeleteEvent)]
    LOCAL PROCEDURE DeleteAttachedDocumentsOnAfterDeleteSalesLine@16(VAR Rec@1000 : Record 37;RunTrigger@1001 : Boolean);
    VAR
      RecRef@1002 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      DeleteAttachedDocuments(RecRef);
    END;

    [EventSubscriber(Table,114,OnAfterDeleteEvent)]
    LOCAL PROCEDURE DeleteAttachedDocumentsOnAfterDeleteSalesCreditMemoHeader@27(VAR Rec@1000 : Record 114;RunTrigger@1001 : Boolean);
    VAR
      RecRef@1002 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      DeleteAttachedDocuments(RecRef);
    END;

    [EventSubscriber(Table,115,OnAfterDeleteEvent)]
    LOCAL PROCEDURE DeleteAttachedDocumentsOnAfterDeleteSalesCreditMemoLine@32(VAR Rec@1000 : Record 115;RunTrigger@1001 : Boolean);
    VAR
      RecRef@1002 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      DeleteAttachedDocuments(RecRef);
    END;

    [EventSubscriber(Table,112,OnAfterDeleteEvent)]
    LOCAL PROCEDURE DeleteAttachedDocumentsOnAfterDeleteSalesInvoiceHeader@34(VAR Rec@1000 : Record 112;RunTrigger@1001 : Boolean);
    VAR
      RecRef@1002 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      DeleteAttachedDocuments(RecRef);
    END;

    [EventSubscriber(Table,113,OnAfterDeleteEvent)]
    LOCAL PROCEDURE DeleteAttachedDocumentsOnAfterDeleteSalesInvoiceLine@38(VAR Rec@1000 : Record 113;RunTrigger@1001 : Boolean);
    VAR
      RecRef@1002 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      DeleteAttachedDocuments(RecRef);
    END;

    [EventSubscriber(Table,122,OnAfterDeleteEvent)]
    LOCAL PROCEDURE DeleteAttachedDocumentsOnAfterDeletePurchInvHeader@39(VAR Rec@1000 : Record 122;RunTrigger@1001 : Boolean);
    VAR
      RecRef@1002 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      DeleteAttachedDocuments(RecRef);
    END;

    [EventSubscriber(Table,123,OnAfterDeleteEvent)]
    LOCAL PROCEDURE DeleteAttachedDocumentsOnAfterDeletePurchInvLine@40(VAR Rec@1000 : Record 123;RunTrigger@1001 : Boolean);
    VAR
      RecRef@1002 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      DeleteAttachedDocuments(RecRef);
    END;

    [EventSubscriber(Table,124,OnAfterDeleteEvent)]
    LOCAL PROCEDURE DeleteAttachedDocumentsOnAfterDeletePurchCreditMemoHeader@41(VAR Rec@1000 : Record 124;RunTrigger@1001 : Boolean);
    VAR
      RecRef@1002 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      DeleteAttachedDocuments(RecRef);
    END;

    [EventSubscriber(Table,125,OnAfterDeleteEvent)]
    LOCAL PROCEDURE DeleteAttachedDocumentsOnAfterDeletePurchCreditMemoLine@42(VAR Rec@1000 : Record 125;RunTrigger@1001 : Boolean);
    VAR
      RecRef@1002 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      DeleteAttachedDocuments(RecRef);
    END;

    [EventSubscriber(Codeunit,86,OnBeforeDeleteSalesQuote)]
    LOCAL PROCEDURE DocAttachFlowFormSalesQuoteToSalesOrder@1(VAR QuoteSalesHeader@1000 : Record 36;VAR OrderSalesHeader@1001 : Record 36);
    VAR
      FromRecRef@1003 : RecordRef;
      ToRecRef@1002 : RecordRef;
    BEGIN
      // SalesHeader - quote
      // SalesOrderHeader - order
      IF QuoteSalesHeader."No." = '' THEN
        EXIT;

      IF QuoteSalesHeader.ISTEMPORARY THEN
        EXIT;

      IF OrderSalesHeader."No." = '' THEN
        EXIT;

      IF OrderSalesHeader.ISTEMPORARY THEN
        EXIT;

      FromRecRef.OPEN(DATABASE::"Sales Header");
      FromRecRef.GETTABLE(QuoteSalesHeader);

      ToRecRef.OPEN(DATABASE::"Sales Header");
      ToRecRef.GETTABLE(OrderSalesHeader);

      CopyAttachments(FromRecRef,ToRecRef);
    END;

    [EventSubscriber(Codeunit,87,OnBeforeInsertSalesOrderHeader)]
    LOCAL PROCEDURE DocAttachFlowForSalesHeaderFromBlanketOrderToSalesOrder@13(VAR SalesOrderHeader@1000 : Record 36;BlanketOrderSalesHeader@1001 : Record 36);
    VAR
      FromRecRef@1003 : RecordRef;
      ToRecRef@1002 : RecordRef;
      RecRef@1004 : RecordRef;
    BEGIN
      // Invoked when a sales order is created from blanket sales order
      // Need to delete docs that came from customer to sales header and copy docs form blanket sales header
      IF SalesOrderHeader."No." = '' THEN
        EXIT;

      IF SalesOrderHeader.ISTEMPORARY THEN
        EXIT;

      IF BlanketOrderSalesHeader."No." = '' THEN
        EXIT;

      IF BlanketOrderSalesHeader.ISTEMPORARY THEN
        EXIT;

      RecRef.GETTABLE(SalesOrderHeader);
      DeleteAttachedDocuments(RecRef);

      // Copy docs for sales header from blanket order to sales order
      FromRecRef.OPEN(DATABASE::"Sales Header");
      FromRecRef.GETTABLE(BlanketOrderSalesHeader);

      ToRecRef.OPEN(DATABASE::"Sales Header");
      ToRecRef.GETTABLE(SalesOrderHeader);

      CopyAttachments(FromRecRef,ToRecRef);
    END;

    [EventSubscriber(Codeunit,87,OnAfterInsertSalesOrderLine)]
    LOCAL PROCEDURE DocAttachFlowForSalesLinesFromBlanketOrderToSalesOrder@43(VAR SalesOrderLine@1000 : Record 37;SalesOrderHeader@1001 : Record 36;BlanketOrderSalesLine@1002 : Record 37;BlanketOrderSalesHeader@1005 : Record 36);
    VAR
      FromRecRef@1003 : RecordRef;
      ToRecRef@1004 : RecordRef;
      RecRef@1006 : RecordRef;
    BEGIN
      // Invoked when a sales order line is created from blanket sales order line
      // Need to delete docs that came from item to sale item for sales order line and copy docs from blanket sales order line

      IF SalesOrderLine."No." = '' THEN
        EXIT;

      IF SalesOrderLine.ISTEMPORARY THEN
        EXIT;

      IF BlanketOrderSalesLine."No." = '' THEN
        EXIT;

      IF BlanketOrderSalesLine.ISTEMPORARY THEN
        EXIT;

      RecRef.GETTABLE(SalesOrderLine);
      DeleteAttachedDocuments(RecRef);

      FromRecRef.OPEN(DATABASE::"Sales Line");
      FromRecRef.GETTABLE(BlanketOrderSalesLine);

      ToRecRef.OPEN(DATABASE::"Sales Line");
      ToRecRef.GETTABLE(SalesOrderLine);

      CopyAttachments(FromRecRef,ToRecRef);
    END;

    [EventSubscriber(Codeunit,1305,OnBeforeDeletionOfQuote)]
    LOCAL PROCEDURE DocAttachFlowForSalesHeaderFromSalesQuoteToSalesInvoice@18(VAR SalesHeader@1000 : Record 36;VAR SalesInvoiceHeader@1001 : Record 36);
    VAR
      FromRecRef@1002 : RecordRef;
      ToRecRef@1003 : RecordRef;
    BEGIN
      IF SalesHeader."No." = '' THEN
        EXIT;

      IF SalesHeader.ISTEMPORARY THEN
        EXIT;

      IF SalesInvoiceHeader."No." = '' THEN
        EXIT;

      IF SalesInvoiceHeader.ISTEMPORARY THEN
        EXIT;

      FromRecRef.OPEN(DATABASE::"Sales Header");
      FromRecRef.GETTABLE(SalesHeader);

      ToRecRef.OPEN(DATABASE::"Sales Header");
      ToRecRef.GETTABLE(SalesInvoiceHeader);

      CopyAttachments(FromRecRef,ToRecRef);
    END;

    [EventSubscriber(Codeunit,1305,OnBeforeInsertSalesInvoiceLine)]
    LOCAL PROCEDURE DocAttachFlowForSalesLinesFromSalesQuoteToSalesInvoice@25(VAR SalesQuoteLine@1000 : Record 37;SalesQuoteHeader@1001 : Record 36;SalesInvoiceLine@1002 : Record 37;SalesInvoiceHeader@1003 : Record 36);
    VAR
      FromRecRef@1004 : RecordRef;
      ToRecRef@1005 : RecordRef;
    BEGIN
      // Copying sales line items from sales quote to a sales invoice

      IF SalesInvoiceLine."No." = '' THEN
        EXIT;

      IF SalesInvoiceLine.ISTEMPORARY THEN
        EXIT;

      FromRecRef.OPEN(DATABASE::"Sales Line");
      ToRecRef.OPEN(DATABASE::"Sales Line");

      FromRecRef.GETTABLE(SalesQuoteLine);
      ToRecRef.GETTABLE(SalesInvoiceLine);

      CopyAttachments(FromRecRef,ToRecRef);
    END;

    [EventSubscriber(Table,36,OnAfterInsertEvent)]
    LOCAL PROCEDURE DocAttachFlowForSalesHeaderInsert@23(VAR Rec@1000 : Record 36;RunTrigger@1001 : Boolean);
    VAR
      Customer@1005 : Record 18;
      FromRecRef@1003 : RecordRef;
      ToRecRef@1004 : RecordRef;
    BEGIN
      // If quote no. is NOT empty that means this sales header came from an existing quote
      // In this case we need to exit out
      IF Rec."Quote No." <> '' THEN
        EXIT;

      IF Rec."No." = '' THEN
        EXIT;

      IF Rec.ISTEMPORARY THEN
        EXIT;

      FromRecRef.OPEN(DATABASE::Customer);
      ToRecRef.OPEN(DATABASE::"Sales Header");

      IF NOT Customer.GET(Rec."Sell-to Customer No.") THEN
        EXIT;

      FromRecRef.GETTABLE(Customer);
      ToRecRef.GETTABLE(Rec);

      CopyAttachments(FromRecRef,ToRecRef);
    END;

    [EventSubscriber(Codeunit,86,OnAfterInsertSalesOrderLine)]
    LOCAL PROCEDURE DocAttachFlowForSalesQuoteToSalesOrderSalesLines@24(VAR SalesOrderLine@1000 : Record 37;SalesOrderHeader@1001 : Record 36;SalesQuoteLine@1002 : Record 37;SalesQuoteHeader@1006 : Record 36);
    VAR
      FromRecRef@1003 : RecordRef;
      ToRecRef@1004 : RecordRef;
    BEGIN
      // Copying sales line items from quote to an order

      IF SalesOrderLine."No." = '' THEN
        EXIT;

      IF SalesOrderLine.ISTEMPORARY THEN
        EXIT;

      FromRecRef.OPEN(DATABASE::"Sales Line");
      ToRecRef.OPEN(DATABASE::"Sales Line");

      FromRecRef.GETTABLE(SalesQuoteLine);
      ToRecRef.GETTABLE(SalesOrderLine);

      CopyAttachments(FromRecRef,ToRecRef);
    END;

    [EventSubscriber(Table,36,OnAfterValidateEvent,"Sell-to Customer No.")]
    LOCAL PROCEDURE DocAttachFlowForSalesHeaderCustomerChg@5(VAR Rec@1000 : Record 36;VAR xRec@1001 : Record 36;CurrFieldNo@1002 : Integer);
    VAR
      RecRef@1003 : RecordRef;
    BEGIN
      IF Rec."No." = '' THEN
        EXIT;

      IF Rec.ISTEMPORARY THEN
        EXIT;

      RecRef.GETTABLE(Rec);
      IF (Rec."Sell-to Customer No." <> xRec."Sell-to Customer No.") AND (xRec."Sell-to Customer No." <> '') THEN
        DeleteAttachedDocuments(RecRef);

      DocAttachFlowForSalesHeaderInsert(Rec,TRUE);
    END;

    [EventSubscriber(Codeunit,6620,OnAfterCopySalesHeader)]
    LOCAL PROCEDURE DocAttachFlowForCopyDocumentSalesHeader@33(ToSalesHeader@1000 : Record 36;OldSalesHeader@1001 : Record 36);
    BEGIN
      DocAttachFlowForSalesHeaderCustomerChg(ToSalesHeader,OldSalesHeader,0);
    END;

    [EventSubscriber(Table,37,OnAfterInsertEvent)]
    LOCAL PROCEDURE DocAttachFlowForSalesLineInsert@6(VAR Rec@1000 : Record 37;RunTrigger@1001 : Boolean);
    VAR
      Item@1005 : Record 27;
      SalesHeader@1002 : Record 36;
      FromRecRef@1003 : RecordRef;
      ToRecRef@1004 : RecordRef;
    BEGIN
      IF Rec."Line No." = 0 THEN
        EXIT;

      IF Rec.ISTEMPORARY THEN
        EXIT;

      // Skipping if the parant sales header came from a quote
      IF SalesHeader.GET(Rec."Document Type",Rec."Document No.") THEN
        IF SalesHeader."Quote No." <> '' THEN
          EXIT;

      FromRecRef.OPEN(DATABASE::Item);
      ToRecRef.OPEN(DATABASE::"Sales Line");

      IF NOT Item.GET(Rec."No.") THEN
        EXIT;

      FromRecRef.GETTABLE(Item);
      ToRecRef.GETTABLE(Rec);

      CopyAttachments(FromRecRef,ToRecRef);
    END;

    [EventSubscriber(Table,37,OnAfterValidateEvent,"No.")]
    LOCAL PROCEDURE DocAttachFlowForSalesLineItemChg@11(VAR Rec@1000 : Record 37;VAR xRec@1001 : Record 37;CurrFieldNo@1002 : Integer);
    VAR
      xRecRef@1003 : RecordRef;
    BEGIN
      IF Rec."Line No." = 0 THEN
        EXIT;

      IF Rec.ISTEMPORARY THEN
        EXIT;

      xRecRef.GETTABLE(xRec);
      IF (Rec."No." <> xRec."No.") AND (xRec."No." <> '') THEN
        DeleteAttachedDocuments(xRecRef);

      DocAttachFlowForSalesLineInsert(Rec,TRUE);
    END;

    [EventSubscriber(Codeunit,80,OnBeforeDeleteAfterPosting)]
    LOCAL PROCEDURE DocAttachForPostedSalesDocs@29(VAR SalesHeader@1000 : Record 36;VAR SalesInvoiceHeader@1001 : Record 112;VAR SalesCrMemoHeader@1002 : Record 114);
    VAR
      FromRecRef@1003 : RecordRef;
      ToRecRef@1004 : RecordRef;
    BEGIN
      // Triggered when a posted sales cr. memo / posted sales invoice is created
      IF SalesHeader.ISTEMPORARY THEN
        EXIT;

      IF SalesInvoiceHeader.ISTEMPORARY THEN
        EXIT;

      IF SalesCrMemoHeader.ISTEMPORARY THEN
        EXIT;

      FromRecRef.GETTABLE(SalesHeader);

      IF SalesInvoiceHeader."No." <> '' THEN
        ToRecRef.GETTABLE(SalesInvoiceHeader);

      IF SalesCrMemoHeader."No." <> '' THEN
        ToRecRef.GETTABLE(SalesCrMemoHeader);

      CopyAttachmentsForPostedDocs(FromRecRef,ToRecRef);
    END;

    [EventSubscriber(Codeunit,90,OnBeforeDeleteAfterPosting)]
    LOCAL PROCEDURE DocAttachForPostedPurchaseDocs@37(VAR PurchaseHeader@1000 : Record 38;VAR PurchInvHeader@1001 : Record 122;VAR PurchCrMemoHdr@1002 : Record 124);
    VAR
      FromRecRef@1003 : RecordRef;
      ToRecRef@1004 : RecordRef;
    BEGIN
      // Triggered when a posted purchase cr. memo / posted purchase invoice is created
      IF PurchaseHeader.ISTEMPORARY THEN
        EXIT;

      IF PurchInvHeader.ISTEMPORARY THEN
        EXIT;

      IF PurchCrMemoHdr.ISTEMPORARY THEN
        EXIT;

      FromRecRef.GETTABLE(PurchaseHeader);

      IF PurchInvHeader."No." <> '' THEN
        ToRecRef.GETTABLE(PurchInvHeader);

      IF PurchCrMemoHdr."No." <> '' THEN
        ToRecRef.GETTABLE(PurchCrMemoHdr);

      CopyAttachmentsForPostedDocs(FromRecRef,ToRecRef);
    END;

    [EventSubscriber(Table,23,OnAfterDeleteEvent)]
    LOCAL PROCEDURE DeleteAttachedDocumentsOnAfterDeleteVendor@9(VAR Rec@1000 : Record 23;RunTrigger@1001 : Boolean);
    VAR
      RecRef@1002 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      DeleteAttachedDocuments(RecRef);
    END;

    [EventSubscriber(Table,38,OnAfterDeleteEvent)]
    LOCAL PROCEDURE DeleteAttachedDocumentsOnAfterDeletePurchaseHeader@15(VAR Rec@1000 : Record 38;RunTrigger@1001 : Boolean);
    VAR
      RecRef@1002 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      DeleteAttachedDocuments(RecRef);
    END;

    [EventSubscriber(Table,39,OnAfterDeleteEvent)]
    LOCAL PROCEDURE DeleteAttachedDocumentsOnAfterDeletePurchaseLine@19(VAR Rec@1000 : Record 39;RunTrigger@1001 : Boolean);
    VAR
      RecRef@1002 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      DeleteAttachedDocuments(RecRef);
    END;

    [EventSubscriber(Table,38,OnAfterInsertEvent)]
    LOCAL PROCEDURE DocAttachFlowForPurchaseHeaderInsert@21(VAR Rec@1000 : Record 38;RunTrigger@1001 : Boolean);
    VAR
      Vendor@1005 : Record 23;
      FromRecRef@1003 : RecordRef;
      ToRecRef@1004 : RecordRef;
    BEGIN
      // If quote no. is NOT empty that means this purchase header came from an existing quote
      // In this case we need to exit out
      IF Rec."Quote No." <> '' THEN
        EXIT;

      IF Rec."No." = '' THEN
        EXIT;

      IF Rec.ISTEMPORARY THEN
        EXIT;

      FromRecRef.OPEN(DATABASE::Vendor);
      ToRecRef.OPEN(DATABASE::"Purchase Header");

      IF NOT Vendor.GET(Rec."Buy-from Vendor No.") THEN
        EXIT;

      FromRecRef.GETTABLE(Vendor);
      ToRecRef.GETTABLE(Rec);

      CopyAttachments(FromRecRef,ToRecRef);
    END;

    [EventSubscriber(Table,38,OnAfterValidateEvent,"Buy-from Vendor No.")]
    LOCAL PROCEDURE DocAttachFlowForPurchaseHeaderVendorChange@8(VAR Rec@1000 : Record 38;VAR xRec@1001 : Record 38;CurrFieldNo@1002 : Integer);
    VAR
      RecRef@1003 : RecordRef;
    BEGIN
      IF Rec."No." = '' THEN
        EXIT;

      IF Rec.ISTEMPORARY THEN
        EXIT;

      RecRef.GETTABLE(Rec);
      IF (Rec."Buy-from Vendor No." <> xRec."Buy-from Vendor No.") AND (xRec."Buy-from Vendor No." <> '') THEN
        DeleteAttachedDocuments(RecRef);

      DocAttachFlowForPurchaseHeaderInsert(Rec,TRUE);
    END;

    [EventSubscriber(Codeunit,6620,OnAfterCopyPurchaseHeader)]
    LOCAL PROCEDURE DocAttachFlowForCopyDocumentPurchHeader@45(ToPurchaseHeader@1000 : Record 38;OldPurchaseHeader@1001 : Record 38);
    BEGIN
      DocAttachFlowForPurchaseHeaderVendorChange(ToPurchaseHeader,OldPurchaseHeader,0);
    END;

    [EventSubscriber(Table,39,OnAfterInsertEvent)]
    LOCAL PROCEDURE DocAttachFlowForPurchaseLineInsert@26(VAR Rec@1000 : Record 39;RunTrigger@1001 : Boolean);
    VAR
      Item@1005 : Record 27;
      FromRecRef@1003 : RecordRef;
      ToRecRef@1004 : RecordRef;
    BEGIN
      IF Rec."Line No." = 0 THEN
        EXIT;

      IF Rec.ISTEMPORARY THEN
        EXIT;

      FromRecRef.OPEN(DATABASE::Item);
      ToRecRef.OPEN(DATABASE::"Purchase Line");

      IF NOT Item.GET(Rec."No.") THEN
        EXIT;

      FromRecRef.GETTABLE(Item);
      ToRecRef.GETTABLE(Rec);

      CopyAttachments(FromRecRef,ToRecRef);
    END;

    [EventSubscriber(Table,39,OnAfterValidateEvent,"No.")]
    LOCAL PROCEDURE DocAttachFlowForPurchaseLineItemChg@28(VAR Rec@1000 : Record 39;VAR xRec@1001 : Record 39;CurrFieldNo@1002 : Integer);
    VAR
      FromRecRef@1003 : RecordRef;
      ToRecRef@1004 : RecordRef;
      xRecRef@1005 : RecordRef;
    BEGIN
      IF Rec."Line No." = 0 THEN
        EXIT;

      IF Rec.ISTEMPORARY THEN
        EXIT;

      xRecRef.GETTABLE(xRec);
      IF (Rec."No." <> xRec."No.") AND (xRec."No." <> '') THEN
        DeleteAttachedDocuments(xRecRef);

      FromRecRef.OPEN(DATABASE::Item);
      ToRecRef.OPEN(DATABASE::"Sales Line");

      DocAttachFlowForPurchaseLineInsert(Rec,TRUE);
    END;

    [EventSubscriber(Codeunit,96,OnBeforeDeletePurchQuote)]
    LOCAL PROCEDURE DocAttachFlowForPurchQuoteToPurchOrder@127(VAR QuotePurchHeader@1000 : Record 38;VAR OrderPurchHeader@1001 : Record 38);
    VAR
      FromRecRef@1003 : RecordRef;
      ToRecRef@1002 : RecordRef;
    BEGIN
      // SalesHeader - quote
      // SalesOrderHeader - order
      IF QuotePurchHeader."No." = '' THEN
        EXIT;

      IF QuotePurchHeader.ISTEMPORARY THEN
        EXIT;

      IF OrderPurchHeader."No." = '' THEN
        EXIT;

      IF OrderPurchHeader.ISTEMPORARY THEN
        EXIT;

      FromRecRef.OPEN(DATABASE::"Purchase Header");
      FromRecRef.GETTABLE(QuotePurchHeader);

      ToRecRef.OPEN(DATABASE::"Purchase Header");
      ToRecRef.GETTABLE(OrderPurchHeader);

      CopyAttachments(FromRecRef,ToRecRef);
    END;

    [EventSubscriber(Codeunit,96,OnAfterInsertPurchOrderLine)]
    LOCAL PROCEDURE DocAttachFlowForPurchQuoteToPurchOrderLines@129(VAR PurchaseQuoteLine@1000 : Record 39;VAR PurchaseOrderLine@1001 : Record 39);
    VAR
      FromRecRef@1003 : RecordRef;
      ToRecRef@1004 : RecordRef;
    BEGIN
      // Copying sales line items from quote to an order

      IF PurchaseOrderLine."No." = '' THEN
        EXIT;

      IF PurchaseOrderLine.ISTEMPORARY THEN
        EXIT;

      FromRecRef.OPEN(DATABASE::"Sales Line");
      ToRecRef.OPEN(DATABASE::"Sales Line");

      FromRecRef.GETTABLE(PurchaseQuoteLine);
      ToRecRef.GETTABLE(PurchaseOrderLine);

      CopyAttachments(FromRecRef,ToRecRef);
    END;

    [EventSubscriber(Codeunit,97,OnBeforePurchOrderHeaderModify)]
    LOCAL PROCEDURE DocAttachFlowForBlanketPurchaseHeader@30(VAR PurchOrderHeader@1000 : Record 38;BlanketOrderPurchHeader@1001 : Record 38);
    VAR
      FromRecRef@1003 : RecordRef;
      ToRecRef@1002 : RecordRef;
      RecRef@1004 : RecordRef;
    BEGIN
      // Invoked when a sales order is created from blanket sales order
      // Need to delete docs that came from customer to sales header and copy docs form blanket sales header
      IF PurchOrderHeader."No." = '' THEN
        EXIT;

      IF PurchOrderHeader.ISTEMPORARY THEN
        EXIT;

      IF BlanketOrderPurchHeader."No." = '' THEN
        EXIT;

      IF BlanketOrderPurchHeader.ISTEMPORARY THEN
        EXIT;

      RecRef.GETTABLE(PurchOrderHeader);
      DeleteAttachedDocuments(RecRef);

      // Copy docs for sales header from blanket order to sales order
      FromRecRef.OPEN(DATABASE::"Purchase Header");
      FromRecRef.GETTABLE(BlanketOrderPurchHeader);

      ToRecRef.OPEN(DATABASE::"Purchase Header");
      ToRecRef.GETTABLE(PurchOrderHeader);

      CopyAttachments(FromRecRef,ToRecRef);
    END;

    [EventSubscriber(Codeunit,97,OnBeforeInsertPurchOrderLine)]
    LOCAL PROCEDURE DocAttachFlowForBlanketPurchaseLine@31(VAR PurchOrderLine@1000 : Record 39;PurchOrderHeader@1001 : Record 38;BlanketOrderPurchLine@1002 : Record 39;BlanketOrderPurchHeader@1005 : Record 38);
    VAR
      FromRecRef@1003 : RecordRef;
      ToRecRef@1004 : RecordRef;
      RecRef@1006 : RecordRef;
    BEGIN
      // Invoked when a purch order line is created from blanket purch order line
      // Need to delete docs that came from item to purch item for purch order line and copy docs from blanket purch order line

      IF PurchOrderLine."No." = '' THEN
        EXIT;

      IF PurchOrderLine.ISTEMPORARY THEN
        EXIT;

      IF BlanketOrderPurchLine."No." = '' THEN
        EXIT;

      IF BlanketOrderPurchLine.ISTEMPORARY THEN
        EXIT;

      RecRef.GETTABLE(PurchOrderLine);
      DeleteAttachedDocuments(RecRef);

      FromRecRef.OPEN(DATABASE::"Purchase Line");
      FromRecRef.GETTABLE(BlanketOrderPurchLine);

      ToRecRef.OPEN(DATABASE::"Purchase Line");
      ToRecRef.GETTABLE(PurchOrderLine);

      CopyAttachments(FromRecRef,ToRecRef);
    END;

    [EventSubscriber(Table,27,OnAfterDeleteEvent)]
    LOCAL PROCEDURE DeleteAttachedDocumentsOnAfterDeleteItem@12(VAR Rec@1000 : Record 27;RunTrigger@1001 : Boolean);
    VAR
      RecRef@1002 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      DeleteAttachedDocuments(RecRef);
    END;

    [EventSubscriber(Table,156,OnAfterDeleteEvent)]
    LOCAL PROCEDURE DeleteAttachedDocumentsOnAfterDeleteResource@14(VAR Rec@1000 : Record 156;RunTrigger@1001 : Boolean);
    VAR
      RecRef@1002 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      DeleteAttachedDocuments(RecRef);
    END;

    [EventSubscriber(Table,11072003,OnAfterDeleteEvent)]
    LOCAL PROCEDURE DeleteAttachedDocumentsOnAfterDeleteJob@17(VAR Rec@1000 : Record 11072003;RunTrigger@1001 : Boolean);
    VAR
      RecRef@1002 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      DeleteAttachedDocuments(RecRef);
    END;

    [EventSubscriber(Table,5200,OnAfterDeleteEvent)]
    LOCAL PROCEDURE DeleteAttachedDocumentsOnAfterDeleteEmployee@20(VAR Rec@1000 : Record 5200;RunTrigger@1001 : Boolean);
    VAR
      RecRef@1002 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      DeleteAttachedDocuments(RecRef);
    END;

    [EventSubscriber(Table,5600,OnAfterDeleteEvent)]
    LOCAL PROCEDURE DeleteAttachedDocumentsOnAfterDeleteFixedAsset@22(VAR Rec@1000 : Record 5600;RunTrigger@1001 : Boolean);
    VAR
      RecRef@1002 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      DeleteAttachedDocuments(RecRef);
    END;

    [EventSubscriber(Table,18,OnAfterRenameEvent)]
    LOCAL PROCEDURE MoveAttachedDocumentsOnAfterRenameCustomer@44(VAR Rec@1000 : Record 18;VAR xRec@1001 : Record 18;RunTrigger@1002 : Boolean);
    VAR
      MoveFromRecRef@1003 : RecordRef;
      MoveToRecRef@1004 : RecordRef;
    BEGIN
      // Moves attached docs when an Customer record is renamed [When No. is changed] from old to new rec
      MoveFromRecRef.GETTABLE(xRec);
      MoveToRecRef.GETTABLE(Rec);

      MoveAttachmentsWithinSameRecordType(MoveFromRecRef,MoveToRecRef);
    END;

    [EventSubscriber(Table,23,OnAfterRenameEvent)]
    LOCAL PROCEDURE MoveAttachedDocumentsOnAfterRenameVendor@49(VAR Rec@1000 : Record 23;VAR xRec@1001 : Record 23;RunTrigger@1002 : Boolean);
    VAR
      MoveFromRecRef@1003 : RecordRef;
      MoveToRecRef@1004 : RecordRef;
    BEGIN
      // Moves attached docs when an Vendor record is renamed [When No. is changed] from old to new rec
      MoveFromRecRef.GETTABLE(xRec);
      MoveToRecRef.GETTABLE(Rec);

      MoveAttachmentsWithinSameRecordType(MoveFromRecRef,MoveToRecRef);
    END;

    [EventSubscriber(Table,27,OnAfterRenameEvent)]
    LOCAL PROCEDURE MoveAttachedDocumentsOnAfterRenameItem@36(VAR Rec@1000 : Record 27;VAR xRec@1001 : Record 27;RunTrigger@1002 : Boolean);
    VAR
      MoveFromRecRef@1003 : RecordRef;
      MoveToRecRef@1004 : RecordRef;
    BEGIN
      // Moves attached docs when an Item record is renamed [When item no. is changed] from old to new rec
      MoveFromRecRef.GETTABLE(xRec);
      MoveToRecRef.GETTABLE(Rec);

      MoveAttachmentsWithinSameRecordType(MoveFromRecRef,MoveToRecRef);
    END;

    [EventSubscriber(Table,5200,OnAfterRenameEvent)]
    LOCAL PROCEDURE MoveAttachedDocumentsOnAfterRenameEmployee@53(VAR Rec@1000 : Record 5200;VAR xRec@1001 : Record 5200;RunTrigger@1002 : Boolean);
    VAR
      MoveFromRecRef@1003 : RecordRef;
      MoveToRecRef@1004 : RecordRef;
    BEGIN
      // Moves attached docs when an Employee record is renamed [When No. is changed] from old to new rec
      MoveFromRecRef.GETTABLE(xRec);
      MoveToRecRef.GETTABLE(Rec);

      MoveAttachmentsWithinSameRecordType(MoveFromRecRef,MoveToRecRef);
    END;

    [EventSubscriber(Table,5600,OnAfterRenameEvent)]
    LOCAL PROCEDURE MoveAttachedDocumentsOnAfterRenameFixedAsset@54(VAR Rec@1000 : Record 5600;VAR xRec@1001 : Record 5600;RunTrigger@1002 : Boolean);
    VAR
      MoveFromRecRef@1003 : RecordRef;
      MoveToRecRef@1004 : RecordRef;
    BEGIN
      // Moves attached docs when an Fixed Asset record is renamed [When No. is changed] from old to new rec
      MoveFromRecRef.GETTABLE(xRec);
      MoveToRecRef.GETTABLE(Rec);

      MoveAttachmentsWithinSameRecordType(MoveFromRecRef,MoveToRecRef);
    END;

    [EventSubscriber(Table,156,OnAfterRenameEvent)]
    LOCAL PROCEDURE MoveAttachedDocumentsOnAfterRenameResource@55(VAR Rec@1000 : Record 156;VAR xRec@1001 : Record 156;RunTrigger@1002 : Boolean);
    VAR
      MoveFromRecRef@1003 : RecordRef;
      MoveToRecRef@1004 : RecordRef;
    BEGIN
      // Moves attached docs when an Resource record is renamed [When No. is changed] from old to new rec
      MoveFromRecRef.GETTABLE(xRec);
      MoveToRecRef.GETTABLE(Rec);

      MoveAttachmentsWithinSameRecordType(MoveFromRecRef,MoveToRecRef);
    END;

    [EventSubscriber(Table,11072003,OnAfterRenameEvent)]
    LOCAL PROCEDURE MoveAttachedDocumentsOnAfterRenameJob@52(VAR Rec@1000 : Record 11072003;VAR xRec@1001 : Record 11072003;RunTrigger@1002 : Boolean);
    VAR
      MoveFromRecRef@1003 : RecordRef;
      MoveToRecRef@1004 : RecordRef;
    BEGIN
      // Moves attached docs when an Job record is renamed [When No. is changed] from old to new rec
      MoveFromRecRef.GETTABLE(xRec);
      MoveToRecRef.GETTABLE(Rec);

      MoveAttachmentsWithinSameRecordType(MoveFromRecRef,MoveToRecRef);
    END;

    [External]
    PROCEDURE IsDuplicateFile@3(TableID@1000 : Integer;DocumentNo@1001 : Code[20];RecDocType@1005 : 'Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order';RecLineNo@1006 : Integer;FileName@1002 : Text;FileExtension@1003 : Text) : Boolean;
    VAR
      DocumentAttachment@1004 : Record 1173;
    BEGIN
      DocumentAttachment.SETRANGE("Table ID",TableID);
      DocumentAttachment.SETRANGE("No.",DocumentNo);
      DocumentAttachment.SETRANGE("Document Type",RecDocType);
      DocumentAttachment.SETRANGE("Line No.",RecLineNo);
      DocumentAttachment.SETRANGE("File Name",FileName);
      DocumentAttachment.SETRANGE("File Extension",FileExtension);

      IF NOT DocumentAttachment.ISEMPTY THEN
        EXIT(TRUE);

      EXIT(FALSE);
    END;

    LOCAL PROCEDURE CopyAttachments@7(VAR FromRecRef@1000 : RecordRef;VAR ToRecRef@1001 : RecordRef);
    VAR
      FromDocumentAttachment@1002 : Record 1173;
      ToDocumentAttachment@1003 : Record 1173;
      FromFieldRef@1005 : FieldRef;
      ToFieldRef@1007 : FieldRef;
      FromDocumentType@1010 : 'Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order';
      FromLineNo@1011 : Integer;
      FromNo@1006 : Code[20];
      ToNo@1008 : Code[20];
      ToDocumentType@1004 : 'Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order';
      ToLineNo@1009 : Integer;
    BEGIN
      FromDocumentAttachment.SETRANGE("Table ID",FromRecRef.NUMBER);
      IF FromDocumentAttachment.ISEMPTY THEN
        EXIT;
      CASE FromRecRef.NUMBER OF
        DATABASE::Customer,
        DATABASE::Vendor,
        DATABASE::Item:
          BEGIN
            FromFieldRef := FromRecRef.FIELD(1);
            FromNo := FromFieldRef.VALUE;
            FromDocumentAttachment.SETRANGE("No.",FromNo);
          END;
        DATABASE::"Sales Header",
        DATABASE::"Purchase Header":
          BEGIN
            FromFieldRef := FromRecRef.FIELD(1);
            FromDocumentType := FromFieldRef.VALUE;
            FromDocumentAttachment.SETRANGE("Document Type",FromDocumentType);
            FromFieldRef := FromRecRef.FIELD(3);
            FromNo := FromFieldRef.VALUE;
            FromDocumentAttachment.SETRANGE("No.",FromNo);
          END;
        DATABASE::"Sales Line",
        DATABASE::"Purchase Line":
          BEGIN
            FromFieldRef := FromRecRef.FIELD(1);
            FromDocumentType := FromFieldRef.VALUE;
            FromDocumentAttachment.SETRANGE("Document Type",FromDocumentType);
            FromFieldRef := FromRecRef.FIELD(3);
            FromNo := FromFieldRef.VALUE;
            FromDocumentAttachment.SETRANGE("No.",FromNo);
            FromFieldRef := FromRecRef.FIELD(4);
            FromLineNo := FromFieldRef.VALUE;
            FromDocumentAttachment.SETRANGE("Line No.",FromLineNo);
          END
      END;

      CASE ToRecRef.NUMBER OF
        DATABASE::"Sales Line":
          IF FromRecRef.NUMBER <> DATABASE::"Sales Line" THEN
            FromDocumentAttachment.SETRANGE("Document Flow Sales",TRUE);
        DATABASE::"Sales Header":
          IF FromRecRef.NUMBER <> DATABASE::"Sales Header" THEN
            FromDocumentAttachment.SETRANGE("Document Flow Sales",TRUE);
        DATABASE::"Purchase Line":
          IF FromRecRef.NUMBER <> DATABASE::"Purchase Line" THEN
            FromDocumentAttachment.SETRANGE("Document Flow Purchase",TRUE);
        DATABASE::"Purchase Header":
          IF FromRecRef.NUMBER <> DATABASE::"Purchase Header" THEN
            FromDocumentAttachment.SETRANGE("Document Flow Purchase",TRUE);
      END;

      IF FromDocumentAttachment.FINDSET THEN BEGIN
        REPEAT
          CLEAR(ToDocumentAttachment);
          ToDocumentAttachment.INIT;
          ToDocumentAttachment.TRANSFERFIELDS(FromDocumentAttachment);
          ToDocumentAttachment.VALIDATE("Table ID",ToRecRef.NUMBER);

          ToFieldRef := ToRecRef.FIELD(3);
          ToNo := ToFieldRef.VALUE;
          ToDocumentAttachment.VALIDATE("No.",ToNo);

          CASE ToRecRef.NUMBER OF
            DATABASE::"Sales Header",
            DATABASE::"Purchase Header":
              BEGIN
                ToFieldRef := ToRecRef.FIELD(1);
                ToDocumentType := ToFieldRef.VALUE;
                ToDocumentAttachment.VALIDATE("Document Type",ToDocumentType);
              END;
            DATABASE::"Sales Line",
            DATABASE::"Purchase Line":
              BEGIN
                ToFieldRef := ToRecRef.FIELD(1);
                ToDocumentType := ToFieldRef.VALUE;
                ToDocumentAttachment.VALIDATE("Document Type",ToDocumentType);

                ToFieldRef := ToRecRef.FIELD(4);
                ToLineNo := ToFieldRef.VALUE;
                ToDocumentAttachment.VALIDATE("Line No.",ToLineNo);
              END;
          END;

          IF NOT ToDocumentAttachment.INSERT(TRUE) THEN;

        UNTIL FromDocumentAttachment.NEXT = 0;
      END;

      // Copies attachments for header and then calls CopyAttachmentsForPostedDocsLines to copy attachments for lines.
    END;

    LOCAL PROCEDURE CopyAttachmentsForPostedDocs@35(VAR FromRecRef@1000 : RecordRef;VAR ToRecRef@1001 : RecordRef);
    VAR
      FromDocumentAttachment@1002 : Record 1173;
      ToDocumentAttachment@1003 : Record 1173;
      FromFieldRef@1005 : FieldRef;
      ToFieldRef@1007 : FieldRef;
      FromDocumentType@1010 : 'Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order';
      FromNo@1006 : Code[20];
      ToNo@1008 : Code[20];
    BEGIN
      FromDocumentAttachment.SETRANGE("Table ID",FromRecRef.NUMBER);

      FromFieldRef := FromRecRef.FIELD(1);
      FromDocumentType := FromFieldRef.VALUE;
      FromDocumentAttachment.SETRANGE("Document Type",FromDocumentType);

      FromFieldRef := FromRecRef.FIELD(3);
      FromNo := FromFieldRef.VALUE;
      FromDocumentAttachment.SETRANGE("No.",FromNo);

      // Find any attached docs for headers (sales / purch)
      IF FromDocumentAttachment.FINDSET THEN BEGIN
        REPEAT
          CLEAR(ToDocumentAttachment);
          ToDocumentAttachment.INIT;
          ToDocumentAttachment.TRANSFERFIELDS(FromDocumentAttachment);
          ToDocumentAttachment.VALIDATE("Table ID",ToRecRef.NUMBER);

          ToFieldRef := ToRecRef.FIELD(3);
          ToNo := ToFieldRef.VALUE;
          ToDocumentAttachment.VALIDATE("No.",ToNo);
          CLEAR(ToDocumentAttachment."Document Type");
          ToDocumentAttachment.INSERT(TRUE);

        UNTIL FromDocumentAttachment.NEXT = 0;
      END;
      CopyAttachmentsForPostedDocsLines(FromRecRef,ToRecRef);
    END;

    LOCAL PROCEDURE CopyAttachmentsForPostedDocsLines@46(VAR FromRecRef@1001 : RecordRef;VAR ToRecRef@1000 : RecordRef);
    VAR
      FromDocumentAttachmentLines@1002 : Record 1173;
      ToDocumentAttachmentLines@1006 : Record 1173;
      FromFieldRef@1004 : FieldRef;
      ToFieldRef@1008 : FieldRef;
      FromDocumentType@1005 : 'Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order';
      FromNo@1003 : Code[20];
      ToNo@1007 : Code[20];
    BEGIN
      FromFieldRef := FromRecRef.FIELD(3);
      FromNo := FromFieldRef.VALUE;
      FromDocumentAttachmentLines.RESET;

      FromFieldRef := FromRecRef.FIELD(1);
      FromDocumentType := FromFieldRef.VALUE;
      FromDocumentAttachmentLines.SETRANGE("Document Type",FromDocumentType);

      ToFieldRef := ToRecRef.FIELD(3);
      ToNo := ToFieldRef.VALUE;

      CASE FromRecRef.NUMBER OF
        DATABASE::"Sales Header":
          FromDocumentAttachmentLines.SETRANGE("Table ID",DATABASE::"Sales Line");
        DATABASE::"Purchase Header":
          FromDocumentAttachmentLines.SETRANGE("Table ID",DATABASE::"Purchase Line");
      END;
      FromDocumentAttachmentLines.SETRANGE("No.",FromNo);
      FromDocumentAttachmentLines.SETRANGE("Document Type",FromDocumentType);

      IF FromDocumentAttachmentLines.FINDSET THEN
        REPEAT
          ToDocumentAttachmentLines.TRANSFERFIELDS(FromDocumentAttachmentLines);
          CASE ToRecRef.NUMBER OF
            DATABASE::"Sales Invoice Header":
              ToDocumentAttachmentLines.VALIDATE("Table ID",DATABASE::"Sales Invoice Line");
            DATABASE::"Sales Cr.Memo Header":
              ToDocumentAttachmentLines.VALIDATE("Table ID",DATABASE::"Sales Cr.Memo Line");
            DATABASE::"Purch. Inv. Header":
              ToDocumentAttachmentLines.VALIDATE("Table ID",DATABASE::"Purch. Inv. Line");
            DATABASE::"Purch. Cr. Memo Hdr.":
              ToDocumentAttachmentLines.VALIDATE("Table ID",DATABASE::"Purch. Cr. Memo Line");
          END;

          CLEAR(ToDocumentAttachmentLines."Document Type");
          ToDocumentAttachmentLines.VALIDATE("No.",ToNo);

          IF ToDocumentAttachmentLines.INSERT(TRUE) THEN;
        UNTIL FromDocumentAttachmentLines.NEXT = 0;
    END;

    LOCAL PROCEDURE MoveAttachmentsWithinSameRecordType@50(VAR MoveFromRecRef@1000 : RecordRef;VAR MoveToRecRef@1001 : RecordRef);
    VAR
      DocumentAttachmentFound@1006 : Record 1173;
      DocumentAttachmentToCreate@1007 : Record 1173;
      MoveFromFieldRef@1002 : FieldRef;
      MoveToFieldRef@1003 : FieldRef;
      MoveFromRecNo@1004 : Code[20];
      MoveToRecNo@1005 : Code[20];
    BEGIN
      // Moves attachments from one record to another for same type
      IF MoveFromRecRef.NUMBER <> MoveToRecRef.NUMBER THEN
        EXIT;
      IF MoveFromRecRef.ISTEMPORARY OR MoveToRecRef.ISTEMPORARY THEN
        EXIT;

      DocumentAttachmentFound.SETRANGE("Table ID",MoveFromRecRef.NUMBER);
      CASE MoveFromRecRef.NUMBER OF
        DATABASE::Customer,
        DATABASE::Vendor,
        DATABASE::Job,
        DATABASE::Employee,
        DATABASE::"Fixed Asset",
        DATABASE::Resource,
        DATABASE::Item:
          BEGIN
            MoveFromFieldRef := MoveFromRecRef.FIELD(1);
            MoveFromRecNo := MoveFromFieldRef.VALUE;
            MoveToFieldRef := MoveToRecRef.FIELD(1);
            MoveToRecNo := MoveToFieldRef.VALUE;
            DocumentAttachmentFound.SETRANGE("No.",MoveFromRecNo);
          END;
      END;

      // Find any attached docs to be moved
      IF DocumentAttachmentFound.ISEMPTY THEN
        EXIT;

      // Create a copy of all found attachments with new number [MoveToRecNo]
      // Need to do this because MODIFY does not support renaming keys for a record.
      IF DocumentAttachmentFound.FINDSET THEN BEGIN
        REPEAT
          CLEAR(DocumentAttachmentToCreate);
          DocumentAttachmentToCreate.INIT;
          DocumentAttachmentToCreate.TRANSFERFIELDS(DocumentAttachmentFound);
          DocumentAttachmentToCreate.VALIDATE("No.",MoveToRecNo);
          DocumentAttachmentToCreate.INSERT(TRUE);
        UNTIL DocumentAttachmentFound.NEXT = 0;
      END;

      // Delete orphan attachments
      DocumentAttachmentFound.DELETEALL(TRUE);
    END;

    BEGIN
    {
      // Code unit to manage document attachment to records.
    }
    END.
  }
}

