OBJECT Page 11012035 Hour Accounting
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00,4PSSE;
  }
  PROPERTIES
  {
    Permissions=TableData 11012600=r;
    CaptionML=[ENU=Hour Accounting;
               NOR=Timerapportering;
               SVE=Tidrapportering];
    SaveValues=Yes;
    SourceTable=Table11012038;
    PageType=Document;
    RefreshOnActivate=Yes;
    PromotedActionCategoriesML=ENU=New,Process,Report,Approve,,,,,Request Approval;
    OnInit=BEGIN
             CheckHours := TRUE;
             IAmAControllerEditable := TRUE;
             EmployeeNoEditable := TRUE;
             IAmAControllerVisible := TRUE;
             PostingEnabled := TRUE;
             DocumentNoEditable := TRUE;
           END;

    OnOpenPage=VAR
                 lvUarHrsSetupRec@1100485000 : Record 11012600;
                 HourAccounting@1100525000 : Record 11012038;
                 lvEmployeeRec@1100285501 : Record 5200;
                 FPSLicenseManagement@1100527400 : Codeunit 11229289;
                 JobsSetup@1100583000 : Record 315;
               BEGIN
                 //**4PS Generated Code. Do Not Change.sn01
                 DPA_IsHardFilter := DPA_SetFilters(CurrPage.OBJECTID(FALSE));
                 //**4PS Generated Code.en01

                 IF FPSLicenseManagement.LicenseAndReadPermissionForTable(lvUarHrsSetupRec) THEN BEGIN
                   IF lvUarHrsSetupRec.GET THEN BEGIN
                     IF lvUarHrsSetupRec."Hour Acc. Rail CLA" THEN
                       ERROR(Text003);
                   END;
                 END;

                 HumanResourcesSetup.GET;

                 IF UserSetup.GET(USERID) AND (NOT ownHoursOverride) THEN BEGIN
                   IF UserSetup."Access own Hours only" THEN BEGIN
                     UserSetup.TESTFIELD("Employee No.");
                     FILTERGROUP(2);
                     SETRANGE("Employee No.",UserSetup."Employee No.");
                     FILTERGROUP(0);
                     EmployeeNoEditable := FALSE;
                     PostingEnabled := (UserSetup."Posting Hours allowed");
                   END ELSE
                     IamController := UserSetup.IsHourController; //kkleermaker.n, 130911, C007383
                 END;

                 //>>141124 IME247
                 JobsSetup.GET;
                 IF Rec."Employee No." <> '' THEN
                   IF JobsSetup."Filter Cost&Wage on Lab. Code" THEN BEGIN
                     lvEmployeeRec.GET(Rec."Employee No.");
                     IF lvEmployeeRec."Labour Rule" = '' THEN
                     ERROR(Text004,Rec."Employee No.");
                   END;
                 //<<141124 IME247

                 IAmAControllerVisible := (IamController);
                 IAmAControllerEditable := IamController;
                 //kkleermaker.sn, 130911, C007383
                 IF NOT IamController THEN
                   ShowMyEmployeesOnly := FALSE;
                 //kkleermaker.en
                 //kkleermaker.sn, 140102, C011567
                 IF ShowMyEmployeesOnly THEN
                   IF NOT FindRec(ShowMyEmployeesOnly, HourAccounting, '-', UserSetup."Employee No.") THEN
                     ShowMyEmployeesOnly := FALSE;
                 //kkleermaker.en
                 ShowHideCheckHoursFields;
               END;

    OnFindRecord=BEGIN
                   EXIT(FindRec(ShowMyEmployeesOnly,Rec,Which,UserSetup."Employee No."));
                 END;

    OnNextRecord=BEGIN
                   EXIT(GetRec(ShowMyEmployeesOnly,Rec,Steps,UserSetup."Employee No."));
                 END;

    OnAfterGetRecord=BEGIN
                       SetControlAppearance;
                       ShowWorkflowStatus := CurrPage.WorkflowStatus.PAGE.SetFilterOnWorkflowRecord(RECORDID);
                       CALCFIELDS("Full Name");
                       EmplDescrOtherCompany;
                     END;

    OnNewRecord=BEGIN
                  //**4PS Generated Code. Do Not Change.sn02
                  DPA_SetDefaultDepartment;
                  //**4PS Generated Code.en02

                  FillYearAndWeekWithDefaultValue;

                  IF UserSetup."Access own Hours only" THEN BEGIN
                    "Employee No." := UserSetup."Employee No.";
                    CALCFIELDS("First Name","Last Name","Full Name");
                  END;

                  EmplDescrOtherCompany;
                END;

    OnAfterGetCurrRecord=BEGIN
                           SetControlAppearance;
                           EmplDescrOtherCompany;
                           ShowWorkflowStatus := CurrPage.WorkflowStatus.PAGE.SetFilterOnWorkflowRecord(RECORDID);
                           ShowApprovals := CurrPage.PendingApprovalFactBox.PAGE.SetFilterOnRecordID(RECORDID);
                           ShowApprovals := CurrPage.ApprovalFactBox.PAGE.SetFilterOnRecordID(RECORDID);
                         END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 24      ;1   ;ActionGroup;
                      Name=ButtonHours;
                      CaptionML=[ENU=&Hours Accounting;
                                 NOR=&Timerapportering;
                                 SVE=&Timbokfîring] }
      { 1100485003;2 ;Action    ;
                      CaptionML=[ENU=Leave Overview;
                                 NOR=Fravërsoversikt;
                                 SVE=FrÜnvaro îversikt];
                      Image=ViewPage;
                      OnAction=VAR
                                 lEmplRec@1100485001 : Record 5200;
                               BEGIN
                                 IF UserSetup.GET(USERID) THEN
                                   IF UserSetup."Access own Hours only" THEN BEGIN
                                     lEmplRec.FILTERGROUP(10);
                                     lEmplRec.SETRANGE("No.", "Employee No.");
                                     lEmplRec.FILTERGROUP(0);
                                   END;

                                 lEmplRec.SETRANGE("No.", "Employee No.");
                                 lEmplRec."No." := "Employee No.";
                                 PAGE.RUNMODAL(PAGE::"Leave Overview",lEmplRec);
                               END;
                                }
      { 1100525002;2 ;Action    ;
                      CaptionML=[ENU=Posted Hours;
                                 NOR=Bokfõrte timer;
                                 SVE=Bokfîrda timmar];
                      RunObject=Page 11012272;
                      RunPageView=SORTING(Employee No.,Document No.,Year,Week)
                                  ORDER(Descending);
                      RunPageLink=Employee No.=FIELD(Employee No.);
                      Image=History;
                      OnAction=BEGIN
                                 //db, 03-09-15: usefull option if start/enddate month within week
                               END;
                                }
      { 1100528411;2 ;ActionGroup;
                      CaptionML=[ENU=&Approval;
                                 SVE=&GodkÑnnanden];
                      Image=BOMLedger }
      { 1100528410;3 ;Action    ;
                      CaptionML=[ENU=Approval Entries;
                                 NOR=Godkjenningstransaksjoner;
                                 SVE=GodkÑnnandetransaktioner];
                      Visible=FALSE;
                      Image=BOMLedger;
                      OnAction=VAR
                                 ApprovalEntry@1100528401 : Record 454;
                                 ApprovalEntries@1100528402 : Page 658;
                               BEGIN
                                 ApprovalEntry.FILTERGROUP(2);
                                 ApprovalEntry.SETRANGE("Table ID", 11012038);
                                 ApprovalEntry.SETRANGE("Record ID to Approve", RECORDID);
                                 ApprovalEntries.SETTABLEVIEW(ApprovalEntry);
                                 ApprovalEntry.FILTERGROUP(0);
                                 ApprovalEntries.RUNMODAL;
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100528420;1 ;ActionGroup;
                      CaptionML=[ENU=Approval;
                                 NOR=Godkjenning;
                                 SVE=GodkÑnnande] }
      { 1100528419;2 ;Action    ;
                      Name=Approve;
                      CaptionML=[ENU=Approve;
                                 NOR=Godkjenn;
                                 SVE=GodkÑnn];
                      Promoted=Yes;
                      Visible=OpenApprovalEntriesExistForCurrUser;
                      PromotedIsBig=Yes;
                      Image=Approve;
                      PromotedCategory=Category4;
                      OnAction=VAR
                                 ApprovalsMgmt@1000 : Codeunit 1535;
                               BEGIN
                                 ApprovalsMgmt.ApproveRecordApprovalRequest(RECORDID);
                               END;
                                }
      { 1100528418;2 ;Action    ;
                      Name=Reject;
                      CaptionML=[ENU=Reject;
                                 NOR=Avvis;
                                 SVE=Avvisa];
                      Promoted=Yes;
                      Visible=OpenApprovalEntriesExistForCurrUser;
                      PromotedIsBig=Yes;
                      Image=Reject;
                      PromotedCategory=Category4;
                      OnAction=VAR
                                 ApprovalsMgmt@1000 : Codeunit 1535;
                               BEGIN
                                 ApprovalsMgmt.RejectRecordApprovalRequest(RECORDID);
                               END;
                                }
      { 1100528417;2 ;Action    ;
                      Name=Delegate;
                      CaptionML=[ENU=Delegate;
                                 NOR=Deleger;
                                 SVE=Delegera];
                      Promoted=Yes;
                      Visible=OpenApprovalEntriesExistForCurrUser;
                      Image=Delegate;
                      PromotedCategory=Category4;
                      OnAction=VAR
                                 ApprovalsMgmt@1000 : Codeunit 1535;
                               BEGIN
                                 ApprovalsMgmt.DelegateRecordApprovalRequest(RECORDID);
                               END;
                                }
      { 1100528404;2 ;Action    ;
                      Name=Comment;
                      CaptionML=[ENU=Comments;
                                 NOR=Merknader;
                                 SVE=Kommentarer];
                      Promoted=Yes;
                      Visible=OpenApprovalEntriesExistForCurrUser;
                      Image=ViewComments;
                      PromotedCategory=Category4;
                      OnAction=VAR
                                 ApprovalsMgmt@1100528500 : Codeunit 1535;
                               BEGIN
                                 ApprovalsMgmt.GetApprovalComment(Rec);
                               END;
                                }
      { 1100528414;1 ;ActionGroup;
                      CaptionML=[ENU=Request Approval;
                                 NOR=Be om godkjenning;
                                 SVE=BegÑr godkÑnnande] }
      { 1100528413;2 ;Action    ;
                      Name=SendApprovalRequest;
                      CaptionML=[ENU=Send A&pproval Request;
                                 NOR=&Send godkjenningsforespõrsel;
                                 SVE=Skicka godk&ÑnnandebegÑran];
                      Image=SendApprovalRequest;
                      OnAction=BEGIN
                                 SendHourAccountingForApproval;
                               END;
                                }
      { 1100528412;2 ;Action    ;
                      Name=CancelApprovalRequest;
                      CaptionML=[ENU=Cancel Approval Re&quest;
                                 NOR=&Annuller godkjenningsforespõrsel;
                                 SVE=Annullera &godkÑnnandebegÑran];
                      Enabled=OpenApprovalEntriesExist;
                      Image=Cancel;
                      OnAction=VAR
                                 ApprovalsBridge4PSConstruct@1100528400 : Codeunit 11125348;
                               BEGIN
                                 ApprovalsBridge4PSConstruct.OnCancelHourAccountingApprovalRequest(Rec)
                               END;
                                }
      { 1100485007;1 ;ActionGroup;
                      Name=ButtonHoursFunc;
                      CaptionML=[ENU=F&unctions;
                                 NOR=Fu&nksjon;
                                 SVE=Fu&nktion] }
      { 1100485005;2 ;Action    ;
                      CaptionML=[ENU=Copy Hours from Employee &Absence;
                                 SVE=Kopiera timmar frÜn &planering];
                      Image=CopyBOM;
                      OnAction=BEGIN
                                 CurrPage.HoursForm.PAGE.GetHoursFromEmployeeAbsence(Rec);
                               END;
                                }
      { 1100528300;2 ;Action    ;
                      CaptionML=ENU=Copy Hours from Change Request Tasks;
                      Visible=FALSE;
                      Image=MoveToNextPeriod;
                      OnAction=BEGIN
                                 CurrPage.HoursForm.PAGE.GetHoursFromChangeRequestTasks(Rec);
                               END;
                                }
      { 1100485006;2 ;Action    ;
                      CaptionML=[ENU=Copy hours from posted &hours;
                                 NOR=Kopier timer fra bokfõrte &timer;
                                 SVE=Kopiera timmar frÜn bokfîrda &timmar];
                      Image=CopyWorksheet;
                      OnAction=BEGIN
                                 CurrPage.HoursForm.PAGE.GetHoursFromPostedHours(Rec);
                               END;
                                }
      { 1100485015;2 ;Action    ;
                      CaptionML=[ENU=Copy Project/Service Order History;
                                 NOR=Kopier prosjekt-/Serviceordrehistorikk;
                                 SVE=Kopiera projekt-/serviceorderhistorik];
                      Image=History;
                      OnAction=BEGIN
                                 CurrPage.HoursForm.PAGE.GetProjAndServiceOrderData(Rec);
                               END;
                                }
      { 1100528700;2 ;Action    ;
                      CaptionML=[ENU=Generate Hour Accounting;
                                 NOR=Generer timerapportering;
                                 SVE=Generera tidrapportering];
                      Image=Account;
                      OnAction=VAR
                                 GenerateHourAccounting@1100409000 : Report 11020252;
                               BEGIN
                                 GenerateHourAccounting.SetDefaults(Year, Week, FALSE);
                                 GenerateHourAccounting.RUNMODAL;
                               END;
                                }
      { 1100485016;2 ;Action    ;
                      CaptionML=[ENU=Hours Log Entries;
                                 NOR=Loggboktransaksjoner for timer;
                                 SVE=Loggboktransaktioner fîr timmar];
                      RunObject=Page 11071847;
                      RunPageLink=Year=FIELD(Year),
                                  Week=FIELD(Week),
                                  Employee No.=FIELD(Employee No.);
                      Visible=FALSE;
                      Image=Log;
                      OnAction=BEGIN
                                 //DJN
                                 //Moved to properties RunObject/RunPageLink
                                 //HoursLogEntrRec.SETRANGE(Year, Year);
                                 //HoursLogEntrRec.SETRANGE(Week, Week);
                                 //HoursLogEntrRec.SETRANGE("Employee No.","Employee No.");
                                 //IF HoursLogEntrRec.FINDFIRST THEN;
                                 //PAGE.RUNMODAL(PAGE::"Hours Log Entries",HoursLogEntrRec);
                               END;
                                }
      { 1100485031;2 ;Action    ;
                      CaptionML=[ENU=Explode Hour Set;
                                 NOR=Utfold timesett;
                                 SVE=Expandera timmall];
                      Visible=FALSE;
                      Image=ExplodeBOM;
                      OnAction=VAR
                                 lHourLine@11012001 : Record 11012039;
                                 lHourLine2@1210190001 : Record 11012039;
                                 lHourSetLine@1210190000 : Record 11020228;
                                 LineSpacing@11012004 : Integer;
                                 NextLineNo@11012005 : Integer;
                               BEGIN
                                 TESTFIELD(Status, Status::Open);

                                 lHourLine.RESET;
                                 lHourLine.SETRANGE(Year,Year);
                                 lHourLine.SETRANGE(Week,Week);
                                 lHourLine.SETRANGE("Employee No.","Employee No.");
                                 lHourLine.SETFILTER("Hour Set",'<>%1', '');
                                 lHourLine.SETFILTER("Connection No.",'0');

                                 IF lHourLine.FIND('-') THEN
                                   REPEAT
                                     lHourSetLine.SETRANGE("Hour Set", lHourLine."Hour Set");
                                     IF lHourSetLine.FIND('-') THEN BEGIN

                                       //Determine Line Spacing
                                       lHourLine2.COPY(lHourLine);
                                       lHourLine2.SETRANGE("Hour Set");
                                       IF lHourLine2.FIND('>') THEN BEGIN
                                         LineSpacing := (lHourLine2."Line No." - lHourLine."Line No.") DIV (lHourSetLine.COUNT);
                                         IF LineSpacing = 0 THEN
                                           ERROR(Text001);
                                       END ELSE
                                         LineSpacing := 10000;

                                       //Explode
                                       NextLineNo := lHourLine."Line No.";
                                       REPEAT
                                         lHourLine2 := lHourLine;
                                         lHourLine2."Line No." := NextLineNo;
                                         lHourLine2."Hour Set":= '';
                                         CASE lHourLine.Type OF
                                           lHourLine.Type::Project:
                                             BEGIN
                                               IF lHourSetLine.Element <> '' THEN
                                                 lHourLine2.Element := lHourSetLine.Element;
                                               IF lHourSetLine."Wage Component" <> '' THEN
                                                 lHourLine2.VALIDATE("Wage Component",lHourSetLine."Wage Component");
                                               IF lHourSetLine."Cost Object" <> '' THEN
                                                 lHourLine2.VALIDATE("Cost Object",lHourSetLine."Cost Object");
                                             END;
                                           lHourLine.Type::Service,
                                           lHourLine.Type::Indirect:
                                             BEGIN
                                               IF lHourSetLine."Wage Component" <> '' THEN
                                                 lHourLine2.VALIDATE("Wage Component",lHourSetLine."Wage Component");
                                               IF lHourSetLine."Cost Object" <> '' THEN
                                                 lHourLine2.VALIDATE("Cost Object",lHourSetLine."Cost Object");
                                               IF lHourSetLine."Cost Code" <>'' THEN
                                                 lHourLine2.VALIDATE("Cost Code",lHourSetLine."Cost Code");   //Check PS
                                             END;
                                           lHourLine.Type::Plant:
                                             BEGIN
                                               lHourLine2.VALIDATE("Plant Type",lHourSetLine."Plant Type");
                                               lHourLine2.VALIDATE("Plant No.",lHourSetLine."Plant No.");
                                               IF lHourSetLine."Wage Component" <> '' THEN
                                                 lHourLine2.VALIDATE("Wage Component",lHourSetLine."Wage Component");
                                             END;
                                         END;
                                         IF lHourSetLine.Comment <> '' THEN
                                           lHourLine2.Comment := lHourSetLine.Comment;
                                         lHourLine2."Unit Cost (LCY)" := 0;
                                         lHourLine2."Amount (LCY)" := 0;
                                         lHourLine2.CalcRateAndAmount();
                                         IF NextLineNo = lHourLine."Line No." THEN
                                           lHourLine2.MODIFY(TRUE)
                                         ELSE
                                           lHourLine2.INSERT(TRUE);
                                         NextLineNo := NextLineNo + LineSpacing;
                                       UNTIL lHourSetLine.NEXT = 0;
                                     END;
                                   UNTIL lHourLine.NEXT = 0;
                               END;
                                }
      { 3       ;1   ;ActionGroup;
                      Name=ButtonPost;
                      CaptionML=[ENU=P&osting;
                                 NOR=B&okfõring;
                                 SVE=B&okfîring] }
      { 4       ;2   ;Action    ;
                      CaptionML=[ENU=Test Report;
                                 NOR=Kontrollrapport;
                                 SVE=Testrapport];
                      Visible=FALSE;
                      Enabled=PostingEnabled;
                      Image=TestReport;
                      OnAction=VAR
                                 PrintDoc@1210190001 : Codeunit 229;
                               BEGIN
                                 HourRec.COPY(Rec);
                                 HourRec.SETFILTER("Document No.", "Document No.");
                                 PrintDoc.PrintHours(HourRec,1,FALSE);
                               END;
                                }
      { 5       ;2   ;Action    ;
                      ShortCutKey=Ctrl+F7;
                      CaptionML=[ENU=Reconcile;
                                 NOR=Avstem;
                                 SVE=AvstÑm konton];
                      Enabled=PostingEnabled;
                      Image=Reconcile;
                      OnAction=BEGIN
                                 CheckHourAccounting.RUN(Rec);
                               END;
                                }
      { 1100485010;2 ;Action    ;
                      ShortCutKey=Ctrl+F9;
                      CaptionML=[ENU=Re&lease;
                                 NOR=&Frigi;
                                 SVE=&SlÑpp];
                      Promoted=Yes;
                      Image=ReleaseDoc;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 CurrPage.UPDATE(TRUE);
                                 SetStatus(TRUE, UserSetup."Employee No.");
                               END;
                                }
      { 1100485011;2 ;Action    ;
                      ShortCutKey=Shift+Ctrl+F9;
                      CaptionML=[ENU=Re&open;
                                 NOR=&èpne pÜ nytt;
                                 SVE=&ôppna igen];
                      Promoted=Yes;
                      Enabled=TRUE;
                      Image=ReOpen;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 SetStatus(FALSE, UserSetup."Employee No.");

                                 IF NOT HumanResourcesSetup."Activation Hour Log Entries" THEN
                                   EXIT;

                                 IF Status <> xRec.Status THEN BEGIN
                                   IF Status = Status::Open THEN BEGIN
                                     gHoursLogEntrRec.INIT;
                                     gHoursLogEntrRec.Year := Year;
                                     gHoursLogEntrRec.Week := Week;
                                     gHoursLogEntrRec."Employee No." := "Employee No.";
                                     gHoursLogEntrRec."Full Name" := "Full Name" ;
                                     gHoursLogEntrRec."Document No." := "Document No.";
                                     gHoursLogEntrRec."Posting Date" := "Posting Date";
                                     gHoursLogEntrRec.Status := Status;
                                     gHoursLogEntrRec."Released By" := "Released by";
                                     IF gHoursLogEntrRec.INSERT THEN;

                                     gHoursLogEntrLinesRec.INIT;
                                     gHoursLogEntrLinesRec.Year := gHoursLogEntrRec.Year;
                                     gHoursLogEntrLinesRec.Week := gHoursLogEntrRec.Week;
                                     gHoursLogEntrLinesRec."Employee No." := gHoursLogEntrRec."Employee No.";
                                     gHoursLogEntrLinesRec.Date := TODAY;
                                     gHoursLogEntrLinesRec.Time := TIME;
                                     IF UserSetup.GET(USERID) THEN
                                       gHoursLogEntrLinesRec."Approver Change" := UserSetup."Employee No."
                                     ELSE
                                       ERROR(Text002);
                                     gHoursLogEntrLinesRec.Field := 'Status';
                                     gHoursLogEntrLinesRec."Old Value" := STRSUBSTNO('%1',xRec.Status);
                                     gHoursLogEntrLinesRec."New Value" := STRSUBSTNO('%1',Rec.Status);

                                     gHoursLogEntrLinesRec.Informed := TRUE; //ENH032

                                     //>>IME-107
                                     IF gHoursLogEntrLinesRec."Approver Change" <> '' THEN BEGIN
                                        IF gHoursLogEntrLinesRec."Employee No." = gHoursLogEntrLinesRec."Approver Change" THEN BEGIN
                                           gHoursLogEntrLinesRec.Informed := TRUE;
                                         END;
                                      END;
                                       //<<IME-107

                                     gHoursLogEntrLinesRec.INSERT;
                                   END;
                                 END;
                               END;
                                }
      { 1210190000;2 ;Action    ;
                      ShortCutKey=F9;
                      CaptionML=[ENU=Post;
                                 NOR=Bokfõr;
                                 SVE=Bokfîr];
                      Promoted=Yes;
                      Enabled=PostingEnabled;
                      PromotedIsBig=Yes;
                      Image=Post;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 PostHours(FALSE, FALSE);
                               END;
                                }
      { 1210190001;2 ;Action    ;
                      ShortCutKey=Shift+F9;
                      CaptionML=[ENU=Post and Print;
                                 NOR=Bokfõr og skriv ut;
                                 SVE=Bokfîr och skriv ut];
                      Promoted=Yes;
                      Visible=FALSE;
                      Enabled=PostingEnabled;
                      PromotedIsBig=Yes;
                      Image=PostPrint;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 PostHours(TRUE, FALSE);
                               END;
                                }
      { 7       ;2   ;Action    ;
                      Name=PostBatch;
                      CaptionML=[ENU=Post Batch;
                                 NOR=Bokfõr Batch;
                                 SVE=Bokfîr batch];
                      Enabled=PostingEnabled;
                      Image=PostBatch;
                      OnAction=BEGIN
                                 PostHours(FALSE, TRUE);
                               END;
                                }
      { 8       ;2   ;Action    ;
                      CaptionML=[ENU=Post Batch and Print;
                                 NOR=Bokfõr Batch og Skriv ut;
                                 SVE=Bokfîr batch och skriv ut];
                      Visible=FALSE;
                      Enabled=PostingEnabled;
                      Image=PrintAttachment;
                      OnAction=BEGIN
                                 PostHours(TRUE, TRUE);
                               END;
                                }
      { 1100285100;2 ;Action    ;
                      Name=PostWithNewDate;
                      CaptionML=[ENU=Post with new date;
                                 SVE=Bokfîr med nytt datum];
                      Image=PostDocument;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 HourAccounting@111285102 : Record 11012038;
                                 HourAccountingLine@111285100 : Record 11012039;
                                 GenJnlCheckLine@111285101 : Codeunit 11;
                                 HoursPostBatch@111285103 : Codeunit 11012005;
                               BEGIN
                                 //RFC1087
                                 IF NOT CONFIRM(Text11128002) THEN
                                   EXIT;

                                 HourAccounting.COPYFILTERS(Rec);
                                 HourAccounting.SETRANGE(Year, Year);
                                 HourAccounting.SETRANGE(Week, Week);
                                 HourAccounting.SETRANGE("Employee No.", "Employee No.");
                                 IF HourAccounting.FINDSET THEN
                                 REPEAT
                                   //check if postingdate is within open period
                                   //if not open period, change postingdate to first open (user) date
                                   //reuse of RFC141
                                   CLEAR(HoursPostBatch);
                                   HoursPostBatch.SetPostOptions(FALSE,TRUE);
                                   HoursPostBatch.SetReplacePostingDateOpenPeriod;  //RFC141
                                   HoursPostBatch.RUN(HourAccounting);
                                 UNTIL HourAccounting.NEXT=0;
                               END;
                                }
      { 27      ;1   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[ENU=&Print;
                                 NOR=&Skriv ut;
                                 SVE=Skriv &ut];
                      Promoted=Yes;
                      Image=Print;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 PrintDoc@1210190001 : Codeunit 229;
                               BEGIN
                                 HourRec.COPY(Rec);
                                 HourRec.SETRECFILTER;
                                 PrintDoc.PrintHours(HourRec,0,FALSE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 11  ;1   ;Group     ;
                CaptionML=[ENU=General;
                           NOR=Generell;
                           SVE=AllmÑnt] }

    { 16  ;2   ;Field     ;
                SourceExpr=Year }

    { 12  ;2   ;Field     ;
                SourceExpr=Week;
                OnValidate=BEGIN
                             WeekOnAfterValidate;
                           END;
                            }

    { 14  ;2   ;Field     ;
                SourceExpr="Employee No.";
                Editable=EmployeeNoEditable;
                OnValidate=VAR
                             lvEmployeeRec@1100285500 : Record 5200;
                             ProjectSetup@1100583000 : Record 315;
                           BEGIN
                             EmployeeNoOnAfterValidate;

                             //>>141124 IME247
                             ProjectSetup.GET;
                             IF ProjectSetup."Filter Cost&Wage on Lab. Code" THEN BEGIN
                               lvEmployeeRec.GET(Rec."Employee No.");
                               IF lvEmployeeRec."Labour Rule" = '' THEN
                               ERROR(Text004,Rec."Employee No.");
                             END;
                             //<<141124 IME247
                           END;

                OnLookup=BEGIN
                           Rec.LookUpEmployee(EmployeeNoEditable);
                           IF EmployeeNoEditable THEN
                             CurrPage.UPDATE(TRUE);
                           CALCFIELDS("Full Name");
                         END;
                          }

    { 1210190002;2;Field  ;
                DrillDown=No;
                CaptionML=[ENU=Name;
                           NOR=Navn;
                           SVE=Namn];
                SourceExpr="Full Name" }

    { 1100528400;2;Field  ;
                SourceExpr="Date Checked";
                Visible=CheckHours;
                OnValidate=BEGIN
                             CurrPage.UPDATE(TRUE);
                             // When hour accounting is checked, it must also be released.
                             IF "Date Checked" <> 0D THEN
                               SetStatus(TRUE, "Check by Empl. No.");
                           END;
                            }

    { 1100528402;2;Field  ;
                CaptionML=[ENU=Check by;
                           NOR=Kontroller etter;
                           SVE=Kontrollera efter];
                SourceExpr="Check by Empl. No.";
                Visible=CheckHours }

    { 1100528406;2;Field  ;
                SourceExpr="Check by Empl. Full Name";
                Visible=CheckHours }

    { 22  ;2   ;Field     ;
                SourceExpr="Document No.";
                Editable=DocumentNoEditable;
                OnValidate=BEGIN
                             DocumentNoOnAfterValidate;
                           END;
                            }

    { 20  ;2   ;Field     ;
                SourceExpr="Posting Date";
                Editable=FALSE }

    { 1100485001;2;Field  ;
                SourceExpr=Status;
                Editable=FALSE }

    { 1100485013;2;Field  ;
                SourceExpr="Released by";
                Visible=FALSE }

    { 1100485017;2;Field  ;
                Name=IAmAController;
                CaptionML=[ENU=Show My Employees Only;
                           NOR=Vise kun mine ansatte;
                           SVE=Visa endast mina anstÑllda];
                SourceExpr=ShowMyEmployeesOnly;
                Visible=IAmAControllerVisible;
                Editable=IAmAControllerEditable;
                OnValidate=BEGIN
                             ShowMyEmployeesOnlyOnAfterVali;
                           END;
                            }

    { 1100527450;2;Field  ;
                Name=EmployerName;
                CaptionML=ENU=Employer Name;
                SourceExpr=EmployerName.GetEmployerName(Employer,External);
                Visible=FALSE;
                Editable=FALSE }

    { 10  ;1   ;Part      ;
                Name=HoursForm;
                SubPageLink=Year=FIELD(Year),
                            Week=FIELD(Week),
                            Employee No.=FIELD(Employee No.),
                            Document No.=FIELD(Document No.);
                PagePartID=Page11012036 }

    { 1100529604;1;Group  ;
                CaptionML=[ENU=Data;
                           NOR=Data;
                           SVE=Data];
                GroupType=Group }

    { 1100529600;2;Field  ;
                SourceExpr="Inserted On" }

    { 1100529601;2;Field  ;
                SourceExpr="Inserted By" }

    { 1100529602;2;Field  ;
                SourceExpr="Modified On" }

    { 1100529603;2;Field  ;
                SourceExpr="Modified By" }

    { 1210190104;0;Container;
                ContainerType=FactBoxArea }

    { 1210190008;1;Part   ;
                SubPageLink=Year=FIELD(Year),
                            Week=FIELD(Week),
                            Employee No.=FIELD(Employee No.),
                            Line No.=FIELD(Line No.);
                PagePartID=Page11130125;
                ProviderID=10;
                PartType=Page }

    { 1210190105;1;Part   ;
                SubPageLink=Year=FIELD(Year),
                            Week=FIELD(Week),
                            Employee No.=FIELD(Employee No.),
                            Line No.=FIELD(Line No.);
                PagePartID=Page11130103;
                ProviderID=10;
                PartType=Page }

    { 1210190007;1;Part   ;
                SubPageLink=Year=FIELD(Year),
                            Week=FIELD(Week),
                            Employee No.=FIELD(Employee No.),
                            Line No.=FIELD(Line No.);
                PagePartID=Page11130123;
                ProviderID=10;
                PartType=Page }

    { 1100528409;1;Part   ;
                Name=PendingApprovalFactBox;
                SubPageView=SORTING(Record ID to Approve,Workflow Step Instance ID,Sequence No.)
                            WHERE(Table ID=FILTER(11012038));
                SubPageLink=Document Line No.=FIELD(Line No.);
                PagePartID=Page9103;
                ProviderID=10;
                Visible=OpenApprovalEntriesExistForCurrUser;
                PartType=Page;
                ShowFilter=No }

    { 1100528403;1;Part   ;
                Name=ApprovalFactBox;
                SubPageView=SORTING(Record ID to Approve,Workflow Step Instance ID,Sequence No.)
                            WHERE(Table ID=FILTER(11012038));
                SubPageLink=Document Line No.=FIELD(Line No.);
                PagePartID=Page9092;
                ProviderID=10;
                Visible=FALSE;
                PartType=Page;
                ShowFilter=No }

    { 1100528401;1;Part   ;
                Name=WorkflowStatus;
                PagePartID=Page1528;
                Visible=ShowWorkflowStatus;
                Enabled=FALSE;
                Editable=FALSE;
                PartType=Page;
                ShowFilter=No }

    { 1100285001;1;Part   ;
                SubPageLink=Year=FIELD(Year),
                            Week=FIELD(Week),
                            Employee No.=FIELD(Employee No.);
                PagePartID=Page11128052;
                ProviderID=10;
                PartType=Page }

    { 1100285000;1;Part   ;
                SubPageLink=Line No.=FIELD(Line No.),
                            Year=FIELD(Year),
                            Week=FIELD(Week),
                            Employee No.=FIELD(Employee No.);
                PagePartID=Page11128053;
                ProviderID=10;
                PartType=Page }

    { 1100525001;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1100525000;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      HumanResourcesSetup@11012000 : Record 5218;
      UserSetup@11012001 : Record 91;
      HourRec@11012002 : Record 11012038;
      gHoursLogEntrRec@1100485006 : Record 11072024;
      gHoursLogEntrLinesRec@1100485005 : Record 11072025;
      CheckHourAccounting@11012003 : Codeunit 11012002;
      PostHoursBatch@1100485000 : Report 11020222;
      Text001@1100485001 : TextConst 'ENU=There is not enough space to explode the Hour Sets.;NOR=Det er ikke nok plass til Ü utfolde timesettene.;SVE=Det finns inte tillrÑckligt med utrymme fîr att expandera timmallarna.';
      Text002@1100485004 : TextConst 'ENU=No valid Employee No. Found For UserID %1;NOR=Ikke noe gyldig Ansattnr funnet for bruker-ID %1;SVE=Inget giltigt anstÑllningsnr hittades fîr anvÑndar-ID %1.';
      IamController@1100485008 : Boolean;
      ShowMyEmployeesOnly@1100485002 : Boolean;
      DPA_IsHardFilter@1100000999 : Boolean;
      DocumentNoEditable@19064841 : Boolean INDATASET;
      EmployeeNoEditable@19049966 : Boolean INDATASET;
      PostingEnabled@19050227 : Boolean INDATASET;
      IAmAControllerVisible@19000194 : Boolean INDATASET;
      IAmAControllerEditable@19060515 : Boolean INDATASET;
      CheckHours@19047177 : Boolean INDATASET;
      Text003@1100409000 : TextConst 'ENU=Not relevant for Rail CLA.;NOR=Ikke relevant for Rail CLA.;SVE=Inte relevant fîr Rail CLA.';
      OpenApprovalEntriesExistForCurrUser@1100528400 : Boolean INDATASET;
      OpenApprovalEntriesExist@1100528401 : Boolean;
      ShowWorkflowStatus@1100528402 : Boolean;
      ShowApprovals@1100528403 : Boolean;
      ownHoursOverride@1100285500 : Boolean;
      Text004@1100285501 : TextConst 'ENU=Employee, %1, has no Labour Rule selected on Employee Card.;NOR=Ansatt, %1, har ikke Arbeidstidsregel valgt pÜ Ansattkort;SVE=AnstÑllda, %1, har ej Arbetstidsregel selekterad pÜ Personalkort.';
      Text11128002@111285100 : TextConst 'ENU=Do you want to post with new open date?;SVE=Vill du bokfîra med nytt îppet datum?';
      EmployerName@1100525000 : Record 11012013;

    PROCEDURE PostHours@1100485000(Print@1100485000 : Boolean;Batch@1100485001 : Boolean);
    BEGIN
      //call C-009959
      CLEAR(PostHoursBatch);
      PostHoursBatch.SetPostOptions(Print, NOT Batch);
      HourRec.RESET;
      HourRec.SETRANGE("Document No.", "Document No.");
      IF NOT Batch THEN BEGIN
        HourRec.SETRANGE(Year, Year);
        HourRec.SETRANGE(Week, Week);
        HourRec.SETRANGE("Employee No.", "Employee No.");
      END ELSE
        HourRec.DPA_SetFilters('');
      PostHoursBatch.SETTABLEVIEW(HourRec);
      PostHoursBatch.USEREQUESTPAGE := Batch;
      PostHoursBatch.RUNMODAL;
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE ShowHideCheckHoursFields@1100528400();
    BEGIN
      CheckHours := HumanResourcesSetup."Check Hours";
    END;

    LOCAL PROCEDURE WeekOnAfterValidate@19014554();
    BEGIN
      IF "Employee No." <> '' THEN
        CurrPage.UPDATE(TRUE);
    END;

    LOCAL PROCEDURE EmployeeNoOnAfterValidate@19040646();
    BEGIN
      CurrPage.UPDATE(TRUE);
      CALCFIELDS("Full Name");
    END;

    LOCAL PROCEDURE DocumentNoOnAfterValidate@19031352();
    BEGIN
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE ShowMyEmployeesOnlyOnAfterVali@19015268();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE SetControlAppearance@5();
    VAR
      ApprovalsMgmt@1002 : Codeunit 1535;
    BEGIN
      OpenApprovalEntriesExistForCurrUser := ApprovalsMgmt.HasOpenApprovalEntriesForCurrentUser(RECORDID);
      OpenApprovalEntriesExist := ApprovalsMgmt.HasOpenApprovalEntries(RECORDID);
    END;

    PROCEDURE SetOverride@1100285500(OverrideInput@1100285500 : Boolean);
    BEGIN
      ownHoursOverride := OverrideInput;
    END;

    BEGIN
    {
      130912 ITERO.SB Added factbox "Norm Hours Calc FB" and "Norm Hours Diff FB"
      131114 ITERO.DJ added condition to subpagelink of Hour accounting overtime FB to get overtime factbox to update

      140213 ITERO.GH //>>IME-107 in the ReOpen Action
      140305 ITERO.DJ IME108ENH added function setOverride
      140408 ITERO.DL ENH028 added informed=true
      141124 ITERO.WG IME247 Error message when no labour rule on employee card
      170817 ITERO.FH Pure#127474 Removed function call PostingEnabled on propertie for Page Action (<Action1100485010> Release)
      180711 ORANGO.DL RFC1087 post hour accounting with new date from open period, new button
    }
    END.
  }
}

