OBJECT Report 11020312 Process Empoly Warehouse Order
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=4PS Warehouse-App-Auftr„ge verarbeiten;
               ENU=Process 4PS Warehouse App Orders;
               NLD=4PS magazijn app orders verwerken];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   IF EndDate = 0D THEN
                     EndDate := TODAY;
                 END;

    OnPreReport=BEGIN
                  IF EndDate = 0D THEN
                    ERROR(Text000);
                  IF EndDate < StartDate THEN
                    ERROR(Text001);
                END;

    OnPostReport=BEGIN
                   ProcessProcessedOrders; //#C038493.n
                 END;

  }
  DATASET
  {
    { 7475;    ;DataItem;OrderHeader         ;
               DataItemTable=Table11126687;
               OnPreDataItem=BEGIN
                               OrderHeader.SETRANGE(Status, Status::Finished);
                               IF GETFILTER("Order Date") = '' THEN
                                 OrderHeader.SETRANGE("Order Date", StartDate, EndDate);
                               OrderHeader.SETRANGE("Processed On", 0DT); //#C038493.n
                             END;

               OnAfterGetRecord=BEGIN
                                  ProcessOrderHeader(OrderHeader);
                                END;

               ReqFilterFields=Process }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525003;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100525001;2;Field  ;
                  CaptionML=[DEU=Startdatum;
                             ENU=Starting Date;
                             NLD=Begindatum;
                             NOR=Startdato;
                             SVE=Startdatum];
                  SourceExpr=StartDate }

      { 1100525002;2;Field  ;
                  CaptionML=[DEU=Enddatum;
                             ENU=Ending Date;
                             NLD=Einddatum;
                             NOR=Sluttdato;
                             SVE=Slutdatum];
                  SourceExpr=EndDate }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      StartDate@1210190001 : Date;
      EndDate@1210190000 : Date;
      Text000@1210190003 : TextConst 'DEU=Das Enddatum muss eingegeben werden.;ENU=The End Date must be filled.;NLD=De einddatum moet ingegeven worden.;NOR=Sluttdatoen m† fylles ut.;SVE=Slutdatumet m†ste fyllas i.';
      Text001@1210190002 : TextConst 'DEU=Startdatum ist nach dem Enddatum.;ENU=Start Date is less than the End Date.;NLD=Begindatum valt na de einddatum.;NOR=Startdato inntreffer tidligere enn sluttdato.;SVE=Startdatum infaller tidigare „n slutdatum.';
      RecId@1100528300 : RecordID;
      Text002@1100525000 : TextConst 'DEU=Keine Auftragszeilen fr %1 %2 gefunden.;ENU=No Order Lines found for %1 %2.;NLD=Geen orderregels gevonden voor %1 %2';

    PROCEDURE ProcessOrderHeader@1100528300(VAR EmpolyWarehouseOrder@1100528300 : Record 11126687);
    VAR
      EmpolyWarehouseOrderLine@1100528302 : Record 11126688;
      EmpolyWarehouseMgt@1100528301 : Codeunit 11126681;
    BEGIN
      EmpolyWarehouseOrder.TESTFIELD(Status, EmpolyWarehouseOrder.Status::Finished);
      EmpolyWarehouseOrder.TESTFIELD("Processed On", 0DT);

      IF (EmpolyWarehouseOrder.Process = EmpolyWarehouseOrder.Process::"Project Delivery") OR
         (EmpolyWarehouseOrder.Process = EmpolyWarehouseOrder.Process::"Project Retour")
      THEN
        EmpolyWarehouseOrder.TESTFIELD("Project No.");
      IF (EmpolyWarehouseOrder.Process = EmpolyWarehouseOrder.Process::"Service Delivery") OR
         (EmpolyWarehouseOrder.Process = EmpolyWarehouseOrder.Process::"Service Retour")
      THEN
        EmpolyWarehouseOrder.TESTFIELD("Service Order No.");
      IF (EmpolyWarehouseOrder.Process = EmpolyWarehouseOrder.Process::"Phys. Inventory") OR
         (EmpolyWarehouseOrder.Process = EmpolyWarehouseOrder.Process::Sales) OR
         (EmpolyWarehouseOrder.Process = EmpolyWarehouseOrder.Process::"Sales Return")
      THEN
        EmpolyWarehouseOrder.TESTFIELD("Location Code");
      IF (EmpolyWarehouseOrder.Process = EmpolyWarehouseOrder.Process::"Stock Transfer") THEN BEGIN
        EmpolyWarehouseOrder.TESTFIELD("Location Code");
        EmpolyWarehouseOrder.TESTFIELD("To Location Code");
      END;

      EmpolyWarehouseOrderLine.SETRANGE("Order No.", EmpolyWarehouseOrder."No.");
      EmpolyWarehouseOrderLine.SETRANGE("Processed On", 0DT);
      IF (NOT EmpolyWarehouseOrderLine.FINDSET(TRUE)) THEN
        ERROR(Text002, EmpolyWarehouseOrder.TABLECAPTION, EmpolyWarehouseOrder."No.");

      REPEAT
        IF (EmpolyWarehouseOrder.Process <> EmpolyWarehouseOrder.Process::"Phys. Inventory") THEN
          EmpolyWarehouseOrderLine.TESTFIELD(Quantity);
        IF (EmpolyWarehouseOrder.Process = EmpolyWarehouseOrder.Process::"Project Delivery") OR
            (EmpolyWarehouseOrder.Process = EmpolyWarehouseOrder.Process::"Service Delivery") OR
            (EmpolyWarehouseOrder.Process = EmpolyWarehouseOrder.Process::"Project Retour") OR
            (EmpolyWarehouseOrder.Process = EmpolyWarehouseOrder.Process::"Service Retour")
        THEN
          EmpolyWarehouseMgt.CreateItemJnlLineDelivery(EmpolyWarehouseOrder, EmpolyWarehouseOrderLine);
        IF (EmpolyWarehouseOrder.Process = EmpolyWarehouseOrder.Process::"Phys. Inventory") THEN
          EmpolyWarehouseMgt.CreateItemJnlLinePhysInventory(EmpolyWarehouseOrder, EmpolyWarehouseOrderLine);
        IF (EmpolyWarehouseOrder.Process = EmpolyWarehouseOrder.Process::"Stock Transfer") THEN
          EmpolyWarehouseMgt.CreateItemJnlLineStockTransfer(EmpolyWarehouseOrder, EmpolyWarehouseOrderLine);
        IF (EmpolyWarehouseOrder.Process = EmpolyWarehouseOrder.Process::"Purch. Requisition") THEN
          EmpolyWarehouseMgt.CreateReqLine(EmpolyWarehouseOrder, EmpolyWarehouseOrderLine);

        IF (EmpolyWarehouseOrder.Process <> EmpolyWarehouseOrder.Process::Sales) AND
            (EmpolyWarehouseOrder.Process <> EmpolyWarehouseOrder.Process::"Sales Return")
        THEN BEGIN
          EmpolyWarehouseOrderLine."Processed On" := CURRENTDATETIME;
          EmpolyWarehouseOrderLine.MODIFY;
        END;
      UNTIL EmpolyWarehouseOrderLine.NEXT = 0;
      IF (EmpolyWarehouseOrder.Process = EmpolyWarehouseOrder.Process::Sales) OR
          (EmpolyWarehouseOrder.Process = EmpolyWarehouseOrder.Process::"Sales Return")
      THEN
        EmpolyWarehouseMgt.ProcessWarehouseSalesOrder(EmpolyWarehouseOrder);

      EmpolyWarehouseOrder."Processed On" := CURRENTDATETIME;
      EmpolyWarehouseOrder.MODIFY;
      EmpolyWarehouseMgt.GetRelatedRecId(RecId);
    END;

    PROCEDURE GetRecId@1100528303(VAR RecIdOut@1100528300 : RecordID);
    BEGIN
      RecIdOut := RecId;
    END;

    LOCAL PROCEDURE "---Locals---"@1100528302();
    BEGIN
    END;

    PROCEDURE ArchiveOrderHeader@1100528305(EmpolyWarehouseOrder@1100528300 : Record 11126687);
    VAR
      EmpolyWarehouseOrderLine@1100529902 : Record 11126688;
      EmpolyWarehouseOrderArch@1100529904 : Record 11126689;
      EmpolyWarehouseOrderLineArch@1100529903 : Record 11126690;
    BEGIN
      IF (EmpolyWarehouseOrder.Status <> EmpolyWarehouseOrder.Status::Finished) THEN
        EXIT;

      EmpolyWarehouseOrderArch.INIT;
      EmpolyWarehouseOrderArch.TRANSFERFIELDS(EmpolyWarehouseOrder);
      IF (NOT EmpolyWarehouseOrderArch.INSERT) THEN
        EmpolyWarehouseOrderArch.MODIFY;

      EmpolyWarehouseOrderLine.SETRANGE("Order No.", EmpolyWarehouseOrder."No.");
      EmpolyWarehouseOrderLine.SETFILTER("Processed On", '<>%1', 0DT);
      IF (EmpolyWarehouseOrderLine.FINDSET) THEN BEGIN
        REPEAT
          EmpolyWarehouseOrderLineArch.INIT;
          EmpolyWarehouseOrderLineArch.TRANSFERFIELDS(EmpolyWarehouseOrderLine);
          IF (NOT EmpolyWarehouseOrderLineArch.INSERT) THEN
            EmpolyWarehouseOrderLineArch.MODIFY;
        UNTIL (EmpolyWarehouseOrderLine.NEXT = 0);
      END;
      CopyCommentLines(EmpolyWarehouseOrder);

      EmpolyWarehouseOrder.DELETE(TRUE);
    END;

    LOCAL PROCEDURE CopyCommentLines@1100525000(EmpolyWarehouseOrder@1100525000 : Record 11126687);
    VAR
      CommentLineFrom@1100525001 : Record 97;
      CommentLineTo@1100525002 : Record 97;
    BEGIN
      CommentLineFrom.SETRANGE("Table Name", CommentLineFrom."Table Name"::EmpolyOrder);
      CommentLineFrom.SETRANGE("No.", EmpolyWarehouseOrder."No.");
      IF (NOT CommentLineFrom.FINDSET) THEN
        EXIT;
      REPEAT
        CommentLineTo.COPY(CommentLineFrom);
        CommentLineTo."Table Name" := CommentLineTo."Table Name"::EmpolyOrderArchive;
        CommentLineTo.INSERT;
      UNTIL (CommentLineFrom.NEXT = 0);
    END;

    LOCAL PROCEDURE ProcessProcessedOrders@1100528308();
    VAR
      EmpolyWarehouseOrder@1100529901 : Record 11126687;
      EmpolyWarehouseOrderLine@1100529902 : Record 11126688;
      EmpolyWarehouseOrderArch@1100529904 : Record 11126689;
      EmpolyWarehouseOrderLineArch@1100529903 : Record 11126690;
      TempEmpolyWarehouseOrder@1100529900 : TEMPORARY Record 11126687;
    BEGIN
      //#C038493.n
      EmpolyWarehouseOrder.SETFILTER("Processed On", '<>%1', 0DT);
      IF (NOT EmpolyWarehouseOrder.FINDSET) THEN
        EXIT;

      TempEmpolyWarehouseOrder.DELETEALL;
      REPEAT
        EmpolyWarehouseOrderArch.INIT;
        EmpolyWarehouseOrderArch.TRANSFERFIELDS(EmpolyWarehouseOrder);
        IF (NOT EmpolyWarehouseOrderArch.INSERT) THEN
          EmpolyWarehouseOrderArch.MODIFY;

        EmpolyWarehouseOrderLine.SETRANGE("Order No.", EmpolyWarehouseOrder."No.");
        EmpolyWarehouseOrderLine.SETFILTER("Processed On", '<>%1', 0DT);
        IF (EmpolyWarehouseOrderLine.FINDSET) THEN BEGIN
          REPEAT
            EmpolyWarehouseOrderLineArch.INIT;
            EmpolyWarehouseOrderLineArch.TRANSFERFIELDS(EmpolyWarehouseOrderLine);
            IF (NOT EmpolyWarehouseOrderLineArch.INSERT) THEN
              EmpolyWarehouseOrderLineArch.MODIFY;
          UNTIL (EmpolyWarehouseOrderLine.NEXT = 0);
        END;
        CopyCommentLines(EmpolyWarehouseOrder);

        TempEmpolyWarehouseOrder.INIT;
        TempEmpolyWarehouseOrder."No." := EmpolyWarehouseOrder."No.";
        TempEmpolyWarehouseOrder.INSERT;
      UNTIL (EmpolyWarehouseOrder.NEXT = 0);

      COMMIT;
      IF (TempEmpolyWarehouseOrder.FINDSET) THEN BEGIN
        REPEAT
          EmpolyWarehouseOrder.GET(TempEmpolyWarehouseOrder."No.");
          EmpolyWarehouseOrder.DELETE(TRUE);
        UNTIL (TempEmpolyWarehouseOrder.NEXT = 0);
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

