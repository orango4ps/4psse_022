OBJECT Table 11071972 Service Survey Object Spec.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF BORG AND NOT DontCheckBORGUser THEN
                 BORGManagement.TestBORGUser;
             END;

    OnModify=BEGIN
               IF BORG AND (xRec."Specification Code" <> '') THEN
                 BORGManagement.TestBORGUser;
             END;

    OnDelete=BEGIN
               IF BORG THEN
                 BORGManagement.TestBORGUser;
             END;

    CaptionML=ENU=Service Survey Object Specification;
    LookupPageID=Page11229799;
    DrillDownPageID=Page11229799;
  }
  FIELDS
  {
    { 10  ;   ;Survey No.          ;Code20        ;TableRelation="Service Survey";
                                                   CaptionML=[ENU=Survey No.;
                                                              NOR=Kartlegging nr.;
                                                              SVE=Kartl„ggningsnr];
                                                   NotBlank=Yes }
    { 20  ;   ;Location Code       ;Code10        ;TableRelation="Service Survey Location"."Location Code" WHERE (Survey No.=FIELD(Survey No.));
                                                   CaptionML=[ENU=Location Code;
                                                              NOR=Plasseringkode;
                                                              SVE=Lagerst„llekod] }
    { 30  ;   ;Seq. No.            ;Integer       ;CaptionML=[ENU=Seq. No.;
                                                              NOR=Sekv. nr;
                                                              SVE=Sekv. nr] }
    { 40  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr];
                                                   Editable=No }
    { 50  ;   ;Standard Object No. ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Serv. Surv. Location Object"."Standard Object No." WHERE (Survey No.=FIELD(Survey No.),
                                                                                                                                 Location Code=FIELD(Location Code),
                                                                                                                                 Seq. No.=FIELD(Seq. No.)));
                                                   CaptionML=[ENU=Standard Object No.;
                                                              NOR=Standardobjektnr;
                                                              SVE=Standardobjektsnr] }
    { 60  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF (xRec.Type <> Type) AND (Type = Type::Group) THEN BEGIN
                                                                  "Field Type" := "Field Type"::" ";
                                                                  "Unit Of Measure" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Typ];
                                                   OptionCaptionML=[ENU=Group,Line;
                                                                    NOR=Gruppe,Rad;
                                                                    SVE=Grupp,Rad];
                                                   OptionString=Group,Line }
    { 70  ;   ;Specification Code  ;Code10        ;TableRelation=IF (Type=CONST(Group)) "Object Specification Group"
                                                                 ELSE IF (Type=CONST(Line)) "Base Object Specification Line" WHERE (Level=CONST(Object));
                                                   OnValidate=VAR
                                                                ObjectSpecificationGroup@1100528601 : Record 11071778;
                                                                BaseObjectSpecificationLine@1100528600 : Record 11071779;
                                                              BEGIN
                                                                IF "Specification Code" <> '' THEN BEGIN
                                                                  CASE Type OF
                                                                    Type::Group:
                                                                      BEGIN
                                                                        ObjectSpecificationGroup.GET("Specification Code");
                                                                        VALIDATE(Description, ObjectSpecificationGroup.Description);
                                                                      END;
                                                                    Type::Line:
                                                                      BEGIN
                                                                        BaseObjectSpecificationLine.GET("Specification Code");
                                                                        VALIDATE(Description, BaseObjectSpecificationLine.Description);
                                                                        VALIDATE("Description 2", BaseObjectSpecificationLine."Description 2");
                                                                        VALIDATE("Field Type", BaseObjectSpecificationLine."Field Type");
                                                                        VALIDATE("Unit Of Measure", BaseObjectSpecificationLine."Unit Of Measure");
                                                                        VALIDATE("Object Level", BaseObjectSpecificationLine.Level = BaseObjectSpecificationLine.Level::Object);
                                                                        BORG := BaseObjectSpecificationLine.BORG;
                                                                        IF BORG AND (CurrFieldNo = FIELDNO("Specification Code")) THEN
                                                                          BORGManagement.TestBORGUser;
                                                                        Value := '';
                                                                      END;
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Specification Code;
                                                              NOR=Spesifikasjonskode;
                                                              SVE=Specifikationskod] }
    { 80  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 85  ;   ;Description 2       ;Text50        ;CaptionML=[ENU=Description 2;
                                                              NOR=Beskrivelse 2;
                                                              SVE=Beskrivning 2] }
    { 90  ;   ;Field Type          ;Option        ;OnValidate=BEGIN
                                                                IF "Field Type" = "Field Type"::" " THEN
                                                                  TESTFIELD(Type, Type::Group);
                                                              END;

                                                   CaptionML=[ENU=Field Type;
                                                              NOR=Felttype;
                                                              SVE=Field Type];
                                                   OptionCaptionML=[ENU="Date,Value,Text,Option, ";
                                                                    NOR="Dato,Verdi,Tekst,Alternativ, ";
                                                                    SVE="Datum,V„rde,Text,Alternativ, "];
                                                   OptionString=[Date,Value,Text,Option, ];
                                                   Editable=No }
    { 100 ;   ;Unit Of Measure     ;Code10        ;TableRelation="Unit of Measure";
                                                   CaptionML=[ENU=Unit Of Measure;
                                                              NOR=Enhet;
                                                              SVE=Enhet];
                                                   Editable=No }
    { 110 ;   ;Value               ;Text80        ;OnValidate=BEGIN
                                                                EvaluateValue;
                                                                IF BORG THEN
                                                                  Value := BORGManagement.EncodeValue(Value);
                                                              END;

                                                   OnLookup=BEGIN
                                                              HandleLookupValue;
                                                            END;

                                                   CaptionML=[ENU=Value;
                                                              NOR=Verdi;
                                                              SVE=V„rde] }
    { 120 ;   ;Visible for Field Service;Boolean  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Base Object Specification Line"."Visible for Field Service" WHERE (Code=FIELD(Specification Code)));
                                                   CaptionML=[ENU=Visible for Field Service;
                                                              NOR=Synlig for feltservice;
                                                              SVE=Visas f”r f„ltservice];
                                                   Editable=No }
    { 130 ;   ;Editable by Field Service;Boolean  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Base Object Specification Line"."Editable by Field Service" WHERE (Code=FIELD(Specification Code)));
                                                   CaptionML=[ENU=Editable by Field Service;
                                                              NOR=Redigerbar gjennom feltservice;
                                                              SVE=Redigerbar genom f„ltservice];
                                                   Editable=No }
    { 140 ;   ;Object Level        ;Boolean       ;OnValidate=VAR
                                                                BaseObjectSpecificationLine@1210190000 : Record 11071779;
                                                              BEGIN
                                                                IF (xRec."Object Level" <> "Object Level") AND "Object Level" THEN BEGIN
                                                                  TESTFIELD(Value, '');
                                                                  IF BaseObjectSpecificationLine.GET("Specification Code") THEN
                                                                    IF BaseObjectSpecificationLine.Level = BaseObjectSpecificationLine.Level::"Standard Object" THEN
                                                                      BaseObjectSpecificationLine.FIELDERROR(Level, Text003);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Object Level;
                                                              NOR=Objektniv†;
                                                              SVE=Objektniv†] }
    { 150 ;   ;Value (Date)        ;Date          ;OnValidate=BEGIN
                                                                Value := FORMAT("Value (Date)");
                                                              END;

                                                   CaptionML=[ENU=Value (Date);
                                                              NOR=Verdi (Dato);
                                                              SVE=Valuteringsdag];
                                                   Editable=No }
    { 160 ;   ;Value (Value)       ;Decimal       ;CaptionML=[ENU=Value (Value);
                                                              NOR=Verdi (Verdi);
                                                              SVE=V„rde (v„rde)];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 170 ;   ;BORG                ;Boolean       ;OnValidate=VAR
                                                                BORGManagement@1100528600 : Codeunit 11012859;
                                                                DecodedValue@1100528601 : Text[80];
                                                                Decoded@1100528602 : Boolean;
                                                              BEGIN
                                                                IF xRec.BORG <> BORG THEN BEGIN
                                                                  BORGManagement.TestBORGUser;
                                                                  IF BORG THEN
                                                                    Value := BORGManagement.EncodeValue(Value)
                                                                  ELSE BEGIN
                                                                    DecodedValue := BORGManagement.DecodeValue(Value, TRUE, Decoded);
                                                                    IF NOT Decoded THEN
                                                                      ERROR(Text004);
                                                                    VALIDATE(Value, DecodedValue);
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=BORG;
                                                              NOR=BORG] }
    { 180 ;   ;Publish on Service Portal;Boolean  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Base Object Specification Line"."Publish on Service Portal" WHERE (Code=FIELD(Specification Code)));
                                                   CaptionML=[ENU=Publish on Service Portal;
                                                              NOR=Publiser p† Serviceportal;
                                                              SVE=Publicera p† serviceportalen];
                                                   Editable=No }
    { 190 ;   ;Specification Group Code;Code10    ;TableRelation="Service Inspection Group";
                                                   CaptionML=[ENU=Specification Group Code;
                                                              NOR=Spesifikasjonsgruppekode;
                                                              SVE=Specifikationsgruppkod];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Survey No.,Location Code,Seq. No.,Line No.;
                                                   Clustered=Yes }
    {    ;Survey No.,Location Code,Seq. No.,Specification Group Code,Line No. }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      BORGManagement@1100528600 : Codeunit 11012859;
      Text001@1100528604 : TextConst 'ENU=Option Value %1 not found;NOR=Alternativverdi %1 ikke funnet;SVE=Alternativv„rde %1 hittades inte';
      Text002@1100528603 : TextConst 'ENU=t;NOR=t;SVE=t';
      Text003@1100528602 : TextConst 'ENU=is not Object;NOR=er ikke Objekt';
      Text004@1100528601 : TextConst 'ENU=Vaue not decoded;NOR=Verdi er ikke dekodet';
      DontCheckBORGUser@1100528605 : Boolean;

    PROCEDURE GetObjSpecificationGroupCode@1100528601() : Code[10];
    VAR
      ServiceSurveyObjectSpec@1100528600 : Record 11071972;
      BaseObjectSpecificationLine@1100528900 : Record 11071779;
    BEGIN
      CALCFIELDS("Standard Object No.");
      ServiceSurveyObjectSpec.SETRANGE("Survey No.", "Survey No.");
      ServiceSurveyObjectSpec.SETRANGE("Location Code", "Location Code");
      ServiceSurveyObjectSpec.SETRANGE("Seq. No.", "Seq. No.");
      ServiceSurveyObjectSpec.SETFILTER("Line No.", '<%1', "Line No.");
      ServiceSurveyObjectSpec.SETRANGE(Type, Type::Group);
      IF ServiceSurveyObjectSpec.FINDLAST THEN
        EXIT(ServiceSurveyObjectSpec."Specification Code");

      IF BaseObjectSpecificationLine.GET("Specification Code") THEN
        EXIT(BaseObjectSpecificationLine."Specification Group Code");
    END;

    PROCEDURE EvaluateValue@1100528603();
    VAR
      DummyDate@1100528600 : Date;
      DummyDecimal@1100528601 : Decimal;
    BEGIN
      "Value (Date)" := 0D;
      "Value (Value)" := 0;
      IF Value = '' THEN EXIT;
      CASE "Field Type" OF
        "Field Type"::Date:
          BEGIN
            IF LOWERCASE(Value) = Text002 THEN BEGIN
              Value := FORMAT(TODAY);
              IF NOT BORG THEN
                "Value (Date)" := TODAY;
            END ELSE BEGIN
              EVALUATE(DummyDate, Value);
              Value := FORMAT(DummyDate);
              IF NOT BORG THEN
                "Value (Date)" := DummyDate;
            END;
          END;
        "Field Type"::Value:
          BEGIN
            EVALUATE(DummyDecimal, Value);
            Value := FORMAT(DummyDecimal, 0, 1);
            IF NOT BORG THEN
              "Value (Value)" := DummyDecimal;
          END;
        "Field Type"::Text:;
        "Field Type"::Option:
          Value := CheckOptionValue;
      END;
    END;

    PROCEDURE CheckOptionValue@1100528604() : Text[80];
    VAR
      BaseObjectSpecificationOpt@1100528600 : Record 11071780;
    BEGIN
      BaseObjectSpecificationOpt.SETRANGE("Base Object Spec. Line Code", "Specification Code");
      BaseObjectSpecificationOpt.SETRANGE(Description, Value);
      IF BaseObjectSpecificationOpt.FINDFIRST THEN
        EXIT(BaseObjectSpecificationOpt.Description);
      BaseObjectSpecificationOpt.SETFILTER(Description, '@' + Value + '*');
      IF BaseObjectSpecificationOpt.COUNT = 1 THEN BEGIN
        BaseObjectSpecificationOpt.FINDFIRST;
        EXIT(BaseObjectSpecificationOpt.Description);
      END;
      ERROR(Text001, Value);
    END;

    PROCEDURE HandleLookupSpecificationCode@1100528600(Edit@1100528600 : Boolean);
    VAR
      ObjectSpecificationGroup@1100528601 : Record 11071778;
      BaseObjectSpecificationLine@1100528602 : Record 11071779;
    BEGIN
      CASE Type OF
        Type::Group:
          BEGIN
            IF PAGE.RUNMODAL(0, ObjectSpecificationGroup) = ACTION::LookupOK THEN
              IF Edit THEN
                VALIDATE("Specification Code", ObjectSpecificationGroup.Code);
          END;
        Type::Line:
          BEGIN
            BaseObjectSpecificationLine.SETFILTER("Specification Group Code", '%1|%2', '', GetObjSpecificationGroupCode);
            IF PAGE.RUNMODAL(0, BaseObjectSpecificationLine) = ACTION::LookupOK THEN
              IF Edit THEN
                VALIDATE("Specification Code", BaseObjectSpecificationLine.Code);
          END;
      END;
    END;

    PROCEDURE GetValue@1100528602(VAR ODecoded@1100528601 : Boolean) : Text[80];
    BEGIN
      ODecoded := TRUE;
      IF NOT BORG THEN
        EXIT(Value);
      EXIT(BORGManagement.DecodeValue(Value, FALSE, ODecoded));
    END;

    PROCEDURE HandleLookupValue@1100528605();
    VAR
      BaseObjectSpecificationOpt@1100528600 : Record 11071780;
      BORGUserManagement@1100528601 : Codeunit 11012861;
    BEGIN
      IF BORG THEN
        IF BORGUserManagement.IsBorgEmplTimedOut THEN
          EXIT;
      IF "Field Type" = "Field Type"::Option THEN BEGIN
        BaseObjectSpecificationOpt.SETRANGE("Base Object Spec. Line Code", "Specification Code");
        IF PAGE.RUNMODAL(0, BaseObjectSpecificationOpt) = ACTION::LookupOK THEN
          VALIDATE(Value, BaseObjectSpecificationOpt.Description);
      END;
    END;

    PROCEDURE SetDontCheckBORGUser@1100409001(IDontCheckBORGUser@1100409000 : Boolean);
    BEGIN
      DontCheckBORGUser := IDontCheckBORGUser;
    END;

    BEGIN
    END.
  }
}

