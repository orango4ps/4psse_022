OBJECT Codeunit 6086351 Mileage-Post (Yes/No)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW110.00.00.3.00;
  }
  PROPERTIES
  {
    TableNo=6086338;
    EventSubscriberInstance=Manual;
    OnRun=BEGIN
            Mileage.COPY(Rec);
            Code;
            Rec := Mileage;
          END;

  }
  CODE
  {
    VAR
      Mileage@1003 : Record 6086338;
      MileagePost@1004 : Codeunit 6086350;
      AccountMissmatch@1160040001 : TextConst 'DEU=Die Buchungskonfiguration wurde geÑndert und %1 fÅr diese Kilometerleistung sollte %1 sein.\\Mîchten Sie diese Kilometerleistung in %1aktualisieren?;ENU=The posting setup has changed and the %1 on this mileage should be %1.\\Do you want to update this mileage to this %1?;NLD=De %1 %2 is afwijkend van de huidige ingestelde output %3.\\Wilt u bijwerken?';
      HasErrorComment@1160040002 : TextConst 'DEU=Diese Kilometerleistung beinhaltet einen oder mehrere Fehler.;ENU=There are one or more errors on this mileage.;NLD=Er zijn ÇÇn of meerdere fouten bij deze uitgave.';
      HasWarningCommentQuestion@1160040000 : TextConst 'DEU=Es gibt eine oder mehrere Warnungen fÅr diese Kilometerleistung.\\Mîchten Sie fortfahren?;ENU=There are one or more warnings on this mileage.\\Do you want to continue?;NLD=Er zijn ÇÇn of meer waarschuwingen voor deze kilometrage.\Wilt u doorgaan?';
      Text001@1001 : TextConst 'DEU=Mîchten Sie einen Buchungsvorschlag fÅr diese Kilometerleistung erstellen?;ENU=Do you want to post this mileage?;NLD=Wilt u de kilometrage boeken?';

    LOCAL PROCEDURE Code@1();
    VAR
      Mileage2@1160040001 : Record 6086338;
      CalculatedAccount@1160040000 : Code[20];
    BEGIN
      IF NOT CONFIRM(Text001,FALSE) THEN
        EXIT;

      CODEUNIT.RUN(CODEUNIT::"Mileage-Validate",Mileage);
      IF Mileage.HasWarningComment(FALSE) THEN
        IF Mileage.PostingSetupChanged(CalculatedAccount) THEN
          IF CONFIRM(AccountMissmatch,TRUE,Mileage.FIELDCAPTION("Mileage Account"),CalculatedAccount) THEN BEGIN
            Mileage2.GET(Mileage."Entry No.");
            Mileage2.VALIDATE("Mileage Account",CalculatedAccount);
            Mileage2."Mil. Account Manually Changed" := FALSE;
            Mileage2.MODIFY;
            Mileage := Mileage2;
          END;

      //RE-VALIDATE
      CODEUNIT.RUN(CODEUNIT::"Mileage-Validate",Mileage);

      IF Mileage.HasWarningComment(FALSE) THEN
        IF NOT CONFIRM(HasWarningCommentQuestion,FALSE) THEN
          EXIT;

      Mileage.HasErrorComment(TRUE,TRUE);
      MileagePost.RUN(Mileage);
    END;

    PROCEDURE Preview@6086301(VAR Rec@6086300 : Record 6086338);
    VAR
      MileagePostYesNo@6086301 : Codeunit 6086351;
      GenJnlPostPreview@6086303 : Codeunit 19;
    BEGIN
      BINDSUBSCRIPTION(MileagePostYesNo);
      GenJnlPostPreview.Preview(MileagePostYesNo,Rec);
    END;

    [EventSubscriber(Codeunit,19,OnRunPreview)]
    LOCAL PROCEDURE OnRunPreview@3(VAR Result@1000 : Boolean;Subscriber@1001 : Variant;RecVar@1002 : Variant);
    VAR
      RecRef@6086300 : RecordRef;
    BEGIN
      IF NOT RecVar.ISRECORD THEN
        EXIT;

      RecRef.GETTABLE(RecVar);
      IF RecRef.NUMBER = DATABASE::Mileage THEN BEGIN
        Mileage.COPY(RecVar);
        MileagePost.SetPreviewMode(TRUE);
        Result := MileagePost.RUN(Mileage);
      END;
    END;

    BEGIN
    END.
  }
}

