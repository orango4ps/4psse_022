OBJECT Table 232 Gen. Journal Batch
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Name,Description;
    OnInsert=BEGIN
               LOCKTABLE;
               GenJnlTemplate.GET("Journal Template Name");
               IF NOT GenJnlTemplate."Copy VAT Setup to Jnl. Lines" THEN
                 "Copy VAT Setup to Jnl. Lines" := FALSE;
               "Allow Payment Export" := GenJnlTemplate.Type = GenJnlTemplate.Type::Payments;

               SetLastModifiedDateTime;
             END;

    OnModify=BEGIN
               SetLastModifiedDateTime;
             END;

    OnDelete=BEGIN
               ApprovalsMgmt.OnCancelGeneralJournalBatchApprovalRequest(Rec);

               GenJnlAlloc.SETRANGE("Journal Template Name","Journal Template Name");
               GenJnlAlloc.SETRANGE("Journal Batch Name",Name);
               GenJnlAlloc.DELETEALL;
               GenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
               GenJnlLine.SETRANGE("Journal Batch Name",Name);
               GenJnlLine.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               ApprovalsMgmt.OnRenameRecordInApprovalRequest(xRec.RECORDID,RECORDID);

               SetLastModifiedDateTime;
             END;

    CaptionML=[ENU=Gen. Journal Batch;
               NOR=Finanskladd;
               SVE=Redovisningsjournal];
    LookupPageID=Page251;
  }
  FIELDS
  {
    { 1   ;   ;Journal Template Name;Code10       ;TableRelation="Gen. Journal Template";
                                                   CaptionML=[ENU=Journal Template Name;
                                                              NOR=Kladdemalnavn;
                                                              SVE=Mallnamn fîr journal];
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Code10        ;CaptionML=[ENU=Name;
                                                              NOR=Navn;
                                                              SVE=Namn];
                                                   NotBlank=Yes }
    { 3   ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 4   ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   OnValidate=BEGIN
                                                                IF "Reason Code" <> xRec."Reason Code" THEN BEGIN
                                                                  ModifyLines(FIELDNO("Reason Code"));
                                                                  MODIFY;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Reason Code;
                                                              NOR=èrsaksspor;
                                                              SVE=Uppfîljningskod] }
    { 5   ;   ;Bal. Account Type   ;Option        ;OnValidate=BEGIN
                                                                "Bal. Account No." := '';
                                                                CLEAR(BalAccountId);
                                                                IF "Bal. Account Type" <> "Bal. Account Type"::"G/L Account" THEN
                                                                  "Bank Statement Import Format" := '';
                                                              END;

                                                   CaptionML=[ENU=Bal. Account Type;
                                                              NOR=Motkontotype;
                                                              SVE=Motkontotyp];
                                                   OptionCaptionML=[ENU=G/L Account,Customer,Vendor,Bank Account,Fixed Asset;
                                                                    NOR=Finans,Kunde,Leverandõr,Bank,Aktiva;
                                                                    SVE=Redov.konto,Kund,Leverantîr,Bankkonto,AnlÑggningstillgÜng];
                                                   OptionString=G/L Account,Customer,Vendor,Bank Account,Fixed Asset }
    { 6   ;   ;Bal. Account No.    ;Code20        ;TableRelation=IF (Bal. Account Type=CONST(G/L Account)) "G/L Account"
                                                                 ELSE IF (Bal. Account Type=CONST(Customer)) Customer
                                                                 ELSE IF (Bal. Account Type=CONST(Vendor)) Vendor
                                                                 ELSE IF (Bal. Account Type=CONST(Bank Account)) "Bank Account"
                                                                 ELSE IF (Bal. Account Type=CONST(Fixed Asset)) "Fixed Asset";
                                                   OnValidate=BEGIN
                                                                IF "Bal. Account Type" = "Bal. Account Type"::"G/L Account" THEN BEGIN
                                                                  CheckGLAcc("Bal. Account No.");
                                                                  UpdateBalAccountId;
                                                                END;
                                                                CheckJnlIsNotRecurring;
                                                              END;

                                                   CaptionML=[ENU=Bal. Account No.;
                                                              NOR=Motkontonr.;
                                                              SVE=Motkonto] }
    { 7   ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF "No. Series" <> '' THEN BEGIN
                                                                  GenJnlTemplate.GET("Journal Template Name");
                                                                  IF GenJnlTemplate.Recurring THEN
                                                                    ERROR(
                                                                      Text000,
                                                                      FIELDCAPTION("Posting No. Series"));
                                                                  IF "No. Series" = "Posting No. Series" THEN
                                                                    VALIDATE("Posting No. Series",'');
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No. Series;
                                                              NOR=Nr.serie;
                                                              SVE=Nr-serie] }
    { 8   ;   ;Posting No. Series  ;Code20        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF ("Posting No. Series" = "No. Series") AND ("Posting No. Series" <> '') THEN
                                                                  FIELDERROR("Posting No. Series",STRSUBSTNO(Text001,"Posting No. Series"));
                                                                ModifyLines(FIELDNO("Posting No. Series"));
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Posting No. Series;
                                                              NOR=Bokfõringsnr.serie;
                                                              SVE=Bokfîringsnr-serie] }
    { 9   ;   ;Copy VAT Setup to Jnl. Lines;Boolean;
                                                   InitValue=Yes;
                                                   CaptionML=[ENU=Copy VAT Setup to Jnl. Lines;
                                                              NOR=Kopier mva-opps. til kld.linj.;
                                                              SVE=Kopiera momsinst. Üt jnlrader] }
    { 10  ;   ;Allow VAT Difference;Boolean       ;OnValidate=BEGIN
                                                                IF "Allow VAT Difference" THEN BEGIN
                                                                  GenJnlTemplate.GET("Journal Template Name");
                                                                  GenJnlTemplate.TESTFIELD("Allow VAT Difference",TRUE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Allow VAT Difference;
                                                              NOR=Tillat mva-differanse;
                                                              SVE=TillÜt momsdifferens] }
    { 11  ;   ;Allow Payment Export;Boolean       ;CaptionML=[ENU=Allow Payment Export;
                                                              NOR=Tillat betalingseksport;
                                                              SVE=TillÜt betalningsexport] }
    { 12  ;   ;Bank Statement Import Format;Code20;TableRelation="Bank Export/Import Setup".Code WHERE (Direction=CONST(Import));
                                                   OnValidate=BEGIN
                                                                IF ("Bank Statement Import Format" <> '') AND ("Bal. Account Type" <> "Bal. Account Type"::"G/L Account") THEN
                                                                  FIELDERROR("Bank Statement Import Format",BankStmtImpFormatBalAccErr);
                                                              END;

                                                   CaptionML=[ENU=Bank Statement Import Format;
                                                              NOR=Importformat for bankkontoutdrag;
                                                              SVE=Format fîr bankutdragsimport] }
    { 21  ;   ;Template Type       ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Gen. Journal Template".Type WHERE (Name=FIELD(Journal Template Name)));
                                                   CaptionML=[ENU=Template Type;
                                                              NOR=Maltype;
                                                              SVE=Malltyp];
                                                   OptionCaptionML=[ENU=General,Sales,Purchases,Cash Receipts,Payments,Assets,Intercompany,Jobs,,,,Cash,Bank,Purchase Invoice Approval,Correction Journal (Transfers),Leave;
                                                                    NOR=Generelt,Salg,Kjõp,Innbetalinger,Betalinger,Aktiva,Konsernintern,Prosjekter;
                                                                    SVE=AllmÑnt,FîrsÑljning,Inkîp,Inbetalning,Betalning,AnlÑggningstillgÜng,Koncernintern,Projekt,,,,Kontant,Bank,GodkÑnnande av inkîpsfaktura,RÑttningsjournal (îverfîringar),FrÜnvaro];
                                                   OptionString=General,Sales,Purchases,Cash Receipts,Payments,Assets,Intercompany,Jobs,,,,Cash,Bank,Purch.Invoice Approval,Correction Journal (Transfers),Leave;
                                                   Description=4PS: options added;
                                                   Editable=No }
    { 22  ;   ;Recurring           ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Gen. Journal Template".Recurring WHERE (Name=FIELD(Journal Template Name)));
                                                   CaptionML=[ENU=Recurring;
                                                              NOR=Gjentakende;
                                                              SVE=èterkommande];
                                                   Editable=No }
    { 23  ;   ;Suggest Balancing Amount;Boolean   ;CaptionML=[ENU=Suggest Balancing Amount;
                                                              NOR=ForeslÜ motkontobelõp;
                                                              SVE=FîreslÜ saldobelopp] }
    { 8000;   ;Id                  ;GUID          ;CaptionML=[ENU=Id;
                                                              NOR=ID;
                                                              SVE=ID] }
    { 8001;   ;Last Modified DateTime;DateTime    ;CaptionML=[ENU=Last Modified DateTime;
                                                              NOR=Dato/klokkeslett for siste endring;
                                                              SVE=Senast Ñndrad, DatumTid] }
    { 8002;   ;BalAccountId        ;GUID          ;OnValidate=VAR
                                                                GLAccount@1000 : Record 15;
                                                              BEGIN
                                                                IF NOT ISNULLGUID(BalAccountId) THEN BEGIN
                                                                  GLAccount.SETRANGE(Id,BalAccountId);
                                                                  IF NOT GLAccount.FINDFIRST THEN
                                                                    ERROR(BalAccountIdDoesNotMatchAGLAccountErr);

                                                                  CheckGLAcc(GLAccount."No.");
                                                                END;

                                                                VALIDATE("Bal. Account Type","Bal. Account Type"::"G/L Account");
                                                                VALIDATE("Bal. Account No.",GLAccount."No.");
                                                              END;

                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=BalAccountId;
                                                              NOR=BalAccountId] }
    { 11012010;;Suppress Plant Entry Depr.;Boolean;OnValidate=BEGIN
                                                                //**4PS.n C018453
                                                                IF "Suppress Plant Entry Depr." THEN BEGIN
                                                                  GenJnlTemplate.GET("Journal Template Name");
                                                                  GenJnlTemplate.TESTFIELD(Type, GenJnlTemplate.Type::General);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Suppress Plant Entry Depreciation;
                                                              SVE=UtelÑmna Maskintransaktionavskrivning];
                                                   Description=4PS }
  }
  KEYS
  {
    {    ;Journal Template Name,Name              ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Only the %1 field can be filled in on recurring journals.;NOR=Det er bare feltet %1 som kan fylles ut i en gjentakelseskladd.;SVE=Endast fÑltet %1 kan fyllas i pÜ Üterkommande journaler.';
      Text001@1001 : TextConst 'ENU=must not be %1;NOR=kan ikke vëre %1;SVE=fÜr inte vara %1';
      GenJnlTemplate@1002 : Record 80;
      GenJnlLine@1003 : Record 81;
      GenJnlAlloc@1004 : Record 221;
      BankStmtImpFormatBalAccErr@1005 : TextConst '@@@="FIELDERROR ex: Bank Statement Import Format must be blank. When Bal. Account Type = Bank Account, then Bank Statement Import Format on the Bank Account card will be used in Gen. Journal Batch Journal Template Name=''GENERAL'',Name=''CASH''.";ENU="must be blank. When Bal. Account Type = Bank Account, then Bank Statement Import Format on the Bank Account card will be used";NOR="mÜ vëre tom. NÜr Motkontotype = Bankkonto, brukes Importformat for bankkontoutdrag pÜ bankkortet";SVE="mÜste vara tomt. NÑr Motkontotyp = Bankkonto kommer Format fîr bankutdragsimport pÜ Bankkonto-kortet anvÑndas"';
      ApprovalsMgmt@1006 : Codeunit 1535;
      CannotBeSpecifiedForRecurrJnlErr@1007 : TextConst 'ENU=cannot be specified when using recurring journals;NOR=kan ikke angis nÜr det brukes gjentakelseskladder';
      BalAccountIdDoesNotMatchAGLAccountErr@1008 : TextConst '@@@={Locked};ENU=The "balancingAccountNumber" does not match to a G/L Account.;NOR=The "balancingAccountNumber" does not match to a G/L Account.';

    [External]
    PROCEDURE SetupNewBatch@1();
    BEGIN
      GenJnlTemplate.GET("Journal Template Name");
      "Bal. Account Type" := GenJnlTemplate."Bal. Account Type";
      "Bal. Account No." := GenJnlTemplate."Bal. Account No.";
      "No. Series" := GenJnlTemplate."No. Series";
      "Posting No. Series" := GenJnlTemplate."Posting No. Series";
      "Reason Code" := GenJnlTemplate."Reason Code";
      "Copy VAT Setup to Jnl. Lines" := GenJnlTemplate."Copy VAT Setup to Jnl. Lines";
      "Allow VAT Difference" := GenJnlTemplate."Allow VAT Difference";
    END;

    LOCAL PROCEDURE CheckGLAcc@2(AccNo@1000 : Code[20]);
    VAR
      GLAcc@1001 : Record 15;
    BEGIN
      IF AccNo <> '' THEN BEGIN
        GLAcc.GET(AccNo);
        GLAcc.CheckGLAcc;
        GLAcc.TESTFIELD("Direct Posting",TRUE);
      END;
    END;

    LOCAL PROCEDURE CheckJnlIsNotRecurring@4();
    BEGIN
      IF "Bal. Account No." = '' THEN
        EXIT;

      GenJnlTemplate.GET("Journal Template Name");
      IF GenJnlTemplate.Recurring THEN
        FIELDERROR("Bal. Account No.",CannotBeSpecifiedForRecurrJnlErr);
    END;

    LOCAL PROCEDURE ModifyLines@3(i@1000 : Integer);
    BEGIN
      GenJnlLine.LOCKTABLE;
      GenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
      GenJnlLine.SETRANGE("Journal Batch Name",Name);
      IF GenJnlLine.FIND('-') THEN
        REPEAT
          CASE i OF
            FIELDNO("Reason Code"):
              GenJnlLine.VALIDATE("Reason Code","Reason Code");
            FIELDNO("Posting No. Series"):
              GenJnlLine.VALIDATE("Posting No. Series","Posting No. Series");
          END;
          GenJnlLine.MODIFY(TRUE);
        UNTIL GenJnlLine.NEXT = 0;
    END;

    [External]
    PROCEDURE LinesExist@5() : Boolean;
    VAR
      GenJournalLine@1000 : Record 81;
    BEGIN
      GenJournalLine.SETRANGE("Journal Template Name","Journal Template Name");
      GenJournalLine.SETRANGE("Journal Batch Name",Name);
      EXIT(NOT GenJournalLine.ISEMPTY);
    END;

    [External]
    PROCEDURE GetBalance@6() : Decimal;
    VAR
      GenJournalLine@1000 : Record 81;
    BEGIN
      GenJournalLine.SETRANGE("Journal Template Name","Journal Template Name");
      GenJournalLine.SETRANGE("Journal Batch Name",Name);
      GenJournalLine.CALCSUMS("Balance (LCY)");
      EXIT(GenJournalLine."Balance (LCY)");
    END;

    [External]
    PROCEDURE CheckBalance@11() Balance : Decimal;
    BEGIN
      Balance := GetBalance;

      IF Balance = 0 THEN
        OnGeneralJournalBatchBalanced
      ELSE
        OnGeneralJournalBatchNotBalanced;
    END;

    [Integration(TRUE)]
    LOCAL PROCEDURE OnGeneralJournalBatchBalanced@15();
    BEGIN
    END;

    [Integration(TRUE)]
    LOCAL PROCEDURE OnGeneralJournalBatchNotBalanced@16();
    BEGIN
    END;

    [Integration(TRUE)]
    PROCEDURE OnCheckGenJournalLineExportRestrictions@7();
    BEGIN
    END;

    [Integration(TRUE)]
    PROCEDURE OnMoveGenJournalBatch@8(ToRecordID@1000 : RecordID);
    BEGIN
    END;

    LOCAL PROCEDURE SetLastModifiedDateTime@1165();
    BEGIN
      "Last Modified DateTime" := CURRENTDATETIME;
    END;

    [External]
    PROCEDURE UpdateBalAccountId@9();
    VAR
      GLAccount@1000 : Record 15;
    BEGIN
      IF "Bal. Account No." = '' THEN BEGIN
        CLEAR(BalAccountId);
        EXIT;
      END;

      IF NOT GLAccount.GET("Bal. Account No.") THEN
        EXIT;

      BalAccountId := GLAccount.Id;
    END;

    BEGIN
    END.
  }
}

