OBJECT Codeunit 11020214 Printer Selection
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    SingleInstance=Yes;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      StoredReportID@1100525001 : Integer;
      StoredPrinterName@1100525000 : Text[250];
      StoredPaperSourceFirstPage@1100528000 : Integer;
      StoredPaperSourceDefaultPage@1100528001 : Integer;
      StoredPaperSourceLastPage@1100528002 : Integer;

    PROCEDURE SetPrinter@1100525000(NewReportID@1100525001 : Integer;NewPrinterName@1100525000 : Text[250]);
    BEGIN
      //* Set a temporary printer for a report, this will overrule the printer selections (if present).
      //* See also the function ''FindPrinter' in codeunit 1.

      StoredReportID := NewReportID;
      StoredPrinterName := NewPrinterName;
    END;

    PROCEDURE SetPrinterWithPaperSources@1100528002(NewReportID@1100525001 : Integer;NewPrinterName@1100525000 : Text[250];NewFirstPage@1100528002 : Integer;NewDefaultPage@1100528001 : Integer;NewLastPage@1100528000 : Integer);
    BEGIN
      //* Set a temporary printer and papersources for a report, this will overrule the printer selections (if present).
      //* See also the function 'FindPrinter' in codeunit 1.

      StoredReportID := NewReportID;
      StoredPrinterName := NewPrinterName;
      StoredPaperSourceFirstPage := NewFirstPage;
      StoredPaperSourceDefaultPage := NewDefaultPage;
      StoredPaperSourceLastPage := NewLastPage;
    END;

    PROCEDURE SetIgnorePrinterSelections@1100525003(ReportID@1100525000 : Integer);
    BEGIN
      //* Set an indicator that the printer selections must be ignored.
      //* See also the function ''FindPrinter' in codeunit 1.

      StoredReportID := ReportID;
      StoredPrinterName := IgnoreIndicator();
    END;

    PROCEDURE SelectPrinter@1100525002(VAR oPrinterName@1100525001 : Text[250]);
    VAR
      lvPrinterRec@1100525000 : Record 2000000039;
    BEGIN
      IF PAGE.RUNMODAL(PAGE::Printers, lvPrinterRec) = ACTION::LookupOK THEN
        oPrinterName := lvPrinterRec.ID;
    END;

    PROCEDURE GetPrinter@1100525001(ReportID@1100525001 : Integer;VAR PrinterName@1100525000 : Text[250]);
    BEGIN
      IF (ReportID = StoredReportID) AND (StoredReportID <> 0) THEN BEGIN
        PrinterName := StoredPrinterName;
        CLEAR(StoredPrinterName);     //* To prevent this printer keeps in memory
        StoredReportID := 0;
      END;
    END;

    PROCEDURE GetPrinterWithPaperSources@1100528003(ReportID@1100525001 : Integer;VAR PrinterName@1100525000 : Text[250];VAR FirstPage@1100528002 : Integer;VAR DefaultPage@1100528001 : Integer;VAR LastPage@1100528000 : Integer);
    BEGIN
      IF (ReportID = StoredReportID) AND (StoredReportID <> 0) THEN BEGIN
        PrinterName := StoredPrinterName;
        FirstPage := StoredPaperSourceFirstPage;
        DefaultPage := StoredPaperSourceDefaultPage;
        LastPage := StoredPaperSourceLastPage;

        //* To prevent this printer and papersources stay in memory
        StoredReportID := 0;
        StoredPrinterName := '';
        StoredPaperSourceFirstPage := 0;
        StoredPaperSourceDefaultPage := 0;
        StoredPaperSourceLastPage := 0;
      END;
    END;

    PROCEDURE IgnoreIndicator@1100525004() : Text[50];
    BEGIN
      EXIT('<IGNORE_REPORT_PRINTER_SELECTIONS>');
    END;

    BEGIN
    END.
  }
}

