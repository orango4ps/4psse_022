OBJECT Codeunit 11012192 Update Proj. Plann. Calendar
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=VAR
            Job@1100525005 : Record 11072003;
            Date@1100525000 : Record 2000000007;
            CompanyInfo@1100525003 : Record 79;
            ProjPlannNonWorkingDate@1100525001 : Record 11012439;
            CalendarMgt@1100525002 : Codeunit 7600;
            Dummy@1100525004 : Text[30];
            EarliestStartingDate@1100525006 : Date;
            LatestEndingDate@1100525007 : Date;
          BEGIN
            CompanyInfo.GET;
            CompanyInfo.TESTFIELD("Base Calendar Code");

            ProjPlannNonWorkingDate.DELETEALL;

            Job.SETRANGE("Project Planning Activities", TRUE);

            // Cheaper than adding two keys, I reckon
            IF Job.FINDSET(FALSE, FALSE) THEN
              REPEAT
                Job.TESTFIELD("Starting Date");
                Job.TESTFIELD("Ending Date");

                IF (Job."Starting Date" < EarliestStartingDate) OR (EarliestStartingDate = 0D) THEN
                  EarliestStartingDate := Job."Starting Date";
                IF (Job."Ending Date" > LatestEndingDate) OR (LatestEndingDate = 0D) THEN
                  LatestEndingDate := Job."Ending Date";
              UNTIL Job.NEXT = 0;

            Date.SETRANGE(Date."Period Type", Date."Period Type"::Date);
            Date.SETRANGE("Period Start", EarliestStartingDate, LatestEndingDate);

            IF Date.FINDSET THEN
              REPEAT
                IF CalendarMgt.CheckDateStatus(CompanyInfo."Base Calendar Code",Date."Period Start",Dummy) THEN BEGIN
                  ProjPlannNonWorkingDate.Date := Date."Period Start";
                  ProjPlannNonWorkingDate.INSERT;
                END;
              UNTIL Date.NEXT = 0;
          END;

  }
  CODE
  {

    BEGIN
    END.
  }
}

