OBJECT Table 11020462 Estimate Quantity Sheet Header
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               QuantitySheetLine.SETRANGE("Estimate No.","Estimate No.");
               QuantitySheetLine.SETRANGE("Quantity Sheet",Code);
               QuantitySheetLine.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=Estimate Quantity Sheet Header;
               NOR=Kalkyle Í antallsspesifikasjonstittel;
               SVE=Kalkyl Í antalsspecifikationsrubrik];
    LookupPageID=Page11020464;
    DrillDownPageID=Page11020464;
  }
  FIELDS
  {
    { 10  ;   ;Estimate No.        ;Code20        ;TableRelation=Estimate;
                                                   CaptionML=[ENU=Estimate No.;
                                                              NOR=Kalkylenr;
                                                              SVE=Kalkylnr];
                                                   NotBlank=Yes }
    { 15  ;   ;Estimate Description;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Estimate.Description WHERE (No.=FIELD(Estimate No.)));
                                                   CaptionML=[ENU=Estimate Description;
                                                              NOR=Kalkylebeskrivelse;
                                                              SVE=Kalkylbeskrivning];
                                                   Editable=No }
    { 20  ;   ;Code                ;Code10        ;CaptionML=[ENU=Estimate Quantity Sheet;
                                                              NOR=Kalkyle Í antallsspesifikasjon;
                                                              SVE=Kalkyl Í antalsspecifikation];
                                                   NotBlank=Yes }
    { 30  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 40  ;   ;Total Quantity      ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Estimate Quantity Sheet Line".Quantity WHERE (Estimate No.=FIELD(Estimate No.),
                                                                                                                  Quantity Sheet=FIELD(Code)));
                                                   CaptionML=[ENU=Total Quantity;
                                                              NOR=Totalt antall;
                                                              SVE=Totalt antal];
                                                   Editable=No }
    { 50  ;   ;Total Length        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Estimate Quantity Sheet Line".Length WHERE (Estimate No.=FIELD(Estimate No.),
                                                                                                                Quantity Sheet=FIELD(Code)));
                                                   CaptionML=[ENU=Total Length;
                                                              NOR=Total Lengde;
                                                              SVE=Total l„ngd];
                                                   Editable=No }
    { 60  ;   ;Total Width         ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Estimate Quantity Sheet Line".Width WHERE (Estimate No.=FIELD(Estimate No.),
                                                                                                               Quantity Sheet=FIELD(Code)));
                                                   CaptionML=[ENU=Total Width;
                                                              NOR=Total Bredde;
                                                              SVE=Total bredd];
                                                   Editable=No }
    { 70  ;   ;Total Height        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Estimate Quantity Sheet Line".Height WHERE (Estimate No.=FIELD(Estimate No.),
                                                                                                                Quantity Sheet=FIELD(Code)));
                                                   CaptionML=[ENU=Total Height;
                                                              NOR=Total H›yde;
                                                              SVE=Total h”jd];
                                                   Editable=No }
    { 80  ;   ;Total Result        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Estimate Quantity Sheet Line".Result WHERE (Estimate No.=FIELD(Estimate No.),
                                                                                                                Quantity Sheet=FIELD(Code)));
                                                   CaptionML=[ENU=Total Result;
                                                              NOR=Totalresultat;
                                                              SVE=Totalresultat];
                                                   Editable=No }
    { 90  ;   ;Sub-Estimate No.    ;Code20        ;TableRelation=Sub-Estimate."Estimate No." WHERE (Estimate No.=FIELD(Estimate No.));
                                                   OnValidate=BEGIN
                                                                ValidateSubEstimate;
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupSubEstimate;
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Sub-Estimate No.;
                                                              NOR=Delkalkylenummer;
                                                              SVE=Delkalkylnr] }
    { 95  ;   ;Part Group          ;Code10        ;TableRelation="Estimate Part"."Part Group" WHERE (Estimate No.=FIELD(Estimate No.),
                                                                                                     Sub-Estimate No.=FIELD(Sub-Estimate No.));
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Part Group;
                                                              NOR=Del gruppe;
                                                              SVE=Delgrupp] }
    { 100 ;   ;Part                ;Code20        ;TableRelation="Estimate Part".Part WHERE (Estimate No.=FIELD(Estimate No.),
                                                                                             Sub-Estimate No.=FIELD(Sub-Estimate No.),
                                                                                             Part Group=FIELD(Part Group));
                                                   OnValidate=BEGIN
                                                                ValidatePart;
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupPart;
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Part;
                                                              NOR=Del;
                                                              SVE=Del] }
    { 101 ;   ;Source Group        ;Code20        ;CaptionML=ENU=Source Group;
                                                   Editable=No }
    { 102 ;   ;Source Code         ;Code20        ;CaptionML=[ENU=Source Code;
                                                              NOR=Kildekode;
                                                              SVE=Ursprungskod];
                                                   Editable=No }
    { 103 ;   ;Source Sheet        ;Code20        ;CaptionML=ENU=Source Sheet;
                                                   Editable=No }
    { 110 ;   ;Source Type         ;Option        ;CaptionML=[ENU=Source Type;
                                                              NOR=Kildetype;
                                                              SVE=Ursprungstyp];
                                                   OptionCaptionML=ENU=" ,Part,Recipe";
                                                   OptionString=[ ,Part,Recipe];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Estimate No.,Code                       ;Clustered=Yes }
    {    ;Estimate No.,Sub-Estimate No.,Part Group,Part }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      QuantityRelation@1100525000 : Record 11020463;
      QuantitySheetLine@1100525002 : Record 11020464;
      Result@1100525003 : Decimal;

    PROCEDURE WriteDataFromSheetToEstimate@1100525000(Estimate@1100525000 : Code[20];Code@1100525001 : Code[10]);
    BEGIN
      QuantityRelation.SETRANGE("Estimate No.", Estimate);
      IF Code <> '' THEN  //DP00686
        QuantityRelation.SETRANGE(QuantityRelation."Quantity Sheet", Code);
      IF QuantityRelation.FINDSET THEN BEGIN
        REPEAT
          IF QuantitySheetLine.GET(QuantityRelation."Estimate No.",
             QuantityRelation."Quantity Sheet",
             QuantityRelation."Quantity Sheet Line") THEN BEGIN
            CalcResultRelation();
            WriteDataToEstimate(Result);
          END;
        UNTIL QuantityRelation.NEXT = 0;
      END;
    END;

    PROCEDURE CalcResultRelation@1100525003();
    VAR
      FieldCde@1100525000 : Code[1];
    BEGIN
      FieldCde := QuantityRelation."Quantity Sheet Field";
      IF QuantitySheetLine.Total <> QuantitySheetLine.Total::Subtotal THEN BEGIN
        CASE FieldCde OF
          'A':Result := QuantitySheetLine.Quantity;
          'B':Result := QuantitySheetLine.Length;
          'C':Result := QuantitySheetLine."Total Length";
          'D':Result := QuantitySheetLine.Width;
          'E':Result := QuantitySheetLine."Total Width";
          'F':Result := QuantitySheetLine.Height;
          'G':Result := QuantitySheetLine."Total Height";
        ELSE
          Result := QuantitySheetLine.Result;
        END;
      END ELSE BEGIN
        QuantitySheetLine.CALCFIELDS("Sum Quantity","Sum Result",
          "Sum Length","Sum Width","Sum Height",
          "Sum Total Length","Sum Total Width","Sum Total Height");
        CASE FieldCde OF
          'A':Result := QuantitySheetLine."Sum Quantity";
          'B':Result := QuantitySheetLine."Sum Length";
          'C':Result := QuantitySheetLine."Sum Total Length";
          'D':Result := QuantitySheetLine."Sum Width";
          'E':Result := QuantitySheetLine."Sum Total Width";
          'F':Result := QuantitySheetLine."Sum Height";
          'G':Result := QuantitySheetLine."Sum Total Height";
        ELSE
          Result := QuantitySheetLine."Sum Result";
        END;
      END;
    END;

    PROCEDURE WriteDataToEstimate@1100525001(iResult@1100525000 : Decimal);
    VAR
      NewEstLineRec@1100528902 : Record 11072072;
      NewRcpLineRec@1100409000 : Record 11072075;
    BEGIN
      //DP00381
      IF QuantityRelation.Recipe <> '' THEN BEGIN  //DP00686
        IF NewRcpLineRec.GET(QuantityRelation."Estimate No.", QuantityRelation."Sub-Estimate No.",
           QuantityRelation."Part Group", QuantityRelation.Part, QuantityRelation."Estimate Line",
           QuantityRelation."Recipe Group", QuantityRelation.Recipe, QuantityRelation."Recipe Line") THEN BEGIN
          NewRcpLineRec.VALIDATE(Quantity,iResult);
          NewRcpLineRec.MODIFY(TRUE);
        END;
      END ELSE BEGIN
        IF NewEstLineRec.GET(QuantityRelation."Estimate No.", QuantityRelation."Sub-Estimate No.",
           QuantityRelation."Part Group", QuantityRelation.Part, QuantityRelation."Estimate Line") THEN BEGIN
          NewEstLineRec.VALIDATE(Quantity,iResult);
          NewEstLineRec.MODIFY(TRUE);
          IF NewEstLineRec.Recipe <> '' THEN BEGIN
            NewEstLineRec.UpdateRecipeTotals;
            NewEstLineRec.CheckIgnoreRecipeQuantity;
          END;
        END;
      END;
    END;

    PROCEDURE ValidateSubEstimate@1100525005();
    VAR
      SubEstRecNEW@1100525000 : Record 11072074;
    BEGIN
      IF "Sub-Estimate No." <> '' THEN
        SubEstRecNEW.GET("Estimate No.", "Sub-Estimate No.");
      IF "Sub-Estimate No." = '' THEN BEGIN
        "Part Group" := '';
        Part := '';
      END;
    END;

    PROCEDURE LookupSubEstimate@1100409000();
    VAR
      SubEstRecNEW@1100525000 : Record 11072074;
    BEGIN
      SubEstRecNEW.SETRANGE("Estimate No.", "Estimate No.");
      IF PAGE.RUNMODAL(0, SubEstRecNEW) = ACTION::LookupOK THEN
        VALIDATE("Sub-Estimate No.", SubEstRecNEW."Sub-Estimate No.");
    END;

    PROCEDURE ValidatePart@1100409001();
    VAR
      EstPartRecNEW@1100409000 : Record 11072073;
    BEGIN
      IF Part <> '' THEN
        EstPartRecNEW.GET("Estimate No.", "Sub-Estimate No.", "Part Group", Part);
      IF Part = '' THEN
        "Part Group" := '';
    END;

    PROCEDURE LookupPart@1100525004();
    VAR
      EstPartRecNEW@1100525000 : Record 11072073;
    BEGIN
      EstPartRecNEW.SETRANGE("Estimate No.", "Estimate No.");
      EstPartRecNEW.SETFILTER("Sub-Estimate No.", "Sub-Estimate No.");
      IF PAGE.RUNMODAL(0, EstPartRecNEW) = ACTION::LookupOK THEN BEGIN
        "Part Group":= EstPartRecNEW."Part Group";
        VALIDATE(Part, EstPartRecNEW.Part);
      END;
    END;

    PROCEDURE ProcessEstimateAfterQuantitySheet@1100409003(CurrEst@1100409003 : Code[20];CurrSubEst@1100409002 : Code[20];CurrPartGrp@1100409000 : Code[20];CurrPart@1100409001 : Code[20]);
    VAR
      QuantitySheetHeader@1100409005 : Record 11020462;
      UpdateMgt@1100409004 : Codeunit 11012229;
    BEGIN
      //DP00686
      QuantitySheetHeader.SETRANGE("Estimate No.", CurrEst);
      QuantitySheetHeader.SETRANGE("Sub-Estimate No.", CurrSubEst);
      QuantitySheetHeader.SETRANGE("Part Group", CurrPartGrp);
      QuantitySheetHeader.SETRANGE(Part, CurrPart);
      PAGE.RUNMODAL(PAGE::"Estimate Quantity Sheet List", QuantitySheetHeader);
      UpdateMgt.UpdateTotals(CurrEst, CurrSubEst, CurrPart, TRUE);
      //db, 24-02-15: is this function unnecessary after introduction of StartQuantitySheet ?
    END;

    PROCEDURE ProcessSheet@1100409002(CurrEst@1100409002 : Code[20];CurrSheet@1100409001 : Code[20]);
    VAR
      SheetLine@1100409000 : Record 11020464;
    BEGIN
      //db, 20-01-15
      SheetLine.SETRANGE("Estimate No.", CurrEst);
      SheetLine.SETRANGE("Quantity Sheet", CurrSheet);
      IF SheetLine.FINDSET THEN BEGIN
        REPEAT
          SheetLine.CalculateResultLine;
          SheetLine.MODIFY;
        UNTIL SheetLine.NEXT = 0;
      END;
    END;

    PROCEDURE StartQuantitySheet@1100525002(CurrEst@1100525003 : Code[20];CurrSubEst@1100525004 : Code[20];CurrPartGrp@1100525005 : Code[20];CurrPart@1100525006 : Code[20]);
    VAR
      QuantitySheetHeader@1100525000 : Record 11020462;
      NumSheet@1100525001 : Integer;
      UpdateMgt@1100525002 : Codeunit 11012229;
    BEGIN
      //db, 24-02-15
      QuantitySheetHeader.SETRANGE("Estimate No.", CurrEst);
      QuantitySheetHeader.SETFILTER("Sub-Estimate No.", '%1|%2', '', CurrSubEst);
      QuantitySheetHeader.SETFILTER("Part Group", '%1|%2', '', CurrPartGrp);
      QuantitySheetHeader.SETFILTER(Part, '%1|%2', '', CurrPart);
      NumSheet := QuantitySheetHeader.COUNT;
      IF NumSheet = 1 THEN BEGIN
        QuantitySheetHeader.FINDFIRST;
        PAGE.RUNMODAL(PAGE::"Estimate Quantity Sheet Header", QuantitySheetHeader);
      END ELSE BEGIN
        PAGE.RUNMODAL(0, QuantitySheetHeader);
      END;
      UpdateMgt.UpdateTotals(CurrEst, CurrSubEst, CurrPart, TRUE);
    END;

    BEGIN
    END.
  }
}

