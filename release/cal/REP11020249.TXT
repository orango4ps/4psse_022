OBJECT Report 11020249 Delete Invoice
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Rechnung l”schen;
               ENU=Delete Invoice;
               NLD=Factuur verwijderen];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF ConfirmDeletingDocument(DocumentType, DocumentNo) THEN
                    DeleteDocument(DocumentType, DocumentNo);
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100528401;;Container;
                  ContainerType=ContentArea }

      { 1100528400;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100528402;2;Field  ;
                  Name=DocumentType;
                  CaptionML=[DEU=Belegart;
                             ENU=Document Type;
                             NLD=Documentsoort;
                             NOR=Dokumenttype;
                             SVE=Dokumenttyp];
                  OptionCaptionML=[DEU=Verkaufsrechnung,Verkaufsgutschrift,Einkaufsrechnung,Einkaufsgutschrift;
                                   ENU=Sales Invoice,Sales Credit Memo,Purchase Invoice,Purchase Credit Memo;
                                   NLD=Verkoopfactuur,Verkoopcreditnota,Inkoopfactuur,Inkoopcreditnota];
                  SourceExpr=DocumentType;
                  OnValidate=BEGIN
                               DocumentNo := '';
                             END;
                              }

      { 1100528403;2;Field  ;
                  Name=DocumentNo;
                  CaptionML=[DEU=Belegnr.;
                             ENU=Document No.;
                             NLD=Documentnr.;
                             NOR=Dokumentnr.;
                             SVE=Dokumentnr];
                  SourceExpr=DocumentNo;
                  OnValidate=BEGIN
                               ValidateDocumentNo(DocumentType, DocumentNo);
                             END;

                  OnLookup=BEGIN
                             IF LookupDocumentNo(DocumentType, DocumentNo) THEN
                               ValidateDocumentNo(DocumentType, DocumentNo);
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      DocumentType@1100528400 : 'Sales Invoice,Sales Credit Memo,Purchase Invoice,Purchase Credit Memo';
      DocumentNo@1100528401 : Code[20];
      Text000@1100528402 : TextConst 'DEU=M”chten Sie wirklich %1 %2 von %3 %4 mit einem %5 von %6 l”schen?;ENU=Are you sure that you want to delete %1 %2 of %3 %4 with an %5 of %6?;NLD=Weet u zeker dat u %1 %2 van %3 %4 met een %5 van %6 wilt verwijderen?';
      Text001@1100528403 : TextConst 'DEU=Verkaufsrechnung;ENU=Sales Invoice;NLD=Verkoopfactuur;NOR=Salgsfaktura;SVE=F”rs.faktura';
      Text002@1100528404 : TextConst 'DEU=Verkaufsgutschrift;ENU=Sales Credit Memo;NLD=Verkoopcreditnota;NOR=Salgskreditnota;SVE=F”rs„ljningskreditnota';
      Text003@1100528405 : TextConst 'DEU=Einkaufsrechnung;ENU=Purchase Invoice;NLD=Inkoopfactuur;NOR=Innkj›psfaktura;SVE=Ink”psfaktura';
      Text004@1100528406 : TextConst 'DEU=Einkaufsgutschrift;ENU=Purchase Credit Memo;NLD=Inkoopcreditnota;NOR=Kj›pskreditnota;SVE=Ink”pskreditnota';

    LOCAL PROCEDURE LookupDocumentNo@1100528403(DocumentType@1100528402 : 'Sales Invoice,Sales Credit Memo,Purchase Invoice,Purchase Credit Memo';VAR DocumentNo@1100528403 : Code[20]) ReturnValue : Boolean;
    VAR
      SalesHeader@1100528401 : Record 36;
      PurchaseHeader@1100528400 : Record 38;
    BEGIN
      CASE DocumentType OF
        DocumentType::"Sales Invoice":
          BEGIN
            SalesHeader.SETRANGE("Document Type", SalesHeader."Document Type"::Invoice);
            SalesHeader."Document Type" :=  SalesHeader."Document Type"::Invoice;
            ReturnValue := LookupSalesDocumentNo(SalesHeader, DocumentNo);
          END;
        DocumentType::"Sales Credit Memo":
          BEGIN
            SalesHeader.SETRANGE("Document Type", SalesHeader."Document Type"::"Credit Memo");
            SalesHeader."Document Type" :=  SalesHeader."Document Type"::"Credit Memo";
            ReturnValue := LookupSalesDocumentNo(SalesHeader, DocumentNo);
          END;
        DocumentType::"Purchase Invoice":
          BEGIN
            PurchaseHeader.SETRANGE("Document Type", PurchaseHeader."Document Type"::Invoice);
            PurchaseHeader."Document Type" :=  PurchaseHeader."Document Type"::Invoice;
            ReturnValue := LookupPurchaseDocumentNo(PurchaseHeader, DocumentNo);
          END;
        DocumentType::"Purchase Credit Memo":
          BEGIN
            PurchaseHeader.SETRANGE("Document Type", PurchaseHeader."Document Type"::"Credit Memo");
            PurchaseHeader."Document Type" :=  PurchaseHeader."Document Type"::"Credit Memo";
            ReturnValue := LookupPurchaseDocumentNo(PurchaseHeader, DocumentNo);
          END;
      END;
    END;

    LOCAL PROCEDURE LookupSalesDocumentNo@1100528405(VAR SalesHeader@1100528402 : Record 36;VAR DocumentNo@1100528403 : Code[20]) ReturnValue : Boolean;
    BEGIN
      SalesHeader."No." := DocumentNo;
      IF PAGE.RUNMODAL(0, SalesHeader) = ACTION::LookupOK THEN BEGIN
        DocumentNo := SalesHeader."No.";
        ReturnValue := TRUE;
      END;
    END;

    LOCAL PROCEDURE LookupPurchaseDocumentNo@1100528408(VAR PurchaseHeader@1100528402 : Record 38;VAR DocumentNo@1100528403 : Code[20]) ReturnValue : Boolean;
    BEGIN
      PurchaseHeader."No." := DocumentNo;
      IF PAGE.RUNMODAL(0, PurchaseHeader) = ACTION::LookupOK THEN BEGIN
        DocumentNo := PurchaseHeader."No.";
        ReturnValue := TRUE;
      END;
    END;

    LOCAL PROCEDURE ValidateDocumentNo@1100528404(DocumentType@1100528402 : 'Sales Invoice,Sales Credit Memo,Purchase Invoice,Purchase Credit Memo';DocumentNo@1100528403 : Code[20]);
    VAR
      SalesHeader@1100528401 : Record 36;
      PurchaseHeader@1100528400 : Record 38;
    BEGIN
      GetDocument(DocumentType, DocumentNo, SalesHeader, PurchaseHeader);
    END;

    LOCAL PROCEDURE ConfirmDeletingDocument@1100528425(DocumentType@1100528402 : 'Sales Invoice,Sales Credit Memo,Purchase Invoice,Purchase Credit Memo';DocumentNo@1100528403 : Code[20]) ReturnValue : Boolean;
    VAR
      SalesHeader@1100528401 : Record 36;
      PurchaseHeader@1100528400 : Record 38;
    BEGIN
      GetDocument(DocumentType, DocumentNo, SalesHeader, PurchaseHeader);
      CASE DocumentType OF
        DocumentType::"Sales Invoice", DocumentType::"Sales Credit Memo":
          BEGIN
            SalesHeader.CALCFIELDS("Amount Including VAT");
            ReturnValue := CONFIRM(
              Text000, FALSE, GetDocumentTypeTranslation(DocumentType), SalesHeader."No.",
              SalesHeader.FIELDCAPTION("Sell-to Customer No."), SalesHeader."Sell-to Customer No.",
              SalesHeader.FIELDCAPTION("Amount Including VAT"), SalesHeader."Amount Including VAT");
          END;
        DocumentType::"Purchase Invoice", DocumentType::"Purchase Credit Memo":
          BEGIN
            PurchaseHeader.CALCFIELDS("Amount Including VAT");
            ReturnValue := CONFIRM(
              Text000, FALSE, GetDocumentTypeTranslation(DocumentType), PurchaseHeader."No.",
              PurchaseHeader.FIELDCAPTION("Buy-from Vendor No."), PurchaseHeader."Buy-from Vendor No.",
              PurchaseHeader.FIELDCAPTION("Amount Including VAT"), PurchaseHeader."Amount Including VAT");
          END;
      END;
    END;

    LOCAL PROCEDURE DeleteDocument@1100528407(DocumentType@1100528402 : 'Sales Invoice,Sales Credit Memo,Purchase Invoice,Purchase Credit Memo';DocumentNo@1100528403 : Code[20]);
    VAR
      SalesHeader@1100528401 : Record 36;
      PurchaseHeader@1100528400 : Record 38;
    BEGIN
      GetDocument(DocumentType, DocumentNo, SalesHeader, PurchaseHeader);
      CASE DocumentType OF
        DocumentType::"Sales Invoice", DocumentType::"Sales Credit Memo":
          BEGIN
            DeleteSalesLines(SalesHeader);
            SalesHeader.DELETE;
          END;
        DocumentType::"Purchase Invoice", DocumentType::"Purchase Credit Memo":
          BEGIN
            DeletePurchaseLines(PurchaseHeader);
            PurchaseHeader.DELETE;
          END;
      END;
    END;

    LOCAL PROCEDURE DeleteSalesLines@1100528417(SalesHeader@1100528402 : Record 36);
    VAR
      SalesLine@1100528401 : Record 37;
    BEGIN
      SalesLine.SETRANGE("Document Type", SalesHeader."Document Type");
      SalesLine.SETRANGE("Document No.", SalesHeader."No.");
      SalesLine.DELETEALL;
    END;

    LOCAL PROCEDURE DeletePurchaseLines@1100528421(PurchaseHeader@1100528402 : Record 38);
    VAR
      PurchaseLine@1100528401 : Record 39;
    BEGIN
      PurchaseLine.SETRANGE("Document Type", PurchaseHeader."Document Type");
      PurchaseLine.SETRANGE("Document No.", PurchaseHeader."No.");
      PurchaseLine.DELETEALL;
    END;

    LOCAL PROCEDURE GetDocument@1100528410(DocumentType@1100528402 : 'Sales Invoice,Sales Credit Memo,Purchase Invoice,Purchase Credit Memo';DocumentNo@1100528403 : Code[20];VAR SalesHeader@1100528404 : Record 36;VAR PurchaseHeader@1100528405 : Record 38);
    BEGIN
      CASE DocumentType OF
        DocumentType::"Sales Invoice":
          SalesHeader.GET(SalesHeader."Document Type"::Invoice, DocumentNo);
        DocumentType::"Sales Credit Memo":
          SalesHeader.GET(SalesHeader."Document Type"::"Credit Memo", DocumentNo);
        DocumentType::"Purchase Invoice":
          PurchaseHeader.GET(PurchaseHeader."Document Type"::Invoice, DocumentNo);
        DocumentType::"Purchase Credit Memo":
          PurchaseHeader.GET(PurchaseHeader."Document Type"::"Credit Memo", DocumentNo);
      END;
    END;

    LOCAL PROCEDURE GetDocumentTypeTranslation@1100528415(DocumentType@1100528400 : 'Sales Invoice,Sales Credit Memo,Purchase Invoice,Purchase Credit Memo') : Text;
    BEGIN
      CASE DocumentType OF
        DocumentType::"Sales Invoice":
          EXIT(Text001);
        DocumentType::"Sales Credit Memo":
          EXIT(Text002);
        DocumentType::"Purchase Invoice":
          EXIT(Text003);
        DocumentType::"Purchase Credit Memo":
          EXIT(Text004);
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

