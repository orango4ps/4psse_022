OBJECT Report 11012014 Create Project Invoices
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00,4PSSE;
  }
  PROPERTIES
  {
    Permissions=TableData 11072003=m;
    CaptionML=[ENU=Create Project Invoices;
               NOR=Opprette prosjektfakturaer;
               SVE=Skapa projektfakturor];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  Window.OPEN( //kkleermaker, 110718, M28248
                    Text007 +
                    Text001 +
                    Text002 +
                    Text003 +
                    Text004);

                  ProjSetup.GET;
                  SalesSetup.GET;

                  NextLinkNo := '1';
                  TmpProjInvBuffer.RESET;
                  TmpProjInvBuffer.DELETEALL;  //* Is a temporary tabel
                  TmpProjInvBuffer2.RESET;
                  TmpProjInvBuffer2.DELETEALL;

                  UnitPriceProductionMot.COPY("Unit Price Production Mot."); //T005358
                END;

    OnPostReport=VAR
                   SaveProjNo@1100525000 : Code[20];
                   InternalCounter@1210190000 : Integer;
                   DocumentNo@1100528600 : Code[20];
                   lvRotInformation@1100285500 : Record 11128101;
                   lvRotInformation2@1100285501 : Record 11128101;
                   LastLineNo@1100285100 : Integer;
                   CustPostGroup@1100285101 : Record 92;
                   lvCustomer@1100285102 : Record 18;
                 BEGIN
                   Window.CLOSE;
                   Window.OPEN(
                     Text000 +
                     Text001 +
                     Text002 +
                     Text003 +
                     Text004);

                   WITH TmpProjInvBuffer DO BEGIN
                     RESET;
                     SETCURRENTKEY("Sorting Order");
                     IF FINDSET THEN
                       REPEAT
                         InsertHeader := TRUE;
                         MultipleProjects := FALSE;
                         MultipleReferences := FALSE;
                         MultipleOrderNos := FALSE;
                         MultipleOrderDates := FALSE;
                         MultipleDim1 := FALSE;
                         AmountLaborForROT := 0; // 140410 ROT
                         Window.UPDATE(1,Principal);
                         CASE "Collect Invoices By" OF
                           "Collect Invoices By"::InstalmPos,
                           "Collect Invoices By"::InstalmNeg,
                           "Collect Invoices By"::InstalmCollectContractAmt: //mg, 27-07-11: C-019332
                             InvoiceInstallments;
                           "Collect Invoices By"::ProductionMotProj,
                           "Collect Invoices By"::ProductionMotPrin,
                           "Collect Invoices By"::ProductionMot:
                             InvoiceProductionMot;
                           ELSE BEGIN
                             SaveProjNo := "Project No.";
                             CollectByMainProjectRun := FALSE;
                             IF "Collect Invoices By" = "Collect Invoices By"::"Main Project" THEN BEGIN
                               TmpProjInvBuffer2.RESET;
                               TmpProjInvBuffer2.SETRANGE("No. Series Debet", "Link No.");
                               IF TmpProjInvBuffer2.FINDFIRST THEN BEGIN
                                 CollectByMainProjectRun := TRUE;
                                 "Project No." := TmpProjInvBuffer2."Project No.";
                               END;
                             END;
                             REPEAT
                               CommissionRec.RESET;
                               CommissionRec.SETRANGE(Principal,Principal);
                               CASE "Collect Invoices By" OF
                                 "Collect Invoices By"::"Main Project":
                                   CommissionRec.SETRANGE("Collect Cost Plus Lines By",CommissionRec."Collect Cost Plus Lines By"::"Main Project");
                                 ELSE
                                   CommissionRec.SETRANGE("Collect Cost Plus Lines By","Collect Invoices By");
                               END;
                               CASE "Collect Invoices By" OF
                                 "Collect Invoices By"::Supervisor:
                                   BEGIN
                                     CommissionRec.SETRANGE(Supervisor,Supervisor);
                                     "Project Principal".COPYFILTER("Project No.", CommissionRec."Project No.");
                                   END;
                                 "Collect Invoices By"::Principal:
                                   "Project Principal".COPYFILTER("Project No.", CommissionRec."Project No.");
                                 "Collect Invoices By"::"Main Project",
                                 "Collect Invoices By"::Project:
                                   CommissionRec.SETRANGE("Project No.","Project No.");
                                 "Collect Invoices By"::Commission:
                                   BEGIN
                                     CommissionRec.SETRANGE("No.","Commission No.");
                                     "Project Principal".COPYFILTER("Project No.", CommissionRec."Project No.");
                                   END;
                                 "Collect Invoices By"::"Settlement Sheet":
                                   BEGIN
                                     CommissionRec.SETRANGE("Project No.","Project No.");
                                     CommissionRec.SETRANGE("No.","Commission No.");
                                   END;
                               END;
                               IF CommissionRec.FIND('-') THEN BEGIN
                                 REPEAT
                                   ProjRec.GET(CommissionRec."Project No.");
                                   TestProject(CommissionRec.Principal);
                                   IF NOT SkipProjTypeSeries(CommissionRec."Project No.") THEN BEGIN
                                     CommissionRec.CALCFIELDS(Prospect,"Supervisor Name");
                                     IF (ProjSetup."Provisions at Closure") THEN BEGIN
                                       IF (ProjRec."Project Status" < ProjRec."Project Status"::Archive) THEN
                                         InvoiceCostPlus;
                                     END ELSE BEGIN
                                       IF (ProjRec."Project Status" < ProjRec."Project Status"::"Administrative Finished") THEN
                                         InvoiceCostPlus;
                                     END;
                                   END;
                                 UNTIL CommissionRec.NEXT = 0;
                               END;
                               IF CollectByMainProjectRun THEN BEGIN
                                 TmpProjInvBuffer2.DELETE;
                                 IF TmpProjInvBuffer2.NEXT = 0 THEN
                                   CollectByMainProjectRun := FALSE
                                 ELSE
                                   "Project No." := TmpProjInvBuffer2."Project No.";
                               END;
                             UNTIL NOT CollectByMainProjectRun;
                             IF "Project No." <> SaveProjNo THEN
                               "Project No." := SaveProjNo;

                             IF NOT InsertHeader THEN
                               SalesHeaderRec.DetermineRemovalContribution;

                             IF MultipleProjects THEN BEGIN
                               SalesHeaderRec."Job No." := '';
                               SalesHeaderRec.MODIFY;
                             END;
                             IF MultipleDim1 THEN BEGIN
                               SalesHeaderRec."Shortcut Dimension 1 Code" := '';
                               SalesHeaderRec.MODIFY;
                             END;
                           END;
                         END;
                         IF MultipleReferences THEN BEGIN
                           SalesHeaderRec."Principal Reference" := '';
                           SalesHeaderRec.MODIFY;
                         END;
                         IF MultipleOrderNos THEN BEGIN
                           SalesHeaderRec."Order No. Customer" := '';
                           SalesHeaderRec.MODIFY;
                         END;
                         IF MultipleOrderDates THEN BEGIN
                           SalesHeaderRec."Commision Date Customer" := 0D;
                           SalesHeaderRec.MODIFY;
                         END;
                         // ITERO.MH I012 ROT Amount calculation 140410 <<<
                         IF (SalesHeaderRec."Job No." <> '') AND (SalesHeaderRec."Document Type" = SalesHeaderRec."Document Type"::Invoice) THEN BEGIN
                            lvRotInformation.RESET();
                            lvRotInformation.SETRANGE(Type, lvRotInformation.Type::Project);
                            lvRotInformation.SETRANGE("Document No.", SalesHeaderRec."Job No.");
                            lvRotInformation.SETRANGE("Project Principle", Principal);
                            IF (lvRotInformation.FINDSET(FALSE)) THEN BEGIN
                               SalesHeaderRec."ROT/RUT reduction" := TRUE;
                               SalesHeaderRec.MODIFY(FALSE);
                               REPEAT
                                  lvRotInformation2 := lvRotInformation;
                                  lvRotInformation2.Type := lvRotInformation2.Type::Invoice;
                                  lvRotInformation2."Invoice No." := SalesHeaderRec."No.";
                                  lvRotInformation2.INSERT();
                                  lvRotInformation2.CopyExtendedROTInformation(lvRotInformation); // 150320
                               UNTIL lvRotInformation.NEXT = 0;
                               IF (AmountLaborForROT > 0) THEN BEGIN
                                  lvRotInformation2.ValidateLaborCost; // 141124
                                  lvRotInformation2.VALIDATE("Labour Cost", AmountLaborForROT);
                                  lvRotInformation2.MODIFY();
                               END;
                               //>>RFC188
                               //add handling charge
                               IF SalesHeaderRec."Bill-to Customer No." <> '' THEN
                                 lvCustomer.GET(SalesHeaderRec."Bill-to Customer No.")
                               ELSE
                                 lvCustomer.GET(SalesHeaderRec."Sell-to Customer No.");
                               CustPostGroup.GET(lvCustomer."Customer Posting Group");
                               IF (CustPostGroup."Taxreduction handling fee"<>'') AND (SalesSetup."ROT+RUT Handling Amt."<>0) THEN BEGIN
                                 LastLineNo := 0;
                                 SalesLineRec.SETRANGE("Document Type",SalesHeaderRec."Document Type");
                                 SalesLineRec.SETRANGE("Document No.",SalesHeaderRec."No.");
                                 IF SalesLineRec.FINDLAST THEN
                                   LastLineNo := SalesLineRec."Line No.";
                                 SalesLineRec.INIT;
                                 SalesLineRec."Document Type" := SalesHeaderRec."Document Type";
                                 SalesLineRec."Document No." := SalesHeaderRec."No.";
                                 SalesLineRec."Line No." := LastLineNo + 10000;
                                 SalesLineRec.INSERT;
                                 SalesLineRec.Type := SalesLineRec.Type::"G/L Account";
                                 SalesLineRec.VALIDATE("No.",CustPostGroup."Taxreduction handling fee");
                                 SalesLineRec.VALIDATE(Quantity, 1);
                                 SalesLineRec.VALIDATE("Unit Price",SalesSetup."ROT+RUT Handling Amt.");
                   //              IF SalesSetup."ROT+RUT Handling Invoice text" <> '' THEN
                                   SalesLineRec.Description := SalesSetup."ROT+RUT Handling Invoice text";
                                 SalesLineRec.MODIFY;
                               END;
                               //<<RFC188
                            END;
                         END;
                         // ITERO.MH I012 ROT AMount calculation 140410 >>>
                       UNTIL NEXT = 0;

                     IF InternalChargeMgt.GetNumInternalCharges > 0 THEN
                       InternalChargeMgt.PostBatch(0, TempGenJnlLine, TmpIcEntry, DocumentNo);  //DP00847.c

                     DELETEALL;
                   END;

                   //Modify Project Status of small projects
                   IF UpdateProjectStatus THEN
                     IF TempProjRec.FIND('-') THEN
                       REPEAT
                         ProjRec := TempProjRec;
                         ProjRec.FIND('='); //necessary for sql
                         ProjRec.VALIDATE("Project Status", ProjRec."Project Status"::"Administrative Finished");
                         ProjRec.MODIFY;
                       UNTIL TempProjRec.NEXT = 0;

                   COMMIT;
                   Window.CLOSE;
                   InternalCounter := InternalChargeMgt.GetNumInternalCharges;  //db, 15-04-11
                   IF (InvCounter = 0) AND (CreditMemoCounter = 0) AND (InternalCounter = 0) THEN
                     // SESB.I022 KD 09.08.2013 >>
                     IF FinalInstNotCreated THEN
                       MESSAGE(Text005 + Text11128230)
                     ELSE
                     // SESB.I022 KD 09.08.2013 <<
                       MESSAGE(Text005)
                   ELSE
                     MESSAGE(
                       Text006,
                       InvCounter, CreditMemoCounter, LineCounter, InternalCounter);
                 END;

  }
  DATASET
  {
    { 8019;    ;DataItem;                    ;
               DataItemTable=Table11072003;
               ReqFilterFields=No.,Global Dimension 1 Code }

    { 7494;1   ;DataItem;                    ;
               DataItemTable=Table11012005;
               DataItemTableView=SORTING(Principal,Project No.);
               PrintOnlyIfDetail=Yes;
               OnPreDataItem=BEGIN
                               InvCounter := 0;
                               CreditMemoCounter := 0;
                               LineCounter := 0;

                               "Project Cost Plus Entry".LOCKTABLE;
                               "Project Installment".LOCKTABLE;
                               SalesHeaderRec.LOCKTABLE;
                               SalesLineRec.LOCKTABLE;
                             END;

               OnAfterGetRecord=VAR
                                  BlockingCode@1100525000 : Record 11012027;
                                BEGIN
                                  ProjRec.GET("Project No.");
                                  IF (ProjSetup."Provisions at Closure") THEN BEGIN
                                    IF (ProjRec."Project Status" >= ProjRec."Project Status"::Archive) THEN
                                      CurrReport.SKIP;
                                    IF (ProjRec."Project Status" = ProjRec."Project Status"::Finished) AND
                                       (ProjRec."Blocking Code" <> '') THEN
                                    BEGIN
                                      IF BlockingCode.CheckBlocked(ProjRec."Blocking Code", BlockingCode.Switch::"Sales Invoice", ProjRec, FALSE) THEN
                                        CurrReport.SKIP;
                                    END;
                                  END ELSE BEGIN
                                    IF (ProjRec."Blocking Code" <> '') AND (ProjRec."Project Status" IN
                                         [ProjRec."Project Status"::"Technical Finished", ProjRec."Project Status"::"Administrative Finished"]) THEN BEGIN
                                      IF BlockingCode.CheckBlocked(ProjRec."Blocking Code", BlockingCode.Switch::"Sales Invoice", ProjRec, FALSE) THEN
                                        CurrReport.SKIP;
                                    END ELSE
                                      IF (ProjRec."Project Status" >= ProjRec."Project Status"::"Administrative Finished") THEN
                                        CurrReport.SKIP;
                                  END;

                                  IF ProjRec.Blocked <> ProjRec.Blocked::" " THEN
                                    CurrReport.SKIP;

                                   // SESB.I022 KD 09.08.2013 >>
                                   IF NOT GenerateInst.CheckFinalInstCreated("Project Principal") THEN BEGIN
                                     FinalInstNotCreated := TRUE;
                                     CurrReport.SKIP;
                                   END;
                                   // SESB.I022 KD 09.08.2013 <<

                                  TmpProjPrincipalRec := "Project Principal";
                                  TmpProjPrincipalRec.INSERT;

                                  Window.UPDATE(1,Principal);
                                  Window.UPDATE(2,"Project No.");
                                  Window.UPDATE(3,'');
                                  Window.UPDATE(4,'');
                                  Window.UPDATE(5,'');
                                  Window.UPDATE(6,'');
                                END;

               ReqFilterFields=Principal,Main Project No.;
               DataItemLink=Project No.=FIELD(No.) }

    { 5136;2   ;DataItem;                    ;
               DataItemTable=Table11012018;
               DataItemTableView=SORTING(Project No.,Principal,Installment No.)
                                 WHERE(Chargeable=CONST(Yes));
               OnAfterGetRecord=VAR
                                  lvProject@1100485000 : Code[20];
                                  lvPlotNo@1100525000 : Code[10];
                                  PaymentTermsCode@1100528400 : Code[10];
                                  AmountInclVAT@1100525001 : Decimal;
                                  InstallmentType@1210190000 : Option;
                                  AdvancePayment@1100529600 : Boolean;
                                BEGIN
                                  Window.UPDATE(3,"Installment No.");
                                  //Test
                                  TESTFIELD("Cost Object");
                                  TESTFIELD("VAT Prod. Posting Group");

                                  TestProject(Principal);
                                  IF NOT TestPlot THEN
                                    CurrReport.SKIP;

                                  //C033117
                                  IF NOT TestInstallmentBasedOnPoints("Project Installment") THEN
                                    CurrReport.SKIP;
                                  //

                                  IF ("Project Principal"."VAT Bus. Posting Group" = '') AND (ProjRec."VAT Bus. Posting Group" = '') THEN
                                    ERROR(Text010, ProjRec.FIELDCAPTION("VAT Bus. Posting Group"), ProjRec."No.", "Project Principal".Principal);

                                  //Fill Invoice Buffer
                                  IF NOT SalesSetup."One Installment per Invoice" THEN BEGIN
                                    //mg.sc, 27-07-11: C-019332
                                    IF "Project Principal"."Collect Installment Invoice By" <>
                                      "Project Principal"."Collect Installment Invoice By"::"Contract Amount/Extensions" THEN
                                      CollectInvPer := TmpProjInvBuffer."Collect Invoices By"::InstalmPos //diekus: 10-09-13 ??
                                                      //* Not only for pos. but for pos. and neg. ('Amount' determines pos./neg.).
                                    ELSE BEGIN
                                      CollectInvPer := TmpProjInvBuffer."Collect Invoices By"::InstalmCollectContractAmt;  //diekus: 10-09-13 ??
                                      IF "Plot No." + "Extension Contract" + Option = '' THEN
                                        InstallmentType := TmpProjInvBuffer."Installment Type"::Main
                                      ELSE
                                        InstallmentType := TmpProjInvBuffer."Installment Type"::Extension;
                                    END;
                                    //mg.ec, 27-07-11: C-019332
                                  END ELSE BEGIN
                                    IF "Invoice Price (LCY)" < 0 THEN
                                      CollectInvPer := TmpProjInvBuffer."Collect Invoices By"::InstalmNeg
                                    ELSE
                                      CollectInvPer := TmpProjInvBuffer."Collect Invoices By"::InstalmPos;
                                  END;

                                  IF "Commission No." <> '' THEN
                                    CommissionRec.GET("Project No.",Principal,"Commission No.")
                                  ELSE
                                    CLEAR(CommissionRec);
                                  IF CommissionRec."Collect Cost Plus Lines By" IN
                                    [CommissionRec."Collect Cost Plus Lines By"::Project,
                                     CommissionRec."Collect Cost Plus Lines By"::"Main Project",
                                     CommissionRec."Collect Cost Plus Lines By"::Principal]
                                  THEN
                                    CommissionRec."No." := '';

                                  IF "Project Principal"."Collect Installment Invoice By" = "Project Principal"."Collect Installment Invoice By"::Principal THEN
                                    lvProject := ''
                                  ELSE
                                    lvProject := "Project No.";
                                  lvPlotNo := '';
                                  IF ("Project Principal"."Collect Installment Invoice By" = "Project Principal"."Collect Installment Invoice By"::Plot) AND
                                    ("Plot No." <> '') THEN BEGIN
                                    lvProject := "Project No.";
                                    lvPlotNo := "Plot No.";
                                  END;
                                  IF ("Project Principal"."Retention %" = 0) AND ("Project Principal"."Retention % 2" = 0) THEN
                                    AdvancePayment := FALSE
                                  ELSE
                                    AdvancePayment := "Advance Payment";
                                  PaymentTermsCode := GetPaymentTermsCode;

                                  AmountInclVAT := CalcAmountInclVat(
                                    "Invoice Price (LCY)",
                                    "VAT Prod. Posting Group",
                                    "Project Principal"."VAT Bus. Posting Group", ProjRec."VAT Bus. Posting Group");
                                  IF "Currency Code" <> '' THEN BEGIN
                                    "Installment Amount (LCY)" :=
                                      CurrencyExchangeRateRec.ExchangeAmtFCYToLCY(
                                        1, "Project No.", WORKDATE, "Currency Code" , "Installment Amount",
                                         CurrencyExchangeRateRec.ExchangeRate(1, "Project No.", WORKDATE,"Currency Code",TRUE),TRUE);
                                  END ELSE BEGIN
                                    "Installment Amount (LCY)" := "Installment Amount";
                                  END;

                                  IF TmpProjInvBuffer.GET(
                                    ProjectTypeRec."Sales Invoice Nos.", ProjectTypeRec."Posted Sales Invoice Nos.",
                                    ProjectTypeRec."Credit Memo Nos.", ProjectTypeRec."Posted Credit Memo Nos.",
                                    Principal, "Project Principal"."Bill-to Customer No.",
                                    "Project Principal"."Alternative Bill-to Address",
                                    lvProject, lvPlotNo, CollectInvPer,'', CommissionRec."No.", '', 0D, '', InstallmentType, //mg.c, 27-07-11: C-019332
                                    '', //DP00887: check on deviating currency not implemented for project installment
                                    AdvancePayment, PaymentTermsCode)
                                  THEN BEGIN
                                    TmpProjInvBuffer.Amount := TmpProjInvBuffer.Amount + AmountInclVAT;
                                    TmpProjInvBuffer.MODIFY;
                                  END ELSE BEGIN
                                    TmpProjInvBuffer.INIT;  // Init does not reset key fields, so fill/empty all keyfields!
                                    TmpProjInvBuffer."No. Series Debet" := ProjectTypeRec."Sales Invoice Nos.";
                                    TmpProjInvBuffer."Posting No. Series Debet" := ProjectTypeRec."Posted Sales Invoice Nos.";
                                    TmpProjInvBuffer."No. Series Credit" := ProjectTypeRec."Credit Memo Nos.";
                                    TmpProjInvBuffer."Posting No. Series Credit" := ProjectTypeRec."Posted Credit Memo Nos.";
                                    TmpProjInvBuffer.Principal := Principal;
                                    TmpProjInvBuffer."Bill-to Customer No." := "Project Principal"."Bill-to Customer No.";
                                    TmpProjInvBuffer."Alternative Bill-to Address" := "Project Principal"."Alternative Bill-to Address";
                                    TmpProjInvBuffer."Project No." := lvProject;
                                    TmpProjInvBuffer."Plot No." := lvPlotNo;
                                    TmpProjInvBuffer."Collect Invoices By" := CollectInvPer;
                                    TmpProjInvBuffer.Supervisor := '';
                                    TmpProjInvBuffer."Commission No." := CommissionRec."No.";
                                    TmpProjInvBuffer."Settl.Sheet No." := '';
                                    TmpProjInvBuffer."Production Date" := 0D;
                                    TmpProjInvBuffer."Production Code" := '';
                                    TmpProjInvBuffer."Installment Type" := InstallmentType; //mg, 27-07-11: C-019332
                                    TmpProjInvBuffer."Currency Code" := '';
                                    TmpProjInvBuffer."Advance Payment" := AdvancePayment;
                                    TmpProjInvBuffer."Payment Terms Code" := PaymentTermsCode;
                                    //
                                    TmpProjInvBuffer.Amount := AmountInclVAT;
                                    SortingOrder := SortingOrder + 1; //01-08-11: 9971;
                                    TmpProjInvBuffer."Sorting Order" := SortingOrder; //01-08-11: 9971;
                                    TmpProjInvBuffer.INSERT;
                                  END;
                                END;

               ReqFilterFields=Project No.,Installment No.,Plot No.,Extension Contract,Option,Block No.;
               DataItemLink=Project No.=FIELD(Project No.),
                            Principal=FIELD(Principal) }

    { 4870;2   ;DataItem;                    ;
               DataItemTable=Table11012019;
               DataItemTableView=SORTING(Project No.,Principal,Commission No.,Settl.Sheet No.,Line No.)
                                 WHERE(Invoiced=CONST(No),
                                       Chargeable=CONST(Yes));
               OnAfterGetRecord=VAR
                                  AmountInclVAT@1100525001 : Decimal;
                                  SalesPrice@1100528900 : Decimal;
                                BEGIN
                                  Window.UPDATE(4,"Commission No.");
                                  Window.UPDATE(5,"Settl.Sheet No.");
                                  Window.UPDATE(6,"Line No.");

                                  //Test
                                  TESTFIELD("Settl.Sheet No.");
                                  SettlRec.GET("Project No.",Principal,"Commission No.","Settl.Sheet No.");
                                  IF SettlRec.Status <> SettlRec.Status::Invoice THEN
                                    CurrReport.SKIP;

                                  ProjPrincipalRec.GET("Project No.",Principal);
                                  ProjPrincipalRec.TESTFIELD("Cost Object");
                                  IF (ProjPrincipalRec."VAT Bus. Posting Group" = '') AND (ProjRec."VAT Bus. Posting Group" = '') THEN
                                    ERROR(Text010, ProjRec.FIELDCAPTION("VAT Bus. Posting Group"), ProjRec."No.", ProjPrincipalRec.Principal);

                                  IF ("Attached to Line No." = 0) AND ("Standard Text Code" = '') THEN
                                    TESTFIELD("VAT Prod. Posting Group");

                                  TestProject(Principal);
                                  CommissionRec.GET("Project No.",Principal,"Commission No.");
                                  IF CommissionRec."Collect Cost Plus Lines By" = CommissionRec."Collect Cost Plus Lines By"::Supervisor THEN
                                    CommissionRec.TESTFIELD(Supervisor);

                                  //Fill Invoice Buffer
                                  ProjNoLinkTable := '';
                                  CostPlusEntryRec := "Project Cost Plus Entry";
                                  CASE CommissionRec."Collect Cost Plus Lines By" OF
                                    CommissionRec."Collect Cost Plus Lines By"::Supervisor:
                                      BEGIN
                                        CostPlusEntryRec."Project No." := '';
                                        CostPlusEntryRec."Commission No." := '';
                                        CostPlusEntryRec."Settl.Sheet No." := '';
                                      END;
                                    CommissionRec."Collect Cost Plus Lines By"::Principal:
                                      BEGIN
                                        CommissionRec.Supervisor := '';
                                        CostPlusEntryRec."Project No." := '';
                                        CostPlusEntryRec."Commission No." := '';
                                        CostPlusEntryRec."Settl.Sheet No." := '';
                                      END;
                                    CommissionRec."Collect Cost Plus Lines By"::Project:
                                      BEGIN
                                        CommissionRec.Supervisor := '';
                                        CostPlusEntryRec."Commission No." := '';
                                        CostPlusEntryRec."Settl.Sheet No." := '';
                                      END;
                                    CommissionRec."Collect Cost Plus Lines By"::"Main Project":
                                      BEGIN
                                        CommissionRec.Supervisor := '';
                                        CostPlusEntryRec."Commission No." := '';
                                        CostPlusEntryRec."Settl.Sheet No." := '';
                                        IF ProjRec."Single/Main/Sub Project" IN
                                          [ProjRec."Single/Main/Sub Project"::"Main Project", ProjRec."Single/Main/Sub Project"::"Sub Project"]
                                        THEN
                                          ProjNoLinkTable := CostPlusEntryRec."Project No.";
                                        IF ProjRec."Single/Main/Sub Project" = ProjRec."Single/Main/Sub Project"::"Sub Project" THEN
                                          CostPlusEntryRec."Project No." := ProjRec."Main Project";
                                      END;
                                    CommissionRec."Collect Cost Plus Lines By"::Commission:
                                      BEGIN
                                        CommissionRec.Supervisor := '';
                                        CostPlusEntryRec."Project No." := '';
                                        CostPlusEntryRec."Settl.Sheet No." := '';
                                      END;
                                    CommissionRec."Collect Cost Plus Lines By"::"Settlement Sheet":
                                      CommissionRec.Supervisor := '';
                                  END;

                                  //DP00887.sn
                                  IF CostPlusEntryRec."Currency Code" <> '' THEN
                                    SalesPrice := "Sales Price (FCY)"
                                  ELSE
                                    SalesPrice := "Sales Price (LCY)";
                                  //DP00887.en

                                  AmountInclVAT := CalcAmountInclVat(
                                    Quantity * DimensionalFactor * SalesPrice,  //DP00887
                                    "VAT Prod. Posting Group",
                                    "Project Principal"."VAT Bus. Posting Group", ProjRec."VAT Bus. Posting Group");

                                  CASE CommissionRec."Collect Cost Plus Lines By" OF
                                    CommissionRec."Collect Cost Plus Lines By"::"Main Project":
                                      CollectInvPer := TmpProjInvBuffer."Collect Invoices By"::"Main Project";
                                    ELSE
                                      CollectInvPer := CommissionRec."Collect Cost Plus Lines By";
                                  END;

                                  IF TmpProjInvBuffer.GET(
                                    ProjectTypeRec."Sales Invoice Nos.",
                                    ProjectTypeRec."Posted Sales Invoice Nos.",
                                    ProjectTypeRec."Credit Memo Nos.",
                                    ProjectTypeRec."Posted Credit Memo Nos.",
                                    Principal,
                                    ProjPrincipalRec."Bill-to Customer No.",
                                    ProjPrincipalRec."Alternative Bill-to Address",
                                    CostPlusEntryRec."Project No.",
                                    '',
                                    CollectInvPer,
                                    CommissionRec.Supervisor,
                                    CostPlusEntryRec."Commission No.",
                                    CostPlusEntryRec."Settl.Sheet No.",
                                    0D,
                                    '',
                                    0,
                                    CostPlusEntryRec."Currency Code",  //DP00887
                                    FALSE,
                                    '')
                                  THEN BEGIN
                                    TmpProjInvBuffer.Amount := TmpProjInvBuffer.Amount + AmountInclVAT;
                                    IF (ProjNoLinkTable <> '') AND (TmpProjInvBuffer."Link No." = '') THEN
                                      TmpProjInvBuffer."Link No." := GetNextLinkNo();
                                    TmpProjInvBuffer.MODIFY;
                                  END ELSE BEGIN
                                    TmpProjInvBuffer.INIT;  // Init does not reset key fields, so fill/empty all keyfields!
                                    TmpProjInvBuffer."No. Series Debet" := ProjectTypeRec."Sales Invoice Nos.";
                                    TmpProjInvBuffer."Posting No. Series Debet" := ProjectTypeRec."Posted Sales Invoice Nos.";
                                    TmpProjInvBuffer."No. Series Credit" := ProjectTypeRec."Credit Memo Nos.";
                                    TmpProjInvBuffer."Posting No. Series Credit" := ProjectTypeRec."Posted Credit Memo Nos.";
                                    TmpProjInvBuffer.Principal := Principal;
                                    TmpProjInvBuffer."Bill-to Customer No." := ProjPrincipalRec."Bill-to Customer No.";
                                    TmpProjInvBuffer."Alternative Bill-to Address" := ProjPrincipalRec."Alternative Bill-to Address";
                                    TmpProjInvBuffer."Project No." := CostPlusEntryRec."Project No.";
                                    TmpProjInvBuffer."Plot No." := '';
                                    TmpProjInvBuffer."Collect Invoices By" := CollectInvPer;
                                    TmpProjInvBuffer.Supervisor := CommissionRec.Supervisor;
                                    TmpProjInvBuffer."Commission No." := CostPlusEntryRec."Commission No.";
                                    TmpProjInvBuffer."Settl.Sheet No." := CostPlusEntryRec."Settl.Sheet No.";
                                    TmpProjInvBuffer."Production Date" := 0D;
                                    TmpProjInvBuffer."Production Code" := '';
                                    TmpProjInvBuffer."Installment Type" := 0;
                                    TmpProjInvBuffer."Currency Code":= CostPlusEntryRec."Currency Code";  //DP00887
                                    TmpProjInvBuffer."Advance Payment" := FALSE;
                                    TmpProjInvBuffer."Payment Terms Code" := '';
                                    //
                                    TmpProjInvBuffer.Amount := AmountInclVAT;
                                    TmpProjInvBuffer."Country/Region of Origin" := CostPlusEntryRec."Country/Region of Origin";
                                    IF (ProjNoLinkTable <> '') THEN
                                      TmpProjInvBuffer."Link No." := GetNextLinkNo();
                                    SortingOrder := SortingOrder + 1;
                                    TmpProjInvBuffer."Sorting Order" := SortingOrder;
                                    TmpProjInvBuffer.INSERT;
                                  END;

                                  IF (ProjNoLinkTable <> '') THEN BEGIN
                                    TmpProjInvBuffer2.RESET;
                                    TmpProjInvBuffer2.SETRANGE("No. Series Debet", TmpProjInvBuffer."Link No.");
                                    TmpProjInvBuffer2.SETRANGE("Project No.", ProjNoLinkTable);
                                    IF TmpProjInvBuffer2.ISEMPTY THEN BEGIN
                                      TmpProjInvBuffer2.INIT;
                                      TmpProjInvBuffer2."No. Series Debet" := TmpProjInvBuffer."Link No.";
                                      TmpProjInvBuffer2."Project No." := ProjNoLinkTable;
                                      SortingOrder := SortingOrder + 1;
                                      TmpProjInvBuffer2."Sorting Order" := SortingOrder;
                                      TmpProjInvBuffer2.INSERT;
                                    END;
                                  END;
                                END;

               ReqFilterFields=Settl.Sheet No.;
               DataItemLink=Project No.=FIELD(Project No.),
                            Principal=FIELD(Principal) }

    { 4325;2   ;DataItem;                    ;
               DataItemTable=Table11020425;
               DataItemTableView=WHERE(Status=CONST(Approved),
                                       Invoiced=CONST(No));
               DataItemLink=Project No.=FIELD(Project No.),
                            Project Principal=FIELD(Principal) }

    { 7342;3   ;DataItem;                    ;
               DataItemTable=Table11020426;
               DataItemTableView=SORTING(Project No.,Project Principal,Extension Contract No.,Production Date,Code,Line No.);
               OnPreDataItem=VAR
                               FPSLicenseManagement@1100528500 : Codeunit 11229289;
                             BEGIN
                               IF NOT FPSLicenseManagement.LicenseAndReadPermissionForTable("Unit Price Production Mot. L.") THEN
                                 CurrReport.BREAK;
                             END;

               OnAfterGetRecord=VAR
                                  lvProj@1100525000 : Code[20];
                                  lvProjPrin@1100525001 : Code[20];
                                  lvProdMot@1100525002 : Code[10];
                                  lvProdMotDate@1100525003 : Date;
                                BEGIN
                                  //**4PS09.n Dataitem inserted

                                  Window.UPDATE(4,"Price List");
                                  Window.UPDATE(5,"Unit Price");

                                  ProjPrincipalRec.GET("Project No.","Project Principal");
                                  ProjPrincipalRec.TESTFIELD("Cost Object");
                                  IF (ProjPrincipalRec."VAT Bus. Posting Group" = '') AND (ProjRec."VAT Bus. Posting Group" = '') THEN
                                    ERROR(Text010, ProjRec.FIELDCAPTION("VAT Bus. Posting Group"), ProjRec."No.", ProjPrincipalRec.Principal);

                                  TestProject("Project Principal");

                                  lvProj := "Project No.";
                                  lvProjPrin := ProjPrincipalRec.Principal;
                                  lvProdMot := Code;
                                  lvProdMotDate := "Production Date";
                                  CASE ProjSetup."Collect Prod.Mot. Inv. By" OF
                                    ProjSetup."Collect Prod.Mot. Inv. By"::"Production Motivation":
                                      BEGIN
                                        CollectInvPer := TmpProjInvBuffer."Collect Invoices By"::ProductionMot;
                                      END;
                                    ProjSetup."Collect Prod.Mot. Inv. By"::Project:
                                      BEGIN
                                        CollectInvPer := TmpProjInvBuffer."Collect Invoices By"::ProductionMotProj;
                                        lvProdMot := '';
                                        lvProdMotDate := 0D;
                                      END;
                                    ProjSetup."Collect Prod.Mot. Inv. By"::Principal:
                                      BEGIN
                                        CollectInvPer := TmpProjInvBuffer."Collect Invoices By"::ProductionMotPrin;
                                        lvProj := '';
                                        lvProdMot := '';
                                        lvProdMotDate := 0D;
                                      END;
                                  END;
                                  IF TmpProjInvBuffer.GET(
                                    ProjectTypeRec."Sales Invoice Nos.",ProjectTypeRec."Posted Sales Invoice Nos.",
                                    ProjectTypeRec."Credit Memo Nos.", ProjectTypeRec."Posted Credit Memo Nos.",
                                    lvProjPrin,ProjPrincipalRec."Bill-to Customer No.",ProjPrincipalRec."Alternative Bill-to Address",lvProj,'',
                                    CollectInvPer,'','','', lvProdMotDate, lvProdMot, 0, //mg.c, 27-07-11: C-019332
                                    '',  //DP00887: check on deviating currency not implemented for Unit Price
                                    FALSE,'')
                                  THEN BEGIN
                                    TmpProjInvBuffer.Amount := TmpProjInvBuffer.Amount + "Production Value";
                                    TmpProjInvBuffer.MODIFY;
                                  END ELSE BEGIN
                                    TmpProjInvBuffer.INIT;  // Init does not reset key fields, so fill/empty all keyfields!
                                    TmpProjInvBuffer."No. Series Debet" := ProjectTypeRec."Sales Invoice Nos.";
                                    TmpProjInvBuffer."Posting No. Series Debet" := ProjectTypeRec."Posted Sales Invoice Nos.";
                                    TmpProjInvBuffer."No. Series Credit" := ProjectTypeRec."Credit Memo Nos.";
                                    TmpProjInvBuffer."Posting No. Series Credit" := ProjectTypeRec."Posted Credit Memo Nos.";
                                    TmpProjInvBuffer.Principal := lvProjPrin;
                                    TmpProjInvBuffer."Bill-to Customer No." := ProjPrincipalRec."Bill-to Customer No.";
                                    TmpProjInvBuffer."Alternative Bill-to Address" := ProjPrincipalRec."Alternative Bill-to Address";
                                    TmpProjInvBuffer."Project No." := lvProj;
                                    TmpProjInvBuffer."Plot No." := '';
                                    TmpProjInvBuffer."Collect Invoices By" := CollectInvPer;
                                    TmpProjInvBuffer.Supervisor := '';
                                    TmpProjInvBuffer."Commission No." := '';
                                    TmpProjInvBuffer."Settl.Sheet No." := '';
                                    TmpProjInvBuffer."Production Date" := lvProdMotDate;
                                    TmpProjInvBuffer."Production Code" := lvProdMot;
                                    TmpProjInvBuffer."Installment Type" := 0;
                                    TmpProjInvBuffer."Currency Code":= '';
                                    TmpProjInvBuffer."Advance Payment" := FALSE;
                                    TmpProjInvBuffer."Payment Terms Code" := '';
                                    //
                                    TmpProjInvBuffer.Amount := "Production Value";
                                    SortingOrder := SortingOrder + 1; //01-08-11: 9971;
                                    TmpProjInvBuffer."Sorting Order" := SortingOrder; //01-08-11: 9971;
                                    TmpProjInvBuffer.INSERT;
                                  END;
                                END;

               DataItemLink=Project No.=FIELD(Project No.),
                            Project Principal=FIELD(Project Principal),
                            Production Date=FIELD(Production Date),
                            Code=FIELD(Code) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=No;
      OnOpenPage=BEGIN
                   TransferCommentLines := TRUE; // **4PS MVOS M28906
                   CheckForInternalProject();  // 160307 ITERO.AC IME443
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1100485000;2;Field  ;
                  CaptionML=[ENU=Change project status to Administrative Finished (Small Cost Plus Projects only);
                             NOR=Endre prosjektstatus til administrativt ferdig (kun lave selvkost-prosjekter);
                             SVE=Žndra projektstatus till administrativt f„rdigt (mindre kostnader och endast projekt)];
                  SourceExpr=UpdateProjectStatus }

      { 1210190000;2;Field  ;
                  CaptionML=[ENU=Transfer Comment Lines;
                             NOR=Overf›r kommentarrader;
                             SVE=™verf”r kommentarsrader];
                  SourceExpr=TransferCommentLines }

      { 1210190001;2;Field  ;
                  CaptionML=[ENU="Extension Text in Invoice Header ";
                             NOR=ETA-tekst i Fakturatittel;
                             SVE="ŽTA-text i fakturarubrik "];
                  SourceExpr=ExtensionTextHeader }

      { 1100409000;2;Field  ;
                  CaptionML=[ENU=Use Sales Price in Internal Invoice;
                             NOR="Bruk salgspris ved internfakturering ";
                             SVE=Anv„nd totalt f”rs„ljningspris vid internfakturering];
                  SourceExpr=gvUseSalesPriceInInternalInvoice;
                  Enabled=gvUseSalesPriceInInternalInvoiceEnabled }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@11012018 : TextConst 'ENU=Creating Invoices...\\;NOR=Oppretter fakturaerÿ...\\;SVE=Skapar fakturorÿ...\\';
      Text001@11012019 : TextConst 'ENU=Principal        #1##########\;NOR=Oppdragsgiver        #1##########\;SVE=Uppdragsgivare        #1##########\';
      Text002@11012020 : TextConst 'ENU=Project          #2##########\;NOR=Prosjekt          #2##########\;SVE=Projekt          #2##########\';
      Text003@11012021 : TextConst 'ENU=Installment No.  #3######\;NOR=Betalingsplansnummer  #3######\;SVE=Betalplansnummer  #3######\';
      Text004@11012022 : TextConst 'ENU=Cost Plus Entry  #4########## #5########## #6######;NOR=Selvkosttransaksjon  #3########## #4########## #5######;SVE=Redovisad sj„lvkostnad  #4########## #5########## #6######';
      Text005@11012023 : TextConst 'ENU=No Invoices Created.;NOR=Ingen fakturaer er opprettet.;SVE=Inga fakturor har skapats.';
      Text006@11012024 : TextConst 'ENU=%1 Invoices, %2 Credit Memos and %3 Invoice Lines and %4 Internal Charge Lines created.;NOR=%1 fakturaer, %2 kreditnotaer, %3 fakturarader og %4 rader for interne gebyr er opprettet.;SVE=%1 fakturor, %2 kreditnotor, %3 fakturarader och %4 interna avgiftsrader har skapats.';
      Text007@11012025 : TextConst 'ENU=Collecting Data...\\;NOR=Samler inn dataÿ...\\;SVE=Samlar in dataÿ...\\';
      ProjSetup@1210190001 : Record 315;
      SalesSetup@1210190000 : Record 311;
      ProjRec@11012000 : Record 11072003;
      PlotRec@1210190003 : Record 11012500;
      InstalmRec@11012001 : Record 11012018;
      SalesHeaderRec@11012002 : Record 36;
      SalesLineRec@11012003 : Record 37;
      ProjectTypeRec@11012004 : Record 11012009;
      CommissionRec@11012005 : Record 11012020;
      SettlRec@11012006 : Record 11012021;
      TmpProjInvBuffer@11012007 : TEMPORARY Record 11012045;
      TmpProjInvBuffer2@1100525008 : TEMPORARY Record 11012045;
      CostPlusEntryRec@11012008 : Record 11012019;
      ProjPrincipalRec@11012009 : Record 11012005;
      TmpProjPrincipalRec@1100485010 : TEMPORARY Record 11012005;
      ExtRec@1210190002 : Record 11012004;
      CustRec@1100485001 : Record 18;
      TempProjRec@1100485009 : TEMPORARY Record 11072003;
      PlantTypeRec@1100485020 : Record 11012551;
      PlantPostingSetupRec@1100485021 : Record 11012570;
      VATPostingSetup@1100485022 : Record 325;
      GenLedgerSetup@1101285000 : Record 98;
      DimValRec@1100525018 : Record 349;
      CurrencyExchangeRateRec@1100528200 : Record 330;
      TempGenJnlLine@1100409000 : TEMPORARY Record 81;
      TmpIcEntry@1100529000 : TEMPORARY Record 11012058;
      UnitPriceProductionMot@1100409001 : Record 11020425;
      Customer@1100529600 : Record 18;
      NoSeriesMngmntCU@1210190005 : Codeunit 396;
      DimMgt@1100525011 : Codeunit 408;
      InternalChargeMgt@1210190009 : Codeunit 11012265;
      Window@11012010 : Dialog;
      CollectInvPer@1210190007 : Option;
      gInvoiceType@1100485023 : 'ProjInstallment,CostPlus,ProductionMot';
      InsertHeader@1210190006 : Boolean;
      InvCounter@11012011 : Integer;
      CreditMemoCounter@11012012 : Integer;
      LineCounter@11012013 : Integer;
      SalesLineNo@11012014 : Integer;
      PrevProjNo@1210190004 : Code[20];
      PrevContractNo@1210190008 : Code[10];
      Text008@1100485002 : TextConst 'ENU="Different WKA data for Cost Plus Entry:  %1, %2, %3, %4, %5 \Make a different selection ";NOR=Forskjellig WKA-data for selvkosttransaksjon:  %1, %2, %3, %4, %5 \Velg noe annet;SVE="Annorlunda WKA-data f”r redovisad sj„lvkostnad:  %1, %2, %3, %4, %5 \V„lj n†got annat "';
      Text009@1100485003 : TextConst 'ENU="Different WKA data for Installment:  %1, %2, %3, %4 \Make a different selection ";NOR=Annerledes WKA-data for avdrag:  %1, %2, %3, %4 \Velg noe annet;SVE="Annorlunda WKA-data f”r extra avbetalning:  %1, %2, %3, %4 \V„lj n†got annat "';
      Text010@1100485006 : TextConst 'ENU=%1 must be filled for Project ''%2'' or for Project Principal ''%3''.;NOR=%1 m† fylles ut for prosjekt ''%2'' eller for prosjektoppdragsgiver ''%3''.;SVE=%1 m†ste fyllas i f”r projekt ''%2'' eller f”r projektuppdragsgivare ''%3''.';
      Text011@1100485025 : TextConst 'ENU=must be zero for internal posting;NOR=m† v‘re null for intern bokf›ring;SVE=m†ste vara noll f”r intern bokf”ring';
      ProjNoLinkTable@1100525004 : Code[20];
      NextLinkNo@1100525006 : Code[10];
      MultipleProjects@1100485000 : Boolean;
      MultipleReferences@1100485004 : Boolean;
      MultipleOrderNos@1100485005 : Boolean;
      MultipleOrderDates@1100485013 : Boolean;
      MultipleDim1@1100485011 : Boolean;
      UpdateProjectStatus@1100485008 : Boolean;
      CollectByMainProjectRun@1100525007 : Boolean;
      RentalPeriod@1100525009 : Text[30];
      OrNoCust@1100485007 : Text[50];
      OrDateCust@1100485012 : Date;
      ProdMotDate@1100525002 : Date;
      ProdMotCode@1100525003 : Code[10];
      gCostPlusAmnt@1100525010 : Decimal;
      gGrossPriceLCY@1100485014 : Decimal;
      gGrossPriceFCY@1100525005 : Decimal;
      gPurchDisc@1100485015 : Decimal;
      gSalesDisc@1100485016 : Decimal;
      gBasicPriceLCY@1100485017 : Decimal;
      gBasicPriceFCY@1100525012 : Decimal;
      gSurchPerc@1100485018 : Decimal;
      gSurchAmntLCY@1100525000 : Decimal;
      gSurchAmntFCY@1100525014 : Decimal;
      gSalesPriceLCY@1100485019 : Decimal;
      gSalesPriceFCY@1100525013 : Decimal;
      gDescr2@1100525001 : Text[50];
      gUPCode@1100525022 : Code[20];
      SortingOrder@1210190010 : Integer;
      TransferCommentLines@1100529800 : Boolean;
      ExtensionTextHeader@1210190011 : Boolean;
      GenerateInst@1100294000 : Codeunit 11012007;
      FinalInstNotCreated@1100294001 : Boolean;
      Text11128230@1100294002 : TextConst 'ENU=\Final Installment is not Calculated.;NOR=\Sluttfaktura er ikke beregnet.;SVE=\Slutfaktura „r inte ber„knad.';
      AmountLaborForROT@1100285500 : Decimal;
      gWithheldAmount@1100285000 : Decimal;
      gvUseSalesPriceInInternalInvoice@1100285300 : Boolean;
      gvUseSalesPriceInInternalInvoiceEnabled@1100285301 : Boolean INDATASET;

    PROCEDURE TestProject@2(CustomerCode@1100529600 : Code[20]);
    VAR
      LedgerbyProjTypeCustGr@1100529601 : Record 11229378;
    BEGIN
      ProjRec.TESTFIELD("Project Type");
      ProjectTypeRec.GET(ProjRec."Project Type");

      IF CustomerCode <> '' THEN BEGIN
        IF Customer."No." <> CustomerCode THEN
          IF NOT Customer.GET(CustomerCode) THEN
            CLEAR(Customer);
        IF Customer."Customer Posting Group" <> '' THEN
          IF LedgerbyProjTypeCustGr.GET(ProjectTypeRec.Code, Customer."Customer Posting Group") THEN;
      END;

      IF LedgerbyProjTypeCustGr."WIP Account Revenue" = '' THEN
        ProjectTypeRec.TESTFIELD("WIP Account Revenue");
      IF (ProjRec."Project Status" >= ProjRec."Project Status"::Finished) AND
         (ProjSetup."Provisions at Closure" = TRUE) AND
         (LedgerbyProjTypeCustGr."Provision Account Revenue" = '')
      THEN
        ProjectTypeRec.TESTFIELD("Provision Account Revenue");
    END;

    PROCEDURE TestPlot@1210190001() : Boolean;
    BEGIN
      IF "Project Installment"."Plot No." <> '' THEN
        IF PlotRec.GET("Project Installment"."Project No.","Project Installment"."Plot No.") THEN
          IF (PlotRec."Interest Applicable") AND (PlotRec."Transfer Date" = 0D) THEN
            EXIT(FALSE);

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE TestInstallmentBasedOnPoints@1100525002(ProjectInstallment@1100525000 : Record 11012018) : Boolean;
    BEGIN
      //C033117
      IF ProjectInstallment.Points <> 0 THEN
        IF ProjectInstallment."Invoiced Price (LCY)" = 0 THEN
          IF ProjectInstallment."Points to be invoiced" = 0 THEN
            EXIT(FALSE);

      EXIT(TRUE);
    END;

    PROCEDURE InvoiceInstallments@6();
    VAR
      SkipRecord@1100485000 : Boolean;
      Qty@1100485001 : Decimal;
      InvPrice@1100485002 : Decimal;
      InstallmentMotivation@1100529701 : Record 11012471;
      RecRef@1100529700 : RecordRef;
      AdvancePayment@1100529600 : Boolean;
      Job@1100285500 : Record 11072003;
      ROTInformation@1100285501 : Record 11128101;
      ROTInformation2@1100285502 : Record 11128101;
      ProjectInstallment@1100285300 : Record 11012018;
    BEGIN
      //reduce parameters for writing saleslines.
      CostPlusEntryRec.INIT;
      CommissionRec.INIT;

      WITH TmpProjInvBuffer DO BEGIN
        TmpProjPrincipalRec.RESET;
        IF "Project No." <> '' THEN
          TmpProjPrincipalRec.SETRANGE("Project No.", "Project No.");
        TmpProjPrincipalRec.SETRANGE(Principal, Principal);
        IF TmpProjPrincipalRec.FIND('-') THEN BEGIN
          REPEAT
            InstalmRec.RESET;
            InstalmRec.SETCURRENTKEY(
              "Project No.", Principal, "Extension Contract", "Plot No.",
              "Main Group", Group, "Sub Group", Option, "Installment No.");

            InstalmRec.COPYFILTERS("Project Installment");
            InstalmRec.FILTERGROUP(4);
            InstalmRec.SETRANGE(Principal);
            InstalmRec.FILTERGROUP(0);
            InstalmRec.SETRANGE("Project No.",TmpProjPrincipalRec."Project No.");
            InstalmRec.SETRANGE(Principal,Principal);
            InstalmRec.SETRANGE(Chargeable,TRUE);
            //mg.sc, 27-07-11: C-019332
      //      IF TmpProjPrincipalRec."One Plot per Installm. Invoice" THEN
            IF TmpProjPrincipalRec."Collect Installment Invoice By" = TmpProjPrincipalRec."Collect Installment Invoice By"::Plot THEN
              InstalmRec.SETRANGE("Plot No.", "Plot No."); //* Not only when plot is filled also when plot is empty!
                                                           //* So don't already process plot level when plot is empty (proj level).
            //mg.ec, 27-07-11: C-019332

            IF "Commission No." <> '' THEN
              InstalmRec.SETRANGE("Commission No.", "Commission No.");
            IF SalesSetup."One Installment per Invoice" THEN BEGIN
              IF "Collect Invoices By" = "Collect Invoices By"::InstalmPos THEN
                InstalmRec.SETFILTER("Invoice Price (LCY)",'>=0')
              ELSE
                InstalmRec.SETFILTER("Invoice Price (LCY)",'<0');
            END;
            IF (TmpProjPrincipalRec."Retention %" = 0) AND (TmpProjPrincipalRec."Retention % 2" = 0) THEN
              AdvancePayment := FALSE
            ELSE BEGIN
              AdvancePayment := "Advance Payment";
              InstalmRec.SETRANGE("Advance Payment", "Advance Payment");
            END;

            IF InstalmRec.FINDSET(TRUE, FALSE) THEN BEGIN
              REPEAT
                InstalmRec.VALIDATE(Chargeable);  //**test status extension contract
                Window.UPDATE(2,InstalmRec."Project No.");
                Window.UPDATE(3,InstalmRec."Installment No.");

                ProjPrincipalRec.GET(InstalmRec."Project No.",Principal);
                ProjRec.GET(InstalmRec."Project No.");
                IF InstalmRec."Extension Contract" <> '' THEN
                  ExtRec.GET(InstalmRec."Project No.", InstalmRec."Extension Contract");

                IF ProjRec."Posting Element Mandatory" THEN       //**4PS04.n
                  InstalmRec.TESTFIELD(Element);

                SkipRecord := FALSE;
                IF SalesSetup."One Installment per Invoice" THEN
                  InsertHeader := TRUE
                ELSE BEGIN
                  IF ("Commission No." = '') AND (InstalmRec."Commission No." <> '') THEN BEGIN
                    CommissionRec.GET(InstalmRec."Project No.",InstalmRec.Principal,InstalmRec."Commission No.");
                    IF NOT (CommissionRec."Collect Cost Plus Lines By" IN
                      [CommissionRec."Collect Cost Plus Lines By"::Project,
                       CommissionRec."Collect Cost Plus Lines By"::"Main Project",
                       CommissionRec."Collect Cost Plus Lines By"::Principal])
                    THEN
                      SkipRecord := TRUE;
                  END;
                  //C033117.sn
                  IF NOT SkipRecord THEN
                    SkipRecord := NOT TestInstallmentBasedOnPoints(InstalmRec);
                  //
                  IF NOT SkipRecord THEN
                    SkipRecord := SkipProjTypeSeries(InstalmRec."Project No.");
                  IF NOT SkipRecord THEN
                    SkipRecord :=
                      ("Project No." = '') AND (ProjPrincipalRec."Collect Installment Invoice By" =
                      ProjPrincipalRec."Collect Installment Invoice By"::Project);
                  IF NOT SkipRecord THEN
                    SkipRecord :=
                      ("Project No." <> '') AND ("Plot No." = '') AND (ProjPrincipalRec."Collect Installment Invoice By" =
                      ProjPrincipalRec."Collect Installment Invoice By"::Principal);
                  //mg.sn, 27-07-11: C-019332
                  IF NOT SkipRecord THEN
                    IF ProjPrincipalRec."Collect Installment Invoice By" =
                      ProjPrincipalRec."Collect Installment Invoice By"::"Contract Amount/Extensions" THEN
                      CASE "Installment Type" OF
                        "Installment Type"::Main:
                          SkipRecord := InstalmRec."Plot No." + InstalmRec."Extension Contract" + InstalmRec.Option <> '';
                        "Installment Type"::Extension:
                          SkipRecord := InstalmRec."Plot No." + InstalmRec."Extension Contract" + InstalmRec.Option = '';
                      END;
                  //mg.en, 27-07-11: C-019332
                  IF NOT SkipRecord THEN
                    IF ("Payment Terms Code" <> '') AND
                       (InstalmRec.GetPaymentTermsCode <> "Payment Terms Code")
                    THEN
                      SkipRecord := TRUE;
                END;

                IF NOT SkipRecord THEN BEGIN
                  OrNoCust := ProjPrincipalRec."Order No. Customer";  //Call C010367.modify
                  OrDateCust := ProjPrincipalRec."Order Date"; //**4PS06.n //Call C010367.modify
                  ProdMotDate := 0D;
                  ProdMotCode := '';
                  RentalPeriod := '';  //DP00495.n

                  //**4PS07.sn
                  gGrossPriceLCY := 0;
                  gGrossPriceFCY := 0;
                  gPurchDisc := 0;
                  gSalesDisc := 0;
                  gBasicPriceLCY := 0;
                  gBasicPriceFCY := 0;
                  gSurchPerc := 0;
                  gSurchAmntLCY := 0;
                  gSurchAmntFCY := 0;
                  gSalesPriceLCY := 0;
                  gSalesPriceFCY := 0;
                  //**4PS07.en

                  IF NOT ProjRec."Internal Project" THEN BEGIN
                    IF InsertHeader THEN BEGIN
                      InsertSalesHeader(
                        InstalmRec."Extension Contract", ProjPrincipalRec."Invoice Text Installments",
                        AdvancePayment, "Payment Terms Code");
                      FillSalesHeaderLaborPerc(InstalmRec."Extension Contract", TRUE, TRUE);
                    END ELSE
                      FillSalesHeaderLaborPerc(InstalmRec."Extension Contract", TRUE, FALSE);
                    //DP01485a.sn
                    InstallmentMotivation.SETRANGE("Project No.", InstalmRec."Project No.");
                    InstallmentMotivation.SETRANGE(Principal, InstalmRec.Principal);
                    //InstallmentMotivation.SETRANGE("Extension Contract", InstalmRec."Extension Contract");  //DP01491a
                    InstallmentMotivation.SETRANGE("Production Date", InstalmRec."Installment Motivation Date");
                    IF InstallmentMotivation.FINDSET THEN
                    //DP01485a.en
                      RecRef.GETTABLE(InstallmentMotivation)
                    ELSE
                      RecRef.GETTABLE(InstalmRec);
                    CopyExternalDocuments(RecRef);

                    //Finishing of last housebuilding installment
                    IF (InstalmRec."Last Installment") AND
                       (ProjRec."Method Last Installment" = ProjRec."Method Last Installment"::Depot) THEN
                    BEGIN
                      ProjPrincipalRec.TESTFIELD("Notary Case Number");
                      SalesHeaderRec."Notary Case Number" := ProjPrincipalRec."Notary Case Number";
                      IF ProjPrincipalRec."Public notary" <> '' THEN
                        SalesHeaderRec."Public notary" := ProjPrincipalRec."Public notary"
                      ELSE BEGIN
                        ProjRec.TESTFIELD("Public notary");
                        SalesHeaderRec."Public notary" := ProjRec."Public notary";
                      END;
                      IF ProjPrincipalRec."Withhold %" <> 0 THEN
                        SalesHeaderRec."Withhold % Last Installment" := ProjPrincipalRec."Withhold %"
                      ELSE BEGIN
                        ProjRec.TESTFIELD("Withhold %");
                        SalesHeaderRec."Withhold % Last Installment" := ProjRec."Withhold %";
                      END;
                      SalesHeaderRec.MODIFY;
                    END;
                  END;

                  IF InstalmRec."Invoice Price (LCY)" < 0 THEN BEGIN
                    Qty := -1;
                    InvPrice := - InstalmRec."Invoice Price (LCY)";
                  END ELSE BEGIN
                    Qty := 1;
                    InvPrice := InstalmRec."Invoice Price (LCY)";
                  END;
                  gDescr2 := '';
                  gUPCode := '';

                  //>> ITERO.LP NAVSE
                  gWithheldAmount := InstalmRec."Withheld Amount";
                  //<< ITERO.LP NAVSE

                  IF ProjRec."Internal Project" THEN BEGIN
                    //db.sn, 15-04-11
                    InternalChargeMgt.InsertTempGLEntry(0, InstalmRec."Project No.",
                      InstalmRec.Description, '', Qty, InvPrice,
                      InstalmRec."Cost Object", InstalmRec."Cost Component",
                      InstalmRec.Element, '', '', TempGenJnlLine, TmpIcEntry,  //DP00847.c
                      0, FALSE);
                    //db.en, 15-04-11
                    //>> 190115 ITERO.SB RFC-1055
                      InstalmRec."Invoiced Amount (Internal)" := InstalmRec."Invoiced Amount (Internal)"+InstalmRec."Invoice Price (LCY)";
                      InstalmRec."Withheld Amount (Internal)" := InstalmRec."Withheld Amount (Internal)"+gWithheldAmount;
                      InstalmRec.MODIFY;
                    //<<
                  END ELSE BEGIN
                    InstalmRec.TESTFIELD(Description);
                    InsertSalesLine(InstalmRec.Description,
                                    Qty,
                                    InvPrice,
                                    InstalmRec."Cost Object",
                                    InstalmRec."VAT Prod. Posting Group",
                                    gInvoiceType::ProjInstallment,
                                    InstalmRec."Installment No.",
                                    InstalmRec."Project No.",
                                    InstalmRec."Commission No.",
                                    '',
                                    0,
                                    SalesLineRec."Cost Type Cost Plus Line"::Revenue,
                                    '',
                                    InstalmRec."Plot No.",
                                    InstalmRec.Element,
                                    InstalmRec."Extension Contract",
                                    0,
                                    InstalmRec."Installment Motivation Date",
                                    InstalmRec."Advance Payment");
                    UpdateSalesHeaderInvText(InstalmRec."Project No.",Principal); //mg, 02-08-11: M24517
                  END;
                  InstalmRec.CALCFIELDS("Invoiced Price", "Invoice in Process", "Credit Memo in Process");
                  InstalmRec."Invoice Price" := InstalmRec."Installment Amount" - InstalmRec."Invoiced Price" -
                                                (InstalmRec."Invoice in Process" + SalesLineRec.RetentionAmount(0)) - InstalmRec."Credit Memo in Process";
                  IF (InstalmRec."Installment Amount" * InstalmRec."Invoice Price" <= 0) OR ProjRec."Internal Project" OR (InstalmRec.Points > 0) THEN BEGIN
                    InstalmRec."Invoice Price" := 0;
                    InstalmRec."Invoice Price (LCY)" := 0;
                  END ELSE BEGIN
                    InstalmRec.SetSuspendAmountCheck;
                    InstalmRec.VALIDATE("Invoice Price");
                  END;
                  InstalmRec."Points to be invoiced" := 0;
                  InstalmRec.Chargeable := FALSE;  //db, 10-09-13: C009275
                  //>> ITERO.LP NAVSE
                  InstalmRec."Withheld Amount" := 0;
                  InstalmRec."Withheld Amount (LCY)" := 0;
                  //<< ITERO.LP NAVSE

                  //>> 161010 ITERO.SB RAD-036 Set "Amount to Invoice" field to amount to invoice (total installment amount - total previous installments)
                  //InstalmRec."Amount to Invoice" := 0;
                  InstalmRec.CALCFIELDS("Invoiced Price","Invoice in Process","Credit Memo in Process","Withheld Amt. (In Process)","Withheld Amt. (Invoiced)","Withheld Amt. (Credit)");
                  //>> 190115 ITERO.SB RFC-1055 Added Invoiced Amount (Internal) and Withheld Amount (Internal) in calculation
                  InstalmRec."Amount to Invoice" := InstalmRec."Installment Amount"-(InstalmRec."Credit Memo in Process"+InstalmRec."Invoice in Process"+InstalmRec."Invoiced Price"+InstalmRec."Invoiced Amount (Internal)"+
                  InstalmRec."Withheld Amount (Internal)"+InstalmRec."Withheld Amt. (In Process)"+InstalmRec."Withheld Amt. (Invoiced)"+InstalmRec."Withheld Amt. (Credit)");
                  //<<
                  InstalmRec.MODIFY;
                END;
              UNTIL InstalmRec.NEXT = 0;
            END;
          UNTIL TmpProjPrincipalRec.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE InvoiceCostPlus@7();
    VAR
      CostPlusDisc@1210190001 : Decimal;
      CostPlusAmnt@1210190000 : Decimal;
      RecRef@1100529700 : RecordRef;
      lvDimensionValue@1100285500 : Record 349;
      lvGeneralLedgerSetup@1100285501 : Record 98;
    BEGIN
      //reduce parameters for writing saleslines.
      InstalmRec.INIT;

      WITH TmpProjInvBuffer DO BEGIN
        Window.UPDATE(2,CommissionRec."Project No.");
        SettlRec.SETRANGE("Project No.",CommissionRec."Project No.");
        SettlRec.SETRANGE(Principal,Principal);
        SettlRec.SETRANGE("Commission No.",CommissionRec."No.");
        IF "Collect Invoices By" = "Collect Invoices By"::"Settlement Sheet" THEN
          SettlRec.SETRANGE("No.","Settl.Sheet No.")
        ELSE
          SettlRec.SETRANGE("No.");
        SettlRec.SETRANGE(Status,SettlRec.Status::Invoice);
        IF SettlRec.FIND('-') THEN
          REPEAT
            CostPlusEntryRec.SETRANGE("Project No.",SettlRec."Project No.");
            CostPlusEntryRec.SETRANGE(Principal,SettlRec.Principal);
            CostPlusEntryRec.SETRANGE("Commission No.",SettlRec."Commission No.");
            CostPlusEntryRec.SETRANGE("Settl.Sheet No.",SettlRec."No.");
            CostPlusEntryRec.SETRANGE(Invoiced,FALSE);
            CostPlusEntryRec.SETRANGE(Chargeable,TRUE);
            //DP00887.sn
            IF TmpProjInvBuffer."Currency Code" <> '' THEN
              CostPlusEntryRec.SETRANGE("Currency Code", TmpProjInvBuffer."Currency Code")
            ELSE
              CostPlusEntryRec.SETFILTER("Currency Code", '%1', '');
            //DP00887.en
            WHILE CostPlusEntryRec.FIND('-') DO BEGIN
              Window.UPDATE(4,CostPlusEntryRec."Commission No.");
              Window.UPDATE(5,CostPlusEntryRec."Settl.Sheet No.");
              Window.UPDATE(6,CostPlusEntryRec."Line No.");

              ProjPrincipalRec.GET(CostPlusEntryRec."Project No.",Principal);
              ProjRec.GET(CostPlusEntryRec."Project No.");
              IF CostPlusEntryRec."Extension Contract" <> '' THEN
                ExtRec.GET(CostPlusEntryRec."Project No.", CostPlusEntryRec."Extension Contract");
              IF ProjRec."Posting Element Mandatory" THEN        //**4PS04.n
                CostPlusEntryRec.TESTFIELD(Element);
              CustRec.GET(Principal);

              CostPlusEntryRec.CALCFIELDS("Cost Type");
              GetCostPlusAmountAndDiscount(CostPlusAmnt, CostPlusDisc);   //DP00887

              OrNoCust :=  SettlRec."Order No. Customer"; //**4PS02.n
              OrDateCust := SettlRec."Commision Date Principal"; //**4PS06.n
              ProdMotDate := 0D;
              ProdMotCode := '';
              RentalPeriod := CostPlusEntryRec."Rental Period";  //DP00495.n

              //**4PS07.sn
              gGrossPriceFCY := CostPlusEntryRec."Gross Price (FCY)";
              gBasicPriceFCY := CostPlusEntryRec."Basic Price (FCY)";
              gSurchAmntFCY := CostPlusEntryRec."Surcharge Amount (FCY)";
              gSalesPriceFCY := CostPlusEntryRec."Sales Price (FCY)";
              gGrossPriceLCY := CostPlusEntryRec."Gross Price (LCY)";
              gBasicPriceLCY := CostPlusEntryRec."Basic Price (LCY)";
              gSurchAmntLCY := CostPlusEntryRec."Surcharge Amount (LCY)";
              gSalesPriceLCY := CostPlusEntryRec."Sales Price (LCY)";
              gPurchDisc := CostPlusEntryRec."Purchase Discount % (Item)";
              gSalesDisc := CostPlusEntryRec."Sales Discount % (Item)";
              gSurchPerc := CostPlusEntryRec."Surcharge %";
              //**4PS07.en

              IF NOT ProjRec."Internal Project" THEN BEGIN
                IF InsertHeader THEN BEGIN
                  InsertSalesHeader(
                    CostPlusEntryRec."Extension Contract", ProjPrincipalRec."Invoice Text Cost Plus", FALSE, ''); //**4PS01.n
                  FillSalesHeaderLaborPerc(CostPlusEntryRec."Extension Contract", FALSE, TRUE);
                END ELSE
                  FillSalesHeaderLaborPerc(CostPlusEntryRec."Extension Contract", FALSE, FALSE);

                RecRef.GETTABLE(SettlRec);
                CopyExternalDocuments(RecRef);
              END;

              gDescr2 := CostPlusEntryRec."Description 2";
              gUPCode := '';
              IF ProjRec."Internal Project" THEN BEGIN
                //db.sn, 15-04-11
                InternalChargeMgt.InsertTempGLEntry(0, CostPlusEntryRec."Project No.",
                  CostPlusEntryRec.Description, CostPlusEntryRec."Description 2",
                  CostPlusEntryRec.Quantity, CostPlusAmnt,
                  CostPlusEntryRec."Cost Object", CostPlusEntryRec."Cost Component",
                  CostPlusEntryRec.Element, CostPlusEntryRec."Unit of Measure", '', TempGenJnlLine, TmpIcEntry, 0, FALSE);  //DP00847.c
                //db.en, 15-04-11
              END ELSE BEGIN
                IF (CostPlusEntryRec."Attached to Line No." = 0) AND (CostPlusEntryRec."Standard Text Code" = '') THEN
                  CostPlusEntryRec.TESTFIELD(Description);
                //call 33642.sn
                IF (CostPlusEntryRec."Cost Object" = '') AND CostPlusEntryRec.CheckItemLine THEN
                   CostPlusEntryRec."Cost Type" := CostPlusEntryRec."Cost Type"::Material;
                //call 33642.en
                InsertSalesLine(CostPlusEntryRec.Description,
                                CostPlusEntryRec.Quantity,
                                gCostPlusAmnt,  //DP01339 (prev: CostPlusAmnt)
                                ProjPrincipalRec."Cost Object",
                                CostPlusEntryRec."VAT Prod. Posting Group",
                                gInvoiceType::CostPlus,
                                '',
                                CostPlusEntryRec."Project No.",
                                CostPlusEntryRec."Commission No.",
                                CostPlusEntryRec."Settl.Sheet No.",
                                CostPlusEntryRec."Line No.",
                                CostPlusEntryRec."Cost Type",
                                CostPlusEntryRec."Unit of Measure",'',
                                CostPlusEntryRec.Element,
                                CostPlusEntryRec."Extension Contract",
                                0,  //DP01339 (prev: CostPlusDisc)
                                CostPlusEntryRec."Execution Date",
                                FALSE);
                UpdateSalesHeaderInvText(CostPlusEntryRec."Project No.", Principal);

                // 140410 New Calculation of ROT Amount <<<
                // *********************************************************************************************************************
                // 1. ROT CALCULATON FROM CostPlus
                // Cost Plus entry is added to a Sales Line. Now check if the entry is ROT-based.
                lvGeneralLedgerSetup.GET;
                IF (lvDimensionValue.GET(lvGeneralLedgerSetup."Global Dimension 2 Code", CostPlusEntryRec."Cost Object")) THEN BEGIN
                   IF (lvDimensionValue."ROT-reduction") THEN AmountLaborForROT += SalesLineRec."Amount Including VAT";
                END;
                // *********************************************************************************************************************
                // 140410 New Calculation of ROT Amount >>>

              END;
              CostPlusEntryRec.Invoiced := TRUE;
              CostPlusEntryRec.Chargeable := FALSE;
              CostPlusEntryRec.MODIFY;
            END;
          UNTIL SettlRec.NEXT = 0;
      END;
    END;

    PROCEDURE InvoiceProductionMot@1100485002();
    VAR
      lvProdMotRec@1100485001 : Record 11020425;
      lvProdMotLineRec@1100485000 : Record 11020426;
      RecRef@1100529700 : RecordRef;
      InstallmentMotivation@1100529701 : Record 11012471;
    BEGIN
      //reduce parameters for writing saleslines.
      CommissionRec.INIT;
      InstalmRec.INIT;
      CostPlusEntryRec.INIT;

      WITH TmpProjInvBuffer DO BEGIN
        lvProdMotRec.RESET;
        lvProdMotRec.COPY(UnitPriceProductionMot);
        lvProdMotRec.FILTERGROUP(8); //C015032
        lvProdMotRec.SETRANGE("Project Principal", Principal);
        OrNoCust := '';
        OrDateCust := 0D;
        RentalPeriod := '';  //DP00495.n
        IF "Collect Invoices By" <> "Collect Invoices By"::ProductionMotPrin THEN BEGIN
          lvProdMotRec.SETRANGE("Project No.", "Project No.");
          OrNoCust := ProjPrincipalRec."Order No. Customer";
          OrDateCust := ProjPrincipalRec."Order Date";
        END;
        IF "Collect Invoices By" = "Collect Invoices By"::ProductionMot THEN BEGIN
          lvProdMotRec.SETRANGE("Production Date", "Production Date");
          lvProdMotRec.SETRANGE(Code, "Production Code");
        END;
        lvProdMotRec.SETRANGE(lvProdMotRec.Invoiced, FALSE);
        lvProdMotRec.SETRANGE(lvProdMotRec.Status, lvProdMotRec.Status::Approved);
        lvProdMotRec.FILTERGROUP(0); //C015032
        IF lvProdMotRec.FINDSET THEN BEGIN
          REPEAT
            IF NOT SkipProjTypeSeries(lvProdMotRec."Project No.") THEN BEGIN
              lvProdMotRec.Invoiced := TRUE;
              lvProdMotRec.MODIFY;

              lvProdMotLineRec.SETRANGE("Project No.", lvProdMotRec."Project No.");
              lvProdMotLineRec.SETRANGE("Project Principal", lvProdMotRec."Project Principal");
              lvProdMotLineRec.SETRANGE("Production Date", lvProdMotRec."Production Date");
              lvProdMotLineRec.SETRANGE(Code, lvProdMotRec.Code);
              IF lvProdMotLineRec.FINDSET THEN BEGIN
                REPEAT
                  Window.UPDATE(2,lvProdMotLineRec."Project No.");
                  Window.UPDATE(4,'');
                  Window.UPDATE(5,'');
                  Window.UPDATE(6,'');

                  ProdMotDate := lvProdMotLineRec."Production Date";
                  ProdMotCode := lvProdMotLineRec.Code;
                  IF NOT ProjRec."Internal Project" THEN BEGIN
                    IF InsertHeader THEN BEGIN
                      InsertSalesHeader('', lvProdMotRec."Invoice Text", FALSE, '');
                      FillSalesHeaderLaborPerc('', TRUE, TRUE); //no need to handle errors in this functions for production motivation
                                                                //always there can be no mix of different WKA percentages on one invoice
                    END ELSE
                      FillSalesHeaderLaborPerc('', TRUE, FALSE);
                    //DP01485a.sn
                    InstallmentMotivation.SETRANGE("Project No.", lvProdMotRec."Project No.");
                    InstallmentMotivation.SETRANGE(Principal, lvProdMotRec."Project Principal");
                    //InstallmentMotivation.SETRANGE("Extension Contract", lvProdMotRec."Extension Contract");
                    InstallmentMotivation.SETRANGE("Production Date", lvProdMotRec."Production Date");
                    IF InstallmentMotivation.FINDSET THEN BEGIN
                    //DP01485a.en
                      RecRef.GETTABLE(InstallmentMotivation);
                      CopyExternalDocuments(RecRef);
                    END;
                  END;

                  ProjPrincipalRec.TESTFIELD("Cost Object");
                  ProjPrincipalRec.TESTFIELD("VAT Prod. Posting Group");
                  gDescr2 := lvProdMotLineRec."Description 2";
                  gUPCode := lvProdMotLineRec."Unit Price";

                  IF ProjRec."Internal Project" THEN BEGIN
                    //db.sn, 15-04-11
                    InternalChargeMgt.InsertTempGLEntry(0, lvProdMotLineRec."Project No.",
                      lvProdMotLineRec.Description, lvProdMotLineRec."Description 2",
                      lvProdMotLineRec.Production,
                      lvProdMotLineRec.CalculatedSalesPrice, //DP00659
                      ProjPrincipalRec."Cost Object", '',
                      lvProdMotLineRec.Element, lvProdMotLineRec."Unit of Measure", '', TempGenJnlLine, TmpIcEntry,  //DP00847.c
                      0, FALSE);
                    //db.en, 15-04-11
                  END ELSE BEGIN
                    IF lvProdMotLineRec.Production <> 0 THEN //call 27189
                      lvProdMotLineRec.TESTFIELD(Description);
                    InsertSalesLine(lvProdMotLineRec.Description,
                                    lvProdMotLineRec.Production,
                                    lvProdMotLineRec.CalculatedSalesPrice, //DP00659
                                    ProjPrincipalRec."Cost Object",
                                    ProjPrincipalRec."VAT Prod. Posting Group",
                                    gInvoiceType::ProductionMot,
                                    '',
                                    lvProdMotLineRec."Project No.",
                                    '',
                                    '',
                                    0,
                                    SalesLineRec."Cost Type Cost Plus Line"::Revenue,
                                    lvProdMotLineRec."Unit of Measure",
                                    '',
                                    lvProdMotLineRec.Element,
                                    lvProdMotLineRec."Extension Contract No.",
                                    0,
                                    0D,
                                    FALSE);
                  END;

                UNTIL lvProdMotLineRec.NEXT = 0;
              END;
            END;
          UNTIL lvProdMotRec.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE InsertSalesHeader@1(ExtContract@1100485000 : Code[10];iInvoiceText@1100525000 : Text[250];AdvancePayment@1100529600 : Boolean;PaymentTermsCode@1100528400 : Code[10]);
    VAR
      ProjPrin@1100409000 : Record 11012005;
    BEGIN
      WITH TmpProjInvBuffer DO BEGIN
        SalesHeaderRec.INIT;
        SalesHeaderRec."No." := '';
        IF Amount < 0 THEN BEGIN
          SalesHeaderRec."Document Type" := SalesHeaderRec."Document Type"::"Credit Memo";
          IF "No. Series Credit" <> '' THEN BEGIN
            SalesHeaderRec."No." := NoSeriesMngmntCU.GetNextNo("No. Series Credit",WORKDATE,TRUE);
            SalesHeaderRec."No. Series" := "No. Series Credit";
          END;
          CreditMemoCounter := CreditMemoCounter + 1;
        END ELSE BEGIN
          SalesHeaderRec."Document Type" := SalesHeaderRec."Document Type"::Invoice;
          IF "No. Series Debet" <> '' THEN BEGIN
            SalesHeaderRec."No." := NoSeriesMngmntCU.GetNextNo("No. Series Debet",WORKDATE,TRUE);
            SalesHeaderRec."No. Series" := "No. Series Debet";
          END;
          InvCounter := InvCounter + 1;
        END;
        SalesHeaderRec."Project Invoice" := TRUE;
        SalesHeaderRec.INSERT(TRUE);

        CASE "Collect Invoices By" OF
          "Collect Invoices By"::InstalmPos,
          "Collect Invoices By"::InstalmNeg,
          "Collect Invoices By"::InstalmCollectContractAmt: //mg.c, 05-12-11: M31253
            BEGIN
              SalesHeaderRec."Job No." := "Project No.";
              SalesHeaderRec."Your Reference" := "Commission No.";
              SalesHeaderRec."Installment Invoice" := TRUE;
            END;
          "Collect Invoices By"::ProductionMotProj,
          "Collect Invoices By"::ProductionMotPrin,
          "Collect Invoices By"::ProductionMot:
            BEGIN
              SalesHeaderRec."Job No." := "Project No.";
              SalesHeaderRec."Your Reference" := "Commission No.";
              SalesHeaderRec."Production Motivation Date" := "Production Date";
              SalesHeaderRec."Production Motivation Code" := "Production Code";
            END;
          "Collect Invoices By"::Supervisor:
            BEGIN
              SalesHeaderRec."Job No." := CostPlusEntryRec."Project No.";
              SalesHeaderRec."Your Reference" := CommissionRec."Supervisor Name";
            END;
          ELSE
            BEGIN
              SalesHeaderRec."Job No." := CostPlusEntryRec."Project No.";
            END;
        END;
        SalesHeaderRec.SetHideValidationDialog(TRUE);
        SalesHeaderRec.SetAdvancePayment(AdvancePayment);
        IF (NOT (ProjRec."Small Project" AND ProjRec."Plant Job Order")) THEN BEGIN
          SalesHeaderRec.VALIDATE("Sell-to Customer No.",Principal);
          IF ("Bill-to Customer No." <> '') AND
             ("Bill-to Customer No." <> SalesHeaderRec."Bill-to Customer No.") THEN
            SalesHeaderRec.VALIDATE("Bill-to Customer No.", "Bill-to Customer No.");
          SalesHeaderRec.VALIDATE("Alternative Bill-to Address","Alternative Bill-to Address");  //C025108.n
        END;
      //>>181009
        ProjRec.GET(SalesHeaderRec."Job No.");
        IF ProjRec."Global Dimension 1 Code" <> '' THEN
          SalesHeaderRec.VALIDATE("Shortcut Dimension 1 Code",ProjRec."Global Dimension 1 Code");
        IF ProjRec."Global Dimension 2 Code" <> '' THEN
          SalesHeaderRec.VALIDATE("Shortcut Dimension 2 Code",ProjRec."Global Dimension 2 Code");
      //<<181009
        FillSalesHeaderContact;
        SalesHeaderRec.FillDefaultSalesPerson();
        SalesHeaderRec.FillDefaultInvoiceLayoutCode();
        IF (PaymentTermsCode <> '') AND
           (SalesHeaderRec."Payment Terms Code" <> PaymentTermsCode)
        THEN
          SalesHeaderRec.VALIDATE("Payment Terms Code", PaymentTermsCode);

        IF SalesHeaderRec."Document Type" = SalesHeaderRec."Document Type"::"Credit Memo" THEN BEGIN
          IF "Posting No. Series Credit" <> '' THEN
            SalesHeaderRec."Posting No. Series" := "Posting No. Series Credit";
        END ELSE
          IF "Posting No. Series Debet" <> '' THEN
            SalesHeaderRec."Posting No. Series" := "Posting No. Series Debet";

        CASE "Collect Invoices By" OF
          "Collect Invoices By"::InstalmPos,
          "Collect Invoices By"::InstalmNeg,
          "Collect Invoices By"::ProductionMotPrin:
            BEGIN
              SalesHeaderRec."Invoice Text" := iInvoiceText;     //ProjPrincipalRec."Invoice Text Installments";
              SalesHeaderRec."Calculate B Amounts based on" := SalesHeaderRec."Calculate B Amounts based on"::"Invoice Amount";
              SalesHeaderRec."WKA Period" := ProjPrincipalRec."WKA Period";
            END;
          ELSE
            BEGIN
              //Cost Plus, ProductionMotProj, ProductionMot
              SalesHeaderRec."Invoice Text" := iInvoiceText; //ProjPrincipalRec."Invoice Text Cost Plus";
              IF ProjRec.GET(SalesHeaderRec."Job No.") THEN
                SalesHeaderRec."Calculate B Amounts based on" := ProjRec."Calculate B Amounts based on";
            END;
        END;
        //**4PS01.sn
        IF ExtContract = '' THEN BEGIN
          SalesHeaderRec."Principal Reference" := ProjPrincipalRec."Principal Reference";
        END ELSE BEGIN
          SalesHeaderRec."Principal Reference" := ExtRec."Your Reference";
        END;
        //**4PS01.en
        //**4PS02.sn
        SalesHeaderRec."Order No. Customer" := OrNoCust;
        //**4PS02.en
        SalesHeaderRec."Commision Date Customer" := OrDateCust; //**4PS06.n
        SalesHeaderRec."Country of Origin" := "Country/Region of Origin";
        IF SalesHeaderRec."Installment Invoice" THEN  // C005444.n
          SalesHeaderRec.VALIDATE("Currency Code", InstalmRec."Currency Code");
        IF (NOT SalesHeaderRec."Installment Invoice") AND (ProjPrincipalRec."Currency Code" <> '') THEN
          SalesHeaderRec.VALIDATE("Currency Code", ProjPrincipalRec."Currency Code");
        //DP00887.sn
        IF TmpProjInvBuffer."Currency Code" <> '' THEN BEGIN
          SalesHeaderRec.VALIDATE("Currency Code", TmpProjInvBuffer."Currency Code");
        END ELSE BEGIN
          IF ProjSetup."Inherit Currency Code CPE from" = ProjSetup."Inherit Currency Code CPE from"::SourceDocument THEN
            //ignore default set by validate customer; accept blank value to create document in local currency (but only for CostPlus)
            //db, 06-10-16: while missing from which table SalesHeader should be created, check on Mandatory field for CostPlus
            IF CostPlusEntryRec.Chargeable THEN  //db, 06-10-16
              SalesHeaderRec.VALIDATE("Currency Code", TmpProjInvBuffer."Currency Code");
        END;
        //DP00887.en
        //DP00640.sn (SEPA-2)
        IF ("Project No." <> '') AND (Principal <> '') THEN BEGIN
          IF ProjPrin.GET("Project No.",Principal) THEN BEGIN
            IF ProjPrin."Payment Method Code (Inv.)" <> '' THEN
              SalesHeaderRec.VALIDATE("Payment Method Code", ProjPrin."Payment Method Code (Inv.)");
          END;
        END;
        //DP00640.en (SEPA-2)
        SalesHeaderRec.GetDirectDebitMandateServiceOrProject('', '', SalesHeaderRec."Job No.", Principal); //DP00640.n (SEPA)
        SalesHeaderRec.MODIFY(TRUE);
        SalesLineNo := 10000;
        InsertHeader := FALSE;
        MultipleProjects := FALSE;
        MultipleReferences := FALSE;  //**4PS01.n
        MultipleOrderNos := FALSE;  //**4PS02.n
        MultipleOrderDates := FALSE; //**4PS06.n
        MultipleDim1 := FALSE;  //**4PS03.n
        PrevProjNo := '';
        PrevContractNo := '';
      END;
    END;

    PROCEDURE UpdateSalesHeaderInvText@1210190002(IProjectNo@1210190005 : Code[20];IPrincipal@1210190006 : Code[20]);
    VAR
      SalesLine@1210190000 : Record 37;
      ExtensionContract@1210190001 : Record 11012004;
      ProjectPrincipal@1210190002 : Record 11012005;
      UseProjectPrincipalText@1210190003 : Boolean;
      InvoiceText@1210190004 : Text[250];
    BEGIN
      SalesLine.SETRANGE("Document Type", SalesHeaderRec."Document Type");
      SalesLine.SETRANGE("Document No.", SalesHeaderRec."No.");
      SalesLine.SETFILTER(Type, '<>%1', SalesLine.Type::" ");
      SalesLine.SETFILTER("Extension Contract", '<>%1', '');
      IF SalesLine.ISEMPTY THEN EXIT;

      SalesLine.FINDFIRST;
      SalesLine.SETFILTER("Job No.", '<>%1', SalesLine."Job No.");
      IF NOT SalesLine.ISEMPTY THEN EXIT;

      SalesLine.SETRANGE("Job No.");
      SalesLine.SETFILTER("Extension Contract", '<>%1', SalesLine."Extension Contract");
      UseProjectPrincipalText := NOT SalesLine.ISEMPTY;

      IF UseProjectPrincipalText THEN
        ProjectPrincipal.GET(IProjectNo, IPrincipal)
      ELSE
        ExtensionContract.GET(SalesLine."Job No.", SalesLine."Extension Contract");

      IF (ExtensionContract."Invoice Text" <> '') AND NOT ExtensionTextHeader THEN
        EXIT;

      IF SalesLine."Installment Invoice" THEN BEGIN
        IF UseProjectPrincipalText THEN
          InvoiceText := ProjectPrincipal."Invoice Text Installments"
        ELSE
          InvoiceText := ExtensionContract."Invoice Text";
      END ELSE BEGIN
        IF UseProjectPrincipalText THEN
          InvoiceText := ProjectPrincipal."Invoice Text Cost Plus"
        ELSE
          InvoiceText := ExtensionContract."Invoice Text Cost Plus";
      END;
      SalesHeaderRec."Invoice Text" := InvoiceText;
      SalesHeaderRec.MODIFY(TRUE);
    END;

    PROCEDURE InsertSalesLine@5(Descr@11012000 : Text[100];Qty@11012001 : Decimal;UnitPriceLCY@11012002 : Decimal;CostObject@11012003 : Code[20];"VAT-ProdGroup"@11012004 : Code[10];InvoiceType@11012005 : 'ProjInstallment,CostPlus,ProductionMot';InstalmNo@11012006 : Code[10];ProjectNo@11012008 : Code[20];CommNo@11012009 : Code[20];SettlNo@11012010 : Code[10];CostPlusEntryNo@11012011 : Integer;CostType@11012013 : 'Labor,Material,Subcontracting,Sundry,Revenues';UnitOfMeasure@11012014 : Code[10];PlotNo@11012015 : Code[10];Elem@11012016 : Code[20];MMC@11012017 : Code[20];DiscPerc@1210190000 : Decimal;LineDate@1100485001 : Date;AdvancePayment@1100529600 : Boolean);
    VAR
      CurrExchRate@1210190003 : Record 330;
      DimValRec@1100485002 : Record 349;
      SalesHeader2@1100528400 : Record 36;
      DimMgt@1100485000 : Codeunit 408;
      UnitPrice@1210190002 : Decimal;
      DimValRec2@1000 : Record 349;
      DimSetID@1100285000 : Integer;
      SESetup@1100525000 : Record 11128004;
    BEGIN
      //when tablefields used (either for costplus or installment), take care of init values.

      ProjRec.GET(ProjectNo);
      ProjectTypeRec.GET(ProjRec."Project Type");

      SalesLineRec.SuspendUpdateVATAmounts(TRUE);

      SalesLineRec.INIT;
      SalesLineRec."Document Type" := SalesHeaderRec."Document Type";
      SalesLineRec."Document No." := SalesHeaderRec."No.";
      SalesLineRec."Line No." := SalesLineNo;
      SalesLineRec."System-Created Entry" := TRUE;
      SalesLineRec."Project Invoice" := TRUE;
      SalesLineRec."Production Motivation Date" := ProdMotDate;
      SalesLineRec."Production Motivation Code" := ProdMotCode;
      SalesLineRec."Unit Price Code" := gUPCode;
      //kzwerver, 110615, sn, #RfC Electr. Purchase Order
      SalesLineRec."Wage Component Cost Plus Line" := CostPlusEntryRec."Wage Component";
      SalesLineRec."Hour Rate Cost Plus Line" := CostPlusEntryRec."Hour Rate Code";
      //kzwerver, 110615, en, #RfC Electr. Purchase Order
      //>> 140326 4PSSE ITERO.SB
      SalesLineRec."Periodic Template Code":= CostPlusEntryRec."Periodic Template Code";
      SalesLineRec."Periodic Starting Date":= CostPlusEntryRec."Periodic Starting Date";
      //<<

      IF ((InvoiceType = InvoiceType::CostPlus) AND
         ((CostPlusEntryRec."Attached to Line No." <> 0))) //OR (CostPlusEntryRec."Standard Text Code" <> '')))  //<<
         OR
         ((TmpProjInvBuffer."Collect Invoices By" >= TmpProjInvBuffer."Collect Invoices By"::ProductionMot) AND (Qty = 0))
      THEN BEGIN
        //Text extensions from cost plus

        SalesLineRec.Type := SalesLineRec.Type::" ";
        IF (NOT (ProjRec."Small Project" AND ProjRec."Plant Job Order")) THEN BEGIN
          SalesLineRec."Sell-to Customer No." := TmpProjInvBuffer.Principal;
          SalesLineRec."Bill-to Customer No." := TmpProjInvBuffer."Bill-to Customer No.";
        END;
        SalesLineRec."Job No." := ProjectNo;
        SalesLineRec.Description := Descr;
        SalesLineRec."Description 2" := gDescr2;
        SalesLineRec."Commission No." := CommNo;
        SalesLineRec."Supervisor Name" := CommissionRec."Supervisor Name";
        SalesLineRec."Failure No." := CommissionRec."Failure No.";
        SalesLineRec."Settl.Sheet No." := SettlNo;
        SalesLineRec."Cost Plus Line No." := CostPlusEntryNo;
        SalesLineRec.Element := Elem;
        SalesLineRec."Extension Contract" := MMC;
      END ELSE BEGIN
        SalesLineRec.Type := SalesLineRec.Type::"G/L Account";
        SalesLineRec."Installment Invoice" := (InvoiceType = InvoiceType::ProjInstallment);
        IF InvoiceType = InvoiceType::ProjInstallment THEN
          SalesLineRec."Installment Motivation" := LineDate;
        IF InvoiceType = InvoiceType::CostPlus THEN
          SalesLineRec."Execution Date" := LineDate;
        //180613
        IF ExtRec."Settlement Method" = ExtRec."Settlement Method"::"Cost Plus" THEN BEGIN
          IF SESetup.GET(SESetup."Line type"::Setup,'EXTENSION','COSTOBJ') THEN
            CostObject := SESetup."Value (txt)";
        END;
        //180613

        SalesLineRec."Shortcut Dimension 2 Code" := CostObject;
        SalesLineRec."Cost Type Cost Plus Line" := CostType;
        SalesLineRec."Cost Object Cost Plus Line" := CostPlusEntryRec."Cost Object";
        SalesLineRec."Employee No." := CostPlusEntryRec."Employee No."; //to validate Job No.
        SalesLineRec.VALIDATE("Job No.", ProjectNo);  //* Must be done before determine account, otherwise it will be overwritten

        IF (NOT (ProjRec."Small Project" AND ProjRec."Plant Job Order")) THEN BEGIN
          SalesLineRec.VALIDATE("Sell-to Customer No.", TmpProjInvBuffer.Principal);
          IF (ProjRec."Project Status" >= ProjRec."Project Status"::Finished) AND
             (ProjSetup."Provisions at Closure" = TRUE) THEN
            SalesLineRec.VALIDATE("No.", ProjectTypeRec."Provision Account Revenue")
          ELSE
            SalesLineRec.VALIDATE("No.", ProjectTypeRec.GetWipRevenueAcc(CostType,
               (InstalmRec."Interest Installment" <> InstalmRec."Interest Installment"::" "),
               COMPANYNAME, SalesHeaderRec."Customer Posting Group"));
          SalesLineRec."Advance Payment" := AdvancePayment;
          SalesLineRec.GetProjTypeRevenueAccount((InstalmRec."Interest Installment" <> InstalmRec."Interest Installment"::" "), FALSE);
        END ELSE BEGIN
          ProjRec.TESTFIELD("Plant Type");
          ProjRec.TESTFIELD("Cost Component Plant");
          PlantTypeRec.GET(ProjRec."Plant Type");
          PlantPostingSetupRec.GET(PlantTypeRec.PlantPostingGrp(ProjRec."Plant No.",'',''),
                                   '', //field for Vendor Posting Group
                                   ProjRec."Cost Component Plant");
          PlantPostingSetupRec.TESTFIELD("Plant Cost Account");
          SalesLineRec.VALIDATE("No.", PlantPostingSetupRec."Plant Cost Account");
        END;
        SalesLineRec."Dimensional Factor" := CostPlusEntryRec."Dimensional Factor";
        //PS, 11-03-08 (call 11005).sn
        IF (SalesHeaderRec."Document Type" = SalesHeaderRec."Document Type"::"Credit Memo") THEN
          SalesLineRec.VALIDATE(Quantity, -1 * Qty)
        ELSE
          SalesLineRec.VALIDATE(Quantity, Qty);
        //PS, 11-03-08 (call 11005).en

        IF SalesHeaderRec."Currency Code" = '' THEN
          UnitPrice := UnitPriceLCY
        ELSE BEGIN
          SalesHeaderRec.TESTFIELD("Currency Factor");
          IF (InvoiceType = InvoiceType::ProjInstallment) AND
             (InstalmRec."Currency Code" <> '') THEN
          BEGIN
            //Amount in FCY is leading
            SalesHeaderRec.TESTFIELD("Currency Code", InstalmRec."Currency Code");
            UnitPrice := InstalmRec."Invoice Price";
            //C002546.sn
            IF InstalmRec."Invoice Price (LCY)" < 0 THEN
              UnitPrice := - UnitPrice; //* Is done fore LCY price before calling, so should be done also voor FCY
            //C002546.en
            UnitPriceLCY :=
              CurrExchRate.ExchangeAmtFCYToLCY(
                1, SalesLineRec."Job No.", SalesHeaderRec."Posting Date", InstalmRec."Currency Code",
                UnitPrice, SalesHeaderRec."Currency Factor",TRUE);
          END ELSE BEGIN
            //DP00887.sn
            IF (InvoiceType = InvoiceType::CostPlus) AND
             (CostPlusEntryRec."Currency Code" <> '') THEN
            BEGIN
              //Amount in FCY is leading
              SalesHeaderRec.TESTFIELD("Currency Code", CostPlusEntryRec."Currency Code");
              UnitPrice := gCostPlusAmnt;
              UnitPriceLCY :=
                CurrExchRate.ExchangeAmtFCYToLCY(
                  1, SalesLineRec."Job No.", SalesHeaderRec."Posting Date", CostPlusEntryRec."Currency Code",
                  UnitPrice, SalesHeaderRec."Currency Factor",TRUE);
            END ELSE BEGIN
              UnitPrice :=
                CurrExchRate.ExchangeAmtLCYToFCY(
                  0, '', SalesHeaderRec."Posting Date",SalesHeaderRec."Currency Code",
                  UnitPriceLCY,SalesHeaderRec."Currency Factor",TRUE);
            END;
            //DP00887.en
          END;
        END;
        SalesLineRec.VALIDATE("Unit Price", UnitPrice);   //PS, 11-03-08 (call 11005).n
      //  SalesLineRec."Employee No." := CostPlusEntryRec."Employee No."; //to validate Job No.
      //  SalesLineRec.VALIDATE("Job No.", ProjectNo);
        //SalesLineRec.UpdateUnitPrice(0);  //DP01339
        SalesLineRec.VALIDATE("VAT Prod. Posting Group", "VAT-ProdGroup");
        IF (ProjPrincipalRec."VAT Bus. Posting Group" <> '') THEN
          SalesLineRec.VALIDATE("VAT Bus. Posting Group", ProjPrincipalRec."VAT Bus. Posting Group")
        ELSE
          SalesLineRec.VALIDATE("VAT Bus. Posting Group", ProjRec."VAT Bus. Posting Group");
        SalesLineRec."Installment No." := InstalmNo;
        SalesLineRec."Commission No." := CommNo;
        SalesLineRec."Supervisor Name" := CommissionRec."Supervisor Name";
        SalesLineRec."Failure No." := CommissionRec."Failure No.";
        SalesLineRec."Settl.Sheet No." := SettlNo;
        SalesLineRec."Cost Plus Line No." := CostPlusEntryNo;
      //SalesLineRec."Amount (LCY)" := UnitPrice * SalesLineRec.Quantity;  //db, 06-10-16 (DP00887): already done by validate UnitPrice
        SalesLineRec."Item No." := CostPlusEntryRec."Item No.";
        SalesLineRec."Serial No." := CostPlusEntryRec."Serial No."; //DP00121
        SalesLineRec."Lot No." := CostPlusEntryRec."Lot No."; //DP00121
        SalesLineRec."Basic Item" := CostPlusEntryRec."Basic Item";
        SalesLineRec."Trade Item" := CostPlusEntryRec."Trade Item";
        SalesLineRec.Manufacturer := CostPlusEntryRec.Manufacturer;
        SalesLineRec."Vendor (Trade Item)" := CostPlusEntryRec."Vendor (Trade Item)";

        //>>4PSSE 150116 SS002067
        IF SalesLineRec."Installment Invoice" THEN BEGIN
          UnitOfMeasure := '';
          GenLedgerSetup.GET;
          IF GenLedgerSetup."Danish Localization Active" THEN BEGIN
            DimValRec2.RESET;
            DimValRec2.SETRANGE("Dimension Code",GenLedgerSetup."Global Dimension 2 Code");   // Filter on Global Dimension 2 Code = Cost Object
            DimValRec2.SETFILTER(Code,SalesLineRec."Shortcut Dimension 2 Code");              // Filter on the Shortcut Dimension 2 Code from the line
            IF DimValRec2.FINDFIRST THEN
              UnitOfMeasure := DimValRec2."Unit of Measure";                                // Fill the Unit of Measure with Unit of Measure of the Cost Object
          END;
        END;
        //<<SS002067

        SalesLineRec.VALIDATE("Unit of Measure Code", UnitOfMeasure);
        //SalesLineRec.VALIDATE("Line Discount %", DiscPerc);  //DP01339
        SalesLineRec."Sales Surcharge Overtime %" := CostPlusEntryRec."Sales Surcharge Overtime %";
        IF CostPlusEntryRec."Plot No." <> '' THEN
          SalesLineRec."Plot No." := CostPlusEntryRec."Plot No."
        ELSE
          SalesLineRec."Plot No." := PlotNo;
        SalesLineRec.Element := Elem;
        SalesLineRec."Extension Contract" := MMC;
        SalesLineRec."Employee No." := CostPlusEntryRec."Employee No.";
        SalesLineRec.VALIDATE(Text, CostPlusEntryRec.Text);
        SalesLineRec.Description := Descr;
        SalesLineRec."Description 2" := gDescr2;
        IF SalesHeader2.GET(SalesLineRec."Document Type", SalesLineRec."Document No.") THEN
          IF (SalesHeader2."Language Code" <> '') AND
             (SalesLineRec."Item No." <> '')
          THEN
            SalesLineRec.GetItemTranslation;
        SalesLineRec."Source Document" := CostPlusEntryRec."Source Document";
      //**4PS01.sn
        IF MMC = '' THEN BEGIN
          SalesLineRec."Principal Reference" := ProjPrincipalRec."Principal Reference";
        END ELSE BEGIN
          SalesLineRec."Principal Reference" := ExtRec."Your Reference";
        END;
      //**4PS01.en
        SalesLineRec."Order No. Customer" := OrNoCust; //**4PS02.n
        SalesLineRec."Commision Date Customer" := OrDateCust; //**4PS06.n
        //** 4PS07.sn
        SalesLineRec."Gross Price (FCY)" := gGrossPriceFCY;
        SalesLineRec."Gross Price (LCY)" := gGrossPriceLCY;
        SalesLineRec."Purchase Discount % (Item)" := gPurchDisc;
        SalesLineRec."Sales Discount % (Item)" := gSalesDisc;
        SalesLineRec."Basic Price (FCY)" := gBasicPriceFCY;
        SalesLineRec."Basic Price (LCY)" := gBasicPriceLCY;
        SalesLineRec."Surcharge %" := gSurchPerc;
        SalesLineRec."Surcharge Amount (FCY)" := gSurchAmntFCY;
        SalesLineRec."Surcharge Amount (LCY)" := gSurchAmntLCY;
        SalesLineRec."Sales Price (FCY)" := gSalesPriceFCY;
        SalesLineRec."Sales Price (LCY)" := gSalesPriceLCY;
        SalesLineRec."Sales Condition Present" := CostPlusEntryRec."Sales Condition Present";  //db, 06-02-20
        SalesLineRec."Price Specification Leading" := TRUE;  //DP01339
        //** 4PS07.en
        SalesLineRec."Rental Period" := RentalPeriod;  //DP00495.n

        //db, 09-07-07: when collect per customer and more then 1 project, the project become empty in invoice header
        //but not cost center that will be copied to invoice lines, although project has different cost center.
        SalesLineRec.VALIDATE("Shortcut Dimension 1 Code", ProjRec."Global Dimension 1 Code");

        IF InvoiceType = InvoiceType::ProjInstallment THEN BEGIN
          SalesLineRec.Points := InstalmRec."Points to be invoiced";
          SalesLineRec."Last Installment" := InstalmRec."Last Installment";
          SalesLineRec."Cost Component" := InstalmRec."Cost Component";
          //**4PS.sn
          IF VATPostingSetup.GET(SalesLineRec."VAT Bus. Posting Group", SalesLineRec."VAT Prod. Posting Group") THEN
          BEGIN
            IF VATPostingSetup.Manually THEN
            BEGIN
              SalesLineRec."Unit Cost" := UnitPrice;
              SalesLineRec."Manually VAT Posting" := TRUE;
              SalesLineRec."VAT %" := 0;
              SalesLineRec."VAT Base Amount" := InstalmRec."Invoice VAT Amount Long Lease";
              SalesLineRec."Amount Including VAT":= InstalmRec."Invoice VAT Amount Long Lease" + UnitPrice;
            END;
          END;
          //**4PS.en
          IF InstalmRec.Option <> '' THEN BEGIN
            IF PlotNo <> '' THEN BEGIN
              PlotRec.GET(ProjectNo, PlotNo);
              SalesLineRec."House Model" := PlotRec."House Model";
            END;
            SalesLineRec."Main Group" := InstalmRec."Main Group";
            SalesLineRec.Group := InstalmRec.Group;
            SalesLineRec."Sub Group" := InstalmRec."Sub Group";
            SalesLineRec.Option := InstalmRec.Option;
          END;

          //>> ITERO.LP NAVSE
          SalesLineRec."Withheld Amount" := gWithheldAmount;
          gWithheldAmount := 0;
          //<< ITERO.LP NAVSE

        END ELSE BEGIN
          SalesLineRec."Cost Entry No. Project Ledger" := CostPlusEntryRec."Entry No. Project Ledger";
          IF SalesLineRec."Shortcut Dimension 2 Code" <> '' THEN BEGIN
            DimMgt.GetDimValueRec(2,SalesLineRec."Shortcut Dimension 2 Code",DimValRec,FALSE,ProjectNo);
            SalesLineRec."Cost Component" := DimValRec."Cost Component";
          END;
          IF SalesLineRec."Cost Component" = '' THEN
            SalesLineRec."Cost Component" := CostPlusEntryRec."Cost Component";
          SalesLineRec."Removal Contribution" := CostPlusEntryRec."Removal Contribution";
          IF CostPlusEntryRec."Removal Contribution" THEN
            SalesLineRec."Attached to Line No. (RC)" := GetAttachedtoLineNoRC(SalesHeaderRec, CostPlusEntryRec);
        END;
        IF (ProjRec."Small Project" AND ProjRec."Plant Job Order") THEN BEGIN
          IF VATPostingSetup.GET(SalesLineRec."VAT Bus. Posting Group", SalesLineRec."VAT Prod. Posting Group") THEN BEGIN
            IF VATPostingSetup."VAT %" <> 0 THEN
              VATPostingSetup.FIELDERROR("VAT %", Text011);
          END;
        END;
      END;
      SalesLineRec.SuspendUpdateVATAmounts(FALSE);
      IF SalesLineRec.Type <> SalesLineRec.Type::" " THEN
        SalesLineRec.UpdateAmounts;  //* this will also execute 'UpdateVATAmounts'
      SalesLineRec.INSERT;

      //>>IME171
      DimSetID := SalesLineRec."Dimension Set ID";
      DimMgt.SetDimensionValueChainsBool(TRUE);
      IF (NOT SalesLineRec."Plant Invoice") THEN
        DimMgt.CheckDepartmentAuthorization(DATABASE::"Sales Line", SalesLineRec."Shortcut Dimension 1 Code");
      DimMgt.ValidateShortcutDimValues(1,SalesLineRec."Shortcut Dimension 1 Code",SalesLineRec."Dimension Set ID");
      DimMgt.ValidateShortcutDimValues(2,SalesLineRec."Shortcut Dimension 2 Code",SalesLineRec."Dimension Set ID");
      DimMgt.SetDimensionValueChainsBool(FALSE);
      IF DimSetID <> SalesLineRec."Dimension Set ID" THEN
        SalesLineRec.MODIFY;
      //<<IME171


      CopyCommentLines();

      ProjRec."Last Invoice date" := TODAY;
      IF ProjRec."Small Project" THEN BEGIN
        IF ProjRec."Small Project Status" < ProjRec."Small Project Status"::Invoice THEN
          ProjRec."Small Project Status" := ProjRec."Small Project Status"::Invoice;
        IF (UpdateProjectStatus) AND (ProjRec."Settlement Method"=ProjRec."Settlement Method"::"Cost Plus" ) THEN
          IF ProjRec."Project Status" < ProjRec."Project Status"::"Administrative Finished" THEN BEGIN
            TempProjRec := ProjRec;
            IF TempProjRec.INSERT THEN;
          END;
      END;
      ProjRec.MODIFY;

      IF SalesHeaderRec."Job No." <> SalesLineRec."Job No." THEN
        MultipleProjects := TRUE;
      IF SalesHeaderRec."Principal Reference" <> SalesLineRec."Principal Reference" THEN
        MultipleReferences := TRUE; //remove reference from header
      IF SalesHeaderRec."Order No. Customer" <> SalesLineRec."Order No. Customer" THEN
        MultipleOrderNos := TRUE;
      IF SalesHeaderRec."Shortcut Dimension 1 Code" <> SalesLineRec."Shortcut Dimension 1 Code" THEN
        MultipleDim1 := TRUE;
      IF SalesHeaderRec."Commision Date Customer" <> SalesLineRec."Commision Date Customer" THEN
        MultipleOrderDates := TRUE;

      SalesLineNo := SalesLineNo + 10000;
      LineCounter := LineCounter + 1;
    END;

    PROCEDURE InsertSalesLineTextInstallm@1210190000();
    BEGIN
      IF (ExtRec."Project No." = PrevProjNo) AND (ExtRec."Contract No." = PrevContractNo) THEN
        EXIT;
      PrevProjNo := ExtRec."Project No.";
      PrevContractNo := ExtRec."Contract No.";

      IF (ExtRec.Description = '') AND (ExtRec."Invoice Text" = '') THEN
        EXIT;

      SalesLineRec.INIT;
      SalesLineRec."Document Type" := SalesHeaderRec."Document Type";
      SalesLineRec."Document No." := SalesHeaderRec."No.";
      SalesLineRec."Line No." := SalesLineNo;
      SalesLineRec."System-Created Entry" := TRUE;
      SalesLineRec."Project Invoice" := TRUE;
      IF InstalmRec."Installment No." <> '' THEN BEGIN
        SalesLineRec."Installment No." := InstalmRec."Installment No.";
        SalesLineRec."Installment Invoice" := TRUE;
      END;
      SalesLineRec."Job No." := ExtRec."Project No.";
      SalesLineRec."Extension Contract" := ExtRec."Contract No.";
      SalesLineRec.Description := ExtRec.Description;

      IF NOT ExtensionTextHeader THEN // C000321.n
        SalesLineRec.VALIDATE(Text, ExtRec."Invoice Text");
      SalesLineRec.INSERT;

      SalesLineNo := SalesLineNo + 10000;
      LineCounter := LineCounter + 1;
    END;

    PROCEDURE FillSalesHeaderLaborPerc@1100485000(ExtensionContract@1100485000 : Code[20];Installm@1100485001 : Boolean;FirstInvLine@1100485003 : Boolean);
    VAR
      PercLabor@1100485005 : Decimal;
      PercBAcc@1100485002 : Decimal;
      lCustRec@1100485004 : Record 18;
    BEGIN
      PercLabor := 0;
      PercBAcc := 0;

      IF ExtensionContract = '' THEN BEGIN
        IF ProjRec."Contract Type" <> ProjRec."Contract Type"::Subcontracting THEN
          EXIT;
      END ELSE BEGIN
        IF ExtRec."Contract Type" <> ExtRec."Contract Type"::Subcontracting THEN
          EXIT;
      END;

      IF ProjPrincipalRec."Specific WKA Percentages" THEN BEGIN
        PercLabor := ProjPrincipalRec."% Labor";
        PercBAcc := ProjPrincipalRec."% to B Account";
      END ELSE BEGIN
        PercLabor := ProjRec."% Labor";
        PercBAcc := ProjRec."% to B Account";
      END;

      IF ExtensionContract <> '' THEN
        IF ExtRec."Specific WKA Percentages" THEN BEGIN
          PercLabor := ExtRec."% Labor";
          PercBAcc := ExtRec."% to B Account";
        END ELSE BEGIN
          IF ProjRec."Contract Type" = ExtRec."Contract Type"::Maincontracting  THEN BEGIN
            IF lCustRec.GET(ExtRec.Principal) THEN BEGIN
              PercLabor := CustRec."% Labor";
              PercBAcc := CustRec."% to B Account";
            END;
          END;
        END;

      IF FirstInvLine THEN BEGIN
        IF (PercLabor <> 0) OR (PercBAcc <> 0) THEN BEGIN
          SalesHeaderRec."% Labor" := PercLabor;
          SalesHeaderRec."% to B Account" := PercBAcc;
          SalesHeaderRec.MODIFY;
        END;
      END ELSE
        IF (PercLabor <> SalesHeaderRec."% Labor") OR
           (PercBAcc <> SalesHeaderRec."% to B Account")
        THEN
          IF Installm THEN
            ERROR(Text009,InstalmRec."Project No.", InstalmRec.Principal, InstalmRec."Plot No.", InstalmRec."Installment No.")
          ELSE
            ERROR(
              Text008,CostPlusEntryRec."Project No.", CostPlusEntryRec.Principal,
              CostPlusEntryRec."Commission No.", CostPlusEntryRec."Settl.Sheet No.",CostPlusEntryRec."Line No.");
    END;

    PROCEDURE FillSalesHeaderContact@1100485001();
    BEGIN
      IF ProjPrincipalRec."Contact Person No." <> '' THEN BEGIN
        IF ProjPrincipalRec."Contact Person No." <> SalesHeaderRec."Sell-to Contact No." THEN
          SalesHeaderRec.VALIDATE("Sell-to Contact No.", ProjPrincipalRec."Contact Person No.");
      END ELSE
        SalesHeaderRec."Sell-to Contact" := '';

      IF ProjPrincipalRec."Bill-to Contact Person" <> '' THEN BEGIN
        IF SalesHeaderRec."Bill-to Contact No." <> ProjPrincipalRec."Bill-to Contact Person" THEN
          SalesHeaderRec.VALIDATE("Bill-to Contact No.", ProjPrincipalRec."Bill-to Contact Person");
      END ELSE
        SalesHeaderRec."Bill-to Contact" := '';
    END;

    PROCEDURE GetNextLinkNo@1100525001() RetNextLinkNo : Code[10];
    BEGIN
      RetNextLinkNo := NextLinkNo;
      NextLinkNo := INCSTR(NextLinkNo);
    END;

    PROCEDURE SkipProjTypeSeries@1100525003(ProjNo@1100525000 : Code[20]) : Boolean;
    VAR
      Project2@1100525001 : Record 11072003;
    BEGIN
      //C025689.sn
      Project2.COPY(Job);
      Project2.FILTERGROUP(8);
      Project2.SETRANGE("No.",ProjNo);
      IF Project2.ISEMPTY THEN
        EXIT(TRUE);
      //C025689.en

      IF ProjRec."No." <> ProjNo THEN
        ProjRec.GET(ProjNo);
      IF ProjectTypeRec.Code <> ProjRec."Project Type" THEN
        ProjectTypeRec.GET(ProjRec."Project Type");
      IF (TmpProjInvBuffer."No. Series Debet" = ProjectTypeRec."Sales Invoice Nos.") AND
         (TmpProjInvBuffer."Posting No. Series Debet" = ProjectTypeRec."Posted Sales Invoice Nos.") AND
         (TmpProjInvBuffer."No. Series Credit" = ProjectTypeRec."Credit Memo Nos.") AND
         (TmpProjInvBuffer."Posting No. Series Credit" = ProjectTypeRec."Posted Credit Memo Nos.") THEN
        EXIT(FALSE)
      ELSE
        EXIT(TRUE);
    END;

    PROCEDURE CalcAmountInclVat@1100525000(Amount@1100525003 : Decimal;VatProdPostingGroup@1100525000 : Code[10];ProjPrincVatBusPostingGroup@1100525006 : Code[10];ProjVatBusPostingGroup@1100525005 : Code[10]) AmountInclVat : Decimal;
    VAR
      VatBusPostingGroup@1100525001 : Code[20];
    BEGIN
      AmountInclVat := Amount;

      IF Amount = 0 THEN
        EXIT;

      IF ProjPrincVatBusPostingGroup <> '' THEN
        VatBusPostingGroup := ProjPrincVatBusPostingGroup
      ELSE
        VatBusPostingGroup := ProjVatBusPostingGroup;

      IF VATPostingSetup.GET(VatBusPostingGroup,VatProdPostingGroup) THEN;

      IF (VATPostingSetup."VAT %" = 0) OR VATPostingSetup.Manually THEN
        EXIT;

      CASE VATPostingSetup."VAT Calculation Type" OF
        VATPostingSetup."VAT Calculation Type"::"Normal VAT":
          AmountInclVat := Amount + Amount * VATPostingSetup."VAT %" / 100;
      END;
    END;

    PROCEDURE CopyCommentLines@1100529801();
    VAR
      ProjectCostPlusEntry@1100529800 : Record 11012019;
      CommentLineFrom@1100529801 : Record 11020634;
      SalesCommentLine@1100529802 : Record 44;
      RecRef@1100529803 : RecordRef;
      LineNo@1100529804 : Integer;
    BEGIN
      IF NOT TransferCommentLines THEN
        EXIT;

      IF ProjectCostPlusEntry.GET(SalesLineRec."Job No.",
                                  SalesLineRec."Sell-to Customer No.",
                                  SalesLineRec."Commission No.",
                                  SalesLineRec."Settl.Sheet No.",
                                  SalesLineRec."Cost Plus Line No.")
      THEN BEGIN
        RecRef.GETTABLE(ProjectCostPlusEntry);

        CommentLineFrom.RESET;
        CommentLineFrom.SETRANGE("Table Name", CommentLineFrom."Table Name"::"Settlement Sheet");
        CommentLineFrom.SETFILTER("Record ID", FORMAT(RecRef.RECORDID()));
        IF CommentLineFrom.FINDSET THEN BEGIN
          SalesCommentLine.RESET;
          SalesCommentLine.SETRANGE("Document Type", SalesLineRec."Document Type");
          SalesCommentLine.SETRANGE("No.", SalesLineRec."Document No.");
          SalesCommentLine.SETRANGE("Document Line No.", SalesLineRec."Line No.");
          IF SalesCommentLine.FINDLAST THEN
            LineNo := SalesLineRec."Line No." + 10000
          ELSE
            LineNo += 10000;

          REPEAT
            SalesCommentLine.INIT;
            SalesCommentLine."Document Type" := SalesLineRec."Document Type";
            SalesCommentLine."No." := SalesLineRec."Document No.";
            SalesCommentLine."Document Line No." := SalesLineRec."Line No.";
            SalesCommentLine."Line No." := LineNo;
            SalesCommentLine.Date := CommentLineFrom.Date;
            SalesCommentLine.Code := CommentLineFrom.Code;
            SalesCommentLine.Comment := CommentLineFrom.Comment;
            SalesCommentLine."Comment Code" := CommentLineFrom."Comment Code";
            SalesCommentLine."Line Break" := CommentLineFrom."Line Break";
            SalesCommentLine."Created by" := CommentLineFrom."Created by";
            SalesCommentLine.INSERT(TRUE);

            LineNo += 10000;
          UNTIL CommentLineFrom.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE GetAttachedtoLineNoRC@1100528600(ISalesHeader@1100528603 : Record 36;IProjectCostPlusEntry@1100528600 : Record 11012019) : Decimal;
    VAR
      ProjectCostPlusEntry@1100528601 : Record 11012019;
      SalesLine@1100528602 : Record 37;
    BEGIN
      IF NOT IProjectCostPlusEntry."Removal Contribution" THEN
        EXIT;
      IF IProjectCostPlusEntry."Attached to Line No. (RC)" = 0 THEN
        EXIT;
      IF NOT ProjectCostPlusEntry.GET(
        IProjectCostPlusEntry."Project No.", IProjectCostPlusEntry.Principal, IProjectCostPlusEntry."Commission No.",
        IProjectCostPlusEntry."Settl.Sheet No.", IProjectCostPlusEntry."Attached to Line No. (RC)")
      THEN
        EXIT;

      SalesLine.SETRANGE("Document Type", ISalesHeader."Document Type");
      SalesLine.SETRANGE("Document No.", ISalesHeader."No.");
      //db.sn, 12-04-13: C006258 (use complete index of CostPlusEntry)
      SalesLine.SETRANGE("Job No.", ProjectCostPlusEntry."Project No.");
      SalesLine.VALIDATE("Sell-to Customer No.", ProjectCostPlusEntry.Principal);
      SalesLine.SETRANGE("Commission No.", ProjectCostPlusEntry."Commission No.");
      SalesLine.SETRANGE("Settl.Sheet No.", ProjectCostPlusEntry."Settl.Sheet No.");
      //db.en, 12-04-13: C006258
      SalesLine.SETRANGE("Cost Plus Line No.", ProjectCostPlusEntry."Line No.");
      IF SalesLine.FINDFIRST THEN
        EXIT(SalesLine."Line No.");
    END;

    LOCAL PROCEDURE GetCostPlusAmountAndDiscount@1100525004(VAR CostPlusAmnt@1100525000 : Decimal;VAR CostPlusDisc@1100525001 : Decimal);
    BEGIN
      //DP00887
      WITH CostPlusEntryRec DO BEGIN
        IF "Currency Code" <> '' THEN BEGIN
          CostPlusDisc := 0;
          CostPlusAmnt := "Sales Price (FCY)";
          //IF CustRec."Item Price Cost Plus Project" = CustRec."Item Price Cost Plus Project"::GrossMin THEN BEGIN
          IF (CustRec."Item Price Cost Plus Project" = CustRec."Item Price Cost Plus Project"::GrossMin) AND (NOT gvUseSalesPriceInInternalInvoice) THEN BEGIN // 160307 ITERO.AC IME443 Added UseSalesPriceInInternalInvoice
            IF ("Gross Price (FCY)" > "Sales Price (FCY)") AND ("Gross Price (FCY)" <> 0) THEN BEGIN
              CostPlusAmnt := "Gross Price (FCY)";
              CostPlusDisc := 100 * ("Gross Price (FCY)" - "Sales Price (FCY)") / "Gross Price (FCY)";
            END ELSE BEGIN
              IF ("Basic Price (FCY)" > "Sales Price (FCY)") AND ("Basic Price (FCY)" <> 0) THEN BEGIN
                CostPlusAmnt := "Basic Price (FCY)";
                CostPlusDisc := 100 * ("Basic Price (FCY)" - "Sales Price (FCY)") / "Basic Price (FCY)";
              END;
            END;
          END;
        END ELSE BEGIN
          CostPlusDisc := 0;
          CostPlusAmnt := "Sales Price (LCY)";
          //IF CustRec."Item Price Cost Plus Project" = CustRec."Item Price Cost Plus Project"::GrossMin THEN BEGIN
          IF (CustRec."Item Price Cost Plus Project" = CustRec."Item Price Cost Plus Project"::GrossMin) AND (NOT gvUseSalesPriceInInternalInvoice) THEN BEGIN // 160307 ITERO.AC IME443 Added UseSalesPriceInInternalInvoice
            IF ("Gross Price (LCY)" > "Sales Price (LCY)") AND ("Gross Price (LCY)" <> 0) THEN BEGIN
              CostPlusAmnt := "Gross Price (LCY)";
              CostPlusDisc := 100 * ("Gross Price (LCY)" - "Sales Price (LCY)") / "Gross Price (LCY)";
            END ELSE BEGIN
              IF ("Basic Price (LCY)" > "Sales Price (LCY)") AND ("Basic Price (LCY)" <> 0) THEN BEGIN
                CostPlusAmnt := "Basic Price (LCY)";
                CostPlusDisc := 100 * ("Basic Price (LCY)" - "Sales Price (LCY)") / "Basic Price (LCY)";
              END;
            END;
          END;
        END;
      END;
      gCostPlusAmnt := CostPlusAmnt * (100-CostPlusDisc)/100;  //DP01339
    END;

    PROCEDURE CopyExternalDocuments@1100525005(VAR IRecRefFrom@1100525000 : RecordRef);
    VAR
      IRecRefTo@1100529700 : RecordRef;
      DocumentLink@1100525004 : Record 11012747;
      DocumentProperties@1100525005 : Record 11012746;
      DocumentLinkMgt@1100525009 : Codeunit 11012401;
      DocumentLink2@1100529701 : Record 11012747;
      DocumentRelation@1100529702 : Record 11012407;
    BEGIN
      DocumentLink.SETCURRENTKEY("Record ID");
      DocumentLink.SETRANGE("Table No.",IRecRefFrom.NUMBER);
      DocumentLink.SETFILTER("Record ID", STRSUBSTNO('''%1''', IRecRefFrom.RECORDID));
      IF DocumentLink.FINDSET THEN BEGIN
        IRecRefTo.GETTABLE(SalesHeaderRec);
        REPEAT
          IF DocumentProperties.GET(DocumentLink."Document No.") THEN BEGIN
            DocumentLink2.SETRANGE("Document No.", DocumentLink."Document No.");
            DocumentLink2.SETRANGE("Table No.", IRecRefTo.NUMBER);
            DocumentLink2.SETFILTER("Record ID", STRSUBSTNO('''%1''', IRecRefTo.RECORDID));
            IF DocumentLink2.ISEMPTY THEN BEGIN
              DocumentLinkMgt.CreateOneDocumentLink(DocumentProperties,IRecRefTo.RECORDID);

              DocumentRelation.INIT;
              CASE SalesHeaderRec."Document Type" OF
                SalesHeaderRec."Document Type"::Invoice : DocumentRelation."Document Type" := DocumentRelation."Document Type"::"Sales Invoice";
                SalesHeaderRec."Document Type"::"Credit Memo" : DocumentRelation."Document Type" := DocumentRelation."Document Type"::"Sales Cr.Memo";
                SalesHeaderRec."Document Type"::Order : DocumentRelation."Document Type" := DocumentRelation."Document Type"::"Sales Order";
                SalesHeaderRec."Document Type"::Quote : DocumentRelation."Document Type" := DocumentRelation."Document Type"::"Sales Quote";
              END;
              DocumentRelation."No." := SalesHeaderRec."No.";
              DocumentRelation.VALIDATE("Related Document No.", DocumentLink."Document No.");
              DocumentRelation.Description := COPYSTR(DocumentProperties.Description, 1, MAXSTRLEN(DocumentRelation.Description));  //C042586: 50->30 !
              DocumentRelation."Send by E-Mail" := TRUE;
              IF DocumentProperties.FileCanBeConvertedToPDF THEN
                DocumentRelation.Print := TRUE;
              DocumentRelation.INSERT(TRUE);
            END
          END
        UNTIL DocumentLink.NEXT = 0;
      END;
    END;

    PROCEDURE CheckForInternalProject@1100409000();
    VAR
      lvJobsSetupRec@1100409000 : Record 315;
      lvJobRec@1100409002 : Record 11072003;
      lvPrincipalInternalProj@1100409001 : Code[20];
    BEGIN
      //>> 160307 ITERO.AC IME443, New function used to set Check Box gvUseSalesPriceInInternalInvoice enabled True/False

      // Determine if current project is an Internal Project
      lvJobsSetupRec.GET;
      lvPrincipalInternalProj := lvJobsSetupRec."Principal Internal Project";

      lvJobRec.SETFILTER("No.",Job.GETFILTER("No."));
        IF lvJobRec.FINDFIRST THEN BEGIN
          IF lvJobRec."Bill-to Customer No." = lvPrincipalInternalProj THEN BEGIN
            gvUseSalesPriceInInternalInvoiceEnabled := TRUE;
            gvUseSalesPriceInInternalInvoice := TRUE;
          END ELSE BEGIN
            gvUseSalesPriceInInternalInvoiceEnabled := FALSE;
            gvUseSalesPriceInInternalInvoice := FALSE;
          END;
        END;
      //<< 160307 ITERO.AC IME443
    END;

    BEGIN
    {
      4PS01 07-11-2006 JD: The field Principal Reference (from table Project Principal) or Your Reference (from table Extension Contract)
      must also be transfered to sales lines. And if the same for all lines, then also transfer it to the header.
      4PS02 13-11-2006 JD: Call C-006438 (and 10051) The field Order No. Customer (from Settlement Sheet / Job)
      must also be transfered to sales lines. And if the same for all lines, then also transfer it to the header.
      4PS03 09-07-2007 DB: validate dimension 1 changed (header/lines)
      4PS04 JD, 8 aug 2007, Call 5990, "Posting Element Mandatory" also on project card
      4PS05 GB, 04-10-2007, Call 9071, The field 'Your Reference' deleted from salesline (only printed in header),
      4PS06 JD, 12 nov 2007, Call 9985, Commision Date to from Settlement Sheet and then to invoice.
      4PS07 JD 07-02-08 Call 9828, How prices of items are determined is not visible on sales line
      4PS08 PS 11-03-08 Call 11005, Quantity in Credit Memo is filled Correctly now
      4PS09 JD 14-08-08 Invoice can be created from Unit Price Production Motivation
      4PS10 JD 11-03-08 Call 14344 Unit Price Production Motivations can be collected by Project or Principal
      140326 4PSSE ITERO.SB Changed function InsertSalesLine (handle periodic template)
      SESB.I022 Final Instalment
         KD 09.08.2013 Trigger "Project Principal - OnAfterGetRecord" changed
         KD 09.08.2013 Trigger "Report - OnPostReport" changed

      *** 4PSSE.I022 130923
                    Added code to handle ROT information.
      140227 ENH020 ROT from fixed price projects
      140324 ENH020 change to ENH020, project -> principal
      140410 ITERO.MH I012 Major Change of ROT calculation
      140905 ITERO.DL IME171 Dimensions are not extended due to lack of VALIDATEs
      141124 ITERO.MH Fixed validation of labor cost for ROT
      150116 ITERO.DL SS002067 Installment Unit of Measurement on invoice
      150320 ITERO.MH Added ROT Extended ROT Information
      150902 ITERO.SB RFC-001 Handle standard text code lines
      160315 ITERO.LP Handling of "Withheld Amount".
      160307 ITERO.AC IME443 New check box "Use Sales Price in Internal Invoice" in request page and changed IF clause in InvoiceCostPlus()
      161010 ITERO.SB RAD-036 Set "Amount to Invoice" field to amount to invoice (total installment amount - total previous installments)
      170405 ITERO.DL RFC188 enhance ROT-handling and extend with RUT
      170822 ITERO.DL clear InstalmRec."Withheld Amount (LCY)" when InstalmRec."Withheld Amount" is cleared
      180613 ITERO.PR IF Extension and Cost Plus - reading SE Setup for "Setup,'EXTENSION','COSTOBJ'
      181006 ORANGO.DL Department code (global dim 1) and CostObject (global dim 2) from Job to SalesHead
      190115 ITERO.SB RFC-1055 Show invoiced amount (Internal projects)
    }
    END.
  }
  RDLDATA
  {
  }
}

