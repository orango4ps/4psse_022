OBJECT Codeunit 11012842 Service-Contract Info Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE GetNoOfContracts@1100528600(ICustomerNo@1100528600 : Code[20]) NoContracts : Integer;
    VAR
      ServiceContract@1100528601 : Record 11012812;
    BEGIN
      //C026176.sn
      IF ICustomerNo = '' THEN
        EXIT(0);
      //C026176.en

      WITH ServiceContract DO BEGIN
        SETCURRENTKEY("Customer No.");
        SETRANGE("Customer No.",ICustomerNo);
        EXIT(COUNT);
      END;
    END;

    PROCEDURE ShowContracts@1100528608(ICustomerNo@1100528600 : Code[20]);
    VAR
      ServiceContract@1100528601 : Record 11012812;
    BEGIN
      WITH ServiceContract DO BEGIN
        SETCURRENTKEY("Customer No.");
        SETRANGE("Customer No.",ICustomerNo);
        IF ISEMPTY THEN
          EXIT;
      END;
      PAGE.RUN(PAGE::"Service Contracts List",ServiceContract);
    END;

    PROCEDURE GetNoOfCustControl@1100528615(ICustomerNo@1100528601 : Code[20]) NoOfRecords : Integer;
    VAR
      ServiceControlCustomer@1100528600 : Record 11071763;
    BEGIN
      //C026176.sn
      IF ICustomerNo = '' THEN
        EXIT(0);
      //C026176.en

      WITH ServiceControlCustomer DO BEGIN
        SETRANGE("Customer No.", ICustomerNo);
        IF FINDLAST THEN
          SETRANGE("Reporting Date", "Reporting Date");
        EXIT(COUNT);
      END;
    END;

    PROCEDURE GetNoOfGlobalDim1Control@1100528613(IGlobalDim1Code@1100528600 : Code[20]) : Integer;
    VAR
      ServiceControlGlobalDim1@1100528601 : Record 11071764;
    BEGIN
      IF IGlobalDim1Code = '' THEN EXIT(0);  //C026680
      WITH ServiceControlGlobalDim1 DO BEGIN
        SETRANGE("Global Dimension 1 Code", IGlobalDim1Code);
        IF FINDLAST THEN
          SETRANGE("Reporting Date", "Reporting Date");
        EXIT(COUNT);
      END;
    END;

    PROCEDURE GetNoOfContractGroupControl@1100528614(IContractGroup@1100528600 : Code[20]) : Integer;
    VAR
      ServiceControlContractGroup@1100528601 : Record 11071787;
    BEGIN
      IF IContractGroup = '' THEN EXIT(0);  //C026680
      WITH ServiceControlContractGroup DO BEGIN
        SETRANGE("Contract Group", IContractGroup);
        IF FINDLAST THEN
          SETRANGE("Reporting Date", "Reporting Date");
        EXIT(COUNT);
      END;
    END;

    PROCEDURE ShowCustomerContractControl@1100528612(ICustomerNo@1100528600 : Code[20]);
    VAR
      ServiceControlCustomer@1100528601 : Record 11071763;
    BEGIN
      WITH ServiceControlCustomer DO BEGIN
        SETRANGE("Customer No.", ICustomerNo);
        IF FINDLAST THEN
          SETRANGE("Reporting Date", "Reporting Date")
        ELSE
          EXIT;
        IF COUNT = 1 THEN
          PAGE.RUN(PAGE::"Service-Contract Control Cust.", ServiceControlCustomer)
        ELSE
          PAGE.RUN(PAGE::"Service Control Cust. L", ServiceControlCustomer)
      END;
    END;

    PROCEDURE ShowGlobalDim1Control@1100528616(IGlobalDim1Code@1100528600 : Code[20]);
    VAR
      ServiceControlGlobalDim1@1100528601 : Record 11071764;
    BEGIN
      WITH ServiceControlGlobalDim1 DO BEGIN
        SETRANGE("Global Dimension 1 Code", IGlobalDim1Code);
        IF FINDLAST THEN
          SETRANGE("Reporting Date", "Reporting Date")
        ELSE
          EXIT;
        IF COUNT = 1 THEN
          PAGE.RUN(PAGE::"SC Control Global Dim. 1", ServiceControlGlobalDim1)
        ELSE
          PAGE.RUN(PAGE::"Serv. Contr. Glob. Dim. 1 List", ServiceControlGlobalDim1);
      END;
    END;

    PROCEDURE ShowContractGroupControl@1100528619(IContractGroup@1100528600 : Code[20]);
    VAR
      ServiceControlContractGroup@1100528601 : Record 11071787;
    BEGIN
      WITH ServiceControlContractGroup DO BEGIN
        SETRANGE("Contract Group", IContractGroup);
        IF FINDLAST THEN
          SETRANGE("Reporting Date", "Reporting Date")
        ELSE
          EXIT;
        IF COUNT = 1 THEN
          PAGE.RUN(PAGE::"SC Control Contract Group", ServiceControlContractGroup)
        ELSE
          PAGE.RUN(PAGE::"SC Control Contract Group List", ServiceControlContractGroup);
      END;
    END;

    PROCEDURE GetNoOfActualContractControls@1100528610(IServiceContractNo@1100528600 : Code[20]) : Integer;
    VAR
      ServiceContractControl@1100528601 : Record 11071762;
    BEGIN
      IF IServiceContractNo = '' THEN EXIT(0);  //C026680
      ServiceContractControl.SETRANGE("Service Contract No.", IServiceContractNo);
      IF ServiceContractControl.FINDLAST THEN
        ServiceContractControl.SETRANGE("Reporting Date", ServiceContractControl."Reporting Date");
      EXIT(ServiceContractControl.COUNT);
    END;

    PROCEDURE ShowActualContractControl@1100528611(IServiceContractNo@1100528600 : Code[20];IControlPeriodCode@1100528603 : Code[10]);
    VAR
      ServiceContract@1100528601 : Record 11012812;
      ServiceContractControl@1100528602 : Record 11071762;
    BEGIN
      ServiceContract.GET(IServiceContractNo);

      ServiceContractControl.SETRANGE("Service Contract No.", IServiceContractNo);
      IF IControlPeriodCode <> '' THEN
        ServiceContractControl.SETRANGE("Control Period Code", IControlPeriodCode);
      IF ServiceContractControl.FINDLAST THEN
        ServiceContractControl.SETRANGE("Reporting Date", ServiceContractControl."Reporting Date");
      IF ServiceContractControl.COUNT = 1 THEN BEGIN
        CASE ServiceContract."Budget Based on" OF
          ServiceContract."Budget Based on"::"Maintenance Scheme":
            PAGE.RUN(PAGE::"Service-Contract Control", ServiceContractControl);
          ServiceContract."Budget Based on"::Category,
          ServiceContract."Budget Based on"::"Category + Maintenance Scheme":
            PAGE.RUN(PAGE::"SC Control (Category) Control", ServiceContractControl);
        END;
      END ELSE BEGIN
        IF ServiceContractControl.FINDFIRST THEN;
        CASE ServiceContract."Budget Based on" OF
          ServiceContract."Budget Based on"::"Maintenance Scheme":
            PAGE.RUN(PAGE::"Service-Contract Control List", ServiceContractControl);
          ServiceContract."Budget Based on"::Category,
          ServiceContract."Budget Based on"::"Category + Maintenance Scheme":
            PAGE.RUN(PAGE::"SC Control (Category) List", ServiceContractControl);
        END;
      END;
    END;

    PROCEDURE GetNoOfContractControlPeriods@1100528605(IServiceContractNo@1100528601 : Code[20];OnlyActivePeriods@1100528602 : Boolean) : Integer;
    VAR
      ContractControlPeriod@1100528600 : Record 11071746;
    BEGIN
      IF IServiceContractNo = '' THEN EXIT(0);  //C026680
      ContractControlPeriod.SETRANGE("Service Contract No.", IServiceContractNo);
      IF OnlyActivePeriods THEN
        ContractControlPeriod.SETRANGE("Date Finished", 0D);
      EXIT(ContractControlPeriod.COUNT);
    END;

    PROCEDURE ShowContractControlPeriods@1100528606(IServiceContractNo@1100528600 : Code[20];OnlyActivePeriods@1100528602 : Boolean);
    VAR
      ContractControlPeriod@1100528601 : Record 11071746;
    BEGIN
      ContractControlPeriod.SETRANGE("Service Contract No.", IServiceContractNo);
      IF OnlyActivePeriods THEN
        ContractControlPeriod.SETRANGE("Date Finished", 0D);
      PAGE.RUN(0, ContractControlPeriod);
    END;

    PROCEDURE GetDSSOsBySourceType@1100525000(IServiceContractNo@1100525000 : Code[20];ISourceType@1100525002 : Option;VAR OServiceOrder@1100525001 : Record 11012823);
    BEGIN
      OServiceOrder.RESET;
      OServiceOrder.FILTERGROUP(7);
      OServiceOrder.SETCURRENTKEY("Service Contract No.");
      OServiceOrder.SETRANGE("Service Contract No.", IServiceContractNo);
      IF ISourceType <> -1 THEN
        OServiceOrder.SETRANGE("Source Type", ISourceType);
      OServiceOrder.FILTERGROUP(0);
    END;

    PROCEDURE GetDSServiceOrders@1100525001(IServiceContractNo@1100525002 : Code[20];VAR OServiceOrder@1100525000 : Record 11012823);
    BEGIN
      GetDSSOsBySourceType(IServiceContractNo, -1, OServiceOrder);
    END;

    PROCEDURE GetDSOpenServiceOrders@1100525009(IServiceContractNo@1100525002 : Code[20];VAR OServiceOrder@1100525000 : Record 11012823);
    BEGIN
      OServiceOrder.RESET;
      OServiceOrder.FILTERGROUP(7);
      OServiceOrder.SETCURRENTKEY("Service Contract No.");
      OServiceOrder.SETRANGE("Service Contract No.", IServiceContractNo);
      OServiceOrder.SETFILTER(Status, '<%1', OServiceOrder.Status::"Technical Finished");
      OServiceOrder.FILTERGROUP(0);
    END;

    PROCEDURE GetNoOfSOsBySourceType@1100525002(IServiceContractNo@1100525001 : Code[20];ISourceType@1100525000 : Option) : Integer;
    VAR
      ServiceOrder@1100525002 : Record 11012823;
    BEGIN
      //C026176.sn
      IF IServiceContractNo = '' THEN
        EXIT(0);
      //C026176.en

      GetDSSOsBySourceType(IServiceContractNo, ISourceType, ServiceOrder);
      EXIT(ServiceOrder.COUNT);
    END;

    PROCEDURE GetNoOfServiceOrders@1100525003(IServiceContractNo@1100525001 : Code[20]) : Integer;
    VAR
      ServiceOrder@1100525000 : Record 11012823;
    BEGIN
      //C026176.sn
      IF IServiceContractNo = '' THEN
        EXIT(0);
      //C026176.en

      GetDSServiceOrders(IServiceContractNo, ServiceOrder);
      EXIT(ServiceOrder.COUNT);
    END;

    PROCEDURE GetNoOfOpenServiceOrders@1100525006(IServiceContractNo@1100525001 : Code[20]) : Integer;
    VAR
      ServiceOrder@1100525000 : Record 11012823;
    BEGIN
      //C026176.sn
      IF IServiceContractNo = '' THEN
        EXIT(0);
      //C026176.en

      GetDSOpenServiceOrders(IServiceContractNo, ServiceOrder);
      EXIT(ServiceOrder.COUNT);
    END;

    PROCEDURE ShowSOsBySourceType@1100525004(IServiceContractNo@1100525001 : Code[20];ISourceType@1100525000 : Option);
    VAR
      ServiceOrder@1100525002 : Record 11012823;
    BEGIN
      GetDSSOsBySourceType(IServiceContractNo, ISourceType, ServiceOrder);
      PAGE.RUNMODAL(0, ServiceOrder);
    END;

    PROCEDURE ShowOpenServiceOrders@1100525007(IServiceContractNo@1100525001 : Code[20]);
    VAR
      ServiceOrder@1100525000 : Record 11012823;
    BEGIN
      GetDSOpenServiceOrders(IServiceContractNo, ServiceOrder);
      PAGE.RUNMODAL(0, ServiceOrder);
    END;

    PROCEDURE ShowServiceOrders@1100525005(IServiceContractNo@1100525001 : Code[20]);
    VAR
      ServiceOrder@1100525000 : Record 11012823;
    BEGIN
      GetDSServiceOrders(IServiceContractNo, ServiceOrder);
      PAGE.RUNMODAL(0, ServiceOrder);
    END;

    PROCEDURE GetDSCategories@1100528601(IServiceContractNo@1100528600 : Code[20];VAR OServiceCategory@1100528601 : Record 11071985);
    BEGIN
      OServiceCategory.RESET;
      OServiceCategory.SETRANGE("Service Contract No.", IServiceContractNo);
    END;

    PROCEDURE GetNoOfCategories@1100528602(IServiceContractNo@1100528601 : Code[20]) : Integer;
    VAR
      ServiceCategory@1100528600 : Record 11071985;
    BEGIN
      IF IServiceContractNo = '' THEN EXIT(0);  //C026680
      GetDSCategories(IServiceContractNo, ServiceCategory);
      EXIT(ServiceCategory.COUNT);
    END;

    PROCEDURE ShowCategories@1100528603(IServiceContractNo@1100528600 : Code[20]);
    VAR
      ServiceCategory@1100528601 : Record 11071985;
    BEGIN
      GetDSCategories(IServiceContractNo, ServiceCategory);
      PAGE.RUNMODAL(0, ServiceCategory);
    END;

    PROCEDURE ShowContractObjectDiscountTerms@1100528604(IServiceContractNo@1100528600 : Code[20];IObjectNo@1100528601 : Code[20]);
    VAR
      ServiceContract@1100528604 : Record 11012812;
      ContractObject@1100528602 : Record 11071702;
      ServiceContractDiscountTerm@1100528603 : Record 11012828;
      ServicePackage@1100528606 : Record 11012806;
      ServicePackageCode@1100528605 : Code[10];
      ServicePackageFilter@1100528607 : Text;
    BEGIN
      ContractObject.GET(IServiceContractNo, IObjectNo);
      ServicePackageCode := ContractObject."Service Package";
      IF ServicePackageCode = '' THEN BEGIN
        ServiceContract.GET(IServiceContractNo);
        ServiceContract.TESTFIELD("Service Package");
        ServicePackageCode := ServiceContract."Service Package";
      END;
      ServicePackage.GET(ServicePackageCode);
      IF (ServicePackage."Service Package Contract" <> '') OR
         (ServicePackage."Service Package Call" <> '') OR
         (ServicePackage."Service Package Direct" <> '')
      THEN BEGIN
        ServicePackageFilter := ServicePackage."Service Package Contract";
        IF ServicePackage."Service Package Call" <> '' THEN BEGIN
          IF ServicePackageFilter <> '' THEN
            ServicePackageFilter += '|';
          ServicePackageFilter += ServicePackage."Service Package Call";
        END;
        IF ServicePackage."Service Package Direct" <> '' THEN BEGIN
          IF ServicePackageFilter <> '' THEN
            ServicePackageFilter += '|';
          ServicePackageFilter += ServicePackage."Service Package Direct";
        END;
      END ELSE
        ServicePackageFilter := ServicePackageCode;

      ServiceContractDiscountTerm.SETRANGE("Service Contract No.", IServiceContractNo);
      ServiceContractDiscountTerm.SETFILTER("Service Package", ServicePackageFilter);
      PAGE.RUNMODAL(0, ServiceContractDiscountTerm);
    END;

    PROCEDURE GetLocationsByContract@1100528607(IServiceContractNo@1100528600 : Code[20];VAR OServiceLocation@1100528601 : Record 11012801);
    VAR
      ContractObject@1100528602 : Record 11071702;
    BEGIN
      ContractObject.SETRANGE("Service Contract No.", IServiceContractNo);
      IF ContractObject.FINDSET THEN
        REPEAT
          OServiceLocation."No." := ContractObject."Service Location No.";
          OServiceLocation.MARK(TRUE);
        UNTIL ContractObject.NEXT = 0;
      OServiceLocation.MARKEDONLY(TRUE);
    END;

    PROCEDURE GetFilteredAnalysisByContract@1100528672(IServiceContractNo@1100528601 : Code[20];IAnalysisType@1100528603 : 'Problem,Cause,Solution';VAR OServiceAnalysis@1100528600 : Record 11012842);
    VAR
      ServiceContractAnalysis@1100528602 : Record 11072329;
    BEGIN
      IF IServiceContractNo = '' THEN
        EXIT;
      ServiceContractAnalysis.SETRANGE("Service Contract No.", IServiceContractNo);
      ServiceContractAnalysis.SETRANGE("Analysis Type", IAnalysisType);
      OServiceAnalysis.SETRANGE("Analysis Type", IAnalysisType);
      OServiceAnalysis.SETFILTER("Contract Filter", IServiceContractNo);
      IF NOT ServiceContractAnalysis.ISEMPTY THEN
        OServiceAnalysis.SETRANGE("Contract Analysis Code", TRUE);
    END;

    PROCEDURE GetNoOfCommentLines@1100525023(IServiceContractNo@1100525000 : Code[20]) : Integer;
    VAR
      CommentLine@1100525001 : Record 97;
    BEGIN
      IF IServiceContractNo = '' THEN
        EXIT(0);

      FilterCommentLines(IServiceContractNo, CommentLine);
      EXIT(CommentLine.COUNT);
    END;

    PROCEDURE ShowCommentLines@1100525024(IServiceContractNo@1100525000 : Code[20]);
    VAR
      CommentLine@1100525001 : Record 97;
    BEGIN
      FilterCommentLines(IServiceContractNo, CommentLine);
      PAGE.RUNMODAL(PAGE::"Comment Sheet", CommentLine);
    END;

    PROCEDURE FilterCommentLines@1100525025(IServiceContractNo@1100525000 : Code[20];VAR OCommentLine@1100525001 : Record 97);
    BEGIN
      OCommentLine.RESET;
      OCommentLine.FILTERGROUP(4);
      OCommentLine.SETRANGE("Table Name", OCommentLine."Table Name"::"Service Contract");
      OCommentLine.SETRANGE("No.", IServiceContractNo);
      OCommentLine.FILTERGROUP(0);
    END;

    BEGIN
    END.
  }
}

