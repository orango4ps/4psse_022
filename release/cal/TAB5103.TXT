OBJECT Table 5103 Interaction Tmpl. Language
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               RemoveAttachment(FALSE);
             END;

    OnRename=VAR
               InteractionTemplate@1000 : Record 11072032;
             BEGIN
               InteractionTemplate.GET("Interaction Template Code");
               IF xRec."Language Code" <> "Language Code" THEN
                 IF InteractionTemplate."Language Code (Default)" = xRec."Language Code" THEN BEGIN
                   InteractionTemplate."Language Code (Default)" := "Language Code";
                   InteractionTemplate.MODIFY;
                 END;
             END;

    CaptionML=[ENU=Interaction Tmpl. Language;
               NOR=Spr†k for samhandl.mal];
    LookupPageID=Page5154;
  }
  FIELDS
  {
    { 1   ;   ;Interaction Template Code;Code10   ;TableRelation="Interaction Template";
                                                   CaptionML=[ENU=Interaction Template Code;
                                                              NOR=Samhandlingsmal - kode;
                                                              SVE=Interaktionsmallkod];
                                                   Editable=No }
    { 2   ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   CaptionML=[ENU=Language Code;
                                                              NOR=Spr†kkode;
                                                              SVE=Spr†kkod] }
    { 3   ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 4   ;   ;Attachment No.      ;Integer       ;CaptionML=[ENU=Attachment No.;
                                                              NOR=Vedleggsnr.;
                                                              SVE=Bilagenr] }
    { 5   ;   ;Custom Layout Code  ;Code20        ;TableRelation="Custom Report Layout" WHERE (Report ID=CONST(5084));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Custom Layout Description");
                                                              END;

                                                   CaptionML=[ENU=Custom Layout Code;
                                                              NOR=Kode for egendefinert oppsett] }
    { 6   ;   ;Custom Layout Description;Text250  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Custom Report Layout".Description WHERE (Code=FIELD(Custom Layout Code)));
                                                   CaptionML=[ENU=Custom Layout Description;
                                                              NOR=Beskrivelse av egendefinert oppsett;
                                                              SVE=Anpassad layoutbeskrivning];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Interaction Template Code,Language Code ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      AttachmentRecord@1003 : Record 11072030;
      Text000@1002 : TextConst 'ENU=You have canceled the create process.;NOR=Du har avbrutt opprettingen.';
      Text001@1001 : TextConst 'ENU=Replace existing attachment?;NOR=Erstatte eksisterende vedlegg?';
      Text002@1000 : TextConst 'ENU=You have canceled the import process.;NOR=Du har avbrutt importen.';
      Text005@1004 : TextConst 'ENU=Export Attachment;NOR=Les ut vedlegg;SVE=Exportera bilaga';
      ClientTypeManagement@1077 : Codeunit 4030;

    [Internal]
    PROCEDURE CreateAttachment@1();
    VAR
      Attachment@1000 : Record 11072030;
      InteractTmplLanguage@1004 : Record 5103;
      WordManagement@1002 : Codeunit 11072023;
      NewAttachNo@1003 : Integer;
    BEGIN
      IF "Attachment No." <> 0 THEN BEGIN
        IF Attachment.GET("Attachment No.") THEN
          Attachment.TESTFIELD("Read Only",FALSE);
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
      END;

      WordManagement.SetTemplateCode("Interaction Template Code"); //**4PS.n
      IF "Custom Layout Code" = '' THEN
        IF ClientTypeManagement.GetCurrentClientType IN [CLIENTTYPE::Web,CLIENTTYPE::Tablet,CLIENTTYPE::Phone] THEN BEGIN
          IF Attachment.ImportAttachmentFromClientFile('',FALSE,FALSE) THEN
            NewAttachNo := Attachment."No.";
        END ELSE
          NewAttachNo :=
            WordManagement.CreateWordAttachment("Interaction Template Code" + ' ' + Description,"Language Code")
      ELSE
        NewAttachNo := CreateHTMLCustomLayoutAttachment;

      IF NewAttachNo <> 0 THEN BEGIN
        IF "Attachment No." <> 0 THEN
          RemoveAttachment(FALSE);
        "Attachment No." := NewAttachNo;
        IF InteractTmplLanguage.GET("Interaction Template Code","Language Code") THEN
          MODIFY
        ELSE
          INSERT;
      END ELSE
        ERROR(Text000);
    END;

    [External]
    PROCEDURE CreateHTMLCustomLayoutAttachment@8() : Integer;
    VAR
      Attachment@1000 : Record 11072030;
    BEGIN
      Attachment.INIT;
      Attachment."Storage Type" := Attachment."Storage Type"::Embedded;
      Attachment."File Extension" := 'HTML';
      Attachment.INSERT(TRUE);
      Attachment.WriteHTMLCustomLayoutAttachment('',"Custom Layout Code");
      EXIT(Attachment."No.");
    END;

    [Internal]
    PROCEDURE OpenAttachment@2();
    VAR
      Attachment@1000 : Record 11072030;
    BEGIN
      IF "Attachment No." = 0 THEN
        EXIT;
      Attachment.GET("Attachment No.");
      Attachment.SetTemplateCode("Interaction Template Code"); //**4PS.n
      Attachment.OpenAttachment("Interaction Template Code" + ' ' + Description,FALSE,"Language Code");
    END;

    [Internal]
    PROCEDURE CopyFromAttachment@3();
    VAR
      InteractTmplLanguage@1000 : Record 5103;
      Attachment@1001 : Record 11072030;
      AttachmentManagement@1002 : Codeunit 11072021;
      NewAttachNo@1003 : Integer;
    BEGIN
      IF Attachment.GET("Attachment No.") THEN
        Attachment.TESTFIELD("Read Only",FALSE);

      IF "Attachment No." <> 0 THEN BEGIN
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
        RemoveAttachment(FALSE);
        "Attachment No." := 0;
        MODIFY;
        COMMIT;
      END;

      InteractTmplLanguage.SETFILTER("Attachment No.",'<>%1',0);
      IF PAGE.RUNMODAL(0,InteractTmplLanguage) = ACTION::LookupOK THEN BEGIN
        NewAttachNo := AttachmentManagement.InsertAttachment(InteractTmplLanguage."Attachment No.");
        IF NewAttachNo <> 0 THEN BEGIN
          "Attachment No." := NewAttachNo;
          MODIFY;
        END;
      END;
    END;

    [Internal]
    PROCEDURE ImportAttachment@5();
    VAR
      Attachment@1000 : Record 11072030;
    BEGIN
      IF "Attachment No." <> 0 THEN BEGIN
        IF Attachment.GET("Attachment No.") THEN
          Attachment.TESTFIELD("Read Only",FALSE);
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
      END;

      IF Attachment.ImportAttachmentFromClientFile('',FALSE,FALSE) THEN BEGIN
        "Attachment No." := Attachment."No.";
        MODIFY;
      END ELSE
        ERROR(Text002);
    END;

    [Internal]
    PROCEDURE ExportAttachment@6();
    VAR
      TempBlob@1002 : Record 99008535;
      MarketingSetup@1004 : Record 5079;
      FileMgt@1003 : Codeunit 419;
      FileName@1001 : Text[1024];
      FileFilter@1005 : Text;
      ExportToFile@1006 : Text;
    BEGIN
      MarketingSetup.GET;
      ExportToFile := '';
      IF NOT AttachmentRecord.GET("Attachment No.") THEN
        EXIT;

      CASE AttachmentRecord."Storage Type" OF
        AttachmentRecord."Storage Type"::Embedded:
          BEGIN
            AttachmentRecord.CALCFIELDS("Attachment File");
            IF AttachmentRecord."Attachment File".HASVALUE THEN BEGIN
              FileName := "Interaction Template Code" + '.' + AttachmentRecord."File Extension";
              TempBlob.Blob := AttachmentRecord."Attachment File";
              FileMgt.BLOBExport(TempBlob,FileName,TRUE);
            END;
          END;
        AttachmentRecord."Storage Type"::"Disk File":
          BEGIN
            IF MarketingSetup."Attachment Storage Type" = MarketingSetup."Attachment Storage Type"::"Disk File" THEN
              MarketingSetup.TESTFIELD("Attachment Storage Location");
            FileFilter :=
              UPPERCASE(AttachmentRecord."File Extension") +
              ' (*.' + AttachmentRecord."File Extension" + ')|*.' + AttachmentRecord."File Extension";
            ExportToFile := "Interaction Template Code" + '.' + AttachmentRecord."File Extension";
            DOWNLOAD(
              AttachmentRecord."Storage Pointer" + '\' + FORMAT(AttachmentRecord."No.") + '.',Text005,'',FileFilter,ExportToFile);
          END;
      END;
    END;

    [Internal]
    PROCEDURE RemoveAttachment@7(Prompt@1000 : Boolean);
    VAR
      Attachment@1001 : Record 11072030;
    BEGIN
      IF Attachment.GET("Attachment No.") THEN
        IF Attachment.RemoveAttachment(Prompt) THEN BEGIN
          "Attachment No." := 0;
          MODIFY;
        END;
    END;

    BEGIN
    END.
  }
}

