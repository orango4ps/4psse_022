OBJECT Report 11012196 Create Authority Perm (PPT)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Projektportal ProjektautoritÑtsber. erstellen;
               ENU=Create Authority Perm (PPT);
               NLD=Projectportaal projectinstanties aanmaken];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 1100525000;;DataItem;                  ;
               DataItemTable=Table11020216;
               DataItemTableView=SORTING(Project No.,Authority Type,Contact No.,Contact Person No.);
               OnPreDataItem=BEGIN
                               RecCount := 0;
                             END;

               OnAfterGetRecord=BEGIN
                                  ProcessProjectAuthority("Project Authority");
                                END;

               OnPostDataItem=BEGIN
                                MESSAGE(Text000, RecCount);
                              END;

               ReqFilterFields=Project No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      RecCount@1100525000 : Integer;
      Text000@1100525001 : TextConst 'DEU=%1 Zugangs- und/oder FilterdatensÑtze hinzugefÅgt.;ENU=%1 Access and/or Filter Records added.;NLD=%1 toegang- en/of filterrecords toegevoegd.';
      SubsiteType@1100528300 : Option;

    PROCEDURE ProcessProjectAuthority@1100525000(ProjectAuthority@1100525000 : Record 11020216);
    VAR
      ProjectPortalSubsite@1100525002 : Record 11229810;
      SubsiteDocTypePPT@1100525001 : Record 11229811;
    BEGIN
      InsertRecord(1, ProjectAuthority, '', '');

      ProjectPortalSubsite.SETRANGE(Type, SubsiteType);
      ProjectPortalSubsite.SETRANGE("High Security Subsite", TRUE);
      IF (ProjectPortalSubsite.FINDSET) THEN BEGIN
        REPEAT
          SubsiteDocTypePPT.SETRANGE("Subsite Type", SubsiteType);
          SubsiteDocTypePPT.SETRANGE("Project Portal Subsite", ProjectPortalSubsite.Code);
          IF (SubsiteDocTypePPT.FINDSET) THEN BEGIN
            InsertRecord(1, ProjectAuthority, ProjectPortalSubsite.Code, ''); //1=Access
            REPEAT
              InsertRecord(1, ProjectAuthority, ProjectPortalSubsite.Code, SubsiteDocTypePPT."Document Type");
            UNTIL(SubsiteDocTypePPT.NEXT = 0);
          END;
        UNTIL (ProjectPortalSubsite.NEXT = 0);
      END;

      ProjectPortalSubsite.SETRANGE("High Security Subsite", FALSE);
      IF (ProjectPortalSubsite.FINDSET) THEN BEGIN
        REPEAT
          InsertRecord(1, ProjectAuthority, ProjectPortalSubsite.Code, ''); //1=Access
        UNTIL (ProjectPortalSubsite.NEXT = 0);
      END;

      ProjectPortalSubsite.SETRANGE("High Security Subsite");
      IF (ProjectPortalSubsite.FINDSET) THEN BEGIN
        REPEAT
          SubsiteDocTypePPT.SETRANGE("Subsite Type", SubsiteType);
          SubsiteDocTypePPT.SETRANGE("Project Portal Subsite", ProjectPortalSubsite.Code);
          IF (SubsiteDocTypePPT.FINDSET) THEN BEGIN
            REPEAT
              InsertRecord(2, ProjectAuthority, ProjectPortalSubsite.Code, SubsiteDocTypePPT."Document Type"); //2=Filter
            UNTIL (SubsiteDocTypePPT.NEXT = 0);
          END;
        UNTIL (ProjectPortalSubsite.NEXT = 0);
      END;
    END;

    PROCEDURE SetSubsiteType@1100528301(iSubsiteType@1100528300 : Option);
    BEGIN
      SubsiteType := iSubsiteType;
    END;

    LOCAL PROCEDURE InsertRecord@1100528300(PermissionType@1100528301 : Option;ProjectAuthority@1100525000 : Record 11020216;SubsiteCode@1100528303 : Code[20];DocumentType@1100528304 : Code[20]);
    VAR
      AuthorityPermPPT@1100528300 : Record 11229813;
      DefaultAuthorityPermPPT@1100528302 : Record 11229815;
    BEGIN
      IF (NOT DefaultAuthorityPermPPT.GET(SubsiteType, PermissionType, ProjectAuthority."Authority Type",
        SubsiteCode, DocumentType))
      THEN BEGIN
        DefaultAuthorityPermPPT.INIT;
        DefaultAuthorityPermPPT."Show in View" := TRUE;
      END;

      AuthorityPermPPT.INIT;
      AuthorityPermPPT."Subsite Type" := SubsiteType;
      AuthorityPermPPT.Type := PermissionType;
      AuthorityPermPPT."Project No." := ProjectAuthority."Project No.";
      AuthorityPermPPT."Authority Type" := ProjectAuthority."Authority Type";
      AuthorityPermPPT."Contact No." := ProjectAuthority."Contact No.";
      AuthorityPermPPT."Contact Person No." := ProjectAuthority."Contact Person No.";
      AuthorityPermPPT."Project Portal Subsite" := SubsiteCode;
      AuthorityPermPPT."Document Type" := DocumentType;
      AuthorityPermPPT."Permission Level" := DefaultAuthorityPermPPT."Permission Level";
      AuthorityPermPPT."Show in View" := DefaultAuthorityPermPPT."Show in View";

      IF (AuthorityPermPPT.INSERT) THEN //not TRUE
        RecCount += 1;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

