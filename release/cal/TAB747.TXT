OBJECT Table 747 VAT Report Archive
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    Permissions=TableData 747=rimd;
    CaptionML=[ENU=VAT Report Archive;
               NOR=Mva-rapportarkiv;
               SVE=Arkiv f”r momsrapport];
  }
  FIELDS
  {
    { 1   ;   ;VAT Report Type     ;Option        ;CaptionML=[ENU=VAT Report Type;
                                                              NOR=Mva-rapporttype;
                                                              SVE=Momsrapporttyp];
                                                   OptionCaptionML=[ENU=EC Sales List,VAT Return;
                                                                    NOR=EU-salg - oversikt,omsetningsoppgave;
                                                                    SVE=EU-f”rs„ljningslista,Momsretur];
                                                   OptionString=EC Sales List,VAT Return }
    { 2   ;   ;VAT Report No.      ;Code20        ;TableRelation="VAT Report Header".No.;
                                                   CaptionML=[ENU=VAT Report No.;
                                                              NOR=Mva-rapportnr.;
                                                              SVE=Momsrapporteringsnr] }
    { 4   ;   ;Submitted By        ;Code50        ;TableRelation=User."User Name";
                                                   TestTableRelation=No;
                                                   DataClassification=EndUserIdentifiableInformation;
                                                   CaptionML=[ENU=Submitted By;
                                                              NOR=Sendt av;
                                                              SVE=Skickad av] }
    { 5   ;   ;Submission Message BLOB;BLOB       ;CaptionML=[ENU=Submission Message BLOB;
                                                              NOR=Meldings-BLOB for sending;
                                                              SVE=Skicka meddelande-BLOB] }
    { 6   ;   ;Submittion Date     ;Date          ;CaptionML=[ENU=Submittion Date;
                                                              NOR=Sendedato;
                                                              SVE=S„ndningsdatum] }
    { 7   ;   ;Response Message BLOB;BLOB         ;CaptionML=[ENU=Response Message BLOB;
                                                              NOR=Meldings-BLOB for svar;
                                                              SVE=Svarsmeddelande-BLOB] }
    { 8   ;   ;Response Received Date;DateTime    ;CaptionML=[ENU=Response Received Date;
                                                              NOR=Svar mottatt den;
                                                              SVE=Mottagningsdatum f”r svar] }
  }
  KEYS
  {
    {    ;VAT Report Type,VAT Report No.          ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      FileManagement@1000 : Codeunit 419;
      NoSubmissionMessageAvailableErr@1001 : TextConst 'ENU=The submission message of the report is not available.;NOR=Sendingsmeldingen for rapporten er ikke tilgjengelig.;SVE=Rapportens s„ndningsmeddelande „r inte tillg„ngligt.';
      NoResponseMessageAvailableErr@1002 : TextConst 'ENU=The response message of the report is not available.;NOR=Svarmeldingen for rapporten er ikke tilgjengelig.;SVE=Rapportens svarsmeddelande „r inte tillg„ngligt.';

    [External]
    PROCEDURE ArchiveSubmissionMessage@1000(VATReportTypeValue@1001 : Option;VATReportNoValue@1002 : Code[20];SubmissionMessageTempBlob@1003 : Record 99008535) : Boolean;
    VAR
      VATReportArchive@1000 : Record 747;
    BEGIN
      IF VATReportNoValue = '' THEN
        EXIT(FALSE);
      IF NOT SubmissionMessageTempBlob.Blob.HASVALUE THEN
        EXIT(FALSE);
      IF VATReportArchive.GET(VATReportTypeValue,VATReportNoValue) THEN
        EXIT(FALSE);

      VATReportArchive.INIT;
      VATReportArchive."VAT Report No." := VATReportNoValue;
      VATReportArchive."VAT Report Type" := VATReportTypeValue;
      VATReportArchive."Submitted By" := USERID;
      VATReportArchive."Submittion Date" := TODAY;
      VATReportArchive."Submission Message BLOB" := SubmissionMessageTempBlob.Blob;
      VATReportArchive.INSERT(TRUE);
      EXIT(TRUE);
    END;

    [External]
    PROCEDURE ArchiveResponseMessage@1001(VATReportTypeValue@1010 : Option;VATReportNoValue@1011 : Code[20];ResponseMessageTempBlob@1000 : Record 99008535) : Boolean;
    VAR
      VATReportArchive@1001 : Record 747;
    BEGIN
      IF NOT VATReportArchive.GET(VATReportTypeValue,VATReportNoValue) THEN
        EXIT(FALSE);
      IF NOT ResponseMessageTempBlob.Blob.HASVALUE THEN
        EXIT(FALSE);

      VATReportArchive."Response Received Date" := CURRENTDATETIME;
      VATReportArchive."Response Message BLOB" := ResponseMessageTempBlob.Blob;
      VATReportArchive.MODIFY(TRUE);

      EXIT(TRUE);
    END;

    [External]
    PROCEDURE DownloadSubmissionMessage@3(VATReportTypeValue@1001 : Option;VATReportNoValue@1000 : Code[20]);
    VAR
      VATReportArchive@1004 : Record 747;
      TempBlob@1006 : Record 99008535;
      ServerFileName@1005 : Text;
      ZipFileName@1007 : Text[250];
    BEGIN
      IF NOT VATReportArchive.GET(VATReportTypeValue,VATReportNoValue) THEN
        ERROR(NoSubmissionMessageAvailableErr);

      IF NOT VATReportArchive."Submission Message BLOB".HASVALUE THEN
        ERROR(NoSubmissionMessageAvailableErr);

      VATReportArchive.CALCFIELDS("Submission Message BLOB");
      TempBlob.INIT;
      TempBlob.Blob := VATReportArchive."Submission Message BLOB";

      ServerFileName := FileManagement.ServerTempFileName('xml');
      FileManagement.BLOBExportToServerFile(TempBlob,ServerFileName);

      ZipFileName := VATReportNoValue + '_Submission.txt';
      DownloadZipFile(ZipFileName,ServerFileName);
      FileManagement.DeleteServerFile(ServerFileName);
    END;

    [External]
    PROCEDURE DownloadResponseMessage@2(VATReportTypeValue@1001 : Option;VATReportNoValue@1000 : Code[20]);
    VAR
      VATReportArchive@1008 : Record 747;
      TempBlob@1007 : Record 99008535;
      ServerFileName@1005 : Text;
      ZipFileName@1002 : Text[250];
    BEGIN
      IF NOT VATReportArchive.GET(VATReportTypeValue,VATReportNoValue) THEN
        ERROR(NoResponseMessageAvailableErr);

      IF NOT VATReportArchive."Response Message BLOB".HASVALUE THEN
        ERROR(NoResponseMessageAvailableErr);

      VATReportArchive.CALCFIELDS("Response Message BLOB");
      TempBlob.INIT;
      TempBlob.Blob := VATReportArchive."Response Message BLOB";

      ServerFileName := FileManagement.ServerTempFileName('xml');
      FileManagement.BLOBExportToServerFile(TempBlob,ServerFileName);

      ZipFileName := VATReportNoValue + '_Response.txt';
      DownloadZipFile(ZipFileName,ServerFileName);
      FileManagement.DeleteServerFile(ServerFileName);
    END;

    LOCAL PROCEDURE DownloadZipFile@4(ZipFileName@1000 : Text[250];ServerFileName@1002 : Text);
    VAR
      ZipArchiveName@1001 : Text;
    BEGIN
      ZipArchiveName := FileManagement.CreateZipArchiveObject;
      FileManagement.AddFileToZipArchive(ServerFileName,ZipFileName);

      FileManagement.CloseZipArchive;
      FileManagement.DownloadHandler(ZipArchiveName,'','','',ZipFileName + '.zip');
    END;

    BEGIN
    END.
  }
}

