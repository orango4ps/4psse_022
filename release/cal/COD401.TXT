OBJECT Codeunit 401 Sales Header Apply
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    TableNo=36;
    OnRun=BEGIN
            SalesHeader.COPY(Rec);
            WITH SalesHeader DO BEGIN
              BilToCustNo := "Bill-to Customer No." ;
              CustLedgEntry.SETCURRENTKEY("Customer No.",Open);
              CustLedgEntry.SETRANGE("Customer No.",BilToCustNo);
              CustLedgEntry.SETRANGE(Open,TRUE);
              IF "Applies-to ID" = '' THEN
                "Applies-to ID" := "No.";
              IF "Applies-to ID" = '' THEN
                ERROR(
                  Text000,
                  FIELDCAPTION("No."),FIELDCAPTION("Applies-to ID"));
              ApplyCustEntries.SetSales(SalesHeader,CustLedgEntry,FIELDNO("Applies-to ID"));
              ApplyCustEntries.SETRECORD(CustLedgEntry);
              ApplyCustEntries.SETTABLEVIEW(CustLedgEntry);
              ApplyCustEntries.LOOKUPMODE(TRUE);
              OK := ApplyCustEntries.RUNMODAL = ACTION::LookupOK;
              CLEAR(ApplyCustEntries);
              IF NOT OK THEN
                EXIT;
              CustLedgEntry.RESET;
              CustLedgEntry.SETCURRENTKEY("Customer No.",Open);
              CustLedgEntry.SETRANGE("Customer No.",BilToCustNo);
              CustLedgEntry.SETRANGE(Open,TRUE);
              CustLedgEntry.SETRANGE("Applies-to ID","Applies-to ID");
              IF CustLedgEntry.FINDFIRST THEN BEGIN
                "Applies-to Doc. Type" := 0;
                "Applies-to Doc. No." := '';
              END ELSE
                "Applies-to ID" := '';

              MODIFY;
            END;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You must specify %1 or %2.;NOR=Du m† angi %1 eller %2.;SVE=Du m†ste specificera %1 eller %2.';
      SalesHeader@1001 : Record 36;
      CustLedgEntry@1002 : Record 21;
      ApplyCustEntries@1003 : Page 232;
      BilToCustNo@1004 : Code[20];
      OK@1005 : Boolean;

    BEGIN
    END.
  }
}

