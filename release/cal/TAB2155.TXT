OBJECT Table 2155 O365 Payment Instructions
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               DocumentDescription@1000 : Text;
             BEGIN
               IF Default THEN
                 ERROR(CannotDeleteDefaultErr);

               DocumentDescription := FindDraftsUsingInstructions;
               IF DocumentDescription <> '' THEN
                 ERROR(PaymentIsUsedErr,FindDraftsUsingInstructions);

               IF GUIALLOWED THEN
                 IF NOT CONFIRM(DoYouWantToDeleteQst) THEN
                   ERROR('');

               DeleteTranslationsForRecord;
             END;

    ReplicateData=No;
    CaptionML=[ENU=O365 Payment Instructions;
               NOR=O365 Betalingsinstruksjoner];
  }
  FIELDS
  {
    { 1   ;   ;Id                  ;Integer       ;AutoIncrement=Yes;
                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Id;
                                                              NOR=ID;
                                                              SVE=ID] }
    { 5   ;   ;Name                ;Text20        ;CaptionML=[ENU=Name;
                                                              NOR=Navn;
                                                              SVE=Namn] }
    { 6   ;   ;Payment Instructions;Text250       ;CaptionML=[ENU=Payment Instruction;
                                                              NOR=Betalingsinstruksjon] }
    { 7   ;   ;Payment Instructions Blob;BLOB     ;CaptionML=[ENU=Payment Instructions Blob;
                                                              NOR=Blob forÿbetalingsinstruksjoner] }
    { 8   ;   ;Default             ;Boolean       ;OnValidate=VAR
                                                                O365PaymentInstructions@1000 : Record 2155;
                                                              BEGIN
                                                                IF Default THEN BEGIN
                                                                  O365PaymentInstructions.SETFILTER(Id,'<>%1',Id);
                                                                  O365PaymentInstructions.MODIFYALL(Default,FALSE,FALSE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Default;
                                                              NOR=Standard;
                                                              SVE=STANDARD] }
  }
  KEYS
  {
    {    ;Id                                      ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DocumentDescriptionTxt@1000 : TextConst '@@@="%1=Document description (e.g. Invoice, Estimate,...); %2=Document Number";ENU=%1 %2;NOR=%1 %2;SVE=%1 %2';
      PaymentIsUsedErr@1001 : TextConst '@@@=%1: Document type and number;ENU=You cannot delete the Payment Instructions because at least one invoice (%1) is using them.;NOR=Du kan ikke slette betalingsinstruksjonene fordi minst ‚n faktura (%1) bruker dem.';
      CannotDeleteDefaultErr@1002 : TextConst 'ENU=You cannot delete the default Payment Instructions.;NOR=Du kan ikkeÿslette standard betalingsinstruksjoner.';
      LanguageManagement@1003 : Codeunit 43;
      DoYouWantToDeleteQst@1004 : TextConst 'ENU=Are you sure you want to delete the payment instructions?;NOR=Er du sikker p† at du vil slette betalingsinstruksjonene?';

    [External]
    PROCEDURE GetPaymentInstructions@14() : Text;
    VAR
      TempBlob@1000 : Record 99008535;
      CR@1001 : Text[1];
    BEGIN
      CALCFIELDS("Payment Instructions Blob");
      IF NOT "Payment Instructions Blob".HASVALUE THEN
        EXIT("Payment Instructions");
      CR[1] := 10;
      TempBlob.Blob := "Payment Instructions Blob";
      EXIT(TempBlob.ReadAsText(CR,TEXTENCODING::Windows));
    END;

    [External]
    PROCEDURE SetPaymentInstructions@15(NewInstructions@1001 : Text);
    VAR
      TempBlob@1000 : Record 99008535;
    BEGIN
      CLEAR("Payment Instructions Blob");
      "Payment Instructions" := COPYSTR(NewInstructions,1,MAXSTRLEN("Payment Instructions"));
      IF STRLEN(NewInstructions) <= MAXSTRLEN("Payment Instructions") THEN
        EXIT; // No need to store anything in the blob
      IF NewInstructions = '' THEN
        EXIT;
      TempBlob.WriteAsText(NewInstructions,TEXTENCODING::Windows);
      "Payment Instructions Blob" := TempBlob.Blob;
      MODIFY;
    END;

    [External]
    PROCEDURE FindDraftsUsingInstructions@1() DocumentDescription : Text;
    VAR
      SalesHeader@1000 : Record 36;
    BEGIN
      DocumentDescription := '';
      SalesHeader.SETRANGE("Payment Instructions Id",Id);

      IF SalesHeader.FINDFIRST THEN
        DocumentDescription := STRSUBSTNO(DocumentDescriptionTxt,SalesHeader.GetDocTypeTxt,SalesHeader."No.");
    END;

    [External]
    PROCEDURE GetNameInCurrentLanguage@2() : Text[20];
    VAR
      O365PaymentInstrTransl@1000 : Record 2156;
      LanguageCode@1001 : Code[10];
    BEGIN
      LanguageCode := LanguageManagement.GetLanguageCodeFromLanguageID(GLOBALLANGUAGE);

      IF NOT O365PaymentInstrTransl.GET(Id,LanguageCode) THEN
        EXIT(Name);

      EXIT(O365PaymentInstrTransl."Transl. Name");
    END;

    [External]
    PROCEDURE GetPaymentInstructionsInCurrentLanguage@3() : Text;
    VAR
      O365PaymentInstrTransl@1000 : Record 2156;
      LanguageCode@1001 : Code[10];
    BEGIN
      LanguageCode := LanguageManagement.GetLanguageCodeFromLanguageID(GLOBALLANGUAGE);

      IF NOT O365PaymentInstrTransl.GET(Id,LanguageCode) THEN
        EXIT(GetPaymentInstructions);

      EXIT(O365PaymentInstrTransl.GetTransPaymentInstructions);
    END;

    [External]
    PROCEDURE DeleteTranslationsForRecord@4();
    VAR
      O365PaymentInstrTransl@1000 : Record 2156;
    BEGIN
      O365PaymentInstrTransl.SETRANGE(Id,Id);
      O365PaymentInstrTransl.DELETEALL(TRUE);
    END;

    [External]
    PROCEDURE CopyInstructionsInCurrentLanguageToBlob@5(VAR TempBlob@1000 : Record 99008535);
    BEGIN
      TempBlob.WriteAsText(GetPaymentInstructionsInCurrentLanguage,TEXTENCODING::Windows);
    END;

    BEGIN
    END.
  }
}

