OBJECT Table 11128071 VP tTempBlob
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=PE6.02 (12048868);
  }
  PROPERTIES
  {
    DataPerCompany=No;
    CaptionML=[ENU=tTempBlob;
               SVE=tTempBlob];
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Integer       ;CaptionML=[ENU=Primary Key;
                                                              NOR=Prim‘rn›kkel;
                                                              SVE=Prim„rnyckel] }
    { 2   ;   ;Blob                ;BLOB          ;CaptionML=[ENU=Blob;
                                                              NOR=Blob;
                                                              SVE=Blob] }
  }
  KEYS
  {
    {    ;Primary Key                             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE ImportFromFile@12047961(pFileName@12047963 : Text[1024]);
    VAR
      IS@12047960 : InStream;
      OS@12047961 : OutStream;
      TempFile@12047962 : File;
    BEGIN
      // Server Side
      TempFile.OPEN(pFileName);
      TempFile.CREATEINSTREAM(IS);
      Blob.CREATEOUTSTREAM(OS);
      COPYSTREAM(OS,IS);
      TempFile.CLOSE;
    END;

    PROCEDURE ExportToFile@12047960(pFileName@12047963 : Text[1024]);
    VAR
      IS@12047960 : InStream;
      OS@12047961 : OutStream;
      TempFile@12047962 : File;
    BEGIN
      // Server Side
      TempFile.CREATE(pFileName);
      TempFile.CREATEOUTSTREAM(OS);
      IF "Primary Key" <> 0 THEN
        CALCFIELDS(Blob);
      Blob.CREATEINSTREAM(IS);
      COPYSTREAM(OS,IS);
      TempFile.CLOSE;
    END;

    PROCEDURE ImportFromClientFile@12047963(pFileName@12047963 : Text[1024]);
    VAR
      FileManagement@12047964 : Codeunit 419;
      TempFilename@12047959 : Text[1024];
    BEGIN
      TempFilename := FileManagement.UploadFileSilent(pFileName);
      ImportFromFile(TempFilename);
      FILE.ERASE(TempFilename);
    END;

    PROCEDURE ExportToClientFile@12047964(pFileName@12047963 : Text[1024]);
    VAR
      FileManagement@12047960 : Codeunit 419;
      TempFile@12047962 : File;
      TempFilename@12047959 : Text[1024];
    BEGIN
      TempFile.CREATETEMPFILE;
      TempFilename := TempFile.NAME;
      TempFile.CLOSE;

      ExportToFile(TempFilename);

      FileManagement.DownloadToFile(TempFilename, pFileName);
      FILE.ERASE(TempFilename);
    END;

    BEGIN
    {
      PEB *** Begin ***
      PEB0103 Wrappers and Tools
      PEB
      PEB *** End ***
    }
    END.
  }
}

