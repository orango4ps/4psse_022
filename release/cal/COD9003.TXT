OBJECT Codeunit 9003 Team Member Action Manager
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      TeamMemberErr@1000 : TextConst 'ENU=You are logged in as a Team Member role, so you cannot complete this task.;NOR=Du er logget p† med rollen Medlem i prosjektgruppe, og dermed kan du ikke fullf›re denne oppgaven.';

    [EventSubscriber(Table,36,OnBeforeInsertEvent)]
    LOCAL PROCEDURE OnBeforeSalesHeaderInsert@2(VAR Rec@1000 : Record 36;RunTrigger@1001 : Boolean);
    BEGIN
      CheckTeamMemberPermissionOnSalesHeaderTable(Rec);
    END;

    [EventSubscriber(Codeunit,80,OnBeforePostSalesDoc)]
    LOCAL PROCEDURE OnBeforeSalesDocPost@6(VAR Sender@1001 : Codeunit 80;VAR SalesHeader@1002 : Record 36;CommitIsSuppressed@1003 : Boolean;PreviewMode@1004 : Boolean);
    BEGIN
      // Team member is not allowed to invoice a sales document.
      IF IsCurrentUserAssignedTeamMemberPlan AND SalesHeader.Invoice THEN
        ERROR(TeamMemberErr);
    END;

    [EventSubscriber(Codeunit,90,OnBeforePostPurchaseDoc)]
    LOCAL PROCEDURE OnBeforePurchaseDocPost@7(VAR Sender@1001 : Codeunit 90;VAR PurchaseHeader@1002 : Record 38;PreviewMode@1003 : Boolean;CommitIsSupressed@1004 : Boolean);
    BEGIN
      // Team member is not allowed to invoice a purchase document.
      IF IsCurrentUserAssignedTeamMemberPlan AND PurchaseHeader.Invoice THEN
        ERROR(TeamMemberErr);
    END;

    LOCAL PROCEDURE CheckTeamMemberPermissionOnSalesHeaderTable@12(VAR SalesHeader@1001 : Record 36);
    BEGIN
      IF IsCurrentUserAssignedTeamMemberPlan AND (SalesHeader."Document Type" <> SalesHeader."Document Type"::Quote) THEN
        ERROR(TeamMemberErr);
    END;

    [EventSubscriber(Table,38,OnBeforeInsertEvent)]
    LOCAL PROCEDURE OnBeforePurchaseHeaderInsert@3(VAR Rec@1000 : Record 38;RunTrigger@1001 : Boolean);
    BEGIN
      CheckTeamMemberPermissionOnPurchaseHeaderTable(Rec);
    END;

    LOCAL PROCEDURE CheckTeamMemberPermissionOnPurchaseHeaderTable@4(VAR PurchaseHeader@1001 : Record 38);
    BEGIN
      IF IsCurrentUserAssignedTeamMemberPlan AND (PurchaseHeader."Document Type" <> PurchaseHeader."Document Type"::Quote) THEN
        ERROR(TeamMemberErr);
    END;

    LOCAL PROCEDURE IsCurrentUserAssignedTeamMemberPlan@5() : Boolean;
    VAR
      UserPlan@1001 : Record 9005;
      Plan@1000 : Record 9004;
    BEGIN
      UserPlan.SETRANGE("User Security ID",USERSECURITYID);
      UserPlan.SETFILTER("Plan ID",'%1|%2',Plan.GetTeamMemberPlanId,Plan.GetTeamMemberISVPlanId);
      EXIT(NOT UserPlan.ISEMPTY);
    END;

    BEGIN
    END.
  }
}

