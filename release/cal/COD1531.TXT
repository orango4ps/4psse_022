OBJECT Codeunit 1531 Workflow Change Rec Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    Permissions=TableData 1525=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      ValueMismatchMsg@1002 : TextConst 'ENU=The current value of the field is different from the value before the change.;NOR=Den gjeldende verdien i feltet er forskjellig fra verdien f›r endringen.;SVE=Det aktuella v„rdet f”r f„ltet skiljer sig fr†n v„rdet innan „ndringen.';
      NoRecordChangesFoundMsg@1000 : TextConst 'ENU=No record changes exist to apply the saved values to using the current options.;NOR=Det finnes ingen postendringer for † bruke de lagrede verdiene med de gjeldende alternativene.;SVE=Det finns inga post„ndringar att till„mpa de sparade v„rdena med aktuella alternativ.';

    [External]
    PROCEDURE RevertValueForField@51(VAR Variant@1000 : Variant;xVariant@1001 : Variant;WorkflowStepInstance@1002 : Record 1504);
    VAR
      WorkflowStepArgument@1004 : Record 1523;
      WorkflowRecordChange@1008 : Record 1525;
      RecRef@1003 : RecordRef;
      OldValue@1005 : Text;
      NewValue@1006 : Text;
    BEGIN
      RecRef.GETTABLE(Variant);

      WorkflowStepArgument.GET(WorkflowStepInstance.Argument);

      OldValue := GetValueFromField(xVariant,WorkflowStepArgument."Field No.");
      NewValue := GetValueFromField(Variant,WorkflowStepArgument."Field No.");

      IF OldValue = NewValue THEN
        EXIT;

      CreateChangeRecord(RecRef,OldValue,NewValue,WorkflowStepArgument."Field No.",WorkflowStepInstance,WorkflowRecordChange);

      SetValueForField(RecRef,WorkflowStepArgument."Field No.",OldValue);

      RecRef.MODIFY(TRUE);

      RecRef.SETTABLE(Variant);
    END;

    [External]
    PROCEDURE ApplyNewValues@65(Variant@1000 : Variant;WorkflowStepInstance@1002 : Record 1504);
    VAR
      ApprovalEntry@1004 : Record 454;
      WorkflowRecordChange@1003 : Record 1525;
      WorkflowStepArgument@1005 : Record 1523;
      RecRef@1001 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Variant);

      CASE RecRef.NUMBER OF
        DATABASE::"Approval Entry":
          BEGIN
            ApprovalEntry.GET(RecRef.RECORDID);
            WorkflowRecordChange.SETRANGE("Workflow Step Instance ID",WorkflowStepInstance.ID);
            WorkflowRecordChange.SETRANGE("Record ID",ApprovalEntry."Record ID to Approve");
            WorkflowRecordChange.SETRANGE(Inactive,FALSE);
          END;
        DATABASE::"Workflow - Record Change":
          BEGIN
            WorkflowRecordChange.GET(RecRef.RECORDID);
            WorkflowRecordChange.SETRANGE("Workflow Step Instance ID",WorkflowStepInstance.ID);
            WorkflowRecordChange.SETRANGE("Record ID",WorkflowRecordChange."Record ID");
            WorkflowRecordChange.SETRANGE(Inactive,FALSE);
          END;
        ELSE BEGIN
          WorkflowRecordChange.SETRANGE("Workflow Step Instance ID",WorkflowStepInstance.ID);
          WorkflowRecordChange.SETRANGE("Record ID",RecRef.RECORDID);
          WorkflowRecordChange.SETRANGE(Inactive,FALSE);
        END
      END;

      IF WorkflowStepArgument.GET(WorkflowStepInstance.Argument) THEN
        IF WorkflowStepArgument."Field No." <> 0 THEN BEGIN
          WorkflowRecordChange.SETRANGE("Table No.",WorkflowStepArgument."Table No.");
          WorkflowRecordChange.SETRANGE("Field No.",WorkflowStepArgument."Field No.");
        END;

      IF WorkflowRecordChange.FINDSET THEN
        REPEAT
          ApplyNewValueFromChangeRecord(WorkflowRecordChange);
        UNTIL WorkflowRecordChange.NEXT = 0
      ELSE
        MESSAGE(NoRecordChangesFoundMsg);
    END;

    [External]
    PROCEDURE DiscardNewValues@1(Variant@1001 : Variant;WorkflowStepInstance@1000 : Record 1504);
    VAR
      ApprovalEntry@1004 : Record 454;
      WorkflowRecordChange@1002 : Record 1525;
      RecRef@1003 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Variant);

      CASE RecRef.NUMBER OF
        DATABASE::"Approval Entry":
          BEGIN
            ApprovalEntry.GET(RecRef.RECORDID);
            RecRef.GET(ApprovalEntry."Record ID to Approve");
          END;
        DATABASE::"Workflow - Record Change":
          BEGIN
            WorkflowRecordChange.GET(RecRef.RECORDID);
            RecRef.GET(WorkflowRecordChange."Record ID");
          END;
      END;

      WorkflowRecordChange.SETRANGE("Table No.",RecRef.NUMBER);
      WorkflowRecordChange.SETRANGE("Record ID",RecRef.RECORDID);
      WorkflowRecordChange.SETRANGE("Workflow Step Instance ID",WorkflowStepInstance.ID);
      WorkflowRecordChange.MODIFYALL(Inactive,TRUE,TRUE);
    END;

    LOCAL PROCEDURE CreateChangeRecord@49(RecRef@1001 : RecordRef;OldValue@1002 : Text;NewValue@1006 : Text;FieldNo@1004 : Integer;WorkflowStepInstance@1005 : Record 1504;VAR WorkflowRecordChange@1003 : Record 1525);
    BEGIN
      CLEAR(WorkflowRecordChange);
      WorkflowRecordChange."Table No." := RecRef.NUMBER;
      WorkflowRecordChange."Field No." := FieldNo;
      WorkflowRecordChange."Old Value" := COPYSTR(OldValue,1,250);
      WorkflowRecordChange."New Value" := COPYSTR(NewValue,1,250);
      WorkflowRecordChange."Record ID" := RecRef.RECORDID;
      WorkflowRecordChange."Workflow Step Instance ID" := WorkflowStepInstance.ID;
      WorkflowRecordChange.INSERT(TRUE);
    END;

    LOCAL PROCEDURE ApplyNewValueFromChangeRecord@70(WorkflowRecordChange@1000 : Record 1525);
    VAR
      RecRef@1001 : RecordRef;
    BEGIN
      RecRef.GET(WorkflowRecordChange."Record ID");

      IF WorkflowRecordChange."Old Value" = GetValueFromField(RecRef,WorkflowRecordChange."Field No.") THEN BEGIN
        SetValueForField(RecRef,WorkflowRecordChange."Field No.",WorkflowRecordChange."New Value");
        RecRef.MODIFY(TRUE);
        WorkflowRecordChange.Inactive := TRUE;
        WorkflowRecordChange.MODIFY(TRUE);
      END ELSE
        MESSAGE(ValueMismatchMsg);
    END;

    LOCAL PROCEDURE GetValueFromField@61(Variant@1000 : Variant;FieldId@1001 : Integer) : Text;
    VAR
      RecRef@1002 : RecordRef;
      FieldRef@1003 : FieldRef;
    BEGIN
      RecRef.GETTABLE(Variant);
      FieldRef := RecRef.FIELD(FieldId);
      EXIT(FORMAT(FieldRef.VALUE,0,9));
    END;

    LOCAL PROCEDURE SetValueForField@33(VAR RecRef@1001 : RecordRef;FieldId@1002 : Integer;NewValue@1000 : Text);
    VAR
      FieldRef@1004 : FieldRef;
    BEGIN
      FieldRef := RecRef.FIELD(FieldId);
      EVALUATE(FieldRef,NewValue,9);
    END;

    BEGIN
    END.
  }
}

