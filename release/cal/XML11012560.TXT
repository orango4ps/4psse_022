OBJECT XMLport 11012560 Export Car Lease Info
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Export Car Lease Info;
    Direction=Export;
    DefaultFieldsValidation=No;
    OnPreXMLport=BEGIN
                   currXMLport.FILENAME := FilePath;
                 END;

    Format=Fixed Text;
    TableSeparator=<NewLine>;
  }
  ELEMENTS
  {
    { [{739066EB-6D34-47B1-AC3E-D02B5D518943}];  ;Root                ;Element ;Text     }

    { [{58C5DB20-0FDF-4704-90E4-7F007D7D5BAD}];1 ;Voorlooprecord      ;Element ;Table   ;
                                                  VariableName=VoorloopRecord;
                                                  SourceTable=Table2000000026;
                                                  SourceTableView=SORTING(Field1)
                                                                  WHERE(Field1=CONST(1));
                                                  Export::OnAfterGetRecord=BEGIN
                                                                             TxtVoorloopRecord:= '00AUTOLEASE' + FORMAT(TODAY, 0, '<Year4><Month,2><Day,2>') + FORMAT(LeaseCompany, 70) +
                                                                               FORMAT(ContactPerson, 70) + FORMAT(Year);
                                                                             TxtVoorloopRecord := FORMAT(TxtVoorloopRecord, 368); //fill with spaces
                                                                             NoRecords += 1;
                                                                           END;
                                                                            }

    { [{44A75B70-2A8D-47CF-B32C-99656D400DEA}];2 ;TxtVoorloopRecord   ;Element ;Text    ;
                                                  Width=368 }

    { [{D5A74394-0960-4878-91B2-AC38CD46F3BC}];1 ;CarLeaseContract    ;Element ;Table   ;
                                                  ReqFilterFields=Field10,Field20;
                                                  SourceTable=Table11012540;
                                                  Export::OnPreXMLItem=BEGIN
                                                                         //* Part or all of the contract must be in the selected Year
                                                                         "Car Lease Contract".SETFILTER("Contract Start", '..%1', DMY2DATE(31, 12, Year));
                                                                         "Car Lease Contract".SETFILTER("Contract End", '%1|%2..',  0D, DMY2DATE(1, 1, Year));
                                                                       END;

                                                  Export::OnAfterGetRecord=VAR
                                                                             VehicleDriver@1100525014 : Record 11072668;
                                                                             PlantNumber@1100525013 : Record 11012552;
                                                                             lStreet@1100525012 : Text[50];
                                                                             lNumber@1100525011 : Text[50];
                                                                             lNumberAddition@1100525010 : Text[50];
                                                                             FormatAddrCdu@1100525009 : Codeunit 365;
                                                                             lvEmpRec@1100525008 : Record 5200;
                                                                             lvDriverLastName@1100525007 : Text[30];
                                                                             lvDriverInitials@1100525006 : Text[30];
                                                                             lvDriverMiddleName@1100525005 : Text[30];
                                                                             lvDriverAddress@1100525004 : Text[100];
                                                                             lvDriverPostCode@1100525003 : Code[20];
                                                                             lvDriverCity@1100525002 : Text[30];
                                                                             lvDriverSSNo@1100525001 : Text[30];
                                                                             lvDriverBirthDate@1100525000 : Date;
                                                                             DriverBirthDateTxt@1100525015 : Text[10];
                                                                             ContractStartDateTxt@1100525016 : Text[10];
                                                                             ContractEndDateTxt@1100525017 : Text[10];
                                                                           BEGIN
                                                                             RecRef.GETTABLE("Car Lease Contract");
                                                                             XMLportStatusDialogMgt.ShowExportStatus(RecRef);
                                                                             WITH "Car Lease Contract" DO BEGIN //With statement added because this code has been copied from dataport.
                                                                               CALCFIELDS(
                                                                                 "Licence Number", Brand, Model, "Year Build",
                                                                                 "Consumer Price (excl. VAT)", "BPM Amount", "Factory Accessories",
                                                                                 "COC Registration No.", "VAT Registration No."
                                                                                 );  //DP00331.n

                                                                               //* Mandatory fields
                                                                               TESTFIELD(Contractor);
                                                                               TESTFIELD("Licence Number");

                                                                               NoRecords := NoRecords + 1;
                                                                               //DP00331.sc  CarLeaseContractDriver -> VehicleDriver (also local record)
                                                                               GetDriver(VehicleDriver);
                                                                               IF VehicleDriver."Driver Company Name" <> '' THEN
                                                                                 lvEmpRec.CHANGECOMPANY(VehicleDriver."Driver Company Name");
                                                                               IF NOT lvEmpRec.GET(VehicleDriver.Driver) THEN
                                                                                 ERROR(
                                                                                   Text001,
                                                                                   VehicleDriver.FIELDCAPTION(Driver),
                                                                                   VehicleDriver.Driver,
                                                                                   FIELDCAPTION("Plant Type"),
                                                                                   PlantNumber.FIELDCAPTION("No."),
                                                                                   "Plant Type",
                                                                                   "Plant No.");
                                                                               //DP00331.ec

                                                                               lvDriverLastName := lvEmpRec."Last Name";
                                                                               lvDriverInitials := lvEmpRec.Initials;
                                                                               lvDriverMiddleName := lvEmpRec."Middle Name";
                                                                               lvDriverAddress := lvEmpRec.Address;
                                                                               lvDriverPostCode := lvEmpRec."Post Code";
                                                                               lvDriverCity := lvEmpRec.City;
                                                                               lvEmpRec.TESTFIELD("Social Security No.");
                                                                               lvDriverSSNo := lvEmpRec."Social Security No.";
                                                                               lvDriverBirthDate := lvEmpRec."Birth Date";

                                                                               CALCFIELDS("Contractor Name");
                                                                               CALCFIELDS("Contractor Address");
                                                                               CALCFIELDS("Contractor Post Code");
                                                                               CALCFIELDS("Contractor City");

                                                                               TxtRecord := '01';
                                                                               TxtRecord += FORMAT(lvDriverLastName, 49);
                                                                               TxtRecord += FORMAT(DELCHR(lvDriverInitials, '=', '., '), 5);
                                                                               TxtRecord += FORMAT(lvDriverMiddleName, 8);
                                                                               FormatAddrCdu.SplitAddress(lvDriverAddress, lStreet, lNumber, lNumberAddition);
                                                                               TxtRecord += FORMAT(lStreet, 25);
                                                                               TxtRecord += FORMAT(lNumber, 10);
                                                                               TxtRecord += FORMAT(lNumberAddition, 10);
                                                                               TxtRecord += FORMAT(DELCHR(lvDriverPostCode, '=', '., '),6);
                                                                               TxtRecord += FORMAT(lvDriverCity, 20);
                                                                               TxtRecord += FORMAT(lvDriverSSNo, 0, '<Text,9><Filler Character,0>');

                                                                               //* Outer format needed to get 00000000 in case of 0D
                                                                               IF lvDriverBirthDate <> 0D THEN
                                                                                 DriverBirthDateTxt := FORMAT(lvDriverBirthDate, 0, '<Day,2><Month,2><Year4>')
                                                                               ELSE
                                                                                 DriverBirthDateTxt := '0';
                                                                               TxtRecord += FORMAT(DriverBirthDateTxt, 0, '<Text,8><Filler Character,0>');

                                                                               TxtRecord += FORMAT("Contractor Name", 49);
                                                                               TxtRecord += FORMAT("Contractor Address", 25);
                                                                               TxtRecord += FORMAT(DELCHR("Contractor Post Code", '=', '., '),6);
                                                                               TxtRecord += FORMAT("Contractor City", 20);
                                                                               IF STRLEN("Licence Number") > 8 THEN  //DP00331.n  Max 8 positions for licence number in file
                                                                                 FIELDERROR("Licence Number", STRSUBSTNO(Text002, "Licence Number", 8));
                                                                               TxtRecord += FORMAT("Licence Number", 8);
                                                                               //TxtRecord += FORMAT("Car Brand", 15);  //DP00331.o
                                                                               TxtRecord += FORMAT(Brand, 15);  //DP00331.n
                                                                               //TxtRecord += FORMAT("Car Type", 15);  //DP00331.o
                                                                               TxtRecord += FORMAT(Model, 15);  //DP00331.n
                                                                               TxtRecord += FORMAT("Year Build", 0, '<Integer,4><Filler Character,0>');
                                                                               //TxtRecord += FORMAT("Consumer Price", 0, '<Integer,6><Filler Character,0>');  //DP00331.o
                                                                               TxtRecord +=
                                                                                 FORMAT(Dec2IntWithMax("Consumer Price (excl. VAT)", 6, FIELDCAPTION("Consumer Price (excl. VAT)")),
                                                                                 0, '<Integer,6><Filler Character,0>');  //DP00331.n
                                                                               //TxtRecord += FORMAT("Monthly Amount", 0, '<Integer,6><Filler Character,0>');  //DP00331.o
                                                                               TxtRecord +=
                                                                                 FORMAT(Dec2IntWithMax("Monthly Amount", 6, FIELDCAPTION("Monthly Amount")),
                                                                                 0, '<Integer,6><Filler Character,0>');  //DP00331.n
                                                                               TxtRecord += FORMAT("Down Payment", 0, '<Integer,6><Filler Character,0>');
                                                                               //TxtRecord += FORMAT(BPM, 0, '<Integer,6><Filler Character,0>');  //DP00331.o
                                                                               TxtRecord +=
                                                                                 FORMAT(Dec2IntWithMax("BPM Amount", 6, FIELDCAPTION("BPM Amount")),
                                                                                 0, '<Integer,6><Filler Character,0>');  //DP00331.n
                                                                               //TxtRecord += FORMAT("Factory Accessories", 0, '<Integer,5><Filler Character,0>');  //DP00331.o
                                                                               TxtRecord +=
                                                                                 FORMAT(Dec2IntWithMax("Factory Accessories", 5, FIELDCAPTION("Factory Accessories")),
                                                                                 0, '<Integer,5><Filler Character,0>');  //DP00331.n

                                                                               IF "Contract Start" <> 0D THEN
                                                                                 ContractStartDateTxt := FORMAT("Contract Start", 0, '<Day,2><Month,2><Year4>')
                                                                               ELSE
                                                                                 ContractStartDateTxt := '0';
                                                                               TxtRecord += FORMAT(ContractStartDateTxt, 0, '<Text,8><Filler Character,0>');
                                                                               IF "Contract End" <> 0D THEN
                                                                                 ContractEndDateTxt := FORMAT("Contract End", 0, '<Day,2><Month,2><Year4>')
                                                                               ELSE
                                                                                 ContractEndDateTxt := '0';
                                                                               TxtRecord += FORMAT(ContractEndDateTxt, 0, '<Text,8><Filler Character,0>');

                                                                               //TxtRecord += FORMAT("Duration (in Months)", 0, '<Integer,3><Filler Character,0>');  //DP00331.o
                                                                               TxtRecord += FORMAT(DateForm2NoOfMonths(Duration), 0, '<Integer,3><Filler Character,0>');  //DP00331.n
                                                                               TxtRecord += FORMAT("Lease Type", 0, '<Number>');
                                                                               TxtRecord += 'EUR ';
                                                                               //TxtRecord += FORMAT("Turnover Tax No.", 0, '<Integer,9><Filler Character,0>');  //DP00331.o
                                                                               TxtRecord += FORMAT(DetermineTurnoverTaxNo("VAT Registration No."), 0, '<Integer,9><Filler Character,0>');  //DP00331.n
                                                                               TxtRecord += FORMAT("COC Registration No.", 12);
                                                                             END; //Belongs to With statement.
                                                                           END;
                                                                            }

    { [{9E44EE94-0C63-4FFC-AC58-D561B98F963E}];2 ;TxtRecord           ;Element ;Text    ;
                                                  Width=368 }

    { [{03093C4E-D453-42EE-931F-F4337CB20618}];1 ;Sluitrecord         ;Element ;Table   ;
                                                  VariableName=SluitRecord;
                                                  SourceTable=Table2000000026;
                                                  SourceTableView=SORTING(Field1)
                                                                  WHERE(Field1=CONST(1));
                                                  Export::OnAfterGetRecord=BEGIN
                                                                             NoRecords += 1;
                                                                             TxtSluitRecord:= '99' + FORMAT(NoRecords, 0, '<Integer,9><Filler Character,0>');
                                                                             TxtSluitRecord := FORMAT(TxtSluitRecord, 368); //fill with spaces
                                                                           END;
                                                                            }

    { [{B199B3B3-5328-4F3C-9249-F830745B6D01}];2 ;TxtSluitRecord      ;Element ;Text    ;
                                                  Width=368 }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   Year := DATE2DMY(TODAY, 3) - 1; //overrule saved values
                   FilePath := STRSUBSTNO('c:\temp\lease%1',FORMAT(DMY2DATE(1, 1, Year), 0, '<Year>'));
                 END;

    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525001;1;Group  ;
                  GroupType=Group }

      { 1100525002;2;Field  ;
                  CaptionML=[ENU=Year;
                             SVE=èr];
                  SourceExpr=Year;
                  MinValue=2000;
                  MaxValue=2999;
                  OnValidate=BEGIN
                               FilePath := STRSUBSTNO('c:\temp\lease%1',FORMAT(DMY2DATE(1, 1, Year), 0, '<Year>'));
                             END;
                              }

      { 1100525003;2;Field  ;
                  CaptionML=[ENU=Lease Company;
                             SVE=Leasingfîretag];
                  SourceExpr=LeaseCompany }

      { 1100525004;2;Field  ;
                  CaptionML=[ENU=Contact Person;
                             SVE=Kontaktperson];
                  SourceExpr=ContactPerson }

    }
  }
  CODE
  {
    VAR
      RecRef@1100525007 : RecordRef;
      XMLportStatusDialogMgt@1100525006 : Codeunit 11130006;
      LeaseCompany@1100525004 : Text[70];
      ContactPerson@1100525003 : Text[70];
      Year@1100525002 : Integer;
      FilePath@1100525001 : Text[1000];
      NoRecords@1100525000 : Integer;
      Text001@1100525005 : TextConst 'ENU=%1 %2 cannot be found for %3/%4 %5/%6.;SVE=%1 %2 kan inte hittas fîr %3/%4 %5/%6.';
      Text002@1100529000 : TextConst 'ENU=(%1) maximum %2 positions allowed for this field in the file';

    PROCEDURE Dec2IntWithMax@1100529005(DecAmt@1100529000 : Decimal;MaxLen@1100529001 : Integer;FldCaption@1100529002 : Text[100]) IntAmt : Integer;
    BEGIN
      IntAmt := ROUND(DecAmt, 1, '<');

      IF IntAmt > POWER(10, MaxLen) - 1 THEN
        "Car Lease Contract".FIELDERROR("Licence Number", STRSUBSTNO(Text002, FldCaption, MaxLen));
    END;

    PROCEDURE DateForm2NoOfMonths@1100529010(Duration@1100529000 : DateFormula) : Integer;
    VAR
      Date1@1100529002 : Date;
      Date2@1100529003 : Date;
      Months@1100529001 : Integer;
    BEGIN
      IF FORMAT(Duration) = '' THEN
        EXIT(0);

      Date1 := DMY2DATE(1, 1, 2000);
      Date2 := CALCDATE(Duration, Date1);
      IF Date2 < Date1 THEN
        EXIT(0);

      Months := (DATE2DMY(Date2, 3) - 2000) * 12;
      Months := Months + (DATE2DMY(Date2, 2) - 1);
      IF DATE2DMY(Date2, 1) > 15 THEN
        Months := Months + 1;
    END;

    LOCAL PROCEDURE DetermineTurnoverTaxNo@1100528402(VATRegistrationNo@1100528400 : Text[20]) TurnoverTaxNo : Integer;
    BEGIN
      IF VATRegistrationNo = '' THEN
        EXIT;

      IF STRLEN(VATRegistrationNo) = 14 THEN BEGIN //VATRegistrationNo should look like NL001234567B01
        IF EVALUATE(TurnoverTaxNo, COPYSTR(VATRegistrationNo, 3, 9)) THEN;
        EXIT(TurnoverTaxNo)
      END;

      IF STRLEN(VATRegistrationNo) = 12 THEN BEGIN //VATRegistrationNo looks like 001234567B01
        IF EVALUATE(TurnoverTaxNo, COPYSTR(VATRegistrationNo, 1, 9)) THEN;
        EXIT(TurnoverTaxNo)
      END;

      IF STRLEN(VATRegistrationNo) = 11 THEN BEGIN //VATRegistrationNo looks like NL001234567
        IF EVALUATE(TurnoverTaxNo, COPYSTR(VATRegistrationNo, 3)) THEN;
        EXIT(TurnoverTaxNo)
      END;

      IF EVALUATE(TurnoverTaxNo, VATRegistrationNo) THEN; //VATRegistrationNo looks like 001234567
      IF TurnoverTaxNo > POWER(10, 9) - 1 THEN
        TurnoverTaxNo := 0;
    END;

    PROCEDURE InitXMLport@1100529500(iLeaseCompany@1100529502 : Text[70];iContactPerson@1100529501 : Text[70];iYear@1100529500 : Integer);
    BEGIN
      LeaseCompany := iLeaseCompany;
      ContactPerson := iContactPerson;
      Year := iYear;
    END;

    BEGIN
    {
      CC: Dataport 11012560
    }
    END.
  }
}

