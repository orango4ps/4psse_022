OBJECT Table 11071969 Object Consumption Component
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ObjectConsumptionComponent@1100528800 : Record 11071969;
               StandardObject@1100528801 : Record 11020334;
               ServiceObject@1100528802 : Record 11071691;
             BEGIN
               IF "Service Object Group" <> '' THEN BEGIN
                 StandardObject.SETRANGE("Object Group", "Service Object Group");
                 IF StandardObject.FINDFIRST THEN BEGIN
                   ObjectConsumptionComponent.SETRANGE("Consumption Component Code", "Consumption Component Code");
                   REPEAT
                     ObjectConsumptionComponent.SETRANGE("Standard Object", StandardObject."No.");
                     IF ObjectConsumptionComponent.FINDFIRST THEN
                       ERROR(Text002, "Consumption Component Code", "Service Object Group", StandardObject."No.");
                     ServiceObject.SETRANGE("Standard Object No.", StandardObject."No.");
                     IF ServiceObject.FINDFIRST THEN
                       REPEAT
                          ObjectConsumptionComponent.SETRANGE("Standard Object");
                         ObjectConsumptionComponent.SETRANGE("Object No.", ServiceObject."No.");
                         IF ObjectConsumptionComponent.FINDFIRST THEN
                           ERROR(Text003,"Consumption Component Code", "Service Object Group", ServiceObject."No.");
                       UNTIL ServiceObject.NEXT = 0;
                   UNTIL StandardObject.NEXT = 0;
                 END;
               END;

               IF "Standard Object" <> '' THEN BEGIN
                 IF StandardObject.GET("Standard Object") THEN BEGIN
                   ObjectConsumptionComponent.SETRANGE("Consumption Component Code", "Consumption Component Code");
                   ObjectConsumptionComponent.SETRANGE("Service Object Group", StandardObject."Object Group");
                   IF ObjectConsumptionComponent.FINDFIRST THEN
                     ERROR(Text004,"Consumption Component Code", "Standard Object", ObjectConsumptionComponent."Service Object Group");
                 END;

                 ServiceObject.SETRANGE("Standard Object No.", StandardObject."No.");
                 IF ServiceObject.FINDFIRST THEN
                   REPEAT
                     ObjectConsumptionComponent.SETRANGE("Service Object Group");
                     ObjectConsumptionComponent.SETRANGE("Object No.", ServiceObject."No.");
                     IF ObjectConsumptionComponent.FINDFIRST THEN
                       ERROR(Text005,"Consumption Component Code", "Standard Object", ObjectConsumptionComponent."Object No.");
                   UNTIL ServiceObject.NEXT = 0;
               END;

               IF "Object No." <> '' THEN BEGIN
                 IF ServiceObject.GET("Object No.") THEN BEGIN
                   ObjectConsumptionComponent.SETRANGE("Consumption Component Code", "Consumption Component Code");
                   ObjectConsumptionComponent.SETRANGE("Standard Object", ServiceObject."Standard Object No.");
                   IF ObjectConsumptionComponent.FINDFIRST THEN
                     ERROR(Text006,"Consumption Component Code", "Object No.", ServiceObject."Standard Object No.");
                 END;
                 IF StandardObject.GET(ServiceObject."Standard Object No.") THEN BEGIN
                   ObjectConsumptionComponent.SETRANGE("Standard Object");
                   ObjectConsumptionComponent.SETRANGE("Service Object Group", StandardObject."Object Group");
                   IF ObjectConsumptionComponent.FINDFIRST THEN
                     ERROR(Text007,"Consumption Component Code", "Object No.", StandardObject."Object Group");
                 END;
               END;
             END;

    CaptionML=ENU=Object Consumption Component;
    LookupPageID=Page11126476;
    DrillDownPageID=Page11126476;
  }
  FIELDS
  {
    { 10  ;   ;Consumption Component Code;Code20  ;TableRelation="Consumption Component".Code WHERE (Component Type=FILTER(Consumption|Counter));
                                                   CaptionML=ENU=Consumption Component Code }
    { 40  ;   ;Service Object Group;Code20        ;TableRelation="Service Group".No. WHERE (Customer No.=CONST());
                                                   OnValidate=BEGIN
                                                                IF "Service Object Group" <> '' THEN BEGIN
                                                                  IF ("Standard Object" <> '') OR ("Object No." <> '') THEN
                                                                  ERROR(Text000);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Service Object Group }
    { 50  ;   ;Standard Object     ;Code20        ;TableRelation="Standard Object".No.;
                                                   OnValidate=BEGIN
                                                                IF "Standard Object" <> '' THEN BEGIN
                                                                  IF ("Service Object Group" <> '') OR ("Object No." <> '') THEN
                                                                  ERROR(Text000);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Standard Object;
                                                              NOR=Standardobjekt;
                                                              SVE=Standardobjekt:] }
    { 60  ;   ;Object No.          ;Code20        ;TableRelation=Service-Object.No.;
                                                   OnValidate=BEGIN
                                                                IF "Object No." <> '' THEN BEGIN
                                                                  IF ("Standard Object" <> '') OR ("Service Object Group" <> '') THEN
                                                                  ERROR(Text000);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Object Number }
  }
  KEYS
  {
    {    ;Consumption Component Code,Service Object Group,Standard Object,Object No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100525001 : TextConst 'ENU=Only one Service Object Group, Standard Object or Object at the time,  can be assigned.';
      Text002@1100528801 : TextConst 'ENU="Consumption Component %1 cannot be related to Service Object Group %2 because Consumption Component %1 is related to Standard Object %3. "';
      Text003@1100528802 : TextConst 'ENU="Consumption Component %1 cannot be related to Service Object Group %2 because Consumption Component %1 is related to Service Object %3. "';
      Text004@1100528803 : TextConst 'ENU="Consumption Component %1 cannot be related to Standard Object %2 because Consumption Component %1 is related to Service Object Group %3. "';
      Text005@1100528804 : TextConst 'ENU="Consumption Component %1 cannot be related to Standard Object %2 because Consumption Component %1 is related to Service Object %3. "';
      Text006@1100528805 : TextConst 'ENU="Consumption Component %1 cannot be related to Service Object %2 because Consumption Component %1 is related to Standard Object %3. "';
      Text007@1100528806 : TextConst 'ENU="Consumption Component %1 cannot be related to Service Object %2 because Consumption Component %1 is related to Service Object Group %3. "';

    BEGIN
    END.
  }
}

