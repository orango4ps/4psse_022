OBJECT Report 513 Move IC Trans. to Partner Comp
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04;
  }
  PROPERTIES
  {
    CaptionML=[DEU=IC-Trans. an Partneruntern. senden;
               ENU=Move IC Trans. to Partner Comp;
               NLD=IC-trans. naar partner verpl.;
               NOR=Flytt KI-trans. til part.sels.];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 1220;    ;DataItem;                    ;
               DataItemTable=Table414;
               DataItemTableView=SORTING(Transaction No.,IC Partner Code,Transaction Source,Document Type)
                                 ORDER(Ascending);
               OnPreDataItem=BEGIN
                               CompanyInfo.GET;
                               CompanyInfo.TESTFIELD("IC Partner Code");
                               GLSetup.GET;
                               GLSetup.TESTFIELD("LCY Code");
                             END;

               OnAfterGetRecord=BEGIN
                                  IF CurrentPartner.Code <> "IC Partner Code" THEN
                                    CurrentPartner.GET("IC Partner Code");

                                  CASE "Line Action" OF
                                    "Line Action"::"Send to IC Partner":
                                      ICInboxOutboxMgt.OutboxTransToInbox(
                                        "IC Outbox Transaction",TempICInboxTransaction,CompanyInfo."IC Partner Code");
                                    "Line Action"::"Return to Inbox":
                                      RecreateInboxTrans("IC Outbox Transaction");
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                TransferToPartner;
                              END;
                               }

    { 9410;1   ;DataItem;                    ;
               DataItemTable=Table415;
               DataItemTableView=SORTING(Transaction No.,IC Partner Code,Transaction Source,Line No.);
               OnAfterGetRecord=BEGIN
                                  IF "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" THEN
                                    ICInboxOutboxMgt.OutboxJnlLineToInbox(TempICInboxTransaction,"IC Outbox Jnl. Line",TempICInboxJnlLine);
                                END;

               DataItemLink=Transaction No.=FIELD(Transaction No.),
                            IC Partner Code=FIELD(IC Partner Code),
                            Transaction Source=FIELD(Transaction Source) }

    { 9924;2   ;DataItem;                    ;
               DataItemTable=Table423;
               DataItemTableView=SORTING(Table ID,Transaction No.,IC Partner Code,Transaction Source,Line No.,Dimension Code)
                                 ORDER(Ascending)
                                 WHERE(Table ID=CONST(415));
               OnAfterGetRecord=BEGIN
                                  IF "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" THEN
                                    ICInboxOutboxMgt.OutboxJnlLineDimToInbox(
                                      TempICInboxJnlLine,"IC Inbox/Outbox Jnl. Line Dim.",
                                      TempInboxOutboxJnlLineDim,DATABASE::"IC Inbox Jnl. Line");
                                END;

               DataItemLink=IC Partner Code=FIELD(IC Partner Code),
                            Transaction No.=FIELD(Transaction No.),
                            Line No.=FIELD(Line No.) }

    { 4982;1   ;DataItem;                    ;
               DataItemTable=Table426;
               DataItemTableView=SORTING(IC Transaction No.,IC Partner Code,Transaction Source);
               OnAfterGetRecord=BEGIN
                                  IF "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" THEN
                                    ICInboxOutboxMgt.OutboxSalesHdrToInbox(TempICInboxTransaction,"IC Outbox Sales Header",TempInboxPurchHeader);
                                END;

               DataItemLink=IC Partner Code=FIELD(IC Partner Code),
                            IC Transaction No.=FIELD(Transaction No.),
                            Transaction Source=FIELD(Transaction Source) }

    { 6933;2   ;DataItem;IC Document Dimension SH;
               DataItemTable=Table442;
               DataItemTableView=SORTING(Table ID,Transaction No.,IC Partner Code,Transaction Source,Line No.,Dimension Code)
                                 ORDER(Ascending)
                                 WHERE(Table ID=CONST(426),
                                       Line No.=CONST(0));
               OnAfterGetRecord=BEGIN
                                  IF "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" THEN
                                    ICInboxOutboxMgt.OutboxDocDimToInbox(
                                      "IC Document Dimension SH",TempICDocDim,DATABASE::"IC Inbox Purchase Header",
                                      TempInboxPurchHeader."IC Partner Code",TempInboxPurchHeader."Transaction Source");
                                END;

               DataItemLink=Transaction No.=FIELD(IC Transaction No.),
                            IC Partner Code=FIELD(IC Partner Code),
                            Transaction Source=FIELD(Transaction Source) }

    { 8929;2   ;DataItem;                    ;
               DataItemTable=Table427;
               DataItemTableView=SORTING(IC Transaction No.,IC Partner Code,Transaction Source);
               OnAfterGetRecord=BEGIN
                                  IF "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" THEN
                                    ICInboxOutboxMgt.OutboxSalesLineToInbox(TempICInboxTransaction,"IC Outbox Sales Line",TempInboxPurchLine);
                                END;

               DataItemLink=IC Transaction No.=FIELD(IC Transaction No.),
                            IC Partner Code=FIELD(IC Partner Code),
                            Transaction Source=FIELD(Transaction Source) }

    { 1776;3   ;DataItem;IC Document Dimension SL;
               DataItemTable=Table442;
               DataItemTableView=SORTING(Table ID,Transaction No.,IC Partner Code,Transaction Source,Line No.,Dimension Code)
                                 ORDER(Ascending)
                                 WHERE(Table ID=CONST(427));
               OnAfterGetRecord=BEGIN
                                  IF "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" THEN
                                    ICInboxOutboxMgt.OutboxDocDimToInbox(
                                      "IC Document Dimension SL",TempICDocDim,DATABASE::"IC Inbox Purchase Line",
                                      TempInboxPurchLine."IC Partner Code",TempInboxPurchLine."Transaction Source");
                                END;

               DataItemLink=Transaction No.=FIELD(IC Transaction No.),
                            IC Partner Code=FIELD(IC Partner Code),
                            Transaction Source=FIELD(Transaction Source),
                            Line No.=FIELD(Line No.) }

    { 5739;1   ;DataItem;                    ;
               DataItemTable=Table428;
               DataItemTableView=SORTING(IC Transaction No.,IC Partner Code,Transaction Source);
               OnAfterGetRecord=BEGIN
                                  IF "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" THEN
                                    ICInboxOutboxMgt.OutboxPurchHdrToInbox(TempICInboxTransaction,"IC Outbox Purchase Header",TempInboxSalesHeader);
                                END;

               DataItemLink=IC Partner Code=FIELD(IC Partner Code),
                            IC Transaction No.=FIELD(Transaction No.),
                            Transaction Source=FIELD(Transaction Source) }

    { 8943;2   ;DataItem;IC Document Dimension PH;
               DataItemTable=Table442;
               DataItemTableView=SORTING(Table ID,Transaction No.,IC Partner Code,Transaction Source,Line No.,Dimension Code)
                                 ORDER(Ascending)
                                 WHERE(Table ID=CONST(428),
                                       Line No.=CONST(0));
               OnAfterGetRecord=BEGIN
                                  IF "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" THEN
                                    ICInboxOutboxMgt.OutboxDocDimToInbox(
                                      "IC Document Dimension PH",TempICDocDim,DATABASE::"IC Inbox Sales Header",
                                      TempInboxSalesHeader."IC Partner Code",TempInboxSalesHeader."Transaction Source");
                                END;

               DataItemLink=Transaction No.=FIELD(IC Transaction No.),
                            IC Partner Code=FIELD(IC Partner Code),
                            Transaction Source=FIELD(Transaction Source) }

    { 4616;2   ;DataItem;                    ;
               DataItemTable=Table429;
               DataItemTableView=SORTING(IC Transaction No.,IC Partner Code,Transaction Source,Line No.);
               OnAfterGetRecord=BEGIN
                                  IF "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" THEN
                                    ICInboxOutboxMgt.OutboxPurchLineToInbox(TempICInboxTransaction,"IC Outbox Purchase Line",TempInboxSalesLine);
                                END;

               DataItemLink=IC Transaction No.=FIELD(IC Transaction No.),
                            IC Partner Code=FIELD(IC Partner Code),
                            Transaction Source=FIELD(Transaction Source) }

    { 7965;3   ;DataItem;IC Document Dimension PL;
               DataItemTable=Table442;
               DataItemTableView=SORTING(Table ID,Transaction No.,IC Partner Code,Transaction Source,Line No.,Dimension Code)
                                 ORDER(Ascending)
                                 WHERE(Table ID=CONST(429));
               OnAfterGetRecord=BEGIN
                                  IF "IC Outbox Transaction"."Line Action" = "IC Outbox Transaction"."Line Action"::"Send to IC Partner" THEN
                                    ICInboxOutboxMgt.OutboxDocDimToInbox(
                                      "IC Document Dimension PL",TempICDocDim,DATABASE::"IC Inbox Sales Line",
                                      TempInboxSalesLine."IC Partner Code",TempInboxSalesLine."Transaction Source");
                                END;

               DataItemLink=Transaction No.=FIELD(IC Transaction No.),
                            IC Partner Code=FIELD(IC Partner Code),
                            Transaction Source=FIELD(Transaction Source),
                            Line No.=FIELD(Line No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      CurrentPartner@1000 : Record 413;
      CompanyInfo@1001 : Record 79;
      GLSetup@1010 : Record 98;
      TempICInboxTransaction@1003 : TEMPORARY Record 418;
      TempICInboxJnlLine@1006 : TEMPORARY Record 419;
      TempInboxPurchHeader@1013 : TEMPORARY Record 436;
      TempInboxPurchLine@1014 : TEMPORARY Record 437;
      TempInboxSalesHeader@1002 : TEMPORARY Record 434;
      TempInboxSalesLine@1004 : TEMPORARY Record 435;
      Text001@1007 : TextConst 'DEU=Ihr IC-Registrierungscode %1 wird von IC-Partner %2 nicht erkannt.;ENU=Your IC registration code %1 is not recognized by IC Partner %2.;NLD=Uw IC-registratiecode %1 wordt niet herkend door IC-partner %2.;NOR=KI-registreringskoden %1 gjenkjennes ikke av KI-partneren %2.';
      TempInboxOutboxJnlLineDim@1005 : TEMPORARY Record 423;
      TempICDocDim@1008 : TEMPORARY Record 442;
      ICInboxOutboxMgt@1009 : Codeunit 427;
      Text002@1011 : TextConst 'DEU=%1 %2 fÅr IC Partner %3 ist bereits im IC-Eingang von IC-Partner %3 vorhanden. Der IC-Partner %3 muss die Zeilenaktion fÅr die Transaktion %2 im IC-Eingang abschlie·en.;ENU=%1 %2 to IC Partner %3 already exists in the IC inbox of IC Partner %3. IC Partner %3 must complete the line action for transaction %2 in their IC inbox.;NLD=%1 %2 naar IC-partner %3 bestaat al in de IC-inbox van IC-partner %3. IC-partner %3 moet de regelactie voor transactie %2 in de IC-inbox voltooien.;NOR=%1 %2 til KI-partneren %3 finnes allerede i KI-innboksen til KI-partneren %3. KI-partneren %3 mÜ fullfõre linjehandlingen for transaksjonen %2 i KI-innboksen.';

    LOCAL PROCEDURE TransferToPartner@9();
    VAR
      PartnerInboxTransaction@1004 : Record 418;
      PartnerInboxJnlLine@1007 : Record 419;
      PartnerInboxSalesHeader@1003 : Record 434;
      PartnerInboxSalesLine@1002 : Record 435;
      PartnerInboxPurchHeader@1001 : Record 436;
      PartnerInboxPurchLine@1000 : Record 437;
      PartnerInboxOutboxJnlLineDim@1010 : Record 423;
      PartnerICDocDim@1008 : Record 442;
      PartnerICPartner@1005 : Record 413;
    BEGIN
      PartnerICPartner.CHANGECOMPANY(CurrentPartner."Inbox Details");
      IF NOT PartnerICPartner.GET(CompanyInfo."IC Partner Code") THEN
        ERROR(Text001,CompanyInfo."IC Partner Code",CurrentPartner.Code);

      PartnerInboxTransaction.CHANGECOMPANY(CurrentPartner."Inbox Details");
      PartnerInboxTransaction.LOCKTABLE;
      IF TempICInboxTransaction.FIND('-') THEN
        REPEAT
          PartnerInboxTransaction := TempICInboxTransaction;
          IF NOT PartnerInboxTransaction.INSERT THEN
            ERROR(
              Text002,TempICInboxTransaction.FIELDCAPTION("Transaction No."),
              TempICInboxTransaction."Transaction No.",
              TempICInboxTransaction."IC Partner Code");
        UNTIL TempICInboxTransaction.NEXT = 0;

      PartnerInboxJnlLine.CHANGECOMPANY(CurrentPartner."Inbox Details");
      IF TempICInboxJnlLine.FIND('-') THEN
        REPEAT
          PartnerInboxJnlLine := TempICInboxJnlLine;
          IF PartnerInboxJnlLine."Currency Code" = '' THEN
            PartnerInboxJnlLine."Currency Code" := GLSetup."LCY Code";
          IF PartnerInboxJnlLine."Currency Code" = CurrentPartner."Currency Code" THEN
            PartnerInboxJnlLine."Currency Code" := '';
          PartnerInboxJnlLine.INSERT;
        UNTIL TempICInboxJnlLine.NEXT = 0;

      PartnerInboxPurchHeader.CHANGECOMPANY(CurrentPartner."Inbox Details");
      IF TempInboxPurchHeader.FIND('-') THEN
        REPEAT
          PartnerInboxPurchHeader := TempInboxPurchHeader;
          PartnerInboxPurchHeader."Buy-from Vendor No." := PartnerICPartner."Vendor No.";
          PartnerInboxPurchHeader."Pay-to Vendor No." := PartnerICPartner."Vendor No.";
          OnBeforePartnerInboxPurchHeaderInsert(PartnerInboxPurchHeader,CurrentPartner);
          PartnerInboxPurchHeader.INSERT;
        UNTIL TempInboxPurchHeader.NEXT = 0;

      PartnerInboxPurchLine.CHANGECOMPANY(CurrentPartner."Inbox Details");
      IF TempInboxPurchLine.FIND('-') THEN
        REPEAT
          PartnerInboxPurchLine := TempInboxPurchLine;
          PartnerInboxPurchLine.INSERT;
        UNTIL TempInboxPurchLine.NEXT = 0;

      PartnerInboxSalesHeader.CHANGECOMPANY(CurrentPartner."Inbox Details");
      IF TempInboxSalesHeader.FIND('-') THEN
        REPEAT
          PartnerInboxSalesHeader := TempInboxSalesHeader;
          PartnerInboxSalesHeader."Sell-to Customer No." := PartnerICPartner."Customer No.";
          PartnerInboxSalesHeader."Bill-to Customer No." := PartnerICPartner."Customer No.";
          PartnerInboxSalesHeader.INSERT;
        UNTIL TempInboxSalesHeader.NEXT = 0;

      PartnerInboxSalesLine.CHANGECOMPANY(CurrentPartner."Inbox Details");
      IF TempInboxSalesLine.FIND('-') THEN
        REPEAT
          PartnerInboxSalesLine := TempInboxSalesLine;
          PartnerInboxSalesLine.INSERT;
        UNTIL TempInboxSalesLine.NEXT = 0;

      PartnerInboxOutboxJnlLineDim.CHANGECOMPANY(CurrentPartner."Inbox Details");
      IF TempInboxOutboxJnlLineDim.FIND('-') THEN
        REPEAT
          PartnerInboxOutboxJnlLineDim := TempInboxOutboxJnlLineDim;
          PartnerInboxOutboxJnlLineDim.INSERT;
        UNTIL TempInboxOutboxJnlLineDim.NEXT = 0;

      PartnerICDocDim.CHANGECOMPANY(CurrentPartner."Inbox Details");
      IF TempICDocDim.FIND('-') THEN
        REPEAT
          PartnerICDocDim := TempICDocDim;
          PartnerICDocDim.INSERT;
        UNTIL TempICDocDim.NEXT = 0;

      OnICInboxTransactionCreated(PartnerInboxTransaction,CurrentPartner."Inbox Details");

      TempICInboxTransaction.DELETEALL;
      TempInboxPurchHeader.DELETEALL;
      TempInboxPurchLine.RESET;
      TempInboxPurchLine.DELETEALL;
      TempInboxSalesHeader.DELETEALL;
      TempInboxSalesLine.RESET;
      TempInboxSalesLine.DELETEALL;
      TempICInboxJnlLine.RESET;
      TempICInboxJnlLine.DELETEALL;
      TempInboxOutboxJnlLineDim.DELETEALL;
      TempICDocDim.DELETEALL;
    END;

    [External]
    PROCEDURE RecreateInboxTrans@1(OutboxTrans@1000 : Record 414);
    VAR
      ICInboxTrans@1001 : Record 418;
      ICInboxJnlLine@1002 : Record 419;
      ICInboxSalesHdr@1003 : Record 434;
      ICInboxSalesLine@1004 : Record 435;
      ICInboxPurchHdr@1005 : Record 436;
      ICInboxPurchLine@1006 : Record 437;
      ICInboxOutboxJnlLineDim@1007 : Record 423;
      HandledICInboxTrans@1014 : Record 420;
      HandledICInboxJnlLine@1013 : Record 421;
      HandledICInboxSalesHdr@1012 : Record 438;
      HandledICInboxSalesLine@1011 : Record 439;
      HandledICInboxPurchHdr@1010 : Record 440;
      HandledICInboxPurchLine@1009 : Record 441;
      HandledICInboxOutboxJnlLineDim@1016 : Record 423;
      ICCommentLine@1017 : Record 424;
      HandledICCommentLine@1018 : Record 424;
    BEGIN
      HandledICInboxTrans.LOCKTABLE;
      ICInboxTrans.LOCKTABLE;

      HandledICInboxTrans.GET(
        OutboxTrans."Transaction No.",OutboxTrans."IC Partner Code",
        ICInboxTrans."Transaction Source"::"Created by Partner",OutboxTrans."Document Type");
      ICInboxTrans.TRANSFERFIELDS(HandledICInboxTrans,TRUE);
      ICInboxTrans."Line Action" := ICInboxTrans."Line Action"::"No Action";
      ICInboxTrans.INSERT;
      HandledICInboxTrans.DELETE;

      HandledICCommentLine.SETRANGE("Table Name",HandledICCommentLine."Table Name"::"Handled IC Inbox Transaction");
      HandledICCommentLine.SETRANGE("Transaction No.",HandledICInboxTrans."Transaction No.");
      HandledICCommentLine.SETRANGE("IC Partner Code",HandledICInboxTrans."IC Partner Code");
      HandledICCommentLine.SETRANGE("Transaction Source",HandledICInboxTrans."Transaction Source");
      IF HandledICCommentLine.FIND('-') THEN
        REPEAT
          ICCommentLine := HandledICCommentLine;
          ICCommentLine."Table Name" := ICCommentLine."Table Name"::"IC Inbox Transaction";
          ICCommentLine.INSERT;
          HandledICCommentLine.DELETE;
        UNTIL HandledICCommentLine.NEXT = 0;

      WITH HandledICInboxJnlLine DO BEGIN
        SETRANGE("Transaction No.",ICInboxTrans."Transaction No.");
        SETRANGE("IC Partner Code",ICInboxTrans."IC Partner Code");
        SETRANGE("Transaction Source",ICInboxTrans."Transaction Source");
        IF FIND('-') THEN
          REPEAT
            ICInboxJnlLine.TRANSFERFIELDS(HandledICInboxJnlLine,TRUE);
            ICInboxJnlLine.INSERT;
            HandledICInboxOutboxJnlLineDim.SETRANGE("Table ID",DATABASE::"Handled IC Inbox Jnl. Line");
            HandledICInboxOutboxJnlLineDim.SETRANGE("Transaction No.","Transaction No.");
            HandledICInboxOutboxJnlLineDim.SETRANGE("IC Partner Code","IC Partner Code");
            IF HandledICInboxOutboxJnlLineDim.FIND('-') THEN
              REPEAT
                ICInboxOutboxJnlLineDim := HandledICInboxOutboxJnlLineDim;
                ICInboxOutboxJnlLineDim."Table ID" := DATABASE::"IC Inbox Jnl. Line";
                ICInboxOutboxJnlLineDim.INSERT;
                HandledICInboxOutboxJnlLineDim.DELETE;
              UNTIL HandledICInboxOutboxJnlLineDim.NEXT = 0;
            DELETE;
          UNTIL NEXT = 0;
      END;

      WITH HandledICInboxSalesHdr DO BEGIN
        SETRANGE("IC Transaction No.",ICInboxTrans."Transaction No.");
        SETRANGE("IC Partner Code",ICInboxTrans."IC Partner Code");
        SETRANGE("Transaction Source",ICInboxTrans."Transaction Source");
        IF FIND('-') THEN
          REPEAT
            ICInboxSalesHdr.TRANSFERFIELDS(HandledICInboxSalesHdr,TRUE);
            ICInboxSalesHdr.INSERT;
            MoveHandledICDocDim(
              DATABASE::"Handled IC Inbox Sales Header",DATABASE::"IC Inbox Sales Header",
              "IC Transaction No.","IC Partner Code");

            HandledICInboxSalesLine.SETRANGE("IC Transaction No.","IC Transaction No.");
            HandledICInboxSalesLine.SETRANGE("IC Partner Code","IC Partner Code");
            HandledICInboxSalesLine.SETRANGE("Transaction Source","Transaction Source");
            IF HandledICInboxSalesLine.FIND('-') THEN
              REPEAT
                ICInboxSalesLine.TRANSFERFIELDS(HandledICInboxSalesLine,TRUE);
                ICInboxSalesLine.INSERT;
                MoveHandledICDocDim(
                  DATABASE::"Handled IC Inbox Sales Line",DATABASE::"IC Inbox Sales Line",
                  "IC Transaction No.","IC Partner Code");
                OnBeforeHandledICInboxSalesLineDelete(HandledICInboxSalesLine);
                HandledICInboxSalesLine.DELETE;
              UNTIL HandledICInboxSalesLine.NEXT = 0;
            OnBeforeHandledICInboxSalesHdrDelete(HandledICInboxSalesHdr);
            DELETE;
          UNTIL NEXT = 0;
      END;

      WITH HandledICInboxPurchHdr DO BEGIN
        SETRANGE("IC Transaction No.",ICInboxTrans."Transaction No.");
        SETRANGE("IC Partner Code",ICInboxTrans."IC Partner Code");
        SETRANGE("Transaction Source",ICInboxTrans."Transaction Source");
        IF FIND('-') THEN
          REPEAT
            ICInboxPurchHdr.TRANSFERFIELDS(HandledICInboxPurchHdr,TRUE);
            ICInboxPurchHdr.INSERT;
            MoveHandledICDocDim(
              DATABASE::"Handled IC Inbox Purch. Header",DATABASE::"IC Inbox Purchase Header",
              "IC Transaction No.","IC Partner Code");

            HandledICInboxPurchLine.SETRANGE("IC Transaction No.","IC Transaction No.");
            HandledICInboxPurchLine.SETRANGE("IC Partner Code","IC Partner Code");
            HandledICInboxPurchLine.SETRANGE("Transaction Source","Transaction Source");
            IF HandledICInboxPurchLine.FIND('-') THEN
              REPEAT
                ICInboxPurchLine.TRANSFERFIELDS(HandledICInboxPurchLine,TRUE);
                ICInboxPurchLine.INSERT;
                MoveHandledICDocDim(
                  DATABASE::"Handled IC Inbox Purch. Line",DATABASE::"IC Inbox Purchase Line",
                  "IC Transaction No.","IC Partner Code");
                OnBeforeHandledICInboxPurchLineDelete(HandledICInboxPurchLine);
                HandledICInboxPurchLine.DELETE;
              UNTIL HandledICInboxPurchLine.NEXT = 0;
            OnBeforeHandledICInboxPurchHdrDelete(HandledICInboxPurchHdr);
            DELETE;
          UNTIL NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE MoveHandledICDocDim@46(FromTableID@1002 : Integer;ToTableID@1007 : Integer;TransactionNo@1003 : Integer;PartnerCode@1004 : Code[20]);
    VAR
      ICDocDim@1000 : Record 442;
      HandledICDocDim@1001 : Record 442;
    BEGIN
      HandledICDocDim.SETRANGE("Table ID",FromTableID);
      HandledICDocDim.SETRANGE("Transaction No.",TransactionNo);
      HandledICDocDim.SETRANGE("IC Partner Code",PartnerCode);
      IF HandledICDocDim.FIND('-') THEN
        REPEAT
          ICDocDim := HandledICDocDim;
          ICDocDim."Table ID" := ToTableID;
          ICDocDim.INSERT;
          HandledICDocDim.DELETE;
        UNTIL HandledICDocDim.NEXT = 0;
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeHandledICInboxPurchHdrDelete@8(VAR HandledICInboxPurchHeader@1000 : Record 440);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeHandledICInboxPurchLineDelete@7(VAR HandledICInboxPurchLine@1000 : Record 441);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeHandledICInboxSalesHdrDelete@3(VAR HandledICInboxSalesHdr@1000 : Record 438);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeHandledICInboxSalesLineDelete@5(VAR HandledICInboxSalesLine@1000 : Record 439);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePartnerInboxPurchHeaderInsert@4(VAR ICInboxPurchaseHeader@1000 : Record 436;ICPartner@1001 : Record 413);
    BEGIN
    END;

    [Integration(TRUE)]
    PROCEDURE OnICInboxTransactionCreated@2(VAR ICInboxTransaction@1000 : Record 418;PartnerCompanyName@1001 : Text);
    BEGIN
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

