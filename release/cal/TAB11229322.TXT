OBJECT Table 11229322 Purch. Rcpt. Line Hand. Unit
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Purch. Rcpt. Line No.");
             END;

    OnRename=BEGIN
               "Handling Unit Quantity" := 0;
             END;

    CaptionML=ENU=Purch. Rcpt. Line Hand. Unit;
  }
  FIELDS
  {
    { 10  ;   ;Purch. Rcpt. No.    ;Code20        ;TableRelation="Purch. Rcpt. Header";
                                                   CaptionML=ENU=Purch. Rcpt. No.;
                                                   NotBlank=Yes }
    { 20  ;   ;Purch. Rcpt. Line No.;Integer      ;TableRelation="Purch. Rcpt. Line"."Line No." WHERE (Document No.=FIELD(Purch. Rcpt. No.));
                                                   OnValidate=VAR
                                                                PurchRcptLineLoc@1100525000 : Record 121;
                                                              BEGIN
                                                                IF PurchRcptLineLoc.GET("Purch. Rcpt. No.","Purch. Rcpt. Line No.") THEN BEGIN
                                                                  "Job No." := PurchRcptLineLoc."Job No.";
                                                                  "Item No." := PurchRcptLineLoc."Item No.";
                                                                  "Vendor (Trade Item)" := PurchRcptLineLoc."Vendor (Trade Item)";
                                                                  Manufacturer := PurchRcptLineLoc.Manufacturer;
                                                                  "Basic Item" := PurchRcptLineLoc."Basic Item";
                                                                  Description := PurchRcptLineLoc.Description;
                                                                  "Description 2" := PurchRcptLineLoc."Description 2";
                                                                  "Receipt Quantity" := PurchRcptLineLoc.Quantity;
                                                                  "Unit of Measure Code" := PurchRcptLineLoc."Unit of Measure Code";
                                                                  "Handling Unit Quantity" := PurchRcptRemainingQty;
                                                                END ELSE BEGIN
                                                                  "Job No." := '';
                                                                  "Item No." := '';
                                                                  "Vendor (Trade Item)" := '';
                                                                  Manufacturer := '';
                                                                  "Basic Item" := '';
                                                                  Description := '';
                                                                  "Description 2" := '';
                                                                  "Receipt Quantity" := 0;
                                                                  "Unit of Measure Code" := '';
                                                                  "Handling Unit Quantity" := 0;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Purch. Rcpt. Line No. }
    { 30  ;   ;Handling Unit Code  ;Code20        ;TableRelation="Handling Unit";
                                                   CaptionML=ENU=Handling Unit Code;
                                                   NotBlank=Yes }
    { 40  ;   ;Handling Unit Quantity;Decimal     ;OnValidate=VAR
                                                                PurchRcptLineHandUnitLoc@1100525000 : Record 11229322;
                                                              BEGIN
                                                                IF "Handling Unit Quantity" > 0 THEN BEGIN
                                                                  TESTFIELD("Purch. Rcpt. No.");
                                                                  TESTFIELD("Purch. Rcpt. Line No.");

                                                                  PurchRcptLineHandUnitLoc := Rec;
                                                                  PurchRcptLineHandUnitLoc.SETFILTER("Handling Unit Code Filter",'<>%1',"Handling Unit Code");
                                                                  PurchRcptLineHandUnitLoc.CALCFIELDS("Total Handling Unit Quantity");
                                                                  IF "Handling Unit Quantity" + PurchRcptLineHandUnitLoc."Total Handling Unit Quantity" > "Receipt Quantity" THEN
                                                                    ERROR(Text001,FIELDCAPTION("Handling Unit Quantity"),FIELDCAPTION("Receipt Quantity"));
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Handling Unit Quantity;
                                                   MinValue=0 }
    { 50  ;   ;Job No.             ;Code20        ;TableRelation=Job;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr];
                                                   Editable=No }
    { 60  ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   CaptionML=[ENU=Item No.;
                                                              NOR=Varenr.;
                                                              SVE=Artikelnr];
                                                   Editable=No }
    { 70  ;   ;Vendor (Trade Item) ;Code15        ;TableRelation="ETIM Vendor - Central Mgt";
                                                   CaptionML=[ENU=Vendor (Trade Item);
                                                              NOR=Leverand›r (katalogartikkel);
                                                              SVE=Leverant”r (skaffningsartikel)];
                                                   Editable=No }
    { 80  ;   ;Manufacturer        ;Code15        ;TableRelation="ETIM Vendor - Central Mgt";
                                                   CaptionML=[ENU=Manufacturer;
                                                              NOR=Produsent;
                                                              SVE=Tillverkare];
                                                   Editable=No }
    { 90  ;   ;Basic Item          ;Code20        ;TableRelation="Basic Item"."Product Code" WHERE (Manufacturer=FIELD(Manufacturer));
                                                   CaptionML=[ENU=Basic Item;
                                                              NOR=Fabrikantartikkel;
                                                              SVE=Basartikel];
                                                   Editable=No }
    { 100 ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning];
                                                   Editable=No }
    { 101 ;   ;Description 2       ;Text50        ;CaptionML=[ENU=Description 2;
                                                              NOR=Beskrivelse 2;
                                                              SVE=Beskrivning 2];
                                                   Editable=No }
    { 110 ;   ;Receipt Quantity    ;Decimal       ;CaptionML=[ENU=Receipt Quantity;
                                                              NOR=Kvittering, antall;
                                                              SVE=Inleverans, antal];
                                                   Editable=No }
    { 120 ;   ;Unit of Measure Code;Code10        ;TableRelation="Item Unit of Measure".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              NOR=Enhetskode;
                                                              SVE=Enhetskod];
                                                   Editable=No }
    { 130 ;   ;Total Handling Unit Quantity;Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("Purch. Rcpt. Line Hand. Unit"."Handling Unit Quantity" WHERE (Purch. Rcpt. No.=FIELD(Purch. Rcpt. No.),
                                                                                                                                  Purch. Rcpt. Line No.=FIELD(Purch. Rcpt. Line No.),
                                                                                                                                  Handling Unit Code=FIELD(Handling Unit Code Filter)));
                                                   CaptionML=ENU=Total Handling Unit Quantity;
                                                   Editable=No }
    { 131 ;   ;Handling Unit Code Filter;Code20   ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Handling Unit Code Filter }
  }
  KEYS
  {
    {    ;Purch. Rcpt. No.,Purch. Rcpt. Line No.,Handling Unit Code;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100525000 : TextConst 'ENU=Sum of %1 in Purchase Receipt Handling Unit Lines cannot be more than %2 in Purchase Receipt Line.';

    PROCEDURE PurchRcptRemainingQty@1100525000() : Decimal;
    BEGIN
      IF ("Purch. Rcpt. No." = '') OR ("Purch. Rcpt. Line No." = 0) THEN
        EXIT(0);
      CALCFIELDS("Total Handling Unit Quantity");
      EXIT("Receipt Quantity" - "Total Handling Unit Quantity");
    END;

    BEGIN
    {
      4PS 06-06-16 UKR-C21923 KD: Record weight and dimensions on receipt (BI004) - Object created
    }
    END.
  }
}

