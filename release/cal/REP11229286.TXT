OBJECT Report 11229286 Get Defaults Advice Order
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Select options for Advice Order;
    ProcessingOnly=Yes;
  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   ShowOption := TRUE;
                   IF (TableNo = DATABASE::"Project Object") OR (ServiceOrderNo <> '') THEN
                     ShowOption := FALSE;
                   IF (ServiceOrderNo <> '') THEN
                     ShippingDate := TODAY;
                 END;

    }
    CONTROLS
    {
      { 1100528900;;Container;
                  Name=transfer;
                  ContainerType=ContentArea }

      { 1100528906;1;Field  ;
                  Name=Deliver;
                  CaptionML=[ENU=Deliver;
                             SVE=Leverera];
                  OptionCaptionML=[ENU=" ,Location,Project Location";
                                   SVE=" ,Lagerst„lle,Etablering"];
                  SourceExpr=Deliver;
                  OnValidate=BEGIN
                               ValidateDefaults(1);
                             END;
                              }

      { 1100528905;1;Field  ;
                  Name=LocationCode;
                  CaptionML=[ENU=Ship to Location;
                             SVE=Leverera till lagerst„lle];
                  SourceExpr=LocationCode;
                  OnValidate=BEGIN
                               ValidateDefaults(2);
                             END;

                  OnLookup=BEGIN
                             IF Deliver = Deliver::"Project Location" THEN BEGIN
                               ProjectLocation.SETRANGE("Project No.", ProjectNo);
                               IF PAGE.RUNMODAL(PAGE::"Project Location", ProjectLocation, ProjectLocation.Code) = ACTION::LookupOK THEN
                                 LocationCode := ProjectLocation.Code;
                             END;
                             IF Deliver = Deliver::Location THEN
                               IF PAGE.RUNMODAL(PAGE::"Location List", Location, Location.Code) = ACTION::LookupOK THEN
                                 LocationCode := Location.Code;
                           END;
                            }

      { 1100528904;1;Field  ;
                  Name=ShippingDate;
                  CaptionML=[ENU=Shipment Date;
                             SVE=Utleveransdatum];
                  SourceExpr=ShippingDate;
                  OnValidate=BEGIN
                               ValidateDefaults(3);
                             END;
                              }

      { 1100528903;1;Field  ;
                  Name=TransferCommentLines;
                  CaptionML=[ENU=Transfer Comment Lines;
                             SVE=™verf”r kommentarsrader];
                  SourceExpr=TransferCommentLines;
                  Visible=ShowOption }

      { 1100528902;1;Field  ;
                  Name=CompressByElement;
                  CaptionML=ENU=Ignore Elements;
                  SourceExpr=CompressByElement;
                  Visible=ShowOption }

      { 1100528901;1;Field  ;
                  Name=CompressByVariContract;
                  CaptionML=ENU=Ignore Variation Contracts;
                  SourceExpr=CompressByVariContract;
                  Visible=ShowOption }

      { 1100527400;1;Field  ;
                  Name=CompressByPlotNo;
                  CaptionML=ENU=Ignore Plot Nos;
                  SourceExpr=CompressByPlotNo;
                  Visible=ShowOption }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Deliver@1100528908 : ' ,Location,Project Location';
      LocationCode@1100528907 : Code[10];
      ShippingDate@1100528906 : Date;
      ProjectNo@1100528905 : Code[20];
      ServiceOrderNo@1100528912 : Code[20];
      TransferCommentLines@1100528904 : Boolean;
      CompressByElement@1100528903 : Boolean;
      CompressByVariContract@1100528902 : Boolean;
      Job@1100528909 : Record 11072003;
      Location@1100528901 : Record 14;
      ProjectLocation@1100528900 : Record 11012011;
      ShowOption@1100528910 : Boolean INDATASET;
      TableNo@1100528911 : Integer;
      CompressByPlotNo@1100527400 : Boolean;

    PROCEDURE SetSelection@1100528900(iProjectNo@1100528900 : Text;iServiceOrderNo@1100528901 : Text;iTableNo@1100528902 : Integer);
    BEGIN
      ProjectNo := iProjectNo;
      ServiceOrderNo := iServiceOrderNo;
      TableNo := iTableNo;
      GetDefaults;  //db, 21-01-20
    END;

    PROCEDURE GetSelection@1100529801(VAR varDeliver@1100528902 : ' ,Location,Project Location';VAR varLocation@1100528901 : Code[10];VAR varShippingDate@1100528900 : Date;VAR varTransferCommentLines@1100529800 : Boolean;VAR varCompressByElement@1210190000 : Boolean;VAR varCompressByVariContract@1210190001 : Boolean;VAR varCompressByPlotNo@1100527400 : Boolean);
    BEGIN
      varDeliver := Deliver;
      varLocation := LocationCode;
      varShippingDate := ShippingDate;
      varTransferCommentLines := TransferCommentLines;
      varCompressByElement := CompressByElement;
      varCompressByVariContract := CompressByVariContract;
      varCompressByPlotNo := CompressByPlotNo;
    END;

    PROCEDURE GetDefaults@1000000001();
    VAR
      ProjectLocation@1100528900 : Record 11012011;
    BEGIN
      //db, 21-01-20
      IF ProjectNo <> '' THEN BEGIN
        Job.GET(ProjectNo);
        CASE Job.Deliver OF
          Job.Deliver::Location:
            BEGIN
              Deliver := Deliver::Location;
              LocationCode := Job."Ship To Location";
            END;
          Job.Deliver::"Project Location":
            BEGIN
              Deliver := Deliver::"Project Location";
              IF Job."Ship To Location" <> '' THEN
                LocationCode := Job."Ship To Location"
              ELSE BEGIN
                ProjectLocation.SETRANGE("Project No.", ProjectNo);
                IF ProjectLocation.FINDFIRST THEN
                  IF ProjectLocation.NEXT = 0 THEN
                    LocationCode := ProjectLocation.Code;
              END;
            END;
        END;
      END;

      ShippingDate := TODAY;
      TransferCommentLines := TRUE;
    END;

    PROCEDURE ValidateDefaults@1(lAction@11012000 : Integer);
    BEGIN
      CASE lAction OF
        1: BEGIN
             IF Deliver = Deliver::" " THEN BEGIN
               LocationCode := ''
             END ELSE BEGIN
               IF Deliver = Deliver::"Project Location" THEN
                 IF NOT ProjectLocation.GET(ProjectNo, LocationCode) THEN
                   LocationCode := '';
               IF Deliver = Deliver::Location THEN
                 IF NOT Location.GET(LocationCode) THEN
                   LocationCode := '';
             END;
           END;
        2: BEGIN
             IF Deliver = Deliver::"Project Location" THEN
               ProjectLocation.GET(ProjectNo, LocationCode);
             IF Deliver = Deliver::Location THEN
               Location.GET(LocationCode);
             IF Deliver = Deliver::" " THEN
               LocationCode := '';
           END;
        3: BEGIN
             IF ShippingDate < TODAY THEN
               IF ShippingDate <> 0D THEN
                 ShippingDate := TODAY;
           END;
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

