OBJECT Page 1626 Office OCR Incoming Documents
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Office Incoming Documents;
               NOR=Inng†ende Office-dokumenter;
               SVE=Inkommande dokument till Office];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table1602;
    DataCaptionExpr=PageCaptionTxt;
    SourceTableView=SORTING(Name)
                    ORDER(Ascending);
    PageType=StandardDialog;
    SourceTableTemporary=Yes;
    OnOpenPage=BEGIN
                 IF COUNT = 1 THEN BEGIN
                   Selected := TRUE;
                   MODIFY;
                 END ELSE
                   IF IncomingDocumentAttachment."Document No. Filter" <> '' THEN
                     MODIFYALL(Selected,FALSE);
               END;

    OnQueryClosePage=VAR
                       IncomingDocument@1001 : Record 130;
                     BEGIN
                       IF CloseAction IN [ACTION::OK,ACTION::LookupOK] THEN BEGIN
                         SETRANGE(Selected,TRUE);
                         IF FINDSET THEN BEGIN
                           REPEAT
                             CASE InitiatedAction OF
                               InitiatedAction::InitiateSendToIncomingDocuments:
                                 OfficeMgt.SendToIncomingDocument(Rec,IncomingDocument,IncomingDocumentAttachment);
                               InitiatedAction::InitiateSendToOCR:
                                 IF OfficeMgt.SendToIncomingDocument(Rec,IncomingDocument,IncomingDocumentAttachment) THEN
                                   OfficeMgt.SendToOCR(IncomingDocument);
                               InitiatedAction::InitiateSendToWorkFlow:
                                 IF OfficeMgt.SendToIncomingDocument(Rec,IncomingDocument,IncomingDocumentAttachment) THEN
                                   OfficeMgt.SendApprovalRequest(IncomingDocument);
                             END;
                           UNTIL NEXT = 0;
                           IF InitiatedAction = InitiatedAction::InitiateSendToOCR THEN
                             OfficeMgt.DisplayOCRUploadSuccessMessage(COUNT);
                         END;
                       END;
                     END;

  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 4   ;2   ;Field     ;
                CaptionML=[ENU=Send;
                           NOR=Send;
                           SVE=Skicka];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Selected;
                OnValidate=BEGIN
                             IF (IncomingDocumentAttachment."Document No. Filter" <> '') AND (COUNT > 1) AND Selected THEN BEGIN
                               MODIFYALL(Selected,FALSE);
                               Selected := TRUE;
                             END;
                           END;
                            }

    { 3   ;2   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Name;
                Editable=False }

  }
  CODE
  {
    VAR
      IncomingDocumentAttachment@1002 : Record 133;
      OfficeMgt@1000 : Codeunit 1630;
      PageCaptionTxt@1001 : TextConst 'ENU=Select Attachment to Send;NOR=Velg vedlegg som skal sendes;SVE=V„lj bilagan som ska skickas';

    [External]
    PROCEDURE InitializeIncomingDocumentAttachment@1(LinkedIncomingDocumentAttachment@1000 : Record 133);
    BEGIN
      IncomingDocumentAttachment := LinkedIncomingDocumentAttachment;
    END;

    [External]
    PROCEDURE InitializeExchangeObject@10(VAR TempExchangeObject@1001 : TEMPORARY Record 1602);
    BEGIN
      IF TempExchangeObject.FINDSET THEN
        REPEAT
          TempExchangeObject.CALCFIELDS(Content);
          TRANSFERFIELDS(TempExchangeObject);
          INSERT;
        UNTIL TempExchangeObject.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

