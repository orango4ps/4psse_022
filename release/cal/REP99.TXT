OBJECT Report 99 Delete Empty G/L Registers
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 45=rimd;
    CaptionML=[DEU=Leere Fibujournale l”schen;
               ENU=Delete Empty G/L Registers;
               NLD=Lege grootboekjnl. verwijderen;
               NOR=Slett tomme finansjournaler];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 9922;    ;DataItem;                    ;
               DataItemTable=Table45;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=VAR
                               ConfirmManagement@1000 : Codeunit 27;
                             BEGIN
                               IF NOT ConfirmManagement.ConfirmProcess(DeleteRegistersQst,TRUE) THEN
                                 CurrReport.BREAK;

                               Window.OPEN(
                                 Text001 +
                                 Text002 +
                                 Text003 +
                                 Text004);
                             END;

               OnAfterGetRecord=BEGIN
                                  GLEntry.SETRANGE("Entry No.","From Entry No.","To Entry No.");
                                  IF GLEntry.FINDFIRST THEN
                                    CurrReport.SKIP;
                                  CustLedgEntry.SETRANGE("Entry No.","From Entry No.","To Entry No.");
                                  IF CustLedgEntry.FINDFIRST THEN
                                    CurrReport.SKIP;
                                  VendLedgEntry.SETRANGE("Entry No.","From Entry No.","To Entry No.");
                                  IF VendLedgEntry.FINDFIRST THEN
                                    CurrReport.SKIP;
                                  VATEntry.SETRANGE("Entry No.","From VAT Entry No.","To VAT Entry No.");
                                  IF VATEntry.FINDFIRST THEN
                                    CurrReport.SKIP;
                                  BankAccLedgEntry.SETRANGE("Entry No.","From Entry No.","To Entry No.");
                                  IF BankAccLedgEntry.FINDFIRST THEN
                                    CurrReport.SKIP;
                                  FAReg.SETCURRENTKEY("Creation Date");
                                  FAReg.SETRANGE("Creation Date","Creation Date");
                                  FAReg.SETRANGE("G/L Register No.","No.");
                                  IF FAReg.FINDFIRST THEN BEGIN
                                    FALedgEntry.SETRANGE("Entry No.",FAReg."From Entry No.",FAReg."To Entry No.");
                                    IF FALedgEntry.FINDFIRST THEN
                                      CurrReport.SKIP;
                                    MaintLedgEntry.SETRANGE("Entry No.",FAReg."From Maintenance Entry No.",FAReg."To Maintenance Entry No.");
                                    IF MaintLedgEntry.FINDFIRST THEN
                                      CurrReport.SKIP;
                                  END;

                                  Window.UPDATE(1,"No.");
                                  Window.UPDATE(2,"Creation Date");
                                  DELETE;
                                  NoOfDeleted := NoOfDeleted + 1;
                                  Window.UPDATE(3,NoOfDeleted);
                                  IF NoOfDeleted >= NoOfDeleted2 + 10 THEN BEGIN
                                    NoOfDeleted2 := NoOfDeleted;
                                    COMMIT;
                                  END;
                                END;

               ReqFilterFields=Creation Date }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'DEU=Leere Fibujournale l”schen...\\;ENU=Deleting empty G/L registers...\\;NLD=Verwijderen lege grootboekjournalen...\\;NOR=Sletter tomme finansjournaler...\\';
      Text002@1002 : TextConst 'DEU=Nr.                      #1######\;ENU=No.                      #1######\;NLD=Nr.                      #1######\;NOR=Nr.                      #1######\';
      Text003@1003 : TextConst 'DEU=Gebucht am               #2######\\;ENU=Posted on                #2######\\;NLD=Geboekt op               #2######\\;NOR=Bokf›rt                  #2######\\';
      Text004@1004 : TextConst 'DEU=Anz. gel”schter Journale #3######;ENU=No. of registers deleted #3######;NLD=Verwijderde journalen    #3######;NOR=Ant. slettede journaler  #3######';
      GLEntry@1005 : Record 17;
      CustLedgEntry@1006 : Record 21;
      VendLedgEntry@1007 : Record 25;
      VATEntry@1008 : Record 254;
      BankAccLedgEntry@1009 : Record 271;
      FAReg@1013 : Record 5617;
      FALedgEntry@1014 : Record 5601;
      MaintLedgEntry@1015 : Record 11072085;
      Window@1010 : Dialog;
      NoOfDeleted@1011 : Integer;
      NoOfDeleted2@1012 : Integer;
      DeleteRegistersQst@1016 : TextConst 'DEU=M”chten Sie die Journale l”schen?;ENU=Do you want to delete the registers?;NLD=Wilt u de journalen verwijderen?;NOR=Vil du slette journalene?';

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

