OBJECT Page 11072679 Alt. Trade Items FactBox
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[DEU=Details alt. Handelsartikel;
               ENU=Alt. Trade Items Details;
               NLD=Details alt. handelsartikelen];
    PageType=CardPart;
  }
  CONTROLS
  {
    { 1100528400;;Container;
                ContainerType=ContentArea }

    { 1100528401;1;Field  ;
                CaptionML=[DEU=Gesamt alternativer Betrag;
                           ENU=Total Alternative Amount;
                           NLD=Totaal alternatief bedrag];
                SourceExpr=TotalAlternativeAmount;
                AutoFormatType=1 }

    { 1100528402;1;Field  ;
                CaptionML=[DEU=Anzahl alternativer Kreditoren;
                           ENU=No. of Alternative Vendors;
                           NLD=Aantal alternatieve leveranciers];
                SourceExpr=NoOfAlternativeVendors }

    { 1100528403;1;Field  ;
                CaptionML=[DEU=Gesamt wirklicher Betrag;
                           ENU=Total Actual Amount;
                           NLD=Totaal werkelijk bedrag];
                SourceExpr=TotalActualAmount;
                AutoFormatType=1 }

    { 1100528404;1;Field  ;
                CaptionML=[DEU=Anzahl wirklicher Debitoren;
                           ENU=No. of Actual Vendors;
                           NLD=Aantal werkelijke leveranciers];
                SourceExpr=NoOfActualVendors }

    { 1100528405;1;Field  ;
                CaptionML=[DEU=Differenz;
                           ENU=Difference;
                           NLD=Verschil;
                           NOR=Differanse;
                           SVE=Differens];
                SourceExpr=Difference;
                AutoFormatType=1 }

  }
  CODE
  {
    VAR
      TempAlternativeTradeItem@1100528405 : TEMPORARY Record 11012744;
      TotalAlternativeAmount@1100528400 : Decimal;
      TotalActualAmount@1100528402 : Decimal;
      Difference@1100528404 : Decimal;
      NoOfAlternativeVendors@1100528401 : Integer;
      NoOfActualVendors@1100528403 : Integer;

    PROCEDURE SetAlternativeTradeItem@1100528400(VAR TempAlternativeTradeItem2@1100528400 : TEMPORARY Record 11012744);
    VAR
      OrigAlternativeTradeItem@1100528401 : Record 11012744;
    BEGIN
      OrigAlternativeTradeItem := TempAlternativeTradeItem2;
      TempAlternativeTradeItem.DELETEALL;
      IF TempAlternativeTradeItem2.FINDSET THEN
        REPEAT
          TempAlternativeTradeItem := TempAlternativeTradeItem2;
          TempAlternativeTradeItem.INSERT(TRUE);
        UNTIL TempAlternativeTradeItem2.NEXT = 0;
      CalculateValues;
      TempAlternativeTradeItem2 := OrigAlternativeTradeItem;
    END;

    LOCAL PROCEDURE CalculateValues@1100528404();
    VAR
      TempETIMVendorCentralMgt@1100528400 : TEMPORARY Record 11012304;
      TempAlternativeETIMVendorCentralMgt@1100528401 : TEMPORARY Record 11012304;
    BEGIN
      CLEAR(TotalAlternativeAmount);
      CLEAR(NoOfAlternativeVendors);
      CLEAR(TotalActualAmount);
      CLEAR(NoOfActualVendors);
      CLEAR(Difference);
      IF TempAlternativeTradeItem.FINDSET THEN
        REPEAT
          TotalAlternativeAmount += TempAlternativeTradeItem."Alternative Amount";
          TempAlternativeETIMVendorCentralMgt.Code := TempAlternativeTradeItem."Alternative Vendor";
          IF TempAlternativeETIMVendorCentralMgt.INSERT THEN;
          TotalActualAmount += TempAlternativeTradeItem.Amount;
          TempETIMVendorCentralMgt.Code := TempAlternativeTradeItem.Vendor;
          IF TempETIMVendorCentralMgt.INSERT THEN;
        UNTIL TempAlternativeTradeItem.NEXT = 0;
      NoOfAlternativeVendors := TempAlternativeETIMVendorCentralMgt.COUNT;
      NoOfActualVendors := TempETIMVendorCentralMgt.COUNT;
      Difference := TotalActualAmount - TotalAlternativeAmount;
    END;

    BEGIN
    END.
  }
}

