OBJECT Codeunit 5632 FA Jnl.-Post Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.01,4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 5601=r,
                TableData 5617=rm,
                TableData 11072085=r,
                TableData 11072088=r;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU="%2 must not be %3 in %4 %5 = %6 for %1.";NOR="%2 kan ikke v‘re %3 i %4 %5 = %6 for %1.";SVE="%2 f†r inte vara %3 i %4 %5 = %6 f”r %1."';
      Text001@1001 : TextConst 'ENU="%2 = %3 must be canceled first for %1.";NOR="%2 = %3 m† f›rst kanselleres for %1.";SVE="%2 = %3 m†ste r„ttas f”rst f”r %1."';
      Text002@1002 : TextConst 'ENU=%1 is not a %2.;NOR=%1 er ikke et %2.;SVE=%1 „r inte en %2.';
      FA@1003 : Record 5600;
      FA2@1004 : Record 5600;
      DeprBook@1005 : Record 5611;
      FADeprBook@1006 : Record 5612;
      FALedgEntry@1007 : Record 5601;
      MaintenanceLedgEntry@1008 : Record 11072085;
      FAInsertLedgEntry@1010 : Codeunit 5600;
      FAJnlCheckLine@1011 : Codeunit 5631;
      DuplicateDeprBook@1012 : Codeunit 5640;
      CalculateDisposal@1013 : Codeunit 5605;
      CalculateDepr@1014 : Codeunit 5610;
      CalculateAcqCostDepr@1015 : Codeunit 5613;
      MakeFALedgEntry@1016 : Codeunit 5604;
      MakeMaintenanceLedgEntry@1017 : Codeunit 11072041;
      FANo@1019 : Code[20];
      BudgetNo@1020 : Code[20];
      DeprBookCode@1021 : Code[10];
      FAPostingType@1022 : 'Acquisition Cost,Depreciation,Write-Down,Appreciation,Custom 1,Custom 2,Disposal,Maintenance,Salvage Value';
      FAPostingDate@1023 : Date;
      Amount2@1024 : Decimal;
      SalvageValue@1025 : Decimal;
      DeprUntilDate@1026 : Boolean;
      DeprAcqCost@1027 : Boolean;
      ErrorEntryNo@1028 : Integer;
      ResultOnDisposal@1029 : Integer;
      Text003@1031 : TextConst 'ENU="%1 = %2 already exists for %5 (%3 = %4).";NOR="%1 = %2 finnes allerede for %5 (%3 = %4).";SVE="%1 = %2 finns redan f”r %5 (%3 = %4)."';

    [External]
    PROCEDURE FAJnlPostLine@11(FAJnlLine@1000 : Record 5621;CheckLine@1001 : Boolean);
    BEGIN
      OnBeforeFAJnlPostLine(FAJnlLine);

      FAInsertLedgEntry.SetGLRegisterNo(0);
      WITH FAJnlLine DO BEGIN
        IF "FA No." = '' THEN
          EXIT;
        IF "Posting Date" = 0D THEN
          "Posting Date" := "FA Posting Date";
        IF CheckLine THEN
          FAJnlCheckLine.CheckFAJnlLine(FAJnlLine);
        DuplicateDeprBook.DuplicateFAJnlLine(FAJnlLine);
        FANo := "FA No.";
        BudgetNo := "Budgeted FA No.";
        DeprBookCode := "Depreciation Book Code";
        FAPostingType := "FA Posting Type";
        FAPostingDate := "FA Posting Date";
        Amount2 := Amount;
        SalvageValue := "Salvage Value";
        DeprUntilDate := "Depr. until FA Posting Date";
        DeprAcqCost := "Depr. Acquisition Cost";
        ErrorEntryNo := "FA Error Entry No.";
        IF "FA Posting Type" = "FA Posting Type"::Maintenance THEN BEGIN
          MakeMaintenanceLedgEntry.CopyFromFAJnlLine(MaintenanceLedgEntry,FAJnlLine);
          PostMaintenance;
        END ELSE BEGIN
          MakeFALedgEntry.CopyFromFAJnlLine(FALedgEntry,FAJnlLine);
          PostFixedAsset;
        END;
      END;

      OnAfterFAJnlPostLine(FAJnlLine);
    END;

    [External]
    PROCEDURE GenJnlPostLine@12(GenJnlLine@1000 : Record 81;FAAmount@1001 : Decimal;VATAmount@1002 : Decimal;NextTransactionNo@1003 : Integer;NextGLEntryNo@1004 : Integer;GLRegisterNo@1005 : Integer);
    BEGIN
      OnBeforeGenJnlPostLine(GenJnlLine);

      FAInsertLedgEntry.SetGLRegisterNo(GLRegisterNo);
      FAInsertLedgEntry.DeleteAllGLAcc;
      WITH GenJnlLine DO BEGIN
        IF "Account No." = '' THEN
          EXIT;
        IF "FA Posting Date" = 0D THEN
          "FA Posting Date" := "Posting Date";
        //**4PS.so  //* Verschil tov 360, hierdoor werd bij VA-post bij aanschaf aantal nul (geeft oa probleem bij aanschaf materieel)
        //IF "Journal Template Name" = '' THEN
        //  Quantity := 0;
        //**4PS.eo
        DuplicateDeprBook.DuplicateGenJnlLine(GenJnlLine,FAAmount);
        FANo := "Account No.";
        BudgetNo := "Budgeted FA No.";
        DeprBookCode := "Depreciation Book Code";
        FAPostingType := "FA Posting Type" - 1;
        FAPostingDate := "FA Posting Date";
        Amount2 := FAAmount;
        SalvageValue := ConvertAmtFCYToLCYForSourceCurrency("Salvage Value");
        DeprUntilDate := "Depr. until FA Posting Date";
        DeprAcqCost := "Depr. Acquisition Cost";
        ErrorEntryNo := "FA Error Entry No.";
        IF "FA Posting Type" = "FA Posting Type"::Maintenance THEN BEGIN
          MakeMaintenanceLedgEntry.CopyFromGenJnlLine(MaintenanceLedgEntry,GenJnlLine);
          MaintenanceLedgEntry.Amount := FAAmount;
          MaintenanceLedgEntry."VAT Amount" := VATAmount;
          MaintenanceLedgEntry."Transaction No." := NextTransactionNo;
          MaintenanceLedgEntry."G/L Entry No." := NextGLEntryNo;
          PostMaintenance;
        END ELSE BEGIN
          MakeFALedgEntry.CopyFromGenJnlLine(FALedgEntry,GenJnlLine);
          FALedgEntry.Amount := FAAmount;
          FALedgEntry."VAT Amount" := VATAmount;
          FALedgEntry."Transaction No." := NextTransactionNo;
          FALedgEntry."G/L Entry No." := NextGLEntryNo;
          OnBeforePostFixedAssetFromGenJnlLine(GenJnlLine,FALedgEntry,FAAmount,VATAmount);
          PostFixedAsset;
        END;
      END;

      OnAfterGenJnlPostLine(GenJnlLine);
    END;

    LOCAL PROCEDURE PostFixedAsset@1();
    BEGIN
      FA.LOCKTABLE;
      DeprBook.GET(DeprBookCode);
      FA.GET(FANo);
      FA.TESTFIELD(Blocked,FALSE);
      FA.TESTFIELD(Inactive,FALSE);
      FADeprBook.GET(FANo,DeprBookCode);
      MakeFALedgEntry.CopyFromFACard(FALedgEntry,FA,FADeprBook);
      FAInsertLedgEntry.SetLastEntryNo(TRUE);
      IF (FALedgEntry."FA Posting Group" = '') AND (FALedgEntry."G/L Entry No." > 0) THEN BEGIN
        FADeprBook.TESTFIELD("FA Posting Group");
        FALedgEntry."FA Posting Group" := FADeprBook."FA Posting Group";
      END;
      IF DeprUntilDate THEN
        PostDeprUntilDate(FALedgEntry,0);
      //**4PS.so
      //IF FAPostingType = FAPostingType::Disposal THEN
      //  PostDisposalEntry(FALedgEntry)
      //ELSE BEGIN
      //**4PS.eo
      //**4PS.sn  23853
      IF FAPostingType = FAPostingType::Disposal THEN BEGIN
        FADeprBook.CALCFIELDS("Book Value");
        IF FADeprBook."Book Value" > 0 THEN BEGIN
          IF (DeprBookCode = FA.GetFA_MainDeprBook(FA."No.", FALSE)) THEN
            FAInsertLedgEntry.PostPlantBookValueOnDisposal(FALedgEntry, FADeprBook."Book Value");
        END;
        PostDisposalEntry(FALedgEntry)
      END ELSE BEGIN
      //**4PS.en
        IF PostBudget THEN
          SetBudgetAssetNo;
        IF NOT DeprLine THEN BEGIN
          FAInsertLedgEntry.SetOrgGenJnlLine(TRUE);
          FAInsertLedgEntry.InsertFA(FALedgEntry);
          FAInsertLedgEntry.SetOrgGenJnlLine(FALSE);
        END;
        PostSalvageValue(FALedgEntry);
      END;
      IF DeprAcqCost THEN
        PostDeprUntilDate(FALedgEntry,1);
      FAInsertLedgEntry.SetLastEntryNo(FALSE);
      IF PostBudget THEN
        PostBudgetAsset;
    END;

    LOCAL PROCEDURE PostMaintenance@13();
    BEGIN
      FA.LOCKTABLE;
      DeprBook.GET(DeprBookCode);
      FA.GET(FANo);
      FADeprBook.GET(FANo,DeprBookCode);
      MakeMaintenanceLedgEntry.CopyFromFACard(MaintenanceLedgEntry,FA,FADeprBook);
      IF NOT DeprBook."Allow Identical Document No." AND (MaintenanceLedgEntry."Journal Batch Name" <> '') THEN
        CheckMaintDocNo(MaintenanceLedgEntry);
      WITH MaintenanceLedgEntry DO
        IF ("FA Posting Group" = '') AND ("G/L Entry No." > 0) THEN BEGIN
          FADeprBook.TESTFIELD("FA Posting Group");
          "FA Posting Group" := FADeprBook."FA Posting Group";
        END;
      IF PostBudget THEN
        SetBudgetAssetNo;
      FAInsertLedgEntry.SetOrgGenJnlLine(TRUE);
      FAInsertLedgEntry.InsertMaintenance(MaintenanceLedgEntry);
      FAInsertLedgEntry.SetOrgGenJnlLine(FALSE);
      IF PostBudget THEN
        PostBudgetAsset;
    END;

    LOCAL PROCEDURE PostDisposalEntry@3(VAR FALedgEntry@1000 : Record 5601);
    VAR
      MaxDisposalNo@1001 : Integer;
      SalesEntryNo@1002 : Integer;
      DisposalType@1003 : 'FirstDisposal,SecondDisposal,ErrorDisposal,LastErrorDisposal';
      OldDisposalMethod@1004 : ' ,Net,Gross';
      EntryAmounts@1005 : ARRAY [14] OF Decimal;
      EntryNumbers@1006 : ARRAY [14] OF Integer;
      i@1007 : Integer;
      j@1008 : Integer;
    BEGIN
      WITH FALedgEntry DO BEGIN
        "Disposal Calculation Method" := DeprBook."Disposal Calculation Method" + 1;
        CalculateDisposal.GetDisposalType(
          FANo,DeprBookCode,ErrorEntryNo,DisposalType,
          OldDisposalMethod,MaxDisposalNo,SalesEntryNo);
        IF (MaxDisposalNo > 0) AND
           ("Disposal Calculation Method" <> OldDisposalMethod)
        THEN
          ERROR(
            Text000,
            FAName,DeprBook.FIELDCAPTION("Disposal Calculation Method"),"Disposal Calculation Method",
            DeprBook.TABLECAPTION,DeprBook.FIELDCAPTION(Code),DeprBook.Code);
        IF ErrorEntryNo = 0 THEN
          "Disposal Entry No." := MaxDisposalNo + 1
        ELSE
          IF SalesEntryNo <> ErrorEntryNo THEN
            ERROR(Text001,
              FAName,FIELDCAPTION("Disposal Entry No."),MaxDisposalNo);
        IF DisposalType = DisposalType::FirstDisposal THEN
          PostReverseType(FALedgEntry);
        IF DeprBook."Disposal Calculation Method" = DeprBook."Disposal Calculation Method"::Gross THEN
          FAInsertLedgEntry.SetOrgGenJnlLine(TRUE);
        FAInsertLedgEntry.InsertFA(FALedgEntry);
        FAInsertLedgEntry.SetOrgGenJnlLine(FALSE);
        "Automatic Entry" := TRUE;
        FAInsertLedgEntry.SetNetdisposal(FALSE);
        IF (DeprBook."Disposal Calculation Method" =
            DeprBook."Disposal Calculation Method"::Net) AND
           DeprBook."VAT on Net Disposal Entries"
        THEN
          FAInsertLedgEntry.SetNetdisposal(TRUE);

        IF DisposalType = DisposalType::FirstDisposal THEN BEGIN
          CalculateDisposal.CalcGainLoss(FANo,DeprBookCode,EntryAmounts);
          FOR i := 1 TO 14 DO
            IF EntryAmounts[i] <> 0 THEN BEGIN
              "FA Posting Category" := CalculateDisposal.SetFAPostingCategory(i);
              "FA Posting Type" := CalculateDisposal.SetFAPostingType(i);
              Amount := EntryAmounts[i];
              IF i = 1 THEN
                "Result on Disposal" := "Result on Disposal"::Gain;
              IF i = 2 THEN
                "Result on Disposal" := "Result on Disposal"::Loss;
              IF i > 2 THEN
                "Result on Disposal" := "Result on Disposal"::" ";
              IF i = 10 THEN
                SetResultOnDisposal(FALedgEntry);
              FAInsertLedgEntry.InsertFA(FALedgEntry);
              PostAllocation(FALedgEntry);
            END;
        END;
        IF DisposalType = DisposalType::SecondDisposal THEN BEGIN
          CalculateDisposal.CalcSecondGainLoss(FANo,DeprBookCode,Amount,EntryAmounts);
          FOR i := 1 TO 2 DO
            IF EntryAmounts[i] <> 0 THEN BEGIN
              "FA Posting Category" := CalculateDisposal.SetFAPostingCategory(i);
              "FA Posting Type" := CalculateDisposal.SetFAPostingType(i);
              Amount := EntryAmounts[i];
              IF i = 1 THEN
                "Result on Disposal" := "Result on Disposal"::Gain;
              IF i = 2 THEN
                "Result on Disposal" := "Result on Disposal"::Loss;
              FAInsertLedgEntry.InsertFA(FALedgEntry);
              PostAllocation(FALedgEntry);
            END;
        END;
        IF DisposalType IN
           [DisposalType::ErrorDisposal,DisposalType::LastErrorDisposal]
        THEN BEGIN
          CalculateDisposal.GetErrorDisposal(
            FANo,DeprBookCode,DisposalType = DisposalType::ErrorDisposal,MaxDisposalNo,
            EntryAmounts,EntryNumbers);
          IF DisposalType = DisposalType::ErrorDisposal THEN
            j := 2
          ELSE BEGIN
            j := 14;
            ResultOnDisposal := CalcResultOnDisposal(FANo,DeprBookCode);
          END;
          FOR i := 1 TO j DO
            IF EntryNumbers[i] <> 0 THEN BEGIN
              Amount := EntryAmounts[i];
              "Entry No." := EntryNumbers[i];
              "FA Posting Category" := CalculateDisposal.SetFAPostingCategory(i);
              "FA Posting Type" := CalculateDisposal.SetFAPostingType(i);
              IF i = 1 THEN
                "Result on Disposal" := "Result on Disposal"::Gain;
              IF i = 2 THEN
                "Result on Disposal" := "Result on Disposal"::Loss;
              IF i > 2 THEN
                "Result on Disposal" := "Result on Disposal"::" ";
              IF i = 10 THEN
                "Result on Disposal" := ResultOnDisposal;
              FAInsertLedgEntry.InsertFA(FALedgEntry);
              PostAllocation(FALedgEntry);
            END;
        END;
        FAInsertLedgEntry.CorrectEntries;
        FAInsertLedgEntry.SetNetdisposal(FALSE);
      END;
    END;

    LOCAL PROCEDURE PostDeprUntilDate@7(FALedgEntry@1000 : Record 5601;Type@1001 : 'UntilDate,AcqCost');
    VAR
      DepreciationAmount@1002 : Decimal;
      Custom1Amount@1003 : Decimal;
      NumberOfDays@1004 : Integer;
      Custom1NumberOfDays@1005 : Integer;
      DummyEntryAmounts@1006 : ARRAY [4] OF Decimal;
    BEGIN
      WITH FALedgEntry DO BEGIN
        "Automatic Entry" := TRUE;
        "FA No./Budgeted FA No." := '';
        "FA Posting Category" := "FA Posting Category"::" ";
        "No. of Depreciation Days" := 0;
        IF Type = Type::UntilDate THEN
          CalculateDepr.Calculate(
            DepreciationAmount,Custom1Amount,NumberOfDays,Custom1NumberOfDays,
            FANo,DeprBookCode,FAPostingDate,DummyEntryAmounts,0D,0)
        ELSE
          CalculateAcqCostDepr.DeprCalc(
            DepreciationAmount,Custom1Amount,FANo,DeprBookCode,
            Amount2 + SalvageValue,Amount2);
        IF Custom1Amount <> 0 THEN BEGIN
          "FA Posting Type" := "FA Posting Type"::"Custom 1";
          Amount := Custom1Amount;
          "No. of Depreciation Days" := Custom1NumberOfDays;
          FAInsertLedgEntry.InsertFA(FALedgEntry);
          IF "G/L Entry No." > 0 THEN
            FAInsertLedgEntry.InsertBalAcc(FALedgEntry);
        END;
        IF DepreciationAmount <> 0 THEN BEGIN
          "FA Posting Type" := "FA Posting Type"::Depreciation;
          Amount := DepreciationAmount;
          "No. of Depreciation Days" := NumberOfDays;
          FAInsertLedgEntry.InsertFA(FALedgEntry);
          IF "G/L Entry No." > 0 THEN
            FAInsertLedgEntry.InsertBalAcc(FALedgEntry);
        END;
      END;
    END;

    LOCAL PROCEDURE PostSalvageValue@4(FALedgEntry@1000 : Record 5601);
    BEGIN
      IF (SalvageValue = 0) OR (FAPostingType <> FAPostingType::"Acquisition Cost") THEN
        EXIT;
      WITH FALedgEntry DO BEGIN
        "Entry No." := 0;
        "Automatic Entry" := TRUE;
        Amount := SalvageValue;
        "FA Posting Type" := "FA Posting Type"::"Salvage Value";
        FAInsertLedgEntry.InsertFA(FALedgEntry);
      END;
    END;

    LOCAL PROCEDURE PostBudget@10() : Boolean;
    BEGIN
      EXIT(BudgetNo <> '');
    END;

    LOCAL PROCEDURE SetBudgetAssetNo@8();
    BEGIN
      FA2.GET(BudgetNo);
      IF NOT FA2."Budgeted Asset" THEN BEGIN
        FA."No." := FA2."No.";
        DeprBookCode := '';
        ERROR(Text002,FAName,FA.FIELDCAPTION("Budgeted Asset"));
      END;
      IF FAPostingType = FAPostingType::Maintenance THEN
        MaintenanceLedgEntry."FA No./Budgeted FA No." := BudgetNo
      ELSE
        FALedgEntry."FA No./Budgeted FA No." := BudgetNo;
    END;

    LOCAL PROCEDURE PostBudgetAsset@9();
    VAR
      FA2@1000 : Record 5600;
      FAPostingType2@1001 : Integer;
    BEGIN
      FA2.GET(BudgetNo);
      FA2.TESTFIELD(Blocked,FALSE);
      FA2.TESTFIELD(Inactive,FALSE);
      IF FAPostingType = FAPostingType::Maintenance THEN BEGIN
        WITH MaintenanceLedgEntry DO BEGIN
          "Automatic Entry" := TRUE;
          "G/L Entry No." := 0;
          "FA No./Budgeted FA No." := "FA No.";
          "FA No." := BudgetNo;
          Amount := -Amount2;
          FAInsertLedgEntry.InsertMaintenance(MaintenanceLedgEntry);
        END;
      END ELSE
        WITH FALedgEntry DO BEGIN
          "Automatic Entry" := TRUE;
          "G/L Entry No." := 0;
          "FA No./Budgeted FA No." := "FA No.";
          "FA No." := BudgetNo;
          IF SalvageValue <> 0 THEN BEGIN
            Amount := -SalvageValue;
            FAPostingType2 := "FA Posting Type";
            "FA Posting Type" := "FA Posting Type"::"Salvage Value";
            FAInsertLedgEntry.InsertFA(FALedgEntry);
            "FA Posting Type" := FAPostingType2;
          END;
          Amount := -Amount2;
          FAInsertLedgEntry.InsertFA(FALedgEntry);
        END;
    END;

    LOCAL PROCEDURE PostReverseType@6(FALedgEntry@1000 : Record 5601);
    VAR
      EntryAmounts@1001 : ARRAY [4] OF Decimal;
      i@1002 : Integer;
    BEGIN
      CalculateDisposal.CalcReverseAmounts(FANo,DeprBookCode,EntryAmounts);
      FALedgEntry."FA Posting Category" := FALedgEntry."FA Posting Category"::" ";
      FALedgEntry."Automatic Entry" := TRUE;
      FOR i := 1 TO 4 DO
        IF EntryAmounts[i] <> 0 THEN BEGIN
          FALedgEntry.Amount := EntryAmounts[i];
          FALedgEntry."FA Posting Type" := CalculateDisposal.SetReverseType(i);
          FAInsertLedgEntry.InsertFA(FALedgEntry);
          IF FALedgEntry."G/L Entry No." > 0 THEN
            FAInsertLedgEntry.InsertBalAcc(FALedgEntry);
        END;
    END;

    LOCAL PROCEDURE PostGLBalAcc@20(FALedgEntry@1000 : Record 5601;AllocatedPct@1001 : Decimal);
    BEGIN
      IF AllocatedPct > 0 THEN BEGIN
        FALedgEntry."Entry No." := 0;
        FALedgEntry."Automatic Entry" := TRUE;
        FALedgEntry.Amount := -FALedgEntry.Amount;
        FALedgEntry.Correction := NOT FALedgEntry.Correction;
        FAInsertLedgEntry.InsertBalDisposalAcc(FALedgEntry);
        FALedgEntry.Correction := NOT FALedgEntry.Correction;
        FAInsertLedgEntry.InsertBalAcc(FALedgEntry);
      END;
    END;

    LOCAL PROCEDURE PostAllocation@17(VAR FALedgEntry@1000 : Record 5601);
    VAR
      FAPostingGr@1001 : Record 5606;
    BEGIN
      WITH FALedgEntry DO BEGIN
        IF "G/L Entry No." = 0 THEN
          EXIT;
        CASE "FA Posting Type" OF
          "FA Posting Type"::"Gain/Loss":
            IF DeprBook."Disposal Calculation Method" = DeprBook."Disposal Calculation Method"::Net THEN BEGIN
              FAPostingGr.GET("FA Posting Group");
              FAPostingGr.CALCFIELDS("Allocated Gain %","Allocated Loss %");
              IF "Result on Disposal" = "Result on Disposal"::Gain THEN
                PostGLBalAcc(FALedgEntry,FAPostingGr."Allocated Gain %")
              ELSE
                PostGLBalAcc(FALedgEntry,FAPostingGr."Allocated Loss %");
            END;
          "FA Posting Type"::"Book Value on Disposal":
            BEGIN
              FAPostingGr.GET("FA Posting Group");
              FAPostingGr.CALCFIELDS("Allocated Book Value % (Gain)","Allocated Book Value % (Loss)");
              IF "Result on Disposal" = "Result on Disposal"::Gain THEN
                PostGLBalAcc(FALedgEntry,FAPostingGr."Allocated Book Value % (Gain)")
              ELSE
                PostGLBalAcc(FALedgEntry,FAPostingGr."Allocated Book Value % (Loss)");
            END;
        END;
      END;
    END;

    LOCAL PROCEDURE DeprLine@5() : Boolean;
    BEGIN
      EXIT((Amount2 = 0) AND (FAPostingType = FAPostingType::Depreciation) AND DeprUntilDate);
    END;

    [External]
    PROCEDURE FindFirstGLAcc@14(VAR FAGLPostBuf@1000 : Record 5637) : Boolean;
    BEGIN
      EXIT(FAInsertLedgEntry.FindFirstGLAcc(FAGLPostBuf));
    END;

    [External]
    PROCEDURE GetNextGLAcc@15(VAR FAGLPostBuf@1000 : Record 5637) : Integer;
    BEGIN
      EXIT(FAInsertLedgEntry.GetNextGLAcc(FAGLPostBuf));
    END;

    LOCAL PROCEDURE FAName@2() : Text[200];
    VAR
      DepreciationCalc@1000 : Codeunit 5616;
    BEGIN
      EXIT(DepreciationCalc.FAName(FA,DeprBookCode));
    END;

    LOCAL PROCEDURE SetResultOnDisposal@16(VAR FALedgEntry@1000 : Record 5601);
    VAR
      FADeprBook@1001 : Record 5612;
    BEGIN
      FADeprBook."FA No." := FALedgEntry."FA No.";
      FADeprBook."Depreciation Book Code" := FALedgEntry."Depreciation Book Code";
      FADeprBook.CALCFIELDS("Gain/Loss");
      IF FADeprBook."Gain/Loss" <= 0 THEN
        FALedgEntry."Result on Disposal" := FALedgEntry."Result on Disposal"::Gain
      ELSE
        FALedgEntry."Result on Disposal" := FALedgEntry."Result on Disposal"::Loss;
    END;

    LOCAL PROCEDURE CalcResultOnDisposal@18(FANo@1000 : Code[20];DeprBookCode@1001 : Code[10]) : Integer;
    VAR
      FADeprBook@1002 : Record 5612;
      FALedgEntry@1003 : Record 5601;
    BEGIN
      FADeprBook."FA No." := FANo;
      FADeprBook."Depreciation Book Code" := DeprBookCode;
      FADeprBook.CALCFIELDS("Gain/Loss");
      IF FADeprBook."Gain/Loss" <= 0 THEN
        EXIT(FALedgEntry."Result on Disposal"::Gain);

      EXIT(FALedgEntry."Result on Disposal"::Loss);
    END;

    LOCAL PROCEDURE CheckMaintDocNo@21(MaintenanceLedgEntry@1003 : Record 11072085);
    VAR
      OldMaintenanceLedgEntry@1002 : Record 11072085;
      FAJnlLine2@1000 : Record 5621;
    BEGIN
      OldMaintenanceLedgEntry.SETCURRENTKEY("FA No.","Depreciation Book Code","Document No.");
      OldMaintenanceLedgEntry.SETRANGE("FA No.",MaintenanceLedgEntry."FA No.");
      OldMaintenanceLedgEntry.SETRANGE("Depreciation Book Code",MaintenanceLedgEntry."Depreciation Book Code");
      OldMaintenanceLedgEntry.SETRANGE("Document No.",MaintenanceLedgEntry."Document No.");
      IF OldMaintenanceLedgEntry.FINDFIRST THEN BEGIN
        FAJnlLine2."FA Posting Type" := FAJnlLine2."FA Posting Type"::Maintenance;
        ERROR(
          Text003,
          OldMaintenanceLedgEntry.FIELDCAPTION("Document No."),
          OldMaintenanceLedgEntry."Document No.",
          FAJnlLine2.FIELDCAPTION("FA Posting Type"),
          FAJnlLine2."FA Posting Type",
          FAName);
      END;
    END;

    [External]
    PROCEDURE UpdateRegNo@19(GLRegNo@1000 : Integer);
    VAR
      FAReg@1001 : Record 5617;
    BEGIN
      IF FAReg.FINDLAST THEN BEGIN
        FAReg."G/L Register No." := GLRegNo;
        FAReg.MODIFY;
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterFAJnlPostLine@22(VAR FAJournalLine@1000 : Record 5621);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterGenJnlPostLine@23(VAR GenJournalLine@1000 : Record 81);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeFAJnlPostLine@24(VAR FAJournalLine@1000 : Record 5621);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeGenJnlPostLine@26(VAR GenJournalLine@1000 : Record 81);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostFixedAssetFromGenJnlLine@28(VAR GenJournalLine@1000 : Record 81;VAR FALedgerEntry@1001 : Record 5601;FAAmount@1002 : Decimal;VATAmount@1003 : Decimal);
    BEGIN
    END;

    BEGIN
    END.
  }
}

