OBJECT Report 7132 Export Item Budget to Excel
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Artikelbudget nach Excel exp.;
               ENU=Export Item Budget to Excel;
               NLD=Artikelbudget naar Excel exp.;
               NOR=Eksporter varebudsj. til Excel];
    ProcessingOnly=Yes;
    UseRequestPage=No;
  }
  DATASET
  {
    { 1   ;    ;DataItem;                    ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number)
                                 WHERE(Number=CONST(1));
               OnAfterGetRecord=VAR
                                  Window@1000 : Dialog;
                                  ClientFileName@1005 : Text;
                                  RecNo@1001 : Integer;
                                  TotalRecNo@1002 : Integer;
                                  RowNo@1003 : Integer;
                                  ColumnNo@1004 : Integer;
                                BEGIN
                                  IF DoUpdateExistingWorksheet THEN BEGIN
                                    IF ServerFileName = '' THEN
                                      ServerFileName := FileMgt.UploadFile(Text002,ExcelFileExtensionTok);
                                    IF ServerFileName = '' THEN
                                      EXIT;
                                    ClientFileName := FileMgt.GetFileName(ServerFileName);
                                    SheetName := TempExcelBuffer.SelectSheetsName(ServerFileName);
                                    IF SheetName = '' THEN
                                      EXIT;
                                  END;

                                  Window.OPEN(
                                    Text000 +
                                    '@1@@@@@@@@@@@@@@@@@@@@@\');

                                  TempExcelBuffer.DELETEALL;
                                  CLEAR(TempExcelBuffer);

                                  ItemBudgetName.GET(AnalysisArea,BudgetName);
                                  GLSetup.GET;

                                  IF DateFilter = '' THEN
                                    ERROR(Text010,Text003);

                                  IF FindLine('-') THEN
                                    REPEAT
                                      TotalRecNo := TotalRecNo + 1;
                                    UNTIL NextLine(1) = 0;

                                  RowNo := 1;
                                  EnterCell(RowNo,1,Text001,FALSE,FALSE,TRUE,'',TempExcelBuffer."Cell Type"::Text);
                                  EnterCell(RowNo,2,'',FALSE,FALSE,TRUE,'',TempExcelBuffer."Cell Type"::Text);

                                  RowNo := RowNo + 1;
                                  EnterFilterInCell(RowNo,BudgetName,ItemBudgetName.TABLECAPTION);

                                  IF GlobalDim1Filter <> '' THEN BEGIN
                                    RowNo := RowNo + 1;
                                    Dim.GET(GLSetup."Global Dimension 1 Code");
                                    EnterFilterInCell(RowNo,GlobalDim1Filter,Dim."Filter Caption");
                                  END;

                                  IF GlobalDim2Filter <> '' THEN BEGIN
                                    RowNo := RowNo + 1;
                                    Dim.GET(GLSetup."Global Dimension 2 Code");
                                    EnterFilterInCell(RowNo,GlobalDim2Filter,Dim."Filter Caption");
                                  END;

                                  IF BudgetDim1Filter <> '' THEN BEGIN
                                    RowNo := RowNo + 1;
                                    Dim.GET(ItemBudgetName."Budget Dimension 1 Code");
                                    EnterFilterInCell(RowNo,BudgetDim1Filter,Dim."Filter Caption");
                                  END;

                                  IF BudgetDim2Filter <> '' THEN BEGIN
                                    RowNo := RowNo + 1;
                                    Dim.GET(ItemBudgetName."Budget Dimension 2 Code");
                                    EnterFilterInCell(RowNo,BudgetDim2Filter,Dim."Filter Caption");
                                  END;

                                  IF BudgetDim3Filter <> '' THEN BEGIN
                                    RowNo := RowNo + 1;
                                    Dim.GET(ItemBudgetName."Budget Dimension 3 Code");
                                    EnterFilterInCell(RowNo,BudgetDim3Filter,Dim."Filter Caption");
                                  END;

                                  IF ItemFilter <> '' THEN BEGIN
                                    RowNo := RowNo + 1;
                                    EnterFilterInCell(RowNo,ItemFilter,Text004);
                                  END;

                                  IF DateFilter <> '' THEN BEGIN
                                    RowNo := RowNo + 1;
                                    EnterFilterInCell(RowNo,DateFilter,Text003);
                                  END;

                                  IF SourceNoFilter <> '' THEN BEGIN
                                    RowNo := RowNo + 1;
                                    IF SourceTypeFilter = SourceTypeFilter::Customer THEN
                                      EnterFilterInCell(RowNo,SourceNoFilter,Text005)
                                    ELSE
                                      EnterFilterInCell(RowNo,SourceNoFilter,Text006);
                                  END;

                                  RowNo := RowNo + 2;
                                  EnterFilterInCell(RowNo,LineDimCode,Text008);

                                  RowNo := RowNo + 1;
                                  EnterFilterInCell(RowNo,ColumnDimCode,Text009);

                                  RowNo := RowNo + 1;
                                  CASE ValueType OF
                                    ValueType::"Sales Amount":
                                      ShowValueAsText := Text012;
                                    ValueType::"Cost Amount":
                                      IF AnalysisArea = AnalysisArea::Sales THEN
                                        ShowValueAsText := Text014
                                      ELSE
                                        ShowValueAsText := Text013;
                                    ValueType::Quantity:
                                      ShowValueAsText := Text015;
                                  END;
                                  EnterFilterInCell(RowNo,ShowValueAsText,Text011);

                                  RowNo := RowNo + 2;
                                  IF FindLine('-') THEN BEGIN
                                    IF FindColumn('-') THEN BEGIN
                                      ColumnNo := 1;
                                      EnterCell(RowNo,ColumnNo,Text007,FALSE,TRUE,FALSE,'',TempExcelBuffer."Cell Type"::Text);
                                      REPEAT
                                        ColumnNo := ColumnNo + 1;
                                        EnterCell(RowNo,ColumnNo,ColumnDimCodeBuffer.Code,FALSE,FALSE,FALSE,'',TempExcelBuffer."Cell Type"::Text);
                                      UNTIL NextColumn(1) = 0;
                                    END;
                                    REPEAT
                                      RecNo := RecNo + 1;
                                      Window.UPDATE(1,ROUND(RecNo / TotalRecNo * 10000,1));
                                      RowNo := RowNo + 1;
                                      ColumnNo := 1;
                                      EnterCell(
                                        RowNo,ColumnNo,LineDimCodeBuffer.Code,LineDimCodeBuffer."Show in Bold",FALSE,FALSE,'',TempExcelBuffer."Cell Type"::Text);
                                      IF FindColumn('-') THEN
                                        REPEAT
                                          ColumnNo := ColumnNo + 1;
                                          ColumnValue :=
                                            ItemBudgetManagement.CalcAmount(
                                              ValueType,TRUE,
                                              ItemStatisticsBuffer,ItemBudgetName,
                                              ItemFilter,SourceTypeFilter,SourceNoFilter,DateFilter,
                                              GlobalDim1Filter,GlobalDim2Filter,BudgetDim1Filter,BudgetDim2Filter,BudgetDim3Filter,
                                              LineDimOption,LineDimCodeBuffer,
                                              ColumnDimOption,ColumnDimCodeBuffer);
                                          EnterCell(
                                            RowNo,
                                            ColumnNo,
                                            MatrixMgt.FormatValue(ColumnValue,RoundingFactor,FALSE),
                                            LineDimCodeBuffer."Show in Bold",
                                            FALSE,
                                            FALSE,
                                            '',
                                            TempExcelBuffer."Cell Type"::Number)
                                        UNTIL NextColumn(1) = 0;
                                    UNTIL NextLine(1) = 0;
                                  END;
                                  Window.CLOSE;

                                  IF DoUpdateExistingWorksheet THEN BEGIN
                                    TempExcelBuffer.UpdateBookExcel(ServerFileName,SheetName,FALSE);
                                    TempExcelBuffer.WriteSheet('',COMPANYNAME,USERID);
                                    TempExcelBuffer.CloseBook;
                                    IF NOT TestMode THEN
                                      TempExcelBuffer.OpenExcelWithName(ClientFileName);
                                  END ELSE BEGIN
                                    TempExcelBuffer.CreateBook(ServerFileName,TempExcelBuffer.GetExcelReference(10));
                                    TempExcelBuffer.WriteSheet(
                                      PADSTR(STRSUBSTNO('%1 %2',ItemBudgetName.Name,ItemBudgetName.Description),30),
                                      COMPANYNAME,USERID);
                                    TempExcelBuffer.CloseBook;
                                    IF NOT TestMode THEN
                                      TempExcelBuffer.OpenExcel;
                                  END;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Daten werden analysiert ...\\;ENU=Analyzing Data...\\;NLD=Analyseren gegevens...\\;NOR=Analyserer data...\\;SVE=Analyserar data...\\';
      Text001@1001 : TextConst 'DEU=Filter;ENU=Filters;NLD=Filters;NOR=Filtre;SVE=Filter';
      Text002@1002 : TextConst 'DEU=Arbeitsmappe aktualisieren;ENU=Update Workbook;NLD=Werkmap bijwerken;NOR=Oppdater arbeidsbok';
      ItemBudgetName@1018 : Record 7132;
      Dim@1022 : Record 348;
      LineDimCodeBuffer@1015 : Record 367;
      ColumnDimCodeBuffer@1040 : Record 367;
      ItemStatisticsBuffer@1041 : Record 5821;
      TempExcelBuffer@1006 : TEMPORARY Record 370;
      GLSetup@1013 : Record 98;
      ItemBudgetManagement@1007 : Codeunit 7130;
      MatrixMgt@1048 : Codeunit 9200;
      FileMgt@1049 : Codeunit 419;
      LineDimCode@1043 : Text[30];
      ColumnDimCode@1042 : Text[30];
      DateFilter@1036 : Text;
      InternalDateFilter@1003 : Text;
      ShowValueAsText@1047 : Text[30];
      ServerFileName@1050 : Text;
      SheetName@1010 : Text[250];
      BudgetName@1019 : Code[10];
      GlobalDim1Filter@1027 : Text;
      GlobalDim2Filter@1026 : Text;
      BudgetDim1Filter@1025 : Text;
      BudgetDim2Filter@1024 : Text;
      BudgetDim3Filter@1023 : Text;
      SourceNoFilter@1031 : Text;
      ItemFilter@1030 : Text;
      ColumnValue@1009 : Decimal;
      AnalysisArea@1020 : 'Sales,Purchase,Inventory';
      ValueType@1021 : 'Sales Amount,Cost Amount,Quantity';
      Text003@1028 : TextConst 'DEU=Datumsfilter;ENU=Date Filter;NLD=Datumfilter;NOR=Datofilter;SVE=Datumfilter';
      Text004@1029 : TextConst 'DEU=Artikelfilter;ENU=Item Filter;NLD=Artikelfilter;NOR=Varefilter;SVE=Artikelfilter';
      Text005@1033 : TextConst 'DEU=Debitorenfilter;ENU=Customer Filter;NLD=Klantfilter;NOR=Kundefilter;SVE=Kundfilter';
      Text006@1034 : TextConst 'DEU=Kreditorenfilter;ENU=Vendor Filter;NLD=Leverancierfilter;NOR=Leverand›rfilter;SVE=Leverant”rsfilter';
      SourceTypeFilter@1032 : ' ,Customer,Vendor,Item';
      PeriodType@1037 : 'Day,Week,Month,Quarter,Year,Accounting Period';
      LineDimOption@1039 : 'Item,Customer,Vendor,Period,Location,Global Dimension 1,Global Dimension 2,Budget Dimension 1,Budget Dimension 2,Budget Dimension 3,Budget Dimension 4';
      ColumnDimOption@1038 : 'Item,Customer,Vendor,Period,Location,Global Dimension 1,Global Dimension 2,Budget Dimension 1,Budget Dimension 2,Budget Dimension 3,Budget Dimension 4';
      RoundingFactor@1012 : 'None,1,1000,1000000';
      PeriodInitialized@1035 : Boolean;
      Text007@1004 : TextConst 'DEU=Tabellendaten;ENU=Table Data;NLD=Tabelgegevens;NOR=Tabelldata';
      Text008@1044 : TextConst 'DEU=Zeilenansicht;ENU=Show as Lines;NLD=Als regels weergeven;NOR=Vis som linjer;SVE=Visa som rader';
      Text009@1045 : TextConst 'DEU=Spaltenansicht;ENU=Show as Columns;NLD=Als kolommen weergeven;NOR=Vis som kolonner;SVE=Visa som kolumner';
      Text010@1005 : TextConst 'DEU=%1 darf nicht leer sein.;ENU=%1 must not be blank.;NLD=%1 mag niet leeg zijn.;NOR=%1 skal ikke v‘re tom.;SVE=%1 m†ste fyllas i.';
      Text011@1008 : TextConst 'DEU=Wert anzeigen als;ENU=Show Value as;NLD=Waarde weergeven als;NOR=Vis verdi som;SVE=Visa v„rde som';
      Text012@1014 : TextConst 'DEU=Verkaufsbetrag;ENU=Sales Amount;NLD=Totale omzet;NOR=Salgsbel›p;SVE=F”rs„ljningsbelopp';
      Text013@1016 : TextConst 'DEU=Einstandsbetrag;ENU=Cost Amount;NLD=Totale kosten;NOR=Kostbel›p;SVE=Kostnadsbelopp';
      Text014@1017 : TextConst 'DEU=Lagerverbrauchsbetrag;ENU=COGS Amount;NLD=KPV-bedrag;NOR=Vareforbruksbel›p';
      Text015@1046 : TextConst 'DEU=Menge;ENU=Quantity;NLD=Aantal;NOR=Antall;SVE=Antal';
      DoUpdateExistingWorksheet@1052 : Boolean;
      ExcelFileExtensionTok@1011 : TextConst '@@@={Locked};DEU=.xlsx;ENU=.xlsx;NLD=.xlsx;NOR=.xlsx';
      TestMode@1051 : Boolean;

    [External]
    PROCEDURE SetOptions@3(NewAnalysisArea@1008 : Integer;NewBudgName@1007 : Code[10];NewValueType@1006 : Integer;NewGlobalDim1Filter@1005 : Text;NewGlobalDim2Filter@1004 : Text;NewBudgDim1Filter@1003 : Text;NewBudgDim2Filter@1002 : Text;NewBudgDim3Filter@1001 : Text;NewDateFilter@1000 : Text;NewSourceTypeFilter@1011 : Integer;NewSourceNoFilter@1010 : Text;NewItemFilter@1009 : Text;NewInternalDateFilter@1013 : Text;NewPeriodInitialized@1012 : Boolean;NewPeriodType@1014 : Integer;NewLineDimOption@1016 : Integer;NewColumnDimOption@1015 : Integer;NewLineDimCode@1018 : Text[30];NewColumnDimCode@1017 : Text[30];NewRoundingFactor@1019 : 'None,1,1000,1000000');
    BEGIN
      AnalysisArea := NewAnalysisArea;
      BudgetName := NewBudgName;
      ValueType := NewValueType;
      GlobalDim1Filter := NewGlobalDim1Filter;
      GlobalDim2Filter := NewGlobalDim2Filter;
      BudgetDim1Filter := NewBudgDim1Filter;
      BudgetDim2Filter := NewBudgDim2Filter;
      BudgetDim3Filter := NewBudgDim3Filter;
      DateFilter := NewDateFilter;
      ItemFilter := NewItemFilter;
      SourceTypeFilter := NewSourceTypeFilter;
      SourceNoFilter := NewSourceNoFilter;
      InternalDateFilter := NewInternalDateFilter;
      PeriodInitialized := NewPeriodInitialized;
      PeriodType := NewPeriodType;
      LineDimOption := NewLineDimOption;
      ColumnDimOption := NewColumnDimOption;
      LineDimCode := NewLineDimCode;
      ColumnDimCode := NewColumnDimCode;
      RoundingFactor := NewRoundingFactor;
    END;

    LOCAL PROCEDURE EnterFilterInCell@5(RowNo@1000 : Integer;Filter@1001 : Text;FieldName@1002 : Text[100]);
    BEGIN
      EnterCell(RowNo,1,FieldName,FALSE,FALSE,FALSE,'',TempExcelBuffer."Cell Type"::Text);
      EnterCell(RowNo,2,COPYSTR(Filter,1,250),FALSE,FALSE,FALSE,'',TempExcelBuffer."Cell Type"::Text);
    END;

    LOCAL PROCEDURE EnterCell@2(RowNo@1000 : Integer;ColumnNo@1001 : Integer;CellValue@1002 : Text[250];Bold@1003 : Boolean;Italic@1004 : Boolean;UnderLine@1005 : Boolean;NumberFormat@1006 : Text[30];CellType@1007 : Option);
    BEGIN
      TempExcelBuffer.INIT;
      TempExcelBuffer.VALIDATE("Row No.",RowNo);
      TempExcelBuffer.VALIDATE("Column No.",ColumnNo);
      TempExcelBuffer."Cell Value as Text" := CellValue;
      TempExcelBuffer.Formula := '';
      TempExcelBuffer.Bold := Bold;
      TempExcelBuffer.Italic := Italic;
      TempExcelBuffer.Underline := UnderLine;
      TempExcelBuffer.NumberFormat := NumberFormat;
      TempExcelBuffer."Cell Type" := CellType;
      TempExcelBuffer.INSERT;
    END;

    LOCAL PROCEDURE FindLine@4(Which@1000 : Text[1024]) : Boolean;
    BEGIN
      EXIT(
        ItemBudgetManagement.FindRec(
          ItemBudgetName,LineDimOption,LineDimCodeBuffer,Which,
          ItemFilter,SourceNoFilter,PeriodType,DateFilter,PeriodInitialized,InternalDateFilter,
          GlobalDim1Filter,GlobalDim2Filter,BudgetDim1Filter,BudgetDim2Filter,BudgetDim3Filter));
    END;

    LOCAL PROCEDURE NextLine@8(Steps@1000 : Integer) : Integer;
    BEGIN
      EXIT(
        ItemBudgetManagement.NextRec(
          ItemBudgetName,LineDimOption,LineDimCodeBuffer,Steps,
          ItemFilter,SourceNoFilter,PeriodType,DateFilter,
          GlobalDim1Filter,GlobalDim2Filter,BudgetDim1Filter,BudgetDim2Filter,BudgetDim3Filter));
    END;

    LOCAL PROCEDURE FindColumn@11(Which@1000 : Text[1024]) : Boolean;
    BEGIN
      EXIT(
        ItemBudgetManagement.FindRec(
          ItemBudgetName,ColumnDimOption,ColumnDimCodeBuffer,Which,
          ItemFilter,SourceNoFilter,PeriodType,DateFilter,PeriodInitialized,InternalDateFilter,
          GlobalDim1Filter,GlobalDim2Filter,BudgetDim1Filter,BudgetDim2Filter,BudgetDim3Filter));
    END;

    LOCAL PROCEDURE NextColumn@12(Steps@1000 : Integer) : Integer;
    BEGIN
      EXIT(
        ItemBudgetManagement.NextRec(
          ItemBudgetName,ColumnDimOption,ColumnDimCodeBuffer,Steps,
          ItemFilter,SourceNoFilter,PeriodType,DateFilter,
          GlobalDim1Filter,GlobalDim2Filter,BudgetDim1Filter,BudgetDim2Filter,BudgetDim3Filter));
    END;

    [External]
    PROCEDURE SetUpdateExistingWorksheet@7(UpdateExistingWorksheet@1000 : Boolean);
    BEGIN
      DoUpdateExistingWorksheet := UpdateExistingWorksheet;
    END;

    [External]
    PROCEDURE SetFileNameSilent@6(NewFileName@1000 : Text);
    BEGIN
      ServerFileName := NewFileName;
    END;

    [External]
    PROCEDURE SetTestMode@1(NewTestMode@1000 : Boolean);
    BEGIN
      TestMode := NewTestMode;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

