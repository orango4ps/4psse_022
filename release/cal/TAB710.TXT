OBJECT Table 710 Activity Log
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04;
  }
  PROPERTIES
  {
    ReplicateData=No;
    CaptionML=[ENU=Activity Log;
               NOR=Aktivitetslogg;
               SVE=Aktivitetslogg];
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[ENU=ID;
                                                              NOR=ID;
                                                              SVE=ID] }
    { 2   ;   ;Record ID           ;RecordID      ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Record ID;
                                                              NOR=Post-ID;
                                                              SVE=Post-ID] }
    { 3   ;   ;Activity Date       ;DateTime      ;CaptionML=[ENU=Activity Date;
                                                              NOR=Aktivitetsdato;
                                                              SVE=Aktivitetsdatum] }
    { 4   ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserMgt.LookupUserID("User ID");
                                                            END;

                                                   TestTableRelation=No;
                                                   DataClassification=EndUserIdentifiableInformation;
                                                   CaptionML=[ENU=User ID;
                                                              NOR=Bruker-ID;
                                                              SVE=Anv„ndar-ID] }
    { 5   ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=[ENU=Success,Failed;
                                                                    NOR=Vellykket,Mislykket;
                                                                    SVE=Klart,Misslyckades];
                                                   OptionString=Success,Failed }
    { 6   ;   ;Context             ;Text30        ;CaptionML=[ENU=Context;
                                                              NOR=Kontekst;
                                                              SVE=Kontext] }
    { 10  ;   ;Description         ;Text250       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 20  ;   ;Activity Message    ;Text250       ;CaptionML=[ENU=Activity Message;
                                                              NOR=Aktivitetsmelding;
                                                              SVE=Aktivitetsmeddelande] }
    { 21  ;   ;Detailed Info       ;BLOB          ;CaptionML=[ENU=Detailed Info;
                                                              NOR=Detaljert informasjon;
                                                              SVE=Detaljerad information] }
    { 22  ;   ;Table No Filter     ;Integer       ;CaptionML=[ENU=Table No Filter;
                                                              NOR=Filter for tabellnummer;
                                                              SVE=Tabellnummerfilter] }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
    {    ;Activity Date                            }
    {    ;Record ID                                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DataTypeNotValidErr@1000 : TextConst 'ENU=The specified variant type is not valid.;NOR=Den angitte varianttypen er ikke gyldig.;SVE=Den angivna varianttypen „r ogiltig.';
      NoDetailsMsg@1001 : TextConst 'ENU=The log does not contain any more details.;NOR=Loggen inneholder ikke flere detaljer.;SVE=Loggen inneh†ller ingen mer information.';
      ConfirmDeletingEntriesQst@1004 : TextConst 'ENU=Are you sure that you want to delete log entries?;NOR=Er du sikker p† at du vil slette loggposter?';
      DeletingMsg@1003 : TextConst 'ENU=Deleting Entries...;NOR=Sletter poster...';
      DeletedMsg@1002 : TextConst 'ENU=The entries were deleted from the log.;NOR=Postene ble slettet fra loggen.';

    [External]
    PROCEDURE LogActivity@1(RelatedVariant@1000 : Variant;NewStatus@1001 : Option;NewContext@1004 : Text[30];ActivityDescription@1002 : Text;ActivityMessage@1003 : Text);
    VAR
      UserCode@1007 : Code[50];
    BEGIN
      UserCode := '';
      LogActivityImplementation(RelatedVariant,NewStatus,NewContext,ActivityDescription,ActivityMessage,UserCode);
    END;

    [External]
    PROCEDURE ShowEntries@2(RecordVariant@1000 : Variant);
    VAR
      DataTypeManagement@1001 : Codeunit 701;
      RecRef@1002 : RecordRef;
    BEGIN
      IF NOT DataTypeManagement.GetRecordRef(RecordVariant,RecRef) THEN
        ERROR(DataTypeNotValidErr);

      SETRANGE("Record ID",RecRef.RECORDID);

      COMMIT;
      PAGE.RUNMODAL(PAGE::"Activity Log",Rec);
    END;

    [External]
    PROCEDURE SetDetailedInfoFromStream@3(InputStream@1000 : InStream);
    VAR
      InfoOutStream@1002 : OutStream;
    BEGIN
      "Detailed Info".CREATEOUTSTREAM(InfoOutStream);
      COPYSTREAM(InfoOutStream,InputStream);
      MODIFY;
    END;

    [External]
    PROCEDURE SetDetailedInfoFromText@5(Details@1000 : Text);
    VAR
      OutputStream@1001 : OutStream;
    BEGIN
      "Detailed Info".CREATEOUTSTREAM(OutputStream);
      OutputStream.WRITETEXT(Details);
      MODIFY;
    END;

    [Internal]
    PROCEDURE Export@4(DefaultFileName@1003 : Text;ShowFileDialog@1002 : Boolean) : Text;
    VAR
      TempBlob@1001 : Record 99008535;
      FileMgt@1000 : Codeunit 419;
    BEGIN
      CALCFIELDS("Detailed Info");
      IF NOT "Detailed Info".HASVALUE THEN BEGIN
        MESSAGE(NoDetailsMsg);
        EXIT;
      END;

      IF DefaultFileName = '' THEN
        DefaultFileName := 'Log.txt';

      TempBlob.Blob := "Detailed Info";

      EXIT(FileMgt.BLOBExport(TempBlob,DefaultFileName,ShowFileDialog));
    END;

    LOCAL PROCEDURE LogActivityImplementation@6(RelatedVariant@1000 : Variant;NewStatus@1001 : Option;NewContext@1004 : Text[30];ActivityDescription@1002 : Text;ActivityMessage@1003 : Text;UserCode@1007 : Code[50]);
    VAR
      DataTypeManagement@1005 : Codeunit 701;
      RecRef@1006 : RecordRef;
    BEGIN
      CLEAR(Rec);

      IF NOT DataTypeManagement.GetRecordRef(RelatedVariant,RecRef) THEN
        ERROR(DataTypeNotValidErr);

      "Record ID" := RecRef.RECORDID;
      "Activity Date" := CURRENTDATETIME;
      "User ID" := UserCode;
      IF "User ID" = '' THEN
        "User ID" := USERID;
      Status := NewStatus;
      Context := NewContext;
      Description := COPYSTR(ActivityDescription,1,MAXSTRLEN(Description));
      "Activity Message" := COPYSTR(ActivityMessage,1,MAXSTRLEN("Activity Message"));
      "Table No Filter" := RecRef.NUMBER;

      INSERT(TRUE);

      IF STRLEN(ActivityMessage) > MAXSTRLEN("Activity Message") THEN
        SetDetailedInfoFromText(ActivityMessage);
    END;

    [External]
    PROCEDURE LogActivityForUser@8(RelatedVariant@1000 : Variant;NewStatus@1001 : Option;NewContext@1004 : Text[30];ActivityDescription@1002 : Text;ActivityMessage@1003 : Text;UserCode@1007 : Code[50]);
    BEGIN
      LogActivityImplementation(RelatedVariant,NewStatus,NewContext,ActivityDescription,ActivityMessage,UserCode);
    END;

    [External]
    PROCEDURE DeleteEntries@7(DaysOld@1000 : Integer);
    VAR
      Window@1001 : Dialog;
    BEGIN
      IF NOT CONFIRM(ConfirmDeletingEntriesQst) THEN
        EXIT;
      Window.OPEN(DeletingMsg);
      IF DaysOld > 0 THEN
        SETFILTER("Activity Date",'<=%1',CREATEDATETIME(TODAY - DaysOld,TIME));
      DELETEALL;
      Window.CLOSE;
      SETRANGE("Activity Date");
      MESSAGE(DeletedMsg);
    END;

    BEGIN
    END.
  }
}

