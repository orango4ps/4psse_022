OBJECT Table 11071748 Base Service Inspection Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD(Code);
               CentralDataManagement.EditBaseServiceInspectionLine(Rec, 1, xRec);
             END;

    OnModify=BEGIN
               CentralDataManagement.EditBaseServiceInspectionLine(Rec, 2, xRec);
             END;

    OnDelete=VAR
               BaseServiceInspectionOption@1100528600 : Record 11071749;
             BEGIN
               IF IsInUse THEN
                 ERROR(Text002);
               BaseServiceInspectionOption.SETRANGE("Base Inspection Line Code", Code);
               BaseServiceInspectionOption.DELETEALL(TRUE);
               CentralDataManagement.EditBaseServiceInspectionLine(Rec, 4, xRec);
             END;

    OnRename=BEGIN
               CentralDataManagement.EditBaseServiceInspectionLine(Rec, 3, xRec);
             END;

    CaptionML=[ENU=Base Service Inspection Line;
               NOR=Rad for basisserviceinspeksjon;
               SVE=Rad f”r basserviceinspektion];
    LookupPageID=Page11125255;
    DrillDownPageID=Page11125255;
  }
  FIELDS
  {
    { 10  ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod] }
    { 20  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 30  ;   ;Inspection Group Code;Code10       ;TableRelation="Service Inspection Group";
                                                   CaptionML=[ENU=Inspection Group Code;
                                                              NOR=Inspeksjonsguppekode;
                                                              SVE=Inspektionsgruppskod] }
    { 40  ;   ;Field Type          ;Option        ;OnValidate=VAR
                                                                BaseServiceInspectionOption@1100525000 : Record 11071749;
                                                              BEGIN
                                                                UpdateServiceInspectionLines;
                                                                IF (xRec."Field Type" IN ["Field Type"::Option, "Field Type"::"Yes/No"]) AND (xRec."Field Type" <> "Field Type") THEN BEGIN
                                                                  IF IsInUse THEN
                                                                    ERROR(Text003);
                                                                  BaseServiceInspectionOption.SETRANGE("Base Inspection Line Code", Code);
                                                                  BaseServiceInspectionOption.DELETEALL(TRUE);
                                                                END;

                                                                IF "Field Type" = "Field Type"::"Yes/No" THEN BEGIN
                                                                  BaseServiceInspectionOption.INIT;
                                                                  BaseServiceInspectionOption."Base Inspection Line Code" := Code;
                                                                  BaseServiceInspectionOption.Code := 'YES';
                                                                  BaseServiceInspectionOption.Description := Text000;
                                                                  BaseServiceInspectionOption.INSERT(TRUE);

                                                                  BaseServiceInspectionOption."Base Inspection Line Code" := Code;
                                                                  BaseServiceInspectionOption.Code := 'NO';
                                                                  BaseServiceInspectionOption.Description := Text001;
                                                                  BaseServiceInspectionOption.INSERT(TRUE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Field Type;
                                                              NOR=Felttype;
                                                              SVE=Field Type];
                                                   OptionCaptionML=[ENU=Date,Value,Text,Option,Yes/No;
                                                                    SVE=Datum,V„rde,Text,Alternativ,Ja/Nej];
                                                   OptionString=Date,Value,Text,Option,Yes/No }
    { 50  ;   ;Unit Of Measure     ;Code10        ;TableRelation="Unit of Measure";
                                                   CaptionML=[ENU=Unit Of Measure;
                                                              NOR=Enhet;
                                                              SVE=Enhet] }
    { 60  ;   ;Mandatory           ;Boolean       ;OnValidate=VAR
                                                                BaseServiceInspectionOption@1100528600 : Record 11071749;
                                                              BEGIN
                                                                IF Mandatory AND ("Field Type" IN ["Field Type"::Option, "Field Type"::"Yes/No"]) THEN BEGIN
                                                                  BaseServiceInspectionOption.SETRANGE("Base Inspection Line Code", Code);
                                                                  IF BaseServiceInspectionOption.ISEMPTY THEN
                                                                    ERROR(Text004);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Mandatory;
                                                              NOR=Obligatorisk;
                                                              SVE=Obligatoriskt] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100525001 : TextConst 'ENU=Yes;NOR=Ja;SVE=Ja';
      Text001@1100525000 : TextConst 'ENU=No;NOR=Nr.;SVE=Nej';
      CentralDataManagement@1100525002 : Codeunit 11012010;
      Text002@1100528600 : TextConst 'ENU=Inspection Code in use, removing not allowed';
      Text003@1100528601 : TextConst 'ENU=Inspection Code in use, modifying not allowed';
      Text004@1100528602 : TextConst 'ENU=No Options available';

    PROCEDURE UpdateServiceInspectionLines@1100528600();
    VAR
      ServiceInspectionLine@1100528600 : Record 11071751;
    BEGIN
      IF ((xRec."Field Type" IN ["Field Type"::Option, "Field Type"::"Yes/No"]) OR
          ("Field Type" IN ["Field Type"::Option, "Field Type"::"Yes/No"]) AND
         (xRec."Field Type" <> "Field Type"))
      THEN BEGIN
        ServiceInspectionLine.SETRANGE(Type, ServiceInspectionLine.Type::Line);
        ServiceInspectionLine.SETRANGE("Inspection Code", Code);
        IF ServiceInspectionLine.FINDSET THEN
          REPEAT
            ServiceInspectionLine."Field Type" := "Field Type";
            ServiceInspectionLine.MODIFY(TRUE);
          UNTIL ServiceInspectionLine.NEXT = 0;
      END;
    END;

    PROCEDURE IsInUse@1100528602() : Boolean;
    VAR
      ServiceOrderInspectionLine@1100528600 : Record 11071753;
      ServiceInspectionLine@1100528601 : Record 11071751;
    BEGIN
      ServiceOrderInspectionLine.SETRANGE(Type, ServiceOrderInspectionLine.Type::Line);
      ServiceOrderInspectionLine.SETRANGE("Inspection Code", Code);
      ServiceInspectionLine.SETRANGE(Type, ServiceInspectionLine.Type::Line);
      ServiceInspectionLine.SETRANGE("Inspection Code", Code);
      EXIT(NOT ServiceOrderInspectionLine.ISEMPTY OR NOT ServiceInspectionLine.ISEMPTY);
    END;

    BEGIN
    END.
  }
}

