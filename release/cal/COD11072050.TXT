OBJECT Codeunit 11072050 Make Ins. Coverage Ledg. Entry
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00 (5657);
  }
  PROPERTIES
  {
    Permissions=TableData 11072088=rm;
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [External]
    PROCEDURE CopyFromJnlLine@1(VAR InsCoverageLedgEntry@1000 : Record 11072088;VAR InsuranceJnlLine@1001 : Record 11072092);
    BEGIN
      WITH InsCoverageLedgEntry DO BEGIN
        "User ID" := USERID;
        "Insurance No." := InsuranceJnlLine."Insurance No.";
        "FA No." := InsuranceJnlLine."FA No.";
        "FA Description" := InsuranceJnlLine."FA Description";
        "Posting Date" := InsuranceJnlLine."Posting Date";
        "Document Type" := InsuranceJnlLine."Document Type";
        "Document Date" := InsuranceJnlLine."Document Date";
        IF "Document Date" = 0D THEN
          "Document Date" := "Posting Date";
        "Document No." := InsuranceJnlLine."Document No.";
        "External Document No." := InsuranceJnlLine."External Document No.";
        Amount := InsuranceJnlLine.Amount;
        Description := InsuranceJnlLine.Description;
        "Index Entry" := InsuranceJnlLine."Index Entry";
        "Global Dimension 1 Code" := InsuranceJnlLine."Shortcut Dimension 1 Code";
        "Global Dimension 2 Code" := InsuranceJnlLine."Shortcut Dimension 2 Code";
        "Source Code" := InsuranceJnlLine."Source Code";
        "Journal Batch Name" := InsuranceJnlLine."Journal Batch Name";
        "Reason Code" := InsuranceJnlLine."Reason Code";
        "Disposed FA" := SetDisposedFA("FA No.");
        "No. Series" := InsuranceJnlLine."Posting No. Series";
      END;

      OnAfterCopyFromJnlLine(InsCoverageLedgEntry,InsuranceJnlLine);
    END;

    [External]
    PROCEDURE CopyFromInsuranceCard@2(VAR InsCoverageLedgEntry@1000 : Record 11072088;VAR Insurance@1001 : Record 11072087);
    BEGIN
      WITH InsCoverageLedgEntry DO BEGIN
        "FA Class Code" := Insurance."FA Class Code";
        "FA Subclass Code" := Insurance."FA Subclass Code";
        "FA Location Code" := Insurance."FA Location Code";
        "Location Code" := Insurance."Location Code";
      END;
    END;

    [External]
    PROCEDURE SetDisposedFA@3(FANo@1000 : Code[20]) : Boolean;
    VAR
      FASetup@1001 : Record 5603;
      FADeprBook@1002 : Record 5612;
    BEGIN
      FASetup.GET;
      FASetup.TESTFIELD("Insurance Depr. Book");
      IF FADeprBook.GET(FANo,FASetup."Insurance Depr. Book") THEN
        EXIT(FADeprBook."Disposal Date" > 0D);

      EXIT(FALSE);
    END;

    [External]
    PROCEDURE UpdateInsCoverageLedgerEntryFromFASetup@4(InsDeprBookCode@1000 : Code[10]);
    VAR
      InsCoverageLedgEntry@1001 : Record 11072088;
      FADeprBook@1002 : Record 5612;
    BEGIN
      WITH InsCoverageLedgEntry DO BEGIN
        IF ISEMPTY THEN
          EXIT;

        SETRANGE("Disposed FA",TRUE);
        MODIFYALL("Disposed FA",FALSE);
        IF InsDeprBookCode <> '' THEN BEGIN
          RESET;
          FADeprBook.SETRANGE("Depreciation Book Code",InsDeprBookCode);
          FADeprBook.SETFILTER("Disposal Date",'<>%1',0D);
          IF FADeprBook.FINDSET THEN
            REPEAT
              SETRANGE("FA No.",FADeprBook."FA No.");
              MODIFYALL("Disposed FA",TRUE)
            UNTIL FADeprBook.NEXT = 0;
        END;
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyFromJnlLine@5(VAR InsCoverageLedgerEntry@1000 : Record 11072088;InsuranceJournalLine@1001 : Record 11072092);
    BEGIN
    END;

    BEGIN
    END.
  }
}

