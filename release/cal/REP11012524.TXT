OBJECT Report 11012524 Calc. Int. Plot Installments
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Berechnung Bauzinsraten;
               ENU=Calc. Int. Plot Installments;
               NLD=Berekening bouwrente termijnen;
               NOR=Beregn rente pÜ tegningsavdrag;
               SVE=BerÑkn. int. Ritningsavbetalningar];
    ProcessingOnly=Yes;
    OnPostReport=BEGIN
                   Inform();
                 END;

  }
  DATASET
  {
    { 5136;    ;DataItem;                    ;
               DataItemTable=Table11012018;
               DataItemTableView=WHERE(Interest Installment=CONST(" "),
                                       Interest Inst. No. Transfer=FILTER(=''),
                                       Plot No.=FILTER(<>''));
               OnPreDataItem=BEGIN
                               SETFILTER("Due Date Base", '01-01-01..');
                             END;

               OnAfterGetRecord=VAR
                                  lProjInstalmRec@1210190005 : Record 11012018;
                                  lJobSetupRec@1210190004 : Record 315;
                                  LJobRec@1210190003 : Record 11072003;
                                  lPlotRec@1210190002 : Record 11012500;
                                  lCustomerRec@1210190001 : Record 18;
                                  lPaymentTermsRec@1210190000 : Record 3;
                                  lVATPostingSetupRec@1210190008 : Record 325;
                                  lDimValRec@1210190009 : Record 349;
                                  lvProjPrinRec@1100485000 : Record 11012005;
                                  lvProjInstallmCU@1100485002 : Codeunit 11012007;
                                  lInstallmAmount@1210190007 : Decimal;
                                  lDays@1210190006 : Integer;
                                  lvVatBusPostGroup@1100485001 : Code[10];
                                  lTempDescription@1210190010 : Text[500];
                                  PaymentTermsCode@1210190011 : Code[10];
                                BEGIN
                                  lJobSetupRec.GET;
                                  LJobRec.GET("Project No.");
                                  lPlotRec.GET("Project No.","Plot No.");

                                  IF NOT lPlotRec."Interest Applicable" THEN
                                      CurrReport.SKIP;

                                  lCustomerRec.GET(Principal);
                                  IF NOT lvProjPrinRec.GET("Project No.", Principal) THEN
                                    lvProjPrinRec.INIT;
                                  IF (lvProjPrinRec."VAT Bus. Posting Group" <> '') THEN
                                    lvVatBusPostGroup := lvProjPrinRec."VAT Bus. Posting Group"
                                  ELSE
                                    lvVatBusPostGroup := LJobRec."VAT Bus. Posting Group";

                                  IF (lPlotRec."Contract Date" <> 0D) AND
                                     (lPlotRec."Transfer Date" <> 0D) THEN
                                  BEGIN
                                    //TESTFIELD("Payment Terms Code"); // 25692.o
                                    lJobSetupRec.TESTFIELD("Cost Object Interest Transfer");
                                    LJobRec.TESTFIELD("VAT Prod. P. Group Inst. Int.");

                                    IF ("Due Date Base" <> 0D) THEN BEGIN
                                      IF "Installment No." <> '0' THEN BEGIN
                                        //lPaymentTermsRec.GET("Payment Terms Code"); // 25692.o
                                        // 25692.sn
                                        PaymentTermsCode := "Project Installment".GetPaymentTermsCode();
                                        IF PaymentTermsCode = '' THEN
                                          ERROR(Text004, Principal);
                                        lPaymentTermsRec.GET(PaymentTermsCode);
                                        // 25692.en
                                        "Due Date Actual" := CALCDATE(lPaymentTermsRec."Due Date Calculation","Due Date Base");
                                      END ELSE BEGIN
                                        "Due Date Actual" := "Due Date Base";
                                      END;

                                      lDays := 0;

                                      IF ("Due Date Actual" <= lPlotRec."Contract Date") THEN
                                         lDays := lPlotRec."Transfer Date" - lPlotRec."Contract Date";

                                      IF ("Due Date Actual" > lPlotRec."Contract Date") AND
                                         ("Due Date Actual" < lPlotRec."Transfer Date") THEN
                                        lDays := lPlotRec."Transfer Date" - "Due Date Actual";

                                      IF lDays > 0 THEN BEGIN
                                        IF lCustomerRec."Prices Including VAT" THEN BEGIN
                                          IF "Installment No." = '0' THEN BEGIN
                                            lVATPostingSetupRec.GET(lvVatBusPostGroup,"VAT Prod. Posting Group");
                                            IF lVATPostingSetupRec."VAT %" <> 0 THEN
                                              // BTW uit termijn halen.
                                              lInstallmAmount := 100 * ("Installment Amount (LCY)"/(100+lVATPostingSetupRec."VAT %"))
                                            ELSE
                                              lInstallmAmount := "Installment Amount (LCY)";
                                          END ELSE BEGIN
                                            lInstallmAmount := "Installment Amount (LCY)";
                                          END;
                                        END ELSE BEGIN
                                          IF "Installment No." <> '0' THEN BEGIN
                                            lVATPostingSetupRec.GET(lvVatBusPostGroup,"VAT Prod. Posting Group");
                                            IF lVATPostingSetupRec."VAT %" <> 0 THEN
                                              lInstallmAmount := "Installment Amount (LCY)" + ("Installment Amount (LCY)" * lVATPostingSetupRec."VAT %"  /100)
                                            ELSE
                                              lInstallmAmount := "Installment Amount (LCY)";
                                          END ELSE BEGIN
                                            lInstallmAmount := "Installment Amount (LCY)";
                                          END;
                                        END;

                                        IF lPlotRec."Div. Int. Before Transfer Date" = 0 THEN
                                          lInstallmAmount := lInstallmAmount *
                                                             (LJobRec."Interest Before Transfer Date"/100) *
                                                             (lDays/365)
                                        ELSE
                                          lInstallmAmount := lInstallmAmount *
                                                             (lPlotRec."Div. Int. Before Transfer Date"/100) *
                                                             (lDays/365);

                                        IF lInstallmAmount <> 0 THEN BEGIN

                                          lDimValRec.SETCURRENTKEY(Code,"Global Dimension No.");
                                          lDimValRec.INIT;
                                          IF lJobSetupRec."Cost Object Interest Transfer" <> '' THEN BEGIN
                                            lDimValRec.SETRANGE(Code,lJobSetupRec."Cost Object Interest Transfer");
                                            lDimValRec.SETRANGE("Global Dimension No.",2);
                                            lDimValRec.FIND('-');
                                          END;

                                          lProjInstalmRec.RESET;
                                          lProjInstalmRec.INIT;
                                          lProjInstalmRec."Project No." := "Project No.";
                                          lProjInstalmRec.Principal := Principal;
                                          lProjInstalmRec."Plot No." := "Plot No.";
                                          lProjInstalmRec."Installment No." :=
                                            lvProjInstallmCU.GetFreeInstallmentNo("Project No.", Principal, "Plot No.");  //30-01-2008
                                          lProjInstalmRec."Extension Contract" := "Extension Contract";
                                          lProjInstalmRec.Option := Option;
                                          lProjInstalmRec."Main Group" := "Main Group";
                                          lProjInstalmRec.Group := Group;
                                          lProjInstalmRec."Sub Group" := "Sub Group";
                                          lProjInstalmRec.Element := Element;
                                          lProjInstalmRec."Cost Object" := lJobSetupRec."Cost Object Interest Transfer";
                                          lTempDescription :=  lDimValRec.Name + ', ' + STRSUBSTNO(Text002,"Installment No.") +
                                                                                  ' ' + STRSUBSTNO(Text003,lDays);
                                          lProjInstalmRec.Description := COPYSTR(lTempDescription,1,50);
                                          lProjInstalmRec."Last Installment" := FALSE;
                                          lProjInstalmRec."VAT Prod. Posting Group" := LJobRec."VAT Prod. P. Group Inst. Int.";
                                          IF lCustomerRec."Prices Including VAT" THEN BEGIN
                                            lVATPostingSetupRec.GET(lvVatBusPostGroup,lProjInstalmRec."VAT Prod. Posting Group");
                                            IF lVATPostingSetupRec."VAT %" <> 0 THEN
                                              lInstallmAmount := lInstallmAmount + (lInstallmAmount * lVATPostingSetupRec."VAT %"  /100);
                                          END;
                                          lProjInstalmRec.VALIDATE("Installment Amount (LCY)",ROUND(lInstallmAmount));
                                          lProjInstalmRec."Due Date Base" := lPlotRec."Transfer Date";
                                          lProjInstalmRec."Due Date Actual":=CALCDATE(lPaymentTermsRec."Due Date Calculation",lProjInstalmRec."Due Date Base");
                                          lProjInstalmRec."Interest Installment" := lProjInstalmRec."Interest Installment"::"Before Transfer Date";
                                          lProjInstalmRec.INSERT;

                                          InstallmentsGenerated := TRUE;
                                          Counter := Counter + 1;

                                          "Interest Inst. No. Transfer" := lProjInstalmRec."Installment No.";
                                          MODIFY;
                                        END;
                                      END;
                                    END;
                                  END;
                                END;

               ReqFilterFields=Project No.,Principal,Plot No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Counter@11012000 : Integer;
      InstallmentsGenerated@1210190001 : Boolean;
      Text000@1210190002 : TextConst 'DEU=Es sind keine Zinsraten generiert worden.;ENU=No interest installments generated.;NLD=Er zijn geen rentetermijnen gegenereerd.;NOR=Ingen renteavdrag er generert.;SVE=Inga rÑnteavbetalningar har genererats.';
      Text001@1210190000 : TextConst 'DEU=Es sind %1 Zinsraten generiert worden.;ENU=%1 interest installments created;NLD=Er zijn %1 rentetermijnen gegenereerd.;NOR=%1 renteavdrag er opprettet;SVE=%1 rÑnteavbetalningar har skapats';
      Text002@1210190003 : TextConst 'DEU=Rate %1;ENU=Installment %1;NLD=Termijn %1;NOR=Betalingsplan %1;SVE=Betalplan %1';
      Text003@1210190004 : TextConst 'DEU=%1 Tage Zinsen.;ENU=%1 days interest.;NLD=%1 dagen rente.;NOR=%1 dagers rente.;SVE=%1 dagars rÑnta.';
      Text004@1210190005 : TextConst 'DEU=Keine Zahlungsbedingungen fÅr Debitor %1 vorhanden;ENU=No Payment Terms present for customer %1;NLD=Geen betalingsconditie aanwezig voor klant %1;NOR=Det finnes ingen betalingsvilkÜr for kunde %1;SVE=Det finns inga betalningsvillkor fîr kunden %1';

    PROCEDURE Inform@7();
    BEGIN
      IF Counter = 0 THEN
        MESSAGE(Text000)
      ELSE
        MESSAGE(Text001,Counter);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

