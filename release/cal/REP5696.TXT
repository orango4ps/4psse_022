OBJECT Report 5696 Date Compress FA Ledger
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    Permissions=TableData 87=rimd,
                TableData 355=imd,
                TableData 5601=rimd,
                TableData 5617=rimd;
    CaptionML=[DEU=Datumskomprimierung Anl.-Post.;
               ENU=Date Compress FA Ledger;
               NLD=VA-posten comprimeren;
               NOR=Datokomprimer aktivaposter];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  DimSelectionBuf.CompareDimText(
                    3,REPORT::"Date Compress FA Ledger",'',RetainDimText,Text012);
                  FALedgEntryFilter := COPYSTR("FA Ledger Entry".GETFILTERS,1,MAXSTRLEN(DateComprReg.Filter));

                  WITH FALedgEntry2 DO BEGIN
                    COPY("FA Ledger Entry");
                    SETRANGE("FA No.");
                    SETRANGE("Depreciation Book Code");
                    IF GETFILTERS <> '' THEN
                      ERROR(
                        Text000,
                        FIELDCAPTION("FA No."),FIELDCAPTION("Depreciation Book Code"));
                    RESET;
                    CLEAR(FALedgEntry2);
                  END;
                END;

  }
  DATASET
  {
    { 9888;    ;DataItem;                    ;
               DataItemTable=Table5601;
               DataItemTableView=SORTING(FA No.,Depreciation Book Code,FA Posting Date);
               OnPreDataItem=VAR
                               GLSetup@1000 : Record 98;
                             BEGIN
                               IF NOT CONFIRM(Text001,FALSE) THEN
                                 CurrReport.BREAK;

                               IF EntrdDateComprReg."Ending Date" = 0D THEN
                                 ERROR(Text004,EntrdDateComprReg.FIELDCAPTION("Ending Date"));

                               Window.OPEN(
                                 Text005 +
                                 Text006 +
                                 Text007 +
                                 Text008 +
                                 Text009);

                               SourceCodeSetup.GET;
                               SourceCodeSetup.TESTFIELD("Compress FA Ledger");

                               SelectedDim.GetSelectedDim(
                                 USERID,3,REPORT::"Date Compress FA Ledger",'',TempSelectedDim);
                               FADimMgt.GetSelectedDim(TempSelectedDim);
                               GLSetup.GET;
                               Retain[4] :=
                                 TempSelectedDim.GET(
                                   USERID,3,REPORT::"Date Compress FA Ledger",'',GLSetup."Global Dimension 1 Code");
                               Retain[5] :=
                                 TempSelectedDim.GET(
                                   USERID,3,REPORT::"Date Compress FA Ledger",'',GLSetup."Global Dimension 2 Code");

                               NewFALedgEntry.LOCKTABLE;
                               FAReg.LOCKTABLE;
                               DateComprReg.LOCKTABLE;

                               IF FALedgEntry2.FIND('+') THEN;
                               LastEntryNo := FALedgEntry2."Entry No.";
                               SETRANGE("Entry No.",0,LastEntryNo);
                               SETRANGE("FA Posting Date",EntrdDateComprReg."Starting Date",EntrdDateComprReg."Ending Date");

                               InitRegisters;
                             END;

               OnAfterGetRecord=BEGIN
                                  IF "FA No." <> '' THEN BEGIN
                                    IF "FA Posting Category" <> "FA Posting Category"::" " THEN
                                      CurrReport.SKIP;
                                    CASE "FA Posting Type" OF
                                      "FA Posting Type"::"Proceeds on Disposal",
                                      "FA Posting Type"::"Gain/Loss":
                                        CurrReport.SKIP;
                                    END;
                                  END;
                                  FALedgEntry2 := "FA Ledger Entry";
                                  WITH FALedgEntry2 DO BEGIN
                                    SETCURRENTKEY("FA No.","Depreciation Book Code","FA Posting Date");
                                    COPYFILTERS("FA Ledger Entry");

                                    SETRANGE("FA No.","FA No.");
                                    SETRANGE("Depreciation Book Code","Depreciation Book Code");
                                    SETRANGE("FA Posting Category","FA Posting Category");
                                    SETRANGE("FA Posting Type","FA Posting Type");
                                    SETRANGE("Part of Book Value","Part of Book Value");
                                    SETRANGE("Part of Depreciable Basis","Part of Depreciable Basis");
                                    SETRANGE("FA Posting Group","FA Posting Group");
                                    SETRANGE("Document Type","Document Type");

                                    SETFILTER("FA Posting Date",DateComprMgt.GetDateFilter("FA Posting Date",EntrdDateComprReg,TRUE));

                                    LastEntryNo := LastEntryNo + 1;

                                    NewFALedgEntry.INIT;
                                    NewFALedgEntry."Entry No." := LastEntryNo;
                                    NewFALedgEntry."FA No." := "FA No.";
                                    NewFALedgEntry."Depreciation Book Code" := "Depreciation Book Code";
                                    NewFALedgEntry."FA Posting Category" := "FA Posting Category";
                                    NewFALedgEntry."FA Posting Type" := "FA Posting Type";
                                    NewFALedgEntry."Part of Book Value" := "Part of Book Value";
                                    NewFALedgEntry."Part of Depreciable Basis" := "Part of Depreciable Basis";
                                    NewFALedgEntry."FA Posting Group" := "FA Posting Group";
                                    NewFALedgEntry."Document Type" := "Document Type";
                                    NewFALedgEntry."FA Posting Date" := GETRANGEMIN("FA Posting Date");
                                    NewFALedgEntry."Posting Date" := GETRANGEMIN("FA Posting Date");
                                    NewFALedgEntry.Description := EntrdFALedgEntry.Description;
                                    NewFALedgEntry."Source Code" := SourceCodeSetup."Compress FA Ledger";
                                    NewFALedgEntry."User ID" := USERID;
                                    Window.UPDATE(1,NewFALedgEntry."FA No.");
                                    Window.UPDATE(2,NewFALedgEntry."FA Posting Date");
                                    DateComprReg."No. of New Records" := DateComprReg."No. of New Records" + 1;
                                    Window.UPDATE(3,DateComprReg."No. of New Records");

                                    IF RetainNo(FIELDNO("Document No.")) THEN BEGIN
                                      SETRANGE("Document No.","Document No.");
                                      NewFALedgEntry."Document No." := "Document No.";
                                    END;
                                    IF RetainNo(FIELDNO("Reclassification Entry")) THEN BEGIN
                                      SETRANGE("Reclassification Entry","Reclassification Entry");
                                      NewFALedgEntry."Reclassification Entry" := "Reclassification Entry";
                                    END;
                                    IF RetainNo(FIELDNO("Index Entry")) THEN BEGIN
                                      SETRANGE("Index Entry","Index Entry");
                                      NewFALedgEntry."Index Entry" := "Index Entry";
                                    END;
                                    IF RetainNo(FIELDNO("Global Dimension 1 Code")) THEN BEGIN
                                      SETRANGE("Global Dimension 1 Code","Global Dimension 1 Code");
                                      NewFALedgEntry."Global Dimension 1 Code" := "Global Dimension 1 Code";
                                    END;
                                    IF RetainNo(FIELDNO("Global Dimension 2 Code")) THEN BEGIN
                                      SETRANGE("Global Dimension 2 Code","Global Dimension 2 Code");
                                      NewFALedgEntry."Global Dimension 2 Code" := "Global Dimension 2 Code";
                                    END;
                                    IF Quantity >= 0 THEN
                                      SETFILTER(Quantity,'>=0')
                                    ELSE
                                      SETFILTER(Quantity,'<0');
                                    FADimMgt.GetFALedgEntryDimID(0,"Dimension Set ID");

                                    IF "FA No." <> '' THEN
                                      FindEqualPostingType(FALedgEntry2);
                                    REPEAT
                                      IF FALedgEntryTmp.GET("Entry No.") OR ("FA No." = '') THEN BEGIN
                                        EqualDim := FADimMgt.TestEqualFALedgEntryDimID("Dimension Set ID");
                                        IF EqualDim THEN
                                          SummarizeEntry(NewFALedgEntry,FALedgEntry2);
                                      END ELSE
                                        EqualDim := FALSE;
                                    UNTIL (NEXT = 0) OR NOT EqualDim;
                                    InsertNewEntry(NewFALedgEntry);
                                  END;

                                  IF DateComprReg."No. Records Deleted" >= NoOfDeleted + 10 THEN BEGIN
                                    NoOfDeleted := DateComprReg."No. Records Deleted";
                                    InsertRegisters(FAReg,DateComprReg);
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                IF DateComprReg."No. Records Deleted" > NoOfDeleted THEN
                                  InsertRegisters(FAReg,DateComprReg);
                              END;

               ReqFilterFields=FA No.,Depreciation Book Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF EntrdDateComprReg."Ending Date" = 0D THEN
                     EntrdDateComprReg."Ending Date" := TODAY;
                   IF EntrdFALedgEntry.Description = '' THEN
                     EntrdFALedgEntry.Description := Text010;

                   WITH "FA Ledger Entry" DO BEGIN
                     InsertField(FIELDNO("Document No."),FIELDCAPTION("Document No."));
                     InsertField(FIELDNO("Reclassification Entry"),FIELDCAPTION("Reclassification Entry"));
                     InsertField(FIELDNO("Index Entry"),FIELDCAPTION("Index Entry"));
                     InsertField(FIELDNO("Global Dimension 1 Code"),FIELDCAPTION("Global Dimension 1 Code"));
                     InsertField(FIELDNO("Global Dimension 2 Code"),FIELDCAPTION("Global Dimension 2 Code"));
                   END;

                   RetainDimText := DimSelectionBuf.GetDimSelectionText(3,REPORT::"Date Compress FA Ledger",'');
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1   ;2   ;Field     ;
                  CaptionML=[DEU=Startdatum;
                             ENU=Starting Date;
                             NLD=Begindatum;
                             NOR=Startdato;
                             SVE=Startdatum];
                  ToolTipML=[DEU=Gibt das erste Datum an, das in der Datumskomprimierung enthalten sein soll. Alle Anlagenposten werden ab diesem Datum bis zum Enddatum von der Komprimierung erfasst.;
                             ENU=Specifies the first date to be included in the date compression. The compression affects all fixed asset ledger entries from this date to the Ending Date.;
                             NLD=Hiermee wordt de eerste datum opgegeven die moet worden opgenomen in de datumcompressie. De compressie is van invloed op alle VA-posten vanaf deze datum tot de einddatum.;
                             NOR=Angir den fõrste datoen som skal vëre med i datokomprimeringen. Komprimeringen pÜvirker alle aktivapostene fra denne datoen og frem til sluttdatoen.];
                  ApplicationArea=#Suite;
                  SourceExpr=EntrdDateComprReg."Starting Date" }

      { 2   ;2   ;Field     ;
                  CaptionML=[DEU=Enddatum;
                             ENU=Ending Date;
                             NLD=Einddatum;
                             NOR=Sluttdato;
                             SVE=Slutdatum];
                  ToolTipML=[DEU=Gibt das Datum an, bis zu dem fÅr den Bericht oder die Stapelverarbeitung Informationen verarbeitet werden.;
                             ENU=Specifies the date to which the report or batch job processes information.;
                             NLD=Hiermee wordt de datum opgegeven tot wanneer het rapport of de batchverwerking informatie verwerkt.;
                             NOR=Angir datoen som rapporten eller kjõrselen behandler informasjon til.;
                             SVE=Anger datumet till vilket rapporten eller buntjobbet bearbetar information.];
                  ApplicationArea=#Suite;
                  SourceExpr=EntrdDateComprReg."Ending Date" }

      { 3   ;2   ;Field     ;
                  CaptionML=[DEU=PeriodenlÑnge;
                             ENU=Period Length;
                             NLD=Periodelengte;
                             NOR=Periodelengde;
                             SVE=PeriodlÑngd];
                  ToolTipML=[DEU=Gibt die LÑnge der Periode an, fÅr die kombinierte Posten erzeugt werden sollen. Wenn Sie die PeriodenlÑnge "Quartal", "Monat" oder "Woche" wÑhlen, werden nur die Posten mit einer gemeinsamen Buchhaltungsperiode komprimiert.;
                             ENU=Specifies the length of the period for which combined entries will be created. If you selected the period length Quarter, Month or Week, then only entries with a common accounting period are compressed.;
                             NLD=Hiermee wordt de lengte opgegeven van de periode waarvoor gecombineerde posten worden gemaakt. Als u de periode Kwartaal, Maand of Week hebt geselecteerd, worden alleen posten met een gezamenlijke boekhoudperiode gecomprimeerd.;
                             NOR=Angir periodelengden som komprimerte poster skal opprettes i. Hvis du valgte Kvartal, MÜned eller Uke, komprimeres bare poster som befinner seg i samme regnskapsperiode.];
                  OptionCaptionML=[DEU=Tag,Woche,Monat,Quartal,Jahr,Periode;
                                   ENU=Day,Week,Month,Quarter,Year,Period;
                                   NLD=Dag,Week,Maand,Kwartaal,Jaar,Periode;
                                   NOR=Dag,Uke,MÜned,Kvartal,èr,Periode;
                                   SVE=Dag,Vecka,MÜnad,Kvartal,èr,Period];
                  ApplicationArea=#Suite;
                  SourceExpr=EntrdDateComprReg."Period Length" }

      { 4   ;2   ;Field     ;
                  CaptionML=[DEU=Buchungsbeschreibung;
                             ENU=Posting Description;
                             NLD=Boekingsomschrijving;
                             NOR=Bokfõringsbeskrivelse;
                             SVE=Bokfîringsbeskrivning];
                  ToolTipML=[DEU=Gibt das Buchungsdatum an, das von der Stapelverarbeitung als Filter verwendet werden soll.;
                             ENU=Specifies the posting date to be used by the batch job as a filter.;
                             NLD=Hiermee wordt de boekingsdatum opgegeven die door de batchverwerking als filter moet worden gebruikt.;
                             NOR=Angir bokfõringsdatoen som skal brukes av kjõrselen som et filter.;
                             SVE=Anger bokfîringsdatumet som ska anvÑndas i batch-jobbet som ett filter.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=EntrdFALedgEntry.Description }

      { 1907651001;2;Group  ;
                  CaptionML=[DEU=Feldinhalt behalten;
                             ENU=Retain Field Contents;
                             NLD=Te bewaren velden;
                             NOR=Behold feltinnhold;
                             SVE=BibehÜll fÑlt] }

      { 5   ;3   ;Field     ;
                  CaptionML=[DEU=Belegnr.;
                             ENU=Document No.;
                             NLD=Documentnr.;
                             NOR=Bilagsnr.;
                             SVE=Dokumentnr];
                  ToolTipML=[DEU=Gibt, wenn das Feld leer ist, die nÑchste verfÅgbare Nummer in der resultierenden Buch.-Blattzeile an. Wenn keine Nummernserie eingerichtet wurde, geben Sie die Belegnummer ein, die der resultierenden Buch.-Blattzeile zugewiesen werden soll.;
                             ENU=Specifies, if you leave the field empty, the next available number on the resulting journal line. If a number series is not set up, enter the document number that you want assigned to the resulting journal line.;
                             NLD=Hiermee wordt, als u het veld leeg laat, het volgende beschikbare nummer op de resulterende dagboekregel opgegeven. Als geen nummerreeks is ingesteld, voert u het documentnummer in dat u wilt toewijzen aan de resulterende dagboekregel.;
                             NOR=Angir, hvis du lar feltet vëre tomt, det neste tilgjengelige nummeret for den resulterende kladdelinjen. Hvis en nummer serie ikke er definert, angir du bilagsnummeret som du vil tilordne til den resulterende kladdelinjen.;
                             SVE=Om du inte anger nÜgot i fÑltet anges nÑsta lediga nummer pÜ journalraden. Om ingen nummerserie har konfigurerats anger du det dokumentnummer som du vill tilldela journalraden.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=Retain[1] }

      { 6   ;3   ;Field     ;
                  CaptionML=[DEU=Umbuchungsposten;
                             ENU=Reclassification Entry;
                             NLD=Herindelingspost;
                             NOR=Reklassifiseringspost;
                             SVE=Grupperingstransaktion];
                  ToolTipML=[DEU=Gibt an, welcher Umbuchungsposten datumskomprimiert werden soll.;
                             ENU=Specifies which reclassification entry to date compress.;
                             NLD=Hiermee wordt opgegeven welke herindelingspost moet worden gecomprimeerd.;
                             NOR=Angir hvilken reklassifiseringspost som skal datokomprimeres.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=Retain[2] }

      { 7   ;3   ;Field     ;
                  CaptionML=[DEU=Indexposten;
                             ENU=Index Entry;
                             NLD=Indexpost;
                             NOR=Indeksreg.post;
                             SVE=Indextrans.];
                  ToolTipML=[DEU=Gibt den Indexposten an, der datumskomprimiert werden soll. Mit der Stapelverarbeitung "Anlagen indexieren" kînnen Sie Anlagen indexieren, die mit einem bestimmten AfA-Buch verbunden sind. Die Stapelverarbeitung erstellt Posten in einem Buch.-Blatt, basierend auf den von Ihnen angegebenen Optionen. Sie kînnen das Buch.-Blatt dann buchen oder die Posten, wenn nîtig, vor dem Buchen korrigieren.;
                             ENU=Specifies the index entry to be data compressed. With the Index Fixed Assets batch job, you can index fixed assets that are linked to a specific depreciation book. The batch job creates entries in a journal based on the conditions that you specify. You can then post the journal or adjust the entries before posting, if necessary.;
                             NLD=Hiermee wordt de indexpost opgegeven die moet worden gecomprimeerd. Met de batchverwerking Index vast activum comprimeren kunt u vaste activa indexeren die zijn gekoppeld aan een specifiek afschrijvingsboek. De batchverwerking maakt posten in een dagboek op basis van de door u opgegeven voorwaarden. U kunt het dagboek vervolgens boeken of de posten eventueel voor het boeken aanpassen.;
                             NOR=Angir indeksposten som skal datokomprimeres. Ved hjelp av kjõrselen Indeksreg. aktiva kan du indeksregulere aktiva som er knyttet til et spesielt avskrivningstablÜ. Kjõrselen oppretter poster i en kladd pÜ bakgrunn av betingelsene du angir. Du kan deretter bokfõre kladden eller justere postene fõr bokfõring, om nõdvendig.;
                             SVE=Anger indextransaktionen som ska datumkomprimeras. Med batch-jobbet Indexera anlÑggningstillgÜngar kan du indexera anlÑggningstillgÜngar som Ñr lÑnkade till en viss avskrivningsregel. Batch-jobbet skapar transaktioner i en journal baserat pÜ de villkor du anger. Sedan kan du bokfîra journalen eller justera transaktionerna fîre bokfîring om sÜ behîvs.];
                  ApplicationArea=#FixedAssets;
                  SourceExpr=Retain[3] }

      { 8   ;2   ;Field     ;
                  CaptionML=[DEU=Dimensionen behalten;
                             ENU=Retain Dimensions;
                             NLD=Te bewaren dimensies;
                             NOR=Behold dimensjoner;
                             SVE=BibehÜll dimensioner];
                  ToolTipML=[DEU=Legt fest, welche Dimensionsdaten bei der Komprimierung der Posten erhalten bleiben sollen. Je mehr Dimensionsinformationen Sie beibehalten, desto detaillierter werden die komprimierten Posten sein.;
                             ENU=Specifies which dimension information you want to retain when the entries are compressed. The more dimension information that you choose to retain, the more detailed the compressed entries are.;
                             NLD=Hiermee wordt opgegeven welke dimensiegegevens u bij de compressie van de posten wilt bewaren. Hoe meer dimensiegegevens u bewaart, hoe gedetailleerder het resultaat van de compressie is.;
                             NOR=Angir hvilken dimensjonsinformasjon du vil beholde nÜr postene komprimeres. Jo mer dimensjonsinformasjon du velger Ü beholde, jo mer detaljerte er de komprimerte postene.;
                             SVE=Anger den dimensionsinformation som du vill behÜlla nÑr transaktionerna komprimeras. Ju mer dimensionsinformation du vÑljer att behÜlla, desto mer detaljerade blir de komprimerade transaktionerna.];
                  ApplicationArea=#Dimensions;
                  SourceExpr=RetainDimText;
                  Editable=FALSE;
                  OnAssistEdit=BEGIN
                                 DimSelectionBuf.SetDimSelectionMultiple(3,REPORT::"Date Compress FA Ledger",RetainDimText);
                               END;
                                }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Sie kînnen nur Filter auf %1 und %2 setzen.;ENU=You may set filters only on %1 and %2.;NLD=U mag alleen filters op %1 en %2 plaatsen.;NOR=Du kan bare sette filtre pÜ %1 og %2.';
      Text001@1001 : TextConst 'DEU=Durch diesen Batchauftrag werden Posten gelîscht. Daher muss vor dem AusfÅhren des Batchauftrags eine Datenbanksicherung erstellt werden.\\Mîchten Sie die Posten datumskomprimieren?;ENU=This batch job deletes entries. Therefore, it is important that you make a backup of the database before you run the batch job.\\Do you want to date compress the entries?;NLD=Met deze batchverwerking worden posten verwijderd. Maak daarom eerst een reservekopie van de database voor u de batchverwerking uitvoert.\\Wilt u de posten comprimeren?;NOR=Denne kjõrselen sletter poster. Derfor er det viktig at du tar en sikkerhetskopi av databasen fõr du starter kjõrselen.\\Vil du datokomprimere postene?;SVE=Detta batchjobb tar bort transaktioner. Det Ñr dÑrfîr viktigt att du tar en sÑkerhetskopia pÜ databasen innan du kîr batchjobbet.\\Vill du datumkomprimera transaktionerna?';
      Text004@1004 : TextConst 'DEU=%1 muss angegeben werden.;ENU=%1 must be specified.;NLD=%1 moet opgegeven worden.;NOR=%1 mÜ angis.;SVE=%1 mÜste specificeras.';
      Text005@1005 : TextConst 'DEU=Anl.-Posten datumskomprimieren...\\;ENU=Date compressing FA ledger entries...\\;NLD=Comprimeren VA-posten...\\;NOR=Datokomprimerer aktivaposter...\\';
      Text006@1006 : TextConst 'DEU=Anlagennr.           #1##########\;ENU=FA No.               #1##########\;NLD=VA-nr.               #1##########\;NOR=Aktivanr.            #1##########\;SVE=Anl.nr               #1##########\';
      Text007@1007 : TextConst 'DEU=Datum                #2######\\;ENU=Date                 #2######\\;NLD=Datum                #2######\\;NOR=Dato                 #2######\\;SVE=Datum                #2######\\';
      Text008@1008 : TextConst 'DEU=Anzahl neue Posten   #3######\;ENU=No. of new entries   #3######\;NLD=Nieuwe posten        #3######\;NOR=Antall nye poster    #3######\;SVE=Antal nya trans.     #3######\';
      Text009@1009 : TextConst 'DEU=Anz. gelîsch. Posten #4######;ENU=No. of entries del.  #4######;NLD=Verwijderde posten   #4######;NOR=Ant. slettede poster #4######;SVE=Antal bortt. trans.  #4######';
      Text010@1010 : TextConst 'DEU=Datumskomprimiert;ENU=Date Compressed;NLD=Gecomprimeerde post;NOR=Dato komprimert;SVE=Datumkomprimerad';
      Text011@1011 : TextConst 'DEU=Die Datumskomprimierung wurde abgebrochen. Ein anderer Anwender hat die Tabelle %1 verÑndert.;ENU=The date compression has been interrupted. Another user changed the table %1.;NLD=Datumcompressie is onderbroken. Een andere gebruiker heeft de tabel %1 gewijzigd.;NOR=Datokomprimeringen ble avbrutt. En annen bruker har endret tabellen %1.';
      Text012@1012 : TextConst 'DEU=Dimensionen behalten;ENU=Retain Dimensions;NLD=Te bewaren dimensies;NOR=Behold dimensjoner;SVE=BibehÜll dimensioner';
      SourceCodeSetup@1013 : Record 242;
      DateComprReg@1014 : Record 87;
      EntrdDateComprReg@1015 : Record 87;
      FAReg@1016 : Record 5617;
      EntrdFALedgEntry@1017 : Record 5601;
      NewFALedgEntry@1018 : Record 5601;
      FALedgEntry2@1019 : Record 5601;
      FALedgEntryTmp@1020 : TEMPORARY Record 5601;
      SelectedDim@1021 : Record 369;
      TempSelectedDim@1022 : TEMPORARY Record 369;
      DimSelectionBuf@1023 : Record 368;
      DateComprMgt@1024 : Codeunit 356;
      DimMgt@1025 : Codeunit 408;
      FACheckConsistency@1026 : Codeunit 5606;
      FADimMgt@1027 : Codeunit 5674;
      Window@1028 : Dialog;
      FALedgEntryFilter@1029 : Text[250];
      NoOfFields@1030 : Integer;
      Retain@1031 : ARRAY [10] OF Boolean;
      FieldNumber@1032 : ARRAY [10] OF Integer;
      FieldNameArray@1033 : ARRAY [10] OF Text[100];
      LastEntryNo@1034 : Integer;
      NoOfDeleted@1035 : Integer;
      FARegExists@1036 : Boolean;
      i@1037 : Integer;
      RetainDimText@1038 : Text[250];
      EqualDim@1039 : Boolean;

    LOCAL PROCEDURE InitRegisters@4();
    BEGIN
      IF FAReg.FIND('+') THEN;
      FAReg.INIT;
      FAReg."No." := FAReg."No." + 1;
      FAReg."Creation Date" := TODAY;
      FAReg."Creation Time" := TIME;
      FAReg."Journal Type" := FAReg."Journal Type"::"Fixed Asset";
      FAReg."Source Code" := SourceCodeSetup."Compress FA Ledger";
      FAReg."User ID" := USERID;
      FAReg."From Entry No." := LastEntryNo + 1;

      IF DateComprReg.FINDLAST THEN;
      DateComprReg.InitRegister(
        DATABASE::"FA Ledger Entry",DateComprReg."No." + 1,EntrdDateComprReg."Starting Date",EntrdDateComprReg."Ending Date",
        EntrdDateComprReg."Period Length",FALedgEntryFilter,FAReg."No.",SourceCodeSetup."Compress FA Ledger");

      FOR i := 1 TO NoOfFields DO
        IF Retain[i] THEN
          DateComprReg."Retain Field Contents" :=
            COPYSTR(
              DateComprReg."Retain Field Contents" + ',' + FieldNameArray[i],1,
              MAXSTRLEN(DateComprReg."Retain Field Contents"));
      DateComprReg."Retain Field Contents" := COPYSTR(DateComprReg."Retain Field Contents",2);

      FARegExists := FALSE;
      NoOfDeleted := 0;
    END;

    LOCAL PROCEDURE InsertRegisters@1(VAR FAReg@1000 : Record 5617;VAR DateComprReg@1001 : Record 87);
    VAR
      FAReg2@1002 : Record 5617;
    BEGIN
      FAReg."To Entry No." := NewFALedgEntry."Entry No.";

      IF FARegExists THEN BEGIN
        FAReg.MODIFY;
        DateComprReg.MODIFY;
      END ELSE BEGIN
        FAReg.INSERT;
        DateComprReg.INSERT;
        FARegExists := TRUE;
      END;
      FALedgEntry2.RESET;
      IF FALedgEntry2.FIND('+') THEN;
      LastEntryNo := FALedgEntry2."Entry No.";
      COMMIT;

      NewFALedgEntry.LOCKTABLE;
      FAReg.LOCKTABLE;
      DateComprReg.LOCKTABLE;

      IF FALedgEntry2.FIND('+') THEN;
      IF LastEntryNo <> FALedgEntry2."Entry No." THEN
        ERROR(
          Text011,FALedgEntry2.TABLECAPTION);

      IF FAReg2.FINDLAST THEN ;
      IF FAReg."No." <> FAReg2."No." THEN
        ERROR(
          Text011,FAReg.TABLECAPTION);
    END;

    LOCAL PROCEDURE InsertField@2(Number@1000 : Integer;Name@1001 : Text[100]);
    BEGIN
      NoOfFields := NoOfFields + 1;
      FieldNumber[NoOfFields] := Number;
      FieldNameArray[NoOfFields] := Name;
    END;

    LOCAL PROCEDURE RetainNo@3(Number@1000 : Integer) : Boolean;
    BEGIN
      EXIT(Retain[Index(Number)]);
    END;

    LOCAL PROCEDURE Index@6(Number@1000 : Integer) : Integer;
    BEGIN
      FOR i := 1 TO NoOfFields DO
        IF Number = FieldNumber[i] THEN
          EXIT(i);
    END;

    LOCAL PROCEDURE FindEqualPostingType@5(VAR FALedgEntry2@1000 : Record 5601);
    VAR
      FALedgEntry@1001 : Record 5601;
      EqualPostingType@1002 : Boolean;
    BEGIN
      FALedgEntryTmp.DELETEALL;
      WITH FALedgEntry DO BEGIN
        SETCURRENTKEY("FA No.","Depreciation Book Code","FA Posting Date");
        SETRANGE("FA No.",FALedgEntry2."FA No.");
        SETRANGE("Depreciation Book Code",FALedgEntry2."Depreciation Book Code");
        SETRANGE("Entry No.",0,"FA Ledger Entry".GETRANGEMAX("Entry No."));
        GET(FALedgEntry2."Entry No.");
        REPEAT
          EqualPostingType :=
            (FALedgEntry2."FA Posting Category" = "FA Posting Category") AND
            (FALedgEntry2."FA Posting Type" = "FA Posting Type") AND
            (FALedgEntry2."Part of Book Value" = "Part of Book Value") AND
            (FALedgEntry2."Part of Depreciable Basis" = "Part of Depreciable Basis");
          IF EqualPostingType THEN BEGIN
            FALedgEntryTmp."Entry No." := "Entry No.";
            FALedgEntryTmp.INSERT;
          END;
        UNTIL (NEXT = 0) OR NOT EqualPostingType;
      END;
    END;

    LOCAL PROCEDURE SummarizeEntry@7(VAR NewFALedgEntry@1000 : Record 5601;FALedgEntry@1001 : Record 5601);
    BEGIN
      WITH FALedgEntry DO BEGIN
        NewFALedgEntry.Quantity := NewFALedgEntry.Quantity + Quantity;
        NewFALedgEntry.Amount := NewFALedgEntry.Amount + Amount;
        DELETE;
        DateComprReg."No. Records Deleted" := DateComprReg."No. Records Deleted" + 1;
        Window.UPDATE(4,DateComprReg."No. Records Deleted");
      END;
    END;

    LOCAL PROCEDURE InsertNewEntry@23(VAR NewFALedgEntry@1000 : Record 5601);
    VAR
      TempDimBuf@1001 : TEMPORARY Record 360;
      TempDimSetEntry@1004 : TEMPORARY Record 480;
    BEGIN
      TempDimBuf.DELETEALL;
      FADimMgt.GetDimensions(TempDimBuf);
      DimMgt.CopyDimBufToDimSetEntry(TempDimBuf,TempDimSetEntry);
      NewFALedgEntry."Dimension Set ID" := DimMgt.GetDimensionSetID(TempDimSetEntry);
      NewFALedgEntry.INSERT;
      IF NewFALedgEntry."FA No." <> '' THEN
        FACheckConsistency.SetFAPostingDate(NewFALedgEntry,FALSE);
    END;

    [External]
    PROCEDURE InitializeRequest@8(StartingDateFrom@1000 : Date;EndingDateFrom@1001 : Date;PeriodLengthFrom@1002 : Option;DescriptionFrom@1003 : Text[50];RetainDimTextFrom@1004 : Text[250]);
    BEGIN
      EntrdDateComprReg."Starting Date" := StartingDateFrom;
      EntrdDateComprReg."Ending Date" := EndingDateFrom;
      EntrdDateComprReg."Period Length" := PeriodLengthFrom;
      EntrdFALedgEntry.Description := DescriptionFrom;
      RetainDimText := RetainDimTextFrom;

      InsertField("FA Ledger Entry".FIELDNO("Reclassification Entry"),"FA Ledger Entry".FIELDCAPTION("Reclassification Entry"));
      InsertField("FA Ledger Entry".FIELDNO("Global Dimension 1 Code"),"FA Ledger Entry".FIELDCAPTION("Global Dimension 1 Code"));
      InsertField("FA Ledger Entry".FIELDNO("Global Dimension 2 Code"),"FA Ledger Entry".FIELDCAPTION("Global Dimension 2 Code"));
    END;

    [External]
    PROCEDURE SetRetainDocumentNo@9(RetainValue@1000 : Boolean);
    BEGIN
      Retain[1] := RetainValue;
      InsertField("FA Ledger Entry".FIELDNO("Document No."),"FA Ledger Entry".FIELDCAPTION("Document No."));
    END;

    [External]
    PROCEDURE SetRetainIndexEntry@10(RetainValue@1000 : Boolean);
    BEGIN
      Retain[3] := RetainValue;
      InsertField("FA Ledger Entry".FIELDNO("Index Entry"),"FA Ledger Entry".FIELDCAPTION("Index Entry"));
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

