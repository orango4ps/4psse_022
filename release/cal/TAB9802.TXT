OBJECT Table 9802 Permission Set Link
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    Permissions=TableData 9802=rmd;
    ReplicateData=No;
    CaptionML=[ENU=Permission Set Link;
               NOR=Kobling for tillatelsessett];
  }
  FIELDS
  {
    { 1   ;   ;Permission Set ID   ;Code20        ;TableRelation="Permission Set"."Role ID";
                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Permission Set ID;
                                                              NOR=Tillatelsessett-ID] }
    { 2   ;   ;Linked Permission Set ID;Code20    ;TableRelation="Tenant Permission Set"."Role ID";
                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Linked Permission Set ID;
                                                              NOR=Tilknyttet tillatelsessett-ID] }
    { 3   ;   ;Source Hash         ;Text250       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Source Hash;
                                                              NOR=Kilde-hash] }
  }
  KEYS
  {
    {    ;Permission Set ID,Linked Permission Set ID;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    [External]
    PROCEDURE SourceHashHasChanged@2() : Boolean;
    BEGIN
      MarkWithChangedSource;
      EXIT(NOT ISEMPTY);
    END;

    [External]
    PROCEDURE UpdateSourceHashesOnAllLinks@5();
    VAR
      PermissionSet@1001 : Record 2000000004;
    BEGIN
      IF FINDSET THEN
        REPEAT
          IF PermissionSet.GET("Permission Set ID") THEN BEGIN
            "Source Hash" := PermissionSet.Hash;
            MODIFY;
          END ELSE
            DELETE;
        UNTIL NEXT = 0;
    END;

    [External]
    PROCEDURE MarkWithChangedSource@1();
    VAR
      PermissionSet@1001 : Record 2000000004;
      SourceChanged@1002 : Boolean;
    BEGIN
      IF FINDSET THEN
        REPEAT
          SourceChanged := FALSE;
          IF PermissionSet.GET("Permission Set ID") THEN
            SourceChanged := "Source Hash" <> PermissionSet.Hash
          ELSE
            SourceChanged := TRUE;
          IF SourceChanged THEN
            MARK(TRUE);
        UNTIL NEXT = 0;
      MARKEDONLY(TRUE);
    END;

    [External]
    PROCEDURE GetSourceForLinkedPermissionSet@3(LinkedPermissionSetId@1000 : Code[20]) : Code[20];
    BEGIN
      SETRANGE("Linked Permission Set ID",LinkedPermissionSetId);
      IF FINDFIRST THEN
        EXIT("Permission Set ID");
    END;

    BEGIN
    END.
  }
}

