OBJECT Codeunit 11072036 FA Reclass. Transfer Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04,4PS14.00 (5642);
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=ist eine %1, und %2 ist keine %1.;ENU=is a %1 and %2 is not a %1.;NLD=is een %1 en %2 is geen %1.;NOR=er en %1 og %2 er ikke en %1.;SVE=„r en %1 och %2 „r inte en %1.';
      Text001@1001 : TextConst 'DEU=unterscheidet sich nicht von %1.;ENU=is not different than %1.;NLD=verschilt niet van %1.;NOR=er ikke forskjellig fra %1.;SVE=„r inte skilt fr†n %1.';
      Text002@1002 : TextConst 'DEU=%1 ist verkauft.;ENU=%1 is disposed.;NLD=%1 is buiten gebruik gesteld.;NOR=%1 er solgt.;SVE=%1 „r avyttrad.';
      Text003@1003 : TextConst 'DEU="%2 = 0 fr %1.";ENU="%2 = 0 for %1.";NLD="%2 = 0 voor %1.";NOR="%2 = 0 for %1.";SVE="%2 = 0 f”r %1."';
      Text004@1004 : TextConst 'DEU=%2 ist gr”áer als %3 fr %1.;ENU=%2 is greater than %3 for %1.;NLD=%2 is groter dan %3 voor %1.;NOR=%2 er st›rre enn %3 for %1.;SVE=%2 „r st”rre „n %3 f”r %1.';
      FAJnlSetup@1005 : Record 5605;
      OldFA@1006 : Record 5600;
      NewFA@1007 : Record 5600;
      FADeprBook@1008 : Record 5612;
      FADeprBook2@1009 : Record 5612;
      GenJnlLine@1010 : Record 81;
      FAJnlLine@1011 : Record 5621;
      DepreciationCalc@1012 : Codeunit 5616;
      FAGetJnl@1013 : Codeunit 5639;
      FAPostingType@1014 : 'Acquisition,Depr,WriteDown,Appr,Custom1,Custom2,Disposal,Maintenance,Salvage Value';
      TransferToGenJnl@1015 : Boolean;
      TemplateName@1016 : Code[10];
      BatchName@1017 : Code[10];
      FANo@1018 : Code[20];
      TransferType@1019 : ARRAY [9] OF Boolean;
      Amounts@1020 : ARRAY [9] OF Decimal;
      FAJnlNextLineNo@1021 : Integer;
      GenJnlNextLineNo@1022 : Integer;
      i@1023 : Integer;
      j@1024 : Integer;
      OldNewFA@1025 : Integer;
      Sign@1026 : Integer;
      GenJnlUsedOnce@1027 : Boolean;
      FAJnlUsedOnce@1028 : Boolean;
      FAJnlDocumentNo@1029 : Code[20];
      GenJnlDocumentNo@1030 : Code[20];
      Text005@1032 : TextConst 'DEU=Es war nicht m”glich, ein %1 in der %2 zu finden.;ENU=It was not possible to find a %1 in %2.;NLD=Er is geen %1 in %2.;NOR=Det var ikke mulig † finne en %1 i %2.;SVE=Det var inte m”jligt att hitta %1 i %2.';
      Text006@1031 : TextConst 'DEU=%1 muss %2 oder %3 fr %4 sein.;ENU=%1 must be %2 or %3 for %4.;NLD=%1 moet %2 of %3 zijn voor %4.;NOR=%1 m† v‘re %2 eller %3 for %4.;SVE=%1 m†ste vara %2 eller %3 f”r %4.';
      Text007@1033 : TextConst 'DEU=%1 muss %2 fr %3 sein.;ENU=%1 must be %2 for %3.;NLD=%1 moet %2 zijn voor %3.;NOR=%1 m† v‘re %2 for %3.;SVE=%1 m†ste vara %2 f”r %3.';
      Text008@1034 : TextConst 'DEU=darf nicht zusammen mit %1 in %2 %3 verwendet werden.;ENU=must not be used together with %1 in %2 %3.;NLD=mag niet samen worden gebruikt met %1 in %2 %3.;NOR=kan ikke brukes sammen med %1 i %2 %3.;SVE=f†r inte anv„ndas med %1 i %2 %3.';
      Text009@1035 : TextConst 'DEU=%1 kann fr %2 nicht berechnet werden.;ENU=%1 cannot be calculated for %2.;NLD=%1 kan niet worden berekend voor %2.;NOR=%1 kan ikke beregnes for %2.;SVE=%1 kan inte ber„knas f”r %2.';
      TmpFAJnlLine@1100409000 : TEMPORARY Record 5621;
      DoFillTmpFAJnlLine@1100409001 : Boolean;

    [External]
    PROCEDURE FAReclassLine@3(VAR FAReclassJnlLine@1000 : Record 11072084;VAR Done@1001 : Boolean);
    BEGIN
      WITH FAReclassJnlLine DO BEGIN
        IF ("FA No." = '') AND ("New FA No." = '') THEN
          EXIT;
        OldFA.GET("FA No.");
        NewFA.GET("New FA No.");
        FADeprBook.GET("FA No.","Depreciation Book Code");
        FADeprBook2.GET("New FA No.","Depreciation Book Code");
        OldFA.TESTFIELD(Blocked,FALSE);
        NewFA.TESTFIELD(Blocked,FALSE);
        OldFA.TESTFIELD(Inactive,FALSE);
        NewFA.TESTFIELD(Inactive,FALSE);

        IF OldFA."Budgeted Asset" AND NOT NewFA."Budgeted Asset" THEN
          FIELDERROR(
            "FA No.",STRSUBSTNO(Text000,
              OldFA.FIELDCAPTION("Budgeted Asset"),FIELDCAPTION("New FA No.")));

        IF NewFA."Budgeted Asset" AND NOT OldFA."Budgeted Asset" THEN
          FIELDERROR(
            "New FA No.",STRSUBSTNO(Text000,
              NewFA.FIELDCAPTION("Budgeted Asset"),FIELDCAPTION("FA No.")));

        IF "FA No." = "New FA No." THEN
          FIELDERROR(
            "FA No.",STRSUBSTNO(Text001,FIELDCAPTION("New FA No.")));

        IF FADeprBook."Disposal Date" > 0D THEN
          ERROR(Text002,FAName(OldFA,"Depreciation Book Code"));

        IF FADeprBook2."Disposal Date" > 0D THEN
          ERROR(Text002,FAName(NewFA,"Depreciation Book Code"));

        SetFAReclassType(FAReclassJnlLine);
        CalcAmounts(FAReclassJnlLine);
        CalcDB1DeprAmount(FAReclassJnlLine);

        FOR OldNewFA := 0 TO 1 DO BEGIN
          j := 0;
          WHILE j < 9 DO BEGIN
            j := j + 1;
            IF j = 7 THEN
              j := 9;
            Convert(OldNewFA,j,FAPostingType,Sign,FANo);
            i := FAPostingType + 1;
            TemplateName := '';
            BatchName := '';
            //IF TransferType[i] AND (Amounts[i] <> 0) THEN BEGIN  //**4PS.o
            //**4PS.sn
            IF TransferType[i] AND
               ((Amounts[i] <> 0) OR ((i = 1) AND (Quantity <> 0))) AND
               (NOT SkipDisposedFA_OnExitOrderPlnt(FANo, FAPostingType, Description))
            THEN BEGIN
            //**4PS.en
              FAGetJnl.JnlName(
                "Depreciation Book Code",OldFA."Budgeted Asset",FAPostingType,
                TransferToGenJnl,TemplateName,BatchName);
              SetJnlRange;
              IF TransferToGenJnl THEN
                //InsertGenJnlLine(FAReclassJnlLine,FANo,Sign * Amounts[i],"Insert Bal. Account") //**4PS.o
                InsertGenJnlLine(FAReclassJnlLine,FANo,Sign * Amounts[i],"Insert Bal. Account", -Sign) //**4PS.n
              ELSE
                //InsertFAJnlLine(FAReclassJnlLine,FANo,Sign * Amounts[i]);  //**4PS.o
                InsertFAJnlLine(FAReclassJnlLine,FANo,Sign * Amounts[i], -Sign);  //**4PS.n
              Done := TRUE;
            END;
          END;
        END;
      END;
    END;

    LOCAL PROCEDURE CalcAmounts@1(VAR FAReclassJnlLine@1000 : Record 11072084);
    VAR
      lvDescription@1100485001 : Text[100];
      lvReason@1100485000 : Code[10];
    BEGIN
      WITH FADeprBook DO BEGIN
        CALCFIELDS("Acquisition Cost");
        IF TransferType[2] THEN
          CALCFIELDS(Depreciation);
        IF TransferType[3] THEN
          CALCFIELDS("Write-Down");
        IF TransferType[4] THEN
          CALCFIELDS(Appreciation);
        IF TransferType[5] THEN
          CALCFIELDS("Custom 1");
        IF TransferType[6] THEN
          CALCFIELDS("Custom 2");
        IF TransferType[9] THEN
          CALCFIELDS("Salvage Value");
        Amounts[1] := "Acquisition Cost";
        Amounts[2] := Depreciation;
        Amounts[3] := "Write-Down";
        Amounts[4] := Appreciation;
        Amounts[5] := "Custom 1";
        Amounts[6] := "Custom 2";
        Amounts[9] := "Salvage Value";
        {//**4PS.so
        IF Amounts[1] = 0 THEN
          ERROR(Text003,
            FAName(OldFA,"Depreciation Book Code"),FIELDCAPTION("Acquisition Cost"));
        }//**4PS.eo
        //**4PS.sn
        IF Amounts[1] = 0 THEN BEGIN
          //* If acquisition amount zero, but acquisition entry exist and from posting exit-order (plant)
          //* and call from posting, then zero amount allowed. (call 8704).
          CALCFIELDS("FA Entry-Acq. Cost Present");
          IF NOT "FA Entry-Acq. Cost Present" THEN
            ERROR(Text003, FAName(OldFA,"Depreciation Book Code"),FIELDCAPTION("Acquisition Cost"))
          ELSE BEGIN
            lvDescription := FAReclassJnlLine.Description;
            lvReason := '';
            CheckAdjustDescriptionReason(lvDescription, lvReason, OldFA."No.");
            IF lvReason <> 'EXIT-CORR' THEN
              ERROR(Text003, FAName(OldFA,"Depreciation Book Code"),FIELDCAPTION("Acquisition Cost"));
          END;
        END;
        //**4PS.en
      END;

      WITH FAReclassJnlLine DO BEGIN
        IF "Reclassify Acq. Cost Amount" <> 0 THEN BEGIN
          IF "Reclassify Acq. Cost Amount" > Amounts[1] THEN
            ERROR(Text004,
              FAName(OldFA,"Depreciation Book Code"),
              FIELDCAPTION("Reclassify Acq. Cost Amount"),
              FADeprBook.FIELDCAPTION("Acquisition Cost"));
          "Reclassify Acq. Cost %" := "Reclassify Acq. Cost Amount" / Amounts[1] * 100;
        END;
        IF "Reclassify Acq. Cost Amount" <> 0 THEN
          Amounts[1] := "Reclassify Acq. Cost Amount"
        ELSE
          Amounts[1] := ROUND(Amounts[1] * "Reclassify Acq. Cost %" / 100);
        FOR i := 2 TO 9 DO
          Amounts[i] := ROUND(Amounts[i] * "Reclassify Acq. Cost %" / 100);
      END;
    END;

    LOCAL PROCEDURE SetFAReclassType@8(VAR FAReclassJnlLine@1000 : Record 11072084);
    BEGIN
      WITH FAReclassJnlLine DO BEGIN
        TransferType[1] := "Reclassify Acquisition Cost";
        TransferType[2] := "Reclassify Depreciation";
        TransferType[3] := "Reclassify Write-Down";
        TransferType[4] := "Reclassify Appreciation";
        TransferType[5] := "Reclassify Custom 1";
        TransferType[6] := "Reclassify Custom 2";
        TransferType[9] := "Reclassify Salvage Value";
      END;
    END;

    LOCAL PROCEDURE SetJnlRange@2();
    BEGIN
      IF (FAJnlNextLineNo = 0) AND NOT TransferToGenJnl THEN BEGIN
        FAJnlLine.LOCKTABLE;
        FAGetJnl.SetFAJnlRange(FAJnlLine,TemplateName,BatchName);
        FAJnlNextLineNo := FAJnlLine."Line No.";
      END;
      IF (GenJnlNextLineNo = 0) AND TransferToGenJnl THEN BEGIN
        GenJnlLine.LOCKTABLE;
        FAGetJnl.SetGenJnlRange(GenJnlLine,TemplateName,BatchName);
        GenJnlNextLineNo := GenJnlLine."Line No.";
      END;
    END;

    LOCAL PROCEDURE Convert@5(OldNewFA@1000 : 'OldFA,NewFA';J@1001 : Integer;VAR FAPostingType@1002 : 'Acquisition,Depr,WriteDown,Appr,Custom1,Custom2,Disposal,Maintenance,Salvage Value';VAR Sign@1003 : Integer;VAR FANo@1004 : Code[20]);
    BEGIN
      IF OldNewFA = OldNewFA::OldFA THEN BEGIN
        Sign := -1;
        FANo := OldFA."No.";
      END ELSE BEGIN
        Sign := 1;
        FANo := NewFA."No.";
      END;
      IF OldNewFA = OldNewFA::OldFA THEN
        CASE J OF
          1:
            FAPostingType := FAPostingType::"Salvage Value";
          2:
            FAPostingType := FAPostingType::Depr;
          3:
            FAPostingType := FAPostingType::WriteDown;
          4:
            FAPostingType := FAPostingType::Custom1;
          5:
            FAPostingType := FAPostingType::Custom2;
          6:
            FAPostingType := FAPostingType::Appr;
          9:
            FAPostingType := FAPostingType::Acquisition;
        END;
      IF OldNewFA = OldNewFA::NewFA THEN
        CASE J OF
          1:
            FAPostingType := FAPostingType::Acquisition;
          2:
            FAPostingType := FAPostingType::"Salvage Value";
          3:
            FAPostingType := FAPostingType::Appr;
          4:
            FAPostingType := FAPostingType::WriteDown;
          5:
            FAPostingType := FAPostingType::Custom1;
          6:
            FAPostingType := FAPostingType::Custom2;
          9:
            FAPostingType := FAPostingType::Depr;
        END;
    END;

    LOCAL PROCEDURE InsertFAJnlLine@13(VAR FAReclassJnlLine@1000 : Record 11072084;FANo@1001 : Code[20];EntryAmount@1002 : Decimal;iIntSign@1210190000 : Integer);
    BEGIN
      IF NOT FAJnlUsedOnce THEN BEGIN;
        FAJnlUsedOnce := TRUE;
        FAJnlDocumentNo :=
          FAJnlSetup.GetFAJnlDocumentNo(FAJnlLine,FAReclassJnlLine."FA Posting Date",FALSE);
      END;

      WITH FAJnlLine DO BEGIN
        INIT;
        "Line No." := 0;
        FAJnlSetup.SetFAJnlTrailCodes(FAJnlLine);
        "FA Posting Type" := FAPostingType;
        VALIDATE("FA No.",FANo);
        "FA Posting Date" := FAReclassJnlLine."FA Posting Date";
        "Posting Date" := FAReclassJnlLine."Posting Date";
        IF "Posting Date" = "FA Posting Date" THEN
          "Posting Date" := 0D;

        "Document No." := FAReclassJnlLine."Document No.";
        IF "Document No." = '' THEN
          "Document No." := FAJnlDocumentNo;
        IF "Document No." = '' THEN
          FAReclassJnlLine.TESTFIELD("Document No.");

        "Posting No. Series" := FAJnlSetup.GetFANoSeries(FAJnlLine);
        VALIDATE("Depreciation Book Code",FAReclassJnlLine."Depreciation Book Code");
        VALIDATE(Amount,EntryAmount);
        Description := FAReclassJnlLine.Description;
        "FA Reclassification Entry" := TRUE;
        FAJnlNextLineNo := FAJnlNextLineNo + 10000;
        "Line No." := FAJnlNextLineNo;

        //**4PS.sn
        IF ("FA Posting Type" = "FA Posting Type"::"Acquisition Cost") THEN
          Quantity := FAReclassJnlLine.Quantity * Sign;
        CheckAdjustDescriptionReason(Description, "Reason Code", FANo);  //**4PS.n (call 7037)
        //**4PS.en

        OnBeforeFAJnlLineInsert(FAJnlLine,FAReclassJnlLine,Sign);
        INSERT(TRUE);
        //**4PS.sn C000883
        IF DoFillTmpFAJnlLine THEN BEGIN
          TmpFAJnlLine := FAJnlLine;
          TmpFAJnlLine.INSERT;
        END;
        //**4PS.en
      END;
    END;

    LOCAL PROCEDURE InsertGenJnlLine@14(VAR FAReclassJnlLine@1000 : Record 11072084;FANo@1001 : Code[20];EntryAmount@1002 : Decimal;BalAccount@1003 : Boolean;iIntSign@1210190000 : Integer);
    VAR
      FAInsertGLAcc@1004 : Codeunit 5601;
      lvHoldReason@1100485000 : Code[10];
    BEGIN
      IF NOT GenJnlUsedOnce THEN BEGIN;
        GenJnlUsedOnce := TRUE;
        GenJnlDocumentNo :=
          FAJnlSetup.GetGenJnlDocumentNo(GenJnlLine,FAReclassJnlLine."FA Posting Date",FALSE);
      END;

      WITH GenJnlLine DO BEGIN
        INIT;
        "Line No." := 0;
        FAJnlSetup.SetGenJnlTrailCodes(GenJnlLine);
        "Account Type" := "Account Type"::"Fixed Asset";
        "FA Posting Type" := FAPostingType + 1;
        VALIDATE("Account No.",FANo);
        VALIDATE("Depreciation Book Code",FAReclassJnlLine."Depreciation Book Code");
        "FA Posting Date" := FAReclassJnlLine."FA Posting Date";
        "Posting Date" := FAReclassJnlLine."Posting Date";
        IF "Posting Date" = 0D THEN
          "Posting Date" := FAReclassJnlLine."FA Posting Date";
        IF "Posting Date" = "FA Posting Date" THEN
          "FA Posting Date" := 0D;

        "Document No." := FAReclassJnlLine."Document No.";
        IF "Document No." = '' THEN
          "Document No." := GenJnlDocumentNo;
        IF "Document No." = '' THEN
          FAReclassJnlLine.TESTFIELD("Document No.");

        "Posting No. Series" := FAJnlSetup.GetGenNoSeries(GenJnlLine);
        VALIDATE(Amount,EntryAmount);
        Description := FAReclassJnlLine.Description;
        "FA Reclassification Entry" := TRUE;
        GenJnlNextLineNo := GenJnlNextLineNo + 10000;
        "Line No." := GenJnlNextLineNo;

        //**4PS.sn
        IF (GenJnlLine."FA Posting Type" = GenJnlLine."FA Posting Type"::"Acquisition Cost") THEN
          Quantity := FAReclassJnlLine.Quantity * Sign;
        lvHoldReason := "Reason Code";
        IF CheckAdjustDescriptionReason(Description, "Reason Code", FANo) THEN  //* Plant Exit Order
          "System-Created Entry" := TRUE;
        //**4PS.en

        OnBeforeGenJnlLineInsert(GenJnlLine,FAReclassJnlLine,Sign);
        INSERT(TRUE);

        "Reason Code" := lvHoldReason;  //**4PS.n

        IF BalAccount THEN BEGIN
          FAInsertGLAcc.GetBalAcc(GenJnlLine);
          IF FIND('+') THEN ;
          GenJnlNextLineNo := "Line No.";
        END;
      END;
    END;

    LOCAL PROCEDURE FAName@7(VAR FA@1000 : Record 5600;DeprBookCode@1001 : Code[10]) : Text[200];
    BEGIN
      EXIT(DepreciationCalc.FAName(FA,DeprBookCode));
    END;

    LOCAL PROCEDURE CalcDB1DeprAmount@4(FAReclassJnlLine@1000 : Record 11072084);
    VAR
      AccountingPeriod@1008 : Record 50;
      DeprBook@1010 : Record 5611;
      CalculateDepr@1001 : Codeunit 5610;
      DeprAmount@1002 : Decimal;
      DeprAmount2@1012 : Decimal;
      Custom1Amount@1003 : Decimal;
      NumberOfDays@1004 : Integer;
      NumberOfDays2@1013 : Integer;
      Custom1NumberOfDays@1005 : Integer;
      DeprUntilDate@1006 : Date;
      DummyEntryAmounts@1007 : ARRAY [4] OF Decimal;
      FixedAmount@1009 : Decimal;
      FixedAmount2@1014 : Decimal;
      DaysInFiscalYear@1011 : Integer;
    BEGIN
      IF NOT FAReclassJnlLine."Calc. DB1 Depr. Amount" THEN
        EXIT;
      DeprBook.GET(FAReclassJnlLine."Depreciation Book Code");
      DeprBook.TESTFIELD("Use Custom 1 Depreciation",FALSE); // better
      IF (FADeprBook."Depreciation Method" <> FADeprBook."Depreciation Method"::"DB1/SL") AND
         (FADeprBook."Depreciation Method" <> FADeprBook."Depreciation Method"::"Declining-Balance 1")
      THEN BEGIN
        FADeprBook."Depreciation Method" := FADeprBook."Depreciation Method"::"Declining-Balance 1";
        FADeprBook2."Depreciation Method" := FADeprBook."Depreciation Method"::"DB1/SL";
        ERROR(Text006,
          FADeprBook.FIELDCAPTION("Depreciation Method"),
          FADeprBook."Depreciation Method",
          FADeprBook2."Depreciation Method",
          FAName(OldFA,FAReclassJnlLine."Depreciation Book Code"));
      END;
      IF FADeprBook."Depreciation Method" <> FADeprBook2."Depreciation Method" THEN
        ERROR(Text007,
          FADeprBook.FIELDCAPTION("Depreciation Method"),
          FADeprBook."Depreciation Method",
          FAName(NewFA,FAReclassJnlLine."Depreciation Book Code"));

      IF DeprBook."Use Custom 1 Depreciation" THEN
        FAReclassJnlLine.FIELDERROR("Calc. DB1 Depr. Amount",
          STRSUBSTNO(
            Text008,
            DeprBook.FIELDCAPTION("Use Custom 1 Depreciation"),
            DeprBook.TABLECAPTION,
            DeprBook.Code));

      FADeprBook.TESTFIELD("Temp. Ending Date",0D);
      FADeprBook2.TESTFIELD("Temp. Ending Date",0D);

      WITH AccountingPeriod DO
        IF ISEMPTY THEN
          DeprUntilDate := CALCDATE('<-CY>',FAReclassJnlLine."FA Posting Date") - 1
        ELSE BEGIN
          SETRANGE("New Fiscal Year",TRUE);
          SETRANGE("Starting Date",FAReclassJnlLine."FA Posting Date",DMY2DATE(31,12,9999));
          IF FINDFIRST THEN BEGIN
            IF "Starting Date" <= 00000101D THEN
              ERROR(Text005,FIELDCAPTION("Starting Date"),TABLECAPTION);
            DeprUntilDate := "Starting Date" - 1
          END ELSE
            ERROR(Text005,FIELDCAPTION("Starting Date"),TABLECAPTION);
        END;

      CalculateDepr.Calculate(
        DeprAmount,Custom1Amount,NumberOfDays,Custom1NumberOfDays,
        FAReclassJnlLine."FA No.",FAReclassJnlLine."Depreciation Book Code",
        DeprUntilDate,DummyEntryAmounts,0D,0);
      IF (DeprAmount >= 0) OR (NumberOfDays <= 0) THEN
        ERROR(Text009,
          FADeprBook.FIELDCAPTION("Temp. Fixed Depr. Amount"),
          FAName(OldFA,FAReclassJnlLine."Depreciation Book Code"));
      CalculateDepr.Calculate(
        DeprAmount2,Custom1Amount,NumberOfDays2,Custom1NumberOfDays,
        FAReclassJnlLine."New FA No.",FAReclassJnlLine."Depreciation Book Code",
        DeprUntilDate,DummyEntryAmounts,0D,0);

      DaysInFiscalYear := DeprBook."No. of Days in Fiscal Year";
      IF DaysInFiscalYear = 0 THEN
        DaysInFiscalYear := 360;

      IF DeprBook."Fiscal Year 365 Days" THEN
        DaysInFiscalYear := 365;

      FixedAmount := ROUND(-DeprAmount / NumberOfDays * DaysInFiscalYear);
      IF NumberOfDays2 > 0 THEN
        FixedAmount2 := ROUND(-DeprAmount2 / NumberOfDays2 * DaysInFiscalYear);

      FADeprBook."Temp. Fixed Depr. Amount" :=
        ROUND(FixedAmount * (100 - FAReclassJnlLine."Reclassify Acq. Cost %") / 100);
      FADeprBook."Temp. Ending Date" := DeprUntilDate;
      FADeprBook.MODIFY;

      FADeprBook2."Temp. Fixed Depr. Amount" :=
        ROUND(FixedAmount2 + FixedAmount - FADeprBook."Temp. Fixed Depr. Amount");
      FADeprBook2."Temp. Ending Date" := FADeprBook."Temp. Ending Date";
      FADeprBook2.MODIFY;
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeGenJnlLineInsert@6(VAR GenJournalLine@1000 : Record 81;VAR FAReclassJournalLine@1001 : Record 11072084;Sign@1002 : Integer);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeFAJnlLineInsert@10(VAR FAJournalLine@1000 : Record 5621;VAR FAReclassJournalLine@1001 : Record 11072084;Sign@1002 : Integer);
    BEGIN
    END;

    LOCAL PROCEDURE CheckAdjustDescriptionReason@1100485000(VAR IODescription@1100485000 : Text[100];VAR IOReason@1100485001 : Code[10];IFixedAssetNo@1100485004 : Code[20]) : Boolean;
    VAR
      lvExitOrderLineRec@1100485003 : Record 11012560;
      lvTrigger@1100485002 : Code[20];
    BEGIN
      //**4PS
      //* Trigger 'Correctie exitorder', see also CU 5616+11012559+13 (call 7037). This CU now also used for call 8704.

      lvTrigger := '<PLANT-EXITORDER>';
      IF STRPOS(IODescription, lvTrigger) = 1 THEN BEGIN
        IODescription := lvExitOrderLineRec.FIELDCAPTION("Exit-order No.")  +': '+ COPYSTR(IODescription, STRLEN(lvTrigger)+1);
        IF IFixedAssetNo = OldFA."No." THEN
          IOReason := 'EXIT-CORR';
        EXIT(TRUE);
      END;
      EXIT(FALSE);
    END;

    LOCAL PROCEDURE SkipDisposedFA_OnExitOrderPlnt@1100530000(IFixedAssetNo@1100485004 : Code[20];IFAPostingType@1100530000 : Option;IDescription@1100530001 : Text[100]) : Boolean;
    VAR
      lvTrigger@1100485002 : Code[20];
    BEGIN
      //**4PS
      //* See also remarks in function 'CheckAdjustDescriptionReason'
      //* Check skip reclass. 'Salvage' on exit plant (when Bulk Lot No is partly exit) for new disposed FA

      IF (IFixedAssetNo = '') OR (IFixedAssetNo <> NewFA."No.") THEN
        EXIT(FALSE);

      IF IFAPostingType = FAPostingType::"Salvage Value" THEN BEGIN
        lvTrigger := '<PLANT-EXITORDER>';
        IF STRPOS(IDescription, lvTrigger) = 1 THEN
          EXIT(TRUE);
      END;
      EXIT(FALSE);
    END;

    PROCEDURE SetFillTmpFAJnlLine@1100409000();
    BEGIN
      //**4PS C000883
      DoFillTmpFAJnlLine := TRUE;
      TmpFAJnlLine.RESET;
      TmpFAJnlLine.DELETEALL;
    END;

    PROCEDURE GetTmpFAJnlLine@1100409001(VAR FAJnlLineTmp@1100409000 : TEMPORARY Record 5621);
    BEGIN
      //**4PS C000883
      WITH TmpFAJnlLine DO BEGIN
        IF FINDFIRST THEN BEGIN
          REPEAT
            IF NOT FAJnlLineTmp.GET("Journal Template Name", "Journal Batch Name", "Line No.") THEN BEGIN
              FAJnlLineTmp := TmpFAJnlLine;
              FAJnlLineTmp.INSERT;
            END;
          UNTIL NEXT = 0;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

