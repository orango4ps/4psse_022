OBJECT Codeunit 11125529 Site Management
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1100525000 : TextConst 'DEU=Angegebener Projekt-Datensatz ist nicht tempor„r;ENU=Given job dataset is not temporary;NLD=Opgegeven project dataset is niet tijdelijk';

    PROCEDURE GetProjectDS@1100525001(VAR OTmpJob@1100525000 : TEMPORARY Record 11072003);
    VAR
      ProjectResponsiblePerson@1100525001 : Record 11012040;
      UserSetup@1100525002 : Record 91;
      Job@1100525003 : Record 11072003;
      JobsSetup@1100525004 : Record 315;
      ExtraStep@1100525005 : Integer;
    BEGIN
      IF NOT OTmpJob.ISTEMPORARY THEN
        ERROR(Text000);

      OTmpJob.RESET;
      OTmpJob.DELETEALL;
      OTmpJob."No." := '';

      UserSetup.GET(USERID);
      JobsSetup.GET;
      IF JobsSetup."Provisions at Closure" THEN
        ExtraStep := 2;

      ProjectResponsiblePerson.SETCURRENTKEY("Employee No.", "Project No.");
      ProjectResponsiblePerson.SETRANGE("Employee No.", UserSetup."Employee No.");
      ProjectResponsiblePerson.SETFILTER("Project No.", '<>%1', '');
      IF ProjectResponsiblePerson.FINDSET THEN
        REPEAT
          IF Job.GET(ProjectResponsiblePerson."Project No.") THEN
            IF ProjectResponsiblePerson."Project No." <> OTmpJob."No." THEN BEGIN
              IF (Job."Project Status" >= Job."Project Status"::Preparation) AND
                 (Job."Project Status" <= Job."Project Status"::"Technical Finished" + ExtraStep)
              THEN BEGIN
                OTmpJob := Job;
                OTmpJob.INSERT;
              END;
            END;
        UNTIL ProjectResponsiblePerson.NEXT = 0;
    END;

    PROCEDURE GetNoOfProjects@1100525002() : Integer;
    VAR
      TmpJob@1100525000 : TEMPORARY Record 11072003;
    BEGIN
      GetProjectDS(TmpJob);
      EXIT(TmpJob.COUNT);
    END;

    PROCEDURE StoreSelectedProjectInfo@1100528600(IProjectNo@1100528600 : Code[20];IPrincipal@1100528601 : Code[20]);
    VAR
      UserSetupSMF@1100528602 : Record 11125542;
    BEGIN
      IF NOT UserSetupSMF.GET(USERID) THEN BEGIN
        UserSetupSMF."User ID" := USERID;
        UserSetupSMF.INSERT(TRUE);
      END;
      UserSetupSMF.VALIDATE("Project No.", IProjectNo);
      UserSetupSMF.VALIDATE(Principal, IPrincipal);
      UserSetupSMF.MODIFY(TRUE);
    END;

    PROCEDURE GetSelectedProjectInfo@1100528602(VAR OProjectNo@1100528600 : Code[20];VAR OPrincipal@1100528601 : Code[20]) : Boolean;
    VAR
      UserSetupSMF@1100528602 : Record 11125542;
    BEGIN
      IF UserSetupSMF.GET(USERID) THEN BEGIN
        OProjectNo := UserSetupSMF."Project No.";
        OPrincipal := UserSetupSMF.Principal;
        EXIT(TRUE);
      END ELSE BEGIN
        OProjectNo := '';
        OPrincipal := '';
        EXIT(FALSE);
      END;
    END;

    BEGIN
    END.
  }
}

