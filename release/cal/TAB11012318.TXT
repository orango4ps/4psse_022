OBJECT Table 11012318 Preferred Vendor
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF (Type = Type::"Navision Item") AND
                 ("Navision Item" = '') THEN
                   ERROR(Text001);

               IF (Type = Type::"Basic Item") AND
                 ("Basic Item" = '') THEN
                   ERROR(Text003);

               //CheckItemUnitsofMeasurement;
             END;

    CaptionML=[ENU=Preferred Vendor;
               NOR=Foretrukket leverand›r;
               SVE=F”rvald leverant”r];
  }
  FIELDS
  {
    { 5   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF Type = Type::"Basic Item" THEN
                                                                  "Navision Item" := '';
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Typ];
                                                   OptionCaptionML=[ENU=Navision Item,Basic  Item;
                                                                    NOR=Navision-artikkel,Fabrikantartikkel;
                                                                    SVE=Navision-artikel,Basartikel];
                                                   OptionString=Navision Item,Basic Item }
    { 10  ;   ;Navision Item       ;Code20        ;TableRelation="Item Relation"."Navision Item";
                                                   OnValidate=BEGIN
                                                                IF (Type = Type::"Basic Item") AND
                                                                  ("Navision Item" <> '') THEN
                                                                  ERROR('');

                                                                IF (Type = Type::"Navision Item") THEN BEGIN
                                                                  ItemRelRec.RESET;
                                                                  ItemRelRec.SETFILTER("Navision Item","Navision Item" );
                                                                  IF NOT ItemRelRec.FINDFIRST THEN BEGIN       //changed 25-04-07   NOT added
                                                                //    "Basic Item" := ItemRelRec."Basic Item";  25-04-07
                                                                //    Manufacturer := ItemRelRec.Manufacturer;  25-04-07
                                                                //  END ELSE BEGIN                              25-04-07
                                                                    ERROR(Text002, "Navision Item")
                                                                  END
                                                                END;

                                                                CALCFIELDS("Description Navision-item");
                                                              END;

                                                   CaptionML=[ENU=Navision Item;
                                                              NOR=Navision-artikkel;
                                                              SVE=Navision-artikel] }
    { 20  ;   ;Description Navision-item;Text100  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Item.Description WHERE (No.=FIELD(Navision Item)));
                                                   CaptionML=[ENU=Description Navision-item;
                                                              NOR=Beskrivelse, Navision-artikkel;
                                                              SVE=Beskrivning, Navision-artikel];
                                                   Editable=No }
    { 30  ;   ;Basic Item          ;Code20        ;TableRelation=IF (Type=CONST(Basic Item)) "Basic Item"."Product Code" WHERE (Manufacturer=FIELD(Manufacturer));
                                                   CaptionML=[ENU=Basic Item;
                                                              NOR=Fabrikantartikkel;
                                                              SVE=Basartikel] }
    { 40  ;   ;Manufacturer        ;Code15        ;TableRelation="ETIM Vendor - Central Mgt";
                                                   OnValidate=BEGIN
                                                                "Basic Item":= '';
                                                              END;

                                                   CaptionML=[ENU=Manufacturer;
                                                              NOR=Produsent;
                                                              SVE=Tillverkare] }
    { 50  ;   ;Trade Item          ;Code20        ;TableRelation=IF (Type=CONST(Navision Item)) "Trade Item"."Item Code" WHERE (Vendor=FIELD(Vendor));
                                                   OnValidate=BEGIN
                                                                CheckUnits;
                                                              END;

                                                   CaptionML=[ENU=Trade Item;
                                                              NOR=Katalogartikkel;
                                                              SVE=Anskaffningsartikel] }
    { 55  ;   ;Vendor              ;Code15        ;TableRelation="ETIM Vendor - Central Mgt";
                                                   OnValidate=BEGIN
                                                                "Trade Item":= '';
                                                              END;

                                                   CaptionML=[ENU=Vendor;
                                                              NOR=Leverand›r;
                                                              SVE=Leverant”r] }
    { 100 ;   ;Source Type         ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Item Relation"."Source Type" WHERE (Navision Item=FIELD(Navision Item)));
                                                   CaptionML=[ENU=Source Type;
                                                              NOR=Kildetype;
                                                              SVE=Ursprungstyp];
                                                   OptionCaptionML=[ENU=Basic Item,Trade Item;
                                                                    NOR=Fabrikantartikkel,Katalogartikkel;
                                                                    SVE=Basartikel,Anskaffningsartikel];
                                                   OptionString=Basic Item,Trade Item;
                                                   Editable=No }
    { 120 ;   ;GTIN Code           ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Item Relation"."GTIN Code" WHERE (Navision Item=FIELD(Navision Item)));
                                                   CaptionML=[ENU=GTIN Code;
                                                              NOR=GTIN-kode;
                                                              SVE=GTIN];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Type,Navision Item,Manufacturer,Basic Item;
                                                   Clustered=Yes }
    {    ;Manufacturer,Basic Item                  }
    {    ;Vendor,Trade Item                        }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100485000 : TextConst 'ENU=''Navision Item'' is Mandatory.;NOR=Navision-artikkel'' er obligatorisk.;SVE=''Navision-artikel'' „r obligatorisk.';
      ItemRelRec@1100485001 : Record 11012319;
      Text002@1100485002 : TextConst 'ENU=Navision Item  ''%1'' is not found in Item Relations.;NOR=Navision-artikkelen ''%1'' finnes ikke i artikkelrelasjoner.;SVE=Navision-artikeln ''%1'' finns inte i artikelrelationer.';
      Text003@1100485003 : TextConst 'ENU=''Basic Item'' is Mandatory.;NOR=''Fabrikantartikkel'' er obligatorisk;SVE=''Basartikel'' „r obligatoriskt';
      Text004@1100485004 : TextConst 'ENU=Unit or calculationfactor of preferred Trade Item differs from Trade Item used in Item relation: this can lead to changes in your stock;NOR=Enheten eller kalkylefaktoren for den valgte katalogartikkelen skiller seg fra katalogartikkelenen som er i bruk i artikkelrelasjon: dette kan f›re til forandringer i ditt lager;SVE=Enheten eller kalkylfaktorn f”r den valda skaffningsartikeln skiljer sig fr†n den skaffningsartikel som anv„nds i artikelrelation: detta kan leda till f”r„ndringar i ditt lager';

    PROCEDURE CheckItemUnitsofMeasurement@1100485000();
    VAR
      lTradeItemRec@1100485000 : Record 11012317;
      lItemRec@1100485001 : Record 27;
      lItemUnitOfMeasure@1100485002 : Record 5404;
      lCentralDataManagement@1100485003 : Codeunit 11012010;
    BEGIN
      //This function is not active
      IF lCentralDataManagement.CentralActive() THEN
        lItemUnitOfMeasure.CHANGECOMPANY(lCentralDataManagement.CentralCompany());

      IF (Type = Type::"Navision Item") AND
         (Vendor <> '') AND
         ("Trade Item" <> '') THEN
      BEGIN
        IF lTradeItemRec.GET(Vendor, "Trade Item") THEN BEGIN
          IF lItemRec.GET("Navision Item") THEN BEGIN

            lItemUnitOfMeasure.SETRANGE("Item No.", "Navision Item");
            lItemUnitOfMeasure.SETRANGE(Code, lTradeItemRec."Application Unit");
            IF NOT lItemUnitOfMeasure.FINDFIRST THEN BEGIN
              lItemUnitOfMeasure.VALIDATE("Item No.", "Navision Item");
              lItemUnitOfMeasure.VALIDATE(Code, lTradeItemRec."Application Unit");
              lItemUnitOfMeasure.INSERT(TRUE);
            END;

            lItemUnitOfMeasure.SETRANGE("Item No.", "Navision Item");
            lItemUnitOfMeasure.SETRANGE(Code, lTradeItemRec."Packaging Unit");
            IF NOT lItemUnitOfMeasure.FINDFIRST THEN BEGIN
              lItemUnitOfMeasure.VALIDATE("Item No.", "Navision Item");
              lItemUnitOfMeasure.VALIDATE(Code, lTradeItemRec."Packaging Unit");
              lItemUnitOfMeasure."Qty. per Unit of Measure" := lTradeItemRec."Qty. per Unit of Measure";
              lItemUnitOfMeasure.INSERT(TRUE);
            END;

          END;
        END;
      END;
    END;

    PROCEDURE CheckUnits@1100485001();
    VAR
      lvTradeItemRec@1100485001 : Record 11012317;
      lvItemRec@1100485000 : Record 27;
      lvItemUnitRec@1100485002 : Record 5404;
    BEGIN
      IF (Type = Type::"Navision Item") THEN BEGIN
        lvItemRec.GET("Navision Item");
        IF lvTradeItemRec.GET(Vendor, "Trade Item") THEN BEGIN
          lvItemUnitRec.GET(lvItemRec."No.", lvTradeItemRec."Packaging Unit");

          IF (UPPERCASE(lvItemRec."Base Unit of Measure") <> UPPERCASE(lvTradeItemRec."Application Unit")) OR
             (UPPERCASE(lvItemRec."Purch. Unit of Measure") <> UPPERCASE(lvTradeItemRec."Packaging Unit")) OR
             (lvItemUnitRec."Qty. per Unit of Measure" <> lvTradeItemRec."Qty. per Unit of Measure") THEN
            MESSAGE(Text004);
        END;
      END;
    END;

    PROCEDURE LookupItem@1100485002();
    VAR
      LookUpForm@1100485000 : Page 11012318;
    BEGIN
      IF Type = Type::"Basic Item" THEN
        EXIT;

      ItemRelRec.RESET;
      ItemRelRec.INIT;
      IF "Navision Item" <> '' THEN BEGIN
        IF ItemRelRec.GET("Navision Item") THEN;
      END;

      LookUpForm.SETTABLEVIEW(ItemRelRec );
      LookUpForm.SETRECORD(ItemRelRec );
      LookUpForm.LOOKUPMODE := TRUE;

      IF LookUpForm.RUNMODAL = ACTION::LookupOK THEN BEGIN
        LookUpForm.GETRECORD(ItemRelRec);
        VALIDATE("Navision Item", ItemRelRec."Navision Item");
      END;

      CLEAR( LookUpForm );
    END;

    PROCEDURE LookupBasicItem@1100485003();
    VAR
      BasicItemRec@1100485000 : Record 11012316;
    BEGIN
      IF Type = Type::"Basic Item" THEN BEGIN
        BasicItemRec.Manufacturer := Manufacturer;
        BasicItemRec."Product Code" := "Basic Item";
        IF PAGE.RUNMODAL(PAGE::"Basic Item List", BasicItemRec) = ACTION::LookupOK THEN BEGIN
          Manufacturer := BasicItemRec.Manufacturer;
          VALIDATE("Basic Item", BasicItemRec."Product Code");
        END;
      END;
    END;

    PROCEDURE LookupTradeItem@1100485004();
    VAR
      TradeItemRec@1100485000 : Record 11012317;
      BasicItemRec@1210190000 : Record 11012316;
    BEGIN
      CASE Type OF
        Type::"Navision Item":
          BEGIN
            CALCFIELDS("Source Type");
            IF "Source Type" = "Source Type"::"Basic Item" THEN BEGIN
      //        IF (Type = Type::"Navision Item") THEN BEGIN //new 26-04-2007 //mg.o, 10-06-11: M26005
              ItemRelRec.INIT;
              ItemRelRec.RESET;
              ItemRelRec.SETFILTER("Navision Item", "Navision Item");
              IF ItemRelRec.FINDFIRST THEN;
      //        END;

              TradeItemRec.RESET;

              TradeItemRec.Vendor := Vendor;
              TradeItemRec."Item Code" := "Trade Item";
              IF TradeItemRec.GET(Vendor,"Trade Item") THEN;
              IF ItemRelRec."Basic Item" <> '' THEN
                TradeItemRec.SETRANGE("Product Code", ItemRelRec."Basic Item");
              IF ItemRelRec.Manufacturer <> '' THEN
                TradeItemRec.SETRANGE(Manufacturer, ItemRelRec.Manufacturer);
              TradeItemRec.SETCURRENTKEY("Product Code", Manufacturer);
            END ELSE BEGIN
              CALCFIELDS("GTIN Code") ;
              TradeItemRec.RESET;
              TradeItemRec.Vendor := Vendor;
              TradeItemRec."Item Code" := "Trade Item";
              IF TradeItemRec.GET(Vendor,"Trade Item") THEN;
              IF "GTIN Code" <> '' THEN
                TradeItemRec.SETRANGE("GTIN Code (Item)", "GTIN Code");
              TradeItemRec.SETCURRENTKEY("GTIN Code (Item)");
            END;
          END;
        Type::"Basic Item":
          BEGIN
            IF (Manufacturer <> '') AND ("Basic Item" <> '') THEN
              IF NOT BasicItemRec.GET(Manufacturer, "Basic Item") THEN
                BasicItemRec.INIT;
            IF BasicItemRec."GTIN Code" <> '' THEN BEGIN
              TradeItemRec.SETRANGE("GTIN Code (Item)", BasicItemRec."GTIN Code");
              TradeItemRec.SETCURRENTKEY("GTIN Code (Item)");
            END ELSE BEGIN
              IF ItemRelRec."Basic Item" <> '' THEN
                TradeItemRec.SETRANGE("Product Code", ItemRelRec."Basic Item");
              IF ItemRelRec.Manufacturer <> '' THEN
                TradeItemRec.SETRANGE(Manufacturer, ItemRelRec.Manufacturer);
              TradeItemRec.SETCURRENTKEY("Product Code", Manufacturer);
            END;
          END;
      END;

      IF PAGE.RUNMODAL(PAGE::"Trade Item List", TradeItemRec) = ACTION::LookupOK THEN BEGIN
      //  Manufacturer := TradeItemRec.Manufacturer;     25-04-07
      //  "Basic Item" := TradeItemRec."Product Code";   25-04-07
        Vendor := TradeItemRec.Vendor;
        VALIDATE("Trade Item", TradeItemRec."Item Code");
      END;
    END;

    BEGIN
    END.
  }
}

