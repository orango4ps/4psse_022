OBJECT Table 11125531 Get Order (SMF)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Project No.");
               IF "No." = '' THEN BEGIN
                 SiteMgtSetupRec.GET;
                 SiteMgtSetupRec.TESTFIELD("Get Order Nos.");
                 NoSeriesMgt.InitSeries(SiteMgtSetupRec."Get Order Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;

               Status := Status::Open;
               "Posted on" := 0DT;
               "Created by" := USERID;
               "Created on" := CURRENTDATETIME;
               IF "Purchaser Code" = '' THEN
                 FillDefaultPurchaser();
               ProjectSiteMgt.LogGetOrder(1, Rec); //**4PS.n
             END;

    OnModify=BEGIN
               IF ("Vendor No." <> xRec."Vendor No.") OR ("Order Date" <> xRec."Order Date") THEN
                 UpdateOrderLinesSubcActivity;
               ProjectSiteMgt.LogGetOrder(2, Rec); //**4PS.n
             END;

    OnDelete=VAR
               lvGetOrderLineRec@1100525000 : Record 11125532;
             BEGIN
               lvGetOrderLineRec.SETRANGE("Get Order No.", "No.");
               lvGetOrderLineRec.DELETEALL(FALSE);
               ProjectSiteMgt.LogGetOrder(3, Rec); //**4PS.n
             END;

    CaptionML=[ENU=Get Order (Site Manager Form);
               NOR=Hente ordre (prosjektlederskjema);
               SVE=HÑmta order (byggledarformulÑr)];
    LookupPageID=Page11125536;
    DrillDownPageID=Page11125536;
  }
  FIELDS
  {
    { 10  ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  SiteMgtSetupRec.GET;
                                                                  NoSeriesMgt.TestManual(SiteMgtSetupRec."Get Order Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              NOR=Nr.;
                                                              SVE=Nr] }
    { 20  ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr];
                                                   Editable=No }
    { 21  ;   ;Project Description ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Job.Description WHERE (No.=FIELD(Project No.)));
                                                   CaptionML=[ENU=Project Description;
                                                              NOR=Prosjektbeskrivelse;
                                                              SVE=Projektbeskrivning];
                                                   Editable=No }
    { 30  ;   ;Order Date          ;Date          ;OnValidate=BEGIN
                                                                TESTFIELD(Status,Status::Open);
                                                              END;

                                                   CaptionML=[ENU=Order Date;
                                                              NOR=Bestillingsdato;
                                                              SVE=Orderdatum] }
    { 40  ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor;
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Status,Status::Open);
                                                                IF "Vendor No." <> xRec."Vendor No." THEN
                                                                  CheckOrderLinesSubcActivity;
                                                                CALCFIELDS("Vendor Name", Address, "Post Code", City);
                                                              END;

                                                   CaptionML=[ENU=Vendor No.;
                                                              NOR=Leverandõrnr.;
                                                              SVE=Leverantîrsnr.] }
    { 41  ;   ;Vendor Name         ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor.Name WHERE (No.=FIELD(Vendor No.)));
                                                   CaptionML=[ENU=Vendor Name;
                                                              NOR=Leverandõrsnavn;
                                                              SVE=Leverantîrsnamn];
                                                   Editable=No }
    { 43  ;   ;Address             ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor.Address WHERE (No.=FIELD(Vendor No.)));
                                                   CaptionML=[ENU=Address;
                                                              NOR=Adresse;
                                                              SVE=Adress];
                                                   Editable=No }
    { 45  ;   ;Post Code           ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor."Post Code" WHERE (No.=FIELD(Vendor No.)));
                                                   TableRelation="Post Code";
                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Post Code;
                                                              NOR=Postnr.;
                                                              SVE=Postnr];
                                                   Editable=No }
    { 46  ;   ;City                ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor.City WHERE (No.=FIELD(Vendor No.)));
                                                   CaptionML=[ENU=City;
                                                              NOR=Sted;
                                                              SVE=Ort];
                                                   Editable=No }
    { 100 ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=[ENU=Open,,,,Posted;
                                                                    NOR=èpen,,,,Bokfõrt;
                                                                    SVE=ôppen,,,,Bokfîrd];
                                                   OptionString=Open,,,,Posted;
                                                   Editable=No }
    { 105 ;   ;Posted on           ;DateTime      ;CaptionML=[ENU=Posted on;
                                                              NOR=Bokfõrt den;
                                                              SVE=Bokfîrt den];
                                                   Editable=No }
    { 110 ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              NOR=Nr-serie;
                                                              SVE=Nr-serie];
                                                   Editable=No }
    { 120 ;   ;Purchaser Code      ;Code20        ;TableRelation=Salesperson/Purchaser;
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Status,Status::Open);
                                                              END;

                                                   CaptionML=[ENU=Purchaser Code;
                                                              NOR=Innkjõperkode;
                                                              SVE=Inkîparkod] }
    { 130 ;   ;Created by          ;Code50        ;TableRelation=User."User Name";
                                                   OnValidate=BEGIN
                                                                UserManagement.ValidateUserID("Created by");
                                                              END;

                                                   OnLookup=BEGIN
                                                              UserManagement.LookupUserID("Created by");
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Created by;
                                                              NOR=Opprettet av;
                                                              SVE=Skapad av];
                                                   Editable=No }
    { 131 ;   ;Name Created by     ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(User."Full Name" WHERE (User Name=FIELD(Created by)));
                                                   CaptionML=[ENU=Name Created by;
                                                              NOR=Navn opprettet av;
                                                              SVE=Namn skapat av];
                                                   Editable=No }
    { 135 ;   ;Created on          ;DateTime      ;CaptionML=[ENU=Created on;
                                                              NOR=Opprettet den;
                                                              SVE=Skapades];
                                                   Editable=No }
    { 150 ;   ;Purchase Order No.  ;Code20        ;TableRelation="Purchase Header".No. WHERE (Document Type=CONST(Order));
                                                   CaptionML=[ENU=Purchase Order No.;
                                                              NOR=Innkjõpsordrenr.;
                                                              SVE=Inkîpsordernr];
                                                   Editable=No }
    { 160 ;   ;Expected Delivery Date;Date        ;CaptionML=[ENU=Expected Delivery Date;
                                                              NOR=Forventet leveringsdato;
                                                              SVE=FîrvÑntat leveransdatum] }
    { 170 ;   ;Deliver             ;Option        ;OnValidate=BEGIN
                                                                IF Deliver <> xRec.Deliver THEN
                                                                  "Ship To Location" := '';

                                                                CASE Deliver OF
                                                                  Deliver::"Project Location":
                                                                    TESTFIELD("Project No.");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Deliver;
                                                              NOR=Lever;
                                                              SVE=Leverera];
                                                   OptionCaptionML=[ENU=" ,Location,Project Location,,Contact Address,Plant Depot";
                                                                    NOR=" ,Lokasjon,Prosjektlokasjon,,Kontaktadresse,Maskindepot";
                                                                    SVE=" ,LagerstÑlle,Projektplats,,Kontaktadress,MaskindepÜ"];
                                                   OptionString=[ ,Location,Project Location,,Contact Address,Plant Depot] }
    { 180 ;   ;Ship To Location    ;Code20        ;TableRelation=IF (Deliver=CONST(Project Location)) "Project Location".Code WHERE (Project No.=FIELD(Project No.))
                                                                 ELSE IF (Deliver=CONST(Location)) Location.Code
                                                                 ELSE IF (Deliver=CONST(Contact Address)) Contact.No.
                                                                 ELSE IF (Deliver=CONST(Plant Depot)) "Plant Location".Code WHERE (Depot=CONST(Yes));
                                                   CaptionML=[ENU=Ship To Location;
                                                              NOR=Lever til lokasjon;
                                                              SVE=Leverera till lagerstÑlle] }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Project No.                              }
    {    ;Purchase Order No.                       }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      SiteMgtSetupRec@1100525001 : Record 11125525;
      NoSeriesMgt@1100525000 : Codeunit 396;
      ProjectSiteMgt@1100528300 : Codeunit 11012282;
      UserManagement@1100409000 : Codeunit 418;
      Text001@1100529000 : TextConst 'ENU=''%1'' not present for %2 ''%3''';

    PROCEDURE AssistEdit@2(IOrderXRec@11012000 : Record 11125531) : Boolean;
    VAR
      lvOrderRec@1100485000 : Record 11125531;
    BEGIN
      WITH lvOrderRec DO BEGIN
        lvOrderRec := Rec;
        SiteMgtSetupRec.GET;
        SiteMgtSetupRec.TESTFIELD("Get Order Nos.");
        IF NoSeriesMgt.SelectSeries(SiteMgtSetupRec."Get Order Nos.",IOrderXRec."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("No.");
          Rec := lvOrderRec;
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE FillDefaultPurchaser@1100525000();
    VAR
      lvProjRec@1100525000 : Record 11072003;
    BEGIN
      IF lvProjRec.GET("Project No.") THEN
        "Purchaser Code" := lvProjRec."Purchaser Code";
    END;

    PROCEDURE PostGetOrder@1100527002();
    VAR
      GetOrderMgt@1100527000 : Codeunit 11125527;
    BEGIN
      TESTFIELD("No.");
      TESTFIELD(Status, Status::Open);

      GetOrderMgt.PostGetOrder(Rec);
    END;

    LOCAL PROCEDURE CheckOrderLinesSubcActivity@1100529000();
    VAR
      GetOrderLineSMF@1100529001 : Record 11125532;
      SubcontractingActivityPrice@1100529000 : Record 11229373;
    BEGIN
      GetOrderLineSMF.SETRANGE("Get Order No.", "No.");
      GetOrderLineSMF.SETFILTER("Subcontracting Activity", '<>%1', '');
      IF GetOrderLineSMF.FINDSET THEN BEGIN
        REPEAT
          IF NOT SubcontractingActivityPrice.GET(GetOrderLineSMF."Subcontracting Activity", "Vendor No.") THEN
           GetOrderLineSMF.FIELDERROR("Subcontracting Activity",
            STRSUBSTNO(Text001, GetOrderLineSMF."Subcontracting Activity", FIELDCAPTION("Vendor No."), "Vendor No."));
        UNTIL GetOrderLineSMF.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE UpdateOrderLinesSubcActivity@1100529003();
    VAR
      GetOrderLineSMF@1100529001 : Record 11125532;
      SubcontractingActivityPrice@1100529000 : Record 11229373;
    BEGIN
      GetOrderLineSMF.SETRANGE("Get Order No.", "No.");
      GetOrderLineSMF.SETFILTER("Subcontracting Activity", '<>%1', '');
      IF GetOrderLineSMF.FINDSET(TRUE, FALSE) THEN BEGIN
        IF "Vendor No." <> xRec."Vendor No." THEN
          MODIFY;
        REPEAT
          IF "Vendor No." <> xRec."Vendor No." THEN
            GetOrderLineSMF.VALIDATE("Subcontracting Activity")
          ELSE BEGIN
            IF "Order Date" <> xRec."Order Date" THEN BEGIN
              IF SubcontractingActivityPrice.GET(GetOrderLineSMF."Subcontracting Activity", "Vendor No.") THEN
                GetOrderLineSMF.VALIDATE(Price, SubcontractingActivityPrice.GetPrice("Order Date"));
            END;
          END;
          GetOrderLineSMF.MODIFY(TRUE);
        UNTIL GetOrderLineSMF.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}

