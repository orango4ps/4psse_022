OBJECT Page 11130148 Check WIP SF
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=WIP-öberprÅfung;
               ENU=Check WIP;
               NLD=OHW controle;
               NOR=Kontroller arbeidskapital;
               SVE=Kontrollera pÜgÜende arbeten];
    SourceTable=Table11020288;
    PopulateAllFields=Yes;
    PageType=ListPart;
    ActionList=ACTIONS
    {
      { 1100525504;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100525500;1 ;Action    ;
                      CaptionML=[DEU=Differenzen anzeigen;
                                 ENU=Show Differences;
                                 NLD=Toon verschillen;
                                 NOR=Vis differanser;
                                 SVE=Visa differenser];
                      Promoted=Yes;
                      Image=View;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 TblData.DELETEALL;
                                 Compare;
                                 PAGE.RUN(11071803);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1000000000;1;Group  ;
                Editable=FALSE;
                GroupType=Repeater }

    { 1000000003;2;Field  ;
                SourceExpr="Account No." }

    { 1000000001;2;Field  ;
                SourceExpr="Global Dimension 1" }

    { 1000000005;2;Field  ;
                SourceExpr="Account Description" }

    { 1000000007;2;Field  ;
                SourceExpr="Balance Total Costs" }

    { 1000000009;2;Field  ;
                SourceExpr="Balance Total Price";
                OnDrillDown=BEGIN
                              RecJobLedgEntries.SETCURRENTKEY(Type,"No.","Global Dimension 1 Code","Posting Date");
                              RecJobLedgEntries.SETRANGE(Type,RecJobLedgEntries.Type::"G/L Account");
                              RecJobLedgEntries.SETFILTER("No.","Account No.");
                              RecJobLedgEntries.SETFILTER("Global Dimension 1 Code","Global Dimension 1");
                              RecJobLedgEntries.SETFILTER("Posting Date",GETFILTER("Balance Preclosure Result"));
                              FrmJobLedgEntries.SETTABLEVIEW(RecJobLedgEntries);
                              FrmJobLedgEntries.RUN;
                            END;
                             }

    { 1000000016;2;Field  ;
                SourceExpr="Balance Preclosure Result" }

    { 1000000021;2;Field  ;
                SourceExpr="Balance Closed Cost" }

    { 1000000023;2;Field  ;
                SourceExpr="Balance Closed Reveues" }

    { 1000000011;2;Field  ;
                SourceExpr="Balance G/L Entries" }

    { 1000000019;2;Field  ;
                CaptionML=[DEU=Differenz;
                           ENU=Difference;
                           NLD=Verschil;
                           NOR=Differanse;
                           SVE=Differens];
                SourceExpr="Balance Total Costs"+"Balance Total Price"+"Balance Preclosure Result"+"Balance Closed Cost"+"Balance Closed Reveues"-"Balance G/L Entries" }

  }
  CODE
  {
    VAR
      RecGenLedgEntries@1100485003 : Record 17;
      TblData@1100485004 : Record 11020289;
      FrmJobLedgEntries@1000000000 : Page 11072005;
      RecJobLedgEntries@1000000001 : Record 11072005;
      Venster@1000000002 : Dialog;
      PerGlobalDimension1Code@1100485000 : Boolean;
      RefDateFilter@1100525500 : Text[30];
      Text001@1100528900 : TextConst 'DEU=Posten vergleichen.;ENU=Compare entries.;NLD=Posten vergelijken.';
      Text002@1100528901 : TextConst 'DEU=Komprimierte Posten vergleichen.;ENU=Compare compressed entries.;NLD=Verdichte posten vergelijken.';

    PROCEDURE Compare@1210190001();
    VAR
      TmpDataPP@1210190000 : TEMPORARY Record 11020289;
      TmpDataJP@1210190002 : TEMPORARY Record 11020289;
      CurDocumentnr@1210190003 : Code[20];
      CurDatum@1210190004 : Date;
      CurOmschrijving@1210190005 : Text[100];
      CurProjectnr@1210190006 : Code[20];
      Totaal@1210190007 : Decimal;
      Klaar@1210190008 : Boolean;
      JPTotaal@1000000000 : Decimal;
      CurServiceOrdernr@1000000001 : Code[20];
      CurServiceContractnr@1000000002 : Code[20];
    BEGIN
      Venster.OPEN('Bezig met #1###############################\'+
                   '          #2#######################');

      //**********************************************************
      //Read project entries and journal entries into temp tables
      //**********************************************************
      TmpDataPP.DELETEALL;
      TmpDataJP.DELETEALL;
      TmpDataPP.CLEARMARKS;

      Venster.UPDATE(1,'Inlezen projectposten ' + "Account No." + ' ' + "Global Dimension 1");

      RecJobLedgEntries.SETCURRENTKEY(Type,"No.","Global Dimension 1 Code","Posting Date");
      RecJobLedgEntries.SETRANGE(Type,RecJobLedgEntries.Type::"G/L Account");
      RecJobLedgEntries.SETFILTER("No.","Account No.");
      RecJobLedgEntries.SETFILTER("Global Dimension 1 Code","Global Dimension 1");
      RecJobLedgEntries.SETFILTER("Posting Date",GETFILTER("Date Filter"));

      IF RecJobLedgEntries.FIND('-') THEN
      REPEAT
        Venster.UPDATE(2,RecJobLedgEntries."Document No.");
        TmpDataPP.Source := TmpDataPP.Source::Job;
        TmpDataPP."Document No." := RecJobLedgEntries."Document No.";
        TmpDataPP.Date := RecJobLedgEntries."Posting Date";
        TmpDataPP.Description := RecJobLedgEntries.Description;
        TmpDataPP."Project No." := RecJobLedgEntries."Job No.";
        TmpDataPP."Service Order No." := RecJobLedgEntries."Service Order No.";
        TmpDataPP."Service Contract No." := RecJobLedgEntries."Service Contract No.";
        TmpDataPP."Employee No." := RecJobLedgEntries."Employee No.";
        TmpDataPP.Amount := ROUND(RecJobLedgEntries."Total Cost (LCY)" + RecJobLedgEntries."Total Price (LCY)"+
                                  RecJobLedgEntries."Actual Preclosure Result" + RecJobLedgEntries."Actual Provision Result" +
                                  RecJobLedgEntries."Closed Costs"+RecJobLedgEntries."Closed Revenues");
        TmpDataPP."Entry No." := RecJobLedgEntries."Entry No.";
        TmpDataPP."Account No." := RecJobLedgEntries."No.";
        TmpDataPP.INSERT;
      UNTIL RecJobLedgEntries.NEXT = 0;

      Venster.UPDATE(1,'Inlezen journaalposten ' + "Account No." + ' ' + "Global Dimension 1");

      RecGenLedgEntries.SETCURRENTKEY("G/L Account No.", "Global Dimension 1 Code", "Global Dimension 2 Code", "Posting Date");
      RecGenLedgEntries.SETRANGE("G/L Account No.","Account No.");
      RecGenLedgEntries.SETFILTER("Global Dimension 1 Code","Global Dimension 1");
      RecGenLedgEntries.SETFILTER("Posting Date",GETFILTER("Date Filter"));
      IF RecGenLedgEntries.FIND('-') THEN
      REPEAT
        Venster.UPDATE(2,RecGenLedgEntries."Document No.");
        TmpDataJP.Source := TmpDataJP.Source::"G/L";
        TmpDataJP."Document No." := RecGenLedgEntries."Document No.";
        TmpDataJP.Date := RecGenLedgEntries."Posting Date";
        TmpDataJP.Description := RecGenLedgEntries.Description;
        TmpDataJP."Project No." := RecGenLedgEntries."Job No.";
        TmpDataJP."Service Order No." := RecGenLedgEntries."Service Order No.";
        TmpDataJP."Service Contract No." := RecGenLedgEntries."Service Contract No.";
        TmpDataJP.Amount := ROUND(RecGenLedgEntries.Amount);
        TmpDataJP."Entry No." := RecGenLedgEntries."Entry No.";
        TmpDataJP."Account No." := RecGenLedgEntries."G/L Account No.";
        TmpDataJP.INSERT;
      UNTIL RecGenLedgEntries.NEXT = 0;

      //****************************************************************
      //Delete equal records from temp journal entries table and
      //mark rest of records in temp project entries table.
      //Records with zero amounts will be compared and if necessary
      //deleted during deleting of compressed journal entries
      //****************************************************************
      Venster.UPDATE(1,Text001);
      IF TmpDataPP.FIND('-') THEN
      REPEAT
        IF TmpDataPP.Amount <> 0 THEN
        BEGIN
          TmpDataJP.SETRANGE(Source,TmpDataPP.Source::"G/L");
          TmpDataJP.SETRANGE("Document No.",TmpDataPP."Document No.");
          TmpDataJP.SETRANGE(Date,TmpDataPP.Date);
          TmpDataJP.SETRANGE(Description,TmpDataPP.Description);
          TmpDataJP.SETRANGE("Project No.",TmpDataPP."Project No.");
          TmpDataJP.SETRANGE(Amount,TmpDataPP.Amount);
          IF TmpDataJP.FIND('-') THEN
          BEGIN
            IF (TmpDataPP."Service Order No." <> TmpDataJP."Service Order No.") OR
               (TmpDataPP."Service Contract No." <> TmpDataJP."Service Contract No.") THEN
            BEGIN
              IF RecGenLedgEntries.GET(TmpDataJP."Entry No.") THEN
              BEGIN
                RecGenLedgEntries."Service Order No." := TmpDataPP."Service Order No.";
                RecGenLedgEntries."Service Contract No." := TmpDataPP."Service Contract No.";
                RecGenLedgEntries.MODIFY;
                COMMIT;
              END;
            END;
            TmpDataJP.DELETE;
          END
            ELSE TmpDataPP.MARK(TRUE);
        END;
      UNTIL TmpDataPP.NEXT = 0;

      //**************************************************
      //Delete compressed journal entries which are equal
      //**************************************************
      Venster.UPDATE(1,Text002);
      Klaar := FALSE;
      Totaal := 0;
      IF TmpDataPP.FIND('-') THEN
      REPEAT
        CurDocumentnr := TmpDataPP."Document No.";
        CurDatum := TmpDataPP.Date;
        CurOmschrijving := TmpDataPP.Description;
        CurProjectnr := TmpDataPP."Project No.";
        CurServiceOrdernr := TmpDataPP."Service Order No.";
        CurServiceContractnr := TmpDataPP."Service Contract No.";
        IF TmpDataPP.MARK = TRUE THEN
          Totaal += TmpDataPP.Amount;

        Klaar := TmpDataPP.NEXT = 0;

        IF Klaar OR
           (CurDocumentnr <> TmpDataPP."Document No.") OR
           (CurDatum <> TmpDataPP.Date) OR
           (CurOmschrijving <> TmpDataPP.Description) OR
           (CurProjectnr <> TmpDataPP."Project No.") THEN
        BEGIN
      //    IF Totaal <> 0 THEN
          BEGIN
            TmpDataJP.SETRANGE(Source,TmpDataPP.Source::"G/L");
            TmpDataJP.SETRANGE("Document No.",CurDocumentnr);
            TmpDataJP.SETRANGE(Date,CurDatum);
            TmpDataJP.SETRANGE(Description,CurOmschrijving);
            TmpDataJP.SETRANGE("Project No.",CurProjectnr);
            TmpDataJP.SETRANGE(Amount,Totaal);
            IF TmpDataJP.FIND('-') OR (Totaal = 0) THEN // One JP is equal to total PP's
            BEGIN
              IF TmpDataJP.FIND('-') THEN
              BEGIN
                IF (CurServiceOrdernr <> TmpDataJP."Service Order No.") OR
                   (CurServiceContractnr <> TmpDataJP."Service Contract No.") THEN
                BEGIN
                  IF RecGenLedgEntries.GET(TmpDataJP."Entry No.") THEN
                  BEGIN
                    RecGenLedgEntries."Service Order No." := CurServiceOrdernr;
                    RecGenLedgEntries."Service Contract No." := CurServiceContractnr;
                    RecGenLedgEntries.MODIFY;
                    COMMIT;
                  END;
                END;
                TmpDataJP.DELETE;
              END;
              IF TmpDataJP.FIND('-') THEN TmpDataJP.DELETE;
              WHILE (TmpDataPP.NEXT(-1) <> 0) AND
                    (CurDocumentnr = TmpDataPP."Document No.") AND
                    (CurDatum = TmpDataPP.Date) AND
                    (CurOmschrijving = TmpDataPP.Description) AND
                    (CurProjectnr = TmpDataPP."Project No.") DO
                  TmpDataPP.MARK(FALSE);
              WHILE (TmpDataPP.NEXT <> 0) AND
                    (CurDocumentnr = TmpDataPP."Document No.") AND
                    (CurDatum = TmpDataPP.Date) AND
                    (CurOmschrijving = TmpDataPP.Description) AND
                    (CurProjectnr = TmpDataPP."Project No.") DO;
            END
            ELSE
            BEGIN
              TmpDataJP.SETRANGE(Amount);
              JPTotaal := 0;
              IF TmpDataJP.FIND('-') THEN
              REPEAT
                JPTotaal += TmpDataJP.Amount;
              UNTIL TmpDataJP.NEXT = 0;
              IF JPTotaal = Totaal THEN // More JP's are equal to one PP
              BEGIN
                IF TmpDataJP.FIND('-') THEN
                REPEAT
                  IF (CurServiceOrdernr <> TmpDataJP."Service Order No.") OR
                     (CurServiceContractnr <> TmpDataJP."Service Contract No.") THEN
                  BEGIN
                    IF RecGenLedgEntries.GET(TmpDataJP."Entry No.") THEN
                    BEGIN
                      RecGenLedgEntries."Service Order No." := CurServiceOrdernr;
                      RecGenLedgEntries."Service Contract No." := CurServiceContractnr;
                      RecGenLedgEntries.MODIFY;
                      COMMIT;
                    END;
                  END;
                UNTIL TmpDataJP.NEXT = 0;

                TmpDataJP.DELETEALL;
                WHILE (TmpDataPP.NEXT(-1) <> 0) AND
                      (CurDocumentnr = TmpDataPP."Document No.") AND
                      (CurDatum = TmpDataPP.Date) AND
                      (CurOmschrijving = TmpDataPP.Description) AND
                      (CurProjectnr = TmpDataPP."Project No.") DO
                    TmpDataPP.MARK(FALSE);
                WHILE (TmpDataPP.NEXT <> 0) AND
                      (CurDocumentnr = TmpDataPP."Document No.") AND
                      (CurDatum = TmpDataPP.Date) AND
                      (CurOmschrijving = TmpDataPP.Description) AND
                      (CurProjectnr = TmpDataPP."Project No.") DO;
              END;
            END;
            Totaal := 0;
          END;
        END;
      UNTIL Klaar;

      //********************************
      //Write remainder to final table
      //********************************
      TmpDataPP.MARKEDONLY(TRUE);
      IF TmpDataPP.FIND('-') THEN
      REPEAT
        TblData.TRANSFERFIELDS(TmpDataPP);
        TblData.INSERT;
      UNTIL TmpDataPP.NEXT = 0;

      TmpDataJP.RESET;
      IF TmpDataJP.FIND('-') THEN
      REPEAT
        TblData.TRANSFERFIELDS(TmpDataJP);
        TblData.INSERT;
      UNTIL TmpDataJP.NEXT = 0;

      COMMIT;

      Venster.CLOSE;
    END;

    PROCEDURE ApplyFilters@1100525004(lvPerGlobalDimension1Code@1100485001 : Boolean;lvRefDateFilter@1100485003 : Text[30]);
    BEGIN
      PerGlobalDimension1Code := lvPerGlobalDimension1Code;
      RefDateFilter := lvRefDateFilter;
      EvaluateReferenceFilter;
      CurrPage.UPDATE;
    END;

    PROCEDURE EvaluateReferenceFilter@1100485003();
    BEGIN
      SETRANGE("Global Dimension 1 Filter");
      IF RefDateFilter <> '' THEN BEGIN
        SETFILTER("Date Filter", RefDateFilter);
      END ELSE BEGIN
        SETRANGE("Date Filter");
      END;
    END;

    BEGIN
    END.
  }
}

