OBJECT Table 11072451 Document Setup Peripheral App
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CheckRecord;
               "Line No." := GetLastLineNo + 10000;
             END;

    OnModify=BEGIN
               CheckRecord;
             END;

    OnRename=BEGIN
               CheckRecord;
             END;

    CaptionML=ENU=Document Setup Peripheral Application;
    LookupPageID=Page11127390;
    DrillDownPageID=Page11127390;
  }
  FIELDS
  {
    { 10  ;   ;Application         ;Option        ;DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Peripheral Application;
                                                   OptionCaptionML=ENU=" ,,Control";
                                                   OptionString=[ ,OSA,CNT];
                                                   NotBlank=Yes }
    { 20  ;   ;Entity              ;Option        ;DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Entity;
                                                   OptionCaptionML=[ENU=" ,,,,,,,,,,,,,,,Quote,WorkOrder,Call";
                                                                    SVE=" ,,,,,,,,,,,,,,,Offert,Arbetsorder,Samtal"];
                                                   OptionString=[ ,Project,Element,Plot,ChecklistCopyLine,Issue,IssueBasedOnChecklistCopyLine,,,,,,,,,Quote,WorkOrder,Call];
                                                   NotBlank=Yes }
    { 30  ;   ;Line No.            ;Integer       ;DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Line No.;
                                                              SVE=Radnr];
                                                   Editable=No }
    { 100 ;   ;Document Portal Subsite;Code20     ;TableRelation="DPT Default Subsite".Code WHERE (Source Type=CONST(Project),
                                                                                                   Quote Request Subsite=CONST(No));
                                                   OnValidate=BEGIN
                                                                "Document Type" := '';
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Document Portal Subsite (Dual) }
    { 110 ;   ;Document Type       ;Code10        ;TableRelation=IF (Document Portal Subsite=FILTER('')) "Document Type".Type
                                                                 ELSE "DPT Default Subsite Doc Type"."Document Type" WHERE (Source Type=CONST(Project),
                                                                                                                            Subsite Code=FIELD(Document Portal Subsite));
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Document Type;
                                                              SVE=Dokumenttyp];
                                                   NotBlank=Yes }
    { 120 ;   ;Default Value Auto Processes;Boolean;
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Default Value for Automated Processes }
  }
  KEYS
  {
    {    ;Application,Entity,Line No.             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      RecordAlreadyExistsTxt@1100528300 : TextConst 'ENU=Record already exists.';

    PROCEDURE AddDocumentSetupToProject@1100528303(ProjectNo@1100528300 : Code[20]);
    VAR
      DocumentSetupPeripheralApp@1100528302 : Record 11072451;
      DPTDocSetupPeripheralApp@1100528301 : Record 11072452;
      LastLineNo@1100528303 : Integer;
    BEGIN
      IF (NOT DocumentSetupPeripheralApp.FINDSET) THEN
        EXIT;

      REPEAT
        DPTDocSetupPeripheralApp.SETRANGE("Project No.", ProjectNo);
        DPTDocSetupPeripheralApp.SETRANGE(Application, DocumentSetupPeripheralApp.Application);
        DPTDocSetupPeripheralApp.SETRANGE(Entity, DocumentSetupPeripheralApp.Entity);
        IF (DPTDocSetupPeripheralApp.ISEMPTY) THEN BEGIN
          DPTDocSetupPeripheralApp.INIT;
          DPTDocSetupPeripheralApp."Project No." := ProjectNo;
          DPTDocSetupPeripheralApp.Application := DocumentSetupPeripheralApp.Application;
          DPTDocSetupPeripheralApp.Entity := DocumentSetupPeripheralApp.Entity;
          DPTDocSetupPeripheralApp."Line No." := DPTDocSetupPeripheralApp.GetLastLineNo + 10000;
          DPTDocSetupPeripheralApp."Document Portal Subsite" := DocumentSetupPeripheralApp."Document Portal Subsite";
          DPTDocSetupPeripheralApp."Document Type" := DocumentSetupPeripheralApp."Document Type";
          DPTDocSetupPeripheralApp."Default Value Auto Processes" := DocumentSetupPeripheralApp."Default Value Auto Processes";
          DPTDocSetupPeripheralApp.INSERT;
        END;
      UNTIL (DocumentSetupPeripheralApp.NEXT = 0);
    END;

    PROCEDURE AddRecord@1100528305(ApplicationOption@1100528301 : Option;EntityOption@1100528302 : Option);
    VAR
      DocumentSetupPeripheralApp@1100528300 : Record 11072451;
    BEGIN
      DocumentSetupPeripheralApp.SETRANGE(Application, ApplicationOption);
      DocumentSetupPeripheralApp.SETRANGE(Entity, EntityOption);
      IF (NOT DocumentSetupPeripheralApp.ISEMPTY) THEN
        EXIT;

      DocumentSetupPeripheralApp.INIT;
      DocumentSetupPeripheralApp.Application := ApplicationOption;
      DocumentSetupPeripheralApp.Entity := EntityOption;
      DocumentSetupPeripheralApp."Line No." := GetLastLineNo + 10000;
      DocumentSetupPeripheralApp.INSERT;
    END;

    PROCEDURE GetLastLineNo@1100528302() : Integer;
    VAR
      DocumentSetupPeripheralApp@1100528300 : Record 11072451;
    BEGIN
      DocumentSetupPeripheralApp.SETRANGE(Application, Application);
      DocumentSetupPeripheralApp.SETRANGE(Entity, Entity);
      IF (DocumentSetupPeripheralApp.FINDLAST) THEN
        EXIT(DocumentSetupPeripheralApp."Line No.");
      EXIT(0);
    END;

    PROCEDURE Initialize@1100528304();
    BEGIN
      {
      AddRecord(1, 1);
      AddRecord(1, 2);
      AddRecord(1, 3);
      AddRecord(1, 4);
      AddRecord(1, 5);
      AddRecord(1, 6);
      }
      AddRecord(2, 15);
      AddRecord(2, 16);
      AddRecord(2, 17);
    END;

    LOCAL PROCEDURE "---Locals---"@1100528301();
    BEGIN
    END;

    LOCAL PROCEDURE CheckRecord@1100528300();
    BEGIN
      CASE Application OF
        Application::CNT: BEGIN
          IF NOT (Entity IN [Entity::Call, Entity::Quote, Entity::WorkOrder]) THEN
            FIELDERROR(Entity);
        END;
        Application::OSA: BEGIN
          IF NOT (Entity IN [Entity::ChecklistCopyLine, Entity::Element, Entity::Issue,
                  Entity::IssueBasedOnChecklistCopyLine, Entity::Plot, Entity::Project])
          THEN
            FIELDERROR(Entity);
        END;
      END;
    END;

    BEGIN
    END.
  }
}

