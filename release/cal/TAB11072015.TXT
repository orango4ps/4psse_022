OBJECT Table 11072015 Base Calendar Change
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW19.00,4PS14.00 (7601);
  }
  PROPERTIES
  {
    DataCaptionFields=Base Calendar Code;
    OnInsert=BEGIN
               CheckEntryLine;
               CheckAbsenceEmployee;  //**4PS.n
             END;

    OnModify=BEGIN
               CheckEntryLine;
               CheckAbsenceEmployee;  //**4PS.n
             END;

    OnDelete=BEGIN
               Nonworking := FALSE;   //**4PS.n
               CheckAbsenceEmployee;  //**4PS.n
             END;

    OnRename=BEGIN
               CheckEntryLine;
             END;

    CaptionML=[ENU=Base Calendar Change;
               NOR=Hovedkalenderendring;
               SVE=Baskalender„ndring];
  }
  FIELDS
  {
    { 1   ;   ;Base Calendar Code  ;Code10        ;TableRelation="Base Calendar";
                                                   CaptionML=[ENU=Base Calendar Code;
                                                              NOR=Hovedkalenderkode;
                                                              SVE=Baskalenderkod];
                                                   Editable=No }
    { 2   ;   ;Recurring System    ;Option        ;OnValidate=BEGIN
                                                                IF "Recurring System" <> xRec."Recurring System" THEN
                                                                  CASE "Recurring System" OF
                                                                    "Recurring System"::"Annual Recurring":
                                                                      Day := Day::" ";
                                                                    "Recurring System"::"Weekly Recurring":
                                                                      Date := 0D;
                                                                  END;
                                                              END;

                                                   CaptionML=[ENU=Recurring System;
                                                              NOR=Gjentakende system;
                                                              SVE=terkommande system];
                                                   OptionCaptionML=[ENU=" ,Annual Recurring,Weekly Recurring";
                                                                    NOR=" ,rlig gjentakende,Ukentlig gjentakende";
                                                                    SVE=" ,rlig †terkommande,Veckovis †terkommande"];
                                                   OptionString=[ ,Annual Recurring,Weekly Recurring] }
    { 3   ;   ;Date                ;Date          ;OnValidate=BEGIN
                                                                IF ("Recurring System" = "Recurring System"::" ") OR
                                                                   ("Recurring System" = "Recurring System"::"Annual Recurring")
                                                                THEN
                                                                  TESTFIELD(Date)
                                                                ELSE
                                                                  TESTFIELD(Date,0D);
                                                                UpdateDayName;
                                                              END;

                                                   CaptionML=[ENU=Date;
                                                              NOR=Dato;
                                                              SVE=Datum] }
    { 4   ;   ;Day                 ;Option        ;OnValidate=BEGIN
                                                                IF "Recurring System" = "Recurring System"::"Weekly Recurring" THEN
                                                                  TESTFIELD(Day);
                                                                UpdateDayName;
                                                              END;

                                                   CaptionML=[ENU=Day;
                                                              NOR=Dag;
                                                              SVE=Dag];
                                                   OptionCaptionML=[ENU=" ,Monday,Tuesday,Wednesday,Thursday,Friday,Saturday,Sunday";
                                                                    NOR=" ,Mandag,Tirsdag,Onsdag,Torsdag,Fredag,L›rdag,S›ndag";
                                                                    SVE=" ,M†ndag,Tisdag,Onsdag,Torsdag,Fredag,L”rdag,S”ndag"];
                                                   OptionString=[ ,Monday,Tuesday,Wednesday,Thursday,Friday,Saturday,Sunday] }
    { 5   ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 6   ;   ;Nonworking          ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[ENU=Nonworking;
                                                              NOR=Ikke arbeidsdag;
                                                              SVE=Ej arbetsdag] }
    { 11012000;;Cause of Absence Code;Code10      ;TableRelation="Cause of Absence";
                                                   CaptionML=[ENU=Cause of Absence Code;
                                                              NOR=Kode frav‘rs†rsak;
                                                              SVE=Kod f”r Orsak till fr†nvaro] }
  }
  KEYS
  {
    {    ;Base Calendar Code,Recurring System,Date,Day;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    LOCAL PROCEDURE UpdateDayName@1();
    VAR
      DateTable@1000 : Record 2000000007;
    BEGIN
      IF (Date > 0D) AND
         ("Recurring System" = "Recurring System"::"Annual Recurring")
      THEN
        Day := Day::" "
      ELSE BEGIN
        DateTable.SETRANGE("Period Type",DateTable."Period Type"::Date);
        DateTable.SETRANGE("Period Start",Date);
        IF DateTable.FINDFIRST THEN
          Day := DateTable."Period No.";
      END;
      IF (Date = 0D) AND (Day = Day::" ") THEN BEGIN
        Day := xRec.Day;
        Date := xRec.Date;
      END;
      IF "Recurring System" = "Recurring System"::"Annual Recurring" THEN
        TESTFIELD(Day,Day::" ");
    END;

    LOCAL PROCEDURE CheckEntryLine@2();
    BEGIN
      CASE "Recurring System" OF
        "Recurring System"::" ":
          BEGIN
            TESTFIELD(Date);
            TESTFIELD(Day);
          END;
        "Recurring System"::"Annual Recurring":
          BEGIN
            TESTFIELD(Date);
            TESTFIELD(Day,Day::" ");
          END;
        "Recurring System"::"Weekly Recurring":
          BEGIN
            TESTFIELD(Date,0D);
            TESTFIELD(Day);
          END;
      END;
    END;

    PROCEDURE CheckAbsenceEmployee@1210190000();
    VAR
      EmplRec@1100485001 : Record 5200;
      EmplAbsenceRec@1100485000 : Record 5207;
    BEGIN
      //**4PS
      //db, 14-06-05: functon moved from form 7604 because of not processing a change on standard form 7602

      EmplAbsenceRec.SETCURRENTKEY("From Date", "To Date");
      EmplAbsenceRec.SETRANGE("From Date", Date);
      EmplAbsenceRec.SETRANGE("Calendar Entry", TRUE);
      EmplAbsenceRec.DELETEALL(TRUE);

      IF Nonworking = FALSE THEN EXIT;

      IF Date <> 0D THEN BEGIN  //db, 26-09-05 (for recurring absence like saterday/sunday)
        //EmplRec.SETFILTER(Schedule, '<>%1', EmplRec.Schedule::No);
        EmplRec.SETRANGE(Status, EmplRec.Status::Active);  //db, 29-12-06
        IF EmplRec.FIND('-') THEN BEGIN
          REPEAT
            EmplAbsenceRec.INIT;
            EmplAbsenceRec."Employee No." := EmplRec."No.";
            EmplAbsenceRec."From Date" := Date;
            EmplAbsenceRec."To Date" := Date;
            IF "Cause of Absence Code" <> '' THEN
              EmplAbsenceRec.VALIDATE("Cause of Absence Code", "Cause of Absence Code");
            EmplAbsenceRec.Description := Description;
            EmplAbsenceRec."Calendar Entry" := TRUE;
            EmplAbsenceRec.INSERT(TRUE);  //generates "Entry No" and updates planning
          UNTIL EmplRec.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE InitAbsenceEmployee@1100485000(EmplRec@1100485005 : Record 5200);
    VAR
      CompInfoRec@1100485001 : Record 79;
      EmplAbsenceRec@1100485003 : Record 5207;
      BaseCalRec@1100485004 : Record 11072015;
    BEGIN
      //**4PS
      CompInfoRec.GET;

      BaseCalRec.SETRANGE("Base Calendar Code", CompInfoRec."Base Calendar Code");
      BaseCalRec.SETFILTER(Date, '<>%1', 0D);
      IF BaseCalRec.FIND('-') THEN BEGIN
        REPEAT
          EmplAbsenceRec.SETRANGE("Employee No.", EmplRec."No.");
          EmplAbsenceRec.SETRANGE("From Date", BaseCalRec.Date);
          EmplAbsenceRec.SETRANGE("Calendar Entry", TRUE);
          EmplAbsenceRec.DELETEALL(TRUE);
        UNTIL BaseCalRec.NEXT = 0;
      END;

      //IF EmplRec.Schedule = EmplRec.Schedule::No THEN EXIT;
      IF EmplRec.Status <> EmplRec.Status::Active THEN EXIT;  //db, 29-12-06

      BaseCalRec.SETRANGE("Base Calendar Code", CompInfoRec."Base Calendar Code");
      BaseCalRec.SETFILTER(Date, '<>%1', 0D);
      IF BaseCalRec.FIND('-') THEN BEGIN
        REPEAT
          EmplAbsenceRec.INIT;
          EmplAbsenceRec."Employee No." := EmplRec."No.";
          EmplAbsenceRec."From Date" := BaseCalRec.Date;
          EmplAbsenceRec."To Date" := BaseCalRec.Date;
          IF BaseCalRec."Cause of Absence Code" <> '' THEN
            EmplAbsenceRec.VALIDATE("Cause of Absence Code", BaseCalRec."Cause of Absence Code");
          EmplAbsenceRec.Description := BaseCalRec.Description;
          EmplAbsenceRec."Calendar Entry" := TRUE;
          EmplAbsenceRec.INSERT(TRUE);  //generates "Entry No" and updates planning
        UNTIL BaseCalRec.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}

