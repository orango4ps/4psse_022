OBJECT Table 11229835 Project Element Feature
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Project Element Feature;
    LookupPageID=Page11126485;
    DrillDownPageID=Page11126485;
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr] }
    { 20  ;   ;Project Element     ;Code20        ;TableRelation="Project Element".Element WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=[ENU=Project Element;
                                                              NOR=Prosjektelement;
                                                              SVE=Projektelement] }
    { 30  ;   ;Feature             ;Code20        ;TableRelation="Feature (Project Element)";
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Unit of Measure");
                                                              END;

                                                   CaptionML=[ENU=Feature;
                                                              NOR=Funksjon;
                                                              SVE=Funktion] }
    { 40  ;   ;Feature Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Feature (Project Element)".Description WHERE (Code=FIELD(Feature)));
                                                   CaptionML=[ENU=Feature Description;
                                                              NOR=Funksjonsbeskrivelse;
                                                              SVE=Funktionsbeskrivning] }
    { 50  ;   ;Value Type          ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Feature (Project Element)"."Value Type" WHERE (Code=FIELD(Feature)));
                                                   CaptionML=[ENU=Field Type;
                                                              NOR=Felttype;
                                                              SVE=Field Type];
                                                   OptionCaptionML=[ENU=Date,Value,Text,Option,Yes/No;
                                                                    SVE=Datum,V„rde,Text,Alternativ,Ja/Nej];
                                                   OptionString=Date,Value,Text,Option,Yes/No;
                                                   Editable=No }
    { 60  ;   ;Value               ;Text80        ;OnValidate=BEGIN
                                                                EvaluateValue;
                                                              END;

                                                   OnLookup=VAR
                                                              ProjectElementFeatureOption@1100528800 : Record 11229836;
                                                              ProjectElementFeatureOptionPAGE@1100528801 : Page 11126535;
                                                            BEGIN
                                                              IF "Value Type" IN ["Value Type"::Option, "Value Type"::"Yes/No"] THEN BEGIN
                                                                ProjectElementFeatureOption.SETRANGE(Feature, Feature);
                                                                ProjectElementFeatureOptionPAGE.SETTABLEVIEW(ProjectElementFeatureOption);
                                                                ProjectElementFeatureOptionPAGE.EDITABLE := FALSE;
                                                                ProjectElementFeatureOptionPAGE.LOOKUPMODE := TRUE;
                                                                IF ProjectElementFeatureOptionPAGE.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                  ProjectElementFeatureOptionPAGE.GETRECORD(ProjectElementFeatureOption);
                                                                  VALIDATE(Value, ProjectElementFeatureOption.Description);
                                                                  MODIFY;
                                                                END;
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Value;
                                                              NOR=Verdi;
                                                              SVE=V„rde] }
    { 70  ;   ;Unit of Measure     ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Feature (Project Element)"."Unit Of Measure" WHERE (Code=FIELD(Feature)));
                                                   CaptionML=[ENU=Unit of Measure;
                                                              NOR=Enhet;
                                                              SVE=M†ttenhet];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Project No.,Project Element,Feature     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100528801 : TextConst 'ENU=Option Value %1 not found;NOR=Alternativverdi %1 ikke funnet;SVE=Alternativv„rde %1 hittades inte';
      Text002@1100528800 : TextConst 'ENU=t;NOR=t;SVE=t';

    PROCEDURE EvaluateValue@1100528600();
    VAR
      DummyDate@1100528600 : Date;
      DummyDecimal@1100528601 : Decimal;
    BEGIN
      IF Value = '' THEN EXIT;

      CASE "Value Type" OF
        "Value Type"::Date:
          BEGIN
            IF LOWERCASE(Value) = Text002 THEN
              Value := FORMAT(TODAY)
            ELSE BEGIN
              EVALUATE(DummyDate, Value);
              Value := FORMAT(DummyDate);
            END;
          END;
        "Value Type"::Value:
          BEGIN
            EVALUATE(DummyDecimal, Value);
            Value := FORMAT(DummyDecimal);
          END;
        "Value Type"::Text:;
        "Value Type"::Option,
        "Value Type"::"Yes/No":
          Value := CheckOptionValue;
      END;
    END;

    PROCEDURE CheckOptionValue@1100528603() : Text[80];
    VAR
      ProjectElementFeatureOption@1100528600 : Record 11229836;
    BEGIN
      ProjectElementFeatureOption.SETRANGE(Feature, Feature);
      ProjectElementFeatureOption.SETRANGE(Description, Value);
      IF ProjectElementFeatureOption.FINDFIRST THEN
        EXIT(ProjectElementFeatureOption.Description);

      ProjectElementFeatureOption.SETFILTER(Description, '@' + Value + '*');
      IF ProjectElementFeatureOption.COUNT = 1 THEN BEGIN
        ProjectElementFeatureOption.FINDFIRST;
        EXIT(ProjectElementFeatureOption.Description);
      END;
      ERROR(Text001, Value);
    END;

    BEGIN
    END.
  }
}

