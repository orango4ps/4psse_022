OBJECT Report 11012910 Document Update Email (PPT)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Belegaktualisierung per E-Mail senden;
               ENU=Send Document Update E-mail;
               NLD=Document update email versturen];
    UseSystemPrinter=No;
    ProcessingOnly=Yes;
    UseRequestPage=No;
  }
  DATASET
  {
    { 1100525000;;DataItem;                  ;
               DataItemTable=Table11229800;
               DataItemTableView=SORTING(Project No.,Seq. No.);
               OnPreDataItem=VAR
                               UserSetup@1100525000 : Record 91;
                             BEGIN
                               UserSetup.GET(USERID);
                               UserSetup.TESTFIELD("Employee No.");
                               CurrentEmployee.GET(UserSetup."Employee No.");
                               CurrentEmployee.TESTFIELD("Company E-Mail");
                             END;

               OnAfterGetRecord=VAR
                                  FileMgt@1100525002 : Codeunit 419;
                                BEGIN
                                  IsValidMail := FALSE;
                                  ServerFileName := FileMgt.ServerTempFileName('txt');
                                  ClientFileName := FileMgt.ClientTempFileName('txt');

                                  CLEAR(FileOut);
                                  FileOut.WRITEMODE(TRUE);
                                  FileOut.TEXTMODE(TRUE);
                                  FileOut.CREATE(ServerFileName);

                                  CreateAndSendMail;
                                  FileOut.CLOSE;
                                  FileMgt.DownloadToFile(ServerFileName, ClientFileName);

                                  ReportSuccessful := TRUE;
                                  IF (IsValidMail) THEN
                                    IF (NOT SMTPMail.RUN) THEN
                                      ReportSuccessful := FALSE;
                                END;

               ReqFilterFields=Project No.,Creation Date }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      CurrentEmployee@1100525000 : Record 5200;
      SMTPMail@1100525007 : Codeunit 400;
      IsValidMail@1100525001 : Boolean;
      Text000@1100525003 : TextConst 'DEU=Projektportal:;ENU=Project Portal:;NLD="Projectportaal: "';
      Text001@1100525004 : TextConst 'DEU=Belege;ENU=Documents;NLD=Documentoverzicht;NOR=Dokumenter;SVE=Dokument';
      Text002@1100525006 : TextConst 'DEU=(Gesamt %1 Belege);ENU=(Total %1 Documents);NLD=(Totaal %1 documenten)';
      ServerFileName@1100525008 : Text[250];
      ClientFileName@1100525002 : Text[250];
      FileOut@1100525009 : File;
      ReportSuccessful@1100409000 : Boolean;

    LOCAL PROCEDURE CreateAndSendMail@1100525001();
    VAR
      DocumentUpdateMemberPPT@1100525000 : Record 11229801;
      DocumentUpdateDocumentPPT@1100525001 : Record 11229802;
    BEGIN
      SMTPMail.CreateMessage(
        CurrentEmployee."Full Name",
        CurrentEmployee."Company E-Mail",
         CurrentEmployee."Company E-Mail",
        "Document Update (PPT)".Description,
        '', TRUE);

      DocumentUpdateMemberPPT.SETRANGE("Project No.", "Document Update (PPT)"."Project No.");
      DocumentUpdateMemberPPT.SETRANGE("Seq. No.", "Document Update (PPT)"."Seq. No.");
      IF (NOT DocumentUpdateMemberPPT.FINDSET) THEN
        EXIT;
      AddReceipients(DocumentUpdateMemberPPT);

      AddText(1); //StartText
      AddBodyText('<br>');

      DocumentUpdateDocumentPPT.SETRANGE("Project No.", "Document Update (PPT)"."Project No.");
      DocumentUpdateDocumentPPT.SETRANGE("Seq. No.", "Document Update (PPT)"."Seq. No.");
      IF (NOT DocumentUpdateDocumentPPT.FINDSET) THEN
        EXIT;
      AddDocuments(DocumentUpdateDocumentPPT);

      AddBodyText('<br>');
      AddText(2); //EndText

      IsValidMail := (DocumentUpdateMemberPPT.COUNT > 0) AND (DocumentUpdateDocumentPPT.COUNT > 0);
    END;

    LOCAL PROCEDURE AddReceipients@1100525002(VAR DocumentUpdateMemberPPT@1100525000 : Record 11229801);
    VAR
      Employee@1100525002 : Record 5200;
      Contact@1100525003 : Record 5050;
    BEGIN
      REPEAT
        CASE DocumentUpdateMemberPPT.Type OF
          DocumentUpdateMemberPPT.Type::"Responsible Person": BEGIN
            Employee.GET(DocumentUpdateMemberPPT."Employee No.");
            IF (Employee."No." <> CurrentEmployee."No.") THEN
              SMTPMail.AddBCC(Employee."Company E-Mail")
          END;
          DocumentUpdateMemberPPT.Type::Authority: BEGIN
            IF (DocumentUpdateMemberPPT."Contact Person No." <> '') THEN
              Contact.GET(DocumentUpdateMemberPPT."Contact Person No.")
            ELSE
              Contact.GET(DocumentUpdateMemberPPT."Contact No.");
            SMTPMail.AddBCC(Contact."E-Mail 2");
          END;
        END;
      UNTIL (DocumentUpdateMemberPPT.NEXT = 0);
    END;

    LOCAL PROCEDURE AddText@1100525003(Type@1100525000 : Option);
    VAR
      DocumentUpdateTextPPT@1100525002 : Record 11229803;
      BodyText@1100525003 : Text;
    BEGIN
      DocumentUpdateTextPPT.SETRANGE("Project No.", "Document Update (PPT)"."Project No.");
      DocumentUpdateTextPPT.SETRANGE("Seq. No.", "Document Update (PPT)"."Seq. No.");
      DocumentUpdateTextPPT.SETRANGE(Type, Type);
      IF (NOT DocumentUpdateTextPPT.FINDSET) THEN
        EXIT;

      REPEAT
        BodyText := DocumentUpdateTextPPT.Comment + ' ';
        IF (DocumentUpdateTextPPT."Line Break") THEN
          BodyText += '<br>';
        AddBodyText(BodyText);
      UNTIL (DocumentUpdateTextPPT.NEXT = 0);
    END;

    LOCAL PROCEDURE AddDocuments@1100525004(VAR DocumentUpdateDocumentPPT@1100525000 : Record 11229802);
    VAR
      DocumentProperties@1100525002 : Record 11012746;
      Job@1100525003 : Record 11072003;
      BodyText@1100525004 : Text;
      Counter@1100525005 : Integer;
    BEGIN
      Job.GET("Document Update (PPT)"."Project No.");
      BodyText := '<b>'+ STRSUBSTNO(Text000) + '</b>'+
        ' <a href="' + Job."SharePoint Site URL" + '">' + Job.Description + '</a><br>';
      AddBodyText(BodyText);
      BodyText := '<b>' + "Document Update (PPT)".FIELDCAPTION("Cover Letter") + ': </b>' +
        "Document Update (PPT)"."Cover Letter" + '<br>';
      AddBodyText(BodyText);
      BodyText := '<br><b>' + STRSUBSTNO(Text001) +'</b><br>';
      AddBodyText(BodyText);

      AddBodyText('<table border="1">');

      BodyText := '<tr>';
      BodyText += '<th></th>';
      BodyText += '<th align=left>' + DocumentUpdateDocumentPPT.FIELDCAPTION(Description) + '</th>';
      BodyText += '<th align=right>' + DocumentUpdateDocumentPPT.FIELDCAPTION(Version) + '</th>';
      BodyText += '</tr>';
      AddBodyText(BodyText);

      REPEAT
        Counter += 1;
        DocumentProperties.GET(DocumentUpdateDocumentPPT."Document No.");

        BodyText := '<tr>';
        BodyText += '<td align=right>' + FORMAT(Counter) + '.</td>';
        IF (DocumentUpdateDocumentPPT."Web Link" <> '') THEN
          BodyText += '<td align=left><a href="' + DocumentUpdateDocumentPPT."Web Link" + '">' +
            DocumentProperties.Description + '</a></td>'
        ELSE
          BodyText += '<td align=left>' + DocumentProperties.Description + '</td>';
        BodyText += '<td align=right>' + DocumentUpdateDocumentPPT.Version + '</td>';
        BodyText += '</tr>';
        AddBodyText(BodyText);
      UNTIL (DocumentUpdateDocumentPPT.NEXT = 0);

      AddBodyText('</table>');

      BodyText := STRSUBSTNO(Text002, Counter) + '<br>';
      AddBodyText(BodyText);
    END;

    LOCAL PROCEDURE AddBodyText@1100525000(BodyText@1100525000 : Text[1024]);
    BEGIN
      SMTPMail.AppendBody(BodyText);
      FileOut.WRITE(BodyText);
    END;

    PROCEDURE GetFileName@1100528301() : Text[250];
    BEGIN
      EXIT(ClientFileName);
    END;

    PROCEDURE IsReportSuccessful@1100409000() : Boolean;
    BEGIN
      EXIT(ReportSuccessful);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

