OBJECT Table 11012312 Discount Term History
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ValidateDiscountType;

               IF FPSLicenseManagement.LicenseAndReadPermissionForTable(CompRelationRec) THEN BEGIN
                 IF "Discount Level" = "Discount Level"::Company THEN
                   CentrManagementCU.EditDiscHist(Rec,1,xRec);
                 IF "Discount Level" = "Discount Level"::Customer THEN BEGIN
                   InvtSetup.GET;
                   IF InvtSetup."Discount Term Groups" THEN
                     CentrManagementCU.EditDiscHist(Rec,1,xRec);
                 END;
               END;
             END;

    OnModify=BEGIN
               ValidateDiscountType;

               IF FPSLicenseManagement.LicenseAndReadPermissionForTable(CompRelationRec) THEN BEGIN
                 IF "Discount Level" = "Discount Level"::Company THEN
                   CentrManagementCU.EditDiscHist(Rec,2,xRec);
                 IF "Discount Level" = "Discount Level"::Customer THEN BEGIN
                   InvtSetup.GET;
                   IF InvtSetup."Discount Term Groups" THEN
                     CentrManagementCU.EditDiscHist(Rec,2,xRec);
                 END;
               END;
             END;

    OnDelete=BEGIN
               IF FPSLicenseManagement.LicenseAndReadPermissionForTable(CompRelationRec) THEN BEGIN
                 IF "Discount Level" = "Discount Level"::Company THEN
                   CentrManagementCU.EditDiscHist(Rec,4,xRec);
                 IF "Discount Level" = "Discount Level"::Customer THEN BEGIN
                   InvtSetup.GET;
                   IF InvtSetup."Discount Term Groups" THEN
                     CentrManagementCU.EditDiscHist(Rec,4,xRec);
                 END;
               END;
             END;

    OnRename=BEGIN
               ValidateDiscountType;
             END;

    CaptionML=[ENU=Discount Term History;
               NOR=Rabattvilk†rshistorikk;
               SVE=Rabattvillkorshistorik];
    LookupPageID=Page11020421;
  }
  FIELDS
  {
    { 1   ;   ;Vendor              ;Code15        ;TableRelation="ETIM Vendor - Central Mgt".Code;
                                                   CaptionML=[ENU=Vendor;
                                                              NOR=Leverand›r;
                                                              SVE=Leverant”r] }
    { 2   ;   ;Discount Group      ;Code35        ;OnValidate=VAR
                                                                DiscHistRec@1100525000 : Record 11012312;
                                                              BEGIN
                                                                IF xRec."Discount Group" <> "Discount Group" THEN BEGIN
                                                                  DiscHistRec := xRec;
                                                                  IF DiscHistRec.FIND THEN
                                                                    CentrManagementCU.EditDiscHist(Rec,3,xRec);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Discount Group;
                                                              NOR="Rabattgruppe ";
                                                              SVE=Rabattgrupp] }
    { 3   ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 4   ;   ;Discount Percentage 1;Decimal      ;CaptionML=[ENU=Discount Percentage 1;
                                                              NOR=Rabattprosent 1;
                                                              SVE=Rabattprocent 1];
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 5   ;   ;Discount Percentage 2;Decimal      ;CaptionML=[ENU=Discount Percentage 2;
                                                              NOR=Rabattprosent 2;
                                                              SVE=Rabattprocent 2];
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 6   ;   ;Discount Percentage 3;Decimal      ;CaptionML=[ENU=Discount Percentage 3;
                                                              NOR=Rabattprosent 3;
                                                              SVE=Rabattprocent 3];
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 7   ;   ;Item Code           ;Code20        ;TableRelation="Trade Item"."Item Code" WHERE (Vendor=FIELD(Vendor));
                                                   OnValidate=VAR
                                                                DiscHistRec@1100525000 : Record 11012312;
                                                              BEGIN
                                                                IF xRec."Item Code" <> "Item Code" THEN BEGIN
                                                                  DiscHistRec := xRec;
                                                                  IF DiscHistRec.FIND THEN
                                                                    CentrManagementCU.EditDiscHist(Rec,3,xRec);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Item Code;
                                                              NOR=Artikkelkode;
                                                              SVE=Artikelkod] }
    { 8   ;   ;Purchase Price      ;Decimal       ;CaptionML=[ENU=Purchase Price;
                                                              NOR=Innkj›pspris;
                                                              SVE=Ink”pspris];
                                                   AutoFormatType=2 }
    { 9   ;   ;Vendor Name         ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ETIM Vendor - Central Mgt".Name WHERE (Code=FIELD(Vendor)));
                                                   CaptionML=[ENU=Vendor Name;
                                                              NOR=Leverand›rnavn;
                                                              SVE=Leverant”rsnamn];
                                                   Editable=No }
    { 10  ;   ;Currency            ;Code10        ;TableRelation=Currency.Code;
                                                   CaptionML=[ENU=Currency;
                                                              NOR=Valuta;
                                                              SVE=Valuta] }
    { 11  ;   ;Sales Discount Group;Code20        ;TableRelation="Item Discount Group";
                                                   CaptionML=[ENU=Sales Discount Group;
                                                              NOR=Salgsrabattgruppe;
                                                              SVE=F”rs„ljningsrabattgrupp] }
    { 12  ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                TESTFIELD("Starting Date");
                                                              END;

                                                   CaptionML=[ENU=Starting Date;
                                                              NOR=Startdato;
                                                              SVE=Startdatum] }
    { 15  ;   ;Ending Date         ;Date          ;CaptionML=[ENU=Ending Date;
                                                              NOR=Sluttdato;
                                                              SVE=Slutdatum] }
    { 20  ;   ;Maximum % Price Difference;Decimal ;CaptionML=[ENU=Maximum % Price Difference;
                                                              NOR=Maksimal % Prisdifferanse;
                                                              SVE=Maximal % prisdifferens] }
    { 100 ;   ;Discount Level      ;Option        ;CaptionML=[ENU=Discount Level;
                                                              NOR=Rabattniv†;
                                                              SVE=Rabattniv†];
                                                   OptionCaptionML=[ENU=Company,Customer,Job;
                                                                    NOR=Selskap,Kunde,Jobb;
                                                                    SVE=F”retag,Kund,Jobb];
                                                   OptionString=Company,Customer,Job }
    { 110 ;   ;Discount Reference  ;Code20        ;TableRelation="Discount Term Group".Code WHERE (Level=FIELD(Discount Level));
                                                   CaptionML=[ENU=Discount Reference;
                                                              NOR=Rabattreferanse;
                                                              SVE=Rabattreferens] }
    { 120 ;   ;Sales Discount Percentage;Decimal  ;CaptionML=[ENU=Sales Discount Percentage;
                                                              NOR=Salgsrabattprosent;
                                                              SVE=F”rs„ljningsrabattprocent];
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 130 ;   ;Sales Price         ;Decimal       ;CaptionML=[ENU=Sales Price;
                                                              NOR=Salgspris;
                                                              SVE=F”rs„ljningspris];
                                                   AutoFormatType=2 }
    { 140 ;   ;Delivery Group Present;Boolean     ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Delivery Group" WHERE (Vendor=FIELD(Vendor),
                                                                                             Code=FIELD(Discount Group)));
                                                   CaptionML=[ENU=Delivery Group Present;
                                                              NOR=Leveransegruppe finnes;
                                                              SVE=Leveransgrupp finns];
                                                   Editable=No }
    { 150 ;   ;Item Description    ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Trade Item"."Item Description" WHERE (Vendor=FIELD(Vendor),
                                                                                                             Item Code=FIELD(Item Code)));
                                                   CaptionML=[ENU=Item Description;
                                                              NOR=Artikkelbeskrivelse;
                                                              SVE=Artikelbeskrivning];
                                                   Editable=No }
    { 155 ;   ;Price Agreement (Purchase);Boolean ;DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Price Agreement (Purchase) }
    { 160 ;   ;Price Agreement (Sales);Boolean    ;CaptionML=ENU=Price Agreement (Sales) }
    { 170 ;   ;Kilogram Price      ;Decimal       ;DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Kilogram Price;
                                                   AutoFormatType=2 }
  }
  KEYS
  {
    {    ;Vendor,Discount Group,Item Code,Discount Level,Discount Reference,Starting Date;
                                                   Clustered=Yes }
    {    ;Discount Level,Discount Reference,Vendor,Discount Group,Item Code,Starting Date }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      CentrManagementCU@1100485001 : Codeunit 11012010;
      CompRelationRec@1100485000 : Record 11012059;
      InvtSetup@1100485002 : Record 313;
      FPSLicenseManagement@1100527400 : Codeunit 11229289;

    PROCEDURE GetPreviousData@1100485001(VAR DiscHistRec@1100485000 : Record 11012312;StartDate@1100485002 : Date);
    VAR
      DiscHistRec2@1100485001 : Record 11012312;
    BEGIN
      //used in dataport to get default (as cost object, sales discount group) from previous import
      WITH DiscHistRec DO BEGIN
        DiscHistRec2.COPY(DiscHistRec);
        DiscHistRec2.SETFILTER("Starting Date", '<%1', StartDate);
        IF DiscHistRec2.FINDLAST THEN
          DiscHistRec := DiscHistRec2
        ELSE
          DiscHistRec.INIT;
      END;
    END;

    PROCEDURE GetPurchaseDiscount@1100525000() PurchDisc : Decimal;
    BEGIN
      PurchDisc := "Discount Percentage 1";
      IF PurchDisc = 0 THEN BEGIN  //C047823
        PurchDisc := "Discount Percentage 2";
      END ELSE BEGIN
        IF "Discount Percentage 2" <> 0 THEN
          PurchDisc := PurchDisc + ((100 - PurchDisc) * ("Discount Percentage 2" / 100));
      END;
      IF PurchDisc = 0 THEN BEGIN  //C047823
        PurchDisc := "Discount Percentage 3";
      END ELSE BEGIN
        IF "Discount Percentage 3" <> 0 THEN
          PurchDisc := PurchDisc + ((100 - PurchDisc) * ("Discount Percentage 3" / 100));
      END;

      EXIT(PurchDisc)
    END;

    PROCEDURE ValidateDiscountType@1100525001();
    BEGIN
      IF "Discount Group" <> '' THEN
        TESTFIELD("Item Code", '');
      IF "Item Code" <> '' THEN
        TESTFIELD("Discount Group", '');

      TESTFIELD("Starting Date");

      IF "Discount Level" <> "Discount Level"::Company THEN BEGIN
        TESTFIELD("Sales Discount Group", '');
        TESTFIELD("Maximum % Price Difference", 0);
      END;
    END;

    PROCEDURE CheckChangeAllowed@1100525004();
    VAR
      ChangeAllowed@1100525001 : Boolean;
      lvText001@1100525002 : TextConst 'ENU=Insert, change or delete not allowed due to central management.;NOR=Sett inn, endre eller fjerne er ikke tillatt p† grunn av sentral ledelse.;SVE=Infoga, „ndra eller ta bort „r inte till†tet p† grund av central hantering.';
      LeadingCompany@1210190000 : Text[50];
      CompRelationRec@1210190001 : Record 11012059;
    BEGIN
      ChangeAllowed := TRUE;
      InvtSetup.GET;
      LeadingCompany := CompRelationRec.GetLeadingCompany(DATABASE::"Inventory Setup",InvtSetup."Central Management");
      IF LeadingCompany <> COMPANYNAME THEN BEGIN
        CASE "Discount Level" OF
          "Discount Level"::Company: ChangeAllowed := (InvtSetup."Discount Terms" = FALSE);
          "Discount Level"::Customer: ChangeAllowed := (InvtSetup."Discount Term Groups" = FALSE);
        END;
      END;
      IF ChangeAllowed = FALSE THEN
        ERROR(lvText001);
    END;

    PROCEDURE GetPriceFromTradeItemHistory@1100528500() GrossPrice : Decimal;
    VAR
      PriceHistoryTradeItem@1100528500 : Record 11012315;
    BEGIN
      //DP02213
      IF "Item Code" <> '' THEN BEGIN
        PriceHistoryTradeItem.SETRANGE(Vendor, Vendor);
        PriceHistoryTradeItem.SETRANGE("Item Code", "Item Code");
        PriceHistoryTradeItem.SETFILTER("Starting Date", '..%1', TODAY);
        IF PriceHistoryTradeItem.FINDLAST THEN
          GrossPrice := PriceHistoryTradeItem."Gross Price";
      END;
      EXIT(GrossPrice);
    END;

    PROCEDURE EvaluateDiscountCondition@1100528600(VAR GrossPrice@1100528904 : Decimal;VAR PurchDisc@1100528905 : Decimal;VAR SalesDisc@1100528906 : Decimal;VAR PurchDiscEditable@1100528907 : Boolean;VAR SalesDiscEditable@1100528908 : Boolean;VAR VirtualPurchDisc@1100528903 : Boolean;VAR VirtualSalesDisc@1100528902 : Boolean);
    VAR
      PurchAmnt@1100528900 : Decimal;
      SalesAmnt@1100528901 : Decimal;
    BEGIN
      //DP02213
      VirtualPurchDisc := FALSE;
      VirtualSalesDisc := FALSE;

      GrossPrice := GetPriceFromTradeItemHistory;
      PurchDisc := GetPurchaseDiscount;
      SalesDisc := "Sales Discount Percentage";
      IF ("Item Code" <> '') THEN BEGIN
        PurchAmnt := GrossPrice - "Purchase Price";
        SalesAmnt := GrossPrice - "Sales Price";
        IF (GrossPrice <> 0) THEN BEGIN
          PurchDisc := 100 * PurchAmnt / GrossPrice;
          SalesDisc := 100 * SalesAmnt / GrossPrice;
        END;
        VirtualPurchDisc := (PurchDisc <> 0);
        VirtualSalesDisc := (SalesDisc <> 0);
      END;

      SalesDiscEditable := ("Item Code" = '');
      PurchDiscEditable := ("Item Code" = '');
      IF PurchDisc <> ("Discount Percentage 1" +"Discount Percentage 2" + "Discount Percentage 3") THEN
        PurchDiscEditable := FALSE;
    END;

    BEGIN
    {
      db, 26-09-14: C016571 (MinValue DiscountPercentage disabled; allowed as surcharge)
    }
    END.
  }
}

