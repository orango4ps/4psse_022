OBJECT Codeunit 232 Gen. Jnl.-Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04,4PS14.00;
  }
  PROPERTIES
  {
    TableNo=81;
    OnRun=BEGIN
            GenJnlLine.COPY(Rec);
            Code;
            COPY(GenJnlLine);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot be filtered when posting recurring journals;NOR=kan ikke filtreres n†r du bokf›rer gjentakelseskladder;SVE=kan inte filtreras n„r †terkommande journaler bokf”rs';
      Text001@1001 : TextConst 'ENU=Do you want to post the journal lines and print the report(s)?;NOR=Vil du bokf›re kladdelinjene og skrive ut rapporten(e)?;SVE=Vill du bokf”ra journalraderna och skriva ut rapporterna?';
      Text002@1002 : TextConst 'ENU=There is nothing to post.;NOR=Det finnes ingenting † bokf›re.;SVE=Det finns inget att bokf”ra.';
      Text003@1003 : TextConst 'ENU=The journal lines were successfully posted.;NOR=Kladdelinjene ble bokf›rt uten problemer.;SVE=Journalraderna „r bokf”rda.';
      Text004@1004 : TextConst 'ENU=The journal lines were successfully posted. You are now in the %1 journal.;NOR=Kladdelinjene ble bokf›rt p† riktig m†te. Du er n† i kladd %1.;SVE=Journalraderna „r bokf”rda. Du „r nu i journal %1.';
      GenJnlTemplate@1005 : Record 80;
      GenJnlLine@1006 : Record 81;
      GLReg@1007 : Record 45;
      CustLedgEntry@1010 : Record 21;
      VendLedgEntry@1011 : Record 25;
      TempJnlBatchName@1009 : Code[10];

    LOCAL PROCEDURE Code@1();
    VAR
      ConfirmManagement@1001 : Codeunit 27;
      HideDialog@1002 : Boolean;
      IsHandled@1000 : Boolean;
      GenJournalTemplatePerRole@1100525000 : Record 11020419;
    BEGIN
      HideDialog := FALSE;
      WITH GenJnlLine DO BEGIN
        GenJournalTemplatePerRole.CheckPermission(TABLECAPTION, "Journal Template Name",TRUE); //**4PS.n

        GenJnlTemplate.GET("Journal Template Name");
        IF GenJnlTemplate."Force Posting Report" OR
           (GenJnlTemplate."Cust. Receipt Report ID" = 0) AND (GenJnlTemplate."Vendor Receipt Report ID" = 0)
        THEN
          GenJnlTemplate.TESTFIELD("Posting Report ID");
        IF GenJnlTemplate.Recurring AND (GETFILTER("Posting Date") <> '') THEN
          FIELDERROR("Posting Date",Text000);

        OnBeforePostJournalBatch(GenJnlLine,HideDialog);

        IF NOT HideDialog THEN
          IF NOT ConfirmManagement.ConfirmProcess(Text001,TRUE) THEN
            EXIT;

        TempJnlBatchName := "Journal Batch Name";

        CODEUNIT.RUN(CODEUNIT::"Gen. Jnl.-Post Batch",GenJnlLine);

        OnAfterPostJournalBatch(GenJnlLine);

        IF GLReg.GET("Line No.") THEN BEGIN
          IF GenJnlTemplate."Cust. Receipt Report ID" <> 0 THEN BEGIN
            CustLedgEntry.SETRANGE("Entry No.",GLReg."From Entry No.",GLReg."To Entry No.");
      //    REPORT.RUN(GenJnlTemplate."Cust. Receipt Report ID",FALSE,FALSE,CustLedgEntry); //**4PS.o
            REPORT.RUN(GenJnlTemplate."Cust. Receipt Report ID",GUIALLOWED,FALSE,CustLedgEntry); //**4PS.n
          END;
          IF GenJnlTemplate."Vendor Receipt Report ID" <> 0 THEN BEGIN
            VendLedgEntry.SETRANGE("Entry No.",GLReg."From Entry No.",GLReg."To Entry No.");
      //    REPORT.RUN(GenJnlTemplate."Vendor Receipt Report ID",FALSE,FALSE,VendLedgEntry); //**4PS.o
            REPORT.RUN(GenJnlTemplate."Vendor Receipt Report ID",GUIALLOWED,FALSE,VendLedgEntry); //**4PS.n
          END;
          IF GenJnlTemplate."Posting Report ID" <> 0 THEN BEGIN
            GLReg.SETRECFILTER;
            OnBeforeGLRegPostingReportPrint(GenJnlTemplate."Posting Report ID",FALSE,FALSE,GLReg,IsHandled);
            IF NOT IsHandled THEN
              //REPORT.RUN(GenJnlTemplate."Posting Report ID",FALSE,FALSE,GLReg); //**4PS.o
              REPORT.RUN(GenJnlTemplate."Posting Report ID",GUIALLOWED,FALSE,GLReg); //**4PS.n
          END;
        END;

        IF NOT HideDialog THEN
          IF "Line No." = 0 THEN
            MESSAGE(Text002)
          ELSE
            IF TempJnlBatchName = "Journal Batch Name" THEN
              MESSAGE(Text003)
            ELSE
              MESSAGE(Text004,"Journal Batch Name");

        IF NOT FIND('=><') OR (TempJnlBatchName <> "Journal Batch Name") THEN BEGIN
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          OnGenJnlLineSetFilter(GenJnlLine);
          FILTERGROUP(0);
          "Line No." := 1;
        END;
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPostJournalBatch@5(VAR GenJournalLine@1000 : Record 81);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeGLRegPostingReportPrint@2(VAR ReportID@1000 : Integer;ReqWindow@1001 : Boolean;SystemPrinter@1002 : Boolean;VAR GLRegister@1003 : Record 45;VAR Handled@1004 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostJournalBatch@3(VAR GenJournalLine@1000 : Record 81;VAR HideDialog@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnGenJnlLineSetFilter@4(VAR GenJournalLine@1000 : Record 81);
    BEGIN
    END;

    BEGIN
    {
      4PS MVOS 20100810: Check permission Journal Template before posting.
    }
    END.
  }
}

