OBJECT Report 11128517 Create NO Electronic Invoices
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVNO10.00 (10640),4PSSE.NO;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create Electronic Invoices;
               NOR=Opprette elektroniske fakturaer;
               SVE=Skapa elektroniska fakturor];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF NOT CurrReport.USEREQUESTPAGE THEN
                    InitLogInteraction;
                END;

  }
  DATASET
  {
    { 5581;    ;DataItem;                    ;
               DataItemTable=Table112;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=VAR
                               SalesInvHeader@1101100000 : Record 112;
                             BEGIN
                               Counter := 0;

                               // Any electronic invoices?
                               SalesInvHeader.COPY("Sales Invoice Header");
                               SalesInvHeader.FILTERGROUP(6);
                               SalesInvHeader.SETRANGE("E-Invoice",TRUE);
                               IF NOT SalesInvHeader.FINDFIRST THEN
                                 ERROR(Text003);

                               // All electronic invoices?
                               SalesInvHeader.SETRANGE("E-Invoice",FALSE);
                               IF SalesInvHeader.FINDFIRST THEN
                                 IF NOT CONFIRM(Text000,TRUE) THEN
                                   CurrReport.QUIT;
                               SalesInvHeader.SETRANGE("E-Invoice");

                               // Some already sent?
                               SalesInvHeader.SETRANGE("E-Invoice Created",TRUE);
                               IF SalesInvHeader.FINDFIRST THEN
                                 IF NOT CONFIRM(Text001,TRUE) THEN
                                   CurrReport.QUIT;

                               SETRANGE("E-Invoice",TRUE);
                             END;

               OnAfterGetRecord=VAR
                                  EInvoiceExportSalesInvoice@1080000 : Codeunit 11128506;
                                BEGIN
                                  EInvoiceExportSalesInvoice.RUN("Sales Invoice Header");
                                  EInvoiceExportSalesInvoice.GetExportedFileInfo(TempEInvoiceTransferFile);
                                  TempEInvoiceTransferFile."Line No." := Counter + 1;
                                  TempEInvoiceTransferFile.INSERT;

                                  IF LogInteraction THEN
                                    IF "Bill-to Contact No." <> '' THEN
                                      SegManagement.LogDocument(
                                        4,"No.",0,0,DATABASE::Contact,"Bill-to Contact No.","Salesperson Code",
                                        "Campaign No.","Posting Description",'')
                                    ELSE
                                      SegManagement.LogDocument(
                                        4,"No.",0,0,DATABASE::Customer,"Bill-to Customer No.","Salesperson Code",
                                        "Campaign No.","Posting Description",'');

                                  COMMIT;
                                  Counter := Counter + 1;
                                END;

               OnPostDataItem=VAR
                                EInvoiceExportCommon@1080000 : Codeunit 11128519;
                              BEGIN
                                EInvoiceExportCommon.DownloadEInvoiceFile(TempEInvoiceTransferFile);
                                MESSAGE(Text002,Counter);
                              END;

               ReqFilterFields=No.,Sell-to Customer No.,Bill-to Customer No.,GLN,E-Invoice Created }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnInit=BEGIN
               LogInteractionEnable := TRUE;
             END;

      OnOpenPage=BEGIN
                   InitLogInteraction;
                   LogInteractionEnable := LogInteraction;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1101100000;2;Field  ;
                  Name=LogInteraction;
                  CaptionML=[ENU=Log Interaction;
                             NOR=Logg Interaksjon;
                             SVE=Logga interaktion];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=LogInteraction;
                  Enabled=LogInteractionEnable }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1080005 : TextConst 'ENU=One or more invoice documents that match your filter criteria are not electronic invoices and will be skipped.\\Do you want to continue?;NOR=Ett eller flere fakturadokumenter som samsvarer med filterkriteriene, er ikke elektroniske fakturaer og hoppes over.\\Vil du fortsette?';
      Text001@1080006 : TextConst 'ENU=One or more invoice documents that match your filter criteria have been created before.\\Do you want to continue?;NOR=Ett eller flere fakturadokumenter som samsvarer med filterkriteriene, er allerede opprettet.\\Vil du fortsette?';
      Text002@1080007 : TextConst 'ENU=Successfully created %1 electronic invoice documents.;NOR=Opprettet %1 elektroniske fakturadokumenter.';
      TempEInvoiceTransferFile@1080000 : TEMPORARY Record 11128516;
      SegManagement@1080002 : Codeunit 5051;
      Counter@1080003 : Integer;
      Text003@1080008 : TextConst 'ENU=Nothing to create.;NOR=Det finnes ingenting † opprette.;SVE=Det finns inget att skapa.';
      LogInteraction@1080004 : Boolean;
      LogInteractionEnable@19003940 : Boolean INDATASET;

    PROCEDURE InitLogInteraction@1080009();
    BEGIN
      LogInteraction := SegManagement.FindInteractTmplCode(4) <> '';
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

