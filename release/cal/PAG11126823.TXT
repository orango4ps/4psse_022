OBJECT Page 11126823 Copy PB Surcharge Set
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Preisbuch Aufschlagsatz kopieren;
               ENU=Copy Price Book Surcharge Set;
               NLD=Prijsboektoeslagenset kopi‰ren];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    PageType=StandardDialog;
    OnQueryClosePage=BEGIN
                       CloseActionOK := CloseAction IN [ACTION::OK, ACTION::LookupOK];
                     END;

  }
  CONTROLS
  {
    { 1100528600;0;Container;
                ContainerType=ContentArea }

    { 1100528601;1;Group  ;
                Name=Algemeen;
                CaptionML=[DEU=Kopieren von;
                           ENU=Copy from;
                           NLD=Kopi‰ren van;
                           NOR=Kopier fra;
                           SVE=Kopiera fr†n];
                GroupType=Group }

    { 1100528602;2;Field  ;
                CaptionML=[DEU=Preisbuch;
                           ENU=Price Book;
                           NLD=Prijsboek];
                SourceExpr=FromPriceBookCode;
                OnValidate=BEGIN
                             ValidateFromPriceBookCode;
                           END;

                OnLookup=BEGIN
                           EXIT(LookupPriceBookCode(Text));
                         END;
                          }

    { 1100528604;2;Field  ;
                CaptionML=[DEU=Indexierungsdatum;
                           ENU=Index Date;
                           NLD=Indexeringsdatum;
                           NOR=Indeksdato;
                           SVE=Indexdatum];
                SourceExpr=FromPriceBookIndexDate;
                OnValidate=BEGIN
                             ValidateFromPriceBookIndexDate;
                           END;

                OnLookup=BEGIN
                           LookupFromPriceBookIndexDate(FromPriceBookCode);
                         END;
                          }

    { 1100528605;2;Field  ;
                CaptionML=[DEU=Aufschlagssatz;
                           ENU=Surcharge Set;
                           NLD=Toeslagset];
                SourceExpr=FromSurchargeSetCode;
                OnValidate=BEGIN
                             ValidateFromSurchargeSetCode;
                           END;

                OnLookup=BEGIN
                           EXIT(LookupSurchargeSetCode(FromPriceBookCode, FromPriceBookIndexDate, Text));
                         END;
                          }

    { 1100528610;2;Field  ;
                CaptionML=[DEU=Beschreibung;
                           ENU=Description;
                           NLD=Omschrijving;
                           NOR=Beskrivelse;
                           SVE=Beskrivning];
                SourceExpr=FromSurchargeSetDescription;
                Editable=FALSE }

    { 1100528603;1;Group  ;
                CaptionML=[DEU=Kopieren in;
                           ENU=Copy to;
                           NLD=Kopi‰ren naar;
                           NOR=Kopier til;
                           SVE=Kopiera till];
                GroupType=Group }

    { 1100528608;2;Field  ;
                CaptionML=[DEU=Preisbuch;
                           ENU=Price Book;
                           NLD=Prijsboek];
                SourceExpr=ToPriceBookCode;
                OnValidate=BEGIN
                             ValidateToPriceBookCode;
                           END;

                OnLookup=BEGIN
                           EXIT(LookupPriceBookCode(Text));
                         END;
                          }

    { 1100528607;2;Field  ;
                CaptionML=[DEU=Indexierungsdatum;
                           ENU=Index Date;
                           NLD=Indexeringsdatum;
                           NOR=Indeksdato;
                           SVE=Indexdatum];
                SourceExpr=ToPriceBookIndexDate;
                OnValidate=BEGIN
                             ValidateToPriceBookIndexDate;
                           END;

                OnLookup=BEGIN
                           LookupToPriceBookIndexDate(ToPriceBookCode);
                         END;
                          }

    { 1100528606;2;Field  ;
                CaptionML=[DEU=Aufschlagssatz;
                           ENU=Surcharge Set;
                           NLD=Toeslagset];
                SourceExpr=ToSurchargeSetCode;
                OnValidate=BEGIN
                             ValidateToSurchargeSetCode;
                           END;

                OnLookup=BEGIN
                           EXIT(LookupSurchargeSetCode(ToPriceBookCode, ToPriceBookIndexDate, Text));
                         END;
                          }

    { 1100528609;2;Field  ;
                CaptionML=[DEU=Beschreibung;
                           ENU=Description;
                           NLD=Omschrijving;
                           NOR=Beskrivelse;
                           SVE=Beskrivning];
                SourceExpr=ToSurchargeSetDescription }

  }
  CODE
  {
    VAR
      FromPriceBookCode@1100528601 : Code[20];
      FromPriceBookIndexDate@1100528602 : Date;
      FromSurchargeSetCode@1100528600 : Code[10];
      FromSurchargeSetDescription@1100528607 : Text[50];
      ToPriceBookCode@1100528605 : Code[20];
      ToPriceBookIndexDate@1100528604 : Date;
      ToSurchargeSetCode@1100528603 : Code[10];
      ToSurchargeSetDescription@1100528606 : Text[50];
      CloseActionOK@1100528608 : Boolean;
      Text000@1100528609 : TextConst 'DEU=Warnung: Aufschlagsatz existiert bereits;ENU=Warning: Surcharge Set already exists;NLD=Let op: Toeslagset bestaat reeds';

    PROCEDURE FillDefaults@1100528604(IPriceBookCode@1100528600 : Code[20];IPriceBookIndexDate@1100528601 : Date;ISurchargeSetCode@1100528602 : Code[10]);
    BEGIN
      FromPriceBookCode := IPriceBookCode;
      FromPriceBookIndexDate := IPriceBookIndexDate;
      FromSurchargeSetCode := ISurchargeSetCode;
      ToPriceBookCode := IPriceBookCode;
      ToPriceBookIndexDate := IPriceBookIndexDate;
      ValidateFromSurchargeSetCode;
    END;

    LOCAL PROCEDURE ValidateFromPriceBookCode@1100528611();
    BEGIN
      IF ToPriceBookCode = '' THEN
        ToPriceBookCode := FromPriceBookCode;
      FromPriceBookIndexDate := 0D;
      ValidateFromPriceBookIndexDate;
      FromSurchargeSetCode := '';
      ValidateFromSurchargeSetCode;
    END;

    LOCAL PROCEDURE ValidateFromPriceBookIndexDate@1100528612();
    BEGIN
      IF ToPriceBookIndexDate = 0D THEN
        ToPriceBookIndexDate := FromPriceBookIndexDate;
      FromSurchargeSetCode := '';
      ValidateFromSurchargeSetCode;
    END;

    LOCAL PROCEDURE ValidateFromSurchargeSetCode@1100528608();
    VAR
      PriceBookSurchargeSet@1100528600 : Record 11072315;
    BEGIN
      IF PriceBookSurchargeSet.GET(FromPriceBookCode, FromPriceBookIndexDate, FromSurchargeSetCode) THEN
        FromSurchargeSetDescription := PriceBookSurchargeSet.Description
      ELSE
        FromSurchargeSetDescription := '';
      ToSurchargeSetDescription := FromSurchargeSetDescription;
    END;

    LOCAL PROCEDURE ValidateToPriceBookCode@1100528617();
    BEGIN
      ToPriceBookIndexDate := 0D;
      ToSurchargeSetCode := '';
      ValidateToSurchargeSetCode;
    END;

    LOCAL PROCEDURE ValidateToPriceBookIndexDate@1100528616();
    BEGIN
      ToSurchargeSetCode := '';
      ValidateToSurchargeSetCode;
    END;

    LOCAL PROCEDURE ValidateToSurchargeSetCode@1100528615();
    VAR
      PriceBookSurchargeSet@1100528600 : Record 11072315;
    BEGIN
      IF PriceBookSurchargeSet.GET(ToPriceBookCode, ToPriceBookIndexDate, ToSurchargeSetCode) THEN
        MESSAGE(Text000);
    END;

    LOCAL PROCEDURE LookupPriceBookCode@1100528619(VAR Text@1100528600 : Text) : Boolean;
    VAR
      ServicePriceBook@1100528601 : Record 11071768;
    BEGIN
      IF PAGE.RUNMODAL(0, ServicePriceBook) = ACTION::LookupOK THEN BEGIN
        Text := ServicePriceBook.Code;
        EXIT(TRUE);
      END;
    END;

    LOCAL PROCEDURE LookupFromPriceBookIndexDate@1100528623(IPriceBookCode@1100528602 : Code[20]);
    VAR
      ServicePriceBook@1100528600 : Record 11071768;
    BEGIN
      ServicePriceBook.SETRANGE(Code, IPriceBookCode);
      IF PAGE.RUNMODAL(0, ServicePriceBook) = ACTION::LookupOK THEN BEGIN
        FromPriceBookIndexDate := ServicePriceBook."Index Date";
        ValidateFromPriceBookIndexDate;
      END;
    END;

    LOCAL PROCEDURE LookupToPriceBookIndexDate@1100528600(IPriceBookCode@1100528602 : Code[20]);
    VAR
      ServicePriceBook@1100528600 : Record 11071768;
    BEGIN
      ServicePriceBook.SETRANGE(Code, IPriceBookCode);
      IF PAGE.RUNMODAL(0, ServicePriceBook) = ACTION::LookupOK THEN BEGIN
        ToPriceBookIndexDate := ServicePriceBook."Index Date";
        ValidateToPriceBookIndexDate;
      END;
    END;

    LOCAL PROCEDURE LookupSurchargeSetCode@1100528631(IPriceBookCode@1100528602 : Code[20];IPriceBookIndexDate@1100528603 : Date;VAR Text@1100528600 : Text) : Boolean;
    VAR
      PriceBookSurchargeSet@1100528601 : Record 11072315;
    BEGIN
      PriceBookSurchargeSet.SETRANGE("Price Book Code", IPriceBookCode);
      PriceBookSurchargeSet.SETRANGE("Price Book Index Date", IPriceBookIndexDate);
      IF PAGE.RUNMODAL(0, PriceBookSurchargeSet) = ACTION::LookupOK THEN BEGIN
        Text := PriceBookSurchargeSet.Code;
        EXIT(TRUE);
      END;
    END;

    LOCAL PROCEDURE StdDialogConfirm@1100528402() : Boolean;
    BEGIN
      CurrPage.RUNMODAL;
      EXIT(CloseActionOK);
    END;

    LOCAL PROCEDURE StdDialogDoAction@1100528401();
    BEGIN
      CopySurchargeSet;
    END;

    PROCEDURE StdDialogRunModal@1100528404();
    BEGIN
      IF StdDialogConfirm THEN
        StdDialogDoAction;
    END;

    LOCAL PROCEDURE CopySurchargeSet@1100528646();
    VAR
      ServiceUnitPriceManagement@1100528600 : Codeunit 11012846;
    BEGIN
      ServiceUnitPriceManagement.CopySurchargeSet(
        FromPriceBookCode, FromPriceBookIndexDate, FromSurchargeSetCode, ToPriceBookCode, ToPriceBookIndexDate, ToSurchargeSetCode, ToSurchargeSetDescription);
    END;

    BEGIN
    END.
  }
}

