OBJECT Table 11012636 Project CF Rubric Phase
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnModify=BEGIN
               CheckFixed;
             END;

    OnDelete=BEGIN
               IF NOT DeleteCompleteProject THEN
                 CheckFixed;
             END;

    CaptionML=ENU=Project CF Rubric Phase;
  }
  FIELDS
  {
    { 4   ;   ;Template Code       ;Code10        ;TableRelation="Project CF Template";
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Template Code;
                                                              SVE=Mallkod] }
    { 5   ;   ;Record Type         ;Option        ;CaptionML=[ENU=Record Type;
                                                              SVE=Posttyp];
                                                   OptionCaptionML=ENU=Company,Project,Cash Flow;
                                                   OptionString=Company,Project,Cash Flow }
    { 10  ;   ;Project No.         ;Code20        ;TableRelation="Project Development";
                                                   CaptionML=[ENU=Project No.;
                                                              SVE=Projektnr];
                                                   NotBlank=Yes }
    { 20  ;   ;Cash Flow           ;DateTime      ;TableRelation="Project CF"."Cash Flow" WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=[ENU=Cash Flow;
                                                              SVE=Kassafl”de];
                                                   NotBlank=Yes }
    { 30  ;   ;Phase               ;Code10        ;CaptionML=[ENU=Phase;
                                                              SVE=Fas];
                                                   NotBlank=Yes }
    { 40  ;   ;Rubric              ;Code10        ;TableRelation="Project CF Rubric".Rubric WHERE (Project No.=FIELD(Project No.),
                                                                                                   Cash Flow=FIELD(Cash Flow),
                                                                                                   Record Type=FIELD(Record Type),
                                                                                                   Template Code=FIELD(Template Code));
                                                   CaptionML=[ENU=Rubric;
                                                              SVE=Rubrik];
                                                   NotBlank=Yes }
    { 60  ;   ;Percentage of Amount;Decimal       ;CaptionML=ENU=% of Amount }
    { 70  ;   ;Parameter Code      ;Code10        ;TableRelation="Project CF Parameter".Code WHERE (Project No.=FIELD(Project No.),
                                                                                                    Cash Flow=FIELD(Cash Flow),
                                                                                                    Record Type=FIELD(Record Type),
                                                                                                    Template Code=FIELD(Template Code));
                                                   CaptionML=ENU=Parameter Code }
    { 80  ;   ;Parameter Name      ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project CF Parameter".Name WHERE (Project No.=FIELD(Project No.),
                                                                                                         Cash Flow=FIELD(Cash Flow),
                                                                                                         Record Type=FIELD(Record Type),
                                                                                                         Code=FIELD(Parameter Code),
                                                                                                         Template Code=FIELD(Template Code)));
                                                   CaptionML=ENU=Parameter Name;
                                                   Editable=No }
    { 90  ;   ;Total Percentage Rubric;Decimal    ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Project CF Rubric Phase"."Percentage of Amount" WHERE (Record Type=FIELD(Record Type),
                                                                                                                           Project No.=FIELD(Project No.),
                                                                                                                           Cash Flow=FIELD(Cash Flow),
                                                                                                                           Rubric=FIELD(Rubric),
                                                                                                                           Template Code=FIELD(Template Code)));
                                                   CaptionML=ENU=Total Percentage Rubric;
                                                   Editable=No }
    { 100 ;   ;Rubric Description  ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project CF Rubric".Description WHERE (Record Type=FIELD(Record Type),
                                                                                                             Project No.=FIELD(Project No.),
                                                                                                             Cash Flow=FIELD(Cash Flow),
                                                                                                             Rubric=FIELD(Rubric),
                                                                                                             Template Code=FIELD(Template Code)));
                                                   CaptionML=[ENU=Rubric Description;
                                                              SVE=Rubrik, beskrivning];
                                                   Editable=No }
    { 110 ;   ;Phase Description   ;Text50        ;FieldClass=Normal;
                                                   CaptionML=[ENU=Phase Description;
                                                              SVE=Fasbeskrivning];
                                                   Editable=No }
    { 120 ;   ;Comment             ;Text50        ;DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Comment;
                                                              SVE=Kommentar] }
  }
  KEYS
  {
    {    ;Record Type,Project No.,Cash Flow,Rubric,Phase,Template Code;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Rubric,Phase                             }
  }
  CODE
  {
    VAR
      DeleteCompleteProject@1100527700 : Boolean;
      Text001@1100527701 : TextConst 'ENU=Do you want to change every phase that uses parameter code %1 to %2%?';

    PROCEDURE InitRecord@1100527704();
    VAR
      ProjectCF@1100527700 : Record 11012635;
    BEGIN
      "Template Code" := ProjectCF.GetTemplateCode("Project No.","Cash Flow");
    END;

    PROCEDURE GetPhaseScheme@1100527700();
    VAR
      ProjectCFPhaseScheme@1100527701 : Record 11012644;
      ProjectCFPhaseSchemeLine@1100527702 : Record 11012645;
    BEGIN
      IF PAGE.RUNMODAL(0,ProjectCFPhaseScheme) = ACTION::LookupOK THEN BEGIN
        ProjectCFPhaseSchemeLine.SETRANGE("Template Code",ProjectCFPhaseScheme."Template Code");
        ProjectCFPhaseSchemeLine.SETRANGE("Scheme Code",ProjectCFPhaseScheme.Code);
        IF ProjectCFPhaseSchemeLine.FINDSET THEN
          REPEAT
            InsertLineFromPhaseScheme(ProjectCFPhaseSchemeLine);
          UNTIL ProjectCFPhaseSchemeLine.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE InsertLineFromPhaseScheme@1100527702(ProjectCFPhaseSchemeLine@1100527700 : Record 11012645);
    VAR
      ProjectCFRubricPhase@1100527701 : Record 11012636;
    BEGIN
      ProjectCFRubricPhase."Record Type" := "Record Type";
      ProjectCFRubricPhase.VALIDATE("Project No.","Project No.");
      ProjectCFRubricPhase.VALIDATE("Cash Flow","Cash Flow");
      ProjectCFRubricPhase.VALIDATE("Template Code","Template Code");
      ProjectCFRubricPhase.VALIDATE(Rubric,ProjectCFPhaseSchemeLine.Rubric);
      ProjectCFRubricPhase.VALIDATE(Phase,ProjectCFPhaseSchemeLine.Phase);
      ProjectCFRubricPhase.VALIDATE("Percentage of Amount",ProjectCFPhaseSchemeLine."Percentage of Amount");
      ProjectCFRubricPhase.VALIDATE("Parameter Code",ProjectCFPhaseSchemeLine."Parameter Code");
      ProjectCFRubricPhase.INSERT;
    END;

    PROCEDURE CheckFixed@1100527703();
    VAR
      ProjectCF@1100527700 : Record 11012635;
    BEGIN
      IF "Record Type" = "Record Type"::"Cash Flow" THEN BEGIN
        ProjectCF.GET("Project No.","Cash Flow");
        ProjectCF.TESTFIELD(Fixed,FALSE);
      END;
    END;

    PROCEDURE SetDeleteCompleteProject@1100527701();
    BEGIN
      DeleteCompleteProject := TRUE;
    END;

    PROCEDURE ChangePercentage@1100527705(ProjectCFRubricPhase@1100527700 : Record 11012636);
    BEGIN
      ProjectCFRubricPhase.MARKEDONLY(TRUE);
      IF ProjectCFRubricPhase.FINDSET THEN
        REPEAT
          ProjectCFRubricPhase."Percentage of Amount" := 100;
          ProjectCFRubricPhase.MODIFY;
        UNTIL ProjectCFRubricPhase.NEXT = 0;
    END;

    PROCEDURE GetStyle@1100527706(Quantity@1100528500 : Decimal) : Text;
    BEGIN
      IF Quantity = 100 THEN
        EXIT('Standard')
      ELSE
        EXIT('Unfavorable');
    END;

    PROCEDURE CopyPercentageToPhasesWithSameParameter@1100527707();
    VAR
      ProjectCFRubricPhase@1100527700 : Record 11012636;
    BEGIN
      IF CONFIRM(Text001,FALSE,"Parameter Code","Percentage of Amount") THEN BEGIN
        ProjectCFRubricPhase.SETRANGE("Record Type","Record Type");
        ProjectCFRubricPhase.SETRANGE("Project No.","Project No.");
        ProjectCFRubricPhase.SETRANGE("Cash Flow","Cash Flow");
        ProjectCFRubricPhase.SETRANGE("Parameter Code","Parameter Code");
        IF ProjectCFRubricPhase.FINDSET THEN
          REPEAT
            ProjectCFRubricPhase."Percentage of Amount" := "Percentage of Amount";
            ProjectCFRubricPhase.MODIFY;
          UNTIL ProjectCFRubricPhase.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}

