OBJECT Page 664 Sales Prepayment Percentages
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Sales Prepayment Percentages;
               NOR=Forskuddsprosenter for salg;
               SVE=Procentandelar, f”rskottsbetalning f”r f”rs„ljning];
    SourceTable=Table459;
    DataCaptionExpr=Caption;
    DelayedInsert=Yes;
    PageType=Worksheet;
    OnInit=BEGIN
             CodeFilterCtrlEnable := TRUE;
             SalesCodeFilterCtrlEnable := TRUE;
             SalesCodeEditable := TRUE;
           END;

    OnOpenPage=BEGIN
                 GetRecFilters;
                 SetRecFilters;
               END;

    OnAfterGetCurrRecord=BEGIN
                           SetEditable;
                         END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 17  ;1   ;Group     ;
                CaptionML=[ENU=General;
                           NOR=Generelt;
                           SVE=Allm„nt] }

    { 18  ;2   ;Field     ;
                CaptionML=[ENU=Sales Type Filter;
                           NOR=Salgstypefilter;
                           SVE=F”rs.typfilter];
                ToolTipML=[ENU=Specifies if the sales prepayments apply to customers or price groups.;
                           NOR=Angir om de utg†ende forskuddsbetalingene gjelder for kunder eller prisgrupper.;
                           SVE=Anger om f”rskottsbetalningar f”r f”rs„ljning g„ller f”r kunder eller prisgrupper.];
                OptionCaptionML=[ENU=Customer,Customer Price Group,All Customers,None;
                                 NOR=Kunde,Kundeprisgruppe,Alle kunder,Ingen;
                                 SVE=Kund,Kundprisgrupp,Alla kunder,Inga];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=SalesTypeFilter;
                OnValidate=BEGIN
                             SalesTypeFilterOnAfterValidate;
                           END;
                            }

    { 19  ;2   ;Field     ;
                Name=SalesCodeFilterCtrl;
                CaptionML=[ENU=Sales Code Filter;
                           NOR=Salgskodefilter;
                           SVE=F”rs.kodfilter];
                ToolTipML=[ENU=Specifies a filter for the customer or price group that the prepayment percentages apply to.;
                           NOR=Angir et filter for kunden eller prisgruppen som forskuddsprosentene gjelder for.;
                           SVE=Anger ett filter f”r kund- eller prisgruppen som f”rskottsbetalningsprocent g„ller f”r.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=SalesCodeFilter;
                Enabled=SalesCodeFilterCtrlEnable;
                OnValidate=BEGIN
                             SalesCodeFilterOnAfterValidate;
                           END;

                OnLookup=VAR
                           CustList@1005 : Page 22;
                           CustPriceGrList@1004 : Page 7;
                         BEGIN
                           IF SalesTypeFilter = SalesTypeFilter::"All Customers" THEN
                             EXIT;

                           CASE SalesTypeFilter OF
                             SalesTypeFilter::Customer:
                               BEGIN
                                 CustList.LOOKUPMODE := TRUE;
                                 IF CustList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                   Text := CustList.GetSelectionFilter;
                                   EXIT(TRUE);
                                 END;
                               END;
                             SalesTypeFilter::"Customer Price Group":
                               BEGIN
                                 CustPriceGrList.LOOKUPMODE := TRUE;
                                 IF CustPriceGrList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                   Text := CustPriceGrList.GetSelectionFilter;
                                   EXIT(TRUE);
                                 END;
                               END;
                           END;
                         END;
                          }

    { 24  ;2   ;Field     ;
                Name=CodeFilterCtrl;
                CaptionML=[ENU=Item No. Filter;
                           NOR=Filter for varenr.;
                           SVE=Artikelnrfilter];
                ToolTipML=[ENU=Specifies a filter for the items that the prepayment percentages apply to.;
                           NOR=Angir et filter for varene som forskuddsprosentene gjelder for.;
                           SVE=Anger ett filter f”r artiklarna som f”rskottsbetalningsprocent g„ller f”r.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ItemNoFilter;
                Enabled=CodeFilterCtrlEnable;
                OnValidate=BEGIN
                             ItemNoFilterOnAfterValidate;
                           END;

                OnLookup=VAR
                           ItemList@1000 : Page 31;
                         BEGIN
                           ItemList.LOOKUPMODE := TRUE;
                           IF ItemList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             Text := ItemList.GetSelectionFilter;
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 20  ;2   ;Field     ;
                CaptionML=[ENU=Starting Date Filter;
                           NOR=Startdatofilter;
                           SVE=Startdatumfilter];
                ToolTipML=[ENU=Specifies a filter for the starting date of the prepayment percentages.;
                           NOR=Angir et filter for startdatoen for forskuddsprosentene.;
                           SVE=Anger ett filter f”r startdatumet f”r f”rskottsbetalningsprocent.];
                ApplicationArea=#Prepayments;
                SourceExpr=StartingDateFilter;
                OnValidate=VAR
                             TextManagement@1000 : Codeunit 41;
                           BEGIN
                             TextManagement.MakeDateFilter(StartingDateFilter);
                             StartingDateFilterOnAfterValid;
                           END;
                            }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the sales type of the prepayment percentage.;
                           NOR=Angir salgstypen for forskuddsprosenten.;
                           SVE=Anger f”rs„ljningstypen f”r f”rskottsbetalningsprocenten.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Sales Type";
                OnValidate=BEGIN
                             SalesTypeOnAfterValidate;
                           END;
                            }

    { 4   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the code that belongs to the sales type.;
                           NOR=Angir koden som h›rer til salgstypen.;
                           SVE=Anger vilken kod som tillh”r f”rs„ljningstypen.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Sales Code";
                Editable=SalesCodeEditable }

    { 1100525001;2;Field  ;
                SourceExpr="Sales Code Description";
                Visible=FALSE }

    { 6   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the number of the item for which the prepayment percentage is valid.;
                           NOR=Angir nummeret for varen som forskuddsprosenten er gyldig for.;
                           SVE=Anger numret p† den artikel som f”rskottsbetalningsprocenten g„ller.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Item No." }

    { 1100525000;2;Field  ;
                SourceExpr="Item Description";
                Visible=FALSE }

    { 8   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the date from which the prepayment percentage is valid.;
                           NOR=Angir datoen som forskuddsprosenten er gyldig fra.;
                           SVE=Anger det datum fr†n vilket f”rskottsbetalningsprocenten g„ller.];
                ApplicationArea=#Prepayments;
                SourceExpr="Starting Date" }

    { 10  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the date to which the prepayment percentage is valid.;
                           NOR=Angir datoen som forskuddsprosenten er gyldig til.;
                           SVE=Anger det datum till vilket f”rskottsbetalningsprocenten g„ller.];
                ApplicationArea=#Prepayments;
                SourceExpr="Ending Date" }

    { 12  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the prepayment percentage to use to calculate the prepayment for sales.;
                           NOR=Angir forskuddsprosenten du bruker til † beregne forskuddet for salg.;
                           SVE=Anger f”rskottsprocenten som anv„nds f”r att ber„kna f”rskottet f”r f”rs„ljning.];
                ApplicationArea=#Prepayments;
                SourceExpr="Prepayment %" }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                ApplicationArea=#RecordLinks;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                ApplicationArea=#Notes;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      SalesTypeFilter@1003 : 'Customer,Customer Price Group,All Customers,None';
      SalesCodeFilter@1002 : Text;
      ItemNoFilter@1001 : Text;
      StartingDateFilter@1000 : Text[30];
      SalesCodeEditable@19031640 : Boolean INDATASET;
      SalesCodeFilterCtrlEnable@19013164 : Boolean INDATASET;
      CodeFilterCtrlEnable@19026169 : Boolean INDATASET;

    LOCAL PROCEDURE SetEditable@3();
    BEGIN
      SalesCodeEditable := "Sales Type" <> "Sales Type"::"All Customers";
    END;

    LOCAL PROCEDURE GetRecFilters@4();
    BEGIN
      IF HASFILTER THEN BEGIN
        IF GETFILTER("Sales Type") <> '' THEN
          SalesTypeFilter := "Sales Type"
        ELSE
          SalesTypeFilter := SalesTypeFilter::None;

        SalesCodeFilter := GETFILTER("Sales Code");
        ItemNoFilter := GETFILTER("Item No.");
        EVALUATE(StartingDateFilter,GETFILTER("Starting Date"));
      END;
    END;

    [External]
    PROCEDURE SetRecFilters@5();
    BEGIN
      SalesCodeFilterCtrlEnable := TRUE;
      CodeFilterCtrlEnable := TRUE;

      IF SalesTypeFilter <> SalesTypeFilter::None THEN
        SETRANGE("Sales Type",SalesTypeFilter)
      ELSE
        SETRANGE("Sales Type");

      IF SalesTypeFilter IN [SalesTypeFilter::"All Customers",SalesTypeFilter::None] THEN BEGIN
        SalesCodeFilterCtrlEnable := FALSE;
        SalesCodeFilter := '';
      END;

      IF SalesCodeFilter <> '' THEN
        SETFILTER("Sales Code",SalesCodeFilter)
      ELSE
        SETRANGE("Sales Code");

      IF ItemNoFilter <> '' THEN
        SETFILTER("Item No.",ItemNoFilter)
      ELSE
        SETRANGE("Item No.");

      IF StartingDateFilter <> '' THEN
        SETFILTER("Starting Date",StartingDateFilter)
      ELSE
        SETRANGE("Starting Date");

      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE Caption@6() : Text;
    VAR
      Item@1005 : Record 27;
      Cust@1004 : Record 18;
      CustPriceGr@1003 : Record 6;
      ItemNoCaption@1001 : Text;
      SalesCodeCaption@1000 : Text;
    BEGIN
      IF ItemNoFilter <> '' THEN BEGIN
        ItemNoCaption := STRSUBSTNO('%1 %2',Item.TABLECAPTION,ItemNoFilter);
        IF Item.GET(COPYSTR(ItemNoFilter,1,MAXSTRLEN(Item."No."))) THEN
          ItemNoCaption := ItemNoCaption + ' - ' + Item.Description;
      END;

      CASE SalesTypeFilter OF
        SalesTypeFilter::Customer:
          BEGIN
            SalesCodeCaption := STRSUBSTNO('%1 %2',Cust.TABLECAPTION,SalesCodeFilter);
            IF Cust.GET(COPYSTR(SalesCodeFilter,1,MAXSTRLEN(Cust."No."))) THEN
              SalesCodeCaption := SalesCodeCaption + ' - ' + Cust.Name;
          END;
        SalesTypeFilter::"Customer Price Group":
          BEGIN
            SalesCodeCaption := STRSUBSTNO('%1 %2',CustPriceGr.TABLECAPTION,SalesCodeFilter);
            IF CustPriceGr.GET(COPYSTR(SalesCodeFilter,1,MAXSTRLEN(CustPriceGr.Code))) THEN
              SalesCodeCaption := SalesCodeCaption + ' - ' + CustPriceGr.Description;
          END;
        SalesTypeFilter::"All Customers":
          SalesCodeCaption := FORMAT(SalesTypeFilter);
      END;

      EXIT(DELCHR(ItemNoCaption + ' ' + SalesCodeCaption,'<>'))
    END;

    LOCAL PROCEDURE SalesTypeOnAfterValidate@19046339();
    BEGIN
      SetEditable;
    END;

    LOCAL PROCEDURE SalesTypeFilterOnAfterValidate@19024044();
    BEGIN
      CurrPage.SAVERECORD;
      SalesCodeFilter := '';
      SetRecFilters;
    END;

    LOCAL PROCEDURE SalesCodeFilterOnAfterValidate@19067727();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    LOCAL PROCEDURE StartingDateFilterOnAfterValid@19055286();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    LOCAL PROCEDURE ItemNoFilterOnAfterValidate@19009808();
    BEGIN
      CurrPage.SAVERECORD;
      SetRecFilters;
    END;

    BEGIN
    END.
  }
}

