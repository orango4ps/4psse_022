OBJECT Codeunit 6085901 DC Purch.-Post Subscr.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW19.00.00.5.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      PurchMatchedTxt@6085573 : TextConst 'DEU=%1 %2 ist abgestimmt auf %3 %4.\\;ENU=%1 %2 is matched to %3 %4.\\;NLD=%1 %2 is gematched aan %3 %4.\\';
      PurchDocMatchedErr@6085574 : TextConst 'DEU=Bestellungen oder Reklamationen, die mit einem anderen Einkaufsbeleg abgeglichen wurden, mÅssen Åber den Einkaufsbeleg gebucht werden (% 1% 2).;ENU=Purchase Orders or Purchase Return Orders that are matched from another Purchase Document must be posted via the Purchase Document (%1 %2).;NLD=Inkooporders of inkoopretourorders die gematched zijn aan een ander inkoopdocument dienen geboekt te worden via het inkoopdocument (%1 %2).';
      PurchBatchPostErr@6085575 : TextConst 'DEU=%1 %2 ist abgeglichen auf %3 %4. \\ Abgeglichene Einkaufsrechnungen und Gutschriften mÅssen dem Document Capture Stapelbuchungsprozess gebucht werden.;ENU=%1 %2 is matched to %3 %4.\\Matched Purchase Invoices and Credit Memos must be posted using the Document Capture Batch Post process.;NLD=%1 %2 is gematched aan %3 %4. \\ Gematchte inkoopfacturen en creditnota''s moeten worden geboekt met behulp van het Document Capture batchboekingsproces.';

    [EventSubscriber(Codeunit,90,OnBeforePostPurchaseDoc,"",Skip)]
    LOCAL PROCEDURE OnBeforePostPurchaseDoc@6085585(VAR PurchaseHeader@6085573 : Record 38);
    VAR
      PurchLineRel@6085576 : Record 6085702;
      PurchDocMgt@6085574 : Codeunit 6085709;
      SIStorage@6085575 : Codeunit 6085789;
      PurchDocLineRelMgt@6085577 : Codeunit 6085761;
    BEGIN
      IF NOT SIStorage.GetAllowPostPurchRtnOrOrderC90 THEN BEGIN
        PurchLineRel.SETCURRENTKEY("Related Document Type","Related Document No.","Related Line No.");
        PurchLineRel.SETRANGE("Related Document Type",PurchaseHeader."Document Type");
        PurchLineRel.SETRANGE("Related Document No.",PurchaseHeader."No.");
        IF PurchLineRel.FINDFIRST THEN
          ERROR(
            STRSUBSTNO(PurchMatchedTxt,PurchaseHeader."Document Type",PurchaseHeader."No.",PurchLineRel."Document Type",
              PurchLineRel."Document No.") +
            STRSUBSTNO(PurchDocMatchedErr,PurchLineRel."Document Type",PurchLineRel."Document No."));
      END;

      IF NOT SIStorage.GetAllowPostRelPurchDocC90 THEN BEGIN
        PurchLineRel.RESET;
        PurchLineRel.SETCURRENTKEY("Document Type","Document No.","Related Document Type","Related Document No.",
          "Related Line No.");
        PurchLineRel.SETRANGE("Document Type",PurchaseHeader."Document Type");
        PurchLineRel.SETRANGE("Document No.",PurchaseHeader."No.");
        IF PurchLineRel.FINDFIRST THEN
          ERROR(PurchBatchPostErr,PurchaseHeader."Document Type",PurchaseHeader."No.",PurchLineRel."Related Document Type",
            PurchLineRel."Related Document No.");
      END;

      PurchDocMgt.PurchDocOnPost(PurchaseHeader);
      PurchDocLineRelMgt.PostRelOrderLines(PurchaseHeader);
    END;

    [EventSubscriber(Codeunit,90,OnBeforePostCommitPurchaseDoc,"",Skip)]
    LOCAL PROCEDURE OnBeforePostCommitPurchaseDoc@6085574(VAR PurchaseHeader@6085573 : Record 38;VAR GenJnlPostLine@6085574 : Codeunit 12;PreviewMode@6085575 : Boolean;ModifyHeader@6085576 : Boolean);
    VAR
      SIStorage@6085578 : Codeunit 6085789;
      PurchDocLineRelMgt@6085577 : Codeunit 6085761;
    BEGIN
      SIStorage.SetC90PurchHeader(PurchaseHeader);

      IF PreviewMode THEN
        PurchDocLineRelMgt.PurchDocMatchedToPreviewError(PurchaseHeader);
    END;

    BEGIN
    END.
  }
}

