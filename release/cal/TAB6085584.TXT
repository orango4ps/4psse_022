OBJECT Table 6085584 Data Translation
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.5.00;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               DeleteDims;
             END;

    CaptionML=[DEU=DatenÅbersetzung;
               ENU=Data Translation;
               NLD=Data vertaling];
    LookupPageID=Page6085592;
    DrillDownPageID=Page6085592;
  }
  FIELDS
  {
    { 1   ;   ;Template No.        ;Code20        ;TableRelation=Template;
                                                   CaptionML=[DEU=Vorlagennr.;
                                                              ENU=Template No.;
                                                              NLD=Sjabloonnr.;
                                                              NOR=Malnr.;
                                                              SVE=Mallnr];
                                                   NotBlank=Yes }
    { 2   ;   ;Type                ;Option        ;CaptionML=[DEU=Art;
                                                              ENU=Type;
                                                              NLD=Soort;
                                                              NOR=Radtype;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=[DEU=Kopffeld,Zeilenfeld,,,Formel;
                                                                    ENU=Header Field,Line Field,,,Formula;
                                                                    NLD=Header Field,Line Field,,,Formule];
                                                   OptionString=Header Field,Line Field,,,Formula }
    { 3   ;   ;Field Code          ;Code20        ;TableRelation=IF (Type=CONST(Header Field)) "Template Field".Code WHERE (Template No.=FIELD(Template No.),
                                                                                                                            Type=FIELD(Type))
                                                                                                                            ELSE IF (Type=CONST(Line Field)) "Template Field".Code WHERE (Template No.=FIELD(Template No.),
                                                                                                                                                                                          Type=FIELD(Type));
                                                   CaptionML=[DEU=Feldcode;
                                                              ENU=Field Code;
                                                              NLD=Veldcode] }
    { 5   ;   ;Translate From      ;Code150       ;CaptionML=[DEU=öbersetzen ab;
                                                              ENU=Translate From;
                                                              NLD=Vertaal van] }
    { 6   ;   ;Field Code / Formula;Text250       ;TableRelation="Template Field".Code WHERE (Template No.=FIELD(Template No.),
                                                                                              Type=CONST(Header),
                                                                                              Data Type=CONST(Number));
                                                   ValidateTableRelation=No;
                                                   CaptionML=[DEU=Feldcode/Formel;
                                                              ENU=Field Code / Formula;
                                                              NLD=Veldcode/formule] }
    { 10  ;   ;Field Description   ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Template Field"."Field Name" WHERE (Template No.=FIELD(Template No.),
                                                                                                           Type=FIELD(Type),
                                                                                                           Code=FIELD(Field Code)));
                                                   CaptionML=[DEU=Feldbeschreibung;
                                                              ENU=Field Description;
                                                              NLD=Veldomschrijving;
                                                              NOR=Feltbeskrivelse;
                                                              SVE=FÑltbeskrivning];
                                                   Editable=No }
    { 90  ;   ;Transl. to VAT Prod. Post.Grp.;Code20;
                                                   TableRelation="VAT Product Posting Group";
                                                   CaptionML=[DEU=Zu MwSt.-Produktbuchungsgruppe Åbersetzen;
                                                              ENU=Translate to VAT Prod. Posting Group;
                                                              NLD=Vertaal naar btw productboekingsgroep] }
    { 100 ;   ;Translate to Type   ;Option        ;OnValidate=BEGIN
                                                                IF "Translate to Type" = "Translate to Type"::"Amount Distribution Code" THEN
                                                                  TESTFIELD(Type,Type::"Header Field");
                                                                VALIDATE("Translate to No.",'');
                                                              END;

                                                   CaptionML=[DEU=öbersetzen zu Art;
                                                              ENU=Translate to Type;
                                                              NLD=Vertaal naar Type];
                                                   OptionCaptionML=[DEU=" ,Sachkonto,Artikel,,Feste Anlage,GebÅhr (Artikel),,Betragsverteilungscodes";
                                                                    ENU=" ,G/L Account,Item,,Fixed Asset,Charge (Item),,Amount Distribution Code";
                                                                    NLD=" ,Grootboek,Artikel,,Vast activum,Toeslag (Artikel),,Bedragverdelingscodes"];
                                                   OptionString=[ ,G/L Account,Item,Resource,Fixed Asset,Charge (Item),,Amount Distribution Code] }
    { 101 ;   ;Translate to No.    ;Code20        ;TableRelation=IF (Translate to Type=CONST(" ")) "Standard Text"
                                                                 ELSE IF (Translate to Type=CONST(G/L Account)) "G/L Account"
                                                                 ELSE IF (Translate to Type=CONST(Item)) Item
                                                                 ELSE IF (Translate to Type=CONST(Resource)) Resource
                                                                 ELSE IF (Translate to Type=CONST(Fixed Asset)) "Fixed Asset"
                                                                 ELSE IF (Translate to Type=CONST("Charge (Item)")) "Item Charge"
                                                                 ELSE IF (Translate to Type=CONST(Amount Distribution Code)) "Std. Amount Distribution Code";
                                                   OnValidate=VAR
                                                                GLAccount@1000000000 : Record 15;
                                                              BEGIN
                                                                IF ("Translate to Type" = "Translate to Type"::"G/L Account") AND ("Translate to No." <> '') THEN BEGIN
                                                                  GLAccount.GET("Translate to No.");
                                                                  GLAccount.TESTFIELD(Blocked,FALSE);
                                                                  GLAccount.TESTFIELD("Direct Posting",TRUE);
                                                                END ELSE
                                                                  VALIDATE("Translate to UOM Code",'');
                                                              END;

                                                   CaptionML=[DEU=öbersetzen zu Nr.;
                                                              ENU=Translate to No.;
                                                              NLD=Vertaal naar nummer] }
    { 102 ;   ;Translate to UOM Code;Code10       ;TableRelation=IF (Translate to Type=CONST(Item)) "Item Unit of Measure".Code WHERE (Item No.=FIELD(Translate to No.));
                                                   CaptionML=[DEU=öbersetzen zu Ma·einheitencode;
                                                              ENU=Translate to UOM Code;
                                                              NLD=Vertaal naar eenheidscode] }
    { 103 ;   ;Translate to (Text) ;Text250       ;CaptionML=[DEU=öbersetzen zu (Text);
                                                              ENU=Translate to (Text);
                                                              NLD=Vertaal naar (Tekst)] }
    { 104 ;   ;Translate to Variant Code;Code10   ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Translate to No.));
                                                   CaptionML=[DEU=öbersetzen zu Variantencode;
                                                              ENU=Translate to Variant Code;
                                                              NLD=Vertaal naar Variantcode] }
  }
  KEYS
  {
    {    ;Template No.,Type,Field Code,Translate From;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      GLSetup@161024013 : Record 98;
      DimMgt@161024015 : Codeunit 408;
      GLSetupShortcutDimCode@161024014 : ARRAY [8] OF Code[20];
      HasGotGLSetup@161024012 : Boolean;

    PROCEDURE ShowShortcutDimCode@161024012(VAR ShortcutDimCode@161024012 : ARRAY [8] OF Code[20]);
    VAR
      i@161024013 : Integer;
    BEGIN
      GetGLSetup;
      FOR i := 1 TO 8 DO
        ShortcutDimCode[i] := GetDimValue(GLSetupShortcutDimCode[i]);
    END;

    LOCAL PROCEDURE GetGLSetup@52();
    BEGIN
      IF NOT HasGotGLSetup THEN BEGIN
        GLSetup.GET;
        GLSetupShortcutDimCode[1] := GLSetup."Shortcut Dimension 1 Code";
        GLSetupShortcutDimCode[2] := GLSetup."Shortcut Dimension 2 Code";
        GLSetupShortcutDimCode[3] := GLSetup."Shortcut Dimension 3 Code";
        GLSetupShortcutDimCode[4] := GLSetup."Shortcut Dimension 4 Code";
        GLSetupShortcutDimCode[5] := GLSetup."Shortcut Dimension 5 Code";
        GLSetupShortcutDimCode[6] := GLSetup."Shortcut Dimension 6 Code";
        GLSetupShortcutDimCode[7] := GLSetup."Shortcut Dimension 7 Code";
        GLSetupShortcutDimCode[8] := GLSetup."Shortcut Dimension 8 Code";
        HasGotGLSetup := TRUE;
      END;
    END;

    PROCEDURE GetDimValue@161024013(DimCode@161024012 : Code[20]) : Code[20];
    VAR
      DataTranslDim@161024013 : Record 6085586;
    BEGIN
      IF DataTranslDim.GET("Template No.",Type,"Field Code","Translate From",DimCode) THEN
        EXIT(DataTranslDim."Dimension Value Code");
    END;

    PROCEDURE LookupShortcutDimCode@161024014(DimensionNo@161024012 : Integer;VAR ValueCode@161024013 : Code[20]);
    BEGIN
      DimMgt.LookupDimValueCode(DimensionNo,ValueCode);
      UpdateDimCode(DimensionNo,ValueCode);
    END;

    PROCEDURE ValidateShortcutDimCode@161024020(DimensionNo@161024012 : Integer;VAR ValueCode@161024013 : Code[20]);
    BEGIN
      DimMgt.ValidateDimValueCode(DimensionNo,ValueCode);
      UpdateDimCode(DimensionNo,ValueCode);
    END;

    PROCEDURE UpdateDimCode@161024015(DimensionNo@161024013 : Integer;ValueCode@161024012 : Code[20]);
    VAR
      DataTranslDim@161024014 : Record 6085586;
    BEGIN
      GetGLSetup;

      IF ValueCode = '' THEN BEGIN
        IF DataTranslDim.GET("Template No.",Type,"Field Code","Translate From",GLSetupShortcutDimCode[DimensionNo]) THEN
          DataTranslDim.DELETE(TRUE);
      END ELSE BEGIN
        IF DataTranslDim.GET("Template No.",Type,"Field Code","Translate From",GLSetupShortcutDimCode[DimensionNo]) THEN BEGIN
          DataTranslDim.VALIDATE("Dimension Value Code",ValueCode);
          DataTranslDim.MODIFY(TRUE);
        END ELSE BEGIN
          DataTranslDim."Template No." := "Template No.";
          DataTranslDim."Field Type" := Type;
          DataTranslDim."Field Code" := "Field Code";
          DataTranslDim."Translate From" := "Translate From";
          DataTranslDim."Dimension Code" := GLSetupShortcutDimCode[DimensionNo];
          DataTranslDim."Dimension Value Code" := ValueCode;
          DataTranslDim.INSERT(TRUE);
        END;
      END;
    END;

    PROCEDURE DeleteDims@161024016();
    VAR
      DataTranslDim@161024012 : Record 6085586;
    BEGIN
      DataTranslDim.SETRANGE("Template No.","Template No.");
      DataTranslDim.SETRANGE("Field Type",Type);
      DataTranslDim.SETRANGE("Field Code","Field Code");
      DataTranslDim.SETRANGE("Translate From","Translate From");
      DataTranslDim.DELETEALL(TRUE);
    END;

    BEGIN
    END.
  }
}

