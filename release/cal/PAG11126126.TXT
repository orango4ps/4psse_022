OBJECT Page 11126126 Serv. Surv. Obj. Add. Act. SF
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=ErgÑnzende TÑtigkeiten ServiceÅbersicht SF;
               ENU=Service Survey Additional Activities SF;
               NLD=Inventarisatie aanvullende werkzaamheden SF];
    SourceTable=Table11071814;
    DelayedInsert=Yes;
    PageType=ListPart;
    AutoSplitKey=Yes;
    OnInit=BEGIN
             RateCodeEditable := TRUE;
             NormEditable := TRUE;
             TradeItemEditable := TRUE;
             VendorTradeItemEditable := TRUE;
             BasicItemEditable := TRUE;
             ManufacturerEditable := TRUE;
             ItemNoEditable := TRUE;
             CostPriceEditable := TRUE;
             PurchaseDiscountEditable := TRUE;
             GrossPriceEditable := TRUE;
             RateEditable := TRUE;
           END;

    OnClosePage=BEGIN
                  IF RecordsModified THEN
                    UpdateDetailTotals;  //DP00978
                END;

    OnAfterGetRecord=VAR
                       AdjustmentFactorNormLevel@1100528600 : Option;
                     BEGIN
                       AdjustmentFactorNorm :=
                         GetAdjustmentFactorNorm("Survey No.", "Location Code", "Seq. No.", "Maintenance Scheme Code", WORKDATE, AdjustmentFactorNormLevel);
                       UpdateEditable;
                     END;

    OnNewRecord=VAR
                  ServSurvLocObjDetail@1100528801 : Record 11071807;
                BEGIN
                  CheckFilterGroup;  //DP00978
                  IF FilterSeqNo = '' THEN  //DP00978
                    IF GETFILTER("Seq. No.") = '' THEN
                      ERROR(Text000);
                  UpdateEditable;
                  "Line Type" := "Line Type"::"Survey Object";
                  AdjustmentFactorNorm := 1;
                  IF ServSurvLocObjDetail.GET("Survey No.", "Location Code", "Seq. No.", "Maintenance Scheme Code") THEN
                    VALIDATE("Maintenance Interval", ServSurvLocObjDetail."Maintenance Interval");
                END;

    OnInsertRecord=BEGIN
                     RecordsModified := TRUE;
                   END;

    OnModifyRecord=BEGIN
                     RecordsModified := TRUE;
                   END;

    OnDeleteRecord=BEGIN
                     RecordsModified := TRUE;
                   END;

    ActionList=ACTIONS
    {
      { 1100525000;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100525001;1 ;Action    ;
                      Name=Internal Text;
                      CaptionML=[DEU=Text&block;
                                 ENU=&Text;
                                 NLD=Tekst&blok;
                                 NOR=&Tekst;
                                 SVE=&Text];
                      Image=SNInfo;
                      OnAction=BEGIN
                                 EditText;  //C029478
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100528600;1;Group  ;
                GroupType=Repeater }

    { 1100528601;2;Field  ;
                SourceExpr="Survey No.";
                Visible=FALSE }

    { 1100528603;2;Field  ;
                SourceExpr="Location Code";
                Visible=FALSE }

    { 1100528609;2;Field  ;
                SourceExpr="Seq. No.";
                Visible=FALSE;
                Editable=FALSE }

    { 1100528613;2;Field  ;
                SourceExpr="Maintenance Scheme Code";
                Visible=FALSE;
                Editable=FALSE }

    { 1100528641;2;Field  ;
                SourceExpr="Cost Type";
                OnValidate=BEGIN
                             StoreValues;  //DP00978
                             CostTypeOnAfterValidate;
                           END;
                            }

    { 1100528647;2;Field  ;
                SourceExpr="Item No.";
                Editable=ItemNoEditable;
                OnValidate=BEGIN
                             StoreValues;  //DP00978
                           END;
                            }

    { 1100528651;2;Field  ;
                SourceExpr=Manufacturer;
                Visible=FALSE;
                Editable=ManufacturerEditable }

    { 1100528649;2;Field  ;
                SourceExpr="Basic Item";
                Visible=FALSE;
                Editable=BasicItemEditable }

    { 1100528655;2;Field  ;
                SourceExpr="Vendor (Trade Item)";
                Editable=VendorTradeItemEditable }

    { 1100528653;2;Field  ;
                SourceExpr="Trade Item";
                Editable=TradeItemEditable;
                OnValidate=BEGIN
                             StoreValues;  //DP00978
                           END;

                OnAssistEdit=BEGIN
                               IF HandleAssistEditTradeItem THEN
                                 CurrPage.UPDATE(TRUE);
                             END;
                              }

    { 1100528665;2;Field  ;
                SourceExpr="Item Brand";
                Visible=FALSE }

    { 1100528663;2;Field  ;
                SourceExpr="Item Type";
                Visible=FALSE }

    { 1100528667;2;Field  ;
                SourceExpr="Item Series";
                Visible=FALSE }

    { 1100528643;2;Field  ;
                SourceExpr="Cost Object";
                OnValidate=BEGIN
                             StoreValues;  //DP00978
                           END;
                            }

    { 1100528645;2;Field  ;
                SourceExpr="Cost Component" }

    { 1100528617;2;Field  ;
                SourceExpr=Description }

    { 1100528671;2;Field  ;
                SourceExpr="Text Block";
                OnLookup=BEGIN
                           EditText;  //C029478
                         END;
                          }

    { 1100528619;2;Field  ;
                SourceExpr=Quantity;
                OnValidate=BEGIN
                             StoreValues;  //DP00978
                           END;
                            }

    { 1100528621;2;Field  ;
                SourceExpr="Unit of Measure" }

    { 1100528669;2;Field  ;
                SourceExpr="Line Status" }

    { 1100528625;2;Field  ;
                SourceExpr=Norm;
                Editable=NormEditable;
                OnValidate=BEGIN
                             StoreValues;  //DP00978
                           END;
                            }

    { 1100528627;2;Field  ;
                SourceExpr="Rate Code";
                Editable=RateCodeEditable;
                OnValidate=BEGIN
                             StoreValues;  //DP00978
                             UpdateEditable;
                           END;
                            }

    { 1100528629;2;Field  ;
                SourceExpr=Rate;
                Editable=RateEditable;
                OnValidate=BEGIN
                             StoreValues;  //DP00978
                             UpdateEditable;
                           END;
                            }

    { 1100528633;2;Field  ;
                SourceExpr="Gross Price";
                Editable=GrossPriceEditable;
                OnValidate=BEGIN
                             StoreValues;  //DP00978
                           END;
                            }

    { 1100528635;2;Field  ;
                SourceExpr="Purchase Discount %";
                Editable=PurchaseDiscountEditable;
                OnValidate=BEGIN
                             StoreValues;  //DP00978
                           END;
                            }

    { 1100528631;2;Field  ;
                SourceExpr="Cost Price";
                Editable=CostPriceEditable;
                OnValidate=BEGIN
                             StoreValues;  //DP00978
                           END;
                            }

    { 1100528637;2;Field  ;
                CaptionML=[DEU=Betrag;
                           ENU=Amount;
                           NLD=Bedrag;
                           NOR=Belõp;
                           SVE=Belopp];
                DecimalPlaces=2:5;
                SourceExpr=GetCostAmount }

    { 1100528639;2;Field  ;
                CaptionML=[DEU=Stunden;
                           ENU=Hours;
                           NLD=Uren;
                           NOR="Timer ";
                           SVE=Timmar];
                DecimalPlaces=2:5;
                SourceExpr=GetHours }

    { 1100528659;2;Field  ;
                SourceExpr=Vendor }

    { 1100528661;2;Field  ;
                SourceExpr="Vendor Name";
                Visible=FALSE }

    { 1100528602;2;Field  ;
                CaptionML=[DEU=Norm Korrekturfaktor;
                           ENU=Adjustment Factor Norm;
                           NLD=Norm correctiefactor];
                SourceExpr=AdjustmentFactorNorm;
                OnValidate=VAR
                             AdjustmentFactorNormLevel@1100528600 : Option;
                           BEGIN
                             SetAdjustmentFactorNorm(AdjustmentFactorNorm, "Survey No.", "Location Code", "Seq. No.", "Maintenance Scheme Code");
                             AdjustmentFactorNorm :=
                               GetAdjustmentFactorNorm("Survey No.", "Location Code", "Seq. No.", "Maintenance Scheme Code", WORKDATE, AdjustmentFactorNormLevel);
                             StoreValues;
                           END;
                            }

    { 1100528800;2;Field  ;
                SourceExpr="Cost per Service" }

  }
  CODE
  {
    VAR
      GrossPriceEditable@19075252 : Boolean INDATASET;
      PurchaseDiscountEditable@19020410 : Boolean INDATASET;
      CostPriceEditable@19074279 : Boolean INDATASET;
      ItemNoEditable@19055681 : Boolean INDATASET;
      ManufacturerEditable@19042322 : Boolean INDATASET;
      BasicItemEditable@19063333 : Boolean INDATASET;
      VendorTradeItemEditable@19034316 : Boolean INDATASET;
      TradeItemEditable@19000132 : Boolean INDATASET;
      NormEditable@19001717 : Boolean INDATASET;
      RateCodeEditable@19068793 : Boolean INDATASET;
      RateEditable@1100525000 : Boolean INDATASET;
      AdjustmentFactorNorm@1100528600 : Decimal;
      Text000@1100528601 : TextConst 'DEU=HinzugefÅgte Zeilen sind nur auf Objektebene zulÑssig;ENU=Added lines only allowed on object level;NLD=Toevoegen van een regel is alleen op object niveau mogelijk';
      RecordsModified@1100528602 : Boolean;
      FilterSurveyNo@1100525001 : Text[250];
      FilterLocationCode@1100525002 : Text[250];
      FilterSeqNo@1100525003 : Text[250];
      FilterMaintenanceSchemeCode@1100525004 : Text[250];

    PROCEDURE UpdateEditable@1100528601();
    BEGIN
      ItemNoEditable := ("Cost Type" = "Cost Type"::Material);
      ManufacturerEditable := ("Cost Type" = "Cost Type"::Material);
      BasicItemEditable := ("Cost Type" = "Cost Type"::Material);
      VendorTradeItemEditable := ("Cost Type" = "Cost Type"::Material);
      TradeItemEditable := ("Cost Type" = "Cost Type"::Material);
      NormEditable := "Cost Type" = ("Cost Type"::Labor);
      RateCodeEditable := "Cost Type" = ("Cost Type"::Labor);
      RateEditable := (
        (("Cost Type" = "Cost Type"::Labor) OR
         ("Cost Type" = "Cost Type"::Subcontracting)) AND
         ("Rate Code" = ''));
      GrossPriceEditable := (Rate = 0);
      PurchaseDiscountEditable := (Rate = 0);
      CostPriceEditable := (Rate = 0);
    END;

    LOCAL PROCEDURE CostTypeOnAfterValidate@19000073();
    BEGIN
      UpdateEditable;
    END;

    LOCAL PROCEDURE CheckFilterGroup@1100525000();
    BEGIN
      //DP00978
      FILTERGROUP(4);
      FilterSurveyNo := GETFILTER("Survey No.");
      FilterLocationCode := GETFILTER("Location Code");
      FilterSeqNo := GETFILTER("Seq. No.");
      FilterMaintenanceSchemeCode := GETFILTER("Maintenance Scheme Code");
      FILTERGROUP(0);
    END;

    LOCAL PROCEDURE StoreValues@1100525001();
    BEGIN
      //DP00978
      CurrPage.SAVERECORD;
      UpdateDetailTotals;
      CurrPage.UPDATE(TRUE);
    END;

    LOCAL PROCEDURE UpdateDetailTotals@1100525002();
    VAR
      ServSurvLocObjDetail@1100525000 : Record 11071807;
    BEGIN
      //DP00978
      CheckFilterGroup;
      IF FilterSurveyNo <> '' THEN
        ServSurvLocObjDetail.SETFILTER("Survey No.", FilterSurveyNo)
      ELSE
        ServSurvLocObjDetail.SETFILTER("Survey No.", GETFILTER("Survey No."));
      IF FilterLocationCode <> '' THEN
        ServSurvLocObjDetail.SETFILTER("Location Code", FilterLocationCode)
      ELSE
        ServSurvLocObjDetail.SETFILTER("Location Code", GETFILTER("Location Code"));
      IF FilterSeqNo <> '' THEN
        ServSurvLocObjDetail.SETFILTER("Seq. No.", FilterSeqNo)
      ELSE
        ServSurvLocObjDetail.SETFILTER("Seq. No.", GETFILTER("Seq. No."));
      IF FilterMaintenanceSchemeCode <> '' THEN
        ServSurvLocObjDetail.SETFILTER("Maintenance Scheme Code", FilterMaintenanceSchemeCode)
      ELSE
        ServSurvLocObjDetail.SETFILTER("Maintenance Scheme Code", GETFILTER("Maintenance Scheme Code"));
      IF ServSurvLocObjDetail.FINDSET THEN BEGIN
        REPEAT
          ServSurvLocObjDetail.UpdateTotals;
          ServSurvLocObjDetail.MODIFY;
          ServSurvLocObjDetail.UpdateTotalsObject;
        UNTIL ServSurvLocObjDetail.NEXT = 0;
      END;
    END;

    PROCEDURE EditText@8();
    VAR
      TextFrm@11012001 : Page 11012120;
    BEGIN
      //C029478
      IF "Line No." = 0 THEN EXIT;

      TextFrm.SetSelection(DATABASE::"Serv. Surv. Object Add. Act.","Survey No.","Location Code",
        "Maintenance Scheme Code",'','','',"Seq. No.","Line No.");

      TextFrm.RUNMODAL;
      CLEAR(TextFrm);
    END;

    BEGIN
    END.
  }
}

