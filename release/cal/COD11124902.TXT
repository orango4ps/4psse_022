OBJECT Codeunit 11124902 Resource Request Invoice Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE ReleaseCollectiveList@1100529000(VAR Rec@1100529000 : Record 11124904);
    VAR
      ResourceReqCollectiveList@1100529001 : Record 11124904;
      ResourceReqCollListLine@1100529005 : Record 11124905;
      ResourceRequest@1100529004 : Record 11124901;
    BEGIN
      ResourceReqCollectiveList.COPY(Rec);
      WITH ResourceReqCollectiveList DO BEGIN
        TESTFIELD("No.");
        TESTFIELD(Status, Status::Open);
        TESTFIELD("Customer No.");
        IF "Collect Invoices by" <> "Collect Invoices by"::Request THEN
          TESTFIELD("Request No.", '')
        ELSE BEGIN
          TESTFIELD("Request No.");
          ResourceRequest.GET("Request No.");
          ResourceRequest.TESTFIELD("Source Type", ResourceRequest."Source Type"::Customer);
          ResourceRequest.TESTFIELD("Source No.", "Customer No.");
        END;

        ResourceReqCollListLine.SETRANGE("Collective List No.", "No.");
        ResourceReqCollListLine.FINDSET; //Must be at least one line
        REPEAT
          IF ResourceReqCollListLine."Line Type" = ResourceReqCollListLine."Line Type"::Resource THEN
            ResourceReqCollListLine.TESTFIELD("Resource No.");
          ResourceReqCollListLine.TESTFIELD(Description);
          ResourceReqCollListLine.TESTFIELD("Global Dimension 2 Code");
          ResourceReqCollListLine.TESTFIELD(Quantity);
          IF ResourceReqCollListLine."Discount %" <> 100 THEN
            ResourceReqCollListLine.TESTFIELD(Amount);
          IF "Collect Invoices by" = "Collect Invoices by"::Request THEN
            ResourceReqCollListLine.TESTFIELD("Request No.", "Request No.")
          ELSE BEGIN
            ResourceRequest.GET(ResourceReqCollListLine."Request No.");
            ResourceRequest.TESTFIELD("Source Type", ResourceRequest."Source Type"::Customer);
            ResourceRequest.TESTFIELD("Source No.", "Customer No.");
          END;
        UNTIL ResourceReqCollListLine.NEXT = 0;

        VALIDATE(Status, Status::Released);
        MODIFY(FALSE);
      END;
      Rec := ResourceReqCollectiveList;
    END;

    PROCEDURE ReopenCollectiveList@1100529001(VAR Rec@1100529000 : Record 11124904);
    VAR
      ResourceReqCollectiveList@1100529001 : Record 11124904;
    BEGIN
      ResourceReqCollectiveList.COPY(Rec);
      WITH ResourceReqCollectiveList DO BEGIN
        TESTFIELD("No.");
        TESTFIELD(Status, Status::Released);

        VALIDATE(Status, Status::Open);
        MODIFY(FALSE);
      END;
      Rec := ResourceReqCollectiveList;
    END;

    BEGIN
    END.
  }
}

