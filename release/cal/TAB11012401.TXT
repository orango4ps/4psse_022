OBJECT Table 11012401 Document Template
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               lDocTemplRec@1210190000 : Record 11012401;
               DocumentDataItem@1199197401 : Record 11012140;
               DocumentDataItemLink@1199197400 : Record 11012141;
               FilterText@1100485000 : Text[50];
             BEGIN
               FilterText := '@';
               FilterText := FilterText + Rec."Prefix File Name";
               lDocTemplRec.SETFILTER("Prefix File Name",FilterText);

               lDocTemplRec.SETRANGE("Document Group","Document Group");
               lDocTemplRec.SETFILTER("Template Code",'<>%1',"Template Code");
               lDocTemplRec.SETRANGE(Type,Type);
               IF lDocTemplRec.FIND('-') THEN
                 ERROR(Text002,UPPERCASE("Prefix File Name"),Type);
               lDocTemplRec.SETRANGE(Type);
               IF lDocTemplRec.FIND('-') THEN
                 MESSAGE(Text001,UPPERCASE("Prefix File Name"));

               //create the basic tables (call 21883)
               DocumentDataItem."Document Group" := "Document Group";
               DocumentDataItem.Document := "Template Code";
               DocumentDataItem."Line No." := 10000;
               DocumentDataItem."Table No." := DATABASE::"Document Properties";
               DocumentDataItem.INSERT(TRUE);
               DocumentDataItem."Line No." := 15000;
               DocumentDataItem."Table No." := DATABASE::"Document Link";
               DocumentDataItem.INSERT(TRUE);

               DocumentDataItemLink."Document Group" := "Document Group";
               DocumentDataItemLink.Document := "Template Code";
               DocumentDataItemLink."Data Item Line No." := 15000;
               DocumentDataItemLink."From Table No." := DATABASE::"Document Link";
               DocumentDataItemLink."To Table No." := DATABASE::"Document Properties";
               DocumentDataItemLink."Field No." := 10;
               DocumentDataItemLink."Reference Field No." := 1;
               DocumentDataItemLink.INSERT(TRUE);
             END;

    OnModify=BEGIN
               CheckDocumentPortalSettings;
               "Modified by" := USERID;
               "Last Date Modified" := TODAY;
             END;

    OnDelete=VAR
               PermissionRec@1210190000 : Record 11012404;
               lDocTableRec@1210190004 : Record 11012140;
               lDocLinkRec@1210190003 : Record 11012141;
               lDocFilterRec@1210190002 : Record 11012143;
               lDocFieldRec@1210190001 : Record 11012142;
             BEGIN
               PermissionRec.SETRANGE("Document Group","Document Group");
               PermissionRec.SETRANGE("Template Code","Template Code");
               PermissionRec.DELETEALL;

               lDocFieldRec.SETRANGE("Document Group", "Document Group");
               lDocFieldRec.SETRANGE(Document, "Template Code");
               lDocFieldRec.DELETEALL;

               lDocFilterRec.SETRANGE("Document Group", "Document Group");
               lDocFilterRec.SETRANGE(Document, "Template Code");
               lDocFilterRec.DELETEALL;

               lDocLinkRec.SETRANGE("Document Group", "Document Group");
               lDocLinkRec.SETRANGE(Document, "Template Code");
               lDocLinkRec.DELETEALL;

               lDocTableRec.SETRANGE("Document Group", "Document Group");
               lDocTableRec.SETRANGE(Document, "Template Code");
               lDocTableRec.DELETEALL;
             END;

    OnRename=VAR
               DocumentProperties@1100525000 : Record 11012746;
               DocumentDataItem@1100527800 : Record 11012140;
             BEGIN
               //**4PS01.n
               DocumentDataItem.SETRANGE("Document Group",xRec."Document Group");
               DocumentDataItem.SETRANGE(Document,xRec."Template Code");
               IF (DocumentDataItem.FINDFIRST) THEN
                 ERROR(unableToRenameTxt)
               ELSE
               BEGIN
                 DocumentProperties.HandleRenameofDocumentTemplate(COMPANYNAME,xRec."Document Group",
                   xRec."Template Code","Document Group","Template Code");

                 "Modified by" := USERID;
                 "Last Date Modified" := TODAY;
               END;
             END;

    CaptionML=[ENU=Document Template;
               NOR=Dokumentmal;
               SVE=Dokumentmall];
    LookupPageID=Page11012401;
    DrillDownPageID=Page11012401;
  }
  FIELDS
  {
    { 1   ;   ;Document Group      ;Code10        ;TableRelation="Document Group";
                                                   OnValidate=VAR
                                                                DocumentManagement@1100528401 : Codeunit 11012406;
                                                                TargetStorageURI@1100528400 : Text;
                                                              BEGIN
                                                                IF "Document Group" <> '' THEN
                                                                  DocumentManagement.GetStorageLocationForTemplates(
                                                                    "Document Group", TargetStorageURI, "Storage Type");
                                                              END;

                                                   CaptionML=[ENU=Document Group;
                                                              NOR=Dokumentgruppen finnes ikke;
                                                              SVE=Dokumentgrupp];
                                                   NotBlank=Yes }
    { 2   ;   ;Template Code       ;Code20        ;CaptionML=[ENU=Template Code;
                                                              NOR=Malkode;
                                                              SVE=Mallkod];
                                                   NotBlank=Yes }
    { 3   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 4   ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Typ];
                                                   OptionCaptionML=ENU=Congratulation Letter,Invitation Meeting,Extension Contract,Delivery Form,Invoice,Option List,Miscellaneous,Purchase Order,Credit Memo,Try-out Quote,Purchase Quote,KOMO Inspection Form,KOMO Finished Form,Blanket Order,Plant,Service Contract,Sales Quote,Sales Blanket Order,Sales Order,Try-Out,Service Order,Option Quote,Complaint,Assignment,Service Location,Estimate,Installment,Tender Application,Delivered Object,Job,Contact,LT. Maint. Plan,Rental Package,Plant Quote,Exit Order,Get Order SMF;
                                                   OptionString=Congratulation Letter,Invitation Meeting,Extension Contract,Delivery Form,Invoice,Option List,Miscellaneous,Purchase Order,Credit Memo,Try-out Quote,Purchase Quote,KOMO Inspection,KOMO Ready,Blanket Order,Plant,Service Contract,S-Quote,S-Blanket Order,S-Order,Try-Out,Service Order,Option Quote,Complaint,Assignment,Service Location,Estimate,Installment,TenderApplication,Delivered Object,Job,Contact,LT. Maint. Plan,Rental Package,Plant Quote,Exit Order,Get Order SMF }
    { 5   ;   ;Word Template       ;Text250       ;CaptionML=[ENU=Word Template;
                                                              NOR=Word-mal;
                                                              SVE=Word-mall] }
    { 6   ;   ;Prefix File Name    ;Text20        ;OnValidate=VAR
                                                                lDocTemplRec@1210190000 : Record 11012401;
                                                                FilterText@1100485000 : Text[50];
                                                              BEGIN
                                                                FilterText := '@';
                                                                FilterText := FilterText + Rec."Prefix File Name";
                                                                lDocTemplRec.SETFILTER("Prefix File Name",FilterText);

                                                                lDocTemplRec.SETRANGE("Document Group","Document Group");
                                                                lDocTemplRec.SETFILTER("Template Code",'<>%1',"Template Code");
                                                                lDocTemplRec.SETRANGE(Type,Type);
                                                                IF lDocTemplRec.FIND('-') THEN
                                                                  ERROR(Text002,UPPERCASE("Prefix File Name"),Type);
                                                                lDocTemplRec.SETRANGE(Type);
                                                                IF lDocTemplRec.FIND('-') THEN
                                                                  MESSAGE(Text001,UPPERCASE("Prefix File Name"));
                                                              END;

                                                   CaptionML=[ENU=Prefix File Name;
                                                              NOR=Prefiksfilnavn;
                                                              SVE=Prefixfilnamn] }
    { 15  ;   ;Document Type       ;Code10        ;OnValidate=BEGIN
                                                                CALCFIELDS("Document Type Description");
                                                              END;

                                                   OnLookup=VAR
                                                              DocumentType@1100525000 : Record 11012405;
                                                              SubsiteDocTypePPT@1100525001 : Record 11229811;
                                                              DPTDefaultSubsiteDocType@1100525002 : Record 11229942;
                                                            BEGIN
                                                              CASE TRUE OF
                                                                ("Project Portal Subsite" <> '') AND ("Document Portal Subsite" = ''): BEGIN
                                                                  SubsiteDocTypePPT.SETRANGE("Subsite Type", SubsiteDocTypePPT."Subsite Type"::Project);
                                                                  SubsiteDocTypePPT.SETRANGE("Project Portal Subsite", "Project Portal Subsite");
                                                                  SubsiteDocTypePPT."Document Type" := "Document Type";
                                                                  IF (PAGE.RUNMODAL(0, SubsiteDocTypePPT) = ACTION::LookupOK) THEN
                                                                    VALIDATE("Document Type", SubsiteDocTypePPT."Document Type");
                                                                END;
                                                                ("Document Portal Subsite Type" = "Document Portal Subsite Type"::Project) AND
                                                                ("Document Portal Subsite" <> ''):
                                                                BEGIN
                                                                  DPTDefaultSubsiteDocType.SETRANGE("Subsite Code", "Document Portal Subsite");
                                                                  SubsiteDocTypePPT."Document Type" := "Document Type";
                                                                  IF (PAGE.RUNMODAL(PAGE::"DPT Def Doc Types Subsite Proj", DPTDefaultSubsiteDocType) = ACTION::LookupOK) THEN
                                                                    VALIDATE("Document Type", DPTDefaultSubsiteDocType."Document Type");
                                                                END;
                                                                ("Document Portal Subsite Type" = "Document Portal Subsite Type"::ServLoc) AND
                                                                ("Document Portal Subsite" <> ''):
                                                                BEGIN
                                                                  DPTDefaultSubsiteDocType.SETRANGE("Subsite Code", "Document Portal Subsite");
                                                                  SubsiteDocTypePPT."Document Type" := "Document Type";
                                                                  IF (PAGE.RUNMODAL(PAGE::"DPT Def Doc Types Subsite SLoc", DPTDefaultSubsiteDocType) = ACTION::LookupOK) THEN
                                                                    VALIDATE("Document Type", DPTDefaultSubsiteDocType."Document Type");
                                                                END;
                                                                ELSE BEGIN
                                                                  DocumentType.Type := "Document Type";
                                                                  IF (PAGE.RUNMODAL(0, DocumentType) = ACTION::LookupOK) THEN
                                                                    VALIDATE("Document Type", DocumentType.Type);
                                                                END;
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Document Type;
                                                              NOR=Dokumenttype;
                                                              SVE=Dokumenttyp] }
    { 16  ;   ;Document Type Description;Text30   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Document Type".Description WHERE (Type=FIELD(Document Type)));
                                                   CaptionML=[ENU=Document Type Description;
                                                              NOR=Dokumenttype, Beskrivelse;
                                                              SVE=Dokumenttyp, beskrivning];
                                                   Editable=No }
    { 18  ;   ;Subject             ;Text100       ;CaptionML=[ENU=Subject;
                                                              NOR=Emne;
                                                              SVE=Ang†ende] }
    { 20  ;   ;Use for             ;Option        ;OnValidate=BEGIN
                                                                IF ("Use for" <> xRec."Use for") AND (xRec."Use for" = "Use for"::Document) THEN
                                                                  VALIDATE("Attach to E-Mail as", "Attach to E-Mail as"::Attachment)
                                                                ELSE
                                                                  VALIDATE("Attach to E-Mail as");
                                                              END;

                                                   CaptionML=[ENU=Use for;
                                                              NOR=Bruk for;
                                                              SVE=Anv„nds f”r];
                                                   OptionCaptionML=[ENU=Document,E-Mail,Document and E-Mail;
                                                                    NOR=Dokument,E-Mail,Dokument og E-Mail;
                                                                    SVE=Dokument,E-post,Dokument och e-post];
                                                   OptionString=Document,E-Mail,Document and E-Mail }
    { 25  ;   ;Attach to E-Mail as ;Option        ;OnValidate=BEGIN
                                                                IF "Use for" = "Use for"::Document THEN
                                                                  "Attach to E-Mail as" := "Attach to E-Mail as"::" "
                                                                ELSE
                                                                  TESTFIELD("Attach to E-Mail as");
                                                              END;

                                                   CaptionML=ENU=Attach to Email as;
                                                   OptionCaptionML=ENU=" ,Attachment,Body,Attachment as PDF";
                                                   OptionString=[ ,Attachment,Body,Attachment as PDF] }
    { 26  ;   ;E-Mail Subject      ;Text50        ;CaptionML=[ENU=Email Subject;
                                                              SVE=Epost „mne] }
    { 30  ;   ;Secured Document    ;Option        ;CaptionML=[ENU=Secured Document;
                                                              NOR=Beskyttet Dokument;
                                                              SVE=Skyddat dokument];
                                                   OptionCaptionML=[ENU=Not,Always,Both;
                                                                    NOR=Ikke,Alltid,Begge;
                                                                    SVE=Inte,Alltid,B†da];
                                                   OptionString=Not,Always,Both }
    { 40  ;   ;Secured Document Directory;Text250 ;OnValidate=VAR
                                                                FileMgt@1100525000 : Codeunit 419;
                                                              BEGIN
                                                                IF "Secured Document Directory" <> '' THEN
                                                                  TESTFIELD("Doc. Srv. Secured Doc. Subdir.", '');
                                                                FileMgt.AddBackSlashToDirectoryName("Secured Document Directory");
                                                              END;

                                                   CaptionML=[ENU=Secured Document Directory;
                                                              NOR=Beskyttet Dokumentkatalog;
                                                              SVE=Skyddad dokumentkatalog] }
    { 50  ;   ;Search Directory    ;BLOB          ;CaptionML=[ENU=Search Directory;
                                                              NOR=S›k katalog;
                                                              SVE=S”kkatalog];
                                                   SubType=User-Defined }
    { 60  ;   ;Project Mandatory   ;Option        ;CaptionML=[ENU=Project Mandatory;
                                                              NOR=Prosjektet er obligatorisk;
                                                              SVE=Projektet „r obligatoriskt];
                                                   OptionCaptionML=[ENU=" ,Mandatory,Warning";
                                                                    NOR=" ,Obligatorisk,Advarsel";
                                                                    SVE=" ,Obligatorisk,Varning"];
                                                   OptionString=[ ,Mandatory,Warning] }
    { 70  ;   ;Modified by         ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Modified by;
                                                              NOR=Endret av;
                                                              SVE=Žndrad av];
                                                   Editable=No }
    { 80  ;   ;Last Date Modified  ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              NOR=Endret den;
                                                              SVE=Uppdateringsdatum];
                                                   Editable=No }
    { 90  ;   ;Comment             ;Text100       ;CaptionML=[ENU=Comment;
                                                              NOR=Merknad;
                                                              SVE=Kommentar] }
    { 185 ;   ;Library Code        ;Code10        ;TableRelation="SharePoint Library";
                                                   CaptionML=[ENU=Library Code;
                                                              NOR=Bibliotekskode;
                                                              SVE=Bibliotekskod] }
    { 186 ;   ;Library Name        ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("SharePoint Library".Name WHERE (Code=FIELD(Library Code)));
                                                   CaptionML=[ENU=Library Name;
                                                              NOR=Biblioteksnavn;
                                                              SVE=Biblioteksnamn];
                                                   Editable=No }
    { 190 ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   OnValidate=VAR
                                                                DocumentMgtSetup@1100528701 : Record 11071831;
                                                                NoSeriesRelationship@1100528700 : Record 310;
                                                              BEGIN
                                                                IF "No. Series" <> '' THEN BEGIN
                                                                  DocumentMgtSetup.GET;
                                                                  NoSeriesRelationship.GET(DocumentMgtSetup."Document Nos.","No. Series")
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No. Series;
                                                              NOR=Nr-serie;
                                                              SVE=Nr-serier] }
    { 300 ;   ;Publish on Service Portal;Boolean  ;CaptionML=[ENU=Publish on Service Portal;
                                                              NOR=Publiser p† Serviceportal;
                                                              SVE=Publicera p† serviceportalen] }
    { 310 ;   ;Publish on Subcont.Portal;Boolean  ;CaptionML=[ENU=Publish on  Subcontractor Portal;
                                                              NOR=Publiser p† underentrepen›rportal;
                                                              SVE=Publicera p† underent.portal] }
    { 320 ;   ;Available in Field Service;Boolean ;CaptionML=[ENU=Available in Field Service;
                                                              NOR=Tilgjengelig i Feltservice] }
    { 325 ;   ;Project Portal Subsite Type;Option ;CaptionML=ENU=Document Portal Subsite Type;
                                                   OptionCaptionML=ENU=Project,Service Location;
                                                   OptionString=Project,ServLoc }
    { 330 ;   ;Project Portal Subsite;Code20      ;TableRelation="Project Portal Subsite".Code WHERE (Type=FIELD(Project Portal Subsite Type));
                                                   CaptionML=ENU=Document Portal Subsite }
    { 331 ;   ;Project Portal Subsite Descr;Text30;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Portal Subsite".Description WHERE (Code=FIELD(Project Portal Subsite)));
                                                   CaptionML=ENU=Document Portal Subsite Description;
                                                   Editable=No }
    { 340 ;   ;Private Company Document;Boolean   ;CaptionML=ENU=Private Company Document }
    { 400 ;   ;Document Portal Subsite Type;Option;CaptionML=ENU=Document Portal Subsite Type (Dual);
                                                   OptionCaptionML=[ENU=" ,Project,Service Location";
                                                                    SVE=,Project,Serviceplats];
                                                   OptionString=[ ,Project,ServLoc] }
    { 410 ;   ;Document Portal Subsite;Code20     ;TableRelation="DPT Default Subsite".Code WHERE (Source Type=FIELD(Document Portal Subsite Type));
                                                   CaptionML=ENU=Document Portal Subsite (Dual) }
    { 411 ;   ;Document Portal Subsite Descr;Text50;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Default Subsite".Description WHERE (Source Type=FIELD(Document Portal Subsite Type),
                                                                                                               Code=FIELD(Document Portal Subsite)));
                                                   CaptionML=ENU=Document Portal Subsite Description (Dual);
                                                   Editable=No }
    { 420 ;   ;Document Portal Private Doc;Boolean;CaptionML=ENU=Document Portal Private Document (Dual) }
    { 500 ;   ;D3 Document Type    ;Text30        ;CaptionML=[ENU=D3 Document Type;
                                                              NOR=D3-dokumenttype] }
    { 501 ;   ;D3 Document Link Name ID;Integer   ;CaptionML=ENU=D3 Document Link Name ID }
    { 502 ;   ;D3 Document Link Key ID;Integer    ;CaptionML=ENU=D3 Document Link Key ID }
    { 510 ;   ;Document Service URL;Text250       ;ExtendedDatatype=URL;
                                                   CaptionML=ENU=Document Service URL }
    { 520 ;   ;Doc. Srv. Secured Doc. Subdir.;Text250;
                                                   OnValidate=BEGIN
                                                                IF "Doc. Srv. Secured Doc. Subdir." <> '' THEN
                                                                  TESTFIELD("Secured Document Directory", '');
                                                              END;

                                                   CaptionML=ENU=Doc. Service Secured Document Subdir. }
    { 530 ;   ;Storage Type        ;Option        ;CaptionML=[ENU=Storage Type;
                                                              SVE=Lagringstyp];
                                                   OptionCaptionML=ENU=,File System on Premise,SharePoint Online,Azure File Storage;
                                                   OptionString=,File System on Premise,SharePoint Online,Azure File Storage;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Document Group,Template Code            ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100525000 : TextConst 'ENU=%1 %2 is already the %3 %4.';
      Text001@1210190000 : TextConst 'ENU=Usage of the same Prefix can lead to loss of documents.;NOR=Hvis samme Prefiks er i bruk kan det f›re til tap av dokumenter.;SVE=Om samma prefix anv„nds kan det leda till dokumentf”rlust.';
      Text002@1100485000 : TextConst 'ENU=Identical Prefix (%1) not allowed within same Document Type (%2);NOR=Identisk Prefiks (%1) er ikke tillatt innenfor samme dokumenttype (%2);SVE=Ett identiskt prefix (%1) „r inte till†tet inom samma dokumenttyp (%2)';
      unableToRenameTxt@1100527800 : TextConst 'ENU=Unable to change the Document Group or Template Code when the Template Lines are already inserted.';

    PROCEDURE UploadTemplateToCloud@1100528700();
    VAR
      FileManagement@1100528701 : Codeunit 419;
      DocumentManagement@1100527800 : Codeunit 11012406;
      ServerFileName@1100527801 : Text;
      MemoryStream@1100527802 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.MemoryStream";
      FileNameShort@1100527803 : Text;
      TargetStorageURI@1100527804 : Text;
      StorageType@1100527807 : Option;
    BEGIN
      ServerFileName := FileManagement.UploadFile('', '');
      IF ServerFileName = '' THEN
        EXIT;

      FileNameShort := FileManagement.GetFileName(ServerFileName);
      MemoryStream:= MemoryStream.MemoryStream;

      FileManagement.ReadFileContentToMemoryStream(ServerFileName,MemoryStream);
      DocumentManagement.GetStorageLocationForTemplates("Document Group",TargetStorageURI,StorageType);
      DocumentManagement.CreateStorageLocationIfNotExists(StorageType,TargetStorageURI);

      TargetStorageURI +=  FileNameShort;
      DocumentManagement.PutContentToStorageByStream(MemoryStream,StorageType,TargetStorageURI);
      ValidateStorageURI(StorageType,TargetStorageURI);

      "Word Template" := FileManagement.GetFileNameFromURI(StorageType,TargetStorageURI);
    END;

    LOCAL PROCEDURE CheckDocumentPortalSettings@1100525000();
    VAR
      DPTDefaultSubsiteDocType@1100525000 : Record 11229942;
      SubsiteDocTypePPT@1100525003 : Record 11229811;
      InstallationManagement@1100525001 : Codeunit 11012268;
    BEGIN
      //#C039464.n
      IF (Type <> Type::"Purchase Quote") THEN
        EXIT;

      IF (InstallationManagement.LicensePermissionForTableData(DATABASE::"Subsite-Doc Type (PPT)")) THEN BEGIN
        SubsiteDocTypePPT.SETRANGE("Subsite Type", SubsiteDocTypePPT."Subsite Type"::Project);
        SubsiteDocTypePPT.SETRANGE("Document Type", "Document Type");
        SubsiteDocTypePPT.SETRANGE("Default Quote Subsite", TRUE);
        IF (NOT SubsiteDocTypePPT.ISEMPTY) THEN
          ERROR(Text000, SubsiteDocTypePPT.FIELDCAPTION("Document Type"), SubsiteDocTypePPT."Document Type",
            SubsiteDocTypePPT.TABLECAPTION, SubsiteDocTypePPT.FIELDCAPTION("Default Quote Subsite"));
      END;

      IF (InstallationManagement.LicensePermissionForTableData(DATABASE::"DPT Default Subsite Doc Type")) THEN BEGIN
        DPTDefaultSubsiteDocType.SETRANGE("Source Type", DPTDefaultSubsiteDocType."Source Type"::Project);
        DPTDefaultSubsiteDocType.SETRANGE("Document Type", "Document Type");
        DPTDefaultSubsiteDocType.SETRANGE("Default Quote Subsite OLD", TRUE);
        IF (NOT DPTDefaultSubsiteDocType.ISEMPTY) THEN
          ERROR(Text000, DPTDefaultSubsiteDocType.FIELDCAPTION("Document Type"), DPTDefaultSubsiteDocType."Document Type",
            DPTDefaultSubsiteDocType.TABLECAPTION, DPTDefaultSubsiteDocType.FIELDCAPTION("Default Quote Subsite OLD"));
      END;
    END;

    PROCEDURE WriteContentToClient@1100527806(VAR ClientFile@1100527800 : Text) : Boolean;
    VAR
      ServerFile@1100527801 : Text;
      FileManagement@1100527802 : Codeunit 419;
    BEGIN
      ClientFile := '';
      IF (WriteContentToServerTempFile(ServerFile)) THEN BEGIN
        ClientFile := FileManagement.DownloadTempFile(ServerFile);
        CLEAR(ServerFile);
        EXIT(TRUE);
      END;
    END;

    PROCEDURE WriteContentToServerTempFile@1100527807(VAR ServerTempFile@1100527800 : Text) : Boolean;
    VAR
      MemoryStream@1100527801 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.MemoryStream";
      FileManagement@1100527802 : Codeunit 419;
      DocumentManagement@1100527803 : Codeunit 11012406;
      OStream@1100527804 : OutStream;
    BEGIN
      MemoryStream := MemoryStream.MemoryStream;
      IF GetContentInMemoryStream(MemoryStream) THEN BEGIN
        ServerTempFile := FileManagement.GetExtension("Word Template");
        ServerTempFile := FileManagement.ServerTempFileName(ServerTempFile);
        OStream := MemoryStream;
        DocumentManagement.WriteStreamContentToServerFile(ServerTempFile,OStream);
        MemoryStream.Dispose;
        CLEAR(MemoryStream);
        EXIT(TRUE);
      END;
    END;

    PROCEDURE GetContentInMemoryStream@1100527801(VAR MemoryStream@1100527802 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.MemoryStream") : Boolean;
    VAR
      DocumentServiceManagement@1100527805 : Codeunit 9510;
      AzureFileStorageManagement@1100527806 : Codeunit 11012350;
      FileManagement@1100527800 : Codeunit 419;
      DocumentGroup@1100527804 : Record 11012400;
    BEGIN
      CASE "Storage Type" OF
        0, //FIXME temporary added cause File Storage is not editable :(
        "Storage Type"::"File System on Premise":
          IF DocumentGroup.GET("Document Group") THEN
            EXIT(FileManagement.ReadFileContentToMemoryStreamFromClient(DocumentGroup."Template Directory" + "Word Template", MemoryStream));

        "Storage Type"::"SharePoint Online":
          BEGIN
            DocumentServiceManagement.SetUseDocumentService4PS(TRUE);
            DocumentServiceManagement.DownloadFileFromCloudToMemoryStream("Document Service URL", MemoryStream);
            EXIT(TRUE);
          END;

        "Storage Type"::"Azure File Storage":
          IF AzureFileStorageManagement.IsConfigured THEN BEGIN
            AzureFileStorageManagement.GetFileContentsViaMemoryStreamByResource("Document Service URL",MemoryStream);
            EXIT(TRUE);
          END;
      END;

      EXIT(FALSE);
    END;

    PROCEDURE ValidateStorageURI@1100527804(NewStorageType@1100527801 : Option;StorageURI@1100527800 : Text);
    BEGIN
      "Storage Type":= NewStorageType;
      "Document Service URL" := '';

      CASE "Storage Type" OF
        "Storage Type"::"Azure File Storage","Storage Type"::"SharePoint Online":
          VALIDATE("Document Service URL", StorageURI);
      END;
    END;

    PROCEDURE ShowSignhostSigners@1100528409(RecID@1100528400 : RecordID;DocumentProperties@1100528404 : Record 11012746;ContactCompanyNoFilter@1100528405 : Text);
    VAR
      SignhostSigner@1100528403 : Record 11229467;
      SignhostSigners@1100528402 : Page 11129726;
    BEGIN
      DocumentProperties.TESTFIELD("No.");
      SignhostSigner.SETRANGE("Record ID", RecID);
      SignhostSigner.SETRANGE("Word Document No.", DocumentProperties."No.");
      SignhostSigners.SETTABLEVIEW(SignhostSigner);
      SignhostSigners.SetContactCompanyNoFilter(ContactCompanyNoFilter);
      SignhostSigners.RUN;
    END;

    PROCEDURE GetTemplateFilePath@1100529600() : Text;
    VAR
      DocumentGroup@1100527804 : Record 11012400;
    BEGIN
      IF "Storage Type" = "Storage Type"::"File System on Premise" THEN BEGIN
        IF NOT DocumentGroup.GET("Document Group") THEN
          CLEAR(DocumentGroup);
        EXIT(DocumentGroup."Template Directory" + "Word Template");
      END;
      EXIT("Document Service URL");
    END;

    BEGIN
    {
      **4PS01 Jan ter Harmsel, Call 2094  At Rename also update references in documents.
      That table is on database level and will not be handled automatically.

      4PS02 Jan ter Harmsel, Call 2782  subject field added, this will be transferred to a document from form: 11012405.

      4PS 11-08-16 UKR-C25590 KD: Disinvestment of plant (BI036) - Option "Exit Order" added to field Type
    }
    END.
  }
}

