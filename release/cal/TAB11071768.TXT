OBJECT Table 11071768 Service Price Book
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ConfirmToCopyPreviousVersion(Rec);
             END;

    OnDelete=VAR
               ServiceUnitPriceLine@1100528600 : Record 11071769;
               UnitPriceRoomVersion@1100528601 : Record 11071982;
               ServiceUnitPriceSet@1100528602 : Record 11072284;
               PriceBookSurchargeSet@1100528603 : Record 11072315;
             BEGIN
               ServiceUnitPriceLine.SETRANGE("Price Book Type", Type);
               ServiceUnitPriceLine.SETRANGE("Price Book Code", Code);
               ServiceUnitPriceLine.SETRANGE("Index Date", "Index Date");
               ServiceUnitPriceLine.DELETEALL(TRUE);

               IF Type = Type::Customer THEN BEGIN
                 UnitPriceRoomVersion.SETRANGE("Customer No.", Code);
                 UnitPriceRoomVersion.SETRANGE("Index Date", "Index Date");
                 UnitPriceRoomVersion.DELETEALL(TRUE);
               END;

               ServiceUnitPriceSet.SETRANGE("Price Book Code", Code);
               ServiceUnitPriceSet.SETRANGE("Price Book Index Date", "Index Date");
               ServiceUnitPriceSet.DELETEALL(TRUE);

               PriceBookSurchargeSet.SETRANGE("Price Book Code", Code);
               PriceBookSurchargeSet.SETRANGE("Price Book Index Date", "Index Date");
               PriceBookSurchargeSet.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               ERROR(Text000);
             END;

    CaptionML=[ENU=Service Price Book;
               SVE=Enhetspris f”r service];
    LookupPageID=Page11125269;
  }
  FIELDS
  {
    { 10  ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Typ];
                                                   OptionCaptionML=[ENU=" ,Internal,Customer";
                                                                    NOR=" ,Intern,Kunde";
                                                                    SVE=" ,Intern,Kund"];
                                                   OptionString=,Internal,Customer;
                                                   NotBlank=Yes }
    { 20  ;   ;Code                ;Code20        ;TableRelation=IF (Type=CONST(Customer)) Customer.No.;
                                                   OnValidate=BEGIN
                                                                TestCodeIsUniqueWithinType;
                                                              END;

                                                   OnLookup=VAR
                                                              ServicePriceBook@1100528600 : Record 11071768;
                                                              Customer@1100528602 : Record 18;
                                                            BEGIN
                                                              CASE Type OF
                                                                Type::Internal:
                                                                  BEGIN
                                                                    ServicePriceBook.SETRANGE(Type, Type);
                                                                    IF PAGE.RUNMODAL(0, ServicePriceBook) = ACTION::LookupOK THEN BEGIN
                                                                      VALIDATE(Code, ServicePriceBook.Code);
                                                                      "Index Date" := ServicePriceBook."Index Date";
                                                                    END;
                                                                  END;
                                                                Type::Customer:
                                                                  IF PAGE.RUNMODAL(0, Customer) = ACTION::LookupOK THEN
                                                                    VALIDATE(Code, Customer."No.");
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 30  ;   ;Index Date          ;Date          ;CaptionML=[ENU=Index Date;
                                                              NOR=Indeksdato;
                                                              SVE=Indexdatum];
                                                   NotBlank=Yes }
    { 40  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 50  ;   ;File Location       ;Text250       ;CaptionML=[ENU=File Location;
                                                              NOR=Fillokasjon;
                                                              SVE=Filplats] }
    { 60  ;   ;Settlement Method   ;Option        ;CaptionML=[ENU=Settlement Method;
                                                              NOR=Oppgj›rsmetode;
                                                              SVE=Avr„kningsmetod];
                                                   OptionCaptionML=ENU=" ,Cost Plus,,Collective Installment Overview,Installment Overview by Service Order";
                                                   OptionString=[ ,Cost Plus,,Collective Installment Overview,Installment Overview by Service Order] }
    { 70  ;   ;Surcharge Set Code  ;Code10        ;TableRelation="Price Book Surcharge Set".Code WHERE (Price Book Code=FIELD(Code),
                                                                                                        Price Book Index Date=FIELD(Index Date));
                                                   CaptionML=ENU=Surcharge Set Code }
    { 500 ;   ;Budget Total Internal;Decimal      ;FieldClass=FlowField;
                                                   CalcFormula=Sum("SUP Internal Budget Line".Amount WHERE (Price Book Type=FIELD(Type),
                                                                                                            Price Book Code=FIELD(Code),
                                                                                                            Index Date=FIELD(Index Date)));
                                                   CaptionML=[ENU=Budget Total Internal;
                                                              NOR=Budsjett totalt, Internt;
                                                              SVE=Budget totalt, internt];
                                                   DecimalPlaces=2:2;
                                                   Editable=No }
    { 510 ;   ;Budget Total Customer;Decimal      ;FieldClass=FlowField;
                                                   CalcFormula=Sum("SUP Customer Budget Line".Total WHERE (Price Book Code=FIELD(Code),
                                                                                                           Index Date=FIELD(Index Date)));
                                                   CaptionML=[ENU=Budget Total Customer;
                                                              NOR=Budsjett Total, Kunde;
                                                              SVE=Budget totalt, kund];
                                                   DecimalPlaces=2:2;
                                                   Editable=No }
    { 520 ;   ;Budget Hours Internal;Decimal      ;FieldClass=FlowField;
                                                   CalcFormula=Sum("SUP Internal Budget Line".Hours WHERE (Price Book Type=FIELD(Type),
                                                                                                           Price Book Code=FIELD(Code),
                                                                                                           Index Date=FIELD(Index Date),
                                                                                                           Cost Type=CONST(Labor)));
                                                   CaptionML=[ENU=Budget Hours Internal;
                                                              NOR=Budsjett Timer, Internt;
                                                              SVE=Budgettimmar, internt];
                                                   DecimalPlaces=2:2;
                                                   Editable=No }
    { 521 ;   ;Budget Hours Plant Internal;Decimal;FieldClass=FlowField;
                                                   CalcFormula=Sum("SUP Internal Budget Line".Hours WHERE (Price Book Type=FIELD(Type),
                                                                                                           Price Book Code=FIELD(Code),
                                                                                                           Index Date=FIELD(Index Date),
                                                                                                           Cost Type=CONST(Plant)));
                                                   CaptionML=ENU=Budget Hours Plant Internal;
                                                   DecimalPlaces=2:2;
                                                   Description=DP02115;
                                                   Editable=No }
    { 530 ;   ;Budget Hours Customer;Decimal      ;FieldClass=FlowField;
                                                   CalcFormula=Sum("SUP Customer Budget Line"."Men Hours" WHERE (Price Book Code=FIELD(Code),
                                                                                                                 Index Date=FIELD(Index Date)));
                                                   CaptionML=[ENU=Budget Hours Customer;
                                                              NOR=Budsjett Timer, Kunde;
                                                              SVE=Budgettimmar, kund];
                                                   DecimalPlaces=2:2;
                                                   Editable=No }
    { 540 ;   ;Budget Labor Internal;Decimal      ;FieldClass=FlowField;
                                                   CalcFormula=Sum("SUP Internal Budget Line".Amount WHERE (Cost Type=CONST(Labor),
                                                                                                            Price Book Code=FIELD(Code),
                                                                                                            Index Date=FIELD(Index Date)));
                                                   CaptionML=[ENU=Budget Labor Internal;
                                                              NOR=Budsjett Arbeidskraft, Internt;
                                                              SVE=Budgetarbetskraft, internt];
                                                   DecimalPlaces=2:2;
                                                   Editable=No }
    { 550 ;   ;Budget Labor Customer;Decimal      ;FieldClass=FlowField;
                                                   CalcFormula=Sum("SUP Customer Budget Line".Labor WHERE (Price Book Code=FIELD(Code),
                                                                                                           Index Date=FIELD(Index Date)));
                                                   CaptionML=[ENU=Budget Labor Customer;
                                                              NOR=Budsjett Arbeidskraft, Kunde;
                                                              SVE=Budgetarbetskraft, kund];
                                                   DecimalPlaces=2:2;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Code,Index Date                         ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528600 : TextConst 'ENU=Rename is not allowed.;NOR=Gi nytt navn ikke tillatt;SVE=Att ge nytt namn „r inte till†tet.';
      Text001@1100528601 : TextConst 'ENU=Do you want to copy the previous version?;NOR=Vil du kopiere den forrige versjonen?;SVE=Vill du kopiera den f”reg†ende versionen?';
      Text002@1100528602 : TextConst 'ENU=%1 already in use with type %2';

    PROCEDURE ConfirmToCopyPreviousVersion@1100528600(VAR IServicePriceBook@1100528600 : Record 11071768);
    VAR
      ServicePriceBook@1100528601 : Record 11071768;
      UnitPriceRoomVersion@1100528604 : Record 11071982;
      CreatedUnitPriceRoomVersion@1100528606 : Record 11071982;
      ServiceUnitPriceManagement@1100528605 : Codeunit 11012846;
    BEGIN
      ServicePriceBook.SETRANGE(Type, IServicePriceBook.Type);
      ServicePriceBook.SETRANGE(Code, IServicePriceBook.Code);
      ServicePriceBook.SETFILTER("Index Date", '<%1', IServicePriceBook."Index Date");
      IF NOT ServicePriceBook.FINDLAST THEN
        EXIT;
      IF NOT CONFIRM(Text001, TRUE) THEN
        EXIT;

      ServiceUnitPriceManagement.CopyUnitPrice(
        ServicePriceBook.Type, ServicePriceBook.Code, ServicePriceBook."Index Date", ServicePriceBook.Description,
        IServicePriceBook.Type, IServicePriceBook.Code, IServicePriceBook."Index Date", IServicePriceBook.Description, TRUE);

      //ServiceUnitPrice.SETRANGE("Price Book Type", ServicePriceBook.Type);
      //ServiceUnitPrice.SETRANGE("Price Book Code", ServicePriceBook.Code);
      //ServiceUnitPrice.SETRANGE("Index Date", ServicePriceBook."Index Date");
      //IF NOT ServiceUnitPrice.FINDSET THEN
      //  EXIT;

      //IServicePriceBook.Description := ServicePriceBook.Description;
      //REPEAT
      //  NewServiceUnitPrice.COPY(ServiceUnitPrice);
      //  NewServiceUnitPrice."Index Date" := IServicePriceBook."Index Date";
      //  NewServiceUnitPrice.INSERT;
      //UNTIL ServiceUnitPrice.NEXT = 0;

      IF Type = Type::Customer THEN BEGIN
        UnitPriceRoomVersion.SETRANGE("Customer No.", IServicePriceBook.Code);
        UnitPriceRoomVersion.SETRANGE("Index Date", ServicePriceBook."Index Date");
        IF UnitPriceRoomVersion.FINDSET THEN
          REPEAT
            ServiceUnitPriceManagement.CopyUnitPriceRoomVersion(
              UnitPriceRoomVersion, IServicePriceBook."Index Date", TRUE, CreatedUnitPriceRoomVersion);
          UNTIL UnitPriceRoomVersion.NEXT = 0;
      END;
    END;

    PROCEDURE TestCodeIsUniqueWithinType@1100528603();
    VAR
      ServicePriceBook@1100528600 : Record 11071768;
    BEGIN
      ServicePriceBook.SETRANGE(Code, Code);
      ServicePriceBook.SETFILTER(Type, '<>%1', Type);
      IF ServicePriceBook.FINDFIRST THEN
        ERROR(Text002, Code, ServicePriceBook.Type);
    END;

    BEGIN
    END.
  }
}

