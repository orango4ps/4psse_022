OBJECT Page 11072370 New Plant Type Code with Group
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Neuen Werkzeugartcode bestimmen mithilfe Gruppierungsebenen;
               ENU=Determine new Plant Type Code using Grouping Levels;
               NLD=Nieuwe materieelsoortcode bepalen m.b.v. groepeerniveaus;
               NOR=Fastsett ny maskintypekode ved † bruke Grupperingsniv†er.;
               SVE=Fastst„ll ny Maskinstypkod med hj„lp av grupperingsniv†er];
    PageType=Card;
    OnInit=BEGIN
             NewPlantTypePart2Editable := TRUE;
             GroupLevel3DescriptionVisible := TRUE;
             GroupLevel3Visible := TRUE;
             GrpLevel3TextVisible := TRUE;
             GroupLevel2DescriptionVisible := TRUE;
             GroupLevel2Visible := TRUE;
             GrpLevel2TextVisible := TRUE;
           END;

    OnOpenPage=BEGIN
                 PlantSetupRec.GET;
                 IF PlantSetupRec."Level 1 Grouping No. of Char." = 0 THEN  //* Moet minimaal 1 groepeerniveau zijn
                   ERROR(Text001);

                 GrpLevel2TextVisible := (PlantSetupRec."Level 2 Grouping No. of Char." > 0);
                 GroupLevel2Visible := (PlantSetupRec."Level 2 Grouping No. of Char." > 0);
                 GroupLevel2DescriptionVisible := (PlantSetupRec."Level 2 Grouping No. of Char." > 0);
                 GrpLevel3TextVisible := (PlantSetupRec."Level 3 Grouping No. of Char." > 0);
                 GroupLevel3Visible := (PlantSetupRec."Level 3 Grouping No. of Char." > 0);
                 GroupLevel3DescriptionVisible := (PlantSetupRec."Level 3 Grouping No. of Char." > 0);

                 IF PlantSetupRec."Level 1 Description" = '' THEN
                   GrpLevel1Text := PlantTypeRec.FIELDCAPTION("Group Level 1")
                 ELSE
                   GrpLevel1Text := PlantSetupRec."Level 1 Description";
                 IF PlantSetupRec."Level 2 Description" = '' THEN
                   GrpLevel2Text := PlantTypeRec.FIELDCAPTION("Group Level 2")
                 ELSE
                   GrpLevel2Text := PlantSetupRec."Level 2 Description";
                 IF PlantSetupRec."Level 3 Description" = '' THEN
                   GrpLevel3Text := PlantTypeRec.FIELDCAPTION("Group Level 3")
                 ELSE
                   GrpLevel3Text := PlantSetupRec."Level 3 Description";

                 IF gGroupLevel1 <> '' THEN BEGIN
                   GroupLevel1 := gGroupLevel1;
                   IF (gGroupLevel2 <> '') AND (PlantSetupRec."Level 2 Grouping No. of Char." > 0) THEN BEGIN
                     GroupLevel2 := gGroupLevel2;
                     IF (gGroupLevel3 <> '') AND (PlantSetupRec."Level 3 Grouping No. of Char." > 0) THEN
                       GroupLevel3 := gGroupLevel3;
                   END;
                 END;
                 FillNewPlantTypeWithGroup();
               END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100485014;1;Group  ;
                CaptionML=[DEU=Allgemein;
                           ENU=General;
                           NLD=Algemeen;
                           NOR=Generell;
                           SVE=Allm„nt];
                GroupType=Group }

    { 1100485003;2;Field  ;
                Name=GroupLevel1;
                CaptionML=[DEU=Gruppenniveau 1;
                           ENU=Group Level 1;
                           NLD=Groepniveau 1;
                           NOR=Gruppeniv† 1;
                           SVE=Gruppniv† 1];
                SourceExpr=GroupLevel1;
                CaptionClass=FORMAT (GrpLevel1Text);
                Importance=Promoted;
                OnValidate=BEGIN
                             CheckGroupLevel(1, GroupLevel1);
                           END;

                OnLookup=BEGIN
                           LookupGroupLevel(1);
                         END;
                          }

    { 1100485011;2;Field  ;
                Name=GroupLevel1Description;
                CaptionML=[DEU=Beschreibung;
                           ENU=Description;
                           NLD=Omschrijving;
                           NOR=Beskrivelse;
                           SVE=Beskrivning];
                SourceExpr=GetGroupLevelDescription(GroupLevel1);
                Enabled=FALSE }

    { 1100485005;2;Field  ;
                Name=GroupLevel2;
                CaptionML=[DEU=Gruppenniveau 2;
                           ENU=Group Level 2;
                           NLD=Groepniveau 2;
                           NOR=Gruppeniv† 2;
                           SVE=Gruppniv† 2];
                SourceExpr=GroupLevel2;
                CaptionClass=FORMAT (GrpLevel2Text);
                Visible=GroupLevel2Visible;
                OnValidate=BEGIN
                             CheckGroupLevel(2, GroupLevel2);
                           END;

                OnLookup=BEGIN
                           LookupGroupLevel(2);
                         END;
                          }

    { 1100485012;2;Field  ;
                Name=GroupLevel2Description;
                CaptionML=[DEU=Beschreibung;
                           ENU=Description;
                           NLD=Omschrijving;
                           NOR=Beskrivelse;
                           SVE=Beskrivning];
                SourceExpr=GetGroupLevelDescription(GroupLevel2);
                Visible=GroupLevel2DescriptionVisible;
                Enabled=FALSE }

    { 1100485007;2;Field  ;
                Name=GroupLevel3;
                CaptionML=[DEU=Gruppenebene 3;
                           ENU=Group Level 3;
                           NLD=Groepniveau 3;
                           NOR=Gruppeniv† 3;
                           SVE=Gruppniv† 3];
                SourceExpr=GroupLevel3;
                CaptionClass=FORMAT (GrpLevel3Text);
                Visible=GroupLevel3Visible;
                OnValidate=BEGIN
                             CheckGroupLevel(3, GroupLevel3);
                           END;

                OnLookup=BEGIN
                           LookupGroupLevel(3);
                         END;
                          }

    { 1100485013;2;Field  ;
                Name=GroupLevel3Description;
                CaptionML=[DEU=Beschreibung;
                           ENU=Description;
                           NLD=Omschrijving;
                           NOR=Beskrivelse;
                           SVE=Beskrivning];
                SourceExpr=GetGroupLevelDescription(GroupLevel3);
                Visible=GroupLevel3DescriptionVisible;
                Enabled=FALSE }

    { 1210190000;2;Field  ;
                CaptionML=[DEU=Neue Werkzeugart - Teil 1;
                           ENU=New Plant Type - Part 1;
                           NLD=Nieuwe materieelsoort - Deel 1;
                           NOR=Ny maskintype del 1];
                SourceExpr=NewPlantTypePart1;
                Enabled=FALSE;
                Editable=FALSE }

    { 1100485004;2;Field  ;
                Name=NewPlantTypePart2;
                CaptionML=[DEU=Neue Werkzeugart - Teil 2;
                           ENU=New Plant Type Part 2;
                           NLD=Nieuwe materieelsoort - Deel 2;
                           NOR=Ny maskintype Del 2];
                SourceExpr=NewPlantTypePart2;
                Editable=NewPlantTypePart2Editable;
                OnValidate=BEGIN
                             IF NewPlantTypePart2 <> '' THEN
                               CheckNewPlantType(TRUE);
                           END;

                OnLookup=VAR
                           lvPlantTypeRec@1100485000 : Record 11012551;
                         BEGIN
                           IF NewPlantTypePart1 <> '' THEN BEGIN
                             lvPlantTypeRec.FILTERGROUP(9);
                             lvPlantTypeRec.SETFILTER(Code, NewPlantTypePart1 + '*');
                             lvPlantTypeRec.FILTERGROUP(0);
                             PAGE.RUNMODAL(0, lvPlantTypeRec); //* Alleen die v/d groep tonen, niet bestaande selecteren
                           END;
                         END;
                          }

  }
  CODE
  {
    VAR
      PlantSetupRec@1100485000 : Record 11012550;
      PlantTypeRec@1100485007 : Record 11012551;
      NewPlantTypePart1@1100485014 : Code[20];
      NewPlantTypePart2@1100485015 : Code[20];
      GroupLevel1@1100485001 : Code[20];
      GroupLevel2@1100485002 : Code[20];
      GroupLevel3@1100485003 : Code[20];
      gGroupLevel1@1100485013 : Code[20];
      gGroupLevel2@1100485012 : Code[20];
      gGroupLevel3@1100485011 : Code[20];
      GrpLevel1Text@1100485006 : Text[100];
      GrpLevel2Text@1100485005 : Text[100];
      GrpLevel3Text@1100485004 : Text[100];
      Text001@1100485008 : TextConst 'DEU=Bestimmen neuer Werkzeugart mithilfe Gruppierungsebenen nicht m”glich, denn es wurden keine Gruppierungsebenen eingerichtet.;ENU=Determine new plant type by using grouping levels not possible, because there are no grouping levels in the set up.;NLD="Bepalen nieuwe materieelsoort m.b.v. groeperniveaus niet mogelijk, want er zijn geen groepeerniveaus ingesteld. ";NOR=Kan ikke fastsette ny maskintype ved † bruke Grupperingsniv†er siden det ikke finnes noen Grupperingsniv†er i innstillingen.;SVE=Det g†r inte att fastst„lla ny Maskinstyp med hj„lp av grupperingsniv†er eftersom det inte finns n†gra grupperingsniv†er i inst„llningen.';
      Text002@1100485009 : TextConst 'DEU=%1 darf nicht mehr als %2 Zeichen enthalten.;ENU=%1 may not contain more than %2 characters.;NLD=%1 mag niet meer dan %2 tekens bevatten.;NOR=%1 kan ikke inneholde mer enn %2 tegn.;SVE=%1 f†r inte inneh†lla mer „n %2 tecken.';
      Text003@1100485010 : TextConst 'DEU=%1 darf nicht weniger als %2 Zeichen enthalten.;ENU=%1 may not contain less than %2 characters.;NLD=%1 mag niet minder dan %2 tekens bevatten.;NOR=%1 kan ikke inneholde mindre enn %2 tegn.;SVE=%1 f†r inte inneh†lla mindre „n %2 tecken.';
      Text004@1100485016 : TextConst 'DEU=Neuer Werkzeugartcode ist %1 lang, max. %2 gestattet.;ENU="New Plant Type Code is %1 positions,  max. allowed  %2. ";NLD="Nieuwe materieelsoortcode is %1 lang,  max. toegestaan %2. ";NOR=Ny maskintypekode er %1 posisjoner, max. %2 er tillatt.;SVE="Ny Maskinstypskod „r %1 positioner, max. %2 „r till†tna. "';
      Text005@1100485017 : TextConst 'DEU=Werkzeugart %1 (%2) bereits vorhanden.;ENU=Plant Type %1 (%2) already exist.;NLD=Materieelsoort %1 (%2) bestaat reeds.;NOR=Maskintype %1 (%2) finnes allerede.;SVE=Maskintyp %1 (%2) finns redan.';
      GrpLevel2TextVisible@19045819 : Boolean INDATASET;
      GroupLevel2Visible@19037729 : Boolean INDATASET;
      GroupLevel2DescriptionVisible@19073063 : Boolean INDATASET;
      GrpLevel3TextVisible@19046844 : Boolean INDATASET;
      GroupLevel3Visible@19079058 : Boolean INDATASET;
      GroupLevel3DescriptionVisible@19076960 : Boolean INDATASET;
      NewPlantTypePart2Editable@19034484 : Boolean INDATASET;

    PROCEDURE SetDefaultLevels@1100485003(IGroupLevel1@1100485002 : Code[20];IGroupLevel2@1100485001 : Code[20];IGroupLevel3@1100485000 : Code[20]);
    BEGIN
      gGroupLevel1 := IGroupLevel1;
      gGroupLevel2 := IGroupLevel2;
      gGroupLevel3 := IGroupLevel3;
    END;

    PROCEDURE GetNewPlantTypeCode@1100485004(VAR VarPlantType@1100485000 : Code[20]);
    BEGIN
      IF (NewPlantTypePart1 = '') OR (NewPlantTypePart2 = '') THEN
        EXIT;

      IF CheckNewPlantType(FALSE) THEN
        VarPlantType := NewPlantTypePart1 + NewPlantTypePart2;
    END;

    PROCEDURE LookupGroupLevel@1100485001(ILevel@1100485000 : Integer);
    VAR
      lvGrpLevelRec@1100485002 : Record 11012576;
      lvGroupLenMin@1100485001 : Integer;
      lvGroupLenMax@1100485005 : Integer;
      lvGroupLen@1100485006 : Integer;
      lvGroupCode@1100485003 : Code[20];
      lvPrevLevelGroupCode@1100485004 : Code[20];
    BEGIN
      CASE ILevel OF
        1:
          BEGIN
            lvGroupLenMin := 1;
            lvGroupLenMax := PlantSetupRec."Level 1 Grouping No. of Char.";
            lvGroupCode := GroupLevel1;
          END;
        2:
          BEGIN
            lvGroupLenMin := PlantSetupRec."Level 1 Grouping No. of Char." + 1;
            lvGroupLenMax := PlantSetupRec."Level 2 Grouping No. of Char.";
            lvGroupCode := GroupLevel2;
            IF GroupLevel1 <> '' THEN
              lvPrevLevelGroupCode := GroupLevel1;
          END;
        3:
          BEGIN
            lvGroupLenMin := PlantSetupRec."Level 2 Grouping No. of Char." + 1;
            lvGroupLenMax := PlantSetupRec."Level 3 Grouping No. of Char.";
            lvGroupCode := GroupLevel3;
            IF GroupLevel2 <> '' THEN
              lvPrevLevelGroupCode := GroupLevel2
            ELSE BEGIN
              IF GroupLevel1 <> '' THEN
                lvPrevLevelGroupCode := GroupLevel1;
            END;
          END;
      END;

      IF lvGrpLevelRec.FINDSET THEN BEGIN
       //* Zit geen level in tabel waar filter op gezet kan worden, daarom maar records markeren
        REPEAT
          lvGroupLen := STRLEN(lvGrpLevelRec.Code);
          IF (lvGroupLen >= lvGroupLenMin) AND (lvGroupLen <= lvGroupLenMax) THEN
            lvGrpLevelRec.MARK(TRUE);
        UNTIL lvGrpLevelRec.NEXT = 0;
      END;
      lvGrpLevelRec.MARKEDONLY(TRUE);
      IF lvPrevLevelGroupCode <> '' THEN
        lvGrpLevelRec.SETFILTER(Code, lvPrevLevelGroupCode + '*');
      lvGrpLevelRec.Code := lvGroupCode;
      IF PAGE.RUNMODAL(0, lvGrpLevelRec) = ACTION::LookupOK THEN BEGIN
        CheckGroupLevel(ILevel, lvGrpLevelRec.Code);
      END;
    END;

    PROCEDURE CheckGroupLevel@1100485002(ILevel@1100485000 : Integer;IGroupLevelCode@1100485001 : Code[20]);
    VAR
      lvGrpLevelRec@1100485002 : Record 11012576;
      lvLen@1100485003 : Integer;
    BEGIN
      IF IGroupLevelCode = '' THEN BEGIN
        IF ILevel < 2 THEN
          GroupLevel2 := '';
        IF ILevel < 3 THEN
          GroupLevel3 := '';
        FillNewPlantTypeWithGroup();
        EXIT;
      END;

      lvGrpLevelRec.GET(IGroupLevelCode);

      CASE ILevel OF
        1:
          BEGIN
            lvLen := STRLEN(IGroupLevelCode);
            IF lvLen > PlantSetupRec."Level 1 Grouping No. of Char." THEN
              ERROR(Text002, GrpLevel1Text, PlantSetupRec."Level 1 Grouping No. of Char.");
            GroupLevel1 := IGroupLevelCode;
            IF COPYSTR(GroupLevel2, 1, PlantSetupRec."Level 1 Grouping No. of Char.") <> GroupLevel1 THEN
              GroupLevel2 := '';
            IF COPYSTR(GroupLevel3, 1, PlantSetupRec."Level 1 Grouping No. of Char.") <> GroupLevel1 THEN
              GroupLevel3 := '';
          END;
        2:
          BEGIN
            lvLen := STRLEN(IGroupLevelCode);
            IF lvLen > PlantSetupRec."Level 2 Grouping No. of Char." THEN
              ERROR(Text002, GrpLevel2Text, PlantSetupRec."Level 2 Grouping No. of Char.");
            IF lvLen <= PlantSetupRec."Level 1 Grouping No. of Char." THEN
              ERROR(Text003, GrpLevel2Text, PlantSetupRec."Level 1 Grouping No. of Char." + 1);
            GroupLevel2 := IGroupLevelCode;
            GroupLevel1 := COPYSTR(GroupLevel2, 1, PlantSetupRec."Level 1 Grouping No. of Char.");
            IF COPYSTR(GroupLevel3, 1, PlantSetupRec."Level 1 Grouping No. of Char.") <> GroupLevel1 THEN
              GroupLevel3 := '';
          END;
        3:
          BEGIN
            lvLen := STRLEN(IGroupLevelCode);
            IF lvLen > PlantSetupRec."Level 3 Grouping No. of Char." THEN
              ERROR(Text002, GrpLevel3Text, PlantSetupRec."Level 3 Grouping No. of Char.");
            IF lvLen <= PlantSetupRec."Level 2 Grouping No. of Char." THEN
              ERROR(Text003, GrpLevel3Text, PlantSetupRec."Level 2 Grouping No. of Char." + 1);
            GroupLevel3 := IGroupLevelCode;
            GroupLevel1 := COPYSTR(GroupLevel3, 1, PlantSetupRec."Level 1 Grouping No. of Char.");
            GroupLevel2 := COPYSTR(GroupLevel3, 1, PlantSetupRec."Level 2 Grouping No. of Char.");
          END;
      END;
      FillNewPlantTypeWithGroup();
    END;

    PROCEDURE GetGroupLevelDescription@1100485000(IGroupLevelCode@1100485000 : Code[20]) : Text[50];
    VAR
      lvGrpLevelRec@1100485001 : Record 11012576;
    BEGIN
      IF IGroupLevelCode = '' THEN
        EXIT('');

      IF lvGrpLevelRec.GET(IGroupLevelCode) THEN
        EXIT(lvGrpLevelRec.Description);

      EXIT('');
    END;

    PROCEDURE FillNewPlantTypeWithGroup@1100485005();
    BEGIN
      IF (PlantSetupRec."Level 3 Grouping No. of Char." > 0) THEN
        NewPlantTypePart1 := GroupLevel3
      ELSE BEGIN
        IF (PlantSetupRec."Level 2 Grouping No. of Char." > 0) THEN
          NewPlantTypePart1 := GroupLevel2
        ELSE
          NewPlantTypePart1 := GroupLevel1;
      END;

      IF NewPlantTypePart1 = '' THEN
        NewPlantTypePart2 := '';

      IF NewPlantTypePart2 <> '' THEN BEGIN
        IF NOT CheckNewPlantType(FALSE) THEN
          NewPlantTypePart2 := '';
      END;

      NewPlantTypePart2Editable := (NewPlantTypePart1 <> '');
    END;

    PROCEDURE CheckNewPlantType@1100485006(IErrorMode@1100485000 : Boolean) : Boolean;
    BEGIN
      IF STRLEN(NewPlantTypePart1) + STRLEN(NewPlantTypePart2) > MAXSTRLEN(PlantTypeRec.Code) THEN BEGIN
        IF IErrorMode THEN
          ERROR(Text004, STRLEN(NewPlantTypePart1)+STRLEN(NewPlantTypePart2), MAXSTRLEN(PlantTypeRec.Code));
        EXIT(FALSE);
      END;

      IF PlantTypeRec.GET(NewPlantTypePart1+NewPlantTypePart2) THEN BEGIN
        IF IErrorMode THEN
          ERROR(Text005, PlantTypeRec.Code, PlantTypeRec.Description);
        EXIT(FALSE);
      END;

      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

