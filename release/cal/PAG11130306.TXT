OBJECT Page 11130306 Import Multiple IBISTRAD XML
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Collectivised Import IBIS-TRAD/CUF Option Budgets;
    PageType=NavigatePage;
    UsageCategory=Tasks;
    ActionList=ACTIONS
    {
      { 1100528503;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100528504;1 ;Action    ;
                      CaptionML=ENU=Import;
                      InFooterBar=Yes;
                      Image=Report;
                      OnAction=BEGIN
                                 ImportFromTRADXML;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100528500;;Container;
                ContainerType=ContentArea }

    { 1100528501;1;Field  ;
                CaptionML=ENU=Project No.;
                SourceExpr=ProjectNo;
                TableRelation=Job.No. WHERE (Housebuilding Project=FILTER(Yes));
                OnValidate=BEGIN
                             IF ProjectNo = '' THEN
                               ProjectDescription := ''
                             ELSE
                               IF JobRec.GET(ProjectNo) THEN BEGIN
                                 ProjectDescription := JobRec.Description;
                                 HouseModel := '';
                                 HouseModelDescription := '';
                               END;
                           END;
                            }

    { 1100529503;1;Field  ;
                CaptionML=ENU=Project Description;
                SourceExpr=ProjectDescription;
                Editable=FALSE }

    { 1100528502;1;Field  ;
                CaptionML=ENU=House model;
                SourceExpr=HouseModel;
                TableRelation="House Model".Model;
                OnValidate=BEGIN
                             IF HouseModel = '' THEN
                               HouseModelDescription := '';
                           END;

                OnLookup=VAR
                           HouseModelsbyProjectList@1100529500 : Page 11012501;
                           HouseModelRec@1100529501 : Record 11012501;
                         BEGIN
                           HouseModelRec.FILTERGROUP(6);
                           HouseModelRec.SETRANGE("Project No.", ProjectNo);
                           HouseModelRec.FILTERGROUP(0);
                           HouseModelsbyProjectList.LOOKUPMODE(TRUE);
                           HouseModelsbyProjectList.SETTABLEVIEW(HouseModelRec);

                           IF HouseModelsbyProjectList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             HouseModelsbyProjectList.GETRECORD(HouseModelRec);
                             HouseModel := HouseModelRec.Model;
                             HouseModelDescription := HouseModelRec.Description;
                           END;
                         END;
                          }

    { 1100529504;1;Field  ;
                CaptionML=ENU=House Model Description;
                SourceExpr=HouseModelDescription;
                Editable=FALSE }

    { 1100529500;1;Field  ;
                CaptionML=ENU=Main Group Filter;
                SourceExpr=MainGroupFilter;
                TableRelation="Option Main Group".Code WHERE (Plot No.=FILTER(''));
                OnValidate=BEGIN
                             DetermineEditableFields;
                           END;

                OnLookup=VAR
                           OptionMainGroupRec@1100529500 : Record 11012509;
                           ProjectOptionMainGroupList@1100529501 : Page 11012543;
                         BEGIN
                           OptionMainGroupRec.FILTERGROUP(6);
                           OptionMainGroupRec.SETRANGE("Project No.", ProjectNo);
                           OptionMainGroupRec.SETRANGE("Plot No.", '');

                           OptionMainGroupRec.FILTERGROUP(0);
                           ProjectOptionMainGroupList.LOOKUPMODE(TRUE);
                           ProjectOptionMainGroupList.SETTABLEVIEW(OptionMainGroupRec);

                           IF ProjectOptionMainGroupList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             ProjectOptionMainGroupList.GETRECORD(OptionMainGroupRec);
                             MainGroupFilter := OptionMainGroupRec.Code;
                           END;

                           DetermineEditableFields;
                         END;
                          }

    { 1100529501;1;Field  ;
                CaptionML=ENU=Group Filter;
                SourceExpr=GroupFilter;
                Editable=MainGroupFieldFilled;
                OnValidate=BEGIN
                             DetermineEditableFields;
                           END;

                OnLookup=VAR
                           OptionGroupRec@1100529500 : Record 11012504;
                           ProjectOptionGroups@1100529501 : Page 11012518;
                         BEGIN
                           OptionGroupRec.FILTERGROUP(6);
                           OptionGroupRec.SETRANGE("Project No.", ProjectNo);
                           OptionGroupRec.SETRANGE("Plot No.", '');
                           OptionGroupRec.FILTERGROUP(0);
                           ProjectOptionGroups.LOOKUPMODE(TRUE);
                           ProjectOptionGroups.SETTABLEVIEW(OptionGroupRec);

                           IF ProjectOptionGroups.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             ProjectOptionGroups.GETRECORD(OptionGroupRec);
                             GroupFilter := OptionGroupRec.Code;
                           END;

                           DetermineEditableFields;
                         END;
                          }

    { 1100529502;1;Field  ;
                CaptionML=ENU=Sub Group Filter;
                SourceExpr=SubGroupFilter;
                TableRelation="Option Sub Group".Code;
                Editable=GroupFieldFilled;
                OnLookup=VAR
                           OptionSubGroup@1100528200 : Record 11012505;
                           ProjectOptionSubGroups@1100528201 : Page 11012542;
                         BEGIN
                           OptionSubGroup.FILTERGROUP(6);
                           OptionSubGroup.SETRANGE("Project No.", ProjectNo);
                           OptionSubGroup.SETRANGE("Plot No.", '');
                           OptionSubGroup.FILTERGROUP(0);
                           ProjectOptionSubGroups.LOOKUPMODE(TRUE);
                           ProjectOptionSubGroups.SETTABLEVIEW(OptionSubGroup);

                           IF ProjectOptionSubGroups.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             ProjectOptionSubGroups.GETRECORD(OptionSubGroup);
                             SubGroupFilter := OptionSubGroup.Code;
                           END;
                         END;
                          }

    { 1100528902;1;Field  ;
                Name=ElementOptout;
                CaptionML=ENU=Do not process Elements;
                SourceExpr=ElementOptout;
                OnValidate=BEGIN
                             IF ElementOptout = TRUE THEN
                               ElementCde := '';
                           END;
                            }

    { 1100528900;1;Field  ;
                Name=ElementCde;
                CaptionML=ENU=Default Element;
                SourceExpr=ElementCde;
                OnValidate=BEGIN
                             IF ElementOptout THEN ElementCde := '';
                             IF ElementCde <> '' THEN
                               BaseElement.GET(ElementCde);
                           END;

                OnLookup=BEGIN
                           IF ElementOptout THEN EXIT;
                           IF PAGE.RUNMODAL(0,BaseElement) = ACTION::LookupOK THEN
                             ElementCde := BaseElement.Code;
                         END;
                          }

  }
  CODE
  {
    VAR
      ProjectNo@1100528500 : Code[20];
      HouseModel@1100528501 : Code[20];
      MainGroupFilter@1100528502 : Code[20];
      GroupFilter@1100528503 : Code[20];
      SubGroupFilter@1100528504 : Code[20];
      XmlFileFilter@1100529500 : TextConst 'ENU=XML Files (*.xml)|*.xml';
      FileMgt@1100529501 : Codeunit 419;
      AdjustmentCde@1100529504 : Code[10];
      ExtensionCde@1100529505 : Code[20];
      MainGroupCde@1100529507 : Code[20];
      GroupCde@1100529508 : Code[20];
      SubGroupCde@1100529509 : Code[20];
      OptionCde@1100529510 : Code[20];
      PlotCde@1100529511 : Code[20];
      ElementOptout@1100529512 : Boolean;
      ElementCde@1100529513 : Code[20];
      Text001@1100529514 : TextConst 'ENU=Import successful.';
      ProjectDescription@1100529515 : Text;
      JobRec@1100529516 : Record 11072003;
      HouseModelDescription@1100529517 : Text;
      MainGroupFieldFilled@1100529518 : Boolean;
      GroupFieldFilled@1100529519 : Boolean;
      ErrorRec@1100529522 : Record 11012051;
      Text002@1100529523 : TextConst 'ENU=House Model required.';
      Text003@1100529524 : TextConst 'ENU=User Setup setting ''Buyer''s Mgt. Exchange'' does not allow importing to projects as the current user.';
      Text004@1100529525 : TextConst 'ENU=User Setup setting ''Buyer Mgt. Exchange'' does not allow importing to standard house models as current user.';
      BaseElement@1100528902 : Record 11012060;

    LOCAL PROCEDURE ImportFromTRADXML@1210190003();
    VAR
      JobSetup@1006 : Record 315;
      TempBlob@1100528502 : TEMPORARY Record 99008535;
      FileName@1004 : Text[250];
      StartFileName@1003 : Text[250];
      FileManagement@1000 : Codeunit 419;
      ParseTRADXMLDocument@1100525000 : Codeunit 11012391;
      ClientTypeManagement@1100528500 : Codeunit 4;
      InStream@1100528501 : InStream;
      OutStream@1001 : OutStream;
      ErrorsPage@1100529500 : Page 11012042;
      UserSetup@1100529501 : Record 91;
    BEGIN
      IF HouseModel = '' THEN
        ERROR(Text002);

      JobSetup.GET;
      UserSetup.GET(USERID);

      IF ProjectNo <> '' THEN
        IF (UserSetup."IBIS-TRAD Import Permission" = UserSetup."IBIS-TRAD Import Permission"::No) OR (UserSetup."IBIS-TRAD Import Permission" = UserSetup."IBIS-TRAD Import Permission"::Company) THEN
          ERROR(Text003);

      IF ProjectNo = '' THEN
        IF (UserSetup."IBIS-TRAD Import Permission" = UserSetup."IBIS-TRAD Import Permission"::No) OR (UserSetup."IBIS-TRAD Import Permission" = UserSetup."IBIS-TRAD Import Permission"::Project) THEN
          ERROR(Text004);

      IF NOT ClientTypeManagement.IsCommonWebClientType THEN BEGIN
        StartFileName := JobSetup."Pathname External Estimates";
        IF StartFileName <> '' THEN BEGIN
          IF StartFileName[STRLEN(StartFileName)] <> '\' THEN
            StartFileName := StartFileName + '\';
          StartFileName := StartFileName + '*.xml';
        END;
        FileName := FileManagement.OpenFileDialog('',StartFileName,XmlFileFilter);
        FileManagement.BLOBImport4PS(TempBlob, FileName, FALSE);
        IF FileName = '' THEN
          EXIT;
        FileMgt.BLOBImport4PS(TempBlob, FileName, FALSE);
      END ELSE BEGIN
        //C062298.sn
        TempBlob.Blob.CREATEOUTSTREAM(OutStream);
        IF NOT UPLOADINTOSTREAM('', '', XmlFileFilter, FileName, InStream) THEN
          EXIT;
        COPYSTREAM(OutStream, InStream);
        //C062298.en
      END;

      ErrorRec.RESET;
      ErrorRec.SETRANGE("User ID", USERID);
      ErrorRec.SETRANGE("Source Type", ErrorRec."Source Type"::"Import Budget");
      ErrorRec.DELETEALL;
      COMMIT;

      ParseTRADXMLDocument.SetMultipleOptionBudgetDocumentType(TRUE);
      ParseTRADXMLDocument.SetMultipleOptionBudgetFilters(MainGroupFilter, GroupFilter, SubGroupFilter);
      ParseTRADXMLDocument.InitCodeunit(ProjectNo,AdjustmentCde,ExtensionCde,
        HouseModel,MainGroupCde,GroupCde,SubGroupCde,OptionCde,PlotCde,
        ElementOptout,ElementCde);
      ParseTRADXMLDocument.ProcessFile(TempBlob);

      MESSAGE(Text001);
      CurrPage.CLOSE;

      ErrorRec.SETRANGE("User ID", USERID);
      ErrorRec.SETRANGE("Source Type", ErrorRec."Source Type"::"Import Budget");
      IF ErrorRec.FIND('-') THEN BEGIN
        COMMIT;
        ErrorsPage.SetDisplayForMultipleOptionBudget;
        ErrorsPage.SETRECORD(ErrorRec);
        ErrorsPage.RUNMODAL;
      END;
    END;

    LOCAL PROCEDURE DetermineEditableFields@1100529526();
    BEGIN
      IF GroupFilter <> '' THEN
        GroupFieldFilled := TRUE
      ELSE
        GroupFieldFilled := FALSE;

      IF MainGroupFilter <> '' THEN
        MainGroupFieldFilled := TRUE
      ELSE BEGIN
        MainGroupFieldFilled := FALSE;
        GroupFieldFilled := FALSE;
        GroupFilter := '';
      END;
    END;

    BEGIN
    {
      db, 19-03-20: synchronise options with page 11012390
    }
    END.
  }
}

