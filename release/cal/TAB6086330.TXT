OBJECT Table 6086330 Bank Transaction
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.3.00.07;
  }
  PROPERTIES
  {
    Permissions=TableData 6086311=r;
    DataCaptionFields=Entry No.,Card No.,Card Name;
    OnDelete=VAR
               ExpenseMatchMgt@1160040000 : Codeunit 6086311;
               BankTransactionPost@1160040001 : Codeunit 6086352;
             BEGIN
               TESTFIELD("Matched to Expense",FALSE);
               ExpenseMatchMgt.DeleteBankTransMatch(Rec);

               BankTransactionPost.RevertPosting(Rec);
             END;

    OnRename=BEGIN
               ERROR(Text001,TABLECAPTION);
             END;

    CaptionML=[DEU=Banktransaktion;
               ENU=Bank Transaction;
               NLD=Banktransactie;
               SVE=Bokf”ring];
    LookupPageID=Page6086332;
    DrillDownPageID=Page6086332;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=[DEU=Lfd. Nr.;
                                                              ENU=Entry No.;
                                                              NLD=Volgnr.;
                                                              NOR=L›penr.;
                                                              SVE=L”pnr];
                                                   NotBlank=Yes }
    { 2   ;   ;Card No.            ;Code20        ;OnValidate=VAR
                                                                EMSetup@1160040001 : Record 6086300;
                                                                ContUserCreditCard@1160040000 : Record 6086311;
                                                              BEGIN
                                                                EMSetup.GET;
                                                                IF NOT EMSetup."Allow duplicate Card ID" THEN BEGIN
                                                                  ContUserCreditCard.SETRANGE("Card No.","Card No.");
                                                                  ContUserCreditCard.FINDFIRST;
                                                                  "Continia User ID" := ContUserCreditCard."Continia User ID";
                                                                  "Bank Account Type" := ContUserCreditCard."Account Type";
                                                                  "Bank Account No." := ContUserCreditCard."Account No.";
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Karten-ID;
                                                              ENU=Card No.;
                                                              NLD=Kaartnr.] }
    { 3   ;   ;Card Name           ;Text50        ;CaptionML=[DEU=Kartenenname;
                                                              ENU=Card Name;
                                                              NLD=Kaartnaam] }
    { 5   ;   ;Posting Date        ;Date          ;OnValidate=BEGIN
                                                                TESTFIELD(Posted,FALSE);
                                                              END;

                                                   CaptionML=[DEU=Buchungsdatum;
                                                              ENU=Posting Date;
                                                              NLD=Boekingsdatum;
                                                              NOR=Bokf›ringsdato;
                                                              SVE=Bokf”ringsdatum];
                                                   Editable=No }
    { 6   ;   ;Document Date       ;Date          ;CaptionML=[DEU=Belegdatum;
                                                              ENU=Document Date;
                                                              NLD=Documentdatum;
                                                              NOR=Bilagsdato;
                                                              SVE=Dokumentdatum] }
    { 7   ;   ;Currency Code       ;Code10        ;OnValidate=BEGIN
                                                                TESTFIELD("Matched to Expense",FALSE);
                                                                TESTFIELD(Posted,FALSE);
                                                              END;

                                                   CaptionML=[DEU=W„hrungscode;
                                                              ENU=Currency Code;
                                                              NLD=Valutacode;
                                                              NOR=Valutakode;
                                                              SVE=Valutakod] }
    { 8   ;   ;Currency Exch. Rate ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("Matched to Expense",FALSE);
                                                                TESTFIELD(Posted,FALSE);
                                                              END;

                                                   CaptionML=[DEU=W„hrungswechselkurs;
                                                              ENU=Currency Exch. Rate;
                                                              NLD=Valutawisselkoers];
                                                   DecimalPlaces=0:15 }
    { 9   ;   ;Amount              ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("Matched to Expense",FALSE);
                                                                TESTFIELD(Posted,FALSE);
                                                              END;

                                                   CaptionML=[DEU=Betrag;
                                                              ENU=Amount;
                                                              NLD=Bedrag;
                                                              NOR=Bel›p;
                                                              SVE=Belopp];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 10  ;   ;Bank-Currency Amount;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("Matched to Expense",FALSE);
                                                                TESTFIELD(Posted,FALSE);
                                                              END;

                                                   CaptionML=[DEU=Betrag (MW);
                                                              ENU=Bank-Currency Amount;
                                                              NLD=Bankvalutabedrag;
                                                              NOR=Bel›p (NOK);
                                                              SVE=Belopp (BVA)];
                                                   AutoFormatType=1 }
    { 11  ;   ;Entry Type          ;Integer       ;CaptionML=[DEU=Postenart;
                                                              ENU=Entry Type;
                                                              NLD=Posttype;
                                                              NOR=Posttype;
                                                              SVE=Transaktionstyp] }
    { 12  ;   ;Business Category ID;Code50        ;CaptionML=[DEU=Unternehmenskategorie-ID;
                                                              ENU=Business Category ID;
                                                              NLD=Branchecategorie-id] }
    { 13  ;   ;Business No.        ;Code20        ;CaptionML=[DEU=Unternehmensnr.;
                                                              ENU=Business No.;
                                                              NLD=Branchenr.] }
    { 14  ;   ;Business Name       ;Text250       ;CaptionML=[DEU=Unternehmensname;
                                                              ENU=Business Name;
                                                              NLD=Branchenaam] }
    { 15  ;   ;Business Address    ;Text80        ;CaptionML=[DEU=Unternehmensadresse;
                                                              ENU=Business Address;
                                                              NLD=Brancheadres;
                                                              SVE=F”retagsadress] }
    { 16  ;   ;Business Address 2  ;Text80        ;CaptionML=[DEU=Unternehmensadresse 2;
                                                              ENU=Business Address 2;
                                                              NLD=Brancheadres 2] }
    { 17  ;   ;Business Address 3  ;Text80        ;CaptionML=[DEU=Unternehmensadresse 3;
                                                              ENU=Business Address 3;
                                                              NLD=Brancheadres 3] }
    { 18  ;   ;Business Country/Region;Code20     ;CaptionML=[DEU=Unternehmensland/-region;
                                                              ENU=Business Country/Region;
                                                              NLD=Brancheland-/regio] }
    { 19  ;   ;Business Post Code  ;Code10        ;CaptionML=[DEU=Unternehmens-PLZ;
                                                              ENU=Business Post Code;
                                                              NLD=Branchepostcode] }
    { 20  ;   ;Document Time       ;Time          ;CaptionML=[DEU=Belegzeit;
                                                              ENU=Document Time;
                                                              NLD=Documenttijd];
                                                   Editable=No }
    { 21  ;   ;Bank Currency Code  ;Code10        ;TableRelation=Currency;
                                                   CaptionML=[DEU=Bankto.-W„hrungscode;
                                                              ENU=Bank Currency Code;
                                                              NLD=Bankvalutacode];
                                                   Editable=No }
    { 22  ;   ;Transaction ID      ;Text50        ;CaptionML=[DEU=Transaktions-ID;
                                                              ENU=Transaction ID;
                                                              NLD=Transactie-id;
                                                              SVE=Transaktions-ID];
                                                   Editable=No }
    { 40  ;   ;Bank Code           ;Code10        ;TableRelation="CEM Bank";
                                                   CaptionML=[DEU=Bankcode;
                                                              ENU=Bank Code;
                                                              NLD=Bankcode;
                                                              SVE=Bank kod] }
    { 41  ;   ;Bank Country/Region Code;Code10    ;TableRelation="Bank Agreement"."Country/Region Code" WHERE (Bank Code=FIELD(Bank Code));
                                                   CaptionML=[DEU=L„nder-/Regionscode der Bank;
                                                              ENU=Bank Country/Region Code;
                                                              NLD=Bankland-/regiocode] }
    { 50  ;   ;Continia User ID    ;Code50        ;TableRelation="Continia User Setup";
                                                   OnValidate=BEGIN
                                                                TESTFIELD("Matched to Expense",FALSE);
                                                                VALIDATE("Card No.");
                                                              END;

                                                   CaptionML=[DEU=Continia-Benutzer-ID;
                                                              ENU=Continia User ID;
                                                              NLD=Continia gebruikers-id;
                                                              SVE=Anst„llningsnr] }
    { 51  ;   ;Exclude Entry       ;Boolean       ;OnValidate=BEGIN
                                                                TESTFIELD("Matched to Expense",FALSE);
                                                              END;

                                                   CaptionML=[DEU=Posten ausschlieáen;
                                                              ENU=Exclude Entry;
                                                              NLD=Post uitsluiten] }
    { 70  ;   ;Posted              ;Boolean       ;OnValidate=BEGIN
                                                                IF Posted THEN BEGIN
                                                                  "Posted Date/Time" := CURRENTDATETIME;
                                                                  "Posted by User ID" := USERID;
                                                                END ELSE BEGIN
                                                                  CLEAR("Posted Date/Time");
                                                                  CLEAR("Posted by User ID");
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Gebucht;
                                                              ENU=Posted;
                                                              NLD=Geboekt;
                                                              NOR=Bokf›rt;
                                                              SVE=Bokf”rd] }
    { 71  ;   ;Posted Date/Time    ;DateTime      ;CaptionML=[DEU=Buchungsdatum/-uhrzeit;
                                                              ENU=Posted Date Time;
                                                              NLD=Boekingsdatum/tijd];
                                                   Editable=No }
    { 72  ;   ;Posted by User ID   ;Code50        ;CaptionML=[DEU=Gebucht von Benutzer-ID;
                                                              ENU=Posted by User ID;
                                                              NLD=Geboekt door gebruikers-id];
                                                   Editable=No }
    { 73  ;   ;Posted Doc. ID      ;Code20        ;CaptionML=[DEU=Gebuchte Beleg-ID;
                                                              ENU=Posted Doc. ID;
                                                              NLD=Geboekt document-id];
                                                   Editable=No }
    { 74  ;   ;Bank Statement Transaction;Boolean ;CaptionML=[DEU=Bankauszugtransaktion;
                                                              ENU=Bank Statement Transaction;
                                                              NLD=Bankafschrifttransactie] }
    { 77  ;   ;Statement Status    ;Option        ;CaptionML=[DEU=Auszugsstatus;
                                                              ENU=Statement Status;
                                                              NLD=Afschriftstatus;
                                                              SVE=Bankkontoutdragsstatus];
                                                   OptionCaptionML=[DEU=Offen,Bankabzug abgeglichen,Geschlossen;
                                                                    ENU=Open,Bank Transaction Applied,Closed;
                                                                    NLD=Open,Bankafschrift vereffend,Gesloten];
                                                   OptionString=Open,Bank Transaction Applied,Closed }
    { 78  ;   ;Statement No.       ;Code20        ;TableRelation="EM Bank Acc. Recon. Line"."Statement No.";
                                                   TestTableRelation=No;
                                                   CaptionML=[DEU=Auszugsnr.;
                                                              ENU=Statement No.;
                                                              NLD=Afschriftnr.;
                                                              SVE=Kontoutdragsnr] }
    { 79  ;   ;Statement Line No.  ;Integer       ;TableRelation="EM Bank Acc. Recon. Line"."Statement Line No.";
                                                   TestTableRelation=No;
                                                   CaptionML=[DEU=Auszugszeilennr.;
                                                              ENU=Statement Line No.;
                                                              NLD=Afschriftregelnr.;
                                                              SVE=Kontoutdragsradnr] }
    { 80  ;   ;Bank Account Type   ;Option        ;CaptionML=[DEU=Bankkontoart;
                                                              ENU=Bank Account Type;
                                                              NLD=Rekeningtype];
                                                   OptionCaptionML=[DEU=Sachkonto,,Kreditor,Bankkonto;
                                                                    ENU=G/L Account,,Vendor,Bank Account;
                                                                    NLD=Grootboekrekening,,Leverancier,Bankrekening];
                                                   OptionString=G/L Account,,Vendor,Bank Account }
    { 81  ;   ;Bank Account No.    ;Code20        ;TableRelation=IF (Bank Account Type=CONST(G/L Account)) "G/L Account"
                                                                 ELSE IF (Bank Account Type=CONST(Bank Account)) "Bank Account"
                                                                 ELSE IF (Bank Account Type=CONST(Vendor)) Vendor;
                                                   CaptionML=[DEU=Bankkontonummer;
                                                              ENU=Bank Account No.;
                                                              NLD=Rekeningnr.;
                                                              SVE=Bankkontonr];
                                                   NotBlank=Yes }
    { 83  ;   ;Reconciled          ;Boolean       ;OnValidate=BEGIN
                                                                IF Reconciled THEN BEGIN
                                                                  "Reconciled Date/Time" := CURRENTDATETIME;
                                                                  "Reconciled by User ID" := USERID;
                                                                END ELSE BEGIN
                                                                  CLEAR("Reconciled Date/Time");
                                                                  CLEAR("Reconciled by User ID");
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Abgestimmt;
                                                              ENU=Reconciled;
                                                              NLD=Verzoend;
                                                              SVE=Avst„mda] }
    { 84  ;   ;Reconciled Date/Time;DateTime      ;CaptionML=[DEU=Abstimmungsdatum und -Zeit;
                                                              ENU=Reconciled Date/Time;
                                                              NLD=Reconciliatie datum en tijd];
                                                   Editable=No }
    { 85  ;   ;Reconciled by User ID;Code50       ;CaptionML=[DEU=Abgestimmt vom Benutzer-ID;
                                                              ENU=Reconciled by User ID;
                                                              NLD=Gereconcilieerd door gebruikers-id];
                                                   Editable=No }
    { 100 ;   ;Bank Agreement ID   ;Text30        ;TableRelation="Bank Agreement"."Agreement ID" WHERE (Bank Code=FIELD(Bank Code),
                                                                                                        Country/Region Code=FIELD(Bank Country/Region Code));
                                                   CaptionML=[DEU=Bankvereinbarungs-ID;
                                                              ENU=Bank Agreement ID;
                                                              NLD=Bankovereenkomst-ID] }
    { 120 ;   ;Employee No.        ;Text50        ;CaptionML=[DEU=Mitarbeiternr.;
                                                              ENU=Employee No.;
                                                              NLD=Werknemersnr.;
                                                              SVE=Anst„llningsnr] }
    { 180 ;   ;Expense Type        ;Code20        ;TableRelation="Expense Type";
                                                   OnValidate=VAR
                                                                ExpenseType@1160040000 : Record 6086307;
                                                              BEGIN
                                                                TESTFIELD("Matched to Expense",FALSE);
                                                                ExpenseType.GET("Expense Type");
                                                                VALIDATE("Exclude Entry",ExpenseType."Exclude Transactions");
                                                              END;

                                                   CaptionML=[DEU=Ausgabenart;
                                                              ENU=Expense Type;
                                                              NLD=Uitgavetype] }
    { 200 ;   ;Matched to Expense  ;Boolean       ;CaptionML=[DEU=Mit Ausgabe abgeglichen;
                                                              ENU=Matched to Expense;
                                                              NLD=Gematched aan uitgave];
                                                   Editable=No }
    { 250 ;   ;Duplicate           ;Boolean       ;OnValidate=BEGIN
                                                                TESTFIELD(Duplicate,FALSE);
                                                              END;

                                                   CaptionML=[DEU=Dublette;
                                                              ENU=Duplicate;
                                                              NLD=Duplicaat;
                                                              NOR=Duplikat;
                                                              SVE=Dubblett] }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Continia User ID,Matched to Expense,Currency Code,Exclude Entry;
                                                   SumIndexFields=Amount,Bank-Currency Amount }
    {    ;Matched to Expense                       }
    {    ;Card No.,Posting Date                    }
    {    ;Posted,Continia User ID,Matched to Expense }
    {    ;Posted Doc. ID                           }
    {    ;Transaction ID                           }
    {    ;Posted,Posted Date/Time,Entry No.        }
    {    ;Reconciled,Bank Statement Transaction    }
    {    ;Bank Statement Transaction               }
    {    ;Bank Account Type,Bank Account No.,Bank Statement Transaction,Reconciled,Posted;
                                                   SumIndexFields=Bank-Currency Amount }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1160040000 : TextConst 'DEU=Sie k”nnen eine %1 nicht umbenennen.;ENU=You cannot rename a %1.;NLD=U kunt een %1 niet hernoemen.;NOR=Du kan ikke gi nytt navn til en %1.;SVE=Du kan inte byta namn p† %1.';
      TransNotMatchedErr@1160040002 : TextConst 'DEU=Diese Banktransaktion ist keinen Ausgaben zugeordnet.;ENU=This bank transaction is not matched to any expenses.;NLD=Deze banktransactie is niet gematched aan een uitgave.';

    PROCEDURE CheckUnProcessedBankInbox@1160040003();
    VAR
      Expense@1160040000 : Record 6086320;
    BEGIN
      Expense.CheckUnProcessedInbox;
    END;

    PROCEDURE ApplyMappingRule@1160040002();
    VAR
      BankMappingRule@1160040000 : Record 6086350;
    BEGIN
      IF "Expense Type" <> '' THEN
        EXIT;
      BankMappingRule.UseBankMappingRules(Rec);
      IF "Expense Type" <> '' THEN
        Rec.MODIFY;
    END;

    PROCEDURE MatchAndCreateExpense@1160040011();
    VAR
      CountryRegion@1160040003 : Record 9;
      EMSetup@1160040005 : Record 6086300;
      ExpenseType@1160040000 : Record 6086307;
      Expense@1160040002 : Record 6086320;
      Matching@1160040007 : Codeunit 6086311;
      SendExpense@1160040006 : Codeunit 6086324;
      NextEntryNo@1160040001 : Integer;
    BEGIN
      TESTFIELD("Bank Statement Transaction",FALSE);

      EMSetup.GET;
      IF ExpenseType.GET("Expense Type") THEN
        IF ExpenseType."Exclude Transactions" THEN
          EXIT;

      IF Matching.MatchInsertExpense(Rec,Expense) THEN
        EXIT;

      Expense.RESET;

      IF Expense.FINDLAST THEN
        NextEntryNo := Expense."Entry No." + 1
      ELSE
        NextEntryNo := 1;

      Expense.INIT;
      Expense."Entry No." := NextEntryNo;
      Expense.VALIDATE("Continia User ID","Continia User ID");
      IF EMSetup."Exp. Description From Bank" THEN
        Expense.Description := COPYSTR("Business Name",1,MAXSTRLEN(Expense.Description));
      Expense."Business Description" := "Business Name";
      Expense."Document Date" := "Document Date";
      IF CountryRegion.GET("Business Country/Region") THEN
        Expense."Country/Region Code" := CountryRegion.Code;
      Expense."Currency Code" := "Currency Code";
      Expense.VALIDATE(Amount,Amount);
      Expense."Bank-Currency Amount" := "Bank-Currency Amount";
      Expense."Document Time" := "Document Time";

      Expense.VALIDATE("Expense Type","Expense Type");
      Expense."Created By User ID" := USERID;
      Expense.INSERT(TRUE);

      Matching.InsertMatch(Rec,Expense);
      Expense.GET(Expense."Entry No.");
      SendExpense.SetBatchMode(TRUE);
      SendExpense.RUN(Expense);

      Rec.GET("Entry No.");
    END;

    PROCEDURE ShowMatchedExpense@1160040000();
    VAR
      Expense@1160040001 : Record 6086320;
      ExpHeader@1160040002 : Record 6086339;
      ExpenseMatch@1160040000 : Record 6086349;
    BEGIN
      IF NOT "Matched to Expense" THEN
        ERROR(TransNotMatchedErr);

      ExpenseMatch.SETCURRENTKEY("Transaction Entry No.");
      ExpenseMatch.SETRANGE("Transaction Entry No.","Entry No.");
      ExpenseMatch.FINDFIRST;

      Expense.SETRANGE("Entry No.",ExpenseMatch."Expense Entry No.");
      Expense.FINDFIRST;
      IF Expense."Settlement No." = '' THEN
        IF NOT Expense.Posted THEN
          PAGE.RUN(PAGE::"Expense Card",Expense)
        ELSE
          PAGE.RUN(PAGE::"Posted Expense Card",Expense)
      ELSE BEGIN
        ExpHeader.GET(ExpHeader."Document Type"::Settlement,Expense."Settlement No.");
        IF NOT ExpHeader.Posted THEN
          PAGE.RUN(PAGE::"Expense Settlement Card",ExpHeader)
        ELSE
          PAGE.RUN(PAGE::"Posted Settlement Card",ExpHeader);
      END;
    END;

    PROCEDURE GetBankAccountCurrencyCode@1160040004() : Code[10];
    VAR
      UserCreditCard@1160040001 : Record 6086311;
    BEGIN
      IF UserCreditCard.GET("Continia User ID","Card No.") THEN
        EXIT(UserCreditCard.GetAccountCurrencyCode(UserCreditCard."Account Type",UserCreditCard."Account No."));
    END;

    PROCEDURE Navigate@2();
    VAR
      Navigate@1000 : Codeunit 6086317;
    BEGIN
      IF "Entry No." <> 0 THEN
        Navigate.NavigateBankTransaction(Rec);
    END;

    PROCEDURE LookupDimensions@1160040001();
    VAR
      EmDim@1160040000 : Record 6086360;
      ExpDim@1160040001 : Page 6086356;
    BEGIN
      EmDim.SETRANGE("Table ID",DATABASE::"Bank Transaction");
      EmDim.SETRANGE("Document Type",0);
      EmDim.SETRANGE("Document No.",'');
      EmDim.SETRANGE("Doc. Ref. No.","Entry No.");

      ExpDim.SETTABLEVIEW(EmDim);
      ExpDim.SetReadOnly;
      ExpDim.RUNMODAL;
    END;

    PROCEDURE GetMatchedExpenseEntryNo@1160040006() : Integer;
    VAR
      ExpenseMatch@1160040000 : Record 6086349;
    BEGIN
      ExpenseMatch.SETRANGE("Transaction Entry No.","Entry No.");
      IF ExpenseMatch.FINDFIRST THEN
        EXIT(ExpenseMatch."Expense Entry No.");
    END;

    PROCEDURE IsApplied@1() : Boolean;
    BEGIN
      IF ("Statement Status" = "Statement Status"::"Bank Transaction Applied") AND
         ("Statement No." <> '') AND
         ("Statement Line No." <> 0)
      THEN
        EXIT(TRUE);
    END;

    PROCEDURE SetStyle@6() : Text[1024];
    BEGIN
      IF IsApplied THEN
        EXIT('Favorable');

      EXIT('');
    END;

    PROCEDURE GetExpenseStatus@1160040005() : Integer;
    VAR
      Expense@1160040001 : Record 6086320;
      ExpenseStatus@1160040000 : 'Not Matched,Open,Pending Expense User,Pending Approval,Released,Posted';
    BEGIN
      IF NOT Expense.GET(GetMatchedExpenseEntryNo) THEN
        EXIT(ExpenseStatus::"Not Matched");

      CASE Expense.Status OF
        Expense.Status::Open:
          EXIT(ExpenseStatus::Open);
        Expense.Status::"Pending Expense User":
          EXIT(ExpenseStatus::"Pending Expense User");
        Expense.Status::"Pending Approval":
          EXIT(ExpenseStatus::"Pending Approval");
        Expense.Status::Released:
          IF Expense.Posted THEN
            EXIT(ExpenseStatus::Posted)
          ELSE
            EXIT(ExpenseStatus::Released);
      END;
    END;

    BEGIN
    END.
  }
}

