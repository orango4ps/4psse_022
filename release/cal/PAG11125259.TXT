OBJECT Page 11125259 Service Inspection Line SF
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Zeilen;
               ENU=Lines;
               NLD=Regels;
               NOR=Rader;
               SVE=Rader];
    SourceTable=Table11071751;
    PopulateAllFields=Yes;
    PageType=ListPart;
    AutoSplitKey=Yes;
    OnInit=BEGIN
             MandatoryEditable := TRUE;
             "Unit Of MeasureEditable" := TRUE;
             "Field TypeEditable" := TRUE;
           END;

    OnOpenPage=VAR
                 CentralDataManagement@1100528600 : Codeunit 11012010;
               BEGIN
                 CASE "Inspection Type" OF
                   "Inspection Type"::"Base Inspection List",
                   "Inspection Type"::"Standard Object",
                   "Inspection Type"::"Standard Object (Calls)":
                     IF NOT CentralDataManagement.ServiceInspectionFormEditable THEN
                       CurrPage.EDITABLE(FALSE);
                 END;
               END;

    OnAfterGetRecord=BEGIN
                       OnAfterGetCurrRecord;
                       DescriptionOnFormat;
                       CALCFIELDS("Depending on Line No.");

                       IF "Depending on Line No." <> 0 THEN
                         DependingOnLineNo := "Depending on Line No."
                       ELSE
                         DependingOnLineNo := 0;
                     END;

    OnNewRecord=BEGIN
                  IF Type = Type::Group THEN
                    "Field Type" := "Field Type"::" ";
                  OnAfterGetCurrRecord;
                END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1900562404;1 ;ActionGroup;
                      CaptionML=[DEU=Zeile;
                                 ENU=Line;
                                 NLD=Regel;
                                 NOR=RAD;
                                 SVE=Rad] }
      { 1903683204;2 ;Action    ;
                      CaptionML=[DEU=Optionswerte;
                                 ENU=Option Values;
                                 NLD=Optie waarden;
                                 NOR=Opsjonsverdier;
                                 SVE=Alternativv„rden];
                      Image=Splitlines;
                      OnAction=BEGIN
                                 ShowOptionValues();
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100528600;1;Group  ;
                IndentationColumnName=DescriptionIndentationValue;
                IndentationControls=Description;
                GroupType=Repeater }

    { 1100528601;2;Field  ;
                SourceExpr="Inspection Type";
                Visible=FALSE }

    { 1100528619;2;Field  ;
                SourceExpr="Service Contract No.";
                Visible=FALSE }

    { 1100528603;2;Field  ;
                SourceExpr=Code;
                Visible=FALSE }

    { 1100528605;2;Field  ;
                SourceExpr="Index Date";
                Visible=FALSE }

    { 1100528607;2;Field  ;
                SourceExpr="Line No.";
                Visible=FALSE }

    { 1100528609;2;Field  ;
                SourceExpr=Type;
                OnValidate=BEGIN
                             TypeOnAfterValidate;
                           END;
                            }

    { 1100528611;2;Field  ;
                SourceExpr="Inspection Code";
                OnValidate=BEGIN
                             DescriptionOnFormat;
                           END;

                OnLookup=VAR
                           BaseServiceInspectionLinesFRM@1100528601 : Page 11125255;
                           BaseServiceInspectionLine@1100528600 : Record 11071748;
                           ServiceInspectionGroup@1100528605 : Record 11071747;
                           ServiceInspectionGroupCode@1100528604 : Code[20];
                           ServiceInspectionGroupDescription@1100528603 : Text[50];
                         BEGIN
                           CASE Type OF
                             Type::Group:
                               BEGIN
                                 IF ServiceInspectionGroup.GET("Inspection Code") THEN;
                                 IF PAGE.RUNMODAL(0, ServiceInspectionGroup) = ACTION::LookupOK THEN
                                   VALIDATE("Inspection Code", ServiceInspectionGroup.Code);
                               END;
                             Type::Line:
                               BEGIN
                                 ServiceInspectionGroupCode := GetServiceInspectionGroupInfo(ServiceInspectionGroupDescription);
                                 BaseServiceInspectionLinesFRM.LOOKUPMODE(TRUE);
                                 BaseServiceInspectionLinesFRM.DetermineInterface(DATABASE::"Service Inspection Line", "Inspection Type",
                                   "Service Contract No.", Code, "Belongs to Scheme", "Belongs to Base Insp. List", "Index Date", "Line No.");
                                 IF BaseServiceInspectionLine.GET("Inspection Code") THEN
                                   BaseServiceInspectionLinesFRM.SETRECORD(BaseServiceInspectionLine);
                                 BaseServiceInspectionLine.SETFILTER("Inspection Group Code", '%1|%2', '', ServiceInspectionGroupCode);
                                 BaseServiceInspectionLinesFRM.SETTABLEVIEW(BaseServiceInspectionLine);
                                 IF BaseServiceInspectionLinesFRM.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                   BaseServiceInspectionLinesFRM.GETRECORD(BaseServiceInspectionLine);
                                   VALIDATE("Inspection Code", BaseServiceInspectionLine.Code);
                                   DescriptionOnFormat;
                                 END ELSE BEGIN
                                   IF BaseServiceInspectionLinesFRM.CheckMultiCopy THEN
                                     CurrPage.UPDATE(FALSE);
                                 END;
                               END;
                           END;
                         END;
                          }

    { 1100528613;2;Field  ;
                SourceExpr=Description;
                Style=Strong;
                StyleExpr=DescriptionEmphasize }

    { 1100528615;2;Field  ;
                SourceExpr="Field Type";
                Editable="Field TypeEditable" }

    { 1100528617;2;Field  ;
                SourceExpr="Unit Of Measure";
                Editable="Unit Of MeasureEditable" }

    { 1210190000;2;Field  ;
                SourceExpr=Mandatory;
                Editable=MandatoryEditable }

    { 1100528602;2;Field  ;
                SourceExpr="Lower Value";
                Editable=LowerValueEditable }

    { 1100528604;2;Field  ;
                SourceExpr="Upper Value";
                Editable=UpperValueEditable }

    { 1100528105;2;Field  ;
                Name=DependingOnLineNo;
                CaptionML=[DEU=Abh„ngig von Zeilennr.;
                           ENU=Depending on Line No.;
                           NLD=Afhankelijk van regelnr.];
                BlankZero=Yes;
                SourceExpr=DependingOnLineNo;
                Visible=false;
                Editable=DependenciesEditable;
                OnValidate=VAR
                             EmptyGuid@1100528100 : GUID;
                             ServiceInspectionLine@1100528101 : Record 11071751;
                           BEGIN
                             IF DependingOnLineNo = 0 THEN
                               VALIDATE("Depending on Id", EmptyGuid)
                             ELSE BEGIN
                               ServiceInspectionLine.SETRANGE("Inspection Type", "Inspection Type");
                               ServiceInspectionLine.SETRANGE("Service Contract No.", "Service Contract No.");
                               ServiceInspectionLine.SETRANGE(Code, Code);
                               ServiceInspectionLine.SETRANGE("Belongs to Scheme", "Belongs to Scheme");
                               ServiceInspectionLine.SETRANGE("Belongs to Base Insp. List", "Belongs to Base Insp. List");
                               ServiceInspectionLine.SETRANGE("Index Date", "Index Date");
                               ServiceInspectionLine.SETRANGE("Line No.", DependingOnLineNo);
                               IF ServiceInspectionLine.FINDFIRST THEN BEGIN
                                 VALIDATE("Depending on Id", ServiceInspectionLine.Id);
                                 MODIFY(TRUE);
                               END ELSE
                                 ERROR(Text001, FIELDCAPTION("Line No."), DependingOnLineNo);
                             END;
                           END;

                OnLookup=BEGIN
                           IF Type = Type::Group THEN
                             EXIT;

                           IF LookupDependencyLine("Depending on Id") THEN BEGIN
                             VALIDATE("Depending on Id");
                             CurrPage.UPDATE(TRUE);
                           END;
                         END;
                          }

    { 1100528101;2;Field  ;
                Lookup=No;
                AssistEdit=Yes;
                SourceExpr="Depending Condition";
                Visible=false;
                Editable=DependenciesEditable;
                OnAssistEdit=VAR
                               ServiceInspectionLine@1100528700 : Record 11071751;
                             BEGIN
                               IF Type = Type::Group THEN
                                 EXIT;

                               IF ISNULLGUID("Depending on Id") THEN
                                 ERROR(Text002, FIELDCAPTION("Depending on Line No."), FIELDCAPTION("Depending Condition"));

                               ServiceInspectionLine.SETRANGE(Id, "Depending on Id");
                               IF ServiceInspectionLine.FINDFIRST THEN BEGIN
                                 IF ServiceInspectionLine.LookupDependingCondition("Depending Condition") THEN
                                   ServiceInspectionLine.VALIDATE("Depending Condition");
                               END;
                             END;
                              }

    { 1100528102;2;Field  ;
                SourceExpr="Dependency Effect";
                Visible=false;
                Editable=DependenciesEditable }

  }
  CODE
  {
    VAR
      DescriptionEmphasize@19010547 : Boolean INDATASET;
      DescriptionIndentationValue@1100525000 : Integer INDATASET;
      "Field TypeEditable"@19039294 : Boolean INDATASET;
      "Unit Of MeasureEditable"@19025701 : Boolean INDATASET;
      MandatoryEditable@19040764 : Boolean INDATASET;
      LowerValueEditable@1100528601 : Boolean;
      UpperValueEditable@1100528600 : Boolean;
      DependingOnLineNo@1100528100 : Integer;
      Text001@1100528101 : TextConst 'DEU=Kann  %1 %2 nicht finden.;ENU=Cannot find the %1 %2.;NLD=Kan %1 %2 niet vinden.';
      Text002@1100528700 : TextConst 'DEU=%1 ist obligatorisch, bevor %2 eingegeben werden kann.;ENU=%1 is required before %2 can be filled in.;NLD=%1 is verplicht voordat %2 ingevuld kan worden.';
      DependenciesEditable@1100528701 : Boolean;

    PROCEDURE UpdateEditable@1100528600();
    BEGIN
      "Field TypeEditable" := Type = Type::Line;
      "Unit Of MeasureEditable" := Type = Type::Line;
      MandatoryEditable := Type = Type::Line;
      LowerValueEditable := (Type = Type::Line) AND ("Field Type" = "Field Type"::Value);
      UpperValueEditable := (Type = Type::Line) AND ("Field Type" = "Field Type"::Value);
      DependenciesEditable := Type = Type::Line;
    END;

    PROCEDURE ShowOptionValues@1100528601();
    VAR
      BaseServiceInspectionOption@1100528600 : Record 11071749;
      BaseServiceInspectionOptionPAG@1100525000 : Page 11125256;
    BEGIN
      TESTFIELD("Field Type", "Field Type"::Option);
      BaseServiceInspectionOption.SETRANGE("Base Inspection Line Code", "Inspection Code");
      BaseServiceInspectionOptionPAG.SETTABLEVIEW(BaseServiceInspectionOption);
      BaseServiceInspectionOptionPAG.EDITABLE := FALSE;
      BaseServiceInspectionOptionPAG.RUNMODAL;
    END;

    LOCAL PROCEDURE TypeOnAfterValidate@19069045();
    BEGIN
      UpdateEditable;
    END;

    LOCAL PROCEDURE OnAfterGetCurrRecord@19077479();
    BEGIN
      xRec := Rec;
      UpdateEditable;
    END;

    LOCAL PROCEDURE DescriptionOnFormat@19023855();
    BEGIN
      DescriptionEmphasize := FALSE;
      DescriptionIndentationValue := 0;
      IF Type = Type::Group THEN
        DescriptionEmphasize := TRUE
      ELSE
        DescriptionIndentationValue := 1;
    END;

    BEGIN
    END.
  }
}

