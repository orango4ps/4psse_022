OBJECT Page 11126676 Set-Up Out of Office Sharing
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Einrichtung der Out-of-Office-Genehmigungsteilung;
               ENU=Set-up Out of Office Approval Sharing;
               NLD=Goedkeuring delen bij afwezigheid instellen];
    OnOpenPage=BEGIN
                 IF NOT ApprovalManagement4PSConstr.GetOutOfOffice(USERID,ToUserId,FromDate,ToDate) THEN BEGIN
                   IF UserSetup.GET(USERID) THEN
                     ToUserId := UserSetup."Approver ID";
                 END;

                 ToUserName := GetApproverName;
               END;

  }
  CONTROLS
  {
    { 1100528405;;Container;
                ContainerType=ContentArea }

    { 1100528404;1;Group  ;
                GroupType=Group }

    { 1100528403;2;Field  ;
                CaptionML=[DEU=Weiterleiten ab;
                           ENU=Forward from date;
                           NLD=Doorsturen vanafdatum];
                SourceExpr=FromDate;
                OnValidate=BEGIN
                             UpdateOutOfOffice;
                           END;
                            }

    { 1100528402;2;Field  ;
                CaptionML=[DEU=Weiterleiten bis;
                           ENU=Forward to date;
                           NLD=Doorsturen totdatum];
                SourceExpr=ToDate;
                OnValidate=BEGIN
                             UpdateOutOfOffice;
                           END;
                            }

    { 1100528401;2;Field  ;
                CaptionML=[DEU=Weiterleiten an;
                           ENU=Forward to;
                           NLD=Doorsturen naar];
                SourceExpr=ToUserId;
                OnValidate=BEGIN
                             IF UPPERCASE(ToUserId) = UPPERCASE(USERID) THEN
                               ERROR(Text001);

                             IF ToUserId <> '' THEN
                               UserSetup.GET(ToUserId);

                             UpdateOutOfOffice;
                             ToUserName := GetApproverName
                           END;

                OnLookup=BEGIN
                           IF PAGE.RUNMODAL(0, UserSetup) = ACTION::LookupOK THEN BEGIN
                             Text := UserSetup."User ID";
                             EXIT(TRUE);
                           END;
                         END;
                          }

    { 1100528400;2;Field  ;
                CaptionML=[DEU=Weiterleiten an Name;
                           ENU=Forward to name;
                           NLD=Doorsturen naar naam];
                SourceExpr=ToUserName;
                Editable=FALSE }

  }
  CODE
  {
    VAR
      ApprovalManagement4PSConstr@1100528404 : Codeunit 11125349;
      UserSetup@1100528406 : Record 91;
      FromDate@1100528403 : Date;
      ToDate@1100528402 : Date;
      ToUserId@1100528401 : Code[50];
      ToUserName@1100528400 : Text[250];
      Text001@1100528405 : TextConst 'DEU=Sie k”nnen Dokumente nicht an sich selbst weiterleiten.;ENU=You can not forward documents to yourself;NLD=U kunt geen document naar uzelf doorsturen.;SVE="Det g†r inte att vidarebefordra dokument till sig sj„lv "';

    PROCEDURE UpdateOutOfOffice@161024035();
    BEGIN
      ApprovalManagement4PSConstr.SetOutOfOffice(USERID,ToUserId,FromDate,ToDate);
    END;

    PROCEDURE GetApproverName@161024014() : Text[1024];
    BEGIN
      UserSetup.INIT;
      IF UserSetup.GET(ToUserId) THEN
        UserSetup.CALCFIELDS("Name Employee");

      IF UserSetup."Name Employee" <> '' THEN
        EXIT(UserSetup."Name Employee")
      ELSE
        EXIT(UserSetup."User ID");
    END;

    BEGIN
    END.
  }
}

