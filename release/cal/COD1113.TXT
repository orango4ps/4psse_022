OBJECT Codeunit 1113 CA Jnl.-Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    TableNo=1101;
    OnRun=BEGIN
            CostJnlLine.COPY(Rec);
            Code;
            COPY(CostJnlLine);
          END;

  }
  CODE
  {
    VAR
      CostJnlLine@1000 : Record 1101;
      CostReg@1001 : Record 1105;
      CostJnlTemplate@1002 : Record 1100;
      Text001@1003 : TextConst 'ENU=Do you want to post the journal lines?;NOR=Vil du bokf›re kladdelinjene?;SVE=Vill du bokf”ra journalraderna?';
      Text002@1004 : TextConst 'ENU=There is nothing to post.;NOR=Det finnes ingenting † bokf›re.;SVE=Det finns inget att bokf”ra.';
      Text003@1005 : TextConst 'ENU=The journal lines were successfully posted.;NOR=Kladdelinjene ble bokf›rt uten problemer.;SVE=Journalraderna „r bokf”rda.';
      Text004@1006 : TextConst 'ENU=The journal lines were successfully posted. You are now in the %1 journal.;NOR=Kladdelinjene ble bokf›rt p† riktig m†te. Du er n† i kladd %1.;SVE=Journalraderna „r bokf”rda. Du „r nu i journal %1.';

    LOCAL PROCEDURE Code@1();
    VAR
      TempJnlBatchName@1000 : Code[10];
      HideDialog@1001 : Boolean;
    BEGIN
      WITH CostJnlLine DO BEGIN
        CostJnlTemplate.GET("Journal Template Name");
        CostJnlTemplate.TESTFIELD("Posting Report ID");

        HideDialog := FALSE;
        OnBeforePostJournalBatch(CostJnlLine,HideDialog);
        IF NOT HideDialog THEN
          IF NOT CONFIRM(Text001) THEN
            EXIT;

        TempJnlBatchName := "Journal Batch Name";
        CODEUNIT.RUN(CODEUNIT::"CA Jnl.-Post Batch",CostJnlLine);
        CostReg.GET("Line No.");
        CostReg.SETRECFILTER;
        REPORT.RUN(CostJnlTemplate."Posting Report ID",FALSE,FALSE,CostReg);

        IF "Line No." = 0 THEN
          MESSAGE(Text002)
        ELSE
          IF TempJnlBatchName = "Journal Batch Name" THEN
            MESSAGE(Text003)
          ELSE
            MESSAGE(Text004,"Journal Batch Name");

        IF NOT FIND('=><') OR (TempJnlBatchName <> "Journal Batch Name") THEN BEGIN
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          FILTERGROUP(0);
          "Line No." := 1;
        END;
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostJournalBatch@2(VAR CostJournalLine@1000 : Record 1101;VAR HideDialog@1001 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

