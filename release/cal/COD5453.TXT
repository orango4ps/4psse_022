OBJECT Codeunit 5453 Graph Sync. Runner - OnModify
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    TableNo=5151;
    OnRun=VAR
            IntegrationTableMapping@1006 : Record 5335;
            GraphIntegrationRecord@1007 : Record 5451;
            GraphSyncRunner@1000 : Codeunit 5452;
            GraphConnectionSetup@1001 : Codeunit 5456;
            GraphDataSetup@1002 : Codeunit 5455;
            GraphIntegrationTableSync@1005 : Codeunit 5451;
            SourceRecordRef@1003 : RecordRef;
            IntegrationMappingCode@1011 : Code[20];
            InboundConnectionName@1012 : Text;
            SynchronizeConnectionName@1013 : Text;
            DestinationGraphId@1008 : Text[250];
            SyncOnRecordSkipped@1004 : Boolean;
          BEGIN
            IF ISTEMPORARY THEN
              EXIT;

            IF NOT GraphSyncRunner.IsGraphSyncEnabled THEN
              EXIT;

            IF NOT GraphConnectionSetup.CanRunSync THEN
              EXIT;

            GraphConnectionSetup.RegisterConnections;
            IntegrationMappingCode := GraphDataSetup.GetMappingCodeForTable("Table ID");
            SynchronizeConnectionName := GraphConnectionSetup.GetSynchronizeConnectionName("Table ID");
            InboundConnectionName := GraphConnectionSetup.GetInboundConnectionName("Table ID");

            SETDEFAULTTABLECONNECTION(TABLECONNECTIONTYPE::MicrosoftGraph,SynchronizeConnectionName,TRUE);

            GraphDataSetup.GetIntegrationTableMapping(IntegrationTableMapping,IntegrationMappingCode);
            SourceRecordRef.GET("Record ID");

            SyncOnRecordSkipped := GraphIntegrationTableSync.PerformRecordSynchToIntegrationTable(IntegrationTableMapping,SourceRecordRef);

            // SyncOnRecordSkipped = TRUE when conflict is detected. In this case we force sync graph to nav
            IF SyncOnRecordSkipped AND GraphIntegrationRecord.FindIDFromRecordID(SourceRecordRef.RECORDID,DestinationGraphId) THEN BEGIN
              SETDEFAULTTABLECONNECTION(TABLECONNECTIONTYPE::MicrosoftGraph,InboundConnectionName,TRUE);
              GraphDataSetup.GetGraphRecord(SourceRecordRef,DestinationGraphId,"Table ID");
              GraphIntegrationTableSync.PerformRecordSynchFromIntegrationTable(IntegrationTableMapping,SourceRecordRef);
            END;
          END;

  }
  CODE
  {

    BEGIN
    END.
  }
}

