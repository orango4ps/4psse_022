OBJECT Codeunit 11072053 Insurance Jnl.-B.Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00 (5673);
  }
  PROPERTIES
  {
    TableNo=11072091;
    OnRun=BEGIN
            InsuranceJnlBatch.COPY(Rec);
            Code;
            COPY(InsuranceJnlBatch);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Mchten Sie die Buch.-Bltter buchen und den Buchungsbericht drucken?;ENU=Do you want to post the journals and print the posting report?;NLD=Wilt u de dagboeken boeken en het boekingsrapport afdrukken?;NOR=Vil du bokfre journalene og skrive ut bokfringsrapporten?;SVE=Vill du bokfra journalerna och skriva ut bokfringsrapporten?';
      Text001@1001 : TextConst 'DEU=Die Buch.-Bltter sind gebucht worden.;ENU=The journals were successfully posted.;NLD=De dagboeken werden probleemloos geboekt.;NOR=Kladdene ble bokfrt p riktig mte.;SVE=Journalerna r bokfrda.';
      Text002@1002 : TextConst 'DEU="Es konnten nicht alle Buch.-Bltter gebucht werden. ";ENU="It was not possible to post all of the journals. ";NLD="Niet alle dagboeken konden geboekt worden. ";NOR="Det er ikke mulig  bokfre alle kladdene. ";SVE="Det var inte mjligt att bokfra alla journaler. "';
      Text003@1003 : TextConst 'DEU=Die nicht erfolgreich gebuchten Buch.-Bltter sind jetzt markiert.;ENU=The journals that were not successfully posted are now marked.;NLD=De dagboeken die niet zonder problemen konden geboekt worden, zijn gemarkeerd.;NOR=Kladdene som ikke ble bokfrt, er merket.;SVE=Journalerna som inte blev bokfrda r nu markerade.';
      InsuranceJnlTempl@1004 : Record 11072090;
      InsuranceJnlBatch@1005 : Record 11072091;
      InsuranceJnlLine@1006 : Record 11072092;
      InsuranceReg@1007 : Record 11072093;
      InsuranceJnlPostBatch@1008 : Codeunit 11072046;
      JournalWithErrors@1009 : Boolean;

    LOCAL PROCEDURE Code@1();
    VAR
      HideDialog@1000 : Boolean;
    BEGIN
      WITH InsuranceJnlBatch DO BEGIN
        InsuranceJnlTempl.GET("Journal Template Name");
        InsuranceJnlTempl.TESTFIELD("Posting Report ID");

        HideDialog := FALSE;
        OnBeforePostJournalBatch(InsuranceJnlBatch,HideDialog);
        IF NOT HideDialog THEN
          IF NOT CONFIRM(Text000,FALSE) THEN
            EXIT;

        FIND('-');
        REPEAT
          InsuranceJnlLine."Journal Template Name" := "Journal Template Name";
          InsuranceJnlLine."Journal Batch Name" := Name;
          InsuranceJnlLine."Line No." := 1;

          CLEAR(InsuranceJnlPostBatch);
          IF InsuranceJnlPostBatch.RUN(InsuranceJnlLine) THEN BEGIN
            IF InsuranceReg.GET(InsuranceJnlLine."Line No.") THEN BEGIN
              InsuranceReg.SETRECFILTER;
              REPORT.RUN(InsuranceJnlTempl."Posting Report ID",FALSE,FALSE,InsuranceReg);
            END;
            MARK(FALSE);
          END
          ELSE BEGIN
            MARK(TRUE);
            JournalWithErrors := TRUE;
          END;
        UNTIL NEXT = 0;

        IF NOT JournalWithErrors THEN
          MESSAGE(Text001)
        ELSE
          MESSAGE(
            Text002 +
            Text003);

        IF NOT FIND('=><') THEN BEGIN
          RESET;
          FILTERGROUP := 2;
          SETRANGE("Journal Template Name","Journal Template Name");
          FILTERGROUP := 0;
          Name := '';
        END;
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostJournalBatch@2(VAR InsuranceJournalBatch@1000 : Record 11072091;VAR HideDialog@1001 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

