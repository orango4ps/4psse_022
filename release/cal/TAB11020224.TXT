OBJECT Table 11020224 Hour Accounting Project
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               VALIDATE(Week,Week);
               TESTFIELD("Project No.");
             END;

    OnDelete=BEGIN
               HourProjLine.SETRANGE(Year,Year);
               HourProjLine.SETRANGE(Week,Week);
               HourProjLine.SETRANGE("Header Project No.","Project No.");
               HourProjLine.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               VALIDATE(Week,Week);
               TESTFIELD("Project No.");
               VALIDATE("Project No.");

               HourProjLine.SETRANGE(Year,xRec.Year);
               HourProjLine.SETRANGE(Week,xRec.Week);
               HourProjLine.SETRANGE("Header Project No.",xRec."Project No.");
               IF HourProjLine.FINDSET(TRUE, FALSE) THEN
                 REPEAT
                   HourProjLine2.TRANSFERFIELDS(HourProjLine);
                   HourProjLine2.Year := Year;
                   HourProjLine2.Week := Week;
                   HourProjLine2."Header Project No." := "Project No.";
                   IF HourProjLine2.Type <> HourProjLine2.Type::Indirect THEN
                     HourProjLine2."Project No." := "Project No.";
                   HourProjLine2."Document No." := "Document No.";
                   HourProjLine2."Posting Date" := "Posting Date";
                   HourProjLine2.SetHeaderForRenameHourLine(Rec);
                   HourProjLine2.INSERT(TRUE);
                   HourProjLine.DELETE(TRUE);
                 UNTIL HourProjLine.NEXT = 0;
             END;

    CaptionML=[ENU=Hour Accounting Project;
               NOR=Timerapporteringsprosjekt;
               SVE=Tidrapporteringsprojekt];
    LookupPageID=Page11020246;
  }
  FIELDS
  {
    { 20  ;   ;Year                ;Integer       ;OnValidate=BEGIN
                                                                ValidateFields(FIELDCAPTION(Year));
                                                              END;

                                                   CaptionML=[ENU=Year;
                                                              NOR=èr;
                                                              SVE=èr];
                                                   MinValue=1 }
    { 30  ;   ;Week                ;Integer       ;OnValidate=BEGIN
                                                                ValidateFields(FIELDCAPTION(Week));

                                                                IF "Posting Date" <> xRec."Posting Date" THEN BEGIN
                                                                  HourProjLine.SETRANGE(Year, Year);
                                                                  HourProjLine.SETRANGE(Week, Week);
                                                                  HourProjLine.SETRANGE("Header Project No.", "Project No.");
                                                                  IF NOT HourProjLine.ISEMPTY THEN //kzwerver, 110609, n, #Rfc ULC
                                                                    IF HourProjLine.FINDSET(TRUE, FALSE) THEN
                                                                      REPEAT
                                                                        HourProjLine."Posting Date" := "Posting Date";
                                                                        HourProjLine.MODIFY(TRUE);
                                                                      UNTIL HourProjLine.NEXT = 0;
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              DateSelectionManagement@1100409000 : Codeunit 11130005;
                                                            BEGIN
                                                              DateSelectionManagement.GetWeekYear(Week, Year);
                                                              VALIDATE(Week);
                                                            END;

                                                   CaptionML=[ENU=Week;
                                                              NOR=Uke;
                                                              SVE=Vecka];
                                                   MinValue=1;
                                                   MaxValue=53 }
    { 35  ;   ;Project No.         ;Code20        ;TableRelation=Job.No.;
                                                   OnValidate=BEGIN
                                                                ValidateFields(FIELDCAPTION("Project No."));
                                                              END;

                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr] }
    { 36  ;   ;Project Description ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Job.Description WHERE (No.=FIELD(Project No.)));
                                                   CaptionML=[ENU=Project Description;
                                                              NOR=Prosjektbeskrivelse;
                                                              SVE=Projektbeskrivning];
                                                   Editable=No }
    { 41  ;   ;Document No.        ;Code20        ;OnValidate=VAR
                                                                HourLineRec@1100485000 : Record 11012039;
                                                              BEGIN
                                                                HourLineRec.SETRANGE(Year,Year);
                                                                HourLineRec.SETRANGE(Week,Week);
                                                                HourLineRec.SETRANGE("Document No.",xRec."Document No.");
                                                                IF HourLineRec.FINDFIRST THEN
                                                                  HourLineRec.TESTFIELD("Document No.", "Document No.");
                                                              END;

                                                   CaptionML=[ENU=Document No.;
                                                              NOR=Dokumentnr.;
                                                              SVE=Verifikationsnr] }
    { 42  ;   ;Posting Date        ;Date          ;CaptionML=[ENU=Posting Date;
                                                              NOR=Bokfõringsdato;
                                                              SVE=Bokfîringsdatum];
                                                   Editable=No }
    { 100 ;   ;Called From Site Mgt Form;Boolean  ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Called From Site Mgt Form;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Year,Week,Project No.                   ;Clustered=Yes }
    {    ;Project No.,Year                         }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      HourProjLine@1210190003 : Record 11020225;
      HourProjLine2@1210190004 : Record 11020225;
      TmpHourHeader@1210190005 : TEMPORARY Record 11012038;
      TmpHourLine@1210190002 : TEMPORARY Record 11012039;

    PROCEDURE ValidateFields@1210190001(ChangedFieldName@1210190000 : Text[100]);
    BEGIN
      TmpHourHeader.TRANSFERFIELDS(Rec);
      CASE ChangedFieldName OF
        FIELDCAPTION(Year) : TmpHourHeader.VALIDATE(Year,Year);
        FIELDCAPTION(Week) : TmpHourHeader.VALIDATE(Week,Week);
        FIELDCAPTION("Project No.") :
          BEGIN
            TmpHourLine."Project No." := "Project No.";
            TmpHourLine.VALIDATE("Project No.");
          END;
      END;
      TRANSFERFIELDS(TmpHourHeader);
    END;

    BEGIN
    END.
  }
}

