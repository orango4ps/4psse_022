OBJECT Table 11012633 Project CF Product
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnModify=BEGIN
               CheckFixed;
             END;

    OnDelete=BEGIN
               IF NOT DeleteCompleteProject THEN
                 CheckFixed;
             END;

    CaptionML=ENU=Project CF Product;
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation="Project Development";
                                                   OnValidate=VAR
                                                                ProjectDevelopment@1100527700 : Record 11012915;
                                                              BEGIN
                                                                ProjectDevelopment.CheckMainProject("Project No.");
                                                              END;

                                                   CaptionML=[ENU=Project No.;
                                                              SVE=Projektnr] }
    { 20  ;   ;Cash Flow           ;DateTime      ;TableRelation="Project CF"."Cash Flow";
                                                   CaptionML=[ENU=Cash Flow;
                                                              SVE=Kassafl”de] }
    { 30  ;   ;Product             ;Code10        ;CaptionML=[ENU=Product;
                                                              SVE=Produkt];
                                                   NotBlank=Yes }
    { 40  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              SVE=Beskrivning] }
    { 50  ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                CalcTotalRevenueExclVAT;
                                                              END;

                                                   CaptionML=[ENU=Quantity;
                                                              SVE=Antal] }
    { 60  ;   ;Unit of Measure Code;Code10        ;TableRelation="Unit of Measure";
                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              SVE=Enhetskod] }
    { 65  ;   ;Sold                ;Decimal       ;CaptionML=[ENU=Sold;
                                                              SVE=S†ld] }
    { 70  ;   ;Sales Price Incl. VAT;Decimal      ;OnValidate=BEGIN
                                                                CalcUnitPriceExclVAT;
                                                              END;

                                                   CaptionML=[ENU=Sales Price Incl. VAT;
                                                              SVE=Ber„knat f”rs„ljningspris inkl. moms];
                                                   AutoFormatType=1 }
    { 75  ;   ;Construction Cost % ;Decimal       ;OnValidate=BEGIN
                                                                CalcConstructionCostExclVAT(FIELDNO("Construction Cost %"));
                                                              END;

                                                   CaptionML=ENU=Construction Cost %;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 80  ;   ;Construction Cost Excl. VAT;Decimal;OnValidate=BEGIN
                                                                CalcConstructionCostPercentage;
                                                                CalcLandCostExclVAT;
                                                              END;

                                                   CaptionML=ENU=Construction Cost Excl. VAT;
                                                   AutoFormatType=1 }
    { 90  ;   ;Land Cost Excl. VAT ;Decimal       ;OnValidate=BEGIN
                                                                CalcConstructionCostExclVAT(FIELDNO("Land Cost Excl. VAT"));
                                                              END;

                                                   CaptionML=ENU=Land Cost Excl. VAT;
                                                   AutoFormatType=1 }
    { 100 ;   ;Total Revenue Excl. VAT;Decimal    ;CaptionML=ENU=Total Revenue Excl. VAT;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 110 ;   ;Unit Price Excl. VAT;Decimal       ;OnValidate=BEGIN
                                                                CalcTotalRevenueExclVAT;
                                                                CalcConstructionCostExclVAT(FIELDNO("Unit Price Excl. VAT"));
                                                              END;

                                                   CaptionML=ENU=Unit Price Excl. VAT;
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 120 ;   ;Record Type         ;Option        ;CaptionML=[ENU=Record Type;
                                                              SVE=Posttyp];
                                                   OptionCaptionML=ENU=Company,Project,Cash Flow;
                                                   OptionString=Company,Project,Cash Flow }
    { 130 ;   ;VAT Bus. Posting Group;Code20      ;TableRelation="VAT Business Posting Group";
                                                   OnValidate=BEGIN
                                                                CalcVATPercentage;
                                                              END;

                                                   CaptionML=[ENU=VAT Bus. Posting Group;
                                                              SVE=Moms r”relsebokf”ringsmall] }
    { 140 ;   ;VAT Prod. Posting Group;Code20     ;TableRelation="VAT Product Posting Group";
                                                   OnValidate=BEGIN
                                                                CalcVATPercentage;
                                                              END;

                                                   CaptionML=[ENU=VAT Prod. Posting Group;
                                                              SVE=Moms produktbokf”ringsmall] }
    { 150 ;   ;VAT %               ;Decimal       ;OnValidate=BEGIN
                                                                CalcUnitPriceExclVAT;
                                                              END;

                                                   CaptionML=[ENU=VAT %;
                                                              SVE=Momssats %];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100;
                                                   Editable=No }
    { 160 ;   ;Rubric Construct    ;Code10        ;TableRelation="Project CF Rubric".Rubric WHERE (Project No.=FIELD(Project No.),
                                                                                                   Cash Flow=FIELD(Cash Flow),
                                                                                                   Record Type=FIELD(Record Type),
                                                                                                   Type=FILTER(Revenues),
                                                                                                   Template Code=FIELD(Template Code));
                                                   CaptionML=ENU=Rubric Construct }
    { 165 ;   ;Rubric Land         ;Code10        ;TableRelation="Project CF Rubric".Rubric WHERE (Project No.=FIELD(Project No.),
                                                                                                   Cash Flow=FIELD(Cash Flow),
                                                                                                   Record Type=FIELD(Record Type),
                                                                                                   Type=FILTER(Revenues),
                                                                                                   Template Code=FIELD(Template Code));
                                                   CaptionML=ENU=Rubric Land }
    { 170 ;   ;Selling Rate (Qty. / Month);Decimal;CaptionML=ENU=Selling Rate (Qty. / Month);
                                                   BlankZero=Yes }
    { 180 ;   ;Installment Scheme  ;Code10        ;TableRelation="Installment Scheme".Code WHERE (Type=CONST(Project));
                                                   CaptionML=[ENU=Installment Scheme;
                                                              SVE=Betalplan] }
    { 190 ;   ;Template Code       ;Code10        ;TableRelation="Project CF Template";
                                                   CaptionML=[ENU=Template Code;
                                                              SVE=Mallkod] }
    { 200 ;   ;Comment             ;Text50        ;DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Comment;
                                                              SVE=Kommentar] }
    { 210 ;   ;Startdate Delay     ;Date          ;OnValidate=BEGIN
                                                                IF "Startdate Delay" = 0D THEN
                                                                  "Delay Duration (Months)" := 0;
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Startdate Delay }
    { 220 ;   ;Delay Duration (Months);Integer    ;DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Delay Duration (Months);
                                                   BlankZero=Yes }
  }
  KEYS
  {
    {    ;Record Type,Project No.,Cash Flow,Product,Template Code;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Product,Description                      }
  }
  CODE
  {
    VAR
      DeleteCompleteProject@1100527700 : Boolean;

    PROCEDURE InitRecord@1100527700();
    VAR
      Job@1100527700 : Record 11072003;
      ProjectPrincipal@1100527701 : Record 11012005;
      ProjectCF@1100527703 : Record 11012635;
    BEGIN
      IF "Project No." <> '' THEN BEGIN
        "Template Code" := ProjectCF.GetTemplateCode("Project No.","Cash Flow");

        Job.GET("Project No.");
        IF Job."Bill-to Customer No." <> '' THEN BEGIN
          ProjectPrincipal.GET("Project No.",Job."Bill-to Customer No.");
          IF ProjectPrincipal."VAT Prod. Posting Group" <> '' THEN
            VALIDATE("VAT Prod. Posting Group",ProjectPrincipal."VAT Prod. Posting Group");
          IF ProjectPrincipal."VAT Bus. Posting Group" <> '' THEN
            VALIDATE("VAT Bus. Posting Group",ProjectPrincipal."VAT Bus. Posting Group");
        END;
      END;
    END;

    LOCAL PROCEDURE CalcConstructionCostExclVAT@1100527706(SourceFieldNo@1100527700 : Integer);
    BEGIN
      CASE SourceFieldNo OF
        FIELDNO("Unit Price Excl. VAT"),
        FIELDNO("Land Cost Excl. VAT"):
          VALIDATE("Construction Cost Excl. VAT","Unit Price Excl. VAT" - "Land Cost Excl. VAT");
        FIELDNO("Construction Cost %"):
          VALIDATE("Construction Cost Excl. VAT",ROUND("Unit Price Excl. VAT" * "Construction Cost %"/100));
      END;
    END;

    LOCAL PROCEDURE CalcConstructionCostPercentage@1100527707();
    BEGIN
      IF "Unit Price Excl. VAT" <> 0 THEN
        "Construction Cost %" := ROUND("Construction Cost Excl. VAT" / "Unit Price Excl. VAT" * 100);
    END;

    LOCAL PROCEDURE CalcLandCostExclVAT@1100527701();
    BEGIN
      IF "Land Cost Excl. VAT" <> "Unit Price Excl. VAT" - "Construction Cost Excl. VAT" THEN
        VALIDATE("Land Cost Excl. VAT","Unit Price Excl. VAT" - "Construction Cost Excl. VAT");
    END;

    LOCAL PROCEDURE CalcVATPercentage@1100527709();
    VAR
      VATPostingSetup@1100527700 : Record 325;
    BEGIN
      IF VATPostingSetup.GET("VAT Bus. Posting Group","VAT Prod. Posting Group") THEN
        VALIDATE("VAT %",VATPostingSetup."VAT %")
      ELSE
        VALIDATE("VAT %",0);
    END;

    LOCAL PROCEDURE CalcUnitPriceExclVAT@1100527708();
    BEGIN
      VALIDATE("Unit Price Excl. VAT",ROUND("Sales Price Incl. VAT" / (1 + "VAT %" / 100)));
    END;

    LOCAL PROCEDURE CalcTotalRevenueExclVAT@1100527702();
    BEGIN
      VALIDATE("Total Revenue Excl. VAT",Quantity * "Unit Price Excl. VAT");
    END;

    PROCEDURE CheckFixed@1100527704();
    VAR
      ProjectCF@1100527700 : Record 11012635;
    BEGIN
      IF "Record Type" = "Record Type"::"Cash Flow" THEN BEGIN
        ProjectCF.GET("Project No.","Cash Flow");
        ProjectCF.TESTFIELD(Fixed,FALSE);
      END;
    END;

    PROCEDURE SetDeleteCompleteProject@1100527703();
    BEGIN
      DeleteCompleteProject := TRUE;
    END;

    PROCEDURE CopyProductsFromProject@1100527705();
    VAR
      NewProjectCFProduct@1100527700 : Record 11012633;
      ProjectCFProduct@1100527701 : Record 11012633;
    BEGIN
      ProjectCFProduct.SETRANGE("Cash Flow","Cash Flow");
      ProjectCFProduct.SETRANGE("Project No.","Project No.");
      ProjectCFProduct.SETRANGE("Record Type",ProjectCFProduct."Record Type"::"Cash Flow");
      ProjectCFProduct.SETRANGE("Template Code","Template Code");
      IF ProjectCFProduct.FINDSET THEN
        ProjectCFProduct.DELETEALL;

      ProjectCFProduct.RESET;
      ProjectCFProduct.SETRANGE("Record Type",ProjectCFProduct."Record Type"::Project);
      ProjectCFProduct.SETRANGE("Project No.","Project No.");
      ProjectCFProduct.SETRANGE("Cash Flow",0DT);
      IF ProjectCFProduct.FINDSET THEN
        REPEAT
          NewProjectCFProduct := ProjectCFProduct;
          NewProjectCFProduct."Cash Flow" := "Cash Flow";
          NewProjectCFProduct."Record Type" := NewProjectCFProduct."Record Type"::"Cash Flow";
          NewProjectCFProduct.INSERT;
        UNTIL ProjectCFProduct.NEXT = 0;
    END;

    PROCEDURE ProductAlreadyExistOnCashflow@1100527710() : Boolean;
    VAR
      ProjectCFProduct@1100527700 : Record 11012633;
    BEGIN
      ProjectCFProduct.SETRANGE("Cash Flow","Cash Flow");
      ProjectCFProduct.SETRANGE("Project No.","Project No.");
      ProjectCFProduct.SETRANGE("Record Type",ProjectCFProduct."Record Type"::"Cash Flow");
      ProjectCFProduct.SETRANGE("Template Code","Template Code");
      IF ProjectCFProduct.ISEMPTY THEN
        EXIT(FALSE)
      ELSE
        EXIT(TRUE);
    END;

    PROCEDURE CopyProductsToProject@1100527711();
    VAR
      NewProjectCFProduct@1100527700 : Record 11012633;
      ProjectCFProduct@1100527701 : Record 11012633;
    BEGIN
      ProjectCFProduct.SETRANGE("Cash Flow",0DT);
      ProjectCFProduct.SETRANGE("Project No.","Project No.");
      ProjectCFProduct.SETRANGE("Record Type",ProjectCFProduct."Record Type"::Project);
      ProjectCFProduct.SETRANGE("Template Code","Template Code");
      IF ProjectCFProduct.FINDSET THEN
        ProjectCFProduct.DELETEALL;

      ProjectCFProduct.RESET;
      ProjectCFProduct.SETRANGE("Cash Flow","Cash Flow");
      ProjectCFProduct.SETRANGE("Project No.","Project No.");
      ProjectCFProduct.SETRANGE("Record Type",ProjectCFProduct."Record Type"::"Cash Flow");
      ProjectCFProduct.SETRANGE("Template Code","Template Code");
      IF ProjectCFProduct.FINDSET THEN
        REPEAT
          NewProjectCFProduct := ProjectCFProduct;
          NewProjectCFProduct."Cash Flow" := 0DT;
          NewProjectCFProduct."Record Type" := NewProjectCFProduct."Record Type"::Project;
          NewProjectCFProduct.INSERT;
        UNTIL ProjectCFProduct.NEXT = 0;
    END;

    PROCEDURE ProductAlreadyExistOnProject@1100527713() : Boolean;
    VAR
      ProjectCFProduct@1100527700 : Record 11012633;
    BEGIN
      ProjectCFProduct.SETRANGE("Cash Flow",0DT);
      ProjectCFProduct.SETRANGE("Project No.","Project No.");
      ProjectCFProduct.SETRANGE("Record Type",ProjectCFProduct."Record Type"::Project);
      ProjectCFProduct.SETRANGE("Template Code","Template Code");
      IF ProjectCFProduct.ISEMPTY THEN
        EXIT(FALSE)
      ELSE
        EXIT(TRUE);
    END;

    PROCEDURE GetTemplateData@1100527712();
    VAR
      ProjectCFTemplate@1100527700 : Record 11012640;
    BEGIN
      IF NOT ProjectCFTemplate.GET("Template Code") THEN
        EXIT;

      VALIDATE("VAT Bus. Posting Group",ProjectCFTemplate."VAT Bus. Posting Group");
      VALIDATE("VAT Prod. Posting Group",ProjectCFTemplate."VAT Prod. Posting Group");
      "Rubric Construct" := ProjectCFTemplate."Rubric Construct";
      "Rubric Land" := ProjectCFTemplate."Rubric Land";
    END;

    BEGIN
    END.
  }
}

