OBJECT Table 11124993 OSA User Setup GAP
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnInsert=BEGIN
               "Product Code" := GAPProducts.GetProductCodeOSA;
               UpdateIndentationColumn;

               IF Username <> '' THEN
                 VALIDATE(Active, Active::Yes);
             END;

    OnModify=BEGIN
               UpdateIndentationColumn;
             END;

    OnDelete=VAR
               PersistedGrantGAP@1100528700 : Record 11124991;
             BEGIN
               PersistedGrantGAP.SETRANGE("Subject Id", Username);
               PersistedGrantGAP.SETRANGE("Client Id", Rec."Product Code");
               PersistedGrantGAP.DELETEALL;
             END;

    OnRename=VAR
               UserByAppSetupGroupGAP@1100528700 : Record 11124980;
             BEGIN
               IF xRec."Group Name" = "Group Name" THEN
                 EXIT;

               IF UserByAppSetupGroupGAP.GET(Username, "Product Code") THEN BEGIN
                 UserByAppSetupGroupGAP.VALIDATE("Group Name", "Group Name");
                 UserByAppSetupGroupGAP.MODIFY(TRUE);
               END;
             END;

    CaptionML=ENU=On Site App User Setup;
    LookupPageID=Page11129166;
  }
  FIELDS
  {
    { 10  ;   ;Group Name          ;Code10        ;TableRelation="App Setup Group GAP".Name WHERE (Product Code=FIELD(Product Code));
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Group Name;
                                                              SVE=Gruppnamn] }
    { 20  ;   ;Username            ;Code100       ;TableRelation="App User GAP".Username;
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Username;
                                                              SVE=Anv„ndarnamn] }
    { 25  ;   ;Indentation Column  ;Integer       ;DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Indentation Column;
                                                   NotBlank=Yes }
    { 30  ;   ;Product Code        ;Code10        ;TableRelation="Products GAP".Code;
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Product Code;
                                                              SVE=Produktkod];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 40  ;   ;Active              ;Option        ;OnValidate=VAR
                                                                LicenseManagement@1100528000 : Codeunit 11229289;
                                                                ValidationMode@1100528001 : ',WebService,UserInput,NoError';
                                                              BEGIN
                                                                CheckIsUserSetting;
                                                                IF Active = Active::Yes THEN BEGIN
                                                                  LicenseManagement.ValidateUserCountOSA(TRUE, ValidationMode::UserInput);
                                                                END ELSE
                                                                  Active := Active::No;
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Active;
                                                              SVE=Aktiv];
                                                   OptionCaptionML=[ENU=" ,Yes,No";
                                                                    SVE=" ,Ja,Nej"];
                                                   OptionString=[ ,Yes,No] }
    { 50  ;   ;App Version         ;Text30        ;OnValidate=BEGIN
                                                                CheckIsUserSetting;
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=App Version;
                                                              SVE=Appversion];
                                                   Editable=No }
    { 60  ;   ;Login Last Validated on;DateTime   ;OnValidate=BEGIN
                                                                CheckIsUserSetting;
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Login Last Validated on;
                                                   Editable=No }
    { 70  ;   ;Common Data Last Sync. on;DateTime ;OnValidate=BEGIN
                                                                CheckIsUserSetting;
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Common Data Last Synchonized on;
                                                   Editable=No }
    { 100 ;   ;Document Type Group ;Code10        ;TableRelation="Document Type Group".Code;
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Document Type Group }
  }
  KEYS
  {
    {    ;Group Name,Username                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      GAPProducts@1100528000 : Record 11124990;
      TEXT000@1100528001 : TextConst 'ENU=This property can not be set as a default value for a group.';

    LOCAL PROCEDURE UpdateIndentationColumn@1100528550();
    BEGIN
      IF Username = '' THEN
        "Indentation Column" := 1
      ELSE
        "Indentation Column" := 2;
    END;

    LOCAL PROCEDURE CheckIsUserSetting@1100528551();
    BEGIN
      IF Username = '' THEN
        ERROR(TEXT000);
    END;

    PROCEDURE GetDocumentTypeGroup@1100528700() : Code[10];
    VAR
      OSAUserSetupGAP@1100528700 : Record 11124993;
    BEGIN
      IF "Document Type Group" <> '' THEN
        EXIT("Document Type Group");

      IF OSAUserSetupGAP.GET("Group Name", '') THEN
        EXIT(OSAUserSetupGAP."Document Type Group");
      // This code can be generalized, you should implement something like codeunit 11012194 / Procedure SendMobileUserSettings
    END;

    BEGIN
    END.
  }
}

