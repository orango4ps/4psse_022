OBJECT XMLport 11012065 Import GL Entries Payroll V2
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Import GL Entries Payroll V2;
    Direction=Import;
    DefaultFieldsValidation=No;
    TextEncoding=WINDOWS;
    OnInitXMLport=BEGIN
                    HumanResourcesSetup.GET;
                    GLSetup.GET;

                    HumanResourcesSetup.TESTFIELD("Salary Application",HumanResourcesSetup."Salary Application"::Payroll);
                    IF (HumanResourcesSetup."Salary Application Version" <> '2') AND
                       (UPPERCASE(HumanResourcesSetup."Salary Application Version") <> 'CSV-2') THEN
                    BEGIN
                      XMLPORT.RUN(XMLPORT::"Import GL Entries Payroll");
                      COMMIT; // Without commit the imported records would be reverted.
                      ERROR('');
                    END;

                    NavDimCode[1] := GLSetup."Shortcut Dimension 1 Code";
                    NavDimCode[2] := GLSetup."Shortcut Dimension 2 Code";
                    NavDimCode[3] := GLSetup."Shortcut Dimension 3 Code";
                    NavDimCode[4] := GLSetup."Shortcut Dimension 4 Code";
                    NavDimCode[5] := GLSetup."Shortcut Dimension 5 Code";
                    NavDimCode[6] := GLSetup."Shortcut Dimension 6 Code";
                    NavDimCode[7] := GLSetup."Shortcut Dimension 7 Code";
                    NavDimCode[8] := GLSetup."Shortcut Dimension 8 Code";

                    HumanResourcesSetup.TESTFIELD("GL Entry Journal Template");
                    HumanResourcesSetup.TESTFIELD("GL Entry Journal Batch");

                    currXMLport.FILENAME := HumanResourcesSetup."Pathname Files from Salary";
                  END;

    OnPreXMLport=BEGIN
                   PostRec.LOCKTABLE;
                   PostRec.SETRANGE("Journal Template Name",HumanResourcesSetup."GL Entry Journal Template");
                   PostRec.SETRANGE("Journal Batch Name",HumanResourcesSetup."GL Entry Journal Batch");
                   IF PostRec.FIND('+') THEN
                     LineCounter := PostRec."Line No."
                   ELSE
                     LineCounter := 0;
                 END;

    OnPostXMLport=BEGIN
                    NewFile := HumanResourcesSetup.DetermineBackupFile(currXMLport.FILENAME);
                    IF NewFile <> currXMLport.FILENAME THEN
                      FileMgt.RenameClientFile(currXMLport.FILENAME, NewFile, TRUE);
                  END;

    Format=Fixed Text;
  }
  ELEMENTS
  {
    { [{87970E60-BDE0-4310-8453-1591E8F64F60}];  ;Root                ;Element ;Text     }

    { [{0EC2CED4-FDBD-429B-9480-B998CF9000BC}];1 ;GenJournalLine      ;Element ;Table   ;
                                                  SourceTable=Table81;
                                                  SourceTableView=SORTING(Field1,Field51,Field2);
                                                  Import::OnAfterInitRecord=BEGIN
                                                                              "Gen. Journal Line".INIT;
                                                                              CLEAR(PayrollVersionTxt);
                                                                              CLEAR(TransactionTypeTxt);
                                                                              CLEAR(DCcode);
                                                                              CLEAR(YearTxt);
                                                                              CLEAR(WeekTxt);
                                                                              CLEAR(DocumentNoTxt);
                                                                              CLEAR(DocumentDateTxt);
                                                                              CLEAR(SupplyingCompany);
                                                                              CLEAR(ReceivingCompany);
                                                                              CLEAR(DescrDimTxt1);
                                                                              CLEAR(ValueDimTxt1);
                                                                              CLEAR(DescrDimTxt2);
                                                                              CLEAR(ValueDimTxt2);
                                                                              CLEAR(DescrDimTxt3);
                                                                              CLEAR(ValueDimTxt3);
                                                                              CLEAR(DescrDimTxt4);
                                                                              CLEAR(ValueDimTxt4);
                                                                              CLEAR(DescrDimTxt5);
                                                                              CLEAR(ValueDimTxt5);
                                                                            END;

                                                  Import::OnBeforeInsertRecord=VAR
                                                                                 RecRef@1100528406 : RecordRef;
                                                                                 dummytxt1@1100528405 : Text[30];
                                                                                 dummytxt2@1100528404 : Text[30];
                                                                                 intTest@1100528403 : Integer;
                                                                                 lvDay@1100528402 : Integer;
                                                                                 lvMonth@1100528401 : Integer;
                                                                                 lvYear@1100528400 : Integer;
                                                                               BEGIN
                                                                                 //First test on company!!

                                                                                 HumanResourcesSetup.GET;
                                                                                 IF (SupplyingCompany  <> '') AND
                                                                                    (HumanResourcesSetup."Delivering Company (Navision)" <> SupplyingCompany) THEN
                                                                                   //This second test is needed because payroll fills the file wrong
                                                                                   IF (HumanResourcesSetup."Delivering Company (Navision)" <> ReceivingCompany) THEN
                                                                                     ERROR(Text002, SupplyingCompany, COMPANYNAME);

                                                                                 WITH "Gen. Journal Line" DO BEGIN
                                                                                   "Journal Template Name" := HumanResourcesSetup."GL Entry Journal Template";
                                                                                   "Journal Batch Name" := HumanResourcesSetup."GL Entry Journal Batch";
                                                                                   "Line No." := LineCounter + 10000;
                                                                                   RecRef.GETTABLE("Gen. Journal Line");
                                                                                   XMLportStatusDialogMgt.ShowImportStatus(RecRef);
                                                                                   INSERT;
                                                                                   LineCounter := LineCounter + 10000;

                                                                                   "Account Type" := "Account Type"::"G/L Account";

                                                                                   IF NOT JnlTemplRec.GET(HumanResourcesSetup."GL Entry Journal Template") THEN
                                                                                     JnlTemplRec.INIT;
                                                                                   IF NOT JnlBatchRec.GET(HumanResourcesSetup."GL Entry Journal Template", HumanResourcesSetup."GL Entry Journal Batch") THEN
                                                                                     JnlBatchRec.INIT;

                                                                                   IF DocNo = '' THEN BEGIN
                                                                                     IF JnlBatchRec."No. Series" <> '' THEN
                                                                                       DocNo := NoSeriesMgt.GetNextNo(JnlBatchRec."No. Series",0D,FALSE)
                                                                                     ELSE
                                                                                       IF JnlTemplRec."No. Series" <> '' THEN
                                                                                         DocNo := NoSeriesMgt.GetNextNo(JnlTemplRec."No. Series",0D,FALSE);
                                                                                   END;
                                                                                   "Document No." := DocNo;

                                                                                   CheckCompany;
                                                                                   "Receiving Company" := ReceivingCompany;

                                                                                   FOR DimLoop := 1 TO 5 DO
                                                                                   BEGIN
                                                                                     IF (DescrDimTxt[DimLoop] <> '') AND (ValueDimTxt[DimLoop] <> '') THEN BEGIN
                                                                                       DescrDimTxt[DimLoop] := UPPERCASE(DescrDimTxt[DimLoop]);
                                                                                       dummytxt1 := DescrDimTxt[DimLoop];
                                                                                       dummytxt2 := ValueDimTxt[DimLoop];
                                                                                       LoopCount := 0;
                                                                                       REPEAT
                                                                                         LoopCount := LoopCount + 1;
                                                                                         IF LoopCount < 9 THEN intTest := LoopCount;
                                                                                       UNTIL (LoopCount = 9) OR (DescrDimTxt[DimLoop] = PayrollDimCode[intTest]);
                                                                                       IF LoopCount < 9 THEN BEGIN
                                                                                         //Dimension Found

                                                                                         IF LoopCount = 1 THEN BEGIN
                                                                                           //This is the cost center
                                                                                           "Shortcut Dimension 1 Code" := ValueDimTxt[DimLoop];
                                                                                           IF ("Receiving Company" <> '') AND ("Receiving Company" <> COMPANYNAME) THEN
                                                                                             DimValRec.CHANGECOMPANY("Receiving Company");
                                                                                           DimMgt.GetDimValueRec(1, "Shortcut Dimension 1 Code", DimValRec, FALSE,'');
                                                                                           IF DimValRec.Code = '' THEN
                                                                                           WHILE (DimValRec.Code = '') AND (STRPOS("Shortcut Dimension 1 Code",'0') = 1) DO BEGIN
                                                                                             "Shortcut Dimension 1 Code" := DELSTR("Shortcut Dimension 1 Code",1,1);
                                                                                             DimMgt.GetDimValueRec(1, "Shortcut Dimension 1 Code", DimValRec, FALSE,'');
                                                                                           END;
                                                                                           IF DimValRec.Code <> '' THEN
                                                                                             VALIDATE("Shortcut Dimension 1 Code");
                                                                                           DimValRec.CHANGECOMPANY(COMPANYNAME);
                                                                                         END;

                                                                                         IF LoopCount = 2 THEN BEGIN
                                                                                           //This is the cost center
                                                                                           "Shortcut Dimension 2 Code" := ValueDimTxt[DimLoop];
                                                                                           IF ("Receiving Company" <> '') AND ("Receiving Company" <> COMPANYNAME) THEN
                                                                                             DimValRec.CHANGECOMPANY("Receiving Company");
                                                                                           DimMgt.GetDimValueRec(2, "Shortcut Dimension 2 Code", DimValRec, FALSE,'');
                                                                                           IF DimValRec.Code = '' THEN
                                                                                           WHILE (DimValRec.Code = '') AND (STRPOS("Shortcut Dimension 2 Code",'0') = 1) DO BEGIN
                                                                                             "Shortcut Dimension 2 Code" := DELSTR("Shortcut Dimension 2 Code",1,1);
                                                                                             DimMgt.GetDimValueRec(2, "Shortcut Dimension 2 Code", DimValRec, FALSE,'');
                                                                                           END;
                                                                                           IF DimValRec.Code <> '' THEN
                                                                                             VALIDATE("Shortcut Dimension 2 Code");
                                                                                           DimValRec.CHANGECOMPANY(COMPANYNAME);
                                                                                         END;

                                                                                         IF LoopCount > 2 THEN BEGIN
                                                                                           //These are the other shortcutdimensions 3 - 8
                                                                                           DimMgt.SetCompany("Receiving Company");  //DP00387 sn
                                                                                             "Dimension Set ID" := DimMgt.GetDimSetAddedDimVal(
                                                                                                "Dimension Set ID",
                                                                                                 NavDimCode[LoopCount],
                                                                                                 ValueDimTxt[DimLoop]);
                                                                                           DimMgt.SetCompany(COMPANYNAME);
                                                                                                                                    //DP00387 en
                                                                                         END;

                                                                                       END;
                                                                                     END;
                                                                                   END;

                                                                                   IF DCcode = Text005 THEN
                                                                                     Amount := Amount * -1;

                                                                                   Amount := Amount / 100;
                                                                                   VALIDATE(Amount);

                                                                                   IF NOT GlAccRec.GET("Account No.") THEN GlAccRec.INIT;
                                                                                   IF GlAccRec."Import Posting Information" = FALSE THEN GlAccRec.INIT;
                                                                                   "Gen. Posting Type" := GlAccRec."Gen. Posting Type";
                                                                                   "Gen. Bus. Posting Group" := GlAccRec."Gen. Bus. Posting Group";
                                                                                   "Gen. Prod. Posting Group" := GlAccRec."Gen. Prod. Posting Group";
                                                                                   "VAT Bus. Posting Group" := GlAccRec."VAT Bus. Posting Group";
                                                                                   "VAT Prod. Posting Group" := GlAccRec."VAT Prod. Posting Group";
                                                                                   "Tax Area Code" := GlAccRec."Tax Area Code";
                                                                                   "Tax Liable" := GlAccRec."Tax Liable";
                                                                                   "Tax Group Code" := GlAccRec."Tax Group Code";
                                                                                   VALIDATE("VAT Prod. Posting Group");

                                                                                   "Source Code"  := JnlTemplRec."Source Code";
                                                                                   IF JnlBatchRec."Reason Code" <> '' THEN
                                                                                     "Reason Code" := JnlBatchRec."Reason Code"
                                                                                   ELSE
                                                                                     "Reason Code" := JnlTemplRec."Reason Code";

                                                                                   EVALUATE(lvDay, COPYSTR(DocumentDateTxt, 1, 2));
                                                                                   EVALUATE(lvMonth, COPYSTR(DocumentDateTxt, 3, 2));
                                                                                   EVALUATE(lvYear, COPYSTR(DocumentDateTxt, 5, 4));

                                                                                   "Posting Date" := DMY2DATE(lvDay, lvMonth, lvYear);
                                                                                   "Document Date" := "Posting Date";
                                                                                   "Origin Salary Application" := TRUE;
                                                                                   MODIFY;
                                                                                 END;

                                                                                 currXMLport.SKIP;
                                                                               END;
                                                                                }

    { [{31B77C03-A4E5-42D6-A5E9-D3717B51E9FC}];2 ;PayrollVersionTxt   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=2 }

    { [{CECC1C8F-BF27-4D79-A82C-32E9CA3069B2}];2 ;TransactionTypeTxt  ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=3 }

    { [{0380469D-17E0-495D-9380-83E773B66755}];2 ;AccountNo           ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=Gen. Journal Line::Account No.;
                                                  MinOccurs=Zero;
                                                  Width=12 }

    { [{118F2D14-94C7-4426-BB78-7C7D9EA735A2}];2 ;Amount              ;Element ;Field   ;
                                                  DataType=Decimal;
                                                  SourceField=Gen. Journal Line::Amount;
                                                  MinOccurs=Zero;
                                                  Width=15 }

    { [{29152AAD-0A6B-4E8C-B887-F74318292079}];2 ;DCcode              ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=1 }

    { [{EC40FDDF-6D1E-4246-9795-95447D0971A6}];2 ;YearTxt             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  FillEmptyTextWithZero(YearTxt);
                                                                                  EVALUATE(Year, YearTxt);
                                                                                END;

                                                  Width=4 }

    { [{1EDC6897-1225-47DE-8CC0-030CC6C54A9A}];2 ;WeekTxt             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  FillEmptyTextWithZero(WeekTxt);
                                                                                  EVALUATE(week, WeekTxt);
                                                                                END;

                                                  Width=2 }

    { [{DDAFAE68-5A49-4D29-9072-AFFAAED30327}];2 ;DocumentNoTxt       ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=8 }

    { [{B36D989C-519A-487F-8C93-A973D78D614E}];2 ;DocumentDateTxt     ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=8 }

    { [{A781965C-BA25-47D3-95C6-865C350245B6}];2 ;SupplyingCompany    ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=3 }

    { [{4BAE5DA2-33DC-4619-A8ED-179A72DC8A30}];2 ;ReceivingCompany    ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=3 }

    { [{ED4B2F2E-487F-4312-BD49-461E18391640}];2 ;Description         ;Element ;Field   ;
                                                  DataType=Text;
                                                  SourceField=Gen. Journal Line::Description;
                                                  MinOccurs=Zero;
                                                  Width=30 }

    { [{3456845D-0F10-4072-9D6D-5D7C2C8297F6}];2 ;Quantity            ;Element ;Field   ;
                                                  DataType=Decimal;
                                                  SourceField=Gen. Journal Line::Quantity;
                                                  MinOccurs=Zero;
                                                  Width=10 }

    { [{7CAB59F9-D4B3-4B31-A97E-47C7AD2871D6}];2 ;DescrDimTxt1        ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  DescrDimTxt[1] := DescrDimTxt1;
                                                                                END;

                                                  Width=6 }

    { [{B21A0746-92EA-46E0-84F1-91D61D28DDE4}];2 ;ValueDimTxt1        ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  ValueDimTxt[1] := ValueDimTxt1;
                                                                                END;

                                                  Width=6 }

    { [{A3236ACF-C516-4F3B-BFC0-C0D34AE2F0F1}];2 ;DescrDimTxt2        ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  DescrDimTxt[2] := DescrDimTxt2;
                                                                                END;

                                                  Width=6 }

    { [{CC8DC20B-C699-416C-9327-FA79BA4C2DC8}];2 ;ValueDimTxt2        ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  ValueDimTxt[2] := ValueDimTxt2;
                                                                                END;

                                                  Width=6 }

    { [{19E27CB1-60DD-4368-91B0-1092EBAFB1C3}];2 ;DescrDimTxt3        ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  DescrDimTxt[3] := DescrDimTxt3;
                                                                                END;

                                                  Width=6 }

    { [{C0CFCC31-CA50-4F80-8C50-4C2DC8728B3E}];2 ;ValueDimTxt3        ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  ValueDimTxt[3] := ValueDimTxt3;
                                                                                END;

                                                  Width=6 }

    { [{6CD7A985-5C5B-4F04-ADEB-2C975185C9D4}];2 ;DescrDimTxt4        ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  DescrDimTxt[4] := DescrDimTxt4;
                                                                                END;

                                                  Width=6 }

    { [{F4B5A558-651D-4B2B-9658-E12C8C4A5BEB}];2 ;ValueDimTxt4        ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  ValueDimTxt[4] := ValueDimTxt4;
                                                                                END;

                                                  Width=6 }

    { [{3CB1A183-BDA9-4294-A6BA-BA70191ACA79}];2 ;DescrDimTxt5        ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  DescrDimTxt[5] := DescrDimTxt5;
                                                                                END;

                                                  Width=6 }

    { [{6607ACBA-5AB5-45FA-8E58-B2A584956F91}];2 ;ValueDimTxt5        ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  ValueDimTxt[5] := ValueDimTxt5;
                                                                                END;

                                                  Width=6 }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=VAR
                   I@1100528400 : Integer;
                 BEGIN
                   IF PayrollDimCodesAreEmpty THEN
                     FOR I := 1 TO ARRAYLEN(PayrollDimCode) DO BEGIN
                       PayrollDimCode[I] := COPYSTR(NavDimCode[I], 1, 6);
                     END;
                 END;

    }
    CONTROLS
    {
      { 1100528401;;Container;
                  ContainerType=ContentArea }

      { 1100528400;1;Group  ;
                  CaptionML=[ENU=Options;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100528402;2;Group  ;
                  CaptionML=ENU=Navision Dimensions;
                  GroupType=Group }

      { 1100528404;3;Field  ;
                  CaptionML=[ENU=Dimension 1 Code;
                             SVE=Dimension 1 kod];
                  SourceExpr=NavDimCode[1];
                  Editable=FALSE }

      { 1100528409;3;Field  ;
                  CaptionML=[ENU=Dimension 2 Code;
                             SVE=Dimension 2 kod];
                  SourceExpr=NavDimCode[2];
                  Editable=FALSE }

      { 1100528410;3;Field  ;
                  CaptionML=[ENU=Dimension 3 Code;
                             SVE=Dimension 3 kod];
                  SourceExpr=NavDimCode[3];
                  Editable=FALSE }

      { 1100528411;3;Field  ;
                  CaptionML=[ENU=Dimension 4 Code;
                             SVE=Dimension 4 kod];
                  SourceExpr=NavDimCode[4];
                  Editable=FALSE }

      { 1100528412;3;Field  ;
                  CaptionML=ENU=Dimension 5 Code;
                  SourceExpr=NavDimCode[5];
                  Editable=FALSE }

      { 1100528413;3;Field  ;
                  CaptionML=ENU=Dimension 6 Code;
                  SourceExpr=NavDimCode[6];
                  Editable=FALSE }

      { 1100528414;3;Field  ;
                  CaptionML=ENU=Dimension 7 Code;
                  SourceExpr=NavDimCode[7];
                  Editable=FALSE }

      { 1100528415;3;Field  ;
                  CaptionML=ENU=Dimension 8 Code;
                  SourceExpr=NavDimCode[8];
                  Editable=FALSE }

      { 1100528403;2;Group  ;
                  CaptionML=ENU=Payroll Dimensions;
                  GroupType=Group }

      { 1100528405;3;Field  ;
                  CaptionML=[ENU=Dimension 1 Code;
                             SVE=Dimension 1 kod];
                  SourceExpr=PayrollDimCode[1] }

      { 1100528416;3;Field  ;
                  CaptionML=[ENU=Dimension 2 Code;
                             SVE=Dimension 2 kod];
                  SourceExpr=PayrollDimCode[2] }

      { 1100528417;3;Field  ;
                  CaptionML=[ENU=Dimension 3 Code;
                             SVE=Dimension 3 kod];
                  SourceExpr=PayrollDimCode[3] }

      { 1100528418;3;Field  ;
                  CaptionML=[ENU=Dimension 4 Code;
                             SVE=Dimension 4 kod];
                  SourceExpr=PayrollDimCode[4] }

      { 1100528419;3;Field  ;
                  CaptionML=ENU=Dimension 5 Code;
                  SourceExpr=PayrollDimCode[5] }

      { 1100528420;3;Field  ;
                  CaptionML=ENU=Dimension 6 Code;
                  SourceExpr=PayrollDimCode[6] }

      { 1100528421;3;Field  ;
                  CaptionML=ENU=Dimension 7 Code;
                  SourceExpr=PayrollDimCode[7] }

      { 1100528422;3;Field  ;
                  CaptionML=ENU=Dimension 8 Code;
                  SourceExpr=PayrollDimCode[8] }

    }
  }
  CODE
  {
    VAR
      GLSetup@1100528428 : Record 98;
      HumanResourcesSetup@1100528427 : Record 5218;
      PostRec@1100528426 : Record 81;
      JnlTemplRec@1100528425 : Record 80;
      JnlBatchRec@1100528424 : Record 232;
      DimValRec@1100528423 : Record 349;
      GlAccRec@1100528422 : Record 15;
      LineCounter@1100528420 : Integer;
      DocNo@1100528418 : Code[20];
      Year@1100528417 : Integer;
      week@1100528416 : Integer;
      DimMgt@1100528413 : Codeunit 408;
      NoSeriesMgt@1100528412 : Codeunit 396;
      FileMgt@1100528409 : Codeunit 419;
      XMLportStatusDialogMgt@1100528411 : Codeunit 11130006;
      NewFile@1100528410 : Text[250];
      DescrDimTxt@1100528405 : ARRAY [8] OF Text[10];
      ValueDimTxt@1100528404 : ARRAY [8] OF Text[10];
      NavDimCode@1100528403 : ARRAY [8] OF Code[20];
      PayrollDimCode@1100528402 : ARRAY [8] OF Code[20];
      DimLoop@1100528401 : Integer;
      LoopCount@1100528400 : Integer;
      Text005@1100528407 : TextConst 'ENU=C;SVE=Text005';
      Text002@1100528406 : TextConst 'ENU=Companies differ %1 and 2%;SVE=F”retag varierar %1 och 2%';

    PROCEDURE CheckCompany@1100528400();
    VAR
      IcRelRec@1100528401 : Record 11012057;
      CompFound@1100528400 : Boolean;
    BEGIN
      CompFound := FALSE;
      IF HumanResourcesSetup."Delivering Company (Navision)" <> ReceivingCompany THEN BEGIN
        IF IcRelRec.FIND('-') THEN BEGIN
          REPEAT
            HumanResourcesSetup.CHANGECOMPANY(IcRelRec."Receiving Company");
            HumanResourcesSetup.GET;
            IF HumanResourcesSetup."Delivering Company (Navision)" = ReceivingCompany THEN BEGIN
              CompFound := TRUE;
              IF COMPANYNAME = IcRelRec."Receiving Company" THEN
                ReceivingCompany := ''
              ELSE
                ReceivingCompany := IcRelRec."Receiving Company";
            END;
          UNTIL (IcRelRec.NEXT = 0) OR (CompFound = TRUE);
        END;
      END;

      IF CompFound = FALSE THEN
        ReceivingCompany := '';

      HumanResourcesSetup.CHANGECOMPANY(COMPANYNAME);
      HumanResourcesSetup.GET;
    END;

    PROCEDURE FillEmptyTextWithZero@1100525013(VAR Text@1100525000 : Text[1024]);
    BEGIN
      IF DELCHR(Text,'<>=',' ') = '' THEN
        Text := '0';
    END;

    LOCAL PROCEDURE PayrollDimCodesAreEmpty@1100528401() : Boolean;
    VAR
      I@1100528400 : Integer;
    BEGIN
      FOR I := 1 TO ARRAYLEN(PayrollDimCode) DO BEGIN
        IF PayrollDimCode[I] <> '' THEN
          EXIT(FALSE);
      END;

      EXIT(TRUE);
    END;

    BEGIN
    {
      CC: Dataport 11012347

      Fieldno Description     StartPos Width Will be filled with

       1      Herkenningscode   1        2 XX to be determined
       2      Mutation kind     3        3
       3      Account No        6       12
       4      Amount           18       15
       5      DC-Code          33        1
       6      Year             34        4
       7      Period           38        2
       8      DocumentNo       40        8
       9      Documentdate     48        8 DDMMYYYY
      10      SupplyingCompany 56        3
      11      ReceivingCompany 59        3
      12      Description      62       30
      13      Quantity         92       10
      14      Description dim1 102       6
      15      Value dim1       108       6
      16      Description dim2 114       6
      17      Value dim2       120       6
      18      Description dim3 126       6
      19      Value dim3       132       6
      20      Description dim4 138       6
      21      Value dim4       144       6
      22      Description dim5 150       6
      23      Value dim5       156       6

      Total lenght 161

      Example file:
      1234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890
               1         2         3         4         5     6789612       7
      XX 6050040        00000000020392D2009126000003317122009644643test RC ooms                   000001000AFD   111111BU    222222KPL   3
      XX 604999         00000000020392C2009126000003317122009643644test RC ooms                   000000000AFD         BU          KPL
      XX 6050020        00000000061171D2009136000003317122009644644test RC ooms                   000003000AFD         BU          KPL
      XX 604999         00000000061171C2009136000003317122009644644test RC ooms                   000000000AFD         BU          KPL
      XX 6040000        00000000068000D200913       023122009644644test jnp ooms (2)              000000000AFD         BU          KPL
      XX 601998         00000000068000C200913       023122009644644test jnp ooms (2)              000000000AFD         BU          KPL
      XX 601998         00000000068000D200913       023122009644644test jnp ooms (2)              000000000AFD         BU          KPL
      XX 6040100        00000000068000C200913       023122009644644test jnp ooms (2)              000000000AFD         BU          KPL
      XX 6040700        00000000024476D200913       023122009644644test jnp ooms (2)              000000000AFD         BU          KPL
      XX 601998         00000000024476C200913       023122009644644test jnp ooms (2)              000000000AFD         BU          KPL
      XX 6040800        00000000037315D200913       023122009644644test jnp ooms (2)              000000000AFD         BU          KPL
      XX 601998         00000000037315C200913       023122009644644test jnp ooms (2)              000000000AFD         BU          KPL
      XX 601998         00000000024476D200913       023122009644644test jnp ooms (2)              000000000AFD         BU          KPL
      XX 6040810        00000000024476C200913       023122009644644test jnp ooms (2)              000000000AFD         BU          KPL
      XX 601998         00000000037315D200913       023122009644644test jnp ooms (2)              000000000AFD         BU          KPL
      XX 6040815        00000000037315C200913       023122009644644test jnp ooms (2)              000000000AFD         BU          KPL
      XX 6020000        00000000068000D200913       023122009644644test jnp ooms (2)              000000000AFD         BU          KPL
      XX 601998         00000000068000C200913       023122009644644test jnp ooms (2)              000000000AFD         BU          KPL
      XX 601998         00000000068000D200913       023122009644644test jnp ooms (2)              000000000AFD         BU          KPL
      XX 6020100        00000000068000C200913       023122009644644test jnp ooms (2)              000000000AFD         BU          KPL
      XX 6020710        00000000024476D200913       023122009644644test jnp ooms (2)              000000000AFD         BU          KPL
      XX 601998         00000000024476C200913       023122009644644test jnp ooms (2)              000000000AFD         BU          KPL
      XX 601998         00000000024476D200913       023122009644644test jnp ooms (2)              000000000AFD         BU          KPL
      XX 6020720        00000000024476C200913       023122009644644test jnp ooms (2)              000000000AFD         BU          KPL
      XX 6020910        00000000037315D200913       023122009644644test jnp ooms (2)              000000000AFD         BU          KPL
      XX 601998         00000000037315C200913       023122009644644test jnp ooms (2)              000000000AFD         BU          KPL
      XX 601998         00000000037315D200913       023122009644644test jnp ooms (2)              000000000AFD         BU          KPL
      XX 6020920        00000000037315C200913       023122009644644test jnp ooms (2)              000000000AFD   555555BU    444444KPL   3

      db, 03-09-12: this is no standard version but customized for Ooms (according to Henk v.d. Berg, administrator of Payroll at Infor).
    }
    END.
  }
}

