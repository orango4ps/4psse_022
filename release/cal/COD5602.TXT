OBJECT Codeunit 5602 FA Get G/L Account No.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW113.00,4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      FAPostingGr@1000 : Record 5606;
      GLAccNo@1001 : Code[20];

    [External]
    PROCEDURE GetAccNo@1(VAR FALedgEntry@1000 : Record 5601) : Code[20];
    BEGIN
      WITH FALedgEntry DO BEGIN
        FAPostingGr.GET("FA Posting Group");
        GLAccNo := '';
        IF "FA Posting Category" = "FA Posting Category"::" " THEN
          CASE "FA Posting Type" OF
            "FA Posting Type"::"Acquisition Cost":
              GLAccNo := FAPostingGr.GetAcquisitionCostAccount;
            "FA Posting Type"::Depreciation:
              GLAccNo := FAPostingGr.GetAccumDepreciationAccount;
            "FA Posting Type"::"Write-Down":
              GLAccNo := FAPostingGr.GetWriteDownAccount;
            "FA Posting Type"::Appreciation:
              GLAccNo := FAPostingGr.GetAppreciationAccount;
            "FA Posting Type"::"Custom 1":
              GLAccNo := FAPostingGr.GetCustom1Account;
            "FA Posting Type"::"Custom 2":
              GLAccNo := FAPostingGr.GetCustom2Account;
            "FA Posting Type"::"Proceeds on Disposal":
              GLAccNo := FAPostingGr.GetSalesAccountOnDisposalGain;
            "FA Posting Type"::"Gain/Loss":
              BEGIN
                IF "Result on Disposal" = "Result on Disposal"::Gain THEN
                  GLAccNo := FAPostingGr.GetGainsAccountOnDisposal;
                IF "Result on Disposal" = "Result on Disposal"::Loss THEN
                  GLAccNo := FAPostingGr.GetLossesAccountOnDisposal;
              END;
          END;

        IF "FA Posting Category" = "FA Posting Category"::Disposal THEN
          CASE "FA Posting Type" OF
            "FA Posting Type"::"Acquisition Cost":
              GLAccNo := FAPostingGr.GetAcquisitionCostAccountOnDisposal;
            "FA Posting Type"::Depreciation:
              GLAccNo := FAPostingGr.GetAccumDepreciationAccountOnDisposal;
            "FA Posting Type"::"Write-Down":
              GLAccNo := FAPostingGr.GetWriteDownAccountOnDisposal;
            "FA Posting Type"::Appreciation:
              GLAccNo := FAPostingGr.GetAppreciationAccountOnDisposal;
            "FA Posting Type"::"Custom 1":
              GLAccNo := FAPostingGr.GetCustom1AccountOnDisposal;
            "FA Posting Type"::"Custom 2":
              GLAccNo := FAPostingGr.GetCustom2AccountOnDisposal;
            "FA Posting Type"::"Book Value on Disposal":
              BEGIN
                IF "Result on Disposal" = "Result on Disposal"::Gain THEN
                  GLAccNo := FAPostingGr.GetBookValueAccountOnDisposalGain;
                IF "Result on Disposal" = "Result on Disposal"::Loss THEN
                  GLAccNo := FAPostingGr.GetBookValueAccountOnDisposalLoss;
                "Result on Disposal" := "Result on Disposal"::" ";
              END;
          END;

        IF "FA Posting Category" = "FA Posting Category"::"Bal. Disposal" THEN
          CASE "FA Posting Type" OF
            "FA Posting Type"::"Write-Down":
              GLAccNo := FAPostingGr.GetWriteDownBalAccountOnDisposal;
            "FA Posting Type"::Appreciation:
              GLAccNo := FAPostingGr.GetAppreciationBalAccountOnDisposal;
            "FA Posting Type"::"Custom 1":
              GLAccNo := FAPostingGr.GetCustom1BalAccountOnDisposal;
            "FA Posting Type"::"Custom 2":
              GLAccNo := FAPostingGr.GetCustom2BalAccountOnDisposal;
          END;
      END;

      OnAfterGetAccNo(FALedgEntry,GLAccNo);
      EXIT(GLAccNo);
    END;

    [External]
    PROCEDURE GetMaintenanceAccNo@3(VAR MaintenanceLedgEntry@1000 : Record 11072085) : Code[20];
    BEGIN
      FAPostingGr.GET(MaintenanceLedgEntry."FA Posting Group");
      EXIT(FAPostingGr.GetMaintenanceExpenseAccount);
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterGetAccNo@2(VAR FALedgEntry@1000 : Record 5601;VAR GLAccNo@1001 : Code[20]);
    BEGIN
    END;

    BEGIN
    END.
  }
}

