OBJECT Report 491 Delete Invd Blnkt Purch Orders
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03,4PS14.00;
  }
  PROPERTIES
  {
    AccessByPermission=TableData 38=RD;
    CaptionML=[DEU=Erledigte Rahmenbestellung l”schen;
               ENU=Delete Invoiced Blanket Purchase Orders;
               NLD=Gefactureerde inkoopafroepcontracten verwijderen;
               NOR=Slett fakturerte rammebestillinger];
    ApplicationArea=#Suite;
    ProcessingOnly=Yes;
    UsageCategory=Tasks;
  }
  DATASET
  {
    { 4458;    ;DataItem;                    ;
               DataItemTable=Table38;
               DataItemTableView=SORTING(Document Type,No.)
                                 WHERE(Document Type=CONST(Blanket Order));
               ReqFilterHeadingML=[DEU=Einkauf Rahmenbestellung;
                                   ENU=Blanket Purchase Order;
                                   NLD=Inkoopraamcontract;
                                   NOR=Rammebestilling;
                                   SVE=Avropsorder, ink”p];
               OnPreDataItem=BEGIN
                               Window.OPEN(Text000);
                             END;

               OnAfterGetRecord=VAR
                                  ApprovalsMgmt@1000 : Codeunit 1535;
                                BEGIN
                                  Window.UPDATE(1,"No.");

                                  PurchLine.RESET;
                                  PurchLine.SETRANGE("Document Type","Document Type");
                                  PurchLine.SETRANGE("Document No.","No.");
                                  PurchLine.SETFILTER("Quantity Invoiced",'<>0');
                                  IF PurchLine.FINDFIRST THEN BEGIN
                                    PurchLine.SETRANGE("Quantity Invoiced");
                                    PurchLine.SETFILTER("Outstanding Quantity",'<>0');
                                    IF NOT PurchLine.FINDFIRST THEN BEGIN
                                      PurchLine.SETRANGE("Outstanding Quantity");
                                      PurchLine.SETFILTER("Qty. Rcd. Not Invoiced",'<>0');
                                      IF NOT PurchLine.FINDFIRST THEN BEGIN
                                        PurchLine.LOCKTABLE;
                                        IF NOT PurchLine.FINDFIRST THEN BEGIN
                                          PurchLine.SETRANGE("Qty. Rcd. Not Invoiced");
                                          PurchLine2.SETRANGE("Blanket Order No.","No.");
                                          IF NOT PurchLine2.FINDFIRST THEN BEGIN
                                            ArchiveManagement.AutoArchivePurchDocument("Purchase Header");

                                            OnBeforeDeletePurchLines(PurchLine);
                                            PurchLine.DELETEALL;

                                            PurchCommentLine.SETRANGE("Document Type","Document Type");
                                            PurchCommentLine.SETRANGE("No.","No.");
                                            PurchCommentLine.DELETEALL;

                                            ApprovalsMgmt.DeleteApprovalEntries(RECORDID);

                                            Delete4PSAdditions; //**4PS.n

                                            OnBeforeDeletePurchaseHeader("Purchase Header");
                                            DELETE;

                                            COMMIT;
                                          END;
                                        END;
                                      END;
                                    END;
                                  END;
                                END;

               ReqFilterFields=No.,Buy-from Vendor No.,Pay-to Vendor No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Bearbeite Bestellungen #1##########;ENU=Processing purch. orders #1##########;NLD=Verwerken ink.-orders   #1##########;NOR=Behandler bestillinger   #1##########;SVE=Behandlar ink”psorder    #1##########';
      PurchLine@1001 : Record 39;
      PurchLine2@1002 : Record 39;
      PurchCommentLine@1003 : Record 43;
      ArchiveManagement@1007 : Codeunit 5063;
      Window@1005 : Dialog;

    [Integration]
    LOCAL PROCEDURE OnBeforeDeletePurchaseHeader@1(VAR PurchaseHeader@1000 : Record 38);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeDeletePurchLines@2(VAR PurchaseLine@1000 : Record 39);
    BEGIN
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

