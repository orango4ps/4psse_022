OBJECT Codeunit 6085666 File Information
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.5.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE GetFilePath@161024016(FullFilename@161024012 : Text[1024]) : Text[1024];
    BEGIN
      IF STRPOS(FullFilename,'\') > 0 THEN
        EXIT(COPYSTR(FullFilename,1,LastIndexOf(FullFilename,'\',TRUE)-1));
    END;

    PROCEDURE GetFilePathHash@6085576(FullFilename@161024012 : Text[1024]) : Text[1024];
    BEGIN
      EXIT(GetTextHash(GetFilePath(FullFilename)));
    END;

    PROCEDURE GetFilename@161024018(FullFilename@161024012 : Text[1024]) : Text[1024];
    BEGIN
      EXIT(COPYSTR(FullFilename,LastIndexOf(FullFilename,'\',TRUE)+1));
    END;

    PROCEDURE GetFilenameWithoutExt@161024019(FullFilename@161024012 : Text[1024]) : Text[1024];
    BEGIN
      IF STRPOS(FullFilename,'\') > 0 THEN
        FullFilename := COPYSTR(FullFilename,LastIndexOf(FullFilename,'\',TRUE)+1);

      IF STRPOS(FullFilename,'.') <> 0 THEN
        FullFilename := COPYSTR(FullFilename,1,LastIndexOf(FullFilename,'.',TRUE)-1);

      EXIT(FullFilename);
    END;

    PROCEDURE GetFileExtension@161024021(FullFilename@161024012 : Text[1024]) : Text[30];
    VAR
      Filename@6085573 : Text[250];
      Index@6085574 : Integer;
    BEGIN
      IF STRPOS(FullFilename,'\') > 0 THEN
        Filename := COPYSTR(FullFilename,LastIndexOf(FullFilename,'\',TRUE) + 1)
      ELSE
        Filename := FullFilename;

      Index := LastIndexOf(Filename,'.',TRUE);
      IF Index = 0 THEN
        EXIT('');

      EXIT(COPYSTR(Filename,Index + 1));
    END;

    LOCAL PROCEDURE GetTextHash@6085574(Text@6085573 : Text[1024]) : Text[50];
    VAR
      CryptoMgt@6085574 : Codeunit 6085599;
    BEGIN
      EXIT(CryptoMgt.HashValueSHA1('1C7754F1876B9EADA2BAD610E8B4DF18ECFD19F1EE8389D4BEC768A9EB883627' +
                                   '983661067DE60965A0C5CB6737AB3E187B5294E64CB1CF3781E8346118148C81',Text));
    END;

    PROCEDURE LastIndexOf@6085573(Text@161024012 : Text[1024];CharToFind@161024013 : Text[1];IgnoreCase@161024014 : Boolean) : Integer;
    VAR
      StartIndex@161024015 : Integer;
    BEGIN
      IF IgnoreCase THEN BEGIN
        Text := UPPERCASE(Text);
        CharToFind := UPPERCASE(CharToFind);
      END;

      StartIndex := STRLEN(Text);
      WHILE StartIndex > 0 DO BEGIN
        IF COPYSTR(Text,StartIndex,1) = CharToFind THEN
          EXIT(StartIndex)
        ELSE
          StartIndex -= 1;
      END;
    END;

    BEGIN
    END.
  }
}

