OBJECT Table 11012566 Rental Correction Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               GetOrderHeader;
               OrderRec.TESTFIELD(Posted,FALSE);
             END;

    OnModify=BEGIN
               GetOrderHeader;
               OrderRec.TESTFIELD(Posted,FALSE);
             END;

    OnDelete=BEGIN
               GetOrderHeader;
               OrderRec.TESTFIELD(Posted,FALSE);
             END;

    OnRename=BEGIN
               GetOrderHeader;
               OrderRec.TESTFIELD(Posted,FALSE);
             END;

    CaptionML=[ENU=Rental Correction Line;
               NOR=Korreksjonsrad for Leie;
               SVE=RÑttningstransaktionsrad fîr uthyrning];
  }
  FIELDS
  {
    { 10  ;   ;Rental  Correction No.;Code20      ;TableRelation="Rental Correction".No.;
                                                   CaptionML=[ENU=Rental  Correction No.;
                                                              NOR=Korreksjonsnr. for Leie;
                                                              SVE=RÑttningstransaktionsnr fîr uthyrning] }
    { 20  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 30  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                TempOrderLine := Rec;
                                                                INIT;
                                                                Type := TempOrderLine.Type;

                                                                VALIDATE("Debit Rate Type");
                                                                VALIDATE("Credit Rate Type");
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=[ENU=Plant,Text;
                                                                    NOR=Maskin,Tekst;
                                                                    SVE=Maskin,Text];
                                                   OptionString=Plant,Text }
    { 40  ;   ;Plant Type          ;Code20        ;TableRelation="Plant Type";
                                                   OnValidate=VAR
                                                                lvPlantInventRec@1100485000 : Record 11012555;
                                                              BEGIN
                                                                IF "Plant Type" <> '' THEN
                                                                  TESTFIELD(Type,Type::Plant)
                                                                ELSE
                                                                  IF Type <> Type::Plant THEN
                                                                    EXIT;

                                                                CALCFIELDS(Bulk,"Unit of Measure");
                                                                IF Bulk THEN BEGIN
                                                                  lvPlantInventRec.SETCURRENTKEY(Removed, "Plant Type", "Plant No.");
                                                                  lvPlantInventRec.SETRANGE(Removed, FALSE);
                                                                  lvPlantInventRec.SETRANGE("Plant Type", "Plant Type");
                                                                  lvPlantInventRec.SETRANGE("Plant No.", '0');
                                                                  IF lvPlantInventRec.FIND('-') THEN
                                                                    "No." := '0'
                                                                  ELSE BEGIN
                                                                    lvPlantInventRec.SETFILTER("Plant No.", '>%1', '0');
                                                                    IF lvPlantInventRec.FIND('-') THEN
                                                                      "No." := ''
                                                                    ELSE
                                                                      "No." := '0';
                                                                  END;
                                                                END ELSE BEGIN
                                                                  IF NOT PlantNoRec.GET("Plant Type","No.") THEN
                                                                    "No." := '';
                                                                END;

                                                                GetPlantDescription;
                                                                GetCreditDiscount;
                                                                GetRates;

                                                                IF "Plant Type" <> '' THEN
                                                                  GetDefaults;

                                                                CalculateNumberRentalPeriods;
                                                                CheckMoreRentalRates();
                                                              END;

                                                   CaptionML=[ENU=Plant Type;
                                                              NOR=Maskintype;
                                                              SVE=Maskintyp] }
    { 50  ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 60  ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST(Text)) "Standard Text"
                                                                 ELSE IF (Type=CONST(Plant),
                                                                          Bulk=CONST(No)) "Plant Number".No. WHERE (Plant Type=FIELD(Plant Type))
                                                                          ELSE IF (Type=CONST(Plant),
                                                                                   Bulk=CONST(Yes)) "Plant Inventory"."Plant No." WHERE (Removed=CONST(No),
                                                                                                                                         Plant Type=FIELD(Plant Type));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS(Bulk);

                                                                CASE Type OF
                                                                  Type::Text:
                                                                    BEGIN
                                                                      IF "No." <> '' THEN BEGIN
                                                                        StandardText.GET("No.");
                                                                        Description := StandardText.Description;
                                                                      END;
                                                                    END;
                                                                  Type::Plant:
                                                                    BEGIN
                                                                      IF ("No." <> '') AND (NOT Bulk) THEN BEGIN
                                                                        IF Quantity = 0 THEN
                                                                          VALIDATE(Quantity,1)
                                                                        ELSE
                                                                          VALIDATE(Quantity);
                                                                      END;
                                                                      GetPlantDescription;
                                                                      GetRates;
                                                                      CalculateNumberRentalPeriods;
                                                                      CheckMoreRentalRates();
                                                                    END;

                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              NOR=Nr.;
                                                              SVE=Nr] }
    { 70  ;   ;Unit of Measure     ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Type"."Unit of Measure" WHERE (Code=FIELD(Plant Type)));
                                                   CaptionML=[ENU=Unit of Measure;
                                                              NOR=MÜleenhet;
                                                              SVE=Enhet];
                                                   Editable=No }
    { 80  ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                IF "Starting Date" <> 0D THEN
                                                                  TESTFIELD(Type,Type::Plant);

                                                                GetRates;
                                                                CalculateNumberRentalPeriods;
                                                                CheckMoreRentalRates();
                                                              END;

                                                   CaptionML=[ENU=Starting Date;
                                                              NOR=Startdato;
                                                              SVE=Startdatum] }
    { 90  ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                IF "Ending Date" <> 0D THEN
                                                                  TESTFIELD(Type,Type::Plant);

                                                                CalculateNumberRentalPeriods;
                                                                CheckMoreRentalRates();
                                                              END;

                                                   CaptionML=[ENU=Ending Date;
                                                              NOR=Sluttdato;
                                                              SVE=Slutdatum] }
    { 100 ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                CASE Type OF
                                                                  Type::Text:
                                                                    TESTFIELD(Quantity,0);
                                                                  Type::Plant:
                                                                    BEGIN
                                                                      CALCFIELDS(Bulk);
                                                                      IF (NOT Bulk) AND ("No." <> '') AND (NOT (Quantity IN [0,1])) THEN
                                                                        FIELDERROR(Quantity,Text000);
                                                                      VALIDATE("Debit Rate");
                                                                      VALIDATE("Credit Rate");
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 110 ;   ;Bulk                ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Type".Bulk WHERE (Code=FIELD(Plant Type)));
                                                   CaptionML=[ENU=Bulk;
                                                              NOR=Bulk;
                                                              SVE=Bulk];
                                                   Editable=No }
    { 125 ;   ;Debit No. of Rental Periods;Decimal;OnValidate=BEGIN
                                                                IF "Debit No. of Rental Periods" <> 0 THEN
                                                                  TESTFIELD("Debit Rental Type","Debit Rental Type"::Rental);

                                                                VALIDATE("Debit Rate");
                                                              END;

                                                   CaptionML=[ENU=Debit No. of Rental Periods;
                                                              NOR=Debet, antall Leieperioder;
                                                              SVE=Debet, antal uthyrningsperioder];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 126 ;   ;Debit Rate Type     ;Option        ;OnValidate=BEGIN
                                                                GetOrderHeader;
                                                                IF (Type <> Type::Plant) OR ("Debit Rental Type" <> "Debit Rental Type"::Rental) OR (OrderRec."Debit Location" = '') THEN
                                                                  "Debit Rate Type" := "Debit Rate Type"::" "
                                                                ELSE BEGIN
                                                                  PlantLocRec.GET(OrderRec."Debit Location");
                                                                  "Debit Rate Type" := PlantLocRec."Rate Type" + 1;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Debit Rate Type;
                                                              NOR=Debetsatstype;
                                                              SVE=Debetavgiftstyp];
                                                   OptionCaptionML=[ENU=" ,day,wk,,mth";
                                                                    NOR=" ,dag,,,mnd";
                                                                    SVE=" ,dag,vk,,mnd"];
                                                   OptionString=[ ,Day,Week,,Month];
                                                   Editable=No }
    { 135 ;   ;Credit No. of Rental Periods;Decimal;
                                                   OnValidate=BEGIN
                                                                IF "Credit No. of Rental Periods" <> 0 THEN
                                                                  TESTFIELD("Credit Rental Type","Credit Rental Type"::Rental);

                                                                VALIDATE("Credit Rate");
                                                              END;

                                                   CaptionML=[ENU=Credit No. of Rental Periods;
                                                              NOR=Antall Leieperioder, Kredit;
                                                              SVE=Antal uthyrningsperioder, kredit];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 136 ;   ;Credit Rate Type    ;Option        ;OnValidate=BEGIN
                                                                GetOrderHeader;
                                                                IF (Type <> Type::Plant) OR ("Credit Rental Type" <> "Credit Rental Type"::Rental) OR (OrderRec."Credit Location" = '') THEN
                                                                  "Credit Rate Type" := "Credit Rate Type"::" "
                                                                ELSE BEGIN
                                                                  PlantLocRec.GET(OrderRec."Credit Location");
                                                                  "Credit Rate Type" := PlantLocRec."Rate Type" + 1;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Credit Rate Type;
                                                              NOR=Kreditsatstype;
                                                              SVE=Kreditavgiftstyp];
                                                   OptionCaptionML=[ENU=" ,day,wk,,mth";
                                                                    NOR=" ,dag,,,mnd";
                                                                    SVE=" ,dag,vk,,mnd"];
                                                   OptionString=[ ,Day,Week,,Month];
                                                   Editable=No }
    { 140 ;   ;Debit Rental Type   ;Option        ;OnValidate=BEGIN
                                                                VALIDATE("Debit Rate Type");
                                                                IF "Debit Rental Type" = "Debit Rental Type"::" " THEN BEGIN
                                                                  TESTFIELD(Type,Type::Text);
                                                                  EXIT;
                                                                END;

                                                                TESTFIELD(Type,Type::Plant);
                                                                IF "Plant Type" <> '' THEN BEGIN
                                                                  PlantRec.GET(Rec."Plant Type");
                                                                  CASE PlantRec."Rental Type" OF
                                                                    PlantRec."Rental Type"::Rental:
                                                                      TESTFIELD("Debit Rental Type", "Debit Rental Type"::Rental);
                                                                    PlantRec."Rental Type"::"Sales/Buy Back":
                                                                      IF NOT ("Debit Rental Type" IN ["Debit Rental Type"::Sales,"Debit Rental Type"::"Buy Back"]) THEN
                                                                        FIELDERROR("Debit Rental Type");
                                                                  END;
                                                                  IF ("Debit Rental Type" <> "Debit Rental Type"::Rental) AND ("Debit Rate Code" <> '') THEN
                                                                    "Debit Rate Code" := '';
                                                                  IF ("Debit Rate Code" <> '') THEN BEGIN
                                                                    GetOrderHeader;
                                                                    IF NOT PlantRateCodeRec.CheckPlantRateCode(OrderRec."Debit Location", "Debit Rate Code", FALSE, '', 0) THEN
                                                                      "Debit Rate Code" := '';
                                                                  END;
                                                                  GetDebitDiscount;
                                                                  GetDebitRate;
                                                                  CalculDebitRentalPeriods;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Debit Rental Type;
                                                              NOR=Debetleietype;
                                                              SVE=Debetuthyrningstyp];
                                                   OptionCaptionML=[ENU=" ,Rental,Sales,Buy Back";
                                                                    NOR=" ,Leie,Salg,Tilbakekjõp";
                                                                    SVE=" ,Uthyrning,FîrsÑljning,èterkîp"];
                                                   OptionString=[ ,Rental,Sales,Buy Back];
                                                   BlankZero=Yes }
    { 150 ;   ;Credit Rental Type  ;Option        ;OnValidate=BEGIN
                                                                VALIDATE("Credit Rate Type");
                                                                IF "Credit Rental Type" = "Credit Rental Type"::" " THEN BEGIN
                                                                  TESTFIELD(Type,Type::Text);
                                                                  EXIT;
                                                                END;

                                                                TESTFIELD(Type,Type::Plant);
                                                                IF "Plant Type" <> '' THEN BEGIN
                                                                  PlantRec.GET(Rec."Plant Type");
                                                                  CASE PlantRec."Rental Type" OF
                                                                    PlantRec."Rental Type"::Rental:
                                                                      TESTFIELD("Credit Rental Type", "Credit Rental Type"::Rental);
                                                                    PlantRec."Rental Type"::"Sales/Buy Back":
                                                                      IF NOT ("Credit Rental Type" IN ["Credit Rental Type"::Sales,"Credit Rental Type"::"Buy Back"]) THEN
                                                                        FIELDERROR("Credit Rental Type");
                                                                  END;
                                                                  IF ("Credit Rental Type" <> "Credit Rental Type"::Rental) AND ("Credit Rate Code" <> '') THEN
                                                                    "Credit Rate Code" := '';
                                                                  IF ("Credit Rate Code" <> '') THEN BEGIN
                                                                    GetOrderHeader;
                                                                    IF NOT PlantRateCodeRec.CheckPlantRateCode(OrderRec."Credit Location", "Credit Rate Code", FALSE, '', 0) THEN
                                                                      "Credit Rate Code" := '';
                                                                  END;
                                                                  GetCreditDiscount;
                                                                  GetCreditRate;
                                                                  CalcCreditRentalPeriods;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Credit Rental Type;
                                                              NOR=Kreditutleietype;
                                                              SVE=Kredituthyrningstyp];
                                                   OptionCaptionML=[ENU=" ,Rental,Sales,Buy Back";
                                                                    NOR=" ,Leie,Salg,Tilbakekjõp";
                                                                    SVE=" ,Uthyrning,FîrsÑljning,èterkîp"];
                                                   OptionString=[ ,Rental,Sales,Buy Back];
                                                   BlankZero=Yes }
    { 160 ;   ;Debit Rate          ;Decimal       ;OnValidate=BEGIN
                                                                "Debet Amount" := ROUND("Debit Rate" * Quantity * DebitNumberPeriods * (1 - "Debit Discount %"/100));
                                                              END;

                                                   CaptionML=[ENU=Debit Rate;
                                                              NOR=Debetsats;
                                                              SVE=Debetavgift];
                                                   MinValue=0;
                                                   BlankZero=Yes;
                                                   AutoFormatType=2 }
    { 170 ;   ;Credit Rate         ;Decimal       ;OnValidate=BEGIN
                                                                "Credit Amount" := ROUND("Credit Rate" * Quantity * CreditNumberPeriods * (1 - "Credit Discount %"/100));
                                                              END;

                                                   CaptionML=[ENU=Credit Rate;
                                                              NOR=Kreditsats;
                                                              SVE=Kreditavgift];
                                                   MinValue=0;
                                                   BlankZero=Yes;
                                                   AutoFormatType=2 }
    { 180 ;   ;Debit Discount %    ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Debit Rate");
                                                              END;

                                                   CaptionML=[ENU=Debit Discount %;
                                                              NOR=Debetrabatt %;
                                                              SVE=Debetrabattprocent];
                                                   MaxValue=100;
                                                   BlankZero=Yes }
    { 190 ;   ;Credit Discount %   ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Credit Rate");
                                                              END;

                                                   CaptionML=[ENU=Credit Discount %;
                                                              NOR=Kreditrabatt %;
                                                              SVE=Kreditrabattprocent];
                                                   MaxValue=100;
                                                   BlankZero=Yes }
    { 200 ;   ;Debet Amount        ;Decimal       ;OnValidate=BEGIN
                                                                OriginAmount := "Debit Rate" * Quantity * DebitNumberPeriods;
                                                                IF OriginAmount <> 0 THEN
                                                                  "Debit Discount %" := ROUND((OriginAmount - "Debet Amount") / OriginAmount * 100, 0.00001)
                                                                ELSE
                                                                  "Debit Discount %" := 0;
                                                              END;

                                                   CaptionML=[ENU=Debit Amount;
                                                              NOR=Debetbelõp;
                                                              SVE=Debetbelopp];
                                                   MinValue=0;
                                                   BlankZero=Yes;
                                                   AutoFormatType=1 }
    { 210 ;   ;Credit Amount       ;Decimal       ;OnValidate=BEGIN
                                                                OriginAmount := "Credit Rate" * Quantity * CreditNumberPeriods;
                                                                IF OriginAmount <> 0 THEN
                                                                  "Credit Discount %" := ROUND((OriginAmount - "Credit Amount") / OriginAmount * 100, 0.00001)
                                                                ELSE
                                                                  "Credit Discount %" := 0;
                                                              END;

                                                   CaptionML=[ENU=Credit Amount;
                                                              NOR=Kreditbelõp;
                                                              SVE=Kreditbelopp];
                                                   MinValue=0;
                                                   BlankZero=Yes;
                                                   AutoFormatType=1 }
    { 220 ;   ;Debit Rate Code     ;Code10        ;OnValidate=BEGIN
                                                                IF "Debit Rate Code" <> '' THEN BEGIN
                                                                  TESTFIELD(Type,Type::Plant);
                                                                  TESTFIELD("Debit Rental Type","Debit Rental Type"::Rental);
                                                                  GetOrderHeader;
                                                                  PlantRateCodeRec.CheckPlantRateCode(OrderRec."Debit Location", "Debit Rate Code", TRUE, '', 0);
                                                                END;

                                                                GetDebitRate;
                                                              END;

                                                   OnLookup=BEGIN
                                                              GetOrderHeader;
                                                              PlantRateCodeRec.Code := PlantRateCodeRec.LookupPlantRateCode(OrderRec."Debit Location", "Debit Rate Code", TRUE, '', 0);
                                                              IF (NOT OrderRec.Posted) AND (Type = Type::Plant) AND
                                                                 ("Debit Rental Type" = "Debit Rental Type"::Rental) AND (PlantRateCodeRec.Code <> "Debit Rate Code")
                                                              THEN
                                                                VALIDATE("Debit Rate Code", PlantRateCodeRec.Code);
                                                            END;

                                                   CaptionML=[ENU=Debit Rate Code;
                                                              NOR=Debetsatskode;
                                                              SVE=Debetavgiftskod] }
    { 230 ;   ;Credit Rate Code    ;Code10        ;OnValidate=BEGIN
                                                                IF "Credit Rate Code" <> '' THEN BEGIN
                                                                  TESTFIELD(Type,Type::Plant);
                                                                  TESTFIELD("Credit Rental Type","Credit Rental Type"::Rental);
                                                                  GetOrderHeader;
                                                                  PlantRateCodeRec.CheckPlantRateCode(OrderRec."Credit Location", "Credit Rate Code", TRUE, '', 0);
                                                                END;

                                                                GetCreditRate;
                                                              END;

                                                   OnLookup=BEGIN
                                                              GetOrderHeader;
                                                              PlantRateCodeRec.Code := PlantRateCodeRec.LookupPlantRateCode(OrderRec."Credit Location", "Credit Rate Code", TRUE, '', 0);
                                                              IF (NOT OrderRec.Posted) AND (Type = Type::Plant) AND
                                                                 ("Credit Rental Type" = "Credit Rental Type"::Rental) AND (PlantRateCodeRec.Code <> "Credit Rate Code")
                                                              THEN
                                                                VALIDATE("Credit Rate Code", PlantRateCodeRec.Code);
                                                            END;

                                                   CaptionML=[ENU=Credit Rate Code;
                                                              NOR=Kreditsatskode;
                                                              SVE=Kreditavgiftskod] }
    { 240 ;   ;Debit Element       ;Code20        ;OnValidate=BEGIN
                                                                //DP00241 (C004118)
                                                                IF "Debit Element" = '' THEN
                                                                  EXIT;
                                                                TESTFIELD(Type, Type::Plant);
                                                                GetOrderHeader();
                                                                OrderRec.TESTFIELD("Debit Location");
                                                                PlantElementMgt.OnValidateElement(OrderRec."Debit Location", "Debit Element", '', FALSE);
                                                              END;

                                                   OnLookup=VAR
                                                              EditableMode@1100525001 : Boolean;
                                                              NewElement@1100525000 : Code[20];
                                                            BEGIN
                                                              //DP00241 (C004118)
                                                              GetOrderHeader();
                                                              IF (NOT OrderRec.Posted) AND (Type <> Type::Text) THEN
                                                                EditableMode := TRUE;
                                                              NewElement := PlantElementMgt.OnLookupElement(OrderRec."Debit Location", "Debit Element", '', EditableMode);
                                                              IF (NewElement <> '') AND (NewElement <> "Debit Element") THEN
                                                                VALIDATE("Debit Element", NewElement);
                                                            END;

                                                   CaptionML=[ENU=Debit Element;
                                                              NOR=Debitelement] }
    { 250 ;   ;Credit Element      ;Code20        ;OnValidate=BEGIN
                                                                //DP00241 (C004118)
                                                                IF "Credit Element" = '' THEN
                                                                  EXIT;
                                                                TESTFIELD(Type, Type::Plant);
                                                                GetOrderHeader();
                                                                OrderRec.TESTFIELD("Credit Location");
                                                                PlantElementMgt.OnValidateElement(OrderRec."Credit Location", "Credit Element", '', FALSE);
                                                              END;

                                                   OnLookup=VAR
                                                              EditableMode@1100525001 : Boolean;
                                                              NewElement@1100525000 : Code[20];
                                                            BEGIN
                                                              //DP00241 (C004118)
                                                              GetOrderHeader();
                                                              IF (NOT OrderRec.Posted) AND (Type <> Type::Text) THEN
                                                                EditableMode := TRUE;
                                                              NewElement := PlantElementMgt.OnLookupElement(OrderRec."Credit Location", "Credit Element", '', EditableMode);
                                                              IF (NewElement <> '') AND (NewElement <> "Credit Element") THEN
                                                                VALIDATE("Credit Element", NewElement);
                                                            END;

                                                   CaptionML=[ENU=Credit Element;
                                                              NOR=Kreditelement] }
    { 800 ;   ;Plant Group         ;Code20        ;TableRelation="Plant Group";
                                                   CaptionML=ENU=Plant Group }
  }
  KEYS
  {
    {    ;Rental  Correction No.,Line No.         ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@11012014 : TextConst 'ENU=must be 0 or 1 for unique Plant;NOR=mÜ vëre 0 eller 1 for unik maskin;SVE=mÜste vara 0 eller 1 fîr unik Maskin';
      GLSetup@11012000 : Record 98;
      PlantSetupRec@1100485000 : Record 11012550;
      OrderRec@11012002 : Record 11012565;
      PlantRec@11012004 : Record 11012551;
      PlantNoRec@11012005 : Record 11012552;
      StandardText@11012006 : Record 7;
      TempOrderLine@11012008 : Record 11012566;
      PlantLocRec@11012010 : Record 11012554;
      PlantRateCodeRec@1100525000 : Record 11020502;
      SearchRate@11012011 : Codeunit 11012567;
      CalculateRentalPeriodsCU@11012012 : Codeunit 11012568;
      PlantElementMgt@1100529000 : Codeunit 11012582;
      OriginAmount@11012013 : Decimal;
      Text001@1210190000 : TextConst 'ENU=Note, as from %1 this Plant has another Rental Rate;NOR=Obs! fra %1 har denne maskinen en annen leiesats;SVE=Obs! frÜn %1 har denna Maskin en annan uthyrningsavgift';

    PROCEDURE InitRecord@6();
    BEGIN
    END;

    PROCEDURE GetPlantDescription@4();
    BEGIN
      IF Type <> Type::Plant THEN
        EXIT;

      PlantNoRec.GetPlantDescription("Plant Type","No.",Description);
    END;

    PROCEDURE GetOrderHeader@5();
    BEGIN
      TESTFIELD("Rental  Correction No.");
      //IF ("Rental  Correction No." <> OrderRec."No.") THEN  //DP00241.o (order header can be modified)
        OrderRec.GET("Rental  Correction No.");
    END;

    PROCEDURE PriceRoundingPrecision@7() : Decimal;
    BEGIN
      GLSetup.GET;
      GLSetup.TESTFIELD("Unit-Amount Rounding Precision");
      EXIT(GLSetup."Unit-Amount Rounding Precision");
    END;

    PROCEDURE GetDefaults@10();
    BEGIN
      PlantRec.GET(Rec."Plant Type");

      CASE PlantRec."Rental Type" OF
        PlantRec."Rental Type"::Rental,PlantRec."Rental Type"::Both:
          BEGIN
            "Debit Rental Type" := "Debit Rental Type"::Rental;
            "Credit Rental Type" := "Credit Rental Type"::Rental;
          END;
        PlantRec."Rental Type"::"Sales/Buy Back":
          BEGIN
            "Debit Rental Type" := "Debit Rental Type"::Sales;
            "Credit Rental Type" := "Credit Rental Type"::Sales;
          END;
      END;
      IF ("Debit Rental Type" <> "Debit Rental Type"::Rental) AND ("Debit Rate Code" <> '') THEN
        "Debit Rate Code" := '';
      IF ("Credit Rental Type" <> "Credit Rental Type"::Rental) AND ("Credit Rate Code" <> '') THEN
        "Credit Rate Code" := '';
      VALIDATE("Debit Rental Type");
      VALIDATE("Credit Rental Type");
    END;

    PROCEDURE GetRates@11();
    BEGIN
      GetCreditRate;
      GetDebitRate;
    END;

    PROCEDURE GetCreditRate@9();
    VAR
      lStartDate@11012000 : Date;
    BEGIN
      "Credit Rate" := 0;

      IF Type <> Type::Plant THEN
        EXIT;

      GetOrderHeader;
      IF OrderRec."Credit Location" = '' THEN
        EXIT;

      IF "Starting Date" = 0D THEN
        lStartDate := TODAY
      ELSE
        lStartDate := "Starting Date";

      IF "Credit Rental Type" <> "Credit Rental Type"::" " THEN
        VALIDATE("Credit Rate",
          SearchRate.Rate(
            OrderRec."Credit Location","Plant Type","No.","Credit Rate Code",
            lStartDate,"Credit Rental Type"-1,GiveRateMessage,''));
    END;

    PROCEDURE GetDebitRate@12();
    VAR
      lStartDate@11012000 : Date;
    BEGIN
      "Debit Rate" := 0;

      IF Type <> Type::Plant THEN
        EXIT;

      GetOrderHeader;
      IF OrderRec."Debit Location" = '' THEN
        EXIT;

      IF "Starting Date" = 0D THEN
        lStartDate := TODAY
      ELSE
        lStartDate := "Starting Date";

      IF "Debit Rental Type" <> "Credit Rental Type"::" " THEN
        VALIDATE("Debit Rate",
          SearchRate.Rate(
            OrderRec."Debit Location","Plant Type","No.","Debit Rate Code",
            lStartDate,"Debit Rental Type"-1,GiveRateMessage,''));
    END;

    PROCEDURE GetDiscounts@8();
    BEGIN
      GetCreditDiscount;
      GetDebitDiscount;
    END;

    PROCEDURE GetCreditDiscount@2();
    BEGIN
      IF NOT ("Credit Rental Type" IN ["Credit Rental Type"::Rental, "Credit Rental Type"::Sales]) THEN
        EXIT;

      GetOrderHeader;
      IF OrderRec."Credit Location" = '' THEN
        EXIT;

      CASE "Credit Rental Type" OF
        "Credit Rental Type"::Rental:
          VALIDATE("Credit Discount %", SearchRate.RentalDiscount(OrderRec."Credit Location",'',"Plant Type","No.", "Starting Date"));
        "Credit Rental Type"::Sales:
          BEGIN
            PlantSetupRec.GET;
            IF NOT PlantSetupRec."Discount Sales/Buy Back" THEN
              EXIT;
            PlantLocRec.GET(OrderRec."Credit Location");
            VALIDATE("Credit Discount %", PlantLocRec."Discount %");
          END;
      END;
    END;

    PROCEDURE GetDebitDiscount@3();
    BEGIN
      IF NOT ("Debit Rental Type" IN ["Debit Rental Type"::Rental, "Debit Rental Type"::Sales]) THEN
        EXIT;

      GetOrderHeader;
      IF OrderRec."Debit Location" = '' THEN
        EXIT;

      CASE "Debit Rental Type" OF
        "Debit Rental Type"::Rental:
          VALIDATE("Debit Discount %", SearchRate.RentalDiscount(OrderRec."Debit Location",'',"Plant Type","No.", "Starting Date"));
        "Debit Rental Type"::Sales:
          BEGIN
            PlantSetupRec.GET;
            IF NOT PlantSetupRec."Discount Sales/Buy Back" THEN
              EXIT;
            PlantLocRec.GET(OrderRec."Debit Location");
            VALIDATE("Debit Discount %", PlantLocRec."Discount %");
          END;
      END;
    END;

    PROCEDURE CalculateNumberRentalPeriods@1();
    BEGIN
      CalcCreditRentalPeriods;
      CalculDebitRentalPeriods;
    END;

    PROCEDURE CalcCreditRentalPeriods@13();
    VAR
      lvRateType@1100485000 : Option;
    BEGIN
      "Credit No. of Rental Periods" := 0;
      IF "Credit Rental Type" = "Credit Rental Type"::Rental THEN BEGIN
        GetOrderHeader;
        IF OrderRec."Credit Location" <> '' THEN BEGIN
          PlantLocRec.GET(OrderRec."Credit Location");
          "Credit No. of Rental Periods" :=
            CalculateRentalPeriodsCU.NumberRentalPeriods(PlantLocRec, "Starting Date", "Ending Date", "Plant Type", "No.",lvRateType);
        END;
      END;

      VALIDATE("Credit No. of Rental Periods");
    END;

    PROCEDURE CalculDebitRentalPeriods@14();
    VAR
      lvRateType@1100485000 : Option;
    BEGIN
      "Debit No. of Rental Periods" := 0;
      IF "Debit Rental Type" = "Debit Rental Type"::Rental THEN BEGIN
        GetOrderHeader;
        IF OrderRec."Debit Location" <> '' THEN BEGIN
          PlantLocRec.GET(OrderRec."Debit Location");
          "Debit No. of Rental Periods" :=
            CalculateRentalPeriodsCU.NumberRentalPeriods(PlantLocRec, "Starting Date", "Ending Date", "Plant Type", "No.",lvRateType);
        END;
      END;

      VALIDATE("Debit No. of Rental Periods");
    END;

    PROCEDURE CreditNumberPeriods@18() : Decimal;
    BEGIN
      IF "Credit Rental Type" = "Credit Rental Type"::Rental THEN
        EXIT("Credit No. of Rental Periods")
      ELSE
        EXIT(1);
    END;

    PROCEDURE DebitNumberPeriods@17() : Decimal;
    BEGIN
      IF "Debit Rental Type" = "Debit Rental Type"::Rental THEN
        EXIT("Debit No. of Rental Periods")
      ELSE
        EXIT(1);
    END;

    PROCEDURE CheckMoreRentalRates@1210190000();
    VAR
      lvNewRateDate@1210190000 : Date;
    BEGIN
      IF ("Plant Type" = '') OR ("Starting Date" = 0D) OR ("Ending Date" = 0D) THEN
        EXIT;

      GetOrderHeader;

      IF SearchRate.RentalRateModifiedInPeriod(OrderRec."Debit Location","Plant Type", "No.",
      "Starting Date", "Ending Date", lvNewRateDate) THEN
        MESSAGE(Text001, lvNewRateDate);
    END;

    PROCEDURE GiveRateMessage@1100485000() : Integer;
    BEGIN
      PlantSetupRec.GET;
      IF PlantSetupRec."Rate Warning" THEN
        EXIT(1);  //* Give message if rate not found

      EXIT(0);  //* No message
    END;

    BEGIN
    END.
  }
}

