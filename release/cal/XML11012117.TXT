OBJECT XMLport 11012117 Imp. Project WIP
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Imp. Project WIP;
    DefaultFieldsValidation=No;
    PreserveWhiteSpace=Yes;
    TextEncoding=WINDOWS;
    OnInitXMLport=BEGIN
                    XmlPortID := 11012117;
                    MainTableID := 81;
                    XMLPortRequestPageID := 11127329;
                  END;

    OnPreXMLport=VAR
                   ConversionXMLPort4PS@1100527700 : Record 11020640;
                 BEGIN
                   IF NOT currXMLport.IMPORTFILE THEN BEGIN
                     AddConversionFileFormatLine;
                     ConversionXMLPort4PS.UpdateFromXMLPort(XmlPortID,MainTableID,MainTableLookupPageID,XMLPortRequestPageID,TRUE,ConversionXMLPort."Journal Template Type"::General);
                     COMMIT;
                     ERROR('');
                   END;

                   ConversionXMLPort.GET(XmlPortID);

                   ConvMgt.DeleteConComment(XmlPortID);
                   ConvMgt.CreateConComment(XmlPortID,STRSUBSTNO('Import File: %1.',currXMLport.FILENAME));
                   ConvMgt.CreateConComment(XmlPortID,'Start');
                 END;

    Format=Variable Text;
    FieldDelimiter=<None>;
    FieldSeparator=[;];
  }
  ELEMENTS
  {
    { [{7B539476-C20A-46BC-A552-62FED55766AD}];  ;Root                ;Element ;Text     }

    { [{A667E51D-CE85-4A0D-946E-DE2CCC52ED51}];1 ;GeneralJournalLine  ;Element ;Table   ;
                                                  SourceTable=Table81;
                                                  SourceTableView=SORTING(Field1,Field51,Field2);
                                                  Import::OnBeforeInsertRecord=VAR
                                                                                 ProjCde@1100409000 : Code[20];
                                                                                 vSource@1100409002 : 'Project,Service';
                                                                               BEGIN
                                                                                 IF AllInputFieldsAreEmpty THEN
                                                                                   currXMLport.SKIP;

                                                                                 CLEAR("Gen. Journal Line");

                                                                                 WITH "Gen. Journal Line" DO BEGIN

                                                                                   IF UseImportDate AND ( KDate <> 0D) THEN
                                                                                     PostingDate := KDate
                                                                                   ELSE
                                                                                    PostingDate := ConversionXMLPort."Posting Date";

                                                                                   ProjCde := ConvMgt.DetermineNewCode(
                                                                                     LookupJobNo,
                                                                                     CommentRec."Table Name"::Job,
                                                                                     JobCommentCde,
                                                                                     A);

                                                                                   ConvMgt.GenerateGenJnlLine(
                                                                                     ConversionXMLPort."Journal Template Name",
                                                                                     ConversionXMLPort."Journal Batch Name",
                                                                                     PostingDate,
                                                                                     B,
                                                                                     ConversionXMLPort."Balance Account No.",
                                                                                     "Gen. Journal Line");

                                                                                   XmlShowStatus.ShowImportCounter;

                                                                                   ConvMgt.UpdateWIPGenJnlLine(
                                                                                     vSource::Project,ProjCde,A,B,C,D,EDec,FDec,"Gen. Journal Line");

                                                                                   ImpLineCounter := ImpLineCounter + 1;

                                                                                   IF FDec = 0 THEN
                                                                                     FDec := 1;

                                                                                   IF "Job No." = '' THEN
                                                                                     ConvMgt.CreateConComment(XmlPortID,ConvMgt.CreateFieldNotFilledCommentTxt(
                                                                                       11072003,
                                                                                       A,
                                                                                       FIELDCAPTION("Job No."),
                                                                                       MainTableID,
                                                                                       STRSUBSTNO('%1-%2-%3',"Journal Template Name","Journal Batch Name","Line No.")));

                                                                                   IF ConvMgt.IsProjectPresent(ProjCde) THEN
                                                                                     IF G <> '' THEN
                                                                                       IF ConvMgt.IsProjectElementPresent(ProjCde,G) THEN
                                                                                         VALIDATE(Element,G)
                                                                                       ELSE
                                                                                         ConvMgt.CreateConComment(XmlPortID,ConvMgt.CreateFieldNotFilledCommentTxt(
                                                                                           11012010,
                                                                                           STRSUBSTNO('%1-%2',ProjCde,G),
                                                                                           FIELDCAPTION(Element),
                                                                                           MainTableID,
                                                                                           STRSUBSTNO('%1-%2-%3',"Journal Template Name","Journal Batch Name","Line No.")));

                                                                                   IF I <> '' THEN
                                                                                     IF ConvMgt.IsTradeItemPresent(H,I) THEN BEGIN
                                                                                       "Vendor (Trade Item)" := H; //Don't validate, price can differ
                                                                                       "Trade Item" := I;          //from conversion import file
                                                                                     END;

                                                                                   IF J <> '' THEN
                                                                                     IF ConvMgt.IsItemPresent(J) THEN
                                                                                       "Item No." := J; //Don't validate, price can differ
                                                                                                        //from conversion import file

                                                                                   ConvMgt.ClearGJNLPostingFields("Gen. Journal Line");

                                                                                   MODIFY(TRUE);
                                                                                 END;

                                                                                 ClearFields;
                                                                                 currXMLport.SKIP;
                                                                               END;
                                                                                }

    { [{3AC77EC7-9326-4B76-BA5F-EA1F70D4DA67}];2 ;A                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=20 }

    { [{1B889F9B-B9E2-4749-96B0-D22BA1117049}];2 ;B                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=50 }

    { [{D76B46B3-243B-4FF7-8D1D-C7B64CCFA492}];2 ;C                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=50 }

    { [{11AAC24C-6612-4FE6-9973-FE5EF2EF2C43}];2 ;D                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=50 }

    { [{F0633544-507F-4C5E-A35B-A0DD95F11E63}];2 ;E                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  IF E <> '' THEN
                                                                                    EVALUATE(EDec,E);
                                                                                END;

                                                  Width=50 }

    { [{98E7308F-19D5-4AF6-89FB-48DB78E52124}];2 ;F                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  IF F <> '' THEN
                                                                                    EVALUATE(FDec,F);
                                                                                END;

                                                  Width=50 }

    { [{23B2F6EB-78F9-48FB-BC70-858F6A53A74C}];2 ;G                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=50 }

    { [{007CC416-7129-43EA-90B0-193FFE5FCA68}];2 ;H                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=50 }

    { [{8F9BF873-E7E2-450A-B9C5-33703DAFE6BE}];2 ;I                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=50 }

    { [{17D83AA6-D251-4A7A-8475-309C7FF2822C}];2 ;J                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{96548088-F8F5-4882-AA70-31D319F2CC43}];2 ;K                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  IF K <> '' THEN
                                                                                    EVALUATE(KDate,K);
                                                                                END;
                                                                                 }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100409000;;Container;
                  Name=Options;
                  ContainerType=ContentArea }

      { 1100409001;1;Field  ;
                  Name=LookupJobNo;
                  CaptionML=ENU=Find Job by Old Job No;
                  SourceExpr=LookupJobNo;
                  OnValidate=BEGIN
                               IF NOT LookupJobNo THEN
                                 JobCommentCde := '';
                             END;
                              }

      { 1100409002;1;Field  ;
                  Name=JobCommentCde;
                  CaptionML=ENU=Comment Code old Job;
                  SourceExpr=JobCommentCde;
                  TableRelation="Comment Code" }

      { 1100525000;1;Field  ;
                  Name=UseImportDate;
                  CaptionML=ENU=Use Import Date;
                  SourceExpr=UseImportDate }

    }
  }
  CODE
  {
    VAR
      ConversionXMLPort@1100409001 : Record 11020640;
      XmlPortID@1100409000 : Integer;
      MainTableID@1100409002 : Integer;
      MainTableLookupPageID@1100527701 : Integer;
      XMLPortRequestPageID@1100527700 : Integer;
      CommentRec@1100525061 : Record 97;
      ConvMgt@1100525059 : Codeunit 11020212;
      ImpLineCounter@1100409007 : Integer;
      XmlShowStatus@1100409010 : Codeunit 11130006;
      LookupJobNo@1100409005 : Boolean;
      JobCommentCde@1100409004 : Code[20];
      UseImportDate@1100525001 : Boolean;
      PostingDate@1100409003 : Date;
      EDec@1100525000 : Decimal;
      FDec@1100409008 : Decimal;
      KDate@1100409017 : Date;
      Text001@1100409006 : TextConst 'ENU=Document No. in Description;SVE=Dokumentnr i beskrivning';

    PROCEDURE AddConversionFileFormatLine@1100529901();
    VAR
      ConversionFileFormat@1100409000 : Record 11020691;
    BEGIN
      // This function stores the file format into table [Conversion File Format].
      // There is no other standard documentation; so please maintain this function in case of customization.
      //
      // Parameters: 1: [XMLport No.],
      //             2: [csv-Column],
      //             3: [NAV Table ID],
      //             4: [NAV Field Name],
      //             5: [Data Type of dataport field],
      //             6: [extra Comment].
      //
      WITH ConversionFileFormat DO BEGIN
        ConversionFileFormat.DeleteColumnLines(XmlPortID);
        InsertColumnLine(XmlPortID,'A',MainTableID,"Gen. Journal Line".FIELDNAME("Job No."),'Code20','OldTxt');
        InsertColumnLine(XmlPortID,'B',0,Text001,'Code20',Text001);
        InsertColumnLine(XmlPortID,'C',MainTableID,"Gen. Journal Line".FIELDNAME("Shortcut Dimension 2 Code"),'Code20','');
        InsertColumnLine(XmlPortID,'D',MainTableID,"Gen. Journal Line".FIELDNAME(Description),'Text50','');
        InsertColumnLine(XmlPortID,'E',MainTableID,"Gen. Journal Line".FIELDNAME(Amount),'Decimal','');
        InsertColumnLine(XmlPortID,'F',MainTableID,"Gen. Journal Line".FIELDNAME(Quantity),'Decimal','');
        InsertColumnLine(XmlPortID,'G',MainTableID,"Gen. Journal Line".FIELDNAME(Element),'Code20','');
        InsertColumnLine(XmlPortID,'H',MainTableID,"Gen. Journal Line".FIELDNAME("Vendor (Trade Item)"),'Code20','');
        InsertColumnLine(XmlPortID,'I',MainTableID,"Gen. Journal Line".FIELDNAME("Trade Item"),'Code20','');
        InsertColumnLine(XmlPortID,'J',MainTableID,"Gen. Journal Line".FIELDNAME("Item No."),'Decimal','');
        InsertColumnLine(XmlPortID,'K',MainTableID,"Gen. Journal Line".FIELDNAME("Posting Date"),'Date','');
      END;
    END;

    PROCEDURE StripData@1100485000();
    BEGIN
    END;

    PROCEDURE ClearFields@1100529902();
    BEGIN
      CLEAR("Gen. Journal Line");

      CLEAR(A);
      CLEAR(B);
      CLEAR(C);
      CLEAR(D);
      CLEAR(E);
      CLEAR(F);
      CLEAR(G);
      CLEAR(H);
      CLEAR(I);
      CLEAR(J);
      CLEAR(K);

      CLEAR(EDec);
      CLEAR(FDec);
      CLEAR(KDate);
    END;

    LOCAL PROCEDURE AllInputFieldsAreEmpty@1100525000() : Boolean;
    BEGIN
      EXIT(
        (A = '') AND
        (B = '') AND
        (C = '') AND
        (D = '') AND
        (E = '') AND
        (F = '') AND
        (G = '') AND
        (H = '') AND
        (I = '') AND
        (J = '') AND
        (K = ''));
    END;

    PROCEDURE InitXMLport@1100529500(iLookupJobNo@1100529502 : Boolean;iJobCommentCde@1100529501 : Code[20];iUseImportDate@1100529500 : Boolean);
    BEGIN
      LookupJobNo := iLookupJobNo;
      JobCommentCde := iJobCommentCde;
      UseImportDate := iUseImportDate;
    END;

    BEGIN
    END.
  }
}

