OBJECT Table 11125769 Glazing Activity Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00,4PSSE;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Glazing Activity Line;
               NOR=Innglassingsaktivitetrad;
               SVE=Inglasningsaktivitet, rad];
    LookupPageID=Page11125771;
    DrillDownPageID=Page11125771;
  }
  FIELDS
  {
    { 1   ;   ;Glazing Activity Code;Code10       ;TableRelation="Glazing Activity";
                                                   OnValidate=VAR
                                                                GlazingActivity@1100525000 : Record 11125768;
                                                              BEGIN
                                                                IF NOT GlazingActivity.GET("Glazing Activity Code") THEN
                                                                  GlazingActivity.INIT;

                                                                "Rate Code" := GlazingActivity."Rate Code";
                                                                "Surcharge %" := GlazingActivity."Default Surcharge %";
                                                                "Profit/Risk %" := GlazingActivity."Default Profit/Risk %";
                                                                "Surface Area" := GlazingActivity."Surface Area";
                                                                "Linear Meters" := GlazingActivity."Linear Meters";
                                                                "Activity Quantity" := GlazingActivity.Quantity;
                                                                "Height Surcharge" := GlazingActivity."Height Surcharge";
                                                                "No. of Glaziers" := GlazingActivity."No. of Glaziers";
                                                                VALIDATE("Price Reference Date", GlazingActivity."Price Reference Date");
                                                              END;

                                                   CaptionML=[ENU=Glazing Activity Code;
                                                              NOR=Innglassingsaktivitetkode;
                                                              SVE=Inglasningsaktivitet, kod];
                                                   NotBlank=Yes }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 10  ;   ;Cost Driver         ;Option        ;OnValidate=BEGIN
                                                                IF "Cost Driver" = "Cost Driver"::" " THEN
                                                                  "Qty. per Unit of Measure" := 0;

                                                                UpdateQuantity;
                                                              END;

                                                   CaptionML=[ENU=Cost Driver;
                                                              NOR=Kostnadsdriver;
                                                              SVE=Kostnadsdrivare];
                                                   OptionCaptionML=[ENU=" ,,,,,Surface Area,,,,,Linear Meters,,,,,,,,,,Height Surcharge,,,,,No. of Glaziers,,,,,Qty. of Call Line";
                                                                    NOR=" ,,,,,Overflateomr†de,,,,,L›pemeter,,,,,,,,,,St›rrelse tilleggsgebyr,,,,,Antall glassmestere,,,,,Antall samtalerader";
                                                                    SVE=" ,,,,,Ytarea,,,,,L”pmeter,,,,,,,,,,H”jd till„ggsavgift,,,,,Antal glasm„stare,,,,,Ant. samtalslinje"];
                                                   OptionString=[ ,,,,,Surface Area,,,,,Linear Meters,,,,,,,,,,Height Surcharge,,,,,No. of Glaziers,,,,,Qty. of Call Line] }
    { 20  ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   OnValidate=VAR
                                                                Item@1100525000 : Record 27;
                                                              BEGIN
                                                                IF NOT Item.GET("Item No.") THEN
                                                                  Item.INIT
                                                                //mg.sn, 13-12-11: M31429
                                                                ELSE BEGIN
                                                                  Item.TESTFIELD(Blocked, FALSE);
                                                                  Item.TESTFIELD("Purchasing Blocked", FALSE); //4PSSE
                                                                END;
                                                                //mg.en, 13-12-11: M31429

                                                                "Cost Object Code Material" := Item."Global Dimension 2 Code";
                                                                "Material Price" := Item."Standard Cost";

                                                                IF Description = '' THEN
                                                                  Description := Item.Description;
                                                              END;

                                                   CaptionML=[ENU=Item No.;
                                                              NOR=Artikkelnr;
                                                              SVE=Artikelnr] }
    { 30  ;   ;Cost Object Code Labor;Code20      ;TableRelation="Dimension Value".Code WHERE (Cost Type=CONST(Labor),
                                                                                               Global Dimension No.=CONST(2));
                                                   OnValidate=VAR
                                                                DimensionValue@1100525000 : Record 349;
                                                              BEGIN
                                                                DimensionValue.SETCURRENTKEY(Code, "Global Dimension No.");
                                                                DimensionValue.SETRANGE(Code, "Cost Object Code Labor");
                                                                DimensionValue.SETRANGE("Global Dimension No.", 2);

                                                                IF NOT DimensionValue.FINDFIRST THEN
                                                                  DimensionValue.INIT;

                                                                IF DimensionValue.Name <> '' THEN
                                                                  IF Description = '' THEN
                                                                    Description := DimensionValue.Name;

                                                                VALIDATE(Norm, 0); // jhoek.100910
                                                              END;

                                                   CaptionML=[ENU=Cost Object Code Labor;
                                                              NOR=Kostnadsobjekt, arbeid;
                                                              SVE=Kostnadsobjektkod, arbetskraft] }
    { 40  ;   ;Cost Object Code Material;Code20   ;TableRelation="Dimension Value".Code WHERE (Cost Type=CONST(Material),
                                                                                               Global Dimension No.=CONST(2));
                                                   OnValidate=VAR
                                                                DimensionValue@1100525000 : Record 349;
                                                              BEGIN
                                                                DimensionValue.SETCURRENTKEY(Code, "Global Dimension No.");
                                                                DimensionValue.SETRANGE(Code, "Cost Object Code Material");
                                                                DimensionValue.SETRANGE("Global Dimension No.", 2);

                                                                IF NOT DimensionValue.FINDFIRST THEN
                                                                  DimensionValue.INIT;

                                                                VALIDATE("Material Price", DimensionValue.Price);

                                                                IF DimensionValue.Name <> '' THEN
                                                                  IF Description = '' THEN
                                                                    Description := DimensionValue.Name;
                                                              END;

                                                   CaptionML=[ENU=Cost Object Code Material;
                                                              NOR=Kostnadsobjektkode, materiale;
                                                              SVE=Kostnadsobjektkod, material] }
    { 50  ;   ;Cost Object Code Subcontr.;Code20  ;TableRelation="Dimension Value".Code WHERE (Cost Type=CONST(Subcontracting),
                                                                                               Global Dimension No.=CONST(2));
                                                   OnValidate=VAR
                                                                DimensionValue@1100525000 : Record 349;
                                                              BEGIN
                                                                DimensionValue.SETCURRENTKEY(Code, "Global Dimension No.");
                                                                DimensionValue.SETRANGE(Code, "Cost Object Code Subcontr.");
                                                                DimensionValue.SETRANGE("Global Dimension No.", 2);

                                                                IF NOT DimensionValue.FINDFIRST THEN
                                                                  DimensionValue.INIT;

                                                                VALIDATE("Subcontracting Price", DimensionValue.Price);

                                                                IF DimensionValue.Name <> '' THEN
                                                                  IF Description = '' THEN
                                                                    Description := DimensionValue.Name;
                                                              END;

                                                   CaptionML=[ENU=Cost Object Code Subcontr.;
                                                              NOR=Kostnadsobjektkode, underentrepren›r;
                                                              SVE=Kostn.obj.kod, underent.] }
    { 60  ;   ;Cost Object Code Plant;Code20      ;TableRelation="Dimension Value".Code WHERE (Cost Type=CONST(Plant),
                                                                                               Global Dimension No.=CONST(2));
                                                   OnValidate=VAR
                                                                DimensionValue@1100525000 : Record 349;
                                                              BEGIN
                                                                DimensionValue.SETCURRENTKEY(Code, "Global Dimension No.");
                                                                DimensionValue.SETRANGE(Code, "Cost Object Code Plant");
                                                                DimensionValue.SETRANGE("Global Dimension No.", 2);

                                                                IF NOT DimensionValue.FINDFIRST THEN
                                                                  DimensionValue.INIT;

                                                                VALIDATE("Plant Price", DimensionValue.Price);

                                                                IF DimensionValue.Name <> '' THEN
                                                                  IF Description = '' THEN
                                                                    Description := DimensionValue.Name;
                                                              END;

                                                   CaptionML=[ENU=Cost Object Code Plant;
                                                              NOR=Kostnadsobjektkode, maskin;
                                                              SVE=Kostnadsobjektkod, maskin] }
    { 70  ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 100 ;   ;Surface Area        ;Decimal       ;OnValidate=BEGIN
                                                                UpdateQuantity;
                                                              END;

                                                   CaptionML=[ENU=Surface Area;
                                                              NOR=Overflateomr†de;
                                                              SVE=Ytarea];
                                                   Editable=No }
    { 110 ;   ;Linear Meters       ;Decimal       ;OnValidate=BEGIN
                                                                UpdateQuantity;
                                                              END;

                                                   CaptionML=[ENU=Linear Meters;
                                                              NOR=L›pemeter;
                                                              SVE=L”pmeter];
                                                   Editable=No }
    { 120 ;   ;Activity Quantity   ;Decimal       ;OnValidate=BEGIN
                                                                UpdateQuantity;
                                                              END;

                                                   CaptionML=[ENU=Activity Quantity;
                                                              NOR=Aktivitet, antall;
                                                              SVE=Aktivitet, antal];
                                                   Editable=No }
    { 130 ;   ;Height Surcharge    ;Decimal       ;OnValidate=BEGIN
                                                                UpdateQuantity;
                                                              END;

                                                   CaptionML=[ENU=Height Surcharge;
                                                              NOR=St›rrelse p† tilleggsgebyr;
                                                              SVE=H”jd, extra avgift];
                                                   Editable=No }
    { 140 ;   ;No. of Glaziers     ;Decimal       ;OnValidate=BEGIN
                                                                UpdateQuantity;
                                                              END;

                                                   CaptionML=[ENU=No. of Glaziers;
                                                              NOR=Antall glassmestere;
                                                              SVE=Antal glasm„stare];
                                                   Editable=No }
    { 200 ;   ;Qty. per Unit of Measure;Decimal   ;InitValue=1;
                                                   OnValidate=BEGIN
                                                                IF "Qty. per Unit of Measure" <> 0 THEN
                                                                  IF ("Cost Driver" = "Cost Driver"::" ") OR ("Cost Driver" = "Cost Driver"::"Qty. of Call Line") THEN
                                                                    FIELDERROR("Qty. per Unit of Measure", STRSUBSTNO(Text000, FIELDCAPTION("Cost Driver")));

                                                                UpdateQuantity;
                                                              END;

                                                   CaptionML=[ENU=Qty. per Unit of Measure;
                                                              NOR=Antall per M†leenhet;
                                                              SVE=Antal per enhet];
                                                   BlankZero=Yes }
    { 210 ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                IF CurrFieldNo = FIELDNO(Quantity) THEN
                                                                //  IF Quantity <> 0 THEN // jhoek061010
                                                                  TESTFIELD("Cost Driver", "Cost Driver"::" ");

                                                                UpdateHours;
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   DecimalPlaces=0:5;
                                                   BlankZero=Yes }
    { 220 ;   ;Unit of Measure Code;Code10        ;TableRelation="Unit of Measure";
                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              NOR=Enhetskode;
                                                              SVE=Enhetskod] }
    { 230 ;   ;Norm                ;Decimal       ;OnValidate=BEGIN
                                                                IF Norm <> 0 THEN
                                                                  TESTFIELD("Cost Object Code Labor"); // jhoek.100910

                                                                UpdateHours;
                                                              END;

                                                   CaptionML=[ENU=Norm;
                                                              NOR=Norm;
                                                              SVE=Norm];
                                                   DecimalPlaces=0:5;
                                                   BlankZero=Yes }
    { 240 ;   ;Rate Code           ;Code10        ;TableRelation="Budget Rate".Code;
                                                   OnValidate=BEGIN
                                                                UpdateRate;
                                                              END;

                                                   CaptionML=[ENU=Rate Code;
                                                              NOR=Avgiftskode;
                                                              SVE=Avgiftskod] }
    { 245 ;   ;Price Reference Date;Date          ;OnValidate=BEGIN
                                                                UpdateRate;
                                                              END;

                                                   CaptionML=[ENU=Price Reference Date;
                                                              NOR=Referansedato for pris;
                                                              SVE=Priss„ttningsdatum];
                                                   Editable=No }
    { 250 ;   ;Rate                ;Decimal       ;OnValidate=BEGIN
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=[ENU=Rate;
                                                              NOR=Avgift;
                                                              SVE=Avgift];
                                                   BlankZero=Yes }
    { 260 ;   ;Hours               ;Decimal       ;OnValidate=BEGIN
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=[ENU=Hours;
                                                              NOR="Timer ";
                                                              SVE=Timmar];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 261 ;   ;Total Hours         ;Decimal       ;CaptionML=[ENU=Total Hours;
                                                              NOR=Timer totalt;
                                                              SVE=Totalt timmar];
                                                   Editable=No }
    { 300 ;   ;Material Price      ;Decimal       ;OnValidate=BEGIN
                                                                IF "Material Price" <> 0 THEN
                                                                  TESTFIELD("Cost Object Code Material"); // jhoek.100910

                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=[ENU=Material Price;
                                                              NOR=Materialepris;
                                                              SVE=Materialpris];
                                                   BlankZero=Yes }
    { 310 ;   ;Subcontracting Price;Decimal       ;OnValidate=BEGIN
                                                                IF "Subcontracting Price" <> 0 THEN
                                                                  TESTFIELD("Cost Object Code Subcontr."); // jhoek.100910

                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=[ENU=Subcontracting Price;
                                                              NOR=Underentrepen›rpris;
                                                              SVE=Underentreprenadspris];
                                                   BlankZero=Yes }
    { 320 ;   ;Plant Price         ;Decimal       ;OnValidate=BEGIN
                                                                IF "Plant Price" <> 0 THEN
                                                                  TESTFIELD("Cost Object Code Plant"); // jhoek.100910

                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=[ENU=Plant Price;
                                                              NOR=Maskinpris;
                                                              SVE=Maskinpris];
                                                   BlankZero=Yes }
    { 350 ;   ;Labor Amount        ;Decimal       ;CaptionML=[ENU=Labor Amount;
                                                              NOR=Arbeidskraftsbel›p;
                                                              SVE=Arbetskraftsbelopp];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 360 ;   ;Material Amount     ;Decimal       ;CaptionML=[ENU=Material Amount;
                                                              NOR=Materialebel›p;
                                                              SVE=Materialbelopp];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 370 ;   ;Subcontracting Amount;Decimal      ;CaptionML=[ENU=Subcontracting Amount;
                                                              NOR=Underentrepren›rbel›p;
                                                              SVE=Underentreprenadsbelopp];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 380 ;   ;Plant Amount        ;Decimal       ;CaptionML=[ENU=Plant Amount;
                                                              NOR=Maskinbel›p;
                                                              SVE=Maskin belopp];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 400 ;   ;Cost Amount         ;Decimal       ;CaptionML=[ENU=Cost Amount;
                                                              NOR=Kostbel›p;
                                                              SVE=Kostnadsbelopp];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 410 ;   ;Surcharge %         ;Decimal       ;OnValidate=BEGIN
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=[ENU=Surcharge %;
                                                              NOR=Tilleggsgebyrsprosent;
                                                              SVE=Till„ggsavgiftsprocent];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 411 ;   ;Surcharge           ;Decimal       ;CaptionML=[ENU=Surcharge;
                                                              NOR=Tilleggsgebyr;
                                                              SVE=Till„ggsavgift];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 412 ;   ;Total Surcharge     ;Decimal       ;CaptionML=[ENU=Total Surcharge;
                                                              NOR=Total tilleggsavgift;
                                                              SVE=Till„ggsavgifter totalt];
                                                   Editable=No }
    { 420 ;   ;Profit/Risk %       ;Decimal       ;OnValidate=BEGIN
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=[ENU=Profit/Risk %;
                                                              NOR=Fortjeneste/Risiko %;
                                                              SVE=Procent vinst/risk];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 430 ;   ;Profit/Risk         ;Decimal       ;CaptionML=[ENU=Profit/Risk;
                                                              NOR=Fortjeneste/Risiko;
                                                              SVE=Vinst/risk];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 431 ;   ;Total Profit/Risk   ;Decimal       ;CaptionML=[ENU=Total Profit/Risk;
                                                              NOR=Total fortjeneste/risiko;
                                                              SVE=Totalt vinst/risk];
                                                   Editable=No }
    { 500 ;   ;Sales Amount        ;Decimal       ;CaptionML=[ENU=Sales Amount;
                                                              NOR=Salgsbel›p;
                                                              SVE=F”rs„ljningsbelopp];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 600 ;   ;Total Cost Amount   ;Decimal       ;CaptionML=[ENU=Total Cost Amount;
                                                              NOR=Totalt kostbel›p;
                                                              SVE=Totalt kostnadsbelopp];
                                                   Editable=No }
    { 610 ;   ;Total Sales Amount  ;Decimal       ;CaptionML=[ENU=Total Sales Amount;
                                                              NOR=Totalt salgsbel›p;
                                                              SVE=Totalt f”rs„ljningsbelopp];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Glazing Activity Code,Line No.          ;SumIndexFields=Total Cost Amount,Total Hours,Total Sales Amount,Total Surcharge,Total Profit/Risk;
                                                   MaintainSIFTIndex=No;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100525000 : TextConst 'ENU=cannot be set if %1 is blank of ''Quantity of Call Line";NOR=Kan ikke settes hvis %1 er blank p† ''Antall av samtalerader''';

    PROCEDURE GetRate@1100525004(RateCode@1100525001 : Code[10];ReferenceDate@1100525002 : Date) : Decimal;
    VAR
      BudgetRate@1100525000 : Record 11012000;
    BEGIN
      BudgetRate.SETRANGE(Code, RateCode);
      BudgetRate.SETFILTER("Starting Date", '<=%1', ReferenceDate);
      // jhoek.281010.begin
      // BudgetRate.FINDLAST;
      IF NOT BudgetRate.FINDLAST THEN
        BudgetRate.INIT;
      // jhoek.281010.end

      EXIT(BudgetRate.Rate);
    END;

    PROCEDURE UpdateRate@1100525016();
    BEGIN
      VALIDATE(Rate, GetRate("Rate Code", "Price Reference Date"));
    END;

    PROCEDURE UpdateQuantity@1100525005();
    BEGIN
      CASE "Cost Driver" OF
        "Cost Driver"::" ": VALIDATE(Quantity, 0);
        "Cost Driver"::"Surface Area": VALIDATE(Quantity, "Qty. per Unit of Measure" * "Surface Area");
        "Cost Driver"::"Linear Meters": VALIDATE(Quantity, "Qty. per Unit of Measure" * "Linear Meters");
        "Cost Driver"::"Height Surcharge": VALIDATE(Quantity, "Qty. per Unit of Measure" * "Height Surcharge");
        "Cost Driver"::"No. of Glaziers": VALIDATE(Quantity, "Qty. per Unit of Measure" * "No. of Glaziers");
      END;
    END;

    PROCEDURE UpdateTotals@1100525001();
    BEGIN
      "Labor Amount" := Rate * Hours;
      "Material Amount" := "Material Price" * Quantity;
      "Subcontracting Amount" := "Subcontracting Price" * Quantity;
      "Plant Amount" := "Plant Price" * Quantity;

      "Cost Amount" := "Labor Amount" + "Material Amount" + "Subcontracting Amount" + "Plant Amount";
      Surcharge := ("Surcharge %" / 100) * "Cost Amount";
      "Profit/Risk" := ("Profit/Risk %" / 100) * "Cost Amount";
      "Sales Amount" := "Cost Amount" + Surcharge + "Profit/Risk";

      "Total Cost Amount" := "Activity Quantity" * "Cost Amount";
      "Total Surcharge" := "Activity Quantity" * Surcharge;
      "Total Profit/Risk" := "Activity Quantity" * "Profit/Risk";
      "Total Sales Amount" := "Activity Quantity" * "Sales Amount";
      "Total Hours" := "Activity Quantity" * Hours;
    END;

    PROCEDURE UpdateHours@1100525003();
    BEGIN
      VALIDATE(Hours, Quantity * Norm);
    END;

    PROCEDURE IsQuantityEditable@1100525000() : Boolean;
    BEGIN
      EXIT("Cost Driver" = "Cost Driver"::" ");
    END;

    BEGIN
    END.
  }
}

