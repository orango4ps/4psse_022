OBJECT Table 11229881 Plant Exceptional Sales Price
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF ("Employee Company" <> '') AND ("Employee No." = '') THEN
                 "Employee Company" := '';
             END;

    OnModify=BEGIN
               IF ("Employee Company" <> '') AND ("Employee No." = '') THEN
                 "Employee Company" := '';
             END;

    OnDelete=VAR
               PlantExceptSalesPriceDate@1100529000 : Record 11229882;
             BEGIN
               PlantExceptSalesPriceDate.SETRANGE(Level, Level);
               PlantExceptSalesPriceDate.DELETEALL;
             END;

    CaptionML=ENU=Plant Exceptional Sales Price;
    LookupPageID=Page11229855;
  }
  FIELDS
  {
    { 10  ;   ;Level               ;Integer       ;CaptionML=[ENU=Level;
                                                              NOR=Niv†;
                                                              SVE=Niv†];
                                                   NotBlank=Yes }
    { 20  ;   ;Employee Company    ;Text30        ;TableRelation=Company;
                                                   OnValidate=BEGIN
                                                                IF "Employee Company" = COMPANYNAME THEN
                                                                  "Employee Company" := '';
                                                                IF "Employee Company" <> xRec."Employee Company" THEN
                                                                  "Employee No." := '';
                                                              END;

                                                   CaptionML=[ENU=Employee Company;
                                                              NOR=Ansattes selskap;
                                                              SVE=Den anst„lldes f”retag] }
    { 25  ;   ;Employee No.        ;Code20        ;TableRelation=IF (Employee Company=FILTER('')) Employee;
                                                   OnValidate=VAR
                                                                Empl@1100529000 : Record 5200;
                                                              BEGIN
                                                                IF "Employee No." <> '' THEN BEGIN
                                                                  IF "Employee Company" <> '' THEN
                                                                    Empl.CHANGECOMPANY("Employee Company");
                                                                  Empl.GET("Employee No.");
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              Empl@1100529000 : Record 5200;
                                                            BEGIN
                                                              IF "Employee Company" <> '' THEN
                                                                Empl.CHANGECOMPANY("Employee Company");
                                                              Empl."No." := "Employee No.";
                                                              IF PAGE.RUNMODAL(0, Empl) = ACTION::LookupOK THEN
                                                                VALIDATE("Employee No.", Empl."No.");
                                                            END;

                                                   CaptionML=[ENU=Employee No.;
                                                              NOR=Ansattnr;
                                                              SVE=Anst„llningsnr] }
    { 30  ;   ;Trade Association   ;Code10        ;TableRelation="Trade Association";
                                                   CaptionML=[ENU=Trade Association;
                                                              NOR=Bransjeorganisasjon;
                                                              SVE=Branschorganisation] }
    { 40  ;   ;Department Code     ;Code20        ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   CaptionML=[ENU=Global Dimension 1 Code;
                                                              NOR=Avdelingskode;
                                                              SVE=Mottagande avdelningskod];
                                                   CaptionClass='1,1,1' }
    { 50  ;   ;Wage Component      ;Code10        ;TableRelation="Wage Component".Code WHERE (Component Type=FILTER(Hours));
                                                   CaptionML=[ENU=Wage Component;
                                                              NOR=L›nnsart;
                                                              SVE=L”neart] }
    { 60  ;   ;Plant Location      ;Code20        ;TableRelation="Plant Location";
                                                   CaptionML=[ENU=Plant Location;
                                                              NOR=Maskinplassering;
                                                              SVE=Maskinutplacering] }
    { 70  ;   ;Customer No.        ;Code20        ;TableRelation=Customer;
                                                   CaptionML=[ENU=Customer No.;
                                                              NOR=Kundenr;
                                                              SVE=Kundnr];
                                                   NotBlank=Yes }
    { 200 ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Date Filter;
                                                              NOR=Dato filter;
                                                              SVE=Datumfilter] }
  }
  KEYS
  {
    {    ;Level                                   ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE GetRate@1100485000(VAR PlantExceptionalSalesPrice@1100485000 : Record 11229881) : Decimal;
    VAR
      PlantExceptSalesPriceDate@1100529000 : Record 11229882;
      StartDate@1100485001 : Date;
    BEGIN
      PlantExceptSalesPriceDate.SETRANGE(Level, PlantExceptionalSalesPrice.Level);
      IF PlantExceptionalSalesPrice.GETFILTER("Date Filter") <> '' THEN
        StartDate := PlantExceptionalSalesPrice.GETRANGEMIN("Date Filter")
      ELSE
        StartDate := WORKDATE;
      PlantExceptSalesPriceDate.SETRANGE("Starting Date", 0D, StartDate);
      PlantExceptSalesPriceDate.SETFILTER("Ending Date",'%1|>=%2',0D, StartDate);
      IF PlantExceptSalesPriceDate.FINDLAST THEN
        EXIT(PlantExceptSalesPriceDate."Sales Price");
    END;

    BEGIN
    END.
  }
}

