OBJECT Codeunit 77780 TTStringManagement
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=TTool;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE SplitObjectAndCaption@1100526500(T1@1100526500 : Text[1024];VAR ObjectID@1100526501 : Code[1024];VAR CaptionText@1100526502 : Text[1024]);
    VAR
      SeparatorPos@1100526503 : Integer;
    BEGIN
      CLEAR(ObjectID);
      CLEAR(CaptionText);

      SeparatorPos := STRPOS(T1,ObjectCaptionSeparator);

      IF SeparatorPos = 0 THEN BEGIN
        ObjectID := T1;
        EXIT;
      END;

      ObjectID := COPYSTR(T1,1,SeparatorPos - 1);

      IF SeparatorPos = STRLEN(T1) THEN
        EXIT;

      CaptionText := COPYSTR(T1,SeparatorPos + 1,STRLEN(T1) - SeparatorPos);
    END;

    PROCEDURE GetSignatureAndValue@1100526504(ObjectID@1100526500 : Code[1024];VAR Signature@1100526501 : Code[10];VAR ValueString@1100526502 : Code[1024]);
    VAR
      DashPosition@1100526503 : Integer;
    BEGIN
      REPEAT
        DashPosition := STRPOS(ObjectID,ElementSeparator);
        Signature := Signature + COPYSTR(ObjectID,1,1);
        IF DashPosition = 0 THEN
          ValueString := ValueString + COPYSTR(ObjectID,2)
        ELSE BEGIN
          ValueString := ValueString + COPYSTR(ObjectID,2,DashPosition - 2) + OptionSeparator;
          ObjectID := COPYSTR(ObjectID,DashPosition + 1);
        END;
      UNTIL DashPosition = 0;
    END;

    PROCEDURE SelectString@1100526507(Position@1100526500 : Integer;ValueString@1100526501 : Text[1024]) : Text[1024];
    VAR
      SeparatorPosition@1100526502 : Integer;
    BEGIN
      IF Position <= 0 THEN
        EXIT('');

      WHILE Position > 1 DO BEGIN
        SeparatorPosition := STRPOS(ValueString,OptionSeparator);
        IF (SeparatorPosition = 0) OR (SeparatorPosition = STRLEN(ValueString)) THEN
          EXIT('');
        ValueString := COPYSTR(ValueString,SeparatorPosition + 1);
        Position := Position - 1;
      END;

      SeparatorPosition := STRPOS(ValueString,OptionSeparator);
      IF SeparatorPosition = 0 THEN
        EXIT(ValueString)
      ELSE
        EXIT(COPYSTR(ValueString,1,SeparatorPosition - 1));
    END;

    PROCEDURE AddElement@1100526508(VAR IDString@1100526502 : Code[1024];ElementType@1100526500 : Code[1];ElementValue@1100526501 : Code[1024]);
    BEGIN
      IF IDString = '' THEN
        IDString := ElementType + ElementValue
      ELSE
        IDString := IDString + ElementSeparator + ElementType + ElementValue;
    END;

    PROCEDURE ObjectCaptionSeparator@1100526501() : Text[1];
    BEGIN
      EXIT(':');
    END;

    PROCEDURE OptionSeparator@1100526503() : Text[1];
    BEGIN
      EXIT(',');
    END;

    PROCEDURE ElementSeparator@1100526505() : Text[1];
    BEGIN
      EXIT('-');
    END;

    BEGIN
    END.
  }
}

