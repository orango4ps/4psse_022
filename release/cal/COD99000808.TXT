OBJECT Codeunit 99000808 PlanningRoutingManagement
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.01;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Wiederkehrende Referenz in Zeile %1, wenn %2 berechnet wird. Berechnete Reihenfolge %3. Max. Reihenfolge %4.;ENU=Circular reference in line %1 when calculating %2. Counted sequences %3. Max. sequences %4.;NLD=Kringverwijzing in regel %1 bij %2 berekening. Getelde sequenties is %3. Max. aantal sequenties is %4.;NOR=Sirkelfereranse p† linje %1 i forbindelse med beregning av %2. Opptalte sekvenser %3. Maks. ant. sekvenser %4.;SVE=Cirkul„r referens i rad %1 vid ber„kning av %2. R„knade sekvenser %3. Max. sekvenser %4.';
      Text001@1001 : TextConst 'DEU=rckw„rts;ENU=back;NLD=achterwaartse;NOR=tilbake;SVE=Tillbaka';
      Text002@1002 : TextConst 'DEU=Aktuelle Anzahl der beendeten Durchl„ufe in Zeile %1 ist %2. Es sollte 1 sein. Prfen Sie %3.;ENU=Actual number of termination processes in line %1 is %2. They should be 1. Check %3.;NLD=Huidig aantal stopprocessen in regel %1 is %2 in plaats van 1. Controleer %3.;NOR=Faktisk antall avbrudd p† linje %1 er %2. Det skulle v‘rt 1. Kontroller %3.;SVE=Faktiskt antal avslutningsprocesser i rad %1 „r %2. Det borde vara 1. Kontrollera %3.';
      Text003@1003 : TextConst 'DEU=Aktuelle Anzahl der begonnenen Durchl„ufe in Zeile %1 ist %2. Es sollte 1 sein. Prfen Sie %3.;ENU=Actual number of start processes in line %1 is %2. They should be 1. Check %3.;NLD=Huidig aantal startprocessen in regel %1 is %2 in plaats van 1. Controleer %3.;NOR=Faktisk antall startprosedyrer p† linje %1 er %2. Det skulle v‘rt 1. Kontroller %3.;SVE=Faktiskt antal startprocesser i rad %1 „r %2. Det borde vara 1. Kontrollera %3.';
      Text004@1004 : TextConst 'DEU=Es sind nicht alle Arbeitsg„nge in der Zeile %1 rckw„rts berechnet. Prfen Sie %2.;ENU=Not all routing lines are sequenced backwards on line %1. Check %2.;NLD=Niet alle bewerkingsplanregels zijn achterwaarts doorgeschoven op regel %1. Controleer %2.;NOR=Ikke alle rutelinjene er i rekkef›lge bakover p† linje %1. Kontroller %2.;SVE=Inte alla oper.f”ljdsrader „r sekvenserade bak†t p† rad %1. Kontrollera %2.';
      NotAllRoutingLineSequencedForwardErr@1005 : TextConst '@@@="%1: Field(Line No.); %2 Text ErrList:";DEU=Es sind nicht alle Arbeitsg„nge in der Zeile %1 vorw„rts berechnet. Prfen Sie %2.;ENU=Not all routing lines are sequenced forward on line %1. Check the %2.;NLD=Niet alle bewerkingsplanregels zijn voorwaarts doorgeschoven op regel %1. Controleer %2.;NOR=Ikke alle rutelinjene er i rekkef›lge fremover p† linje %1. Kontroller %2.;SVE=Inte alla operationsf”ljdrader „r sekvenserade fram†t i operationsf”ljd %1. Kontrollera %2.';
      Text006@1006 : TextConst 'DEU=Vorhergehende Arbeitsg„nge fr %1 k”nnen nicht gefunden werden.;ENU=Previous operations for %1 cannot be found.;NLD=Kan vorige bewerkingen voor %1 niet vinden.;NOR=Finner ikke tidligere operasjoner for %1.;SVE=F”reg†ende operationer f”r %1 kan inte hittas.';
      Text007@1007 : TextConst 'DEU=Nachfolgende Arbeitsg„nge fr %1 k”nnen nicht gefunden werden.;ENU=Next operations for %1 cannot be found.;NLD=Kan volgende bewerkingen voor %1 niet vinden.;NOR=Finner ikke neste operasjoner for %1.;SVE=De n„sta operationerna f”r %1 kan inte hittas.';
      UOMMgt@1009 : Codeunit 5402;
      ErrList@1008 : Text[50];

    [External]
    PROCEDURE NeedsCalculation@8(WkShName@1000 : Code[10];BatchName@1001 : Code[10];LineNo@1002 : Integer) : Boolean;
    VAR
      PlanningRtngLine@1003 : Record 99000830;
    BEGIN
      PlanningRtngLine.SETRANGE("Worksheet Template Name",WkShName);
      PlanningRtngLine.SETRANGE("Worksheet Batch Name",BatchName);
      PlanningRtngLine.SETRANGE("Worksheet Line No.",LineNo);
      PlanningRtngLine.SETRANGE(Recalculate,TRUE);

      EXIT(PlanningRtngLine.FINDFIRST);
    END;

    LOCAL PROCEDURE ErrorInRouting@1(LineNo@1000 : Integer;Direction@1001 : Text[20];ActualSequence@1002 : Integer;MaxSequences@1003 : Integer);
    BEGIN
      ERROR(
        Text000,
        LineNo,Direction,ActualSequence,MaxSequences);
    END;

    LOCAL PROCEDURE InsertInErrList@10(PlanningRtngLine@1000 : Record 99000830);
    BEGIN
      IF (STRLEN(ErrList) + STRLEN(PlanningRtngLine."Operation No.") + 1) > MAXSTRLEN(ErrList) THEN
        EXIT;

      IF STRLEN(ErrList) > 0 THEN
        ErrList := ErrList + ',' + PlanningRtngLine."Operation No."
      ELSE
        ErrList := PlanningRtngLine."Operation No.";
    END;

    [External]
    PROCEDURE SetRtngLineSequenceBack@6(RoutingType@1000 : 'Serial,Parallel';PlanningRtngLine@1001 : Record 99000830;Maxsequences@1002 : Integer;Actsequences@1003 : Integer;TotalCalculation@1004 : Boolean);
    VAR
      PlanningRtngLine2@1005 : Record 99000830;
      SequenceNo@1006 : Integer;
    BEGIN
      IF RoutingType = RoutingType::Parallel THEN BEGIN
        IF (Actsequences - 1) > Maxsequences THEN
          ErrorInRouting(
            PlanningRtngLine."Worksheet Line No.",Text001,Actsequences,Maxsequences);

        IF TotalCalculation THEN
          PlanningRtngLine."Sequence No.(Backward)" := 1
        ELSE
          PlanningRtngLine."Sequence No. (Actual)" := 1;

        PlanningRtngLine2.SETRANGE("Worksheet Template Name",PlanningRtngLine."Worksheet Template Name");
        PlanningRtngLine2.SETRANGE("Worksheet Batch Name",PlanningRtngLine."Worksheet Batch Name");
        PlanningRtngLine2.SETRANGE("Worksheet Line No.",PlanningRtngLine."Worksheet Line No.");
        IF PlanningRtngLine."Next Operation No." <> '' THEN BEGIN
          PlanningRtngLine2.SETFILTER("Operation No.",PlanningRtngLine."Next Operation No.");
          IF PlanningRtngLine2.FIND('-') THEN
            REPEAT
              IF TotalCalculation THEN BEGIN
                IF (PlanningRtngLine2."Sequence No.(Backward)" + 1) > PlanningRtngLine."Sequence No.(Backward)" THEN
                  PlanningRtngLine."Sequence No.(Backward)" := PlanningRtngLine2."Sequence No.(Backward)" + 1;
              END ELSE BEGIN
                IF (PlanningRtngLine2."Sequence No. (Actual)" + 1) > PlanningRtngLine."Sequence No. (Actual)" THEN
                  PlanningRtngLine."Sequence No. (Actual)" := PlanningRtngLine2."Sequence No. (Actual)" + 1;
              END;
            UNTIL PlanningRtngLine2.NEXT = 0;
        END;
        PlanningRtngLine.MODIFY;

        IF PlanningRtngLine."Previous Operation No." <> '' THEN BEGIN
          PlanningRtngLine2.SETFILTER("Operation No.",PlanningRtngLine."Previous Operation No.");
          IF PlanningRtngLine2.FIND('-') THEN
            REPEAT
              SetRtngLineSequenceBack(
                RoutingType,
                PlanningRtngLine2,
                Maxsequences,
                Actsequences + 1,
                TotalCalculation);
            UNTIL PlanningRtngLine2.NEXT = 0;
        END;
      END ELSE BEGIN
        SequenceNo := 1;
        PlanningRtngLine2 := PlanningRtngLine;
        PlanningRtngLine2.SETRANGE("Worksheet Template Name",PlanningRtngLine."Worksheet Template Name");
        PlanningRtngLine2.SETRANGE("Worksheet Batch Name",PlanningRtngLine."Worksheet Batch Name");
        PlanningRtngLine2.SETRANGE("Worksheet Line No.",PlanningRtngLine."Worksheet Line No.");
        IF PlanningRtngLine2.FIND THEN
          REPEAT
            IF TotalCalculation THEN
              PlanningRtngLine2."Sequence No.(Backward)" := SequenceNo
            ELSE
              PlanningRtngLine2."Sequence No. (Actual)" := SequenceNo;
            PlanningRtngLine2.MODIFY;
            SequenceNo := SequenceNo + 1;
          UNTIL PlanningRtngLine2.NEXT(-1) = 0;
      END;
    END;

    [External]
    PROCEDURE SetRtngLineSequenceForward@7(RoutingType@1000 : 'Serial,Parallel';PlanningRtngLine@1001 : Record 99000830;MaxSequences@1002 : Integer;ActSequences@1003 : Integer;TotalCalculation@1004 : Boolean);
    VAR
      PlanningRtngLine2@1005 : Record 99000830;
      SequenceNo@1006 : Integer;
    BEGIN
      IF RoutingType = RoutingType::Parallel THEN BEGIN
        IF ActSequences > MaxSequences THEN
          ErrorInRouting(
            PlanningRtngLine."Worksheet Line No.",Text001,ActSequences,MaxSequences);

        IF TotalCalculation THEN
          PlanningRtngLine."Sequence No.(Forward)" := 1
        ELSE
          PlanningRtngLine."Sequence No. (Actual)" := 1;

        PlanningRtngLine2.SETRANGE("Worksheet Template Name",PlanningRtngLine."Worksheet Template Name");
        PlanningRtngLine2.SETRANGE("Worksheet Batch Name",PlanningRtngLine."Worksheet Batch Name");
        PlanningRtngLine2.SETRANGE("Worksheet Line No.",PlanningRtngLine."Worksheet Line No.");
        IF PlanningRtngLine."Previous Operation No." <> '' THEN BEGIN
          PlanningRtngLine2.SETFILTER("Operation No.",PlanningRtngLine."Previous Operation No.");
          IF PlanningRtngLine2.FIND('-') THEN
            REPEAT
              IF TotalCalculation THEN BEGIN
                IF (PlanningRtngLine2."Sequence No.(Forward)" + 1) > PlanningRtngLine."Sequence No.(Forward)" THEN
                  PlanningRtngLine."Sequence No.(Forward)" := PlanningRtngLine2."Sequence No.(Forward)" + 1;
              END ELSE BEGIN
                IF (PlanningRtngLine2."Sequence No. (Actual)" + 1) > PlanningRtngLine."Sequence No. (Actual)" THEN
                  PlanningRtngLine."Sequence No. (Actual)" := PlanningRtngLine2."Sequence No. (Actual)" + 1;
              END;
            UNTIL PlanningRtngLine2.NEXT = 0;
        END;
        PlanningRtngLine.MODIFY;

        IF PlanningRtngLine."Next Operation No." <> '' THEN BEGIN
          PlanningRtngLine2.SETFILTER("Operation No.",PlanningRtngLine."Next Operation No.");
          IF PlanningRtngLine2.FIND('-') THEN
            REPEAT
              SetRtngLineSequenceForward(
                RoutingType,
                PlanningRtngLine2,
                MaxSequences,
                ActSequences + 1,
                TotalCalculation);
            UNTIL PlanningRtngLine2.NEXT = 0;
        END;
      END ELSE BEGIN
        SequenceNo := 1;
        PlanningRtngLine2 := PlanningRtngLine;
        PlanningRtngLine2.SETRANGE("Worksheet Template Name",PlanningRtngLine."Worksheet Template Name");
        PlanningRtngLine2.SETRANGE("Worksheet Batch Name",PlanningRtngLine."Worksheet Batch Name");
        PlanningRtngLine2.SETRANGE("Worksheet Line No.",PlanningRtngLine."Worksheet Line No.");
        IF PlanningRtngLine2.FIND THEN
          REPEAT
            IF TotalCalculation THEN
              PlanningRtngLine2."Sequence No.(Forward)" := SequenceNo
            ELSE
              PlanningRtngLine2."Sequence No. (Actual)" := SequenceNo;
            PlanningRtngLine2.MODIFY;
            SequenceNo := SequenceNo + 1;
          UNTIL PlanningRtngLine2.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE CalcSequenceBack@2(ReqLine@1000 : Record 246);
    VAR
      PlanningRtngLine@1001 : Record 99000830;
      MaxSeq@1002 : Integer;
    BEGIN
      PlanningRtngLine.SETRANGE("Worksheet Template Name",ReqLine."Worksheet Template Name");
      PlanningRtngLine.SETRANGE("Worksheet Batch Name",ReqLine."Journal Batch Name");
      PlanningRtngLine.SETRANGE("Worksheet Line No.",ReqLine."Line No.");

      PlanningRtngLine.MODIFYALL("Sequence No.(Backward)",0);
      PlanningRtngLine.MODIFYALL("Fixed Scrap Qty. (Accum.)",0);
      PlanningRtngLine.MODIFYALL("Scrap Factor % (Accumulated)",0);

      MaxSeq := PlanningRtngLine.COUNT;

      PlanningRtngLine.SETFILTER("Next Operation No.",'%1','');
      PlanningRtngLine.FINDFIRST;
      SetRtngLineSequenceBack(ReqLine."Routing Type",PlanningRtngLine,MaxSeq,1,TRUE);
    END;

    LOCAL PROCEDURE CalcSequenceForward@3(ReqLine@1000 : Record 246);
    VAR
      PlanningRtngLine@1001 : Record 99000830;
      MaxSeq@1002 : Integer;
    BEGIN
      PlanningRtngLine.SETRANGE("Worksheet Template Name",ReqLine."Worksheet Template Name");
      PlanningRtngLine.SETRANGE("Worksheet Batch Name",ReqLine."Journal Batch Name");
      PlanningRtngLine.SETRANGE("Worksheet Line No.",ReqLine."Line No.");

      PlanningRtngLine.MODIFYALL("Sequence No.(Forward)",0);

      MaxSeq := PlanningRtngLine.COUNT;

      PlanningRtngLine.SETFILTER("Previous Operation No.",'%1','');
      PlanningRtngLine.FINDFIRST;
      SetRtngLineSequenceForward(ReqLine."Routing Type",PlanningRtngLine,MaxSeq,1,TRUE);
    END;

    [External]
    PROCEDURE CalcSequenceFromActual@9(PlanningRtngLine@1000 : Record 99000830;Direction@1001 : 'Forward,Backward';ReqLine@1002 : Record 246);
    VAR
      PlanningRtngLine2@1003 : Record 99000830;
      MaxSeq@1004 : Integer;
    BEGIN
      IF NeedsCalculation(
           ReqLine."Worksheet Template Name",
           ReqLine."Journal Batch Name",
           ReqLine."Line No.")
      THEN
        Calculate(ReqLine);

      PlanningRtngLine2.SETRANGE("Worksheet Template Name",ReqLine."Worksheet Template Name");
      PlanningRtngLine2.SETRANGE("Worksheet Batch Name",ReqLine."Journal Batch Name");
      PlanningRtngLine2.SETRANGE("Worksheet Line No.",ReqLine."Line No.");
      PlanningRtngLine2.MODIFYALL("Sequence No. (Actual)",0);

      MaxSeq := PlanningRtngLine2.COUNT;

      CASE Direction OF
        Direction::Forward:
          SetRtngLineSequenceForward(ReqLine."Routing Type",PlanningRtngLine,MaxSeq,1,FALSE);
        Direction::Backward:
          SetRtngLineSequenceBack(ReqLine."Routing Type",PlanningRtngLine,MaxSeq,1,FALSE);
      END;
    END;

    [External]
    PROCEDURE Calculate@4(ReqLine@1000 : Record 246);
    VAR
      PlanningRtngLine@1001 : Record 99000830;
      PlanningRtngLine2@1002 : Record 99000830;
      CalcScrapFactor@1003 : Decimal;
      CalcScrapQty@1004 : Decimal;
      ScrapFactorThis@1005 : Decimal;
      ScrapQtyThis@1006 : Decimal;
    BEGIN
      CalcSequenceBack(ReqLine);
      CalcSequenceForward(ReqLine);

      PlanningRtngLine.SETCURRENTKEY(
        "Worksheet Template Name",
        "Worksheet Batch Name",
        "Worksheet Line No.",
        "Sequence No.(Backward)");
      PlanningRtngLine.SETRANGE("Worksheet Template Name",ReqLine."Worksheet Template Name");
      PlanningRtngLine.SETRANGE("Worksheet Batch Name",ReqLine."Journal Batch Name");
      PlanningRtngLine.SETRANGE("Worksheet Line No.",ReqLine."Line No.");
      IF PlanningRtngLine.FIND('-') THEN
        REPEAT
          IF PlanningRtngLine."Next Operation No." <> '' THEN BEGIN
            PlanningRtngLine2.SETRANGE("Worksheet Template Name",ReqLine."Worksheet Template Name");
            PlanningRtngLine2.SETRANGE("Worksheet Batch Name",ReqLine."Journal Batch Name");
            PlanningRtngLine2.SETRANGE("Worksheet Line No.",ReqLine."Line No.");
            PlanningRtngLine2.SETFILTER("Operation No.",PlanningRtngLine."Next Operation No.");
            CalcScrapFactor := 0;
            CalcScrapQty := 0;
            IF PlanningRtngLine2.FIND('-') THEN
              REPEAT
                ScrapFactorThis :=
                  PlanningRtngLine2."Scrap Factor % (Accumulated)";
                ScrapQtyThis :=
                  PlanningRtngLine2."Fixed Scrap Qty. (Accum.)";
                CalcScrapFactor := CalcScrapFactor + ScrapFactorThis;
                CalcScrapQty := CalcScrapQty + ScrapQtyThis;
              UNTIL PlanningRtngLine2.NEXT = 0;
          END;
          IF CalcScrapFactor <> 0 THEN BEGIN
            IF PlanningRtngLine."Scrap Factor %" <> 0 THEN
              CalcScrapFactor :=
                ROUND((1 + CalcScrapFactor) *
                  (1 + PlanningRtngLine."Scrap Factor %" / 100),UOMMgt.QtyRndPrecision) - 1;
          END ELSE
            CalcScrapFactor :=
              ROUND(1 + PlanningRtngLine."Scrap Factor %" / 100,UOMMgt.QtyRndPrecision) - 1;
          CalcScrapQty := CalcScrapQty + PlanningRtngLine."Fixed Scrap Quantity";
          PlanningRtngLine."Fixed Scrap Qty. (Accum.)" := CalcScrapQty;
          PlanningRtngLine."Scrap Factor % (Accumulated)" := CalcScrapFactor;
          PlanningRtngLine.MODIFY;
        UNTIL PlanningRtngLine.NEXT = 0;

      PlanningRtngLine.MODIFYALL(Recalculate,FALSE);
      Check(ReqLine);
    END;

    LOCAL PROCEDURE Check@5(ReqLine@1000 : Record 246);
    VAR
      PlanningRtngLine@1001 : Record 99000830;
      PlanningRtngLine2@1002 : Record 99000830;
      NoOfProcesses@1003 : Integer;
    BEGIN
      PlanningRtngLine.SETRANGE("Worksheet Template Name",ReqLine."Worksheet Template Name");
      PlanningRtngLine.SETRANGE("Worksheet Batch Name",ReqLine."Journal Batch Name");
      PlanningRtngLine.SETRANGE("Worksheet Line No.",ReqLine."Line No.");

      PlanningRtngLine.SETFILTER("Next Operation No.",'%1','');

      NoOfProcesses := PlanningRtngLine.COUNT;
      IF NoOfProcesses <> 1 THEN BEGIN
        REPEAT
          InsertInErrList(PlanningRtngLine);
        UNTIL PlanningRtngLine.NEXT = 0;
        ERROR(
          Text002,
          ReqLine."Line No.",
          NoOfProcesses,
          ErrList);
      END;

      PlanningRtngLine.SETFILTER("Previous Operation No.",'%1','');
      PlanningRtngLine.SETRANGE("Next Operation No.");
      NoOfProcesses := PlanningRtngLine.COUNT;
      IF NoOfProcesses <> 1 THEN BEGIN
        REPEAT
          InsertInErrList(PlanningRtngLine);
        UNTIL PlanningRtngLine.NEXT = 0;
        ERROR(
          Text003,
          ReqLine."Line No.",
          NoOfProcesses,
          ErrList);
      END;
      PlanningRtngLine.SETRANGE("Previous Operation No.");

      PlanningRtngLine.SETRANGE("Sequence No.(Backward)",0);
      IF PlanningRtngLine.FIND('-') THEN BEGIN
        REPEAT
          InsertInErrList(PlanningRtngLine);
        UNTIL PlanningRtngLine.NEXT = 0;
        ERROR(
          Text004,
          ReqLine."Line No.",
          ErrList);
      END;
      PlanningRtngLine.SETRANGE("Sequence No.(Backward)");

      PlanningRtngLine.SETRANGE("Sequence No.(Forward)",0);
      IF PlanningRtngLine.FIND('-') THEN BEGIN
        REPEAT
          InsertInErrList(PlanningRtngLine);
        UNTIL PlanningRtngLine.NEXT = 0;
        ERROR(
          NotAllRoutingLineSequencedForwardErr,
          ReqLine."Line No.",
          ErrList);
      END;
      PlanningRtngLine.SETRANGE("Sequence No.(Forward)");

      PlanningRtngLine.SETCURRENTKEY(
        "Worksheet Template Name",
        "Worksheet Batch Name",
        "Worksheet Line No.",
        "Sequence No.(Backward)");
      PlanningRtngLine.SETFILTER("Previous Operation No.",'<>%1','');

      IF PlanningRtngLine.FIND('-') THEN
        REPEAT
          PlanningRtngLine2.SETRANGE("Worksheet Template Name",ReqLine."Worksheet Template Name");
          PlanningRtngLine2.SETRANGE("Worksheet Batch Name",ReqLine."Journal Batch Name");
          PlanningRtngLine2.SETRANGE("Worksheet Line No.",ReqLine."Line No.");
          PlanningRtngLine2.SETFILTER("Operation No.",PlanningRtngLine."Previous Operation No.");
          IF PlanningRtngLine2.ISEMPTY THEN
            ERROR(
              Text006,
              PlanningRtngLine."Operation No.");
        UNTIL PlanningRtngLine.NEXT = 0;

      PlanningRtngLine.SETCURRENTKEY(
        "Worksheet Template Name",
        "Worksheet Batch Name",
        "Worksheet Line No.",
        "Sequence No.(Backward)");

      PlanningRtngLine.SETFILTER("Next Operation No.",'<>%1','');
      PlanningRtngLine.SETRANGE("Operation No.");

      IF PlanningRtngLine.FIND('-') THEN
        REPEAT
          PlanningRtngLine2.SETRANGE("Worksheet Template Name",ReqLine."Worksheet Template Name");
          PlanningRtngLine2.SETRANGE("Worksheet Batch Name",ReqLine."Journal Batch Name");
          PlanningRtngLine2.SETRANGE("Worksheet Line No.",ReqLine."Line No.");
          PlanningRtngLine2.SETFILTER("Operation No.",PlanningRtngLine."Next Operation No.");
          IF PlanningRtngLine2.ISEMPTY THEN
            ERROR(Text007,PlanningRtngLine."Worksheet Line No.");
        UNTIL PlanningRtngLine.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

