OBJECT Table 8619 Config. Template Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ConfigTemplateHeader@1000 : Record 8618;
             BEGIN
               IF ConfigTemplateHeader.GET("Data Template Code") THEN BEGIN
                 "Table ID" := ConfigTemplateHeader."Table ID";
                 IF "Language ID" = 0 THEN
                   "Language ID" := GLOBALLANGUAGE;
               END;
             END;

    ReplicateData=No;
    CaptionML=[ENU=Config. Template Line;
               NOR=Konfig.mallinje];
  }
  FIELDS
  {
    { 1   ;   ;Data Template Code  ;Code10        ;TableRelation="Config. Template Header";
                                                   CaptionML=[ENU=Data Template Code;
                                                              NOR=Datamalkode];
                                                   Editable=No }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr];
                                                   Editable=No }
    { 3   ;   ;Type                ;Option        ;InitValue=Field;
                                                   OnValidate=BEGIN
                                                                CASE Type OF
                                                                  Type::Field:
                                                                    CLEAR("Template Code");
                                                                  Type::Template:
                                                                    BEGIN
                                                                      CLEAR("Field Name");
                                                                      CLEAR("Field ID");
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              NOR=Type;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=[ENU=Field,Template,Related Template;
                                                                    NOR=Felt,Mal,Relatert mal];
                                                   OptionString=Field,Template,Related Template }
    { 4   ;   ;Field ID            ;Integer       ;TableRelation=IF (Type=CONST(Field)) Field.No. WHERE (TableNo=FIELD(Table ID),
                                                                                                         Class=CONST(Normal));
                                                   CaptionML=[ENU=Field ID;
                                                              NOR=Felt-ID;
                                                              SVE=F„lt-ID] }
    { 5   ;   ;Field Name          ;Text30        ;FieldClass=Normal;
                                                   OnValidate=VAR
                                                                ConfigTemplateLine@1000 : Record 8619;
                                                                ConfigTemplateMgt@1001 : Codeunit 8612;
                                                              BEGIN
                                                                ConfigTemplateLine.SETRANGE("Data Template Code","Data Template Code");
                                                                ConfigTemplateLine.SETRANGE("Field Name","Field Name");
                                                                IF NOT ConfigTemplateLine.ISEMPTY THEN
                                                                  ERROR(Text004,"Field Name");

                                                                ConfigTemplateMgt.TestHierarchy(Rec);
                                                              END;

                                                   OnLookup=BEGIN
                                                              SelectFieldName;
                                                            END;

                                                   CaptionML=[ENU=Field Name;
                                                              NOR=Feltnavn;
                                                              SVE=F„ltnamn];
                                                   Editable=No }
    { 6   ;   ;Table ID            ;Integer       ;CaptionML=[ENU=Table ID;
                                                              NOR=Tabell-ID;
                                                              SVE=Tabell-ID] }
    { 7   ;   ;Table Name          ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Name" WHERE (Object Type=FILTER(Table),
                                                                                                             Object ID=FIELD(Table ID)));
                                                   CaptionML=[ENU=Table Name;
                                                              NOR=Tabellnavn;
                                                              SVE=Tabellnamn];
                                                   Editable=No }
    { 8   ;   ;Template Code       ;Code10        ;TableRelation="Config. Template Header";
                                                   OnValidate=VAR
                                                                ConfigTemplateLine@1003 : Record 8619;
                                                                ConfigTemplateMgt@1002 : Codeunit 8612;
                                                              BEGIN
                                                                IF Type = Type::Field THEN
                                                                  ERROR(Text005);

                                                                IF "Template Code" = "Data Template Code" THEN
                                                                  ERROR(Text000);

                                                                ConfigTemplateMgt.TestHierarchy(Rec);

                                                                ConfigTemplateLine.SETRANGE("Data Template Code","Data Template Code");
                                                                ConfigTemplateLine.SETRANGE("Template Code","Template Code");
                                                                IF NOT ConfigTemplateLine.ISEMPTY THEN
                                                                  ERROR(Text003,"Template Code");
                                                              END;

                                                   OnLookup=VAR
                                                              ConfigTemplateHeader@1002 : Record 8618;
                                                              ConfigTemplateList@1000 : Page 8620;
                                                            BEGIN
                                                              IF Type = Type::Field THEN
                                                                EXIT;

                                                              ConfigTemplateList.LOOKUPMODE := TRUE;
                                                              ConfigTemplateList.EDITABLE := FALSE;
                                                              IF ConfigTemplateList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                ConfigTemplateList.GETRECORD(ConfigTemplateHeader);
                                                                IF ConfigTemplateHeader.Code = "Data Template Code" THEN
                                                                  ERROR(Text000);
                                                                CALCFIELDS("Template Description");
                                                                VALIDATE("Template Code",ConfigTemplateHeader.Code);
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Template Code;
                                                              NOR=Malkode;
                                                              SVE=Mallkod] }
    { 9   ;   ;Template Description;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Config. Template Header".Description WHERE (Code=FIELD(Data Template Code)));
                                                   CaptionML=[ENU=Template Description;
                                                              NOR=Malbeskrivelse;
                                                              SVE=Mallbeskrivning];
                                                   Editable=No }
    { 10  ;   ;Mandatory           ;Boolean       ;OnValidate=BEGIN
                                                                IF Mandatory AND ("Default Value" = '') THEN
                                                                  ERROR(EmptyDefaultValueErr);
                                                              END;

                                                   CaptionML=[ENU=Mandatory;
                                                              NOR=Obligatorisk;
                                                              SVE=Obligatoriskt] }
    { 11  ;   ;Reference           ;Text250       ;ExtendedDatatype=URL;
                                                   CaptionML=[ENU=Reference;
                                                              NOR=Referanse;
                                                              SVE=Referens] }
    { 12  ;   ;Default Value       ;Text250       ;OnValidate=VAR
                                                                TempConfigPackageField@1005 : TEMPORARY Record 8616;
                                                                ConfigPackageManagement@1004 : Codeunit 8611;
                                                                ConfigValidateMgt@1003 : Codeunit 8617;
                                                                RecRef@1002 : RecordRef;
                                                                FieldRef@1001 : FieldRef;
                                                                ValidationError@1000 : Text[250];
                                                              BEGIN
                                                                IF Mandatory AND ("Default Value" = '') THEN
                                                                  ERROR(EmptyDefaultValueErr);
                                                                IF ("Field ID" <> 0) AND ("Default Value" <> '') THEN BEGIN
                                                                  RecRef.OPEN("Table ID",TRUE);
                                                                  FieldRef := RecRef.FIELD("Field ID");
                                                                  ValidationError := ConfigValidateMgt.EvaluateValue(FieldRef,"Default Value",FALSE);
                                                                  IF ValidationError <> '' THEN
                                                                    ERROR(ValidationError);

                                                                  "Default Value" := FORMAT(FieldRef.VALUE);

                                                                  IF NOT "Skip Relation Check" THEN BEGIN
                                                                    ConfigPackageManagement.GetFieldsOrder(RecRef,'',TempConfigPackageField);
                                                                    ConfigValidateMgt.TransferRecordDefaultValues("Data Template Code",RecRef,"Field ID","Default Value");
                                                                    ValidationError := ConfigValidateMgt.ValidateFieldRefRelationAgainstCompanyData(FieldRef,TempConfigPackageField);

                                                                    IF ValidationError <> '' THEN
                                                                      ERROR(ValidationError);
                                                                  END;

                                                                  IF GLOBALLANGUAGE <> "Language ID" THEN
                                                                    VALIDATE("Language ID",GLOBALLANGUAGE);
                                                                END
                                                              END;

                                                   CaptionML=[ENU=Default Value;
                                                              NOR=Standardverdi;
                                                              SVE=Standardv„rde] }
    { 13  ;   ;Table Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=FILTER(Table),
                                                                                                                Object ID=FIELD(Table ID)));
                                                   CaptionML=[ENU=Table Caption;
                                                              NOR=Tabelloverskrift;
                                                              SVE=Tabellrubrik];
                                                   Editable=No }
    { 14  ;   ;Field Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Table ID),
                                                                                                   No.=FIELD(Field ID)));
                                                   CaptionML=[ENU=Field Caption;
                                                              NOR=Felttekst;
                                                              SVE=F„ltrubrik];
                                                   Editable=No }
    { 15  ;   ;Skip Relation Check ;Boolean       ;CaptionML=[ENU=Skip Relation Check;
                                                              NOR=Hopp over relasjonskontroll] }
    { 16  ;   ;Language ID         ;Integer       ;InitValue=0;
                                                   CaptionML=[ENU=Language ID;
                                                              NOR=Spr†k-ID;
                                                              SVE=Spr†k-ID] }
  }
  KEYS
  {
    {    ;Data Template Code,Line No.             ;Clustered=Yes }
    {    ;Data Template Code,Type                  }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=A template cannot relate to itself. Specify a different template.;NOR=En mal kan ikke v‘re knyttet til seg selv. Angi en annen mal.';
      Text003@1003 : TextConst 'ENU=The template %1 is already in this hierarchy.;NOR=Malen %1 er allerede i dette hierarkiet.';
      Text004@1004 : TextConst 'ENU=Field %1 is already in the template.;NOR=Feltet %1 er allerede i malen.';
      Text005@1005 : TextConst 'ENU=The template line cannot be edited if type is Field.;NOR=Mallinjen kan ikke redigeres hvis linjetypen er Felt.';
      EmptyDefaultValueErr@1001 : TextConst 'ENU=The Default Value field must be filled in if the Mandatory check box is selected.;NOR=Feltet Standardverdi m† fylles ut hvis det er merket av for Obligatorisk.';

    [External]
    PROCEDURE SelectFieldName@2();
    VAR
      ConfigTemplateHeader@1003 : Record 8618;
      Field@1002 : Record 2000000041;
      ConfigPackageMgt@1000 : Codeunit 8611;
      FieldList@1001 : Page 6218;
    BEGIN
      IF Type = Type::Template THEN
        EXIT;

      ConfigTemplateHeader.GET("Data Template Code");

      IF ConfigTemplateHeader."Table ID" = 0 THEN
        EXIT;

      CLEAR(FieldList);
      ConfigPackageMgt.SetFieldFilter(Field,ConfigTemplateHeader."Table ID",0);
      FieldList.SETTABLEVIEW(Field);
      FieldList.LOOKUPMODE := TRUE;
      IF FieldList.RUNMODAL = ACTION::LookupOK THEN BEGIN
        FieldList.GETRECORD(Field);
        "Table ID" := Field.TableNo;
        VALIDATE("Field ID",Field."No.");
        VALIDATE("Field Name",Field.FieldName);
      END;
    END;

    [External]
    PROCEDURE GetLine@1(VAR ConfigTemplateLine@1000 : Record 8619;DataTemplateCode@1001 : Code[10];FieldID@1002 : Integer) : Boolean;
    BEGIN
      ConfigTemplateLine.SETRANGE("Data Template Code",DataTemplateCode);
      ConfigTemplateLine.SETRANGE("Field ID",FieldID);
      IF NOT ConfigTemplateLine.FINDFIRST THEN
        EXIT(FALSE);
      EXIT(TRUE)
    END;

    BEGIN
    END.
  }
}

