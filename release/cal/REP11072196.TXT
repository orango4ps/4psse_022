OBJECT Report 11072196 INSBOU Send Maint Status
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=INSBOU Wartungsstatus senden;
               ENU=INSBOU Send Maintenance Status;
               NLD=Onderhoudsstatus versturen];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   Date := WORKDATE;
                 END;

  }
  DATASET
  {
    { 1100528800;;DataItem;                  ;
               DataItemTable=Table11012823;
               OnAfterGetRecord=VAR
                                  CustomerINSBOULogin@1100528813 : Record 11229341;
                                  CompanyInformation@1100528831 : Record 79;
                                  Customer@1100528830 : Record 18;
                                  TempBlob@1100528812 : Record 99008535;
                                  FileManagement@1100528811 : Codeunit 419;
                                  ConsumedWebServiceMgt@1100528810 : Codeunit 11229310;
                                  INSBOUMaintStatusWSv005@1100528809 : XMLport 11072013;
                                  ClientTempFileName@1100528807 : Text;
                                  Ostream@1100528804 : OutStream;
                                  MaintStatusXML@1100528803 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
                                  BuyerNode@1100528829 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
                                  ContractorNode@1100528832 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
                                  FoundNode@1100528814 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
                                  InstructionDataNode@1100528822 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
                                  AttachmentNode@1100528819 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
                                  InstructionLineNode@1100528815 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
                                  XmlDeclareNode@1100528800 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
                                  base64EncodedContents@1100528816 : Text;
                                  errorMessage@1100528817 : Text;
                                  ServerFileName@1100528818 : Text;
                                  DeliveryTimeFrameNode@1100525001 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
                                  MaintenanceStatusRootNode@1100525002 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
                                  MsgType@1100525000 : Text[20];
                                BEGIN
                                  SendStatusDescription := SendStatus;

                                  CustomerINSBOULogin.GET("Service Order"."Customer No.", CustomerINSBOULogin.Type::MTNSTA); //2 = "Maint.Status (INSBOU)"
                                  CustomerINSBOULogin.TESTFIELD(URL);
                                  CustomerINSBOULogin.TESTFIELD("User Name");
                                  CustomerINSBOULogin.TESTFIELD(Password);
                                  ConsumedWebService.GET(CustomerINSBOULogin."Consumed Web Service Code");
                                  ConsumedWebServiceLine.GET(ConsumedWebService.Code, 40);

                                  TempBlob.Blob.CREATEOUTSTREAM(Ostream);
                                  INSBOUMaintStatusWSv005.SETDESTINATION(Ostream);
                                  INSBOUMaintStatusWSv005.EXPORT;
                                  ClientTempFileName := FileManagement.ClientTempFileName('xml');
                                  FileManagement.BLOBExport4PS(TempBlob, ClientTempFileName, FALSE, TRUE);
                                  IF (NOT ISNULL(MaintStatusXML)) THEN
                                    CLEAR(MaintStatusXML);
                                  MaintStatusXML := MaintStatusXML.XmlDocument;
                                  XMLDOMManagement4PS.LoadXMLFromClientFile(MaintStatusXML, ClientTempFileName, TRUE);

                                  CASE CustomerINSBOULogin."MessageService Version" OF
                                    CustomerINSBOULogin."MessageService Version"::"3.0",
                                    CustomerINSBOULogin."MessageService Version"::"3.1":
                                      MaintStatusXML.DocumentElement.SetAttribute('xmlns', 'http://www.ketenstandaard.nl/onderhoudstatus/SALES/005');
                                  END;
                                  MaintStatusXML.DocumentElement.SetAttribute('xmlns:xs', 'http://www.w3.org/2001/XMLSchema');
                                  MaintStatusXML.DocumentElement.SetAttribute('xmlns:xsi', 'http://www.w3.org/2001/XMLSchema-instance');

                                  IF FilePath <> '' THEN BEGIN
                                    ServerFileName := FileManagement.UploadFileSilent(FilePath);
                                    DocumentManagementWebservice.GetFileAsBase64(ServerFileName, base64EncodedContents, errorMessage);
                                    FileManagement.DeleteServerFile(ServerFileName);
                                  END;

                                  XMLDOMManagement4PS.FindNode(MaintStatusXML, STRSUBSTNO(LocalName, 'MaintenanceStatus'), MaintenanceStatusRootNode);
                                  XMLDOMManagement4PS.FindNode(MaintStatusXML, STRSUBSTNO(LocalName, 'InstructionData'), InstructionDataNode);
                                  XMLDOMManagement4PS.FindNode(MaintenanceStatusRootNode, STRSUBSTNO(LocalName, 'Attachment'), AttachmentNode);
                                  XMLDOMManagement4PS.FindNode(MaintenanceStatusRootNode, STRSUBSTNO(LocalName, 'InstructionLine'), InstructionLineNode);
                                  XMLDOMManagement4PS.FindNode(MaintenanceStatusRootNode, STRSUBSTNO(LocalName, 'Buyer'), BuyerNode);
                                  XMLDOMManagement4PS.FindNode(MaintenanceStatusRootNode, STRSUBSTNO(LocalName, 'Contractor'), ContractorNode);
                                  XMLDOMManagement4PS.FindNode(InstructionDataNode, STRSUBSTNO(LocalName, 'DeliveryTimeFrame'), DeliveryTimeFrameNode);

                                  Customer.GET("Service Order"."Customer No.");
                                  SetValue(BuyerNode, 'GLN', Customer.GLN);

                                  CompanyInformation.GET;
                                  SetValue(ContractorNode, 'GLN', CompanyInformation.GLN);

                                  SetValue(MaintenanceStatusRootNode, 'MessageDate', FORMAT(TODAY,0,9));

                                  CASE CustomerINSBOULogin."MessageService Version" OF
                                    CustomerINSBOULogin."MessageService Version"::" ",
                                    CustomerINSBOULogin."MessageService Version"::"2.4": BEGIN
                                      SetValue(MaintenanceStatusRootNode, 'MessageNumber', STRSUBSTNO('%1%2%3', '4PS', FORMAT(TODAY), FORMAT(TIME)));
                                      SetValue(MaintenanceStatusRootNode, 'MessageTime', FORMAT(TIME,5));
                                    END;
                                    CustomerINSBOULogin."MessageService Version"::"3.0",
                                    CustomerINSBOULogin."MessageService Version"::"3.1": BEGIN
                                      SetValue(MaintenanceStatusRootNode, 'MessageNumber', COPYSTR(CREATEGUID(),1,17));
                                      SetValue(MaintenanceStatusRootNode, 'MessageTime', FORMAT(TIME,0,9));
                                    END;
                                  END;

                                  SetValue(InstructionDataNode, 'InstructionNumber', "Service Order"."Order No. Customer");
                                  SetValue(InstructionDataNode, 'InstructionSubNumber', "Service Order"."Your Reference");
                                  SetValue(InstructionDataNode, 'Status', STRSUBSTNO('%1',SendStatus));
                                  SetValue(InstructionDataNode, 'StatusDescription', STRSUBSTNO('%1',SendStatusDescription));
                                  SetValue(InstructionDataNode, 'DateReady', FORMAT(Date, 0,9));

                                  IF SendStatus = SendStatus::AFH THEN BEGIN
                                    SetValue(DeliveryTimeFrameNode, 'DeliveryDateEarliest', FORMAT("Service Order"."Starting Date",0,9));
                                    SetValue(DeliveryTimeFrameNode, 'DeliveryTimeEarliest', FORMAT("Service Order"."Starting Time",5));
                                    SetValue(DeliveryTimeFrameNode, 'DeliveryDateLatest', FORMAT("Service Order"."Ending Date",0,9));
                                    CASE CustomerINSBOULogin."MessageService Version" OF
                                      CustomerINSBOULogin."MessageService Version"::" ",
                                      CustomerINSBOULogin."MessageService Version"::"2.4": BEGIN
                                        SetValue(DeliveryTimeFrameNode, 'DeliveryTimeEarliest', FORMAT("Service Order"."Starting Time",5));
                                        SetValue(DeliveryTimeFrameNode, 'DeliveryTimeLatest', FORMAT("Service Order"."Ending Time",5));
                                      END;
                                      CustomerINSBOULogin."MessageService Version"::"3.0",
                                      CustomerINSBOULogin."MessageService Version"::"3.1": BEGIN
                                        SetValue(DeliveryTimeFrameNode, 'DeliveryTimeEarliest', FORMAT("Service Order"."Starting Time",0,9));
                                        SetValue(DeliveryTimeFrameNode, 'DeliveryTimeLatest', FORMAT("Service Order"."Ending Time",0,9));
                                      END;
                                    END;
                                  END;

                                  IF base64EncodedContents <> '' THEN BEGIN
                                    SetValue(AttachmentNode, 'AttachedData', base64EncodedContents);
                                    SetValue(AttachmentNode, 'DocumentType', 'OTD');
                                    SetValue(AttachmentNode, 'FileType', FileManagement.GetExtension(ShortFileName));
                                    SetValue(AttachmentNode, 'FileName', ShortFileName);

                                    SetValue(InstructionLineNode, 'LineNumber', '1');
                                    SetValue(InstructionLineNode, 'Quantity', '1');
                                    SetValue(InstructionLineNode, 'MeasurementUnitQuantity', '1');
                                    SetValue(InstructionLineNode, 'Status', STRSUBSTNO('%1',SendStatus));
                                  END;

                                  XmlDeclareNode := MaintStatusXML.FirstChild;
                                  MaintStatusXML.RemoveChild(XmlDeclareNode);

                                  CASE CustomerINSBOULogin."MessageService Version" OF
                                    CustomerINSBOULogin."MessageService Version"::" ",
                                    CustomerINSBOULogin."MessageService Version"::"2.4": BEGIN
                                      MsgType := 'MaintenanceStatus';
                                    END;
                                    CustomerINSBOULogin."MessageService Version"::"3.0",
                                    CustomerINSBOULogin."MessageService Version"::"3.1": BEGIN
                                      MsgType := 'MTNSTA';
                                    END;
                                  END;

                                  ConsumedWebServiceMgt.CreateRequest(ConsumedWebService.Code, ConsumedWebServiceLine."Seq. No.", RequestXML, RequestNamespaceManager);
                                  FillSOAPHeaderWithActualData(RequestXML,STRSUBSTNO('%1',CustomerINSBOULogin."MessageService Version"));

                                  CASE CustomerINSBOULogin."Version OLD" OF
                                    '005': CreatePostMessageRequest(MsgType, CustomerINSBOULogin);
                                  ELSE
                                    ERROR(Text008, CustomerINSBOULogin.FIELDCAPTION("Version OLD"), CustomerINSBOULogin."Version OLD", MaintStatus);
                                  END;

                                  XMLDOMManagement4PS.FindNode(RequestXML, STRSUBSTNO(LocalName, 'MsgContent'), FoundNode);
                                  CASE CustomerINSBOULogin."MessageService Version" OF
                                    CustomerINSBOULogin."MessageService Version"::" ",
                                    CustomerINSBOULogin."MessageService Version"::"2.4": BEGIN
                                      FoundNode.InnerXml := MaintStatusXML.OuterXml;
                                    END;
                                    CustomerINSBOULogin."MessageService Version"::"3.0",
                                    CustomerINSBOULogin."MessageService Version"::"3.1": BEGIN
                                      XMLDOMManagement4PS.RemoveEmptyTags(MaintStatusXML);
                                      FoundNode.InnerXml := STRSUBSTNO('<![CDATA[%1]]>', MaintStatusXML.OuterXml);
                                    END;
                                  END;

                                  XMLDOMManagement4PS.RemoveEmptyTags(RequestXML);

                                  ConsumedWebServiceMgt.SendCWS(ConsumedWebService.Code, ConsumedWebServiceLine."Seq. No.", RequestXML, ResponseXML,
                                  ResponseNamespaceManager, 'Onderhoudsbericht');
                                  CheckResponseSoapFault;

                                  MESSAGE(MaintStatusSendRefPointChanged, "Service Order".TABLECAPTION, "Service Order"."No.");
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100528800;;Container;
                  ContainerType=ContentArea }

      { 1100528804;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100528801;2;Field  ;
                  CaptionML=[DEU=Status;
                             ENU=Status;
                             NLD=Status;
                             NOR=Status;
                             SVE=Status];
                  OptionCaptionML=[DEU=Akzeptieren,Termin Mieter,Auf Antwort des Supervisors warten,Wetterbedingtes Warten,Anweisung storniert,Bewohner ruft zurÅck,Bewohner nicht zu Hause,bereit,Fakturierbar,in Bearbeitung,VerlÑngerung,Ablehnen;
                                   ENU=Accept,Appointment tenant,Wait response supervisor,Wait in regard to weather,Instruction canceled,Resident calls back,Resident not at home,Ready,Billable,In progress,Prolongation,Refuse;
                                   NLD=Accepteren,Afspraak huurder,Afwachten reactie opzichter,Afwachten in verband met weer,Opdracht geannuleerd,Bewoner belt terug,Bewoner niet thuis,Gereedmelding,Te factureren,In uitvoering,Verlenging,Weigeren];
                  SourceExpr=SendStatus }

      { 1100528802;2;Field  ;
                  CaptionML=[DEU=Datum;
                             ENU=Date;
                             NLD=Datum;
                             NOR=Dato;
                             SVE=Datum];
                  SourceExpr=Date }

      { 1100528803;2;Field  ;
                  AssistEdit=Yes;
                  CaptionML=[DEU=Datei;
                             ENU=Bestand;
                             NLD=Bestand];
                  SourceExpr=FilePath;
                  OnAssistEdit=BEGIN
                                 IF FileManagement.SelectFile('*',DirectoryNameWithBackSlash,ShortFileName,FileAbsPathName) THEN
                                   FilePath := FileAbsPathName;
                               END;
                                }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ConsumedWebService@1100528817 : Record 11229797;
      ConsumedWebServiceLine@1100528816 : Record 11229798;
      XMLDOMManagement4PS@1100528807 : Codeunit 11020220;
      FileManagement@1100528803 : Codeunit 419;
      DocumentManagementWebservice@1100528809 : Codeunit 11012405;
      DirectoryNameWithBackSlash@1100528804 : Text;
      ShortFileName@1100528805 : Text;
      FileAbsPathName@1100528806 : Text;
      SendStatus@1100528800 : 'ACC,AFH,AFR,AFW,ANN,BBT,BNT,GER,TFC,UIT,VER,WEI';
      Date@1100528801 : Date;
      FilePath@1100528802 : Text;
      RequestNamespaceManager@1100528812 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNamespaceManager";
      ResponseNamespaceManager@1100528810 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNamespaceManager";
      RequestXML@1100528813 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      ResponseXML@1100528811 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      LocalName@1100528814 : TextConst 'DEU=".//*[local-name()=""%1""]";ENU=".//*[local-name()=""%1""]";NLD=".//*[local-name()=""%1""]"';
      MaintStatus@1100528820 : TextConst 'DEU=Wartungsstatus;ENU=Maintenance Status;NLD=Onderhoudsstatus';
      Text001@1100528815 : TextConst 'DEU=Fehler aufgetreten bei Kommunikation mit INSBOU-Backend.\\ Methode: %1\ Fehlercode: %2\ Fehlerbericht:\ %3;ENU=Error occurred in communication with INSBOU Backend.\\ Method: %1\ Error Code: %2\ Error Message:\ %3;NLD=Er is een fout opgetreden bij de communicatie met INSBOU backend.\\ Methode: %1\Foutcode: %2\ Foutboodschap:\ %3';
      RootNode@1100528818 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      Text008@1100528819 : TextConst 'DEU=%1 %2 wird nicht unterstÅtzt fÅr das Versenden von INSBOU %3.;ENU=%1 %2 is not supported for sending a INSBOU %3.;NLD=%1 %2 wordt niet ondersteund voor het sturen van de INSBOU %3.';
      MaintStatusSendRefPointChanged@1100528822 : TextConst 'DEU=%1 %2: Wartungsstatus ist gesendet;ENU=%1 %2: Maintenance Status is send.;NLD=%1 %2: Onderhoudsstatus verzonden.';
      SendStatusDescription@1100525000 : 'Accepteren,Afspraak huurder,Afwachten reactie opzichter,Afwachten in verband met weer,Opdracht geannuleerd,Bewoner belt terug,Bewoner niet thuis,Gereedmelding,Te factureren,In uitvoering,Verlenging,Weigeren';

    PROCEDURE SetServiceOrder@1100528801(ServiceOrder@1100528800 : Record 11012823);
    BEGIN
      "Service Order".COPYFILTERS(ServiceOrder);
    END;

    LOCAL PROCEDURE CreatePostMessageRequest@1100528309(IMsgType@1100528800 : Text;CustINSBOULogin@1100525000 : Record 11229341);
    VAR
      MessageNode@1100528300 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      FoundNode@1100528801 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      MessageFormat@1100528500 : Text;
    BEGIN
      MessageFormat := 'SALES';

      XMLDOMManagement4PS.FindNode(RequestXML, STRSUBSTNO(LocalName, 'MsgContent'), FoundNode);
      CASE CustINSBOULogin."MessageService Version" OF
        CustINSBOULogin."MessageService Version"::" ",
        CustINSBOULogin."MessageService Version"::"2.4": BEGIN
          XMLDOMManagement4PS.FindNode(RequestXML, STRSUBSTNO(LocalName, ConsumedWebServiceLine."Request Root Tag"),
            RootNode);
          XMLDOMManagement4PS.AddElement(RootNode, STRSUBSTNO('%1:MessageServiceMessage', ConsumedWebServiceLine."Prefix Method Namespace"),
            '', ConsumedWebServiceLine."Method Namespace", FoundNode);
          XMLDOMManagement4PS.FindNode(RequestXML, STRSUBSTNO(LocalName, 'MessageServiceMessage'), MessageNode);
          XMLDOMManagement4PS.AddElement(MessageNode, STRSUBSTNO('%1:MsgId', ConsumedWebServiceLine."Prefix Method Namespace"),
            DELCHR(CREATEGUID, '<=>', '{}'), ConsumedWebServiceLine."Method Namespace", FoundNode);
          XMLDOMManagement4PS.AddElement(MessageNode, STRSUBSTNO('%1:MsgType', ConsumedWebServiceLine."Prefix Method Namespace"),
            IMsgType, ConsumedWebServiceLine."Method Namespace", FoundNode);
          XMLDOMManagement4PS.AddElement(MessageNode, STRSUBSTNO('%1:MsgFormat', ConsumedWebServiceLine."Prefix Method Namespace"),
            MessageFormat, ConsumedWebServiceLine."Method Namespace", FoundNode);
          XMLDOMManagement4PS.AddElement(MessageNode, STRSUBSTNO('%1:MsgVersion', ConsumedWebServiceLine."Prefix Method Namespace"),
            CustINSBOULogin."Version OLD", ConsumedWebServiceLine."Method Namespace", FoundNode);
          XMLDOMManagement4PS.AddElement(MessageNode, STRSUBSTNO('%1:MsgDateTime', ConsumedWebServiceLine."Prefix Method Namespace"),
            FORMAT(CURRENTDATETIME, 0, 9), ConsumedWebServiceLine."Method Namespace", FoundNode);
          XMLDOMManagement4PS.AddElement(MessageNode, STRSUBSTNO('%1:MsgContent', ConsumedWebServiceLine."Prefix Method Namespace"),
            '', ConsumedWebServiceLine."Method Namespace", FoundNode);
        END;
        CustINSBOULogin."MessageService Version"::"3.0",
        CustINSBOULogin."MessageService Version"::"3.1": BEGIN
          XMLDOMManagement4PS.FindNode(RequestXML, STRSUBSTNO(LocalName, ConsumedWebServiceLine."Request Root Tag"),RootNode);
          XMLDOMManagement4PS.AddElement(RootNode, STRSUBSTNO('%1:MsgProperties', ConsumedWebServiceLine."Prefix Method Namespace"),
            '', ConsumedWebServiceLine."Method Namespace", FoundNode);
          XMLDOMManagement4PS.AddElement(RootNode, STRSUBSTNO('%1:MsgContent', ConsumedWebServiceLine."Prefix Method Namespace"),
            '', ConsumedWebServiceLine."Method Namespace", FoundNode);
          XMLDOMManagement4PS.FindNode(RequestXML, STRSUBSTNO(LocalName, 'MsgProperties'), MessageNode);
          XMLDOMManagement4PS.AddElement(MessageNode, STRSUBSTNO('%1:MsgId', ConsumedWebServiceLine."Prefix Method Namespace"),
            DELCHR(CREATEGUID, '<=>', '{}'), ConsumedWebServiceLine."Method Namespace", FoundNode);
          XMLDOMManagement4PS.AddElement(MessageNode, STRSUBSTNO('%1:MsgDateTime', ConsumedWebServiceLine."Prefix Method Namespace"),
            FORMAT(CURRENTDATETIME, 0, 9), ConsumedWebServiceLine."Method Namespace", FoundNode);
          XMLDOMManagement4PS.AddElement(MessageNode, STRSUBSTNO('%1:MsgFormat', ConsumedWebServiceLine."Prefix Method Namespace"),
            MessageFormat, ConsumedWebServiceLine."Method Namespace", FoundNode);
          XMLDOMManagement4PS.AddElement(MessageNode, STRSUBSTNO('%1:MsgVersion', ConsumedWebServiceLine."Prefix Method Namespace"),
            CustINSBOULogin."Version OLD", ConsumedWebServiceLine."Method Namespace", FoundNode);
          XMLDOMManagement4PS.AddElement(MessageNode, STRSUBSTNO('%1:MsgType', ConsumedWebServiceLine."Prefix Method Namespace"),
            IMsgType, ConsumedWebServiceLine."Method Namespace", FoundNode);
        END;
      END;
    END;

    LOCAL PROCEDURE CheckResponseSoapFault@1100528330();
    VAR
      FaultCode@1100528301 : Text;
      FaultString@1100528302 : Text;
      FoundNode@1100528800 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      IF (XMLDOMManagement4PS.FindNode(ResponseXML, './/*[local-name()="Fault"]', FoundNode)) THEN BEGIN
        IF (XMLDOMManagement4PS.FindNode(ResponseXML, './/*[local-name()="faultcode"]', FoundNode)) THEN
          FaultCode := FoundNode.InnerText;
        IF (XMLDOMManagement4PS.FindNode(ResponseXML, './/*[local-name()="faultstring"]', FoundNode)) THEN
          FaultString := FoundNode.InnerText;
        IF (XMLDOMManagement4PS.FindNode(ResponseXML, './/*[local-name()="description"]', FoundNode)) THEN
          FaultString := FoundNode.InnerText;
        ERROR(Text001, 'Onderhoudsstatus verzenden', FaultCode, FaultString);
      END;
    END;

    LOCAL PROCEDURE SetValue@1100528817(IRootNode@1100528800 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";IElementName@1100528803 : Text;IValue@1100528802 : Text);
    VAR
      FoundNode@1100528801 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      XMLDOMManagement4PS.FindNode(IRootNode, STRSUBSTNO(LocalName, IElementName), FoundNode);
      FoundNode.InnerText(IValue);
    END;

    LOCAL PROCEDURE FillSOAPHeaderWithActualData@1100525015(VAR XMLRequest@1100525000 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";MessageServiceVersion@1100525001 : Text[10]);
    VAR
      HeaderNode@1100525004 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      CustomInfoNode@1100525003 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      ApplicationIdElement@1100525002 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      AuthenticationInfoNode@1100525005 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      ApplicationSystemConstants@1100525006 : Codeunit 9015;
    BEGIN
      XMLDOMManagement4PS.FindNode(XMLRequest, STRSUBSTNO(LocalName, 'Header'), HeaderNode);
      CASE MessageServiceVersion OF
        ' ',
        '2.4': BEGIN
          XMLDOMManagement4PS.FindNode(HeaderNode, STRSUBSTNO(LocalName, 'AuthenticationInfo'), AuthenticationInfoNode);
          XMLDOMManagement4PS.FindNode(AuthenticationInfoNode, STRSUBSTNO(LocalName, 'ApplicationId'), ApplicationIdElement);
          ApplicationIdElement.InnerText(ApplicationSystemConstants.ApplicationVersion);
        END;
        '3.0',
        '3.1': BEGIN
          XMLDOMManagement4PS.FindNode(HeaderNode, STRSUBSTNO(LocalName, 'CustomInfo'), CustomInfoNode);
          XMLDOMManagement4PS.FindNode(CustomInfoNode, STRSUBSTNO(LocalName, 'ApplicationId'), ApplicationIdElement);
          ApplicationIdElement.InnerText(ApplicationSystemConstants.ApplicationVersion);
        END;
      END;
    END;

    EVENT ResponseXML@1100528811::NodeInserting@94(sender@1100528801 : Variant;e@1100528800 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT ResponseXML@1100528811::NodeInserted@95(sender@1100528801 : Variant;e@1100528800 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT ResponseXML@1100528811::NodeRemoving@96(sender@1100528801 : Variant;e@1100528800 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT ResponseXML@1100528811::NodeRemoved@97(sender@1100528801 : Variant;e@1100528800 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT ResponseXML@1100528811::NodeChanging@98(sender@1100528801 : Variant;e@1100528800 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT ResponseXML@1100528811::NodeChanged@99(sender@1100528801 : Variant;e@1100528800 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT RequestXML@1100528813::NodeInserting@94(sender@1100528801 : Variant;e@1100528800 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT RequestXML@1100528813::NodeInserted@95(sender@1100528801 : Variant;e@1100528800 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT RequestXML@1100528813::NodeRemoving@96(sender@1100528801 : Variant;e@1100528800 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT RequestXML@1100528813::NodeRemoved@97(sender@1100528801 : Variant;e@1100528800 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT RequestXML@1100528813::NodeChanging@98(sender@1100528801 : Variant;e@1100528800 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    EVENT RequestXML@1100528813::NodeChanged@99(sender@1100528801 : Variant;e@1100528800 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeChangedEventArgs");
    BEGIN
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

