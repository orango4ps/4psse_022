OBJECT Table 5741 Transfer Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.05,4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               TransLine2@1000 : Record 5741;
             BEGIN
               DPA_OnInsert; //**4PS.n
               TestStatusOpen;
               TransLine2.RESET;
               TransLine2.SETFILTER("Document No.",TransHeader."No.");
               IF TransLine2.FINDLAST THEN
                 "Line No." := TransLine2."Line No." + 10000;
               ReserveTransferLine.VerifyQuantity(Rec,xRec);
             END;

    OnModify=BEGIN
               IF ItemExists(xRec."Item No.") THEN
                 ReserveTransferLine.VerifyChange(Rec,xRec);
             END;

    OnDelete=VAR
               ItemChargeAssgntPurch@1000 : Record 5805;
             BEGIN
               TestStatusOpen;

               TESTFIELD("Quantity Shipped","Quantity Received");
               TESTFIELD("Qty. Shipped (Base)","Qty. Received (Base)");
               CALCFIELDS("Reserved Qty. Inbnd. (Base)","Reserved Qty. Outbnd. (Base)");
               TESTFIELD("Reserved Qty. Inbnd. (Base)",0);
               TESTFIELD("Reserved Qty. Outbnd. (Base)",0);

               ReserveTransferLine.DeleteLine(Rec);
               WhseValidateSourceLine.TransLineDelete(Rec);

               ItemChargeAssgntPurch.SETCURRENTKEY(
                 "Applies-to Doc. Type","Applies-to Doc. No.","Applies-to Doc. Line No.");
               ItemChargeAssgntPurch.SETRANGE("Applies-to Doc. Type",ItemChargeAssgntPurch."Applies-to Doc. Type"::"Transfer Receipt");
               ItemChargeAssgntPurch.SETRANGE("Applies-to Doc. No.","Document No.");
               ItemChargeAssgntPurch.SETRANGE("Applies-to Doc. Line No.","Line No.");
               ItemChargeAssgntPurch.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               ERROR(Text001,TABLECAPTION);
             END;

    CaptionML=[ENU=Transfer Line;
               NOR=Overfõringslinje];
    LookupPageID=Page5749;
    DrillDownPageID=Page5749;
  }
  FIELDS
  {
    { 1   ;   ;Document No.        ;Code20        ;CaptionML=[ENU=Document No.;
                                                              NOR=Bilagsnr.;
                                                              SVE=Dokumentnr] }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 3   ;   ;Item No.            ;Code20        ;TableRelation=Item WHERE (Type=CONST(Inventory),
                                                                             Blocked=CONST(No));
                                                   OnValidate=VAR
                                                                TempTransferLine@1000 : TEMPORARY Record 5741;
                                                                ReturnValue@1001 : Text[50];
                                                              BEGIN
                                                                TESTFIELD("Quantity Shipped",0);
                                                                IF CurrFieldNo <> 0 THEN
                                                                  TestStatusOpen;
                                                                Item.TryGetItemNo(ReturnValue,"Item No.",TRUE);
                                                                "Item No." := COPYSTR(ReturnValue,1,MAXSTRLEN("Item No."));
                                                                ReserveTransferLine.VerifyChange(Rec,xRec);
                                                                CALCFIELDS("Reserved Qty. Inbnd. (Base)");
                                                                TESTFIELD("Reserved Qty. Inbnd. (Base)",0);
                                                                WhseValidateSourceLine.TransLineVerifyChange(Rec,xRec);

                                                                TempTransferLine := Rec;
                                                                INIT;
                                                                "Item No." := TempTransferLine."Item No.";
                                                                IF "Item No." = '' THEN
                                                                  EXIT;

                                                                OnValidateItemNoOnAfterInitLine(Rec,TempTransferLine);

                                                                GetTransHeader;
                                                                GetItem;
                                                                GetDefaultBin("Transfer-from Code","Transfer-to Code");

                                                                Item.TESTFIELD("Inventory Blocked", FALSE); //**4PS.n
                                                                Item.TESTFIELD(Blocked,FALSE);
                                                                Item.TESTFIELD(Type,Item.Type::Inventory);

                                                                Description := Item.Description;
                                                                "Description 2" := Item."Description 2";
                                                                VALIDATE("Gen. Prod. Posting Group",Item."Gen. Prod. Posting Group");
                                                                VALIDATE("Inventory Posting Group",Item."Inventory Posting Group");
                                                                VALIDATE(Quantity,xRec.Quantity);
                                                                VALIDATE("Unit of Measure Code",Item."Base Unit of Measure");
                                                                VALIDATE("Gross Weight",Item."Gross Weight");
                                                                VALIDATE("Net Weight",Item."Net Weight");
                                                                VALIDATE("Unit Volume",Item."Unit Volume");
                                                                VALIDATE("Units per Parcel",Item."Units per Parcel");
                                                                "Item Category Code" := Item."Item Category Code";

                                                                CreateDim(
                                                                  DATABASE::Item,"Item No.",
                                                                  DATABASE::Location,"Transfer-from Code"); //**4PS.n
                                                                DimMgt.UpdateGlobalDimFromDimSetID("Dimension Set ID","Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
                                                              END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Item No.;
                                                              NOR=Varenr.;
                                                              SVE=Artikelnr] }
    { 4   ;   ;Quantity            ;Decimal       ;OnValidate=VAR
                                                                IsHandled@1000 : Boolean;
                                                              BEGIN
                                                                IF CurrFieldNo <> 0 THEN
                                                                  TestStatusOpen;
                                                                IF Quantity <> 0 THEN
                                                                  TESTFIELD("Item No.");
                                                                "Quantity (Base)" := CalcBaseQty(Quantity);
                                                                IF ((Quantity * "Quantity Shipped") < 0) OR
                                                                   (ABS(Quantity) < ABS("Quantity Shipped"))
                                                                THEN
                                                                  FIELDERROR(Quantity,STRSUBSTNO(Text002,FIELDCAPTION("Quantity Shipped")));
                                                                IF (("Quantity (Base)" * "Qty. Shipped (Base)") < 0) OR
                                                                   (ABS("Quantity (Base)") < ABS("Qty. Received (Base)"))
                                                                THEN
                                                                  FIELDERROR("Quantity (Base)",STRSUBSTNO(Text002,FIELDCAPTION("Qty. Shipped (Base)")));
                                                                InitQtyInTransit;
                                                                InitOutstandingQty;
                                                                InitQtyToShip;
                                                                InitQtyToReceive;
                                                                CheckItemAvailable(FIELDNO(Quantity));

                                                                ReserveTransferLine.VerifyQuantity(Rec,xRec);

                                                                UpdateWithWarehouseShipReceive;

                                                                IsHandled := FALSE;
                                                                OnValidateQuantityOnBeforeTransLineVerifyChange(Rec,xRec,IsHandled);
                                                                IF NOT IsHandled THEN
                                                                  WhseValidateSourceLine.TransLineVerifyChange(Rec,xRec);
                                                              END;

                                                   CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 5   ;   ;Unit of Measure     ;Text50        ;OnValidate=BEGIN
                                                                IF CurrFieldNo <> 0 THEN
                                                                  TestStatusOpen;
                                                              END;

                                                   CaptionML=[ENU=Unit of Measure;
                                                              NOR=Enhet;
                                                              SVE=Enhet] }
    { 6   ;   ;Qty. to Ship        ;Decimal       ;OnValidate=BEGIN
                                                                GetLocation("Transfer-from Code");
                                                                IF CurrFieldNo <> 0 THEN BEGIN
                                                                  IF Location."Require Shipment" AND
                                                                     ("Qty. to Ship" <> 0)
                                                                  THEN
                                                                    CheckWarehouse("Transfer-from Code",FALSE);
                                                                  WhseValidateSourceLine.TransLineVerifyChange(Rec,xRec);
                                                                END;

                                                                IF "Qty. to Ship" > "Outstanding Quantity" THEN
                                                                  IF "Outstanding Quantity" > 0 THEN
                                                                    ERROR(
                                                                      Text005,
                                                                      "Outstanding Quantity")
                                                                  ELSE
                                                                    ERROR(Text006);
                                                                "Qty. to Ship (Base)" := CalcBaseQty("Qty. to Ship");

                                                                IF ("In-Transit Code" = '') AND ("Quantity Shipped" = "Quantity Received") THEN
                                                                  VALIDATE("Qty. to Receive","Qty. to Ship");
                                                              END;

                                                   CaptionML=[ENU=Qty. to Ship;
                                                              NOR=Levere (antall);
                                                              SVE=Ant. att utleverera];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 7   ;   ;Qty. to Receive     ;Decimal       ;OnValidate=BEGIN
                                                                GetLocation("Transfer-to Code");
                                                                IF CurrFieldNo <> 0 THEN BEGIN
                                                                  IF Location."Require Receive" AND
                                                                     ("Qty. to Receive" <> 0)
                                                                  THEN
                                                                    CheckWarehouse("Transfer-to Code",TRUE);
                                                                  WhseValidateSourceLine.TransLineVerifyChange(Rec,xRec);
                                                                END;

                                                                GetTransferHeaderNoVerification;

                                                                IF NOT TransHeader."Direct Transfer" AND ("Direct Transfer" = xRec."Direct Transfer") THEN
                                                                  IF "Qty. to Receive" > "Qty. in Transit" THEN
                                                                    IF "Qty. in Transit" > 0 THEN
                                                                      ERROR(
                                                                        Text008,
                                                                        "Qty. in Transit")
                                                                    ELSE
                                                                      ERROR(Text009);
                                                                "Qty. to Receive (Base)" := CalcBaseQty("Qty. to Receive");
                                                              END;

                                                   CaptionML=[ENU=Qty. to Receive;
                                                              NOR=Motta (antall);
                                                              SVE=Ant. att inlevereras];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 8   ;   ;Quantity Shipped    ;Decimal       ;OnValidate=BEGIN
                                                                "Qty. Shipped (Base)" := CalcBaseQty("Quantity Shipped");
                                                                InitQtyInTransit;
                                                                InitOutstandingQty;
                                                                InitQtyToShip;
                                                                InitQtyToReceive;
                                                              END;

                                                   CaptionML=[ENU=Quantity Shipped;
                                                              NOR=Levert (antall);
                                                              SVE=Utlevererat antal];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 9   ;   ;Quantity Received   ;Decimal       ;OnValidate=BEGIN
                                                                "Qty. Received (Base)" := CalcBaseQty("Quantity Received");
                                                                InitQtyInTransit;
                                                                InitOutstandingQty;
                                                                InitQtyToReceive;
                                                              END;

                                                   CaptionML=[ENU=Quantity Received;
                                                              NOR=Mottatt (antall);
                                                              SVE=Inlevererat antal];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 10  ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=[ENU=Open,Released;
                                                                    NOR=èpen,Frigitt;
                                                                    SVE=ôppen,SlÑppt];
                                                   OptionString=Open,Released;
                                                   Editable=No }
    { 11  ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
                                                              END;

                                                   CaptionML=[ENU=Shortcut Dimension 1 Code;
                                                              NOR=Snarveisdimensjon 1 - kode;
                                                              SVE=GenvÑg dimension 1 kod];
                                                   CaptionClass='1,2,1' }
    { 12  ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");
                                                              END;

                                                   CaptionML=[ENU=Shortcut Dimension 2 Code;
                                                              NOR=Snarveisdimensjon 2 - kode;
                                                              SVE=GenvÑg dimension 2 kod];
                                                   CaptionClass='1,2,2' }
    { 13  ;   ;Description         ;Text100       ;TableRelation=Item WHERE (Type=CONST(Inventory),
                                                                             Blocked=CONST(No));
                                                   OnValidate=VAR
                                                                Item@1001 : Record 27;
                                                                ReturnValue@1003 : Text[50];
                                                                ItemDescriptionIsNo@1000 : Boolean;
                                                              BEGIN
                                                                IF (STRLEN(Description) <= MAXSTRLEN(Item."No.")) AND ("Item No." <> '') THEN
                                                                  ItemDescriptionIsNo := Item.GET(Description);

                                                                IF ("Item No." <> '') AND (NOT ItemDescriptionIsNo) AND (Description <> '') THEN BEGIN
                                                                  Item.SETFILTER(Description,'''@' + CONVERTSTR(Description,'''','?') + '''');
                                                                  IF NOT Item.FINDFIRST THEN
                                                                    EXIT;
                                                                  IF Item."No." = "Item No." THEN
                                                                    EXIT;
                                                                  IF CONFIRM(AnotherItemWithSameDescrQst,FALSE,Item."No.",Item.Description) THEN
                                                                    VALIDATE("Item No.",Item."No.");
                                                                  EXIT;
                                                                END;

                                                                IF Item.TryGetItemNoOpenCard(ReturnValue,Description,FALSE,TRUE,TRUE) THEN
                                                                  CASE ReturnValue OF
                                                                    '':
                                                                      Description := xRec.Description;
                                                                    "Item No.":
                                                                      Description := xRec.Description;
                                                                    ELSE BEGIN
                                                                      CurrFieldNo := FIELDNO("Item No.");
                                                                      VALIDATE("Item No.",COPYSTR(ReturnValue,1,MAXSTRLEN(Item."No.")));
                                                                    END;
                                                                  END;

                                                                IF "Item No." <> '' THEN
                                                                  GetItem;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 14  ;   ;Gen. Prod. Posting Group;Code20    ;TableRelation="Gen. Product Posting Group";
                                                   OnValidate=BEGIN
                                                                IF CurrFieldNo <> 0 THEN
                                                                  TestStatusOpen;
                                                              END;

                                                   CaptionML=[ENU=Gen. Prod. Posting Group;
                                                              NOR=Bokfõringsgruppe - vare;
                                                              SVE=Produktbokfîringsmall] }
    { 15  ;   ;Inventory Posting Group;Code20     ;TableRelation="Inventory Posting Group";
                                                   OnValidate=BEGIN
                                                                IF CurrFieldNo <> 0 THEN
                                                                  TestStatusOpen;
                                                              END;

                                                   CaptionML=[ENU=Inventory Posting Group;
                                                              NOR=Bokfõringsgruppe - lager;
                                                              SVE=Lagerbokfîringsmall] }
    { 16  ;   ;Quantity (Base)     ;Decimal       ;OnValidate=BEGIN
                                                                IF CurrFieldNo <> 0 THEN
                                                                  TestStatusOpen;
                                                                TESTFIELD("Qty. per Unit of Measure",1);
                                                                VALIDATE(Quantity,"Quantity (Base)");
                                                              END;

                                                   CaptionML=[ENU=Quantity (Base);
                                                              NOR=Antall (lagerenhet);
                                                              SVE=Antal (bas)];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 17  ;   ;Outstanding Qty. (Base);Decimal    ;CaptionML=[ENU=Outstanding Qty. (Base);
                                                              NOR=Restantall (lagerenhet);
                                                              SVE=Restnoterat antal (bas)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 18  ;   ;Qty. to Ship (Base) ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("Qty. per Unit of Measure",1);
                                                                VALIDATE("Qty. to Ship","Qty. to Ship (Base)");
                                                              END;

                                                   CaptionML=[ENU=Qty. to Ship (Base);
                                                              NOR=Antall som skal leveres (l.enh);
                                                              SVE=Ant. att utlevereras (bas)];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 19  ;   ;Qty. Shipped (Base) ;Decimal       ;CaptionML=[ENU=Qty. Shipped (Base);
                                                              NOR=Antall levert (lagerenhet);
                                                              SVE=Ant. levererat (bas)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 20  ;   ;Qty. to Receive (Base);Decimal     ;OnValidate=BEGIN
                                                                TESTFIELD("Qty. per Unit of Measure",1);
                                                                VALIDATE("Qty. to Receive","Qty. to Receive (Base)");
                                                              END;

                                                   CaptionML=[ENU=Qty. to Receive (Base);
                                                              NOR=Antall som skal mottas (l.enh);
                                                              SVE=Ant. att inlevereras (bas)];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 21  ;   ;Qty. Received (Base);Decimal       ;CaptionML=[ENU=Qty. Received (Base);
                                                              NOR=Ant. mottatt (lagerenhet);
                                                              SVE=Inlevererat antal (bas)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 22  ;   ;Qty. per Unit of Measure;Decimal   ;InitValue=1;
                                                   CaptionML=[ENU=Qty. per Unit of Measure;
                                                              NOR=Antall per enhet;
                                                              SVE=Antal per enhet];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 23  ;   ;Unit of Measure Code;Code10        ;TableRelation="Item Unit of Measure".Code WHERE (Item No.=FIELD(Item No.));
                                                   OnValidate=VAR
                                                                UnitOfMeasure@1000 : Record 204;
                                                                UOMMgt@1001 : Codeunit 5402;
                                                              BEGIN
                                                                IF CurrFieldNo <> 0 THEN
                                                                  TestStatusOpen;
                                                                TESTFIELD("Quantity Shipped",0);
                                                                TESTFIELD("Qty. Shipped (Base)",0);
                                                                TESTFIELD("Quantity Received",0);
                                                                TESTFIELD("Qty. Received (Base)",0);
                                                                ReserveTransferLine.VerifyChange(Rec,xRec);
                                                                WhseValidateSourceLine.TransLineVerifyChange(Rec,xRec);
                                                                IF "Unit of Measure Code" = '' THEN
                                                                  "Unit of Measure" := ''
                                                                ELSE BEGIN
                                                                  IF NOT UnitOfMeasure.GET("Unit of Measure Code") THEN
                                                                    UnitOfMeasure.INIT;
                                                                  "Unit of Measure" := UnitOfMeasure.Description;
                                                                END;
                                                                GetItem;
                                                                VALIDATE("Qty. per Unit of Measure",UOMMgt.GetQtyPerUnitOfMeasure(Item,"Unit of Measure Code"));
                                                                "Gross Weight" := Item."Gross Weight" * "Qty. per Unit of Measure";
                                                                "Net Weight" := Item."Net Weight" * "Qty. per Unit of Measure";
                                                                "Unit Volume" := Item."Unit Volume" * "Qty. per Unit of Measure";
                                                                "Units per Parcel" := ROUND(Item."Units per Parcel" / "Qty. per Unit of Measure",UOMMgt.QtyRndPrecision);
                                                                VALIDATE(Quantity);
                                                              END;

                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              NOR=Enhetskode;
                                                              SVE=Enhetskod] }
    { 24  ;   ;Outstanding Quantity;Decimal       ;CaptionML=[ENU=Outstanding Quantity;
                                                              NOR=Restantall;
                                                              SVE=Restnoterat antal];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 25  ;   ;Gross Weight        ;Decimal       ;CaptionML=[ENU=Gross Weight;
                                                              NOR=Bruttovekt;
                                                              SVE=Bruttovikt];
                                                   DecimalPlaces=0:5 }
    { 26  ;   ;Net Weight          ;Decimal       ;CaptionML=[ENU=Net Weight;
                                                              NOR=Nettovekt;
                                                              SVE=Nettovikt];
                                                   DecimalPlaces=0:5 }
    { 27  ;   ;Unit Volume         ;Decimal       ;CaptionML=[ENU=Unit Volume;
                                                              NOR=Volum;
                                                              SVE=Volym];
                                                   DecimalPlaces=0:5 }
    { 30  ;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   OnValidate=VAR
                                                                ItemVariant@1000 : Record 5401;
                                                              BEGIN
                                                                IF CurrFieldNo <> 0 THEN
                                                                  TestStatusOpen;
                                                                ReserveTransferLine.VerifyChange(Rec,xRec);
                                                                WhseValidateSourceLine.TransLineVerifyChange(Rec,xRec);

                                                                IF "Variant Code" = '' THEN
                                                                  EXIT;

                                                                GetDefaultBin("Transfer-from Code","Transfer-to Code");
                                                                ItemVariant.GET("Item No.","Variant Code");
                                                                Description := ItemVariant.Description;
                                                                "Description 2" := ItemVariant."Description 2";

                                                                CheckItemAvailable(FIELDNO("Variant Code"));
                                                              END;

                                                   CaptionML=[ENU=Variant Code;
                                                              NOR=Variantkode;
                                                              SVE=Variantkod] }
    { 31  ;   ;Units per Parcel    ;Decimal       ;CaptionML=[ENU=Units per Parcel;
                                                              NOR=Antall per kolli;
                                                              SVE=Antal per kolli];
                                                   DecimalPlaces=0:5 }
    { 32  ;   ;Description 2       ;Text50        ;CaptionML=[ENU=Description 2;
                                                              NOR=Beskrivelse 2;
                                                              SVE=Beskrivning 2] }
    { 33  ;   ;In-Transit Code     ;Code10        ;TableRelation=Location WHERE (Use As In-Transit=CONST(Yes));
                                                   OnValidate=BEGIN
                                                                TESTFIELD("Quantity Shipped",0);
                                                              END;

                                                   CaptionML=[ENU=In-Transit Code;
                                                              NOR=I transitt-kode;
                                                              SVE=Transitkod];
                                                   Editable=No }
    { 34  ;   ;Qty. in Transit     ;Decimal       ;CaptionML=[ENU=Qty. in Transit;
                                                              NOR=Ant. i transitt;
                                                              SVE=Ant. i transit];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 35  ;   ;Qty. in Transit (Base);Decimal     ;CaptionML=[ENU=Qty. in Transit (Base);
                                                              NOR=Ant. i transitt (lag.enh)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 36  ;   ;Transfer-from Code  ;Code10        ;TableRelation=Location;
                                                   OnValidate=BEGIN
                                                                TESTFIELD("Quantity Shipped",0);
                                                                IF CurrFieldNo <> 0 THEN
                                                                  TestStatusOpen;
                                                                IF "Transfer-from Code" <> xRec."Transfer-from Code" THEN BEGIN
                                                                  "Transfer-from Bin Code" := '';
                                                                  GetDefaultBin("Transfer-from Code",'');
                                                                END;

                                                                CheckItemAvailable(FIELDNO("Transfer-from Code"));
                                                                ReserveTransferLine.VerifyChange(Rec,xRec);
                                                                UpdateWithWarehouseShipReceive;
                                                                WhseValidateSourceLine.TransLineVerifyChange(Rec,xRec);

                                                                //**4PS.sn
                                                                CreateDim(
                                                                  DATABASE::Item,"Item No.",
                                                                  DATABASE::Location,"Transfer-from Code");
                                                                DimMgt.UpdateGlobalDimFromDimSetID("Dimension Set ID","Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
                                                                //**4PS.en
                                                              END;

                                                   CaptionML=[ENU=Transfer-from Code;
                                                              NOR=Overfõr fra-kode;
                                                              SVE=ôverfîring frÜn kod];
                                                   Editable=No }
    { 37  ;   ;Transfer-to Code    ;Code10        ;TableRelation=Location;
                                                   OnValidate=BEGIN
                                                                TESTFIELD("Quantity Shipped",0);
                                                                IF CurrFieldNo <> 0 THEN
                                                                  TestStatusOpen;
                                                                IF "Transfer-to Code" <> xRec."Transfer-to Code" THEN BEGIN
                                                                  "Transfer-To Bin Code" := '';
                                                                  GetDefaultBin('',"Transfer-to Code");
                                                                END;

                                                                ReserveTransferLine.VerifyChange(Rec,xRec);
                                                                UpdateWithWarehouseShipReceive;
                                                                WhseValidateSourceLine.TransLineVerifyChange(Rec,xRec);
                                                              END;

                                                   CaptionML=[ENU=Transfer-to Code;
                                                              NOR=Overfõr til-kode;
                                                              SVE=ôverfîring till kod];
                                                   Editable=No }
    { 38  ;   ;Shipment Date       ;Date          ;OnValidate=BEGIN
                                                                IF CurrFieldNo <> 0 THEN
                                                                  TestStatusOpen;
                                                                TransferRoute.CalcReceiptDate("Shipment Date","Receipt Date",
                                                                  "Shipping Time","Outbound Whse. Handling Time","Inbound Whse. Handling Time",
                                                                  "Transfer-from Code","Transfer-to Code","Shipping Agent Code","Shipping Agent Service Code");
                                                                CheckItemAvailable(FIELDNO("Shipment Date"));
                                                                DateConflictCheck;
                                                              END;

                                                   CaptionML=[ENU=Shipment Date;
                                                              NOR=Forsendelsesdato;
                                                              SVE=Utleveransdatum] }
    { 39  ;   ;Receipt Date        ;Date          ;OnValidate=BEGIN
                                                                IF CurrFieldNo <> 0 THEN
                                                                  TestStatusOpen;
                                                                TransferRoute.CalcShipmentDate("Shipment Date","Receipt Date",
                                                                  "Shipping Time","Outbound Whse. Handling Time","Inbound Whse. Handling Time",
                                                                  "Transfer-from Code","Transfer-to Code","Shipping Agent Code","Shipping Agent Service Code");
                                                                CheckItemAvailable(FIELDNO("Shipment Date"));
                                                                DateConflictCheck;
                                                              END;

                                                   CaptionML=[ENU=Receipt Date;
                                                              NOR=Mottaksdato;
                                                              SVE=Inleveransdatum] }
    { 40  ;   ;Derived From Line No.;Integer      ;TableRelation="Transfer Line"."Line No." WHERE (Document No.=FIELD(Document No.));
                                                   CaptionML=[ENU=Derived From Line No.;
                                                              NOR=Avledet fra linjenr.] }
    { 41  ;   ;Shipping Agent Code ;Code10        ;TableRelation="Shipping Agent";
                                                   OnValidate=BEGIN
                                                                IF CurrFieldNo <> 0 THEN
                                                                  TestStatusOpen;
                                                                IF "Shipping Agent Code" <> xRec."Shipping Agent Code" THEN
                                                                  VALIDATE("Shipping Agent Service Code",'');
                                                              END;

                                                   AccessByPermission=TableData 5790=R;
                                                   CaptionML=[ENU=Shipping Agent Code;
                                                              NOR=Transportõrkode;
                                                              SVE=Speditîrkod] }
    { 42  ;   ;Shipping Agent Service Code;Code10 ;TableRelation="Shipping Agent Services".Code WHERE (Shipping Agent Code=FIELD(Shipping Agent Code));
                                                   OnValidate=BEGIN
                                                                IF CurrFieldNo <> 0 THEN
                                                                  TestStatusOpen;
                                                                TransferRoute.GetShippingTime(
                                                                  "Transfer-from Code","Transfer-to Code",
                                                                  "Shipping Agent Code","Shipping Agent Service Code",
                                                                  "Shipping Time");
                                                                TransferRoute.CalcReceiptDate("Shipment Date","Receipt Date",
                                                                  "Shipping Time","Outbound Whse. Handling Time","Inbound Whse. Handling Time",
                                                                  "Transfer-from Code","Transfer-to Code","Shipping Agent Code","Shipping Agent Service Code");
                                                                CheckItemAvailable(FIELDNO("Shipping Agent Service Code"));
                                                                DateConflictCheck;
                                                              END;

                                                   CaptionML=[ENU=Shipping Agent Service Code;
                                                              NOR=Transportõrservicekode;
                                                              SVE=Speditîr servicekod] }
    { 43  ;   ;Appl.-to Item Entry ;Integer       ;OnValidate=VAR
                                                                ItemLedgEntry@1000 : Record 32;
                                                                ItemTrackingLines@1001 : Page 6510;
                                                              BEGIN
                                                                IF "Appl.-to Item Entry" <> 0 THEN BEGIN
                                                                  TESTFIELD(Quantity);
                                                                  ItemLedgEntry.GET("Appl.-to Item Entry");
                                                                  ItemLedgEntry.TESTFIELD(Positive,TRUE);
                                                                  IF (ItemLedgEntry."Lot No." <> '') OR (ItemLedgEntry."Serial No." <> '') THEN
                                                                    ERROR(MustUseTrackingErr,ItemTrackingLines.CAPTION,FIELDCAPTION("Appl.-to Item Entry"));
                                                                  IF ABS("Qty. to Ship (Base)") > ItemLedgEntry.Quantity THEN
                                                                    ERROR(ShippingMoreUnitsThanReceivedErr,ItemLedgEntry.Quantity,ItemLedgEntry."Document No.");

                                                                  ItemLedgEntry.TESTFIELD("Location Code","Transfer-from Code");
                                                                  IF NOT ItemLedgEntry.Open THEN
                                                                    MESSAGE(LedgEntryWillBeOpenedMsg,"Appl.-to Item Entry");
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              SelectItemEntry(FIELDNO("Appl.-to Item Entry"));
                                                            END;

                                                   AccessByPermission=TableData 27=R;
                                                   CaptionML=[ENU=Appl.-to Item Entry;
                                                              NOR=Utligningsvarepost;
                                                              SVE=Koppla till artikellîpnr] }
    { 50  ;   ;Reserved Quantity Inbnd.;Decimal   ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Reservation Entry".Quantity WHERE (Source ID=FIELD(Document No.),
                                                                                                       Source Ref. No.=FIELD(Line No.),
                                                                                                       Source Type=CONST(5741),
                                                                                                       Source Subtype=CONST(1),
                                                                                                       Source Prod. Order Line=FIELD(Derived From Line No.),
                                                                                                       Reservation Status=CONST(Reservation)));
                                                   CaptionML=[ENU=Reserved Quantity Inbnd.;
                                                              NOR=Reservert antall - inngÜende];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 51  ;   ;Reserved Quantity Outbnd.;Decimal  ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("Reservation Entry".Quantity WHERE (Source ID=FIELD(Document No.),
                                                                                                        Source Ref. No.=FIELD(Line No.),
                                                                                                        Source Type=CONST(5741),
                                                                                                        Source Subtype=CONST(0),
                                                                                                        Source Prod. Order Line=FIELD(Derived From Line No.),
                                                                                                        Reservation Status=CONST(Reservation)));
                                                   CaptionML=[ENU=Reserved Quantity Outbnd.;
                                                              NOR=Reservert antall - utgÜende];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 52  ;   ;Reserved Qty. Inbnd. (Base);Decimal;FieldClass=FlowField;
                                                   CalcFormula=Sum("Reservation Entry"."Quantity (Base)" WHERE (Source ID=FIELD(Document No.),
                                                                                                                Source Ref. No.=FIELD(Line No.),
                                                                                                                Source Type=CONST(5741),
                                                                                                                Source Subtype=CONST(1),
                                                                                                                Source Prod. Order Line=FIELD(Derived From Line No.),
                                                                                                                Reservation Status=CONST(Reservation)));
                                                   CaptionML=[ENU=Reserved Qty. Inbnd. (Base);
                                                              NOR=Reservert ant. - inngÜ. (l.enh)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 53  ;   ;Reserved Qty. Outbnd. (Base);Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=-Sum("Reservation Entry"."Quantity (Base)" WHERE (Source ID=FIELD(Document No.),
                                                                                                                 Source Ref. No.=FIELD(Line No.),
                                                                                                                 Source Type=CONST(5741),
                                                                                                                 Source Subtype=CONST(0),
                                                                                                                 Source Prod. Order Line=FIELD(Derived From Line No.),
                                                                                                                 Reservation Status=CONST(Reservation)));
                                                   CaptionML=[ENU=Reserved Qty. Outbnd. (Base);
                                                              NOR=Reservert ant. - utgÜ. (l.enh)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 54  ;   ;Shipping Time       ;DateFormula   ;OnValidate=BEGIN
                                                                IF CurrFieldNo <> 0 THEN
                                                                  TestStatusOpen;
                                                                TransferRoute.CalcReceiptDate("Shipment Date","Receipt Date",
                                                                  "Shipping Time","Outbound Whse. Handling Time","Inbound Whse. Handling Time",
                                                                  "Transfer-from Code","Transfer-to Code","Shipping Agent Code","Shipping Agent Service Code");
                                                                DateConflictCheck;
                                                              END;

                                                   AccessByPermission=TableData 5790=R;
                                                   CaptionML=[ENU=Shipping Time;
                                                              NOR=Leveringstid;
                                                              SVE=Leveranstid] }
    { 55  ;   ;Reserved Quantity Shipped;Decimal  ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Reservation Entry".Quantity WHERE (Source ID=FIELD(Document No.),
                                                                                                       Source Ref. No.=FILTER(<>0),
                                                                                                       Source Type=CONST(5741),
                                                                                                       Source Subtype=CONST(1),
                                                                                                       Source Prod. Order Line=FIELD(Line No.),
                                                                                                       Reservation Status=CONST(Reservation)));
                                                   CaptionML=[ENU=Reserved Quantity Shipped;
                                                              NOR=Levert reservert antall];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 56  ;   ;Reserved Qty. Shipped (Base);Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("Reservation Entry"."Quantity (Base)" WHERE (Source ID=FIELD(Document No.),
                                                                                                                Source Ref. No.=FILTER(<>0),
                                                                                                                Source Type=CONST(5741),
                                                                                                                Source Subtype=CONST(1),
                                                                                                                Source Prod. Order Line=FIELD(Line No.),
                                                                                                                Reservation Status=CONST(Reservation)));
                                                   CaptionML=[ENU=Reserved Qty. Shipped (Base);
                                                              NOR=Levert reservert antall (lagerenhet)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 70  ;   ;Direct Transfer     ;Boolean       ;CaptionML=[ENU=Direct Transfer;
                                                              NOR=Direkte overfõring] }
    { 480 ;   ;Dimension Set ID    ;Integer       ;TableRelation="Dimension Set Entry";
                                                   OnValidate=BEGIN
                                                                DimMgt.UpdateGlobalDimFromDimSetID("Dimension Set ID","Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              ShowDimensions;
                                                            END;

                                                   CaptionML=[ENU=Dimension Set ID;
                                                              NOR=Dimensjonssett-ID;
                                                              SVE=DimensionsuppsÑttnings-ID];
                                                   Editable=No }
    { 5704;   ;Item Category Code  ;Code20        ;TableRelation="Item Category";
                                                   CaptionML=[ENU=Item Category Code;
                                                              NOR=Varekategorikode;
                                                              SVE=Artikelkategorikod] }
    { 5707;   ;Product Group Code  ;Code10        ;TableRelation="Product Group".Code WHERE (Item Category Code=FIELD(Item Category Code));
                                                   ValidateTableRelation=No;
                                                   ObsoleteState=Removed;
                                                   ObsoleteReason=Product Groups became first level children of Item Categories.;
                                                   CaptionML=[ENU=Product Group Code;
                                                              NOR=Produktgruppekode;
                                                              SVE=Produktgruppkod] }
    { 5750;   ;Whse. Inbnd. Otsdg. Qty (Base);Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("Warehouse Receipt Line"."Qty. Outstanding (Base)" WHERE (Source Type=CONST(5741),
                                                                                                                             Source Subtype=CONST(1),
                                                                                                                             Source No.=FIELD(Document No.),
                                                                                                                             Source Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=Whse. Inbnd. Otsdg. Qty (Base);
                                                              NOR=Restantall - inngÜende lager (lagerenhet)];
                                                   DecimalPlaces=0:5;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 5751;   ;Whse Outbnd. Otsdg. Qty (Base);Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("Warehouse Shipment Line"."Qty. Outstanding (Base)" WHERE (Source Type=CONST(5741),
                                                                                                                              Source Subtype=CONST(0),
                                                                                                                              Source No.=FIELD(Document No.),
                                                                                                                              Source Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=Whse Outbnd. Otsdg. Qty (Base);
                                                              NOR=Restantall - utgÜende lager (lagerenhet)];
                                                   DecimalPlaces=0:5;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 5752;   ;Completely Shipped  ;Boolean       ;CaptionML=[ENU=Completely Shipped;
                                                              NOR=Fullstendig levert;
                                                              SVE=FÑrdigt levererat];
                                                   Editable=No }
    { 5753;   ;Completely Received ;Boolean       ;CaptionML=[ENU=Completely Received;
                                                              NOR=Fullstendig mottatt;
                                                              SVE=FÑrdigt inlevererat];
                                                   Editable=No }
    { 5793;   ;Outbound Whse. Handling Time;DateFormula;
                                                   OnValidate=BEGIN
                                                                IF CurrFieldNo <> 0 THEN
                                                                  TestStatusOpen;
                                                                TransferRoute.CalcReceiptDate("Shipment Date","Receipt Date",
                                                                  "Shipping Time","Outbound Whse. Handling Time","Inbound Whse. Handling Time",
                                                                  "Transfer-from Code","Transfer-to Code","Shipping Agent Code","Shipping Agent Service Code");
                                                                DateConflictCheck;
                                                              END;

                                                   CaptionML=[ENU=Outbound Whse. Handling Time;
                                                              NOR=UtgÜende lagerhÜndteringstid;
                                                              SVE=AvgÜende lagerhanteringstid] }
    { 5794;   ;Inbound Whse. Handling Time;DateFormula;
                                                   OnValidate=BEGIN
                                                                IF CurrFieldNo <> 0 THEN
                                                                  TestStatusOpen;
                                                                TransferRoute.CalcReceiptDate("Shipment Date","Receipt Date",
                                                                  "Shipping Time","Outbound Whse. Handling Time","Inbound Whse. Handling Time",
                                                                  "Transfer-from Code","Transfer-to Code","Shipping Agent Code","Shipping Agent Service Code");
                                                                DateConflictCheck;
                                                              END;

                                                   CaptionML=[ENU=Inbound Whse. Handling Time;
                                                              NOR=InngÜende lagerhÜndteringstid;
                                                              SVE=Ankommande lagerhanteringstid] }
    { 7300;   ;Transfer-from Bin Code;Code20      ;TableRelation="Bin Content"."Bin Code" WHERE (Location Code=FIELD(Transfer-from Code),
                                                                                                 Item No.=FIELD(Item No.),
                                                                                                 Variant Code=FIELD(Variant Code));
                                                   OnValidate=BEGIN
                                                                IF "Transfer-from Bin Code" <> xRec."Transfer-from Bin Code" THEN BEGIN
                                                                  TESTFIELD("Transfer-from Code");
                                                                  IF "Transfer-from Bin Code" <> '' THEN BEGIN
                                                                    GetLocation("Transfer-from Code");
                                                                    Location.TESTFIELD("Bin Mandatory");
                                                                    Location.TESTFIELD("Directed Put-away and Pick",FALSE);
                                                                    GetBin("Transfer-from Code","Transfer-from Bin Code");
                                                                    TESTFIELD("Transfer-from Code",Bin."Location Code");
                                                                    HandleDedicatedBin(TRUE);
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Transfer-from Bin Code;
                                                              NOR=Overfõr fra-hyllekode] }
    { 7301;   ;Transfer-To Bin Code;Code20        ;TableRelation=Bin.Code WHERE (Location Code=FIELD(Transfer-to Code));
                                                   OnValidate=BEGIN
                                                                IF "Transfer-To Bin Code" <> xRec."Transfer-To Bin Code" THEN BEGIN
                                                                  TESTFIELD("Transfer-to Code");
                                                                  IF "Transfer-To Bin Code" <> '' THEN BEGIN
                                                                    GetLocation("Transfer-to Code");
                                                                    Location.TESTFIELD("Bin Mandatory");
                                                                    Location.TESTFIELD("Directed Put-away and Pick",FALSE);
                                                                    GetBin("Transfer-to Code","Transfer-To Bin Code");
                                                                    TESTFIELD("Transfer-to Code",Bin."Location Code");
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Transfer-To Bin Code;
                                                              NOR=Overfõr til-hyllekode] }
    { 99000755;;Planning Flexibility;Option       ;OnValidate=BEGIN
                                                                IF "Planning Flexibility" <> xRec."Planning Flexibility" THEN
                                                                  ReserveTransferLine.UpdatePlanningFlexibility(Rec);
                                                              END;

                                                   CaptionML=[ENU=Planning Flexibility;
                                                              NOR=Planleggingsfleksibilitet;
                                                              SVE=Planeringsflexibilitet];
                                                   OptionCaptionML=[ENU=Unlimited,None;
                                                                    NOR=Ubegrenset,Ingen;
                                                                    SVE=ObegrÑnsad,Ingen];
                                                   OptionString=Unlimited,None }
  }
  KEYS
  {
    {    ;Document No.,Line No.                   ;Clustered=Yes }
    {    ;Transfer-to Code,Status,Derived From Line No.,Item No.,Variant Code,Shortcut Dimension 1 Code,Shortcut Dimension 2 Code,Receipt Date,In-Transit Code;
                                                   SumIndexFields=Qty. in Transit (Base),Outstanding Qty. (Base);
                                                   MaintainSIFTIndex=No }
    {    ;Transfer-from Code,Status,Derived From Line No.,Item No.,Variant Code,Shortcut Dimension 1 Code,Shortcut Dimension 2 Code,Shipment Date,In-Transit Code;
                                                   SumIndexFields=Outstanding Qty. (Base);
                                                   MaintainSIFTIndex=No }
    {    ;Item No.,Variant Code                    }
    {    ;Transfer-to Code,Receipt Date,Item No.,Variant Code }
    {    ;Transfer-from Code,Shipment Date,Item No.,Variant Code }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=You cannot rename a %1.;NOR=Du kan ikke gi nytt navn til en %1.;SVE=Du kan inte byta namn pÜ %1.';
      Text002@1002 : TextConst 'ENU=must not be less than %1;NOR=kan ikke vëre mindre enn %1;SVE=fÜr inte vara mindre Ñn %1';
      Text003@1003 : TextConst 'ENU="Warehouse %1 is required for %2 = %3.";NOR="Lager %1 kreves for %2 = %3."';
      Text004@1004 : TextConst 'ENU=\The entered information may be disregarded by warehouse operations.;NOR=\Det kan vëre at de angitte opplysningene ikke tas med i lageroperasjoner.';
      Text005@1005 : TextConst 'ENU=You cannot ship more than %1 units.;NOR=Du kan ikke levere mer enn %1 enheter.;SVE=Du kan inte leverera mer Ñn %1 stycken.';
      Text006@1006 : TextConst 'ENU=All items have been shipped.;NOR=Alle varene er levert.';
      Text008@1008 : TextConst 'ENU=You cannot receive more than %1 units.;NOR=Du kan ikke motta mer enn %1 enheter.;SVE=Du kan inte erhÜlla mer Ñn %1 stycken.';
      Text009@1009 : TextConst 'ENU=No items are currently in transit.;NOR=Ingen varer i transitt i õyeblikket.';
      Text011@1011 : TextConst 'ENU=Outbound,Inbound;NOR=UtgÜende,InngÜende;SVE=AvgÜende,Ankommande';
      Text012@1020 : TextConst 'ENU=You have changed one or more dimensions on the %1, which is already shipped. When you post the line with the changed dimension to General Ledger, amounts on the Inventory Interim account will be out of balance when reported per dimension.\\Do you want to keep the changed dimension?;NOR=Du har endret Çn eller flere dimensjoner pÜ %1, som allerede er levert. NÜr du bokfõrer linjen med den endrede dimensjonen i Finans, vil belõp pÜ den midlertidige lagerkontoen vëre i ubalanse nÜr de rapporteres per dimensjon.\\Vil du beholde den endrede dimensjonen?;SVE=Du har Ñndrat en eller flera dimensioner i %1, som redan levererats. NÑr du bokfîr raden med den Ñndrade dimensionen i redovisningen kommer summorna pÜ kontot fîr interimlager inte att stÑmma nÑr de rapporteras per dimension.\\Vill du behÜlla den Ñndrade dimensionen?';
      Text013@1017 : TextConst 'ENU=Cancelled.;NOR=Avbrutt.;SVE=Avbruten.';
      TransferRoute@1012 : Record 5742;
      Item@1013 : Record 27;
      TransHeader@1014 : Record 5740;
      Location@1016 : Record 14;
      Bin@1000 : Record 7354;
      DimMgt@1019 : Codeunit 408;
      WhseValidateSourceLine@1022 : Codeunit 5777;
      ReserveTransferLine@1024 : Codeunit 99000836;
      CheckDateConflict@1025 : Codeunit 99000815;
      WMSManagement@1015 : Codeunit 7302;
      UOMMgt@1027 : Codeunit 5402;
      Reservation@1018 : Page 498;
      TrackingBlocked@1007 : Boolean;
      MustUseTrackingErr@1023 : TextConst '@@@="%1 = Form Name, %2 = Value to Enter";ENU=You must use the %1 page to specify the %2, if you use item tracking.;NOR=Du mÜ bruke siden %1 til Ü angi %2 hvis du bruker varesporing.';
      LedgEntryWillBeOpenedMsg@1021 : TextConst '@@@="%1 = Entry No.";ENU=When posting the Applied to Ledger Entry %1 will be opened first.;NOR=Ved bokfõring av utligning Üpnes posten %1 fõrst.';
      ShippingMoreUnitsThanReceivedErr@1010 : TextConst '@@@="%1 = Quantity Value, %2 = Document No.";ENU=You cannot ship more than the %1 units that you have received for document no. %2.;NOR=Du kan ikke levere mer enn de %1 enhetene som er mottatt for bilagsnummer %2.;SVE=Du kan inte utleverera fler Ñn de %1 enheter som har inlevererats fîr dokumentnr %2.';
      AnotherItemWithSameDescrQst@1026 : TextConst '@@@="%1=Item no., %2=item description";ENU=We found an item with the description "%2" (No. %1).\Did you mean to change the current item to %1?;NOR=Vi fant en vare med beskrivelsen %2 (nr. %1).\Vil du endre gjeldende vare til %1?;SVE=Vi hittade en artikel med beskrivningen "%2" (nr %1).\Vill du byta aktuell artikel mot %1?';
      StatusCheckSuspended@1028 : Boolean;
      InventorySetup@1100529000 : Record 313;
      DPA_Management@1100000999 : Codeunit 11012783;

    LOCAL PROCEDURE InitOutstandingQty@16();
    BEGIN
      "Outstanding Quantity" := Quantity - "Quantity Shipped";
      "Outstanding Qty. (Base)" := "Quantity (Base)" - "Qty. Shipped (Base)";
      "Completely Shipped" := (Quantity <> 0) AND ("Outstanding Quantity" = 0);
    END;

    LOCAL PROCEDURE InitQtyToShip@15();
    BEGIN
      //**4PS.sn
      InventorySetup.GET;
      IF InventorySetup."Not Fill Def. Qty. to Ship TO" THEN BEGIN
        "Qty. to Ship" := 0;
        "Qty. to Ship (Base)" := 0;
        EXIT;
      END;
      //**4PS.en

      "Qty. to Ship" := "Outstanding Quantity";
      "Qty. to Ship (Base)" := "Outstanding Qty. (Base)";
    END;

    LOCAL PROCEDURE InitQtyToReceive@2();
    BEGIN
      IF "In-Transit Code" <> '' THEN BEGIN
        "Qty. to Receive" := "Qty. in Transit";
        "Qty. to Receive (Base)" := "Qty. in Transit (Base)";
      END;
      IF ("In-Transit Code" = '') AND ("Quantity Shipped" = "Quantity Received") THEN BEGIN
        "Qty. to Receive" := "Qty. to Ship";
        "Qty. to Receive (Base)" := "Qty. to Ship (Base)";
      END;
    END;

    LOCAL PROCEDURE InitQtyInTransit@4();
    BEGIN
      IF "In-Transit Code" <> '' THEN BEGIN
        "Qty. in Transit" := "Quantity Shipped" - "Quantity Received";
        "Qty. in Transit (Base)" := "Qty. Shipped (Base)" - "Qty. Received (Base)";
      END ELSE BEGIN
        "Qty. in Transit" := 0;
        "Qty. in Transit (Base)" := 0;
      END;
      "Completely Received" := (Quantity <> 0) AND (Quantity = "Quantity Received");
    END;

    [External]
    PROCEDURE ResetPostedQty@6();
    BEGIN
      "Quantity Shipped" := 0;
      "Qty. Shipped (Base)" := 0;
      "Quantity Received" := 0;
      "Qty. Received (Base)" := 0;
      "Qty. in Transit" := 0;
      "Qty. in Transit (Base)" := 0;

      OnAfterResetPostedQty(Rec);
    END;

    LOCAL PROCEDURE CalcBaseQty@14(Qty@1000 : Decimal) : Decimal;
    BEGIN
      TESTFIELD("Qty. per Unit of Measure");
      EXIT(ROUND(Qty * "Qty. per Unit of Measure",UOMMgt.QtyRndPrecision));
    END;

    LOCAL PROCEDURE GetTransHeader@1();
    BEGIN
      GetTransferHeaderNoVerification;

      TransHeader.TESTFIELD("Shipment Date");
      TransHeader.TESTFIELD("Receipt Date");
      TransHeader.TESTFIELD("Transfer-from Code");
      TransHeader.TESTFIELD("Transfer-to Code");
      IF NOT TransHeader."Direct Transfer" AND ("Direct Transfer" = xRec."Direct Transfer") THEN
        TransHeader.TESTFIELD("In-Transit Code");
      "In-Transit Code" := TransHeader."In-Transit Code";
      "Transfer-from Code" := TransHeader."Transfer-from Code";
      "Transfer-to Code" := TransHeader."Transfer-to Code";
      "Shipment Date" := TransHeader."Shipment Date";
      "Receipt Date" := TransHeader."Receipt Date";
      "Shipping Agent Code" := TransHeader."Shipping Agent Code";
      "Shipping Agent Service Code" := TransHeader."Shipping Agent Service Code";
      "Shipping Time" := TransHeader."Shipping Time";
      "Outbound Whse. Handling Time" := TransHeader."Outbound Whse. Handling Time";
      "Inbound Whse. Handling Time" := TransHeader."Inbound Whse. Handling Time";
      Status := TransHeader.Status;
      "Direct Transfer" := TransHeader."Direct Transfer";

      OnAfterGetTransHeader(Rec,TransHeader);
    END;

    LOCAL PROCEDURE GetItem@9();
    BEGIN
      TESTFIELD("Item No.");
      IF "Item No." <> Item."No." THEN
        Item.GET("Item No.");
    END;

    [External]
    PROCEDURE BlockDynamicTracking@23(SetBlock@1000 : Boolean);
    BEGIN
      TrackingBlocked := SetBlock;
      ReserveTransferLine.Block(SetBlock);
    END;

    [External]
    PROCEDURE ShowDimensions@25();
    BEGIN
      DimMgt.SetTableID(DATABASE::"Transfer Line"); //**4PS.n
      "Dimension Set ID" :=
        DimMgt.EditDimensionSet("Dimension Set ID",STRSUBSTNO('%1 %2 %3',TABLECAPTION,"Document No.","Line No."));
      VerifyItemLineDim;
      DimMgt.UpdateGlobalDimFromDimSetID("Dimension Set ID","Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
    END;

    [External]
    PROCEDURE CreateDim@26(Type1@1000 : Integer;No1@1001 : Code[20];Type2@1100528251 : Integer;No2@1100528250 : Code[20]);
    VAR
      SourceCodeSetup@1002 : Record 242;
      TableID@1003 : ARRAY [10] OF Integer;
      No@1004 : ARRAY [10] OF Code[20];
    BEGIN
      SourceCodeSetup.GET;
      TableID[1] := Type1;
      No[1] := No1;

      //**4PS.sn
      TableID[2] := Type2;
      No[2] := No2;
      //**4PS.en

      OnAfterCreateDimTableIDs(Rec,CurrFieldNo,TableID,No);

      "Shortcut Dimension 1 Code" := '';
      "Shortcut Dimension 2 Code" := '';
      "Dimension Set ID" :=
        DimMgt.GetRecDefaultDimID(
          Rec,CurrFieldNo,TableID,No,SourceCodeSetup.Transfer,
          "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code",TransHeader."Dimension Set ID",DATABASE::Item);
    END;

    [External]
    PROCEDURE ValidateShortcutDimCode@29(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateShortcutDimValues(FieldNumber,ShortcutDimCode,"Dimension Set ID");
      VerifyItemLineDim;
    END;

    [External]
    PROCEDURE LookupShortcutDimCode@28(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.LookupDimValueCode(FieldNumber,ShortcutDimCode);
      ValidateShortcutDimCode(FieldNumber,ShortcutDimCode);
    END;

    [External]
    PROCEDURE ShowShortcutDimCode@27(VAR ShortcutDimCode@1000 : ARRAY [8] OF Code[20]);
    BEGIN
      DimMgt.GetShortcutDimensions("Dimension Set ID",ShortcutDimCode);
    END;

    LOCAL PROCEDURE CheckItemAvailable@3(CalledByFieldNo@1000 : Integer);
    VAR
      ItemCheckAvail@1001 : Codeunit 311;
    BEGIN
      IF (CurrFieldNo <> 0) AND
         (CurrFieldNo = CalledByFieldNo) AND
         ("Item No." <> '') AND
         ("Outstanding Quantity" > 0)
      THEN
        IF ItemCheckAvail.TransferLineCheck(Rec) THEN
          ItemCheckAvail.RaiseUpdateInterruptedError;
    END;

    [External]
    PROCEDURE OpenItemTrackingLines@6500(Direction@1002 : 'Outbound,Inbound');
    BEGIN
      TESTFIELD("Item No.");
      TESTFIELD("Quantity (Base)");

      ReserveTransferLine.CallItemTracking(Rec,Direction);
    END;

    LOCAL PROCEDURE TestStatusOpen@37();
    BEGIN
      IF StatusCheckSuspended THEN
        EXIT;

      TESTFIELD("Document No.");
      IF TransHeader."No." <> "Document No." THEN
        TransHeader.GET("Document No.");

      OnBeforeTestStatusOpen(Rec,TransHeader);

      TransHeader.TESTFIELD(Status,TransHeader.Status::Open);

      OnAfterTestStatusOpen(Rec,TransHeader);
    END;

    [External]
    PROCEDURE ShowReservation@10();
    VAR
      OptionNumber@1000 : Integer;
    BEGIN
      TESTFIELD("Item No.");
      CLEAR(Reservation);
      OptionNumber := STRMENU(Text011);
      IF OptionNumber > 0 THEN BEGIN
        Reservation.SetTransLine(Rec,OptionNumber - 1);
        Reservation.RUNMODAL;
      END;
    END;

    [External]
    PROCEDURE UpdateWithWarehouseShipReceive@41();
    BEGIN
      IF Location.RequireShipment("Transfer-from Code") THEN
        VALIDATE("Qty. to Ship",0)
      //**4PS.so
      {
      ELSE
        VALIDATE("Qty. to Ship","Outstanding Quantity");
      }
      //**4PS.eo
      //**4PS.sn
      ELSE BEGIN
        InventorySetup.GET;
        IF InventorySetup."Not Fill Def. Qty. to Ship TO" THEN BEGIN
          IF ("Qty. to Ship" > "Outstanding Quantity") AND (("Qty. to Ship" <> 0)) THEN
            VALIDATE("Qty. to Ship", 0);
        END ELSE
          VALIDATE("Qty. to Ship","Outstanding Quantity");
      END;
      //**4PS.en

      IF Location.RequireReceive("Transfer-to Code") THEN
        VALIDATE("Qty. to Receive",0)
      ELSE BEGIN
        IF "In-Transit Code" <> '' THEN
          VALIDATE("Qty. to Receive","Qty. in Transit");
        IF ("In-Transit Code" = '') AND ("Quantity Shipped" = "Quantity Received") THEN
          VALIDATE("Qty. to Receive","Qty. to Ship");
      END;
    END;

    [External]
    PROCEDURE RenameNo@12(OldNo@1001 : Code[20];NewNo@1002 : Code[20]);
    BEGIN
      RESET;
      SETRANGE("Item No.",OldNo);
      MODIFYALL("Item No.",NewNo,TRUE);
    END;

    LOCAL PROCEDURE CheckWarehouse@47(LocationCode@1003 : Code[10];Receive@1004 : Boolean);
    VAR
      ShowDialog@1001 : ' ,Message,Error';
      DialogText@1000 : Text[50];
      IsHandled@1002 : Boolean;
    BEGIN
      GetLocation(LocationCode);
      IsHandled := FALSE;
      OnBeforeCheckWarehouse(Rec,Location,Receive,IsHandled);
      IF IsHandled THEN
        EXIT;

      IF Location."Directed Put-away and Pick" THEN BEGIN
        ShowDialog := ShowDialog::Error;
        IF Receive THEN
          DialogText := Location.GetRequirementText(Location.FIELDNO("Require Receive"))
        ELSE
          DialogText := Location.GetRequirementText(Location.FIELDNO("Require Shipment"));
      END ELSE BEGIN
        IF Receive AND (Location."Require Receive" OR Location."Require Put-away") THEN BEGIN
          IF WhseValidateSourceLine.WhseLinesExist(
               DATABASE::"Transfer Line",
               1,
               "Document No.",
               "Line No.",
               0,
               Quantity)
          THEN
            ShowDialog := ShowDialog::Error
          ELSE
            IF Location."Require Receive" THEN
              ShowDialog := ShowDialog::Message;
          IF Location."Require Receive" THEN
            DialogText := Location.GetRequirementText(Location.FIELDNO("Require Receive"))
          ELSE
            DialogText := Location.GetRequirementText(Location.FIELDNO("Require Put-away"));
        END;

        IF NOT Receive AND (Location."Require Shipment" OR Location."Require Pick") THEN BEGIN
          IF WhseValidateSourceLine.WhseLinesExist(
               DATABASE::"Transfer Line",
               0,
               "Document No.",
               "Line No.",
               0,
               Quantity)
          THEN
            ShowDialog := ShowDialog::Error
          ELSE
            IF Location."Require Shipment" THEN
              ShowDialog := ShowDialog::Message;
          IF Location."Require Shipment" THEN
            DialogText := Location.GetRequirementText(Location.FIELDNO("Require Shipment"))
          ELSE
            DialogText := Location.GetRequirementText(Location.FIELDNO("Require Pick"));
        END;
      END;

      CASE ShowDialog OF
        ShowDialog::Message:
          MESSAGE(Text003 + Text004,DialogText,FIELDCAPTION("Line No."),"Line No.");
        ShowDialog::Error:
          ERROR(Text003,DialogText,FIELDCAPTION("Line No."),"Line No.");
      END;
    END;

    LOCAL PROCEDURE GetLocation@44(LocationCode@1000 : Code[10]);
    BEGIN
      IF Location.Code <> LocationCode THEN
        Location.GET(LocationCode);
    END;

    LOCAL PROCEDURE GetBin@36(LocationCode@1000 : Code[10];BinCode@1001 : Code[20]);
    BEGIN
      IF BinCode = '' THEN
        CLEAR(Bin)
      ELSE
        IF Bin.Code <> BinCode THEN
          Bin.GET(LocationCode,BinCode);
    END;

    LOCAL PROCEDURE GetDefaultBin@5(FromLocationCode@1000 : Code[10];ToLocationCode@1001 : Code[10]);
    BEGIN
      IF (FromLocationCode <> '') AND ("Item No." <> '') THEN BEGIN
        GetLocation(FromLocationCode);
        IF Location."Bin Mandatory" AND NOT Location."Directed Put-away and Pick" THEN BEGIN
          WMSManagement.GetDefaultBin("Item No.","Variant Code",FromLocationCode,"Transfer-from Bin Code");
          HandleDedicatedBin(FALSE);
        END;
      END;

      IF (ToLocationCode <> '') AND ("Item No." <> '') THEN BEGIN
        GetLocation(ToLocationCode);
        IF Location."Bin Mandatory" AND NOT Location."Directed Put-away and Pick" THEN
          WMSManagement.GetDefaultBin("Item No.","Variant Code",ToLocationCode,"Transfer-To Bin Code");
      END;
    END;

    [External]
    PROCEDURE IsInbound@8() : Boolean;
    BEGIN
      EXIT("Quantity (Base)" < 0);
    END;

    LOCAL PROCEDURE HandleDedicatedBin@7(IssueWarning@1000 : Boolean);
    VAR
      WhseIntegrationMgt@1001 : Codeunit 7317;
    BEGIN
      IF NOT IsInbound AND ("Quantity (Base)" <> 0) THEN
        WhseIntegrationMgt.CheckIfBinDedicatedOnSrcDoc("Transfer-from Code","Transfer-from Bin Code",IssueWarning);
    END;

    [External]
    PROCEDURE FilterLinesWithItemToPlan@70(VAR Item@1000 : Record 27;IsReceipt@1001 : Boolean;IsSupplyForPlanning@1002 : Boolean);
    BEGIN
      RESET;
      SETCURRENTKEY("Item No.");
      SETRANGE("Item No.",Item."No.");
      SETFILTER("Variant Code",Item.GETFILTER("Variant Filter"));
      IF NOT IsSupplyForPlanning THEN
        SETRANGE("Derived From Line No.",0);
      IF IsReceipt THEN BEGIN
        SETFILTER("Transfer-to Code",Item.GETFILTER("Location Filter"));
        SETFILTER("Receipt Date",Item.GETFILTER("Date Filter"))
      END ELSE BEGIN
        SETFILTER("Transfer-from Code",Item.GETFILTER("Location Filter"));
        SETFILTER("Shipment Date",Item.GETFILTER("Date Filter"));
        SETFILTER("Outstanding Qty. (Base)",'<>0');
      END;
      SETFILTER("Shortcut Dimension 1 Code",Item.GETFILTER("Global Dimension 1 Filter"));
      SETFILTER("Shortcut Dimension 2 Code",Item.GETFILTER("Global Dimension 2 Filter"));
    END;

    [External]
    PROCEDURE FindLinesWithItemToPlan@68(VAR Item@1000 : Record 27;IsReceipt@1001 : Boolean;IsSupplyForPlanning@1002 : Boolean) : Boolean;
    BEGIN
      FilterLinesWithItemToPlan(Item,IsReceipt,IsSupplyForPlanning);
      EXIT(FIND('-'));
    END;

    [External]
    PROCEDURE LinesWithItemToPlanExist@67(VAR Item@1000 : Record 27;IsReceipt@1001 : Boolean) : Boolean;
    BEGIN
      FilterLinesWithItemToPlan(Item,IsReceipt,FALSE);
      EXIT(NOT ISEMPTY);
    END;

    LOCAL PROCEDURE VerifyItemLineDim@87();
    BEGIN
      IF IsShippedDimChanged THEN
        ConfirmShippedDimChange;
    END;

    [External]
    PROCEDURE IsShippedDimChanged@105() : Boolean;
    BEGIN
      EXIT(("Dimension Set ID" <> xRec."Dimension Set ID") AND
        (("Quantity Shipped" <> 0) OR ("Qty. Shipped (Base)" <> 0)));
    END;

    [External]
    PROCEDURE ConfirmShippedDimChange@106() : Boolean;
    BEGIN
      IF NOT CONFIRM(Text012,FALSE,TABLECAPTION) THEN
        ERROR(Text013);

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE SelectItemEntry@11(CurrentFieldNo@1000 : Integer);
    VAR
      ItemLedgEntry@1001 : Record 32;
      TransferLine2@1002 : Record 5741;
    BEGIN
      ItemLedgEntry.SETRANGE("Item No.","Item No.");
      IF "Transfer-from Code" <> '' THEN
        ItemLedgEntry.SETRANGE("Location Code","Transfer-from Code");
      ItemLedgEntry.SETRANGE("Variant Code","Variant Code");

      ItemLedgEntry.SETRANGE(Positive,TRUE);
      ItemLedgEntry.SETRANGE(Open,TRUE);

      IF PAGE.RUNMODAL(PAGE::"Item Ledger Entries",ItemLedgEntry) = ACTION::LookupOK THEN BEGIN
        TransferLine2 := Rec;
        TransferLine2.VALIDATE("Appl.-to Item Entry",ItemLedgEntry."Entry No.");
        CheckItemAvailable(CurrentFieldNo);
        Rec := TransferLine2;
      END;
    END;

    [External]
    PROCEDURE SuspendStatusCheck@39(Suspend@1000 : Boolean);
    BEGIN
      StatusCheckSuspended := Suspend;
    END;

    LOCAL PROCEDURE GetTransferHeaderNoVerification@112();
    BEGIN
      TESTFIELD("Document No.");
      IF "Document No." <> TransHeader."No." THEN
        TransHeader.GET("Document No.");
    END;

    [External]
    PROCEDURE DateConflictCheck@13();
    BEGIN
      IF NOT TrackingBlocked THEN
        CheckDateConflict.TransferLineCheck(Rec);
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCreateDimTableIDs@138(VAR TransferLine@1000 : Record 5741;FieldNo@1001 : Integer;VAR TableID@1003 : ARRAY [10] OF Integer;VAR No@1002 : ARRAY [10] OF Code[20]);
    BEGIN
    END;

    LOCAL PROCEDURE ItemExists@17(ItemNo@1000 : Code[20]) : Boolean;
    VAR
      IEItem@1001 : Record 27;
    BEGIN
      EXIT(IEItem.GET(ItemNo));
    END;

    [External]
    PROCEDURE RowID1@48(Direction@1001 : 'Outbound,Inbound') : Text[250];
    VAR
      ItemTrackingMgt@1000 : Codeunit 6500;
    BEGIN
      EXIT(ItemTrackingMgt.ComposeRowID(DATABASE::"Transfer Line",Direction,"Document No.",'',"Derived From Line No.","Line No."));
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterGetTransHeader@19(VAR TransferLine@1000 : Record 5741;TransferHeader@1001 : Record 5740);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterResetPostedQty@20(VAR TransferLine@1000 : Record 5741);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterTestStatusOpen@22(VAR TransferLine@1000 : Record 5741;TransferHeader@1001 : Record 5740);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCheckWarehouse@30(TransferLine@1000 : Record 5741;Location@1001 : Record 14;Receive@1002 : Boolean;VAR IsHandled@1003 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeTestStatusOpen@21(VAR TransferLine@1000 : Record 5741;TransferHeader@1001 : Record 5740);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnValidateItemNoOnAfterInitLine@24(VAR TransferLine@1000 : Record 5741;TempTransferLine@1001 : TEMPORARY Record 5741);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnValidateQuantityOnBeforeTransLineVerifyChange@18(VAR TransferLine@1000 : Record 5741;xTransferLine@1001 : Record 5741;VAR IsHandled@1002 : Boolean);
    BEGIN
    END;

    LOCAL PROCEDURE DPA_OnInsert@1100000991();
    BEGIN
      //**4PS
      IF NOT DPA_IsActive THEN
        EXIT;

      IF "Shortcut Dimension 1 Code" <> '' THEN
        VALIDATE("Shortcut Dimension 1 Code");
    END;

    PROCEDURE DPA_GetFieldNoDepartmentFilter@1100000993() : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      EXIT(0);
    END;

    PROCEDURE DPA_GetFiltersFltrGrp@1100000997(FilterGroupNo@1100525000 : Integer) FilterText : Text[250];
    BEGIN
      //**4PS Generated Code - Do Not Change
      FILTERGROUP(FilterGroupNo);
      FilterText := COPYSTR(GETFILTERS, 1, MAXSTRLEN(FilterText));
      FILTERGROUP(0);
    END;

    PROCEDURE DPA_SetFilters@1100000998(Context@1100525000 : Text[30]) : Boolean;
    VAR
      RecRef@1100525003 : RecordRef;
      i@1100525004 : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN
        EXIT;

      RecRef.GETTABLE(Rec);
      DPA_Management.SetDPAFilterFromContext(
        RecRef,
        Context,
        DPA_GetFieldNoDepartmentCode,
        DPA_GetFieldNoDepartmentFilter );

      FOR i := 10 DOWNTO 0 DO BEGIN
        RecRef.FILTERGROUP(i);
        FILTERGROUP(i);
        SETVIEW(RecRef.GETVIEW(FALSE));
      END;

      EXIT(DPA_Management.IsHardFilter);
    END;

    LOCAL PROCEDURE DPA_IsActive@1100000999() : Boolean;
    VAR
      DepartmentAuthorizObject@1100526000 : Record 11012466;
      FPSLicenseManagement@1100528500 : Codeunit 11229289;
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT FPSLicenseManagement.LicenseAndReadPermissionForTable(DepartmentAuthorizObject) THEN
        EXIT(FALSE);

      IF NOT DPA_Management.AuthorizationActive THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    PROCEDURE DPA_FindRecWithDPAFilterAppld@1100000989();
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN BEGIN
        FIND;
        EXIT;
      END;

      IF NOT DPA_Management.HasTableHardFilter(5741) THEN BEGIN
        FIND;
        EXIT;
      END;

      DPA_SetFilters('Table 5741');
      IF NOT FIND THEN
        DPA_Management.RaiseDPAErrorForTable(TABLECAPTION);
    END;

    PROCEDURE DPA_ChkDepartmentCodeAllowed@1100000990(IsHardFilter@1100526000 : Boolean);
    VAR
      DimensionManagement@1100526001 : Codeunit 408;
    BEGIN
      //**4PS Generated Code - Do Not Change
      DimensionManagement.CheckDepartmAuthWithFilterType(
        DATABASE::"Transfer Line", "Shortcut Dimension 1 Code", IsHardFilter);
    END;

    PROCEDURE DPA_GetFieldNoDepartmentCode@1100000994() : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      EXIT(FIELDNO("Shortcut Dimension 1 Code"));
    END;

    PROCEDURE DPA_SetDefaultDepartment@1100000996();
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN
        EXIT;

      DPA_Management.SetDepartmentCodeUser(TABLENAME, "Shortcut Dimension 1 Code");
    END;

    PROCEDURE SpreadOnBasisOfBinContent@1100525000();
    VAR
      BinContent@1100525000 : Record 7302;
      QuantityToSplit@1100525002 : Decimal;
      QuantitySplit@1100525003 : Decimal;
    BEGIN
      //**4PS
      IF ("Transfer-from Code" = '') OR ("Transfer-from Bin Code" = '') THEN
        EXIT;

      BinContent.SETRANGE("Location Code","Transfer-from Code");
      BinContent.SETRANGE("Bin Code","Transfer-from Bin Code");
      BinContent.SETRANGE("Item No.","Item No.");
      BinContent.SETRANGE("Variant Code","Variant Code");
      BinContent.SETRANGE("Unit of Measure Code","Unit of Measure Code");
      IF BinContent.FIND('-') THEN BEGIN
        BinContent.CALCFIELDS(Quantity);
        IF Quantity <= BinContent.Quantity THEN
          EXIT;
      END;
      QuantityToSplit := Quantity - BinContent.Quantity;

      BinContent.SETFILTER("Bin Code",'<>%1',"Transfer-from Bin Code");
      IF BinContent.FINDSET THEN
        REPEAT
          BinContent.CALCFIELDS(Quantity);
          IF BinContent.Quantity > 0 THEN
            IF QuantityToSplit > BinContent.Quantity THEN BEGIN
              InsertCopyOfTransferLine(BinContent.Quantity,BinContent."Bin Code");
              QuantitySplit := QuantitySplit + BinContent.Quantity;
              QuantityToSplit := QuantityToSplit - BinContent.Quantity;
            END ELSE BEGIN
              InsertCopyOfTransferLine(QuantityToSplit,BinContent."Bin Code");
              QuantitySplit := QuantitySplit + QuantityToSplit;
              QuantityToSplit := 0;
            END;
        UNTIL (BinContent.NEXT = 0) OR (QuantityToSplit = 0);

      IF QuantitySplit > 0 THEN BEGIN
        VALIDATE(Quantity,Quantity-QuantitySplit);
        MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE InsertCopyOfTransferLine@1100525002(QuantityToInsert@1100525001 : Decimal;BinCode@1100525002 : Code[20]);
    VAR
      TransferLine@1100525003 : Record 5741;
    BEGIN
      //**4PS
      TransferLine := Rec;
      TransferLine."Transfer-from Bin Code" := BinCode;
      TransferLine.VALIDATE(Quantity,QuantityToInsert);
      TransferLine.INSERT(TRUE);
    END;

    PROCEDURE AdaptQtyToShipToInventory@1100528400(TransferHeader@1100528401 : Record 5740);
    VAR
      Item@1100528402 : Record 27;
    BEGIN
      //**4PS
      IF NOT Item.GET("Item No.") THEN
        EXIT;

      IF "Unit of Measure Code" <> '' THEN
        TESTFIELD("Unit of Measure Code", Item."Base Unit of Measure");

      Item.SETFILTER("Variant Filter", "Variant Code");
      Item.SETFILTER("Location Filter", "Transfer-from Code");
      Item.CALCFIELDS(Inventory);

      IF "Qty. to Ship" > Item.Inventory THEN BEGIN
        VALIDATE("Qty. to Ship", Item.Inventory);
        MODIFY(TRUE);
      END;
    END;

    BEGIN
    END.
  }
}

