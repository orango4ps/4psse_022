OBJECT Codeunit 11012843 Vessel Search Management
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      VesselSearchWordDetail@1210190000 : Record 11071741;
      SearchWord@1100529800 : Record 11071740;
      TempSearchResultRec@1210190008 : TEMPORARY Record 11071739;
      Delimiters@1210190002 : Text[250];
      LineNo@1210190005 : Integer;
      i@1210190001 : Integer;
      Status@1100485002 : Dialog;
      maxrec@1100485001 : Integer;
      currec@1100485000 : Integer;
      Text002@1100485003 : TextConst 'DEU=Besch„ftigt mit  #1################# @2@@@@@@@@@@@@@;ENU=Busy with       #1################# @2@@@@@@@@@@@@@;NLD=Bezig met       #1################# @2@@@@@@@@@@@@@;NOR=Opptatt med       #1################# @2@@@@@@@@@@@@@;SVE=Upptagen med    #1################# @2@@@@@@@@@@@@@';
      RecRef@1100485006 : RecordRef;
      FldRef@1100485005 : FieldRef;
      FieldRec@1100485004 : Record 2000000041;
      SelectFldRec@1100485007 : Record 11020263;

    PROCEDURE ParseField@1210190003(TextToParse@1210190001 : Text[80];VesselNo@1100529800 : Code[20];VesselNameHistLineNo@1100529801 : Integer;ServLoc@1210190012 : Code[20];ServiceObjectNo@1100528600 : Code[20];ServLine@1100485000 : Integer;FieldNumber@1210190003 : Integer);
    VAR
      WordPos@1210190005 : Integer;
      StartPos@1210190006 : Integer;
      SeparateWord@1210190009 : Text[80];
    BEGIN
      Delimiters := ' @*?';

      DeletePreviousDetails(
        VesselNo, VesselNameHistLineNo, ServLoc, ServiceObjectNo, ServLine, FieldNumber);
      TextToParse := LOWERCASE(TextToParse);

      WordPos := 1;
      StartPos := 1;
      FOR i := 1 TO STRLEN(TextToParse) DO BEGIN
        CASE STRPOS(Delimiters,FORMAT(TextToParse[i])) OF
          0: ;
          1..STRLEN(' @*?'):
            BEGIN
              IF i > StartPos THEN BEGIN
                SeparateWord := COPYSTR(TextToParse, StartPos, i - StartPos);
                SearchWord.InsertWord(
                  SeparateWord, VesselNo, VesselNameHistLineNo, ServLoc,
                  ServiceObjectNo, ServLine, FieldNumber, WordPos);
                WordPos := WordPos + 1;
              END;
              StartPos := i + 1;
            END;
            ELSE BEGIN
              IF i > StartPos THEN BEGIN
                SeparateWord := COPYSTR(TextToParse, StartPos, i - StartPos);
                SearchWord.InsertWord(
                  SeparateWord, VesselNo, VesselNameHistLineNo, ServLoc,
                  ServiceObjectNo, ServLine, FieldNumber, WordPos);
                WordPos := WordPos + 1;
              END;
              SeparateWord := FORMAT(TextToParse[i]);
              SearchWord.InsertWord(
                SeparateWord, VesselNo, VesselNameHistLineNo, ServLoc,
                ServiceObjectNo, ServLine, FieldNumber, WordPos);
              WordPos := WordPos + 1;
              StartPos := i + 1;
            END;
          END;
      END;
      IF STRLEN(TextToParse) >= StartPos THEN BEGIN
        SeparateWord := COPYSTR(TextToParse, StartPos, STRLEN(TextToParse) - StartPos + 1);
        SearchWord.InsertWord(
          SeparateWord, VesselNo, VesselNameHistLineNo, ServLoc,
          ServiceObjectNo, ServLine, FieldNumber, WordPos);
      END;
    END;

    PROCEDURE DeletePreviousDetails@1210190006(VesselNo@1100529800 : Code[20];VesselNameHistLineNo@1100529801 : Integer;ServLoc@1210190010 : Code[20];ServiceObjectNo@1100528600 : Code[20];ServLine@1100485000 : Integer;FieldNumber@1210190005 : Integer);
    BEGIN
      WITH VesselSearchWordDetail DO BEGIN
        SETCURRENTKEY("Vessel No.", "Vessel Name History Line No.", "Service Location No.",
          "Service Object No.", "Service Line No.", "Field No.");
        SETRANGE("Vessel No.", VesselNo);
        SETRANGE("Vessel Name History Line No.", VesselNameHistLineNo);
        SETRANGE("Service Location No.", ServLoc);
        SETRANGE("Service Object No.", ServiceObjectNo);
        SETRANGE("Service Line No.", ServLine);
        SETRANGE("Field No.", FieldNumber);
        DELETEALL(TRUE);
      END;
    END;

    PROCEDURE ParseVessel@1100529800(Vessel@1100529800 : Record 11071737);
    BEGIN
      WITH Vessel DO BEGIN
        SelectFldRec.SETRANGE(Type, SelectFldRec.Type::Vessel);
        SelectFldRec.SETRANGE("Table No.", DATABASE::Vessel);
        IF SelectFldRec.FINDSET THEN BEGIN
          REPEAT
            RecRef.GETTABLE(Vessel);
            ParseField(AnyTableFieldValue(SelectFldRec."Table No.", SelectFldRec."Field No."),
              "No.", 0, '', '', 0, SelectFldRec."Field No.");
          UNTIL SelectFldRec.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE ParseVesselNameHistory@1100529801(NameHistoryVessel@1100529800 : Record 11071732);
    BEGIN
      WITH NameHistoryVessel DO BEGIN
        SelectFldRec.SETRANGE(Type, SelectFldRec.Type::Vessel);
        SelectFldRec.SETRANGE("Table No.", DATABASE::"Name History Vessel");
        IF SelectFldRec.FINDSET THEN BEGIN
          REPEAT
            RecRef.GETTABLE(NameHistoryVessel);
            ParseField(AnyTableFieldValue(SelectFldRec."Table No.", SelectFldRec."Field No."),
              "Vessel No.", "Line No.", '', '', 0, SelectFldRec."Field No.");
          UNTIL SelectFldRec.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE ParseServiceLocation@1210190005(ServLocRec@1210190000 : Record 11012801;VesselNo@1100529802 : Code[20];VesselHistNameLineNo@1100529800 : Integer);
    BEGIN
      WITH ServLocRec DO BEGIN
        SelectFldRec.SETRANGE(Type, SelectFldRec.Type::Vessel);
        SelectFldRec.SETRANGE("Table No.", DATABASE::"Service Location");
        IF SelectFldRec.FINDSET THEN BEGIN
          REPEAT
            RecRef.GETTABLE(ServLocRec);
            ParseField(AnyTableFieldValue(SelectFldRec."Table No.", SelectFldRec."Field No."),
              VesselNo, VesselHistNameLineNo, "No.", '', 0, SelectFldRec."Field No.");
          UNTIL SelectFldRec.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE ParseServiceObject@1100528600(ServiceObject@1210190000 : Record 11071691;VesselNo@1100529801 : Code[20];VesselHistNameLineNo@1100529800 : Integer);
    BEGIN
      WITH ServiceObject DO BEGIN
        SelectFldRec.SETRANGE(Type, SelectFldRec.Type::Vessel);
        SelectFldRec.SETRANGE("Table No.", DATABASE::"Service-Object");
        IF SelectFldRec.FINDSET THEN BEGIN
          REPEAT
            RecRef.GETTABLE(ServiceObject);
            ParseField(AnyTableFieldValue(SelectFldRec."Table No.", SelectFldRec."Field No."),
              VesselNo, VesselHistNameLineNo, "Service Location No.", '', 0, SelectFldRec."Field No.");
          UNTIL SelectFldRec.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE CombineSearchResult@1210190002(searchstring@1210190000 : Text[250]);
    BEGIN
      IF TempSearchResultRec.FINDSET THEN BEGIN
        REPEAT
          IF NOT CheckCombination(searchstring) THEN
            TempSearchResultRec.DELETE;
        UNTIL TempSearchResultRec.NEXT = 0;
      END;
      VesselSearchWordDetail.RESET;
    END;

    PROCEDURE CheckCombination@1210190009(searchstring@1210190002 : Text[250]) Combination : Boolean;
    VAR
      CombBln@1210190001 : Boolean;
    BEGIN
      CombBln := TRUE;
      SearchWord.SETFILTER("Search Argument", searchstring);
      IF SearchWord.FINDSET THEN BEGIN
        REPEAT
          VesselSearchWordDetail.SETRANGE("No.", SearchWord."No.");
          VesselSearchWordDetail.SETRANGE("Vessel No.", TempSearchResultRec."Vessel No.");
          VesselSearchWordDetail.SETRANGE("Vessel Name History Line No.", TempSearchResultRec."Vessel Name History Line No.");
          VesselSearchWordDetail.SETRANGE("Service Location No.", TempSearchResultRec."Service Location No.");
          VesselSearchWordDetail.SETRANGE("Service Object No.", TempSearchResultRec."Object No.");
          VesselSearchWordDetail.SETRANGE("Service Line No.", TempSearchResultRec."Line No.");
          IF NOT VesselSearchWordDetail.FINDFIRST THEN
            CombBln := FALSE;
        UNTIL SearchWord.NEXT = 0;
      END;
      EXIT(CombBln);
    END;

    PROCEDURE SearchItems@1210190007(searchstring@1210190001 : Text[250];Combine@1210190007 : Boolean;VAR TmpResultRec@1210190009 : TEMPORARY Record 11071739);
    BEGIN
      Delimiters := ' @?';
      searchstring := LOWERCASE(searchstring);
      LineNo := 10000;

      TempSearchResultRec.RESET;
      TempSearchResultRec.DELETEALL;
      TmpResultRec.RESET;
      TmpResultRec.DELETEALL;

      InsertTempSearchResult(searchstring);
      IF Combine THEN
        CombineSearchResult(searchstring);

      IF TempSearchResultRec.FINDSET THEN BEGIN
        REPEAT
          TmpResultRec := TempSearchResultRec;
          TmpResultRec.INSERT;
        UNTIL TempSearchResultRec.NEXT = 0;
      END;
    END;

    PROCEDURE InsertTempSearchResult@1210190012(SearchSubString@1210190000 : Text[250]);
    VAR
      Vessel@1100529800 : Record 11071737;
      NameHistoryVessel@1100529801 : Record 11071732;
      ServLocRec@1210190002 : Record 11012801;
      ServiceObject@1100528600 : Record 11071691;
      ServiceSetup@1100528601 : Record 11012800;
    BEGIN
      Status.OPEN(Text002);

      ServiceSetup.GET;
      SearchWord.SETFILTER("Search Argument", SearchSubString);
      IF SearchWord.FINDSET THEN BEGIN
        currec := 0;
        maxrec := SearchWord.COUNT;
        REPEAT
          VesselSearchWordDetail.SETRANGE("No.", SearchWord."No.");
          IF VesselSearchWordDetail.FINDSET THEN BEGIN
            REPEAT
              IF NOT TempSearchResultRec.GET(VesselSearchWordDetail."Vessel No.",
                                             VesselSearchWordDetail."Vessel Name History Line No.",
                                             VesselSearchWordDetail."Service Location No.",
                                             VesselSearchWordDetail."Service Object No.",
                                             VesselSearchWordDetail."Service Line No.") THEN
              BEGIN
                TempSearchResultRec.INIT;
                TempSearchResultRec."Vessel No." := VesselSearchWordDetail."Vessel No.";
                TempSearchResultRec."Vessel Name History Line No." := VesselSearchWordDetail."Vessel Name History Line No.";
                TempSearchResultRec."Service Location No." := VesselSearchWordDetail."Service Location No.";
                TempSearchResultRec."Object No." := VesselSearchWordDetail."Service Object No.";
                TempSearchResultRec."Line No." := VesselSearchWordDetail."Service Line No.";

                // Vessel
                IF Vessel.GET(VesselSearchWordDetail."Vessel No.") THEN
                  TempSearchResultRec."Vessel Name" := Vessel.Name;

                // Vessel Name History
                IF VesselSearchWordDetail."Vessel Name History Line No." <> 0 THEN BEGIN
                  NameHistoryVessel.SETRANGE("Vessel No.", VesselSearchWordDetail."Vessel No.");
                  NameHistoryVessel.SETRANGE("Line No.", VesselSearchWordDetail."Vessel Name History Line No.");
                  IF NameHistoryVessel.FINDFIRST THEN
                    TempSearchResultRec."Vessel Name" := NameHistoryVessel."Vessel Name";
                END;

                // Service Location
                IF ServLocRec.GET(VesselSearchWordDetail."Service Location No.") THEN BEGIN
                  TempSearchResultRec."Description Location" := ServLocRec.Description;
                  TempSearchResultRec.Address := ServLocRec.Address;
                  TempSearchResultRec."Address 2" := ServLocRec."Address 2";
                  TempSearchResultRec."Post Code" := ServLocRec."Post Code";
                  TempSearchResultRec.City := ServLocRec.City;
                  TempSearchResultRec."Territory Code" := ServLocRec."Territory Code";
                  TempSearchResultRec."Phone No." := ServLocRec."Phone No.";
                  TempSearchResultRec."Phone No. 2" := ServLocRec."Phone No. 2";
                  TempSearchResultRec."Mobile Phone No." := ServLocRec."Mobile Phone No.";
                  TempSearchResultRec."Customer No." := ServLocRec."Customer No.";
                  TempSearchResultRec."Contact Name" := ServLocRec."Contact Name";
                END;

                IF ServiceObject.GET(VesselSearchWordDetail."Service Object No.") THEN BEGIN
                  TempSearchResultRec."Object No." := ServiceObject."No.";
                  TempSearchResultRec."Object Description" := ServiceObject.Description;
                  TempSearchResultRec."Object Description 2" := ServiceObject."Description 2";
                  IF ServiceObject.Building <> '' THEN
                    TempSearchResultRec.Building := ServiceObject.Building;
                  IF ServiceObject.Floor <> '' THEN
                    TempSearchResultRec.Floor := ServiceObject.Floor;
                  IF ServiceObject.Room <> '' THEN
                    TempSearchResultRec.Room := ServiceObject.Room;
                  IF ServiceObject."Object Customer" <> '' THEN
                    TempSearchResultRec."Object Customer" := ServiceObject."Object Customer";
                END;

                TempSearchResultRec.INSERT;
              END;
            UNTIL VesselSearchWordDetail.NEXT =0;
          END;

          currec := currec + 1;
          Status.UPDATE(1, SearchWord."Search Argument");
          Status.UPDATE(2, ROUND(currec / maxrec * 10000, 1));
        UNTIL SearchWord.NEXT = 0;
      END;

      Status.CLOSE;
    END;

    PROCEDURE AnyTableFieldValue@1100485001(TableNo@1100485000 : Integer;FieldNo@1100485001 : Integer) : Text[250];
    VAR
      lvText000@1100485002 : TextConst 'DEU=******************************;ENU=******************************;NLD=******************************;NOR=******************************;SVE=******************************';
      lvDec@1100485003 : Decimal;
    BEGIN
      FldRef := RecRef.FIELD(FieldNo);
      IF NOT FieldRec.GET(TableNo,FieldNo) THEN
        EXIT(lvText000);

      IF FieldRec.Class = FieldRec.Class::FlowField THEN
        FldRef.CALCFIELD;

      IF (FieldRec.Type = FieldRec.Type::Decimal) OR
         (FieldRec.Type = FieldRec.Type::Integer) THEN
      BEGIN
        lvDec := FldRef.VALUE;
        EXIT(FORMAT(lvDec,0,'<Precision,2:><Standard format,0>'))
      END;

      EXIT(FORMAT(FldRef));
    END;

    BEGIN
    END.
  }
}

