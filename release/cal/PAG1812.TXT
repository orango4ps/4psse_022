OBJECT Page 1812 Item Approval WF Setup Wizard
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Item Approval Workflow Setup;
               NOR=Oppsett av arbeidsflyt for varegodkjenning;
               SVE=Konfiguration av arbetsfl”de f”r artikelgodk„nnande];
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table1804;
    PageType=NavigatePage;
    SourceTableTemporary=Yes;
    ShowFilter=No;
    OnInit=BEGIN
             IF NOT GET THEN BEGIN
               INIT;
               SetDefaultValues;
               INSERT;
             END;
             LoadTopBanners;
           END;

    OnOpenPage=BEGIN
                 ShowIntroStep;
               END;

    OnQueryClosePage=VAR
                       AssistedSetup@1001 : Record 1803;
                     BEGIN
                       IF CloseAction = ACTION::OK THEN
                         IF AssistedSetup.GetStatus(PAGE::"Item Approval WF Setup Wizard") = AssistedSetup.Status::"Not Completed" THEN
                           IF NOT CONFIRM(NAVNotSetUpQst,FALSE) THEN
                             ERROR('');
                     END;

    ActionList=ACTIONS
    {
      { 8       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 15      ;1   ;Action    ;
                      Name=PreviousPage;
                      CaptionML=[ENU=Back;
                                 NOR=Tilbake;
                                 SVE=Bak†t];
                      ApplicationArea=#Suite;
                      Enabled=BackEnabled;
                      InFooterBar=Yes;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 NextStep(TRUE);
                               END;
                                }
      { 14      ;1   ;Action    ;
                      Name=NextPage;
                      CaptionML=[ENU=Next;
                                 NOR=Neste;
                                 SVE=N„sta];
                      ApplicationArea=#Suite;
                      Enabled=NextEnabled;
                      InFooterBar=Yes;
                      Image=NextRecord;
                      OnAction=BEGIN
                                 NextStep(FALSE);
                               END;
                                }
      { 9       ;1   ;Action    ;
                      Name=Finish;
                      CaptionML=[ENU=Finish;
                                 NOR=Fullf›r;
                                 SVE=Slutf”r];
                      ApplicationArea=#Suite;
                      Enabled=FinishEnabled;
                      InFooterBar=Yes;
                      Image=Approve;
                      OnAction=VAR
                                 AssistedSetup@1004 : Record 1803;
                                 ApprovalWorkflowSetupMgt@1001 : Codeunit 1804;
                               BEGIN
                                 ApprovalWorkflowSetupMgt.ApplyItemWizardUserInput(Rec);
                                 AssistedSetup.SetStatus(PAGE::"Item Approval WF Setup Wizard",AssistedSetup.Status::Completed);

                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 96  ;1   ;Group     ;
                Visible=TopBannerVisible AND NOT DoneVisible;
                Editable=FALSE;
                GroupType=Group }

    { 97  ;2   ;Field     ;
                ApplicationArea=#Suite;
                SourceExpr=MediaResourcesStandard."Media Reference";
                Editable=FALSE;
                ShowCaption=No }

    { 98  ;1   ;Group     ;
                Visible=TopBannerVisible AND DoneVisible;
                Editable=FALSE;
                GroupType=Group }

    { 99  ;2   ;Field     ;
                ApplicationArea=#Suite;
                SourceExpr=MediaResourcesDone."Media Reference";
                Editable=FALSE;
                ShowCaption=No }

    { 11  ;1   ;Group     ;
                Name=Step1;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                Visible=IntroVisible;
                GroupType=Group }

    { 18  ;2   ;Group     ;
                Name=Para1.1;
                CaptionML=[ENU=Welcome to Item Approval Workflow Setup;
                           NOR=Velkommen til oppsettet av arbeidsflyt for varegodkjenning;
                           SVE=V„lkommen till Konfiguration av arbetsfl”de f”r artikelgodk„nnande];
                GroupType=Group }

    { 19  ;3   ;Group     ;
                Name=Para1.1.1;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                GroupType=Group;
                InstructionalTextML=[ENU=You can create approval workflows that automatically notify an approver when a user tries to create or change an item card.;
                                     NOR=Du kan opprette arbeidsflyter for godkjenning som automatisk varsler en godkjenner n†r en bruker pr›ver † opprette eller endre et varekort.;
                                     SVE=Du kan skapa arbetsfl”den f”r godk„nnande som automatiskt meddelar en godk„nnare n„r en anv„ndare f”rs”ker skapa eller „ndra ett artikelkort.] }

    { 21  ;2   ;Group     ;
                Name=Para1.2;
                CaptionML=[ENU=Let's go!;
                           NOR=La oss sette i gang!;
                           SVE=S„tt ig†ng!];
                GroupType=Group;
                InstructionalTextML=[ENU=Choose Next to specify basic approval workflow settings for changing an item card.;
                                     NOR=Velg Neste for † angi grunnleggende innstillinger for godkjenningsarbeidsflyt for endring av varekort.;
                                     SVE=V„lj N„sta om du vill ange grundl„ggande inst„llningar f”r arbetsfl”de f”r godk„nnande f”r „ndring av ett artikelkort.] }

    { 67  ;1   ;Group     ;
                Name=Step2;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                Visible=ItemApproverSetupVisible;
                GroupType=Group }

    { 5   ;2   ;Group     ;
                Name=Para2.1;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                GroupType=Group }

    { 4   ;3   ;Group     ;
                Name=Para2.1.1;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                GroupType=Group;
                InstructionalTextML=[ENU=Choose who is authorized to approve or reject new or changed item cards.;
                                     NOR=Velg hvem som er autorisert for † godkjenne eller avvise nye eller endrede varekort.;
                                     SVE=V„lj vem som „r beh”rig att godk„nna eller avvisa nya eller „ndrade artikelkort.] }

    { 2   ;4   ;Field     ;
                CaptionML=[ENU=Approver;
                           NOR=Godkjenner;
                           SVE=Godk„nnare];
                ApplicationArea=#Suite;
                SourceExpr="Approver ID";
                LookupPageID=Approval User Setup;
                OnValidate=BEGIN
                             CanEnableNext;
                           END;
                            }

    { 66  ;2   ;Group     ;
                Name=Para2.2;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                GroupType=Group }

    { 13  ;3   ;Group     ;
                Name=Para2.2.1;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                GroupType=Group;
                InstructionalTextML=[ENU=Choose if the approval process starts automatically or if the user must start the process.;
                                     NOR=Velg om godkjenningsprosessen skal starte automatisk eller om brukeren m† starte prosessen.;
                                     SVE=V„lj om godk„nnandeprocessen startar automatiskt eller om anv„ndaren m†ste starta processen.] }

    { 65  ;3   ;Field     ;
                CaptionML=[ENU=The workflow starts when;
                           NOR=Arbeidsflyten starter n†r;
                           SVE=Arbetsfl”det startar n„r];
                ApplicationArea=#Suite;
                SourceExpr="App. Trigger";
                OnValidate=BEGIN
                             CanEnableNext;
                           END;
                            }

    { 12  ;1   ;Group     ;
                Name=Step3;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                Visible=ItemAutoAppDetailsVisible;
                GroupType=Group }

    { 62  ;2   ;Group     ;
                Name=Para3.1;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                GroupType=Group;
                InstructionalTextML=[ENU=Choose criteria for when the approval process starts automatically.;
                                     NOR=Velg kriterier for n†r godkjenningsprosessen starter automatisk.;
                                     SVE=V„lj vilket villkor som styr n„r godk„nnandeprocessen ska starta automatiskt.] }

    { 61  ;3   ;Group     ;
                Name=Para3.1.1;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                GroupType=GridLayout;
                Layout=Rows }

    { 60  ;4   ;Group     ;
                Name=Para3.1.1.1;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                GroupType=Group;
                InstructionalTextML=[ENU=The workflow starts when:;
                                     NOR=Arbeidsflyten starter n†r:;
                                     SVE=Arbetsfl”det startar n„r:] }

    { 59  ;5   ;Field     ;
                Name=ItemFieldCap;
                CaptionML=[ENU=Field;
                           NOR=Felt;
                           SVE=F„lt];
                ApplicationArea=#Suite;
                SourceExpr=ItemFieldCaption;
                OnValidate=VAR
                             FieldRec@1000 : Record 2000000041;
                           BEGIN
                             IF ItemFieldCaption = '' THEN BEGIN
                               SetItemField(0);
                               EXIT;
                             END;

                             IF NOT FindAndFilterToField(FieldRec,ItemFieldCaption) THEN
                               ERROR(FieldNotExistErr,ItemFieldCaption);

                             IF FieldRec.COUNT = 1 THEN BEGIN
                               SetItemField(FieldRec."No.");
                               EXIT;
                             END;

                             IF PAGE.RUNMODAL(PAGE::"Field List",FieldRec) = ACTION::LookupOK THEN
                               SetItemField(FieldRec."No.")
                             ELSE
                               ERROR(FieldNotExistErr,ItemFieldCaption);
                           END;

                OnLookup=VAR
                           FieldRec@1000 : Record 2000000041;
                         BEGIN
                           FindAndFilterToField(FieldRec,Text);
                           FieldRec.SETRANGE("Field Caption");
                           FieldRec.SETRANGE("No.");

                           IF PAGE.RUNMODAL(PAGE::"Field List",FieldRec) = ACTION::LookupOK THEN
                             SetItemField(FieldRec."No.");
                         END;

                ShowCaption=No }

    { 58  ;5   ;Field     ;
                CaptionML=[ENU=is;
                           NOR=er;
                           SVE=„r];
                ApplicationArea=#Suite;
                ShowCaption=No }

    { 57  ;5   ;Field     ;
                CaptionML=[ENU=Operator;
                           NOR=Operator;
                           SVE=Operator];
                ApplicationArea=#Suite;
                SourceExpr="Field Operator";
                ShowCaption=No }

    { 56  ;3   ;Group     ;
                Name=Para3.1.2;
                CaptionML=[ENU=Specify the message to display when the workflow starts.;
                           NOR=Angi meldingen som skal vises n†r arbeidsflyten starter.;
                           SVE=Ange meddelandet som ska visas n„r arbetsfl”det startar.];
                GroupType=Group }

    { 54  ;4   ;Field     ;
                CaptionML=[ENU=Message;
                           NOR=Melding;
                           SVE=Meddelande];
                ApplicationArea=#Suite;
                SourceExpr="Custom Message";
                ShowCaption=No }

    { 10  ;1   ;Group     ;
                Name=Step10;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                Visible=DoneVisible;
                GroupType=Group }

    { 7   ;2   ;Group     ;
                Name=Para10.1;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                GroupType=Group;
                InstructionalTextML=[ENU=Item Approval Workflow Overview;
                                     NOR=Oversikt over arbeidsflyt for varegodkjenning;
                                     SVE=™versikt: Arbetsfl”de f”r artikelgodk„nnande] }

    { 6   ;3   ;Field     ;
                Name=Overview;
                ApplicationArea=#Suite;
                SourceExpr=SummaryText;
                Editable=FALSE;
                MultiLine=Yes;
                Style=StrongAccent;
                StyleExpr=TRUE;
                ShowCaption=No }

    { 25  ;2   ;Group     ;
                Name=Para10.2;
                CaptionML=[ENU=That's it!;
                           NOR=Det var det!;
                           SVE=Klart!];
                GroupType=Group;
                InstructionalTextML=[ENU=Choose Finish to enable the workflow with the specified settings.;
                                     NOR=Velg Fullf›r for † aktivere arbeidsflyten med de angitte innstillingene.;
                                     SVE=V„lj Slutf”r om du vill aktivera arbetsfl”det med angivna inst„llningar.] }

  }
  CODE
  {
    VAR
      MediaRepositoryStandard@1040 : Record 9400;
      MediaRepositoryDone@1041 : Record 9400;
      MediaResourcesStandard@1005 : Record 2000000182;
      MediaResourcesDone@1003 : Record 2000000182;
      ClientTypeManagement@1009 : Codeunit 4030;
      Step@1015 : 'Intro,Item Approver Setup,Automatic Approval Setup,Done';
      BackEnabled@1014 : Boolean;
      NextEnabled@1013 : Boolean;
      FinishEnabled@1010 : Boolean;
      TopBannerVisible@1042 : Boolean;
      IntroVisible@1001 : Boolean;
      ItemApproverSetupVisible@1011 : Boolean;
      ItemAutoAppDetailsVisible@1027 : Boolean;
      DoneVisible@1004 : Boolean;
      NAVNotSetUpQst@1000 : TextConst 'ENU=Item Approval has not been set up.\\Are you sure that you want to exit?;NOR=Varegodkjenning er ikke konfigurert.\\Er du sikker p† at du vil avslutte?;SVE=Artikelgodk„nnande har inte konfigurerats.\\Vill du avsluta?';
      MandatoryApproverErr@1002 : TextConst '@@@="%1 = User Name";ENU=You must select an approver before continuing.;NOR=Du m† velge en godkjenner f›r du fortsetter.;SVE=Du m†ste v„lja en godk„nnare innan du forts„tter.';
      ItemFieldCaption@1021 : Text[250];
      FieldNotExistErr@1028 : TextConst '@@@="%1 = Field Caption";ENU=Field %1 does not exist.;NOR=Feltet %1 finnes ikke.;SVE=F„ltet %1 finns inte.';
      SummaryText@1006 : Text;
      ManualTriggerTxt@1008 : TextConst '@@@="%1 = User Name (eg. An approval request will be sent to the user Domain/Username when the user sends the request manually.)";ENU=An approval request will be sent to the user %1 when the user sends the request manually.;NOR=En godkjenningsforesp›rsel sendes til brukeren %1 n†r brukeren sender foresp›rselen manuelt.;SVE=Beg„ran om godk„nnande skickas till anv„ndaren %1 n„r anv„ndaren skickar en beg„ran manuellt.';
      AutoTriggerTxt@1007 : TextConst '@@@="%1 = User Name, %2 = Field caption, %3 = Of of this 3 values: Increased, Decreased, Changed (eg. An approval request will be sent to the user Domain/Username when the value in the Credit Limit (LCY) field is Increased.)";ENU=An approval request will be sent to the user %1 when the value in the %2 field is %3.;NOR=En godkjenningsforesp›rsel sendes til brukeren %1 n†r verdien i feltet %2 er %3.;SVE=Beg„ran om godk„nnande skickas till anv„ndaren %1 n„r v„rdet i f„ltet %2 „r %3.';

    LOCAL PROCEDURE NextStep@3(Backwards@1000 : Boolean);
    BEGIN
      IF Backwards THEN
        Step := Step - 1
      ELSE BEGIN
        IF ItemApproverSetupVisible THEN
          ValidateApprover;
        IF ItemAutoAppDetailsVisible THEN
          ValidateFieldSelection;
        Step := Step + 1;
      END;

      CASE Step OF
        Step::Intro:
          ShowIntroStep;
        Step::"Item Approver Setup":
          ShowApprovalUserSetupDetailsStep;
        Step::"Automatic Approval Setup":
          IF "App. Trigger" = "App. Trigger"::"The user changes a specific field"
          THEN
            ShowItemApprovalDetailsStep
          ELSE
            NextStep(Backwards);
        Step::Done:
          ShowDoneStep;
      END;
      CurrPage.UPDATE(TRUE);
    END;

    LOCAL PROCEDURE ShowIntroStep@1();
    BEGIN
      ResetWizardControls;
      IntroVisible := TRUE;
      BackEnabled := FALSE;
    END;

    LOCAL PROCEDURE ShowApprovalUserSetupDetailsStep@9();
    BEGIN
      ResetWizardControls;
      ItemApproverSetupVisible := TRUE;
    END;

    LOCAL PROCEDURE ShowItemApprovalDetailsStep@25();
    BEGIN
      ResetWizardControls;
      ItemAutoAppDetailsVisible := TRUE;
      SetItemField(Field);
    END;

    LOCAL PROCEDURE ShowDoneStep@6();
    BEGIN
      ResetWizardControls;
      DoneVisible := TRUE;
      NextEnabled := FALSE;
      FinishEnabled := TRUE;

      IF "App. Trigger" = "App. Trigger"::"The user sends an approval requests manually" THEN
        SummaryText := STRSUBSTNO(ManualTriggerTxt,"Approver ID");
      IF "App. Trigger" = "App. Trigger"::"The user changes a specific field"
      THEN BEGIN
        CALCFIELDS("Field Caption");
        SummaryText := STRSUBSTNO(AutoTriggerTxt,"Approver ID","Field Caption","Field Operator");
      END;

      SummaryText := CONVERTSTR(SummaryText,'\','/');
    END;

    LOCAL PROCEDURE ResetWizardControls@10();
    BEGIN
      // Buttons
      BackEnabled := TRUE;
      NextEnabled := TRUE;
      FinishEnabled := FALSE;

      // Tabs
      IntroVisible := FALSE;
      ItemApproverSetupVisible := FALSE;
      ItemAutoAppDetailsVisible := FALSE;
      DoneVisible := FALSE;
    END;

    LOCAL PROCEDURE SetDefaultValues@8();
    VAR
      Workflow@1000 : Record 1501;
      WorkflowRule@1007 : Record 1524;
      WorkflowStep@1003 : Record 1502;
      WorkflowStepArgument@1006 : Record 1523;
      WorkflowSetup@1002 : Codeunit 1502;
      WorkflowResponseHandling@1005 : Codeunit 1521;
      WorkflowCode@1001 : Code[20];
    BEGIN
      TableNo := DATABASE::Item;
      WorkflowCode := WorkflowSetup.GetWorkflowTemplateCode(WorkflowSetup.ItemUnitPriceChangeApprovalWorkflowCode);
      IF Workflow.GET(WorkflowCode) THEN BEGIN
        WorkflowRule.SETRANGE("Workflow Code",WorkflowCode);
        IF WorkflowRule.FINDFIRST THEN BEGIN
          Field := WorkflowRule."Field No.";
          "Field Operator" := WorkflowRule.Operator;
        END;
        WorkflowStep.SETRANGE("Workflow Code",WorkflowCode);
        WorkflowStep.SETRANGE("Function Name",WorkflowResponseHandling.ShowMessageCode);
        IF WorkflowStep.FINDFIRST THEN BEGIN
          WorkflowStepArgument.GET(WorkflowStep.Argument);
          "Custom Message" := WorkflowStepArgument.Message;
        END;
      END;
    END;

    LOCAL PROCEDURE ValidateApprover@11();
    BEGIN
      IF "Approver ID" = '' THEN
        ERROR(MandatoryApproverErr);
    END;

    LOCAL PROCEDURE ValidateFieldSelection@7();
    BEGIN
    END;

    LOCAL PROCEDURE CanEnableNext@32();
    BEGIN
      NextEnabled := TRUE;
    END;

    LOCAL PROCEDURE SetItemField@54(FieldNo@1000 : Integer);
    BEGIN
      Field := FieldNo;
      CALCFIELDS("Field Caption");
      ItemFieldCaption := "Field Caption";
    END;

    LOCAL PROCEDURE FindAndFilterToField@57(VAR FieldRec@1000 : Record 2000000041;CaptionToFind@1001 : Text) : Boolean;
    BEGIN
      FieldRec.FILTERGROUP(2);
      FieldRec.SETRANGE(TableNo,DATABASE::Item);
      FieldRec.SETFILTER(Type,STRSUBSTNO('%1|%2|%3|%4|%5|%6|%7|%8|%9|%10|%11|%12|%13',
          FieldRec.Type::Boolean,
          FieldRec.Type::Text,
          FieldRec.Type::Code,
          FieldRec.Type::Decimal,
          FieldRec.Type::Integer,
          FieldRec.Type::BigInteger,
          FieldRec.Type::Date,
          FieldRec.Type::Time,
          FieldRec.Type::DateTime,
          FieldRec.Type::DateFormula,
          FieldRec.Type::Option,
          FieldRec.Type::Duration,
          FieldRec.Type::RecordID));
      FieldRec.SETRANGE(Class,FieldRec.Class::Normal);
      FieldRec.SETFILTER(ObsoleteState,'<>%1',FieldRec.ObsoleteState::Removed);

      IF CaptionToFind = "Field Caption" THEN
        FieldRec.SETRANGE("No.",Field)
      ELSE
        IF CaptionToFind = 'Blocked' THEN
          FieldRec.SETFILTER("Field Caption",'%1','@' + CaptionToFind)
        ELSE
          FieldRec.SETFILTER("Field Caption",'%1','@' + CaptionToFind + '*');

      EXIT(FieldRec.FINDFIRST);
    END;

    LOCAL PROCEDURE LoadTopBanners@40();
    BEGIN
      IF MediaRepositoryStandard.GET('AssistedSetup-NoText-400px.png',FORMAT(ClientTypeManagement.GetCurrentClientType)) AND
         MediaRepositoryDone.GET('AssistedSetupDone-NoText-400px.png',FORMAT(ClientTypeManagement.GetCurrentClientType))
      THEN
        IF MediaResourcesStandard.GET(MediaRepositoryStandard."Media Resources Ref") AND
           MediaResourcesDone.GET(MediaRepositoryDone."Media Resources Ref")
        THEN
          TopBannerVisible := MediaResourcesDone."Media Reference".HASVALUE;
    END;

    BEGIN
    END.
  }
}

