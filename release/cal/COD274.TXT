OBJECT Codeunit 274 Res. Jnl.-B.Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04;
  }
  PROPERTIES
  {
    TableNo=236;
    OnRun=BEGIN
            ResJnlBatch.COPY(Rec);
            Code;
            Rec := ResJnlBatch;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to post the journals and print the posting report?;NOR=Vil du bokfre kladdene og skrive ut bokfringsrapporten?;SVE=Vill du bokfra journalerna och skriva ut bokfringsrapporten?';
      Text001@1001 : TextConst 'ENU=The journals were successfully posted.;NOR=Kladdene ble bokfrt p riktig mte.;SVE=Journalerna r bokfrda.';
      Text002@1002 : TextConst 'ENU="It was not possible to post all of the journals. ";NOR="Det er ikke mulig  bokfre alle kladdene. ";SVE="Det var inte mjligt att bokfra alla journaler. "';
      Text003@1003 : TextConst 'ENU=The journals that were not successfully posted are now marked.;NOR=Kladdene som ikke ble bokfrt, er merket.;SVE=Journalerna som inte blev bokfrda r nu markerade.';
      ResJnlTemplate@1004 : Record 206;
      ResJnlBatch@1005 : Record 236;
      ResJnlLine@1006 : Record 207;
      ResReg@1007 : Record 240;
      ResJnlPostBatch@1008 : Codeunit 213;
      JnlWithErrors@1009 : Boolean;

    LOCAL PROCEDURE Code@1();
    VAR
      HideDialog@1000 : Boolean;
    BEGIN
      WITH ResJnlBatch DO BEGIN
        ResJnlTemplate.GET("Journal Template Name");
        ResJnlTemplate.TESTFIELD("Posting Report ID");

        HideDialog := FALSE;
        OnBeforePostJournalBatch(ResJnlBatch,HideDialog);
        IF NOT HideDialog THEN
          IF NOT CONFIRM(Text000) THEN
            EXIT;

        FIND('-');
        REPEAT
          ResJnlLine."Journal Template Name" := "Journal Template Name";
          ResJnlLine."Journal Batch Name" := Name;
          ResJnlLine."Line No." := 1;
          CLEAR(ResJnlPostBatch);
          IF ResJnlPostBatch.RUN(ResJnlLine) THEN BEGIN
            OnAfterPostJournalBatch(ResJnlBatch);
            MARK(FALSE);
            IF ResReg.GET(ResJnlLine."Line No.") THEN BEGIN
              ResReg.SETRECFILTER;
              REPORT.RUN(ResJnlTemplate."Posting Report ID",FALSE,FALSE,ResReg);
            END;
          END ELSE BEGIN
            MARK(TRUE);
            JnlWithErrors := TRUE;
          END;
        UNTIL NEXT = 0;

        IF NOT JnlWithErrors THEN
          MESSAGE(Text001)
        ELSE
          MESSAGE(
            Text002 +
            Text003);

        IF NOT FIND('=><') THEN BEGIN
          RESET;
          Name := '';
        END;
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPostJournalBatch@3(VAR ResJournalBatch@1000 : Record 236);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostJournalBatch@2(VAR ResJournalBatch@1000 : Record 236;VAR HideDialog@1001 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

