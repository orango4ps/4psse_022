OBJECT Codeunit 11012284 Server Environment Management
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE GetDatabaseServerName@1100528101() : Text[80];
    BEGIN
      IF GetInstanceSettingValue('DatabaseInstance') <> '' THEN
        EXIT(STRSUBSTNO(
          '%1\%2', GetInstanceSettingValue('DatabaseServer'), GetInstanceSettingValue('DatabaseInstance')))
      ELSE
        EXIT(GetInstanceSettingValue('DatabaseServer'));
    END;

    PROCEDURE GetDatabaseName@1100528102() : Text[80];
    VAR
      ActiveSession@1100525000 : Record 2000000110;
    BEGIN
      ActiveSession.SETRANGE("Session ID", SESSIONID);
      IF ActiveSession.FINDFIRST THEN
        EXIT(ActiveSession."Database Name");
    END;

    PROCEDURE GetClientServicesPort@1100525000() : Integer;
    VAR
      SettingValueTxt@1100525001 : Text;
      Port@1100525000 : Integer;
    BEGIN
      SettingValueTxt := GetInstanceSettingValue('ClientServicesPort');
      IF SettingValueTxt <> '' THEN
        EVALUATE(Port, SettingValueTxt);
      EXIT(Port);
    END;

    LOCAL PROCEDURE GetInstanceSettingValue@1100525013(SettingName@1100525002 : Text) : Text;
    VAR
      XmlDoc@1100525001 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      XmlDocNode@1100525000 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
    BEGIN
      IF NOT LoadInstanceSettings(XmlDoc) THEN
        EXIT;

      XmlDocNode := XmlDoc.SelectSingleNode('//appSettings/add[@key=''' + SettingName + ''']');

      IF NOT ISNULL(XmlDocNode) THEN
        EXIT(XmlDocNode.Attributes.GetNamedItem('value').Value);
    END;

    LOCAL PROCEDURE LoadInstanceSettings@1100525002(VAR XmlDoc@1100525000 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument") : Boolean;
    VAR
      ActiveSession@1100525003 : Record 2000000110;
      FileName@1100525001 : Text;
    BEGIN
      ActiveSession.SETRANGE("Session ID", SESSIONID);
      IF NOT ActiveSession.FINDFIRST THEN
        EXIT(FALSE);

      FileName := STRSUBSTNO(
        '%1\%2\%3\%4',
        DELCHR(APPLICATIONPATH, '>', '\'),
        'Instances',
        ActiveSession."Server Instance Name",
        'CustomSettings.config');

      IF NOT EXISTS(FileName) THEN
        FileName := STRSUBSTNO(
          '%1\%2',
          DELCHR(APPLICATIONPATH, '>', '\'),
          'CustomSettings.config');

      IF NOT EXISTS(FileName) THEN
        EXIT(FALSE);

      XmlDoc := XmlDoc.XmlDocument;
      XmlDoc.Load(FileName);

      EXIT(TRUE);
    END;

    PROCEDURE GetUserName@1100528107() : Text[80];
    VAR
      ActiveSession@1100525000 : Record 2000000110;
    BEGIN
      ActiveSession.SETRANGE("Session ID", SESSIONID);
      IF ActiveSession.FINDFIRST THEN
        EXIT(ActiveSession."User ID");
    END;

    BEGIN
    END.
  }
}

