OBJECT Page 5551 Fixed Asset Acquisition Wizard
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Fixed Asset Acquisition;
               NOR=Anskaffelse av aktiva;
               SVE=Anskaffning av anl„ggningstillg†ngar];
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table81;
    PageType=NavigatePage;
    SourceTableTemporary=Yes;
    ShowFilter=No;
    OnInit=BEGIN
             LoadTopBanners;
           END;

    OnOpenPage=BEGIN
                 // We could check if values like FA Posting code, descirption are in the temp
                 IF NOT GET THEN BEGIN
                   INIT;
                   "Journal Template Name" := FixedAssetAcquisitionWizard.SelectFATemplate;
                   "Journal Batch Name" := FixedAssetAcquisitionWizard.GetAutogenJournalBatch;
                   "Document Type" := "Document Type"::Invoice;
                   "Account Type" := "Account Type"::"Fixed Asset";
                   "FA Posting Type" := "FA Posting Type"::"Acquisition Cost";
                   "Posting Date" := WORKDATE;
                   SetAccountNoFromFilter;
                   INSERT;
                 END;

                 EnableOpenFAGLJournal := JournalBatchIsEmpty;
                 OpenFAGLJournal := NOT EnableOpenFAGLJournal;
                 VerifyFADoNotExistInGLJournalLines;
                 ValidateCurrentStep(Step);
               END;

    ActionList=ACTIONS
    {
      { 8       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 15      ;1   ;Action    ;
                      Name=PreviousPage;
                      CaptionML=[ENU=Back;
                                 NOR=Tilbake;
                                 SVE=Bak†t];
                      ApplicationArea=#FixedAssets;
                      Visible=Step <> Step::"Already In Journal";
                      Enabled=Step <> Step::Intro;
                      InFooterBar=Yes;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 NextStep(TRUE);
                               END;
                                }
      { 14      ;1   ;Action    ;
                      Name=NextPage;
                      CaptionML=[ENU=Next;
                                 NOR=Neste;
                                 SVE=N„sta];
                      ApplicationArea=#FixedAssets;
                      Visible=Step <> Step::"Already In Journal";
                      Enabled=(Step <> Step::Done) AND (CurrStepIsValid);
                      InFooterBar=Yes;
                      Image=NextRecord;
                      OnAction=BEGIN
                                 NextStep(FALSE);
                               END;
                                }
      { 9       ;1   ;Action    ;
                      Name=Finish;
                      CaptionML=[ENU=Finish;
                                 NOR=Fullf›r;
                                 SVE=Slutf”r];
                      ApplicationArea=#FixedAssets;
                      Enabled=(Step = Step::Done) OR (Step = Step::"Already In Journal");
                      InFooterBar=Yes;
                      Image=Approve;
                      OnAction=VAR
                                 GenJnlLine@1010 : Record 81;
                                 PageGenJnlLine@1007 : Record 81;
                               BEGIN
                                 IF Step <> Step::"Already In Journal" THEN
                                   CreateFAAcquisitionLines(GenJnlLine);

                                 IF OpenFAGLJournal THEN BEGIN
                                   PageGenJnlLine.VALIDATE("Journal Template Name","Journal Template Name");
                                   PageGenJnlLine.VALIDATE("Journal Batch Name","Journal Batch Name");
                                   PageGenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
                                   PageGenJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");
                                   PAGE.RUN(PAGE::"Fixed Asset G/L Journal",PageGenJnlLine);
                                 END ELSE
                                   CODEUNIT.RUN(CODEUNIT::"Gen. Jnl.-Post Batch",GenJnlLine);

                                 CurrPage.CLOSE;
                               END;
                                }
      { 7       ;1   ;Action    ;
                      Name=Exit;
                      CaptionML=[ENU=Exit;
                                 NOR=Avslutt;
                                 SVE=Avsluta];
                      ApplicationArea=#FixedAssets;
                      OnAction=BEGIN
                                 CurrPage.CLOSE
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 96  ;1   ;Group     ;
                Visible=TopBannerVisible AND (Step <> Step::Done);
                Editable=FALSE;
                GroupType=Group }

    { 97  ;2   ;Field     ;
                ApplicationArea=#FixedAssets;
                SourceExpr=MediaResourcesStandard."Media Reference";
                Editable=FALSE;
                ShowCaption=No }

    { 98  ;1   ;Group     ;
                Visible=TopBannerVisible AND (Step = Step::Done);
                Editable=FALSE;
                GroupType=Group }

    { 99  ;2   ;Field     ;
                ApplicationArea=#FixedAssets;
                SourceExpr=MediaResourcesDone."Media Reference";
                Editable=FALSE;
                ShowCaption=No }

    { 11  ;1   ;Group     ;
                Name=Step1;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                Visible=Step = Step::Intro;
                GroupType=Group }

    { 18  ;2   ;Group     ;
                Name=Para1.1;
                CaptionML=[ENU=Welcome to Assisted Fixed Asset Acquisition;
                           NOR=Velkommen til assisterte anskaffelse av akitiva;
                           SVE=V„lkommen till den assisterade anskaffningen av anl„ggningstillg†ngar];
                GroupType=Group }

    { 20  ;3   ;Group     ;
                Name=Para1.1.1;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                GroupType=Group;
                InstructionalTextML=[ENU=When you acquire a fixed asset, you can post the transaction to a G/L account, a vendor, or a bank account.;
                                     NOR=N†r du anskaffer et aktiva, kan du bokf›re transaksjonen til en til finans-, leverand›r- eller bankkonto.;
                                     SVE=N„r du anskaffar en anl„ggningstillg†ng kan du bokf”ra transaktionen p† ett redovisnings-, leverant”rs- eller bankkonto.] }

    { 19  ;3   ;Group     ;
                Name=Para1.1.2;
                Visible=FALSE;
                GroupType=Group }

    { 17  ;2   ;Group     ;
                Name=Para1.2;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                GroupType=Group;
                InstructionalTextML=[ENU=Choose Next to specify how to post the acquisition.;
                                     NOR=Velg Neste for † angi hvordan du vil bokf›re anskaffelsen.;
                                     SVE=V„lj N„sta f”r att ange hur anskaffningen ska bokf”ras.] }

    { 2   ;1   ;Group     ;
                Name=Step2;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                Visible=Step = Step::"FA Details";
                GroupType=Group }

    { 22  ;2   ;Group     ;
                Name=Para2.1;
                CaptionML=[ENU=Provide information about the fixed asset.;
                           NOR=Angi informasjon om aktivaet.;
                           SVE=Ange information om anl„ggningstillg†ngen.];
                GroupType=Group }

    { 3   ;3   ;Field     ;
                Name=AcquisitionCost;
                CaptionML=[ENU=Acquisition Cost Incl. VAT;
                           NOR=Anskaffelseskost inkl. mva;
                           SVE=Anskaffningskostnad inkl. moms];
                ApplicationArea=#FixedAssets;
                SourceExpr=Amount;
                OnValidate=BEGIN
                             ValidateCurrentStep(Step);
                           END;
                            }

    { 16  ;3   ;Field     ;
                Name=AcquisitionDate;
                CaptionML=[ENU=Acquisition Date;
                           NOR=Anskaffelsesdato;
                           SVE=Anskaffningsdatum];
                ApplicationArea=#FixedAssets;
                SourceExpr="Posting Date";
                OnValidate=BEGIN
                             ValidateCurrentStep(Step);
                           END;
                            }

    { 32  ;1   ;Group     ;
                Name=Step3;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                Visible=Step = Step::"Register Details";
                GroupType=Group }

    { 33  ;2   ;Group     ;
                Name=Para3.1;
                CaptionML=[ENU=Which ledger do you want to post the acquisition to?;
                           NOR=Hvilken post vil du bokf›re anskaffelsen til?;
                           SVE=I vilken redovisning vill du bokf”ra transaktionen?];
                GroupType=Group }

    { 36  ;3   ;Field     ;
                Name=TypeOfAcquisitions;
                CaptionML=[ENU=Post to;
                           NOR=Bokf›r i;
                           SVE=Bokf”r p†];
                OptionCaptionML=[ENU=G/L Account,Vendor,Bank Account;
                                 NOR=Finanskonto,Leverand›r,Bankkonto;
                                 SVE=Redovisningskonto,Leverant”r,Bankkonto];
                ApplicationArea=#FixedAssets;
                SourceExpr=AcquisitionOptions;
                OnValidate=BEGIN
                             CASE AcquisitionOptions OF
                               AcquisitionOptions::"G/L Account":
                                 VALIDATE("Bal. Account Type","Bal. Account Type"::"G/L Account");
                               AcquisitionOptions::Vendor:
                                 "Bal. Account Type" := "Bal. Account Type"::Vendor;
                               AcquisitionOptions::"Bank Account":
                                 VALIDATE("Bal. Account Type","Bal. Account Type"::"Bank Account");
                             END;
                             ValidateCurrentStep(Step);
                           END;
                            }

    { 34  ;3   ;Group     ;
                Visible=AcquisitionOptions = AcquisitionOptions::"G/L Account";
                GroupType=Group }

    { 26  ;4   ;Field     ;
                Name=BalancingAccountNo;
                CaptionML=[ENU=Balancing Account No.;
                           NOR=Motkontonr.;
                           SVE=Balanskontonr];
                ApplicationArea=#FixedAssets;
                SourceExpr="Bal. Account No.";
                OnValidate=BEGIN
                             ValidateCurrentStep(Step);
                           END;
                            }

    { 27  ;3   ;Group     ;
                Visible=AcquisitionOptions = AcquisitionOptions::Vendor;
                GroupType=Group }

    { 28  ;4   ;Field     ;
                Name=VendorNo;
                CaptionML=[ENU=Vendor;
                           NOR=Leverand›r;
                           SVE=Leverant”r];
                ApplicationArea=#FixedAssets;
                SourceExpr="Bal. Account No." }

    { 29  ;4   ;Field     ;
                Name=ExternalDocNo;
                CaptionML=[ENU=External Document No.;
                           NOR=Eksterndokumentnr.;
                           SVE=Externt dokumentnr];
                ApplicationArea=#FixedAssets;
                SourceExpr="External Document No.";
                OnValidate=BEGIN
                             ValidateCurrentStep(Step);
                           END;
                            }

    { 30  ;3   ;Group     ;
                Visible=AcquisitionOptions = AcquisitionOptions::"Bank Account";
                GroupType=Group }

    { 31  ;4   ;Field     ;
                Name=Bank Account;
                CaptionML=[ENU=Bank Account;
                           NOR=Bankkonto;
                           SVE=Bankkonto];
                ApplicationArea=#FixedAssets;
                SourceExpr="Bal. Account No.";
                OnValidate=BEGIN
                             ValidateCurrentStep(Step);
                           END;
                            }

    { 10  ;1   ;Group     ;
                Name=Step4;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                Visible=Step = Step::Done;
                GroupType=Group }

    { 25  ;2   ;Group     ;
                Name=Para4.1;
                CaptionML=[ENU=That's it!;
                           NOR=Det er alt!;
                           SVE=Klart!];
                GroupType=Group;
                InstructionalTextML=[ENU=The information needed to post the acquisition is  now ready in the fixed asset G/L journal.;
                                     NOR=Informasjonen som kreves for † bokf›re anskaffelsen er n† klar i aktivafinanskladden.;
                                     SVE=Den information som kr„vs f”r att bokf”ra anskaffningen „r nu redo i redovisningsjournalen f”r anl„ggningstillg†ngen.] }

    { 5   ;2   ;Group     ;
                Name=Para4.2;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                GroupType=Group;
                InstructionalTextML=[ENU=Choose Finish to automatically post the fixed asset G/L journal lines.;
                                     NOR=Velg Fullf›r for † automatisk bokf›re linjene i aktivafinanskladden.;
                                     SVE=V„lj Avsluta f”r att automatiskt bokf”ra redovisningsjournalraderna f”r anl„ggningstillg†ngen.] }

    { 4   ;3   ;Field     ;
                CaptionML=[ENU=Upon Finish, open the FA G/L journal.;
                           NOR=Ved Fullf›r, †pne aktivafinanskladden.;
                           SVE=™ppna redovisningsjournaler f”r anl.tillg. n„r du har avslutat.];
                ApplicationArea=#FixedAssets;
                SourceExpr=OpenFAGLJournal;
                Enabled=EnableOpenFAGLJournal }

    { 24  ;1   ;Group     ;
                Name=StepInJournal;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                Visible=Step = Step::"Already In Journal";
                GroupType=Group }

    { 23  ;2   ;Group     ;
                Name=Para5.1;
                CaptionML=[ENU=The fixed asset is already planned to be acquired.;
                           NOR=Anskaffelse av aktivaet er allerede planlagt.;
                           SVE=Anskaffningen av anl„ggningstillg†ngen planeras redan.];
                GroupType=Group;
                InstructionalTextML=[ENU=Fixed asset G/L journal already contains a line for the acquisition of this fixed asset.;
                                     NOR=Aktivafinanskladden inneholder allerede en linje for anskaffelsen av aktivaet.;
                                     SVE=Red. journal f”r anl„ggningstillg†ngar inneh†ller redan en rad f”r anskaffningen av denna anl„ggningstillg†ng.] }

    { 21  ;2   ;Group     ;
                Name=Para5.2;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                GroupType=Group;
                InstructionalTextML=[ENU=Choose Finish to open the fixed asset G/L journal. Close this window to take no further action.;
                                     NOR=Velg Fullf›r for † †pne aktivafinanskladden. Lukk dette vinduet for ikke † utf›re flere handlinger.;
                                     SVE=V„lj Avsluta f”r att ”ppna red. journalen f”r anl„ggningstillg†ngar. St„ng detta f”nster utan att vidta †tg„rd.] }

  }
  CODE
  {
    VAR
      MediaRepositoryStandard@1040 : Record 9400;
      MediaRepositoryDone@1041 : Record 9400;
      MediaResourcesStandard@1006 : Record 2000000182;
      MediaResourcesDone@1004 : Record 2000000182;
      TempBalancingGenJournalLine@1002 : TEMPORARY Record 81;
      FixedAssetAcquisitionWizard@1005 : Codeunit 5550;
      ClientTypeManagement@1077 : Codeunit 4030;
      Step@1015 : 'Intro,FA Details,Register Details,Done,Already In Journal';
      TopBannerVisible@1042 : Boolean;
      AcquisitionOptions@1000 : 'G/L Account,Vendor,Bank Account';
      OpenFAGLJournal@1018 : Boolean;
      CurrStepIsValid@1001 : Boolean;
      EnableOpenFAGLJournal@1003 : Boolean;

    LOCAL PROCEDURE NextStep@3(Backwards@1000 : Boolean);
    BEGIN
      IF Backwards THEN
        Step := Step - 1
      ELSE
        Step := Step + 1;
      ValidateCurrentStep(Step);

      IF Step = Step::Done THEN BEGIN
        TempBalancingGenJournalLine.INIT;
        TempBalancingGenJournalLine.TRANSFERFIELDS(Rec);
        TempBalancingGenJournalLine."Account No." := '';
        IF NOT TempBalancingGenJournalLine.INSERT THEN
          TempBalancingGenJournalLine.MODIFY(TRUE);
      END;

      CurrPage.UPDATE(TRUE);
    END;

    LOCAL PROCEDURE LoadTopBanners@40();
    BEGIN
      IF MediaRepositoryStandard.GET('AssistedSetup-NoText-400px.png',FORMAT(ClientTypeManagement.GetCurrentClientType)) AND
         MediaRepositoryDone.GET('AssistedSetupDone-NoText-400px.png',FORMAT(ClientTypeManagement.GetCurrentClientType))
      THEN
        IF MediaResourcesStandard.GET(MediaRepositoryStandard."Media Resources Ref") AND
           MediaResourcesDone.GET(MediaRepositoryDone."Media Resources Ref")
        THEN
          TopBannerVisible := MediaResourcesDone."Media Reference".HASVALUE;
    END;

    LOCAL PROCEDURE ValidateCurrentStep@7(CurrentStep@1000 : Option);
    BEGIN
      CASE CurrentStep OF
        Step::Intro:
          CurrStepIsValid := TRUE;
        Step::"FA Details":
          CurrStepIsValid := (Amount >= 0.0) AND ("Posting Date" <> 0D);
        Step::"Register Details":
          BEGIN
            CurrStepIsValid := "Bal. Account No." <> '';
            IF AcquisitionOptions = AcquisitionOptions::Vendor THEN
              CurrStepIsValid := CurrStepIsValid AND ("External Document No." <> '');
          END;
        Step::Done:
          CurrStepIsValid := TRUE;
        ELSE
          CurrStepIsValid := TRUE;
      END;
    END;

    LOCAL PROCEDURE JournalBatchIsEmpty@1() : Boolean;
    VAR
      GenJournalLine@1000 : Record 81;
    BEGIN
      GenJournalLine.SETRANGE("Journal Batch Name","Journal Batch Name");
      EXIT(GenJournalLine.COUNT = 0);
    END;

    LOCAL PROCEDURE VerifyFADoNotExistInGLJournalLines@6();
    VAR
      GenJournalLine@1000 : Record 81;
    BEGIN
      GenJournalLine.SETRANGE("Account No.","Account No.");
      GenJournalLine.SETRANGE("Account Type",GenJournalLine."Account Type"::"Fixed Asset");
      GenJournalLine.SETRANGE("FA Posting Type","FA Posting Type"::"Acquisition Cost");
      IF GenJournalLine.FINDFIRST THEN BEGIN
        Step := Step::"Already In Journal";
        OpenFAGLJournal := TRUE;
        COPY(GenJournalLine);
        INSERT;
      END
    END;

    BEGIN
    END.
  }
}

