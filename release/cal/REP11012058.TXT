OBJECT Report 11012058 Project Costs Graph
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Projektkosten Grafik;
               ENU=Project Costs Graph;
               NLD=Projectkostengrafiek;
               NOR=Diagram over prosjektkoster];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   PeriodType := PeriodType::Month;
                 END;

    OnPreReport=BEGIN
                  IF PeriodFilter = '' THEN
                    ERROR(Text017);

                  ExcelAutomation.InitExcel;
                  ExcelAutomation.InitWorkbook(Text000);

                  ExcelAutomation.WriteCell(1, 1, Text002);
                  ExcelAutomation.WriteCell(1, 3, Text004);
                  ExcelAutomation.WriteCell(2, 3, Text006);
                  ExcelAutomation.WriteCell(3, 3, Text008);
                  ExcelAutomation.WriteCell(4, 3, Text009);
                  ExcelAutomation.WriteCell(5, 3, Text010);
                END;

    OnPostReport=VAR
                   ExcelBuffer@1100528705 : Record 370;
                   XlWrkBk@1100528700 : DotNet "'Microsoft.Office.Interop.Excel, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Excel.Workbook" RUNONCLIENT;
                   XlChart@1100528702 : DotNet "'Microsoft.Office.Interop.Excel, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Excel.Chart" RUNONCLIENT;
                   XlAxis@1100528703 : DotNet "'Microsoft.Office.Interop.Excel, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Excel.Axis" RUNONCLIENT;
                   TypeMissing@1100528701 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Object" RUNONCLIENT;
                   i@1100528704 : Integer;
                 BEGIN
                   Counter := 3;
                   FOR i := 10 DOWNTO 1 DO BEGIN
                     ExcelAutomation.WriteCell(2, Counter + i, FORMAT(ActCostDec[i]));
                     ExcelAutomation.WriteCell(3, Counter + i, FORMAT(RevDec[i]));
                     ExcelAutomation.WriteCell(4, Counter + i, FORMAT(ContrAmntDec[i]));
                     ExcelAutomation.WriteCell(5, Counter + i, FORMAT(TotCostDec[i]));
                   END;

                   ExcelAutomation.SetNumberFormatInRange(ExcelBuffer.RC2Range(4, 2, 13, 5), '#.##0');
                   ExcelAutomation.AutoFit(1, 5);

                   ExcelAutomation.AddChart(
                     Text014,
                     1, 3, 5, 13,
                     ExcelEnumeration.XlChartType_xlLine,
                     1,
                     ExcelEnumeration.XlRowCol_xlColumns,
                     1,
                     1,
                     TRUE,
                     STRSUBSTNO(Text016, Job."No.", Job.Description));

                   DotNetManagement.GetTypeMissing(TypeMissing);

                   ExcelAutomation.GetXlWrkBk(XlWrkBk);
                   XlChart := XlWrkBk.Charts.Item(XlWrkBk.Charts.Count);
                   XlAxis := XlChart.Axes(ExcelEnumeration.XlAxisType_xlCategory, TypeMissing);
                   XlAxis.HasMajorGridlines := FALSE;
                   XlAxis.HasMinorGridlines := FALSE;
                   XlAxis.BaseUnitIsAuto := TRUE;
                   XlAxis.Crosses := ExcelEnumeration.XlAxisCrosses_xlAxisCrossesAutomatic;
                   XlAxis.AxisBetweenCategories := FALSE;
                   XlAxis.ReversePlotOrder := FALSE;
                   XlAxis.CategoryType := ExcelEnumeration.XlCategoryType_xlCategoryScale;

                   XlAxis := XlChart.Axes(ExcelEnumeration.XlAxisType_xlValue, TypeMissing);
                   XlAxis.HasMajorGridlines := TRUE;
                   XlAxis.HasMinorGridlines := FALSE;

                   ExcelAutomation.GiveUserControl;
                 END;

  }
  DATASET
  {
    { 8019;    ;DataItem;                    ;
               DataItemTable=Table11072003;
               OnPreDataItem=VAR
                               i@1100528703 : Integer;
                               DateFilter@1100528702 : Text;
                               CumulationFilter@1100528701 : Text;
                             BEGIN
                               DateFilter := PeriodFilter;

                               Counter := 3;
                               PeriodStartDate[10] := PeriodFilterStart;
                               PeriodEndDate[10] := PeriodFilterEnd;

                               FOR i := 9 DOWNTO 1 DO BEGIN
                                 SearchPeriod.FindPeriod(PeriodStartDate[i], PeriodEndDate[i], CumulationFilter, DateFilter, PeriodType, '<=');
                                 ExcelAutomation.WriteCell(1, Counter + i, STRSUBSTNO('%1', PeriodStartDate[i]));
                               END;
                               ExcelAutomation.WriteCell(1, Counter + 10, STRSUBSTNO('%1', PeriodStartDate[10]));
                             END;

               OnAfterGetRecord=VAR
                                  i@1100528700 : Integer;
                                BEGIN
                                  CreateCostControl.RUN(Job);

                                  ProjRec.GET("No.");
                                  ProjRec.SETFILTER("Project Filter","No.");

                                  FOR i := 10 DOWNTO 1 DO BEGIN
                                    ProjRec.SETRANGE("Cumulation Filter",0D,PeriodEndDate[i]);
                                    ProjRec.CALCFIELDS("Actual Costs", Revenues, "Project Contract Amount", "Extension Contract Amount", "Options Contract Amount");
                                    ActCostDec[i] := ProjRec."Actual Costs";
                                    RevDec[i] := ProjRec.Revenues;
                                    TotCostDec[i] := CostControlMgt.TotCostProject(ProjRec,TRUE);
                                    ContrAmntDec[i] := ProjRec."Project Contract Amount" + ProjRec."Extension Contract Amount" + ProjRec."Options Contract Amount";
                                  END;
                                END;

               ReqFilterFields=No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      CaptionML=[DEU=Periodenfilter;
                 ENU=Period Filter;
                 NLD=Periodefilter;
                 NOR=Periodefilter;
                 SVE=Periodfilter];
    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525001;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100528700;2;Field  ;
                  CaptionML=[DEU=Periodentyp;
                             ENU=Period Type;
                             NLD=Periodesoort;
                             NOR=Periodetype;
                             SVE=Periodtyp];
                  OptionCaptionML=[DEU=Tag,Woche,Monat,Quartal,Jahr,Buchhaltungsperiode;
                                   ENU=Day,Week,Month,Quarter,Year,Accounting Period;
                                   NLD=Dag,Week,Maand,Kwartaal,Jaar,Boekhoudperiode;
                                   NOR=Dag,Uke,M†ned,Kvartal,r,bokf›ringsperiode;
                                   SVE=Dag,Vecka,M†nad,Kvartal,r,Bokf”ringsperiod];
                  SourceExpr=PeriodType }

      { 1100528701;2;Field  ;
                  CaptionML=[DEU=Periodenfilter;
                             ENU=Period Filter;
                             NLD=Periodefilter;
                             NOR=Periodefilter;
                             SVE=Periodfilter];
                  SourceExpr=PeriodFilter;
                  OnValidate=VAR
                               DateSelectionManagement@1100528700 : Codeunit 11130005;
                             BEGIN
                               DateSelectionManagement.ConvertPeriodStringToDates(PeriodFilter, PeriodFilterStart, PeriodFilterEnd);
                             END;

                  OnLookup=VAR
                             DateSelectionManagement@1100528700 : Codeunit 11130005;
                           BEGIN
                             DateSelectionManagement.GetPeriod(PeriodFilter);
                             DateSelectionManagement.ConvertPeriodStringToDates(PeriodFilter, PeriodFilterStart, PeriodFilterEnd);
                             Text := PeriodFilter;
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@11012031 : TextConst 'DEU=Periodenbersicht;ENU=Period List;NLD=Periodeoverzicht;NOR=Periodeliste';
      Text002@11012033 : TextConst 'DEU=Projektkosten und Ertr„ge;ENU=Project Cost and Revenue;NLD=Projectkosten en opbrengsten;NOR=Prosjektkoster og inntekter';
      Text004@11012035 : TextConst 'DEU=Periode;ENU=Period;NLD=Periode;NOR=Periode;SVE=Period';
      Text006@11012037 : TextConst 'DEU=Ist-Kosten;ENU=Actual Cost;NLD=Besteed;NOR=Faktisk kost;SVE=Faktisk kostnad';
      Text008@11012039 : TextConst 'DEU=Ertr„ge;ENU=Revenues;NLD=Opbrengsten;NOR=Inntekter;SVE=Int„kter';
      Text009@1100485001 : TextConst 'DEU=Vertragspreis;ENU=Contract Amount;NLD=Aanneemsom;NOR=Kontraktsbel›p;SVE=Kontraktsbelopp';
      Text010@1100485002 : TextConst 'DEU=Einstandsbetrag;ENU=Total Cost;NLD=Totale kosten;NOR=Total kost;SVE=Totalkostnad';
      Text014@11012045 : TextConst 'DEU=Diagramm;ENU=Graph;NLD=Grafiek;NOR=Graf;SVE=Diagram';
      Text016@11012047 : TextConst 'DEU=S-Kurve Projekt %1;ENU=S Curve Project %1;NLD=S curve project %1;NOR=S-kurveprosjekt %1';
      Text017@11012048 : TextConst 'DEU=W„hlen Sie die Periode in Registerkart ''Optionen'' aus;ENU=Select Period on Option Tab;NLD=Geef periodeselectie in onder optie tabblad.;NOR=Velg periode p† alternativfanen;SVE=V„lj period p† alternativfliken.';
      DotNetManagement@1100528704 : Codeunit 11012374;
      ExcelEnumeration@1100528703 : Codeunit 11012375;
      ExcelAutomation@1100528706 : Codeunit 11012402;
      PeriodStartDate@11012002 : ARRAY [10] OF Date;
      PeriodEndDate@11012003 : ARRAY [10] OF Date;
      ActCostDec@11012004 : ARRAY [10] OF Decimal;
      RevDec@11012005 : ARRAY [10] OF Decimal;
      ContrAmntDec@1100485004 : ARRAY [10] OF Decimal;
      TotCostDec@1100485005 : ARRAY [10] OF Decimal;
      PeriodType@11012012 : 'Day,Week,Month,Quarter,Year,Accounting Period';
      PeriodFilter@1100528705 : Text;
      SearchPeriod@11012013 : Codeunit 11012016;
      PeriodFilterStart@11012014 : Date;
      PeriodFilterEnd@11012015 : Date;
      ProjRec@11012019 : Record 11072003;
      CreateCostControl@11012021 : Codeunit 11012012;
      CostControlMgt@1100485006 : Codeunit 11012006;
      Counter@11012028 : Integer;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

