OBJECT Codeunit 9000 Users - Create Super User
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    OnRun=VAR
            User@1000 : Record 2000000120;
          BEGIN
            IF NOT User.ISEMPTY THEN
              EXIT;

            SafeCreateUser(USERID,SID);
          END;

  }
  CODE
  {
    VAR
      SuperPermissonSetDescTxt@1000 : TextConst 'ENU=This user has all permissions.;NOR=Denne brukeren har alle tillatelser.;SVE=Anv„ndaren har alla beh”righeter.';

    PROCEDURE SafeCreateUser@47(UserID@1002 : Code[50];SID@1003 : Text[119]);
    VAR
      User@1001 : Record 2000000120;
      PermissionSet@1000 : Record 2000000004;
    BEGIN
      User.SETRANGE("User Name",UserID);
      IF User.FINDFIRST THEN
        EXIT;

      GetSuperRole(PermissionSet);
      CreateUser(User,UserID,SID);
      AssignPermissionSetToUser(User,PermissionSet);
    END;

    LOCAL PROCEDURE GetSuperRole@1(VAR PermissionSet@1000 : Record 2000000004);
    VAR
      Permission@1001 : Record 2000000005;
    BEGIN
      IF PermissionSet.GET('SUPER') THEN
        EXIT;
      PermissionSet."Role ID" := 'SUPER';
      PermissionSet.Name := COPYSTR(SuperPermissonSetDescTxt,1,MAXSTRLEN(PermissionSet.Name));
      PermissionSet.INSERT(TRUE);
      AddPermissionToPermissionSet(PermissionSet,Permission."Object Type"::"Table Data",0);
      AddPermissionToPermissionSet(PermissionSet,Permission."Object Type"::Table,0);
      AddPermissionToPermissionSet(PermissionSet,Permission."Object Type"::Report,0);
      AddPermissionToPermissionSet(PermissionSet,Permission."Object Type"::Codeunit,0);
      AddPermissionToPermissionSet(PermissionSet,Permission."Object Type"::XMLport,0);
      AddPermissionToPermissionSet(PermissionSet,Permission."Object Type"::MenuSuite,0);
      AddPermissionToPermissionSet(PermissionSet,Permission."Object Type"::Page,0);
      AddPermissionToPermissionSet(PermissionSet,Permission."Object Type"::Query,0);
      AddPermissionToPermissionSet(PermissionSet,Permission."Object Type"::System,0);
    END;

    LOCAL PROCEDURE CreateUser@4(VAR User@1003 : Record 2000000120;UserName@1000 : Code[50];WindowsSecurityID@1001 : Text[119]);
    BEGIN
      User.INIT;
      User."User Security ID" := CREATEGUID;
      User."User Name" := UserName;
      User."Windows Security ID" := WindowsSecurityID;
      User.INSERT(TRUE);
    END;

    LOCAL PROCEDURE AddPermissionToPermissionSet@7(VAR PermissionSet@1000 : Record 2000000004;ObjectType@1001 : Option;ObjectID@1002 : Integer);
    VAR
      Permission@1003 : Record 2000000005;
    BEGIN
      WITH Permission DO BEGIN
        INIT;
        "Role ID" := PermissionSet."Role ID";
        "Object Type" := ObjectType;
        "Object ID" := ObjectID;
        IF "Object Type" = "Object Type"::"Table Data" THEN
          "Execute Permission" := "Execute Permission"::" "
        ELSE BEGIN
          "Read Permission" := "Read Permission"::" ";
          "Insert Permission" := "Insert Permission"::" ";
          "Modify Permission" := "Modify Permission"::" ";
          "Delete Permission" := "Delete Permission"::" ";
        END;
        INSERT(TRUE);
      END;
    END;

    LOCAL PROCEDURE AssignPermissionSetToUser@5(VAR User@1000 : Record 2000000120;VAR PermissionSet@1001 : Record 2000000004);
    VAR
      AccessControl@1002 : Record 2000000053;
    BEGIN
      AccessControl.SETRANGE("User Security ID",User."User Security ID");
      AccessControl.SETRANGE("Role ID",PermissionSet."Role ID");
      IF NOT AccessControl.ISEMPTY THEN
        EXIT;
      AccessControl."User Security ID" := User."User Security ID";
      AccessControl."Role ID" := PermissionSet."Role ID";
      AccessControl.INSERT(TRUE);
    END;

    PROCEDURE AddUserAsSuper@2(VAR User@1000 : Record 2000000120);
    VAR
      PermissionSet@1001 : Record 2000000004;
    BEGIN
      GetSuperRole(PermissionSet);
      AssignPermissionSetToUser(User,PermissionSet);
    END;

    BEGIN
    END.
  }
}

