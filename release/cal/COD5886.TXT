OBJECT Codeunit 5886 Phys. Invt.-Show Duplicates
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    TableNo=5875;
    OnRun=BEGIN
            PhysInvtOrderHeader.COPY(Rec);
            Code;
            Rec := PhysInvtOrderHeader;
          END;

  }
  CODE
  {
    VAR
      CheckingLinesMsg@1000 : TextConst '@@@="%2 = counter";ENU=Checking lines        #2######;NOR=Kontrollerer linjer        #2######';
      NoDuplicateLinesMsg@1001 : TextConst '@@@="%1 = Order No.";ENU=There are no duplicate order lines in order %1.;NOR=Det er ingen dupliserte ordrelinjer i ordren %1.';
      DuplicatesFoundQst@1002 : TextConst '@@@="%1 = duplicates count, %2 = Order No.";ENU=There are %1 duplicate order lines in order %2.\Do you want to show it?;NOR=Det finnes %1 duplikat ordrelinjer i ordren %2.\Vil du vise den?';
      PhysInvtOrderHeader@1004 : Record 5875;
      PhysInvtOrderLine@1005 : Record 5876;
      PhysInvtOrderLine2@1006 : Record 5876;
      Window@1007 : Dialog;
      ErrorText@1008 : Text[250];
      LineCount@1009 : Integer;
      DuplicateCount@1010 : Integer;

    [External]
    PROCEDURE Code@1000();
    BEGIN
      WITH PhysInvtOrderHeader DO BEGIN
        Window.OPEN(
          '#1################################\\' + CheckingLinesMsg);
        Window.UPDATE(1,STRSUBSTNO('%1 %2',TABLECAPTION,"No."));

        LineCount := 0;
        DuplicateCount := 0;
        PhysInvtOrderLine.RESET;
        PhysInvtOrderLine.SETRANGE("Document No.","No.");
        PhysInvtOrderLine.CLEARMARKS;
        IF PhysInvtOrderLine.FIND('-') THEN
          REPEAT
            LineCount := LineCount + 1;
            Window.UPDATE(2,LineCount);
            IF NOT PhysInvtOrderLine.EmptyLine THEN BEGIN
              PhysInvtOrderLine.TESTFIELD("Item No.");
              IF
                 GetSamePhysInvtOrderLine(
                   PhysInvtOrderLine."Item No.",PhysInvtOrderLine."Variant Code",
                   PhysInvtOrderLine."Location Code",PhysInvtOrderLine."Bin Code",
                   ErrorText,
                   PhysInvtOrderLine2) > 1
              THEN BEGIN
                PhysInvtOrderLine.MARK(TRUE);
                DuplicateCount := DuplicateCount + 1;
              END;
            END;
          UNTIL PhysInvtOrderLine.NEXT = 0;

        Window.CLOSE;

        IF DuplicateCount = 0 THEN
          MESSAGE(STRSUBSTNO(NoDuplicateLinesMsg,"No."))
        ELSE
          IF CONFIRM(STRSUBSTNO(DuplicatesFoundQst,DuplicateCount,"No."),TRUE) THEN BEGIN
            PhysInvtOrderLine.MARKEDONLY(TRUE);
            PAGE.RUN(0,PhysInvtOrderLine);
          END;
      END;
    END;

    BEGIN
    END.
  }
}

