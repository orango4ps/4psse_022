OBJECT Report 11012657 Create IC Revaluations
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=IC-Neubewertungen erstellen;
               ENU=Create IC Revaluations;
               NLD=IC herwaarderingen aanmaken;
               NOR=Opprette IC-revalueringer;
               SVE=Skapa IC-omv„rderingar];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   PostingDate := TODAY;
                 END;

  }
  DATASET
  {
    { 8129;    ;DataItem;                    ;
               DataItemTable=Table27;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=VAR
                               ICRevaluation@1100525000 : Record 11020696;
                             BEGIN
                               ICRevaluation.SETRANGE("Post Date", PostingDate);
                               ICRevaluation.DELETEALL;
                             END;

               OnAfterGetRecord=VAR
                                  ICRevaluation@1100525000 : Record 11020696;
                                BEGIN
                                  IF CalcBase = CalcBase::"Vendor Price" THEN BEGIN
                                    IF ("Vendor No." = '') THEN
                                      CurrReport.SKIP;
                                  END ELSE BEGIN
                                    IF (NOT ICRevaluation.GET(PostingDate, Item."No.")) THEN BEGIN
                                      ICRevaluation."Post Date" := PostingDate;
                                      ICRevaluation."Item No." := Item."No.";
                                      ICRevaluation.INSERT;
                                    END;
                                    ICRevaluation."Unit Cost (Calculated)" := Item."Standard Cost";
                                    ICRevaluation."Unit Cost (Revalued)" := Item."Last Direct Cost";
                                    ICRevaluation.MODIFY;
                                  END;
                                END;

               ReqFilterFields=No. }

    { 3889;1   ;DataItem;                    ;
               DataItemTable=Table7012;
               DataItemTableView=SORTING(Item No.,Vendor No.,Starting Date,Currency Code,Variant Code,Unit of Measure Code,Minimum Quantity);
               OnPreDataItem=BEGIN
                               IF CalcBase <> CalcBase::"Vendor Price" THEN
                                 CurrReport.BREAK;

                               "Purchase Price".SETFILTER("Starting Date", '..%1', PostingDate);
                               "Purchase Price".SETFILTER("Ending Date", '%1|%2..', 0D, PostingDate);
                             END;

               OnAfterGetRecord=BEGIN
                                  ModifyICRevaluation;
                                END;

               DataItemLink=Item No.=FIELD(No.),
                            Vendor No.=FIELD(Vendor No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525001;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100525002;2;Field  ;
                  Name=CalcBase;
                  CaptionML=[DEU=Berechnungsgrundlage;
                             ENU=Calculation Base;
                             NLD=Berekeningsbasis;
                             NOR=Beregningsgrunnlag;
                             SVE=Ber„kningsbas];
                  OptionCaptionML=[DEU=Kreditorenpreis,Letzter Einkaufspreis;
                                   ENU=Vendor Price,Last Purchase Price;
                                   NLD=Leveranciers prijs,Laatste inkoopprijs];
                  SourceExpr=CalcBase }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      PostingDate@1100528300 : Date;
      CalcBase@1100525000 : 'Vendor Price,Last Direct Cost';

    PROCEDURE ModifyICRevaluation@1100528300();
    VAR
      PurchasePrice@1100525000 : Record 7012;
      ICRevaluation@1100528300 : Record 11020696;
    BEGIN
      PurchasePrice.SETRANGE("Item No.", Item."No.");
      PurchasePrice.SETRANGE("Vendor No.", Item."Vendor No.");
      PurchasePrice.SETRANGE("Unit of Measure Code", Item."Base Unit of Measure"); //#C020481.n
      PurchasePrice.SETRANGE("Minimum Quantity", 0); //#C020481.n
      PurchasePrice.SETFILTER("Starting Date", '..%1', PostingDate);
      PurchasePrice.SETFILTER("Ending Date", '%1|%2..', 0D, PostingDate);
      IF (PurchasePrice.FINDLAST) THEN BEGIN
        IF (NOT ICRevaluation.GET(PostingDate, Item."No.")) THEN BEGIN
          ICRevaluation."Post Date" := PostingDate;
          ICRevaluation."Item No." := Item."No.";
          ICRevaluation.INSERT;
        END;
        ICRevaluation."Unit Cost (Calculated)" := Item."Standard Cost";
        ICRevaluation."Unit Cost (Revalued)" := PurchasePrice."Direct Unit Cost" * GetPurchaseLineDiscountFactor;
        ICRevaluation.MODIFY;

        IF (Item.GetItemLedgerEntryInventory = 0) THEN BEGIN
          Item.VALIDATE("Standard Cost", PurchasePrice."Direct Unit Cost" *
            GetPurchaseLineDiscountFactor);
          Item.MODIFY(TRUE);
        END;
      END;
    END;

    PROCEDURE GetPurchaseLineDiscountFactor@1100525000() : Decimal;
    VAR
      PurchaseLineDiscount@1100525000 : Record 7014;
    BEGIN
      PurchaseLineDiscount.SETRANGE("Item No.", Item."No.");
      PurchaseLineDiscount.SETRANGE("Vendor No.", Item."Vendor No.");
      PurchaseLineDiscount.SETRANGE("Unit of Measure Code", Item."Base Unit of Measure"); //#C020481.n
      PurchaseLineDiscount.SETRANGE("Minimum Quantity", 0); //#C020481.n
      PurchaseLineDiscount.SETFILTER("Starting Date", '..%1', PostingDate);
      PurchaseLineDiscount.SETFILTER("Ending Date", '%1|%2..', 0D, PostingDate);
      IF (PurchaseLineDiscount.FINDLAST) THEN
        EXIT(1 - (PurchaseLineDiscount."Line Discount %" / 100));
      EXIT(1);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

