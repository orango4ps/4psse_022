OBJECT Table 11072211 Plant - Service Order
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               InitServiceCompany;
               CheckLine;
             END;

    OnModify=BEGIN
               CheckLine;
             END;

    OnDelete=BEGIN
               TESTFIELD(Processed, FALSE);
             END;

    CaptionML=ENU=Plant - Service Order;
  }
  FIELDS
  {
    { 5   ;   ;Source Type         ;Option        ;DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Source Type;
                                                              SVE=Ursprungstyp];
                                                   OptionCaptionML=ENU=Plant Order Line,Plant Number,Inspection;
                                                   OptionString=PlantOrderLine,PlantNumber,Inspection }
    { 10  ;   ;Plant Order         ;Code20        ;CaptionML=[ENU=Plant Order;
                                                              NOR=Maskinordre;
                                                              SVE=Maskinorder] }
    { 15  ;   ;Plant Type          ;Code20        ;TableRelation="Plant Type";
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Plant Type;
                                                              SVE=Maskintyp];
                                                   Description=Must be empty for Source Type "Plant Order Line" (then use field 130) }
    { 16  ;   ;Plant No.           ;Code10        ;TableRelation="Plant Number".No. WHERE (Plant Type=FIELD(Plant Type));
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Plant No.;
                                                              SVE=Maskinnr];
                                                   NotBlank=Yes;
                                                   Description=Must be empty for Source Type "Plant Order Line" (then use field 140);
                                                   Editable=No }
    { 20  ;   ;Source Line No.     ;Integer       ;CaptionML=[ENU=Source Line No.;
                                                              NOR=Maskinordre, Radnr.;
                                                              SVE=Maskinorder, radnr] }
    { 30  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 40  ;   ;Cost Component      ;Code10        ;TableRelation="Cost Component Plant".Code;
                                                   OnValidate=BEGIN
                                                                IF NOT "New Service Order" THEN
                                                                  TESTFIELD("Cost Component", '');
                                                                CALCFIELDS("Cost Component Description");
                                                              END;

                                                   CaptionML=[ENU=Cost Component;
                                                              NOR=Kostnadskomponent;
                                                              SVE=Kostnadsdel] }
    { 45  ;   ;Cost Component Description;Text50  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Cost Component Plant".Description WHERE (Code=FIELD(Cost Component)));
                                                   CaptionML=[ENU=Cost Component Description;
                                                              SVE=Kostnadsdel, beskrivning];
                                                   Editable=Yes }
    { 50  ;   ;Service Order No.   ;Code20        ;TableRelation=IF (Service Company=FILTER('')) "Service Order";
                                                   CaptionML=[ENU=Service Order No.;
                                                              NOR=Serviceordre;
                                                              SVE=Serviceorder];
                                                   Editable=No }
    { 55  ;   ;Service Company     ;Text30        ;TableRelation=Company;
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Service Company;
                                                   Editable=No }
    { 60  ;   ;New Service Order   ;Boolean       ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                IF NOT "New Service Order" THEN BEGIN
                                                                  TESTFIELD("Cost Component", '');
                                                                  TESTFIELD("Order Kind", '');
                                                                  TESTFIELD("Order Type", '');
                                                                  TESTFIELD("Plant Location (SO)", '');
                                                                  TESTFIELD(Description, '');
                                                                  TESTFIELD("Description 2", '');
                                                                  TESTFIELD("Starting Date (SO)", 0D);
                                                                  TESTFIELD("No. Series (SO)", '');
                                                                END ELSE BEGIN
                                                                  IF "Maintenance Scheme" <> '' THEN
                                                                    VALIDATE("Maintenance Scheme");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=New Service Order;
                                                              NOR=Ny serviceordre;
                                                              SVE=Ny serviceorder] }
    { 70  ;   ;Description         ;Text50        ;OnValidate=BEGIN
                                                                IF NOT "New Service Order" THEN
                                                                  TESTFIELD(Description, '');
                                                              END;

                                                   CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 80  ;   ;Description 2       ;Text50        ;OnValidate=BEGIN
                                                                IF NOT "New Service Order" THEN
                                                                  TESTFIELD("Description 2", '');
                                                              END;

                                                   CaptionML=[ENU=Description 2;
                                                              NOR=Beskrivelse 2;
                                                              SVE=Beskrivning 2] }
    { 90  ;   ;Maintenance Scheme  ;Code20        ;TableRelation="Plant Stnd. Maintenance Scheme";
                                                   OnValidate=VAR
                                                                PlantMaintenanceScheme@1100529000 : Record 11072677;
                                                                PlantStndMaintenanceScheme@1100529001 : Record 11072673;
                                                              BEGIN
                                                                IF "Maintenance Scheme" <> '' THEN BEGIN
                                                                  CalcPlantFields;
                                                                  PlantMaintenanceScheme.GET("Plant Type (PO)", "Plant No. (PO)", "Maintenance Scheme");
                                                                  IF "New Service Order" THEN BEGIN
                                                                    IF ("Maintenance Scheme" <> xRec."Maintenance Scheme") OR (Description = '') THEN BEGIN
                                                                      CALCFIELDS("Maintenance Scheme Description");
                                                                      IF "Maintenance Scheme Description" <> '' THEN
                                                                        Description := "Maintenance Scheme Description";
                                                                    END;
                                                                    PlantStndMaintenanceScheme.GET("Maintenance Scheme");
                                                                    IF (("Maintenance Scheme" <> xRec."Maintenance Scheme") OR ("Cost Component" = '')) AND (PlantStndMaintenanceScheme."Cost Component" <> '') THEN BEGIN
                                                                      "Cost Component" := PlantStndMaintenanceScheme."Cost Component";
                                                                      CALCFIELDS("Cost Component Description");
                                                                    END;
                                                                    IF (("Maintenance Scheme" <> xRec."Maintenance Scheme") OR ("Order Kind" = '')) AND (PlantStndMaintenanceScheme."Order Kind" <> '') THEN
                                                                      "Order Kind" := PlantStndMaintenanceScheme."Order Kind";
                                                                    IF (("Maintenance Scheme" <> xRec."Maintenance Scheme") OR ("Order Type" = '')) AND (PlantStndMaintenanceScheme."Order Type" <> '') THEN
                                                                      "Order Type" := PlantStndMaintenanceScheme."Order Type";
                                                                  END;
                                                                END;
                                                                CALCFIELDS("Maintenance Scheme Description");
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupMaintenanceScheme(NOT Processed);
                                                            END;

                                                   CaptionML=[ENU=Maintenance Scheme;
                                                              NOR=Vedlikeholdsskjema;
                                                              SVE=Underh†llsschema] }
    { 91  ;   ;Maintenance Scheme Description;Text50;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Stnd. Maintenance Scheme".Description WHERE (Code=FIELD(Maintenance Scheme)));
                                                   CaptionML=[ENU=Maintenance Scheme Description;
                                                              SVE=Beskrivning av underh†llsschema];
                                                   Editable=No }
    { 100 ;   ;Processed           ;Boolean       ;CaptionML=[ENU=Processed;
                                                              NOR=Behandlede;
                                                              SVE=Behandlades];
                                                   Editable=No }
    { 110 ;   ;Process Date        ;DateTime      ;CaptionML=[ENU=Process Date;
                                                              NOR=Behandlingsdato;
                                                              SVE=Processdatum];
                                                   Editable=No }
    { 120 ;   ;Processed By        ;Code50        ;CaptionML=[ENU=Processed By;
                                                              SVE=Behandlades av];
                                                   Editable=No }
    { 130 ;   ;Plant Type (PO)     ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Order Line"."Plant Type" WHERE (Plant Order No.=FIELD(Plant Order),
                                                                                                             Line No.=FIELD(Source Line No.)));
                                                   CaptionML=[ENU=Plant Type (PO);
                                                              NOR=Maskintype;
                                                              SVE=Maskintyp];
                                                   Editable=No }
    { 140 ;   ;Plant No. (PO)      ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Order Line".No. WHERE (Plant Order No.=FIELD(Plant Order),
                                                                                                    Line No.=FIELD(Source Line No.)));
                                                   CaptionML=[ENU=Plant No. (PO);
                                                              NOR=Maskinnr.;
                                                              SVE=Maskinnummer];
                                                   Editable=No }
    { 150 ;   ;Plant Location (SO) ;Code20        ;TableRelation="Plant Location";
                                                   OnValidate=VAR
                                                                PlantLocation@1100529000 : Record 11012554;
                                                              BEGIN
                                                                IF NOT "New Service Order" THEN
                                                                  TESTFIELD("Plant Location (SO)", '');
                                                                IF "Plant Location (SO)" <> '' THEN BEGIN
                                                                  PlantLocation.GET("Plant Location (SO)");
                                                                  PlantLocation.TESTFIELD("Service Order No.", '');  // As check on field 'Plant Location' in 'Service Order' table
                                                                END;
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Plant Location (SO) }
    { 160 ;   ;Starting Date (SO)  ;Date          ;OnValidate=BEGIN
                                                                IF NOT "New Service Order" THEN
                                                                  TESTFIELD("Starting Date (SO)", 0D);
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Starting Date (SO) }
    { 170 ;   ;Order Kind          ;Code10        ;TableRelation=IF (Service Company=FILTER('')) "Service Order Indication".Code WHERE (Indication=CONST(Kind));
                                                   OnValidate=VAR
                                                                ServiceOrderIndication@1100529000 : Record 11012837;
                                                                CostComponentPlant@1100529001 : Record 11012575;
                                                              BEGIN
                                                                IF "Order Kind" <> '' THEN BEGIN
                                                                  IF NOT "New Service Order" THEN
                                                                    TESTFIELD("Order Kind", '');
                                                                  IF "Service Company" <> '' THEN
                                                                    ServiceOrderIndication.CHANGECOMPANY("Service Company");
                                                                  ServiceOrderIndication.GET(ServiceOrderIndication.Indication::Kind, "Order Kind");
                                                                  IF ("Order Kind" <> xRec."Order Kind") AND (ServiceOrderIndication."Cost Component Plant" <> '') AND "New Service Order" THEN BEGIN
                                                                    IF CostComponentPlant.GET(ServiceOrderIndication."Cost Component Plant") THEN
                                                                      VALIDATE("Cost Component", ServiceOrderIndication."Cost Component Plant");
                                                                  END;
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              ServiceOrderIndication@1100529000 : Record 11012837;
                                                            BEGIN
                                                              IF NOT "New Service Order" THEN
                                                                EXIT;
                                                              IF "Service Company" <> '' THEN
                                                                ServiceOrderIndication.CHANGECOMPANY("Service Company");
                                                              ServiceOrderIndication.SETRANGE(Indication, ServiceOrderIndication.Indication::Kind);
                                                              ServiceOrderIndication.FILTERGROUP(0);
                                                              ServiceOrderIndication.Indication := ServiceOrderIndication.Indication::Kind;
                                                              ServiceOrderIndication.Code := "Order Kind";
                                                              IF PAGE.RUNMODAL(0, ServiceOrderIndication) = ACTION::LookupOK THEN
                                                                VALIDATE("Order Kind", ServiceOrderIndication.Code);
                                                            END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Order Kind;
                                                              SVE=Typ av order] }
    { 180 ;   ;Order Type          ;Code10        ;TableRelation=IF (Service Company=FILTER('')) "Service Order Indication".Code WHERE (Indication=CONST(Type));
                                                   OnValidate=VAR
                                                                ServiceOrderIndication@1100529000 : Record 11012837;
                                                              BEGIN
                                                                IF "Order Type" <> '' THEN BEGIN
                                                                  IF NOT "New Service Order" THEN
                                                                    TESTFIELD("Order Type", '');
                                                                  IF "Service Company" <> '' THEN
                                                                    ServiceOrderIndication.CHANGECOMPANY("Service Company");
                                                                  ServiceOrderIndication.GET(ServiceOrderIndication.Indication::Type, "Order Type");
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              ServiceOrderIndication@1100529000 : Record 11012837;
                                                            BEGIN
                                                              IF NOT "New Service Order" THEN
                                                                EXIT;
                                                              IF "Service Company" <> '' THEN
                                                                ServiceOrderIndication.CHANGECOMPANY("Service Company");
                                                              ServiceOrderIndication.SETRANGE(Indication, ServiceOrderIndication.Indication::Type);
                                                              ServiceOrderIndication.FILTERGROUP(0);
                                                              ServiceOrderIndication.Indication := ServiceOrderIndication.Indication::Type;
                                                              ServiceOrderIndication.Code := "Order Type";
                                                              IF PAGE.RUNMODAL(0, ServiceOrderIndication) = ACTION::LookupOK THEN
                                                                VALIDATE("Order Type", ServiceOrderIndication.Code);
                                                            END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Order Type;
                                                              SVE=Typ av order] }
    { 190 ;   ;No. Series (SO)     ;Code20        ;TableRelation=IF (Service Company=FILTER('')) "No. Series";
                                                   OnValidate=VAR
                                                                ServiceSourceTypeSetup@1100529002 : Record 11071726;
                                                                NoSeries@1100529001 : Record 308;
                                                                NoSeriesRelationship@1100529000 : Record 310;
                                                              BEGIN
                                                                IF "No. Series (SO)" <> '' THEN BEGIN
                                                                  IF NOT "New Service Order" THEN
                                                                   TESTFIELD("No. Series (SO)", '');
                                                                  IF "Service Company" <> '' THEN BEGIN
                                                                    ServiceSourceTypeSetup.CHANGECOMPANY("Service Company");
                                                                    NoSeries.CHANGECOMPANY("Service Company");
                                                                    NoSeriesRelationship.CHANGECOMPANY("Service Company");
                                                                  END;
                                                                  NoSeries.GET("No. Series (SO)");
                                                                  ServiceSourceTypeSetup.GET(ServiceSourceTypeSetup."Source Type"::Direct);
                                                                  ServiceSourceTypeSetup.TESTFIELD("No. Series");
                                                                  IF "No. Series (SO)" <> ServiceSourceTypeSetup."No. Series" THEN BEGIN
                                                                    NoSeriesRelationship.SETRANGE(Code, ServiceSourceTypeSetup."No. Series");
                                                                    NoSeriesRelationship.FINDFIRST;
                                                                  END;
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupNoSeriesSO;
                                                            END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=No. Series (SO) }
  }
  KEYS
  {
    {    ;Source Type,Plant Order,Plant Type,Plant No.,Source Line No.,Line No.;
                                                   Clustered=Yes }
    {    ;Service Order No.                        }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PlantSetup@1100529000 : Record 11012550;

    PROCEDURE InitServiceCompany@1100529000();
    BEGIN
      PlantSetup.GET;
      PlantSetup.TESTFIELD("Service Company");
      IF PlantSetup."Service Company" = COMPANYNAME THEN
        "Service Company" := ''
      ELSE
        "Service Company" := PlantSetup."Service Company";
    END;

    PROCEDURE CalcPlantFields@1100529005();
    BEGIN
      IF "Source Type" <> "Source Type"::PlantOrderLine THEN BEGIN
        "Plant Type (PO)" := "Plant Type";
        "Plant No. (PO)" := "Plant No.";
      END ELSE BEGIN
        CALCFIELDS("Plant Type (PO)", "Plant No. (PO)");
        IF "Plant No. (PO)" = '0' THEN
          "Plant No. (PO)" := '';
      END;
    END;

    PROCEDURE LookupMaintenanceScheme@1100525000(EditableMode@1100525002 : Boolean);
    VAR
      PlantMaintenanceScheme@1100525001 : Record 11072677;
      PlantStndMaintenanceScheme@1100525000 : Record 11072673;
    BEGIN
      IF Processed THEN
        EditableMode := FALSE;
      CalcPlantFields;
      PlantMaintenanceScheme.SETRANGE("Plant Type", "Plant Type (PO)");
      PlantMaintenanceScheme.SETRANGE("Plant No.", "Plant No. (PO)");
      IF PlantMaintenanceScheme.FINDSET THEN BEGIN
        REPEAT
          IF PlantStndMaintenanceScheme.GET(PlantMaintenanceScheme."Maintenance Scheme") THEN
            PlantStndMaintenanceScheme.MARK(TRUE);
        UNTIL PlantMaintenanceScheme.NEXT = 0;
      END;
      PlantStndMaintenanceScheme.MARKEDONLY(TRUE);
      PlantStndMaintenanceScheme.Code := "Maintenance Scheme";
      IF NOT EditableMode THEN
        PAGE.RUNMODAL(0, PlantStndMaintenanceScheme)
      ELSE BEGIN
        IF PAGE.RUNMODAL(0, PlantStndMaintenanceScheme) = ACTION::LookupOK THEN
          VALIDATE("Maintenance Scheme", PlantStndMaintenanceScheme.Code);
      END;
    END;

    LOCAL PROCEDURE LookupNoSeriesSO@1100529001();
    VAR
      ServiceSourceTypeSetup@1100529000 : Record 11071726;
      NoSeries@1100529005 : Record 308;
      NoSeriesRelationship@1100529001 : Record 310;
      NoSeriesMgt@1100529003 : Codeunit 396;
      NewNoSeries@1100529004 : Code[10];
    BEGIN
      IF NOT "New Service Order" THEN
        EXIT;
      IF "Service Company" <> '' THEN BEGIN
        ServiceSourceTypeSetup.CHANGECOMPANY("Service Company");
        NoSeries.CHANGECOMPANY("Service Company");
        NoSeriesRelationship.CHANGECOMPANY("Service Company");
      END;
      ServiceSourceTypeSetup.GET(ServiceSourceTypeSetup."Source Type"::Direct);
      ServiceSourceTypeSetup.TESTFIELD("No. Series");
      IF "Service Company" = '' THEN BEGIN
        IF NoSeriesMgt.SelectSeries(ServiceSourceTypeSetup."No. Series", ServiceSourceTypeSetup."No. Series", NewNoSeries) THEN
          VALIDATE("No. Series (SO)", NewNoSeries);
      END ELSE BEGIN
        NoSeriesRelationship.SETRANGE(Code, ServiceSourceTypeSetup."No. Series");
        IF NoSeriesRelationship.FINDSET THEN BEGIN
          REPEAT
            IF NoSeries.GET(NoSeriesRelationship."Series Code") THEN
              NoSeries.MARK(TRUE);
          UNTIL NoSeriesRelationship.NEXT = 0;
        END;
        IF NoSeries.GET(ServiceSourceTypeSetup."No. Series") THEN
          NoSeries.MARK(TRUE);
        NoSeries.MARKEDONLY(TRUE);
        IF PAGE.RUNMODAL(0,NoSeries) = ACTION::LookupOK THEN
          VALIDATE("No. Series (SO)", NoSeries.Code);
      END;
    END;

    PROCEDURE CheckLine@1100529009();
    VAR
      PlantServiceOrder@1100529000 : Record 11072211;
      ServiceOrder@1100529001 : Record 11012823;
    BEGIN
      TESTFIELD(Processed, FALSE);
      IF "Maintenance Scheme" = '' THEN
        TESTFIELD("New Service Order", TRUE);

      IF NOT "New Service Order" THEN BEGIN
        PlantServiceOrder.SETRANGE("Source Type", "Source Type");
        PlantServiceOrder.SETRANGE("Plant Order", "Plant Order");
        PlantServiceOrder.SETRANGE("Plant Type", "Plant Type");
        PlantServiceOrder.SETRANGE("Plant No.", "Plant No.");
        PlantServiceOrder.SETRANGE("Source Line No.", "Source Line No.");
        PlantServiceOrder.SETFILTER("Line No.", '<%1', "Line No.");
        PlantServiceOrder.SETRANGE("New Service Order", TRUE);
        IF NOT PlantServiceOrder.FINDLAST THEN
          TESTFIELD("New Service Order", TRUE)
        ELSE BEGIN
          IF PlantServiceOrder."Maintenance Scheme" = '' THEN
            TESTFIELD("New Service Order", TRUE);
          IF PlantServiceOrder."Service Order No." <> '' THEN BEGIN
            IF PlantServiceOrder."Service Company" <> '' THEN
              ServiceOrder.CHANGECOMPANY(PlantServiceOrder."Service Company");
            ServiceOrder.GET(PlantServiceOrder."Service Order No.");
            IF ServiceOrder.Status > ServiceOrder.Status::"In Process" THEN
              ServiceOrder.FIELDERROR(Status);
          END;
          PlantServiceOrder.SETRANGE("New Service Order");
          PlantServiceOrder.SETRANGE("Line No.", PlantServiceOrder."Line No.", "Line No."-1);
          PlantServiceOrder.SETRANGE("Maintenance Scheme", "Maintenance Scheme");
          IF NOT PlantServiceOrder.ISEMPTY THEN
            FIELDERROR("Maintenance Scheme");
        END;
      END;
    END;

    BEGIN
    END.
  }
}

