OBJECT Page 9401 VAT Amount Lines
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=VAT Amount Lines;
               NOR=Mva-bel›pslinjer;
               SVE=Momsbelopprader];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table290;
    PageType=List;
    OnInit=BEGIN
             InvoiceDiscountAmountEditable := TRUE;
             VATAmountEditable := TRUE;
           END;

    OnFindRecord=BEGIN
                   TempVATAmountLine.COPY(Rec);
                   IF TempVATAmountLine.FIND(Which) THEN BEGIN
                     Rec := TempVATAmountLine;
                     EXIT(TRUE);
                   END;
                   EXIT(FALSE);
                 END;

    OnNextRecord=VAR
                   ResultSteps@1001 : Integer;
                 BEGIN
                   TempVATAmountLine.COPY(Rec);
                   ResultSteps := TempVATAmountLine.NEXT(Steps);
                   IF ResultSteps <> 0 THEN
                     Rec := TempVATAmountLine;
                   EXIT(ResultSteps);
                 END;

    OnAfterGetRecord=BEGIN
                       VATAmountEditable := AllowVATDifference AND NOT "Includes Prepayment";
                       InvoiceDiscountAmountEditable := AllowInvDisc AND NOT "Includes Prepayment";
                     END;

    OnModifyRecord=BEGIN
                     ModifyRec;
                     EXIT(FALSE);
                   END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the contents of this field from the VAT Identifier field in the VAT Posting Setup table.;
                           NOR=Angir innholdet i dette feltet fra feltet Mva-type i tabellen Mva-bokf›ringsoppsett.;
                           SVE=Anger inneh†llet i det h„r f„ltet fr†n f„ltet Moms-id i tabellen Moms bokf”ringsinst„llning.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="VAT Identifier";
                Visible=FALSE }

    { 6   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the VAT % that was used on the sales or purchase lines with this VAT Identifier.;
                           NOR=Angir mva-prosentandelen som er brukt p† salgs- eller bestillingslinjer med denne mva-typen.;
                           SVE=Anger den momsprocentsats som anv„ndes p† f”rs„ljnings- eller ink”psraderna med detta moms-id.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="VAT %" }

    { 8   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies how VAT will be calculated for purchases or sales of items with this particular combination of VAT business posting group and VAT product posting group.;
                           NOR=Angir hvordan mva. skal beregnes for kj›p eller salg av varer med denne spesielle kombinasjonen av mva-bokf›ringsgruppe - firma og mva-bokf›ringsgruppe - vare.;
                           SVE=Anger hur moms ska ber„knas vid ink”p och f”rs„ljning av artiklar med just denna kombination av r”relse- och produktbokf”ringsmall med moms.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="VAT Calculation Type";
                Visible=FALSE }

    { 10  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the net VAT amount that must be paid for products on the line.;
                           NOR=Angir nettobel›pet for merverdiavgift som m† betales for produkter p† linjen.;
                           SVE=Anger nettomomsbeloppet som m†ste betalas f”r produkter p† raden.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Line Amount";
                AutoFormatType=1;
                AutoFormatExpr=CurrencyCode }

    { 12  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the invoice discount base amount.;
                           NOR=Angir grunnlaget for fakturarabattbel›pet.;
                           SVE=Anger grundfakturarabatten.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Inv. Disc. Base Amount";
                AutoFormatType=1;
                AutoFormatExpr=CurrencyCode;
                Visible=FALSE }

    { 14  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the invoice discount amount for a specific VAT identifier.;
                           NOR=Angir fakturarabattbel›pet for en bestemt mva-type.;
                           SVE=Anger fakturarabatten f”r ett visst moms-id.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Invoice Discount Amount";
                AutoFormatType=1;
                AutoFormatExpr=CurrencyCode;
                Visible=FALSE;
                Editable=InvoiceDiscountAmountEditable;
                OnValidate=BEGIN
                             CalcVATFields(CurrencyCode,PricesIncludingVAT,VATBaseDiscPct);
                             ModifyRec;
                           END;
                            }

    { 16  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the total net amount (amount excluding VAT) for sales or purchase lines with a specific VAT Identifier.;
                           NOR=Angir det totale nettobel›pet (bel›pet ekskl. mva.) for salgs- eller bestillingslinjer med en bestemt mva-type.;
                           SVE=Anger det totala nettobeloppet (belopp exklusive moms) f”r f”rs„ljnings- eller ink”psrader med ett visst moms-id.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="VAT Base";
                AutoFormatType=1;
                AutoFormatExpr=CurrencyCode }

    { 20  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the amount of VAT that is included in the total amount.;
                           NOR=Angir mva-bel›pet som er inkludert i totalbel›pet.;
                           SVE=Anger momsbeloppet som ing†r i det totala beloppet.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="VAT Amount";
                AutoFormatType=1;
                AutoFormatExpr=CurrencyCode;
                Editable=VATAmountEditable;
                OnValidate=BEGIN
                             IF AllowVATDifference AND NOT AllowVATDifferenceOnThisTab THEN
                               ERROR(Text000,FIELDCAPTION("VAT Amount"));

                             IF PricesIncludingVAT THEN
                               "VAT Base" := "Amount Including VAT" - "VAT Amount"
                             ELSE
                               "Amount Including VAT" := "VAT Amount" + "VAT Base";

                             FormCheckVATDifference;
                             ModifyRec;
                           END;
                            }

    { 24  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the calculated VAT amount and is only used for reference when the user changes the VAT Amount manually.;
                           NOR=Angir det beregnede mva-bel›pet, og brukes bare som referanse n†r brukeren endrer mva-bel›pet manuelt.;
                           SVE=Anger det ber„knade momsbeloppet och anv„nds endast som referens n„r anv„ndaren „ndrar momsbeloppet manuellt.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Calculated VAT Amount";
                AutoFormatType=1;
                AutoFormatExpr=CurrencyCode;
                Visible=FALSE }

    { 26  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the difference between the calculated VAT amount and a VAT amount that you have entered manually.;
                           NOR=Angir forskjellen mellom det beregnede mva-bel›pet og et mva-bel›p du har angitt manuelt.;
                           SVE=Anger skillnaden mellan det ber„knade momsbeloppet och ett momsbelopp som du har angett manuellt.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="VAT Difference";
                AutoFormatType=1;
                AutoFormatExpr=CurrencyCode;
                Visible=FALSE }

    { 22  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the total of the amounts, including VAT, on all the lines on the document.;
                           NOR=Angir summen av bel›pene, inkludert mva, p† alle linjene i dokumentet.;
                           SVE=Anger summan av beloppen, inklusive moms, p† fakturaraderna i dokumentet.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Amount Including VAT";
                AutoFormatType=1;
                AutoFormatExpr=CurrencyCode;
                OnValidate=BEGIN
                             FormCheckVATDifference;
                           END;
                            }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                ApplicationArea=#RecordLinks;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                ApplicationArea=#Notes;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 can only be modified on the Invoicing tab.;NOR=%1 kan bare endres i kategorien Fakturering.;SVE=%1 kan endast „ndras p† fliken Fakturering.';
      TempVATAmountLine@1002 : TEMPORARY Record 290;
      Currency@1003 : Record 4;
      CurrencyCode@1004 : Code[10];
      AllowVATDifference@1005 : Boolean;
      AllowVATDifferenceOnThisTab@1006 : Boolean;
      PricesIncludingVAT@1007 : Boolean;
      AllowInvDisc@1008 : Boolean;
      VATBaseDiscPct@1009 : Decimal;
      VATAmountEditable@19075252 : Boolean INDATASET;
      InvoiceDiscountAmountEditable@19042140 : Boolean INDATASET;
      Text001@1010 : TextConst 'ENU=The total %1 for a document must not exceed the value %2 in the %3 field.;NOR=Totalen %1 for et dokument skal ikke overstige verdien %2 i feltet %3.;SVE=Totalen %1 f”r ett dokument f†r inte ”verskrida v„rdet %2 i f„ltet %3.';

    [External]
    PROCEDURE SetTempVATAmountLine@1(VAR NewVATAmountLine@1000 : Record 290);
    BEGIN
      TempVATAmountLine.DELETEALL;
      IF NewVATAmountLine.FIND('-') THEN
        REPEAT
          TempVATAmountLine.COPY(NewVATAmountLine);
          TempVATAmountLine.INSERT;
        UNTIL NewVATAmountLine.NEXT = 0;
    END;

    [External]
    PROCEDURE GetTempVATAmountLine@5(VAR NewVATAmountLine@1000 : Record 290);
    BEGIN
      NewVATAmountLine.DELETEALL;
      IF TempVATAmountLine.FIND('-') THEN
        REPEAT
          NewVATAmountLine.COPY(TempVATAmountLine);
          NewVATAmountLine.INSERT;
        UNTIL TempVATAmountLine.NEXT = 0;
    END;

    [External]
    PROCEDURE InitGlobals@2(NewCurrencyCode@1000 : Code[10];NewAllowVATDifference@1001 : Boolean;NewAllowVATDifferenceOnThisTab@1002 : Boolean;NewPricesIncludingVAT@1003 : Boolean;NewAllowInvDisc@1004 : Boolean;NewVATBaseDiscPct@1005 : Decimal);
    BEGIN
      CurrencyCode := NewCurrencyCode;
      AllowVATDifference := NewAllowVATDifference;
      AllowVATDifferenceOnThisTab := NewAllowVATDifferenceOnThisTab;
      PricesIncludingVAT := NewPricesIncludingVAT;
      AllowInvDisc := NewAllowInvDisc;
      VATBaseDiscPct := NewVATBaseDiscPct;
      VATAmountEditable := AllowVATDifference;
      InvoiceDiscountAmountEditable := AllowInvDisc;
      IF CurrencyCode = '' THEN
        Currency.InitRoundingPrecision
      ELSE
        Currency.GET(CurrencyCode);
    END;

    LOCAL PROCEDURE FormCheckVATDifference@4();
    VAR
      VATAmountLine2@1000 : Record 290;
      TotalVATDifference@1001 : Decimal;
    BEGIN
      CheckVATDifference(CurrencyCode,AllowVATDifference);
      VATAmountLine2 := TempVATAmountLine;
      TotalVATDifference := ABS("VAT Difference") - ABS(xRec."VAT Difference");
      IF TempVATAmountLine.FIND('-') THEN
        REPEAT
          TotalVATDifference := TotalVATDifference + ABS(TempVATAmountLine."VAT Difference");
        UNTIL TempVATAmountLine.NEXT = 0;
      TempVATAmountLine := VATAmountLine2;
      IF TotalVATDifference > Currency."Max. VAT Difference Allowed" THEN
        ERROR(
          Text001,FIELDCAPTION("VAT Difference"),
          Currency."Max. VAT Difference Allowed",Currency.FIELDCAPTION("Max. VAT Difference Allowed"));
    END;

    LOCAL PROCEDURE ModifyRec@3();
    BEGIN
      TempVATAmountLine := Rec;
      TempVATAmountLine.Modified := TRUE;
      TempVATAmountLine.MODIFY;
    END;

    BEGIN
    END.
  }
}

