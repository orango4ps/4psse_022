OBJECT Page 1818 Cash Flow Forecast Wizard
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Set Up Cash Flow Forecast;
               NOR=Konfigurer kontantstrõmprognose;
               SVE=Konfigurera kassaflîdesprognos];
    PageType=NavigatePage;
    OnInit=BEGIN
             LoadTopBanners;
           END;

    OnOpenPage=BEGIN
                 Step := Step::Start;
                 EnableControls(FALSE);
                 UpdateFrequency := UpdateFrequency::Daily;
                 TaxablePeriod := TaxablePeriod::Quarterly;
                 LiquidFundsGLAccountFilter := GetLiquidFundsGLAccountFilter;
                 CortanaInteligenceEnabled := TRUE;
               END;

    OnQueryClosePage=VAR
                       AssistedSetup@1000 : Record 1803;
                     BEGIN
                       IF CloseAction = ACTION::OK THEN
                         IF AssistedSetup.GetStatus(PAGE::"Cash Flow Forecast Wizard") = AssistedSetup.Status::"Not Completed" THEN
                           IF NOT CONFIRM(SetupNotCompletedQst,FALSE) THEN
                             ERROR('');
                     END;

    ActionList=ACTIONS
    {
      { 2       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 3       ;1   ;Action    ;
                      Name=ActionBack;
                      CaptionML=[ENU=Back;
                                 NOR=Tilbake;
                                 SVE=BakÜt];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=BackActionEnabled;
                      InFooterBar=Yes;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 NextStep(TRUE);
                               END;
                                }
      { 4       ;1   ;Action    ;
                      Name=ActionNext;
                      CaptionML=[ENU=Next;
                                 NOR=Neste;
                                 SVE=NÑsta];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=NextActionEnabled;
                      InFooterBar=Yes;
                      Image=NextRecord;
                      OnAction=BEGIN
                                 NextStep(FALSE);
                               END;
                                }
      { 5       ;1   ;Action    ;
                      Name=ActionFinish;
                      CaptionML=[ENU=Finish;
                                 NOR=Fullfõr;
                                 SVE=Slutfîr];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=FinishActionEnabled;
                      InFooterBar=Yes;
                      Image=Approve;
                      OnAction=BEGIN
                                 FinishAction;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 6   ;1   ;Group     ;
                Visible=TopBannerVisible AND NOT FinalStepVisible;
                Editable=FALSE;
                GroupType=Group }

    { 7   ;2   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=MediaResourcesStandard."Media Reference";
                Editable=FALSE;
                ShowCaption=No }

    { 8   ;1   ;Group     ;
                Visible=TopBannerVisible AND FinalStepVisible;
                Editable=FALSE;
                GroupType=Group }

    { 9   ;2   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=MediaResourcesDone."Media Reference";
                Editable=FALSE;
                ShowCaption=No }

    { 10  ;1   ;Group     ;
                Visible=FirstStepVisible;
                GroupType=Group }

    { 11  ;2   ;Group     ;
                CaptionML=[ENU=Welcome to the setup of Cash Flow Forecasting;
                           NOR=Velkommen til konfigurasjonen av kontantstrõmprognose;
                           SVE=VÑlkommen till konfigurationen av kassaflîdesprognos];
                Visible=FirstStepVisible;
                GroupType=Group }

    { 12  ;3   ;Group     ;
                Visible=FirstStepVisible;
                GroupType=Group;
                InstructionalTextML=[ENU=The Cash Flow Forecast can help you manage your cash flow. In this guide, you will specify the general ledger accounts that you want to include in the forecast, and how often you want to update the forecast. If you do not want to set this up right now, close this page.;
                                     NOR=Kontantstrõmprognosen kan hjelpe deg med Ü administrere kontantstrõmmen. I denne veiledningen skal du angi finanskontiene du vil inkludere i prognosen, og hvor ofte du vil oppdatere prognosen. Hvis du ikke vil konfigurere nÜ, lukker du denne siden.;
                                     SVE=Med hjÑlp av en kassaflîdesprognos kan du hantera ditt kassaflîde. I den hÑr guiden ska du ange vilka redovisningskonton som ska ingÜ i prognosen och hur ofta du vill uppdatera prognosen. StÑng sidan om du inte vill gîra konfigurationen nu.] }

    { 13  ;1   ;Group     ;
                Visible=CreationStepVisible;
                GroupType=Group }

    { 14  ;2   ;Group     ;
                CaptionML=[ENU=Set up accounts and forecast frequency, and turn on or off predictions;
                           NOR=Konfigurer hyppighet for konti og prognose, og aktiver eller deaktiver prognoser;
                           SVE=Konfigurera konton och prognosfrekvens, och slÜ pÜ eller stÑng av fîrutsÑgelser];
                Visible=CreationStepVisible;
                GroupType=Group }

    { 22  ;3   ;Group     ;
                GroupType=Group;
                InstructionalTextML=[ENU=Specify the accounts that you want to base the forecast on. We have suggested your cash accounts, but you can change that.;
                                     NOR=Angi kontiene du vil basere prognosen pÜ. Vi har foreslÜtt kontantkontiene dine, men du kan endre det.;
                                     SVE=Ange vilka konton som du vill basera prognosen pÜ. Vi har fîreslagit dina kassakonton, men du kan Ñndra det.] }

    { 21  ;4   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=LiquidFundsGLAccountFilter;
                OnLookup=VAR
                           GLAccount@1002 : Record 15;
                           GLAccountList@1001 : Page 18;
                           OldText@1000 : Text;
                         BEGIN
                           OldText := Text;
                           GLAccount.SETRANGE("Account Category",GLAccount."Account Category"::" ",GLAccount."Account Category"::Assets);
                           GLAccountList.SETTABLEVIEW(GLAccount);
                           GLAccountList.LOOKUPMODE(TRUE);
                           IF NOT (GLAccountList.RUNMODAL = ACTION::LookupOK) THEN
                             EXIT(FALSE);

                           Text := OldText + GLAccountList.GetSelectionFilter;
                           EXIT(TRUE);
                         END;

                ShowCaption=No }

    { 15  ;3   ;Group     ;
                GroupType=Group;
                InstructionalTextML=[ENU=How often would you like us to update your cash flow forecast?;
                                     NOR=Hvor ofte vil du at vi skal oppdatere kontantstrõmprognosen din?;
                                     SVE=Hur ofta vill du uppdatera kassaflîdesprognosen?] }

    { 16  ;4   ;Field     ;
                OptionCaptionML=[ENU=Never,Daily,Weekly;
                                 NOR=Aldri,Daglig,Ukentlig;
                                 SVE=Aldrig,Dagligen,Veckovis];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=UpdateFrequency;
                ShowCaption=No }

    { 36  ;2   ;Group     ;
                CaptionML=[ENU=Cortana Intelligence;
                           NOR=Cortana Intelligence;
                           SVE=Cortana Intelligence];
                GroupType=Group }

    { 32  ;3   ;Group     ;
                Name=CortanaIntelligenceForecastsDescription;
                CaptionML=[ENU=Cortana Intelligence forecasts can help you anticipate and proactively address upcoming cash flow needs. If you are using Dynamics 365 Business Central, Azure Machine Learning credentials are set automatically.;
                           NOR=Cortana Intelligence-prognoser kan bidra til Ü forutse og proaktivt lõse kommende kontantstrõmbehov. Hvis du bruker Dynamics 365 Business Central, angis legitimasjon for Azure Machine Learning automatisk.;
                           SVE=Cortana Intelligence-prognoser kan hjÑlpa dig att fîrutse och proaktivt hantera kommande kassaflîdesbrister. Om du anvÑnder Dynamics 365 Business Central stÑlls autentiseringsuppgifter fîr Azure Machine Learning in automatiskt.];
                GroupType=Group }

    { 33  ;4   ;Field     ;
                Name=<CortanaInteligenceEnabled>;
                CaptionML=[ENU=Enable Cortana Intelligence;
                           NOR=Aktiver Cortana Intelligence;
                           SVE=Aktivera Cortana Intelligence];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=CortanaInteligenceEnabled;
                ShowCaption=Yes }

    { 38  ;1   ;Group     ;
                Visible=CortanaStepVisible;
                GroupType=Group }

    { 37  ;2   ;Group     ;
                CaptionML=[ENU=If you have your own Machine Learning service, enter the API URL and API key for the service and use that instead.;
                           NOR=Hvis du har din egen Machine Learning-tjeneste, angir du URL-adressen for API URL og API-nõkkelen for tjenesten og bruker dette i stedet.;
                           SVE=Om du har en egen Machine Learning-tjÑnst, ange dÜ API-URL och API-nyckel fîr tjÑnsten och anvÑnd den istÑllet.];
                GroupType=Group }

    { 34  ;3   ;Field     ;
                Name=APIURL;
                CaptionML=[ENU=API URL;
                           NOR=URL-adresse for API;
                           SVE=API-URL];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=APIURL;
                Visible=CortanaInteligenceEnabled;
                Editable=CortanaInteligenceEnabled }

    { 35  ;3   ;Field     ;
                Name=APIKEY;
                CaptionML=[ENU=API Key;
                           NOR=API-nõkkel;
                           SVE=API-nyckel];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=APIKEY;
                Visible=CortanaInteligenceEnabled;
                Editable=CortanaInteligenceEnabled }

    { 26  ;1   ;Group     ;
                Visible=TaxStepVisible;
                GroupType=Group }

    { 27  ;2   ;Group     ;
                CaptionML=[ENU=Welcome to the setup of Cash Flow Forecasting;
                           NOR=Velkommen til konfigurasjonen av kontantstrõmprognose;
                           SVE=VÑlkommen till konfigurationen av kassaflîdesprognos];
                Visible=TaxStepVisible;
                GroupType=Group }

    { 31  ;3   ;Group     ;
                GroupType=Group;
                InstructionalTextML=[ENU=How often must your company pay taxes?;
                                     NOR=Hvor ofte mÜ firmaet betale avgifter?;
                                     SVE=Hur ofta mÜste fîretaget betala skatter?] }

    { 30  ;4   ;Field     ;
                OptionCaptionML=[ENU=Monthly,Quarterly,Accounting Period,Yearly;
                                 NOR=MÜnedlig,Kvartalsmessig,Regnskapsperiode,èrlig;
                                 SVE=MÜnadsvis,Kvartalsvis,Bokfîringsperiod,èrsvis];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=TaxablePeriod;
                ShowCaption=No }

    { 29  ;3   ;Group     ;
                GroupType=Group;
                InstructionalTextML=[ENU=What is the payment window for paying your taxes? For example, if your payment window is 20 days, specify 20D.;
                                     NOR=Hva er betalingsvinduet for Ü betale avgiftene? Hvis betalingsvinduet for eksempel er 20 dager, angir du 20D.;
                                     SVE=Vilket betalningsfînster gÑller fîr betalning av dina skatter? Om betalningsfînstret Ñr 20 dagar anger du 20D.] }

    { 28  ;4   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=TaxPaymentWindow;
                ShowCaption=No }

    { 25  ;3   ;Group     ;
                GroupType=Group;
                InstructionalTextML=[ENU=Which type of account do you pay your taxes to?;
                                     NOR=Hvilken type konto betaler du avgiftene til?;
                                     SVE=Vilken kontotyp betalas skatterna till?] }

    { 24  ;4   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=DummyCashFlowSetup."Tax Bal. Account Type";
                OnValidate=BEGIN
                             DummyCashFlowSetup.EmptyTaxBalAccountIfTypeChanged(CurrentTaxBalAccountType);
                             CurrentTaxBalAccountType := DummyCashFlowSetup."Tax Bal. Account Type";
                             TaxAccountValidType := DummyCashFlowSetup.HasValidTaxAccountInfo;
                             CurrPage.UPDATE;
                           END;

                ShowCaption=No }

    { 23  ;3   ;Group     ;
                Visible=TaxAccountValidType;
                GroupType=Group;
                InstructionalTextML=[ENU=What is the number of the account that you pay your taxes to?;
                                     NOR=Hva er nummeret pÜ kontoen du betaler avgiftene til?;
                                     SVE=Vilket Ñr kontonumret dÑr du betalar skatterna?] }

    { 20  ;4   ;Field     ;
                ApplicationArea=#Basic,#Suite;
                SourceExpr=DummyCashFlowSetup."Tax Bal. Account No.";
                OnLookup=VAR
                           Vendor@1000 : Record 23;
                           GLAccount@1003 : Record 15;
                           Result@1002 : Boolean;
                         BEGIN
                           CASE DummyCashFlowSetup."Tax Bal. Account Type" OF
                             DummyCashFlowSetup."Tax Bal. Account Type"::Vendor:
                               BEGIN
                                 IF Vendor.GET(Text) THEN;
                                 Result := PAGE.RUNMODAL(PAGE::"Vendor List",Vendor) = ACTION::LookupOK;
                                 Text := Vendor."No.";
                               END;
                             DummyCashFlowSetup."Tax Bal. Account Type"::"G/L Account":
                               BEGIN
                                 IF GLAccount.GET(Text) THEN;
                                 Result := PAGE.RUNMODAL(PAGE::"G/L Account List",GLAccount) = ACTION::LookupOK;
                                 Text := GLAccount."No.";
                               END;
                           END;

                           EXIT(Result);
                         END;

                ShowCaption=No }

    { 17  ;1   ;Group     ;
                Visible=FinalStepVisible;
                GroupType=Group }

    { 18  ;2   ;Group     ;
                CaptionML=[ENU=That's it!;
                           NOR=Det var det!;
                           SVE=Klart!];
                GroupType=Group }

    { 19  ;3   ;Group     ;
                GroupType=Group;
                InstructionalTextML=[ENU=When you choose Finish, cash flow forecasting is set up for you. The forecast is shown in a chart on your Home page. If it does not show a forecast yet, you might have to refresh the page.;
                                     NOR=NÜr du velger Fullfõr, blir kontantstrõmprognosen konfigurert. Prognosen vises i et diagram pÜ startsiden. Hvis det ikke vises en prognose ennÜ, mÜ du kanskje oppdatere siden.;
                                     SVE=NÑr du vÑljer Slutfîr konfigureras kassaflîdesprognosen. Prognosen visas i ett diagram pÜ din startsida. Om prognosen inte visas Ñnnu, kanske du mÜste uppdatera sidan.] }

  }
  CODE
  {
    VAR
      MediaRepositoryStandard@1000 : Record 9400;
      MediaRepositoryDone@1001 : Record 9400;
      MediaResourcesStandard@1025 : Record 2000000182;
      MediaResourcesDone@1024 : Record 2000000182;
      DummyCashFlowSetup@1019 : Record 843;
      ClientTypeManagement@1026 : Codeunit 4030;
      Step@1002 : 'Start,Creation,Cortana,Tax,Finish';
      TopBannerVisible@1003 : Boolean;
      FirstStepVisible@1004 : Boolean;
      CreationStepVisible@1005 : Boolean;
      TaxStepVisible@1016 : Boolean;
      FinalStepVisible@1006 : Boolean;
      FinishActionEnabled@1007 : Boolean;
      BackActionEnabled@1008 : Boolean;
      NextActionEnabled@1009 : Boolean;
      UpdateFrequency@1010 : 'Never,Daily,Weekly';
      ExistingSetupWillBeDeletedQst@1011 : TextConst 'ENU=The existing cash flow forecast setup will be deleted. Are you sure you want to continue?;NOR=Den eksisterende konfigurasjonen av kontantstrõmprognose blir slettet. Er du sikker pÜ at du vil du fortsette?;SVE=Den befintliga konfigurationen av kassaflîdesprognosen tas bort. Vill du fortsÑtta?';
      LiquidFundsGLAccountFilter@1012 : Code[250];
      SetupNotCompletedQst@1013 : TextConst 'ENU=Setup of cash flow forecast has not been completed.\\Are you sure that you want to exit?;NOR=Konfigurasjonen av kontantstrõmprognose er ikke fullfõrt.\\Er du sikker pÜ at du vil avslutte?;SVE=Konfigurationen av kassaflîdesprognosen har inte slutfîrts.\\Vill du avsluta?';
      TaxPaymentWindow@1014 : DateFormula;
      TaxablePeriod@1015 : 'Monthly,Quarterly,Accounting Period,Yearly';
      TaxAccountValidType@1018 : Boolean;
      CurrentTaxBalAccountType@1017 : Option;
      CortanaInteligenceEnabled@1020 : Boolean;
      APIURL@1021 : Text[250];
      APIKEY@1022 : Text[250];
      CortanaStepVisible@1023 : Boolean;

    LOCAL PROCEDURE EnableControls@1(Backwards@1001 : Boolean);
    VAR
      CashFlowForecast@1000 : Record 840;
    BEGIN
      IF (Step = Step::Creation) AND NOT Backwards THEN
        IF NOT CashFlowForecast.ISEMPTY THEN
          IF NOT CONFIRM(ExistingSetupWillBeDeletedQst) THEN
            CurrPage.CLOSE;

      ResetControls;

      CASE Step OF
        Step::Start:
          ShowStartStep;
        Step::Creation:
          ShowCreationStep;
        Step::Cortana:
          ShowCortanaStep;
        Step::Tax:
          ShowTaxStep;
        Step::Finish:
          ShowFinalStep;
      END;
    END;

    LOCAL PROCEDURE FinishAction@2();
    VAR
      AssistedSetup@1001 : Record 1803;
      CashFlowSetup@1002 : Record 843;
      CashFlowManagement@1000 : Codeunit 841;
    BEGIN
      CashFlowManagement.SetupCashFlow(LiquidFundsGLAccountFilter);

      CashFlowSetup.UpdateTaxPaymentInfo(TaxablePeriod,TaxPaymentWindow,
        DummyCashFlowSetup."Tax Bal. Account Type",DummyCashFlowSetup."Tax Bal. Account No.");

      CashFlowSetup.GET;
      CashFlowSetup.VALIDATE("Cortana Intelligence Enabled",CortanaInteligenceEnabled);
      CashFlowSetup.VALIDATE("API URL",APIURL);
      CashFlowSetup.SaveUserDefinedAPIKey(APIKEY);
      CashFlowSetup.VALIDATE("Automatic Update Frequency",UpdateFrequency);
      CashFlowSetup.MODIFY;

      CashFlowManagement.UpdateCashFlowForecast(CortanaInteligenceEnabled);
      AssistedSetup.SetStatus(PAGE::"Cash Flow Forecast Wizard",AssistedSetup.Status::Completed);
      CurrPage.CLOSE;
    END;

    LOCAL PROCEDURE NextStep@3(Backwards@1000 : Boolean);
    BEGIN
      IF Backwards THEN
        // Skip Cortana setup page if it is SaaS or Cortana is disabled
        IF (Step = Step::Tax) AND (NOT CortanaInteligenceEnabled OR OnSaaS) THEN
          Step := Step - 2
        ELSE
          Step := Step - 1
      ELSE
        // Skip Cortana setup page if it is SaaS or Cortana is disabled
        IF (Step = Step::Creation) AND (NOT CortanaInteligenceEnabled OR OnSaaS) THEN
          Step := Step + 2
        ELSE
          Step := Step + 1;

      EnableControls(Backwards);
    END;

    LOCAL PROCEDURE ShowStartStep@4();
    BEGIN
      FirstStepVisible := TRUE;

      FinishActionEnabled := FALSE;
      BackActionEnabled := FALSE;
    END;

    LOCAL PROCEDURE ShowCreationStep@5();
    BEGIN
      CreationStepVisible := TRUE;
      CortanaStepVisible := FALSE;
      FinishActionEnabled := FALSE;
    END;

    LOCAL PROCEDURE ShowTaxStep@13();
    BEGIN
      TaxStepVisible := TRUE;

      FinishActionEnabled := FALSE;
    END;

    LOCAL PROCEDURE ShowFinalStep@6();
    BEGIN
      FinalStepVisible := TRUE;
      NextActionEnabled := FALSE;
    END;

    LOCAL PROCEDURE ShowCortanaStep@17();
    BEGIN
      CortanaStepVisible := TRUE;

      FinishActionEnabled := FALSE;
    END;

    LOCAL PROCEDURE ResetControls@7();
    BEGIN
      FinishActionEnabled := TRUE;
      BackActionEnabled := TRUE;
      NextActionEnabled := TRUE;

      TaxStepVisible := FALSE;
      FirstStepVisible := FALSE;
      CreationStepVisible := FALSE;
      FinalStepVisible := FALSE;
      CortanaStepVisible := FALSE;
    END;

    LOCAL PROCEDURE LoadTopBanners@8();
    BEGIN
      IF MediaRepositoryStandard.GET('AssistedSetup-NoText-400px.png',FORMAT(ClientTypeManagement.GetCurrentClientType)) AND
         MediaRepositoryDone.GET('AssistedSetupDone-NoText-400px.png',FORMAT(ClientTypeManagement.GetCurrentClientType))
      THEN
        IF MediaResourcesStandard.GET(MediaRepositoryStandard."Media Resources Ref") AND
           MediaResourcesDone.GET(MediaRepositoryDone."Media Resources Ref")
        THEN
          TopBannerVisible := MediaResourcesDone."Media Reference".HASVALUE;
    END;

    LOCAL PROCEDURE GetLiquidFundsGLAccountFilter@9() : Code[250];
    VAR
      CashFlowAccount@1000 : Record 841;
      CashFlowManagement@1001 : Codeunit 841;
    BEGIN
      IF CashFlowAccount.GET(FORMAT(CashFlowAccount."Source Type"::"Liquid Funds",20)) THEN
        IF CashFlowAccount."G/L Account Filter" <> '' THEN
          EXIT(CashFlowAccount."G/L Account Filter");

      EXIT(COPYSTR(CashFlowManagement.GetCashAccountFilter,1,250));
    END;

    LOCAL PROCEDURE OnSaaS@12() : Boolean;
    VAR
      PermissionManager@1000 : Codeunit 9002;
    BEGIN
      EXIT(PermissionManager.SoftwareAsAService)
    END;

    BEGIN
    END.
  }
}

