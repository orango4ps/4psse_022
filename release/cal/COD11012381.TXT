OBJECT Codeunit 11012381 Project Forecast Automation
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=VAR
            JobsSetup@1100528802 : Record 315;
            NoOfDaysAhead@1100528803 : Integer;
            AutoCreateProjectForecast@1100528800 : Codeunit 11012382;
            LogMessage@1100528801 : Text[250];
          BEGIN
            JobsSetup.GET();
            IF JobsSetup."Forecast Auto. No. Days Ahead" = 0 THEN
              NoOfDaysAhead := 1
            ELSE
              NoOfDaysAhead := JobsSetup."Forecast Auto. No. Days Ahead";

            ProjectForecastAutoMgt.GetProjectForecastAutomationList(TODAY+NoOfDaysAhead,TODAY,ProjectForecastListDS);

            ProjForecastAutomationLog.DELETEALL;
            COMMIT;

            IF ProjectForecastListDS.FINDSET THEN
              REPEAT
                IF AutoCreateProjectForecast.RUN(ProjectForecastListDS) THEN
                  COMMIT
                ELSE BEGIN
                  LogMessage := COPYSTR(GETLASTERRORTEXT, 1, MAXSTRLEN(LogMessage));
                  LogErrorMessage(LogMessage, ProjectForecastListDS);
                  CLEARLASTERROR;
                END;
              UNTIL ProjectForecastListDS.NEXT = 0;
          END;

  }
  CODE
  {
    VAR
      ProjectForecastAutoMgt@1100528800 : Codeunit 11130009;
      ProjectForecastListDS@1100528801 : TEMPORARY Record 11020579;
      ProjForecastAutomationLog@1100528804 : Record 11229897;

    PROCEDURE LogErrorMessage@1100528800(ILogMessage@1100528800 : Text[250];ProjectForecast@1100528801 : Record 11020579);
    BEGIN
      ProjForecastAutomationLog.INIT;
      ProjForecastAutomationLog."Project No." := ProjectForecast."Key Code1";
      ProjForecastAutomationLog."Forecast Date" := ProjectForecast.Date1;
      ProjForecastAutomationLog.LogText := ILogMessage;
      ProjForecastAutomationLog."Logged at" := CURRENTDATETIME;
      ProjForecastAutomationLog.INSERT;
      COMMIT;
    END;

    BEGIN
    END.
  }
}

