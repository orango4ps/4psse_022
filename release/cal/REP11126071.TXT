OBJECT Report 11126071 FSW Create Srv. Ord. Cost Plus
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Serviceauftrag RegieeintrÑge erstellen;
               ENU=Create Service Order Cost Plus Entries;
               NLD=Serviceorder regiemutaties aanmaken;
               NOR=Opprett selvkosttransaksjoner for serviceordre;
               SVE=Skapa redovisade sjÑlvkostnader fîr serviceorder];
    ProcessingOnly=Yes;
    OnPostReport=BEGIN
                   IF TotalNumberOfCreatedCPE = 0 THEN
                     MESSAGE(Text007)
                   ELSE
                     MESSAGE(Text008,TotalNumberOfCreatedCPE, CounterOK);
                   CheckCompressLines;
                 END;

  }
  DATASET
  {
    { 1100528000;;DataItem;                  ;
               DataItemTable=Table11125878;
               OnAfterGetRecord=VAR
                                  ServiceOrder@1100528000 : Record 11012823;
                                BEGIN
                                  IF "Service Order No." = '' THEN
                                    CurrReport.SKIP;

                                  ServiceOrder.GET("Service Order No.");
                                  ServiceOrder.CheckStatus(1);
                                  IF ServiceOrder.CheckBlockedByFSA() THEN
                                    ERROR(Text009, "Service Order No.");
                                END;

               OnPostDataItem=VAR
                                FSWorksheetLine@1100525000 : Record 11125879;
                              BEGIN
                                FSWorksheetLine.SETRANGE("Worksheet No.","Field Service Worksheet"."No.");
                                FSWorksheetLine.SETRANGE(Chargeable, TRUE);
                                FSWorksheetLine.SETRANGE("Cost Plus Entry Created", FALSE);

                                IF NOT FSWorksheetLine.FINDFIRST THEN BEGIN
                                  IF "Field Service Worksheet".Status = "Field Service Worksheet".Status::Open THEN
                                    "Field Service Worksheet".Status := "Field Service Worksheet".Status::"Revenue Processed"
                                  ELSE IF "Field Service Worksheet".Status = "Field Service Worksheet".Status::"Costs Processed" THEN
                                    "Field Service Worksheet".Status := "Field Service Worksheet".Status::Completed;

                                   "Field Service Worksheet".MODIFY;
                                END;
                              END;
                               }

    { 1100528001;1;DataItem;                 ;
               DataItemTable=Table11125879;
               DataItemTableView=WHERE(Cost Plus Entry Created=CONST(No),
                                       Chargeable=CONST(Yes));
               OnPreDataItem=BEGIN
                               TempFSWorksheetLine.DELETEALL;
                               CounterTotal := COUNT;

                               Window.OPEN(Text000 +
                                           Text001);
                             END;

               OnAfterGetRecord=VAR
                                  CreateCostPlusEntries@1100528000 : Codeunit 11126072;
                                  NumberOfCreatedCPE@1100528001 : Integer;
                                BEGIN
                                  Counter := Counter + 1;
                                  Window.UPDATE(1,ROUND(Counter / CounterTotal * 10000,1));


                                  CreateCostPlusEntries.CreateServOrderCPE("FS Worksheet Line", NumberOfCreatedCPE);
                                  TotalNumberOfCreatedCPE += NumberOfCreatedCPE;

                                  TempFSWorksheetLine := "FS Worksheet Line";
                                  TempFSWorksheetLine.INSERT;

                                  CounterOK := CounterOK + 1;
                                END;

               OnPostDataItem=VAR
                                FSWorksheetLine@1100528000 : Record 11125879;
                              BEGIN
                                IF TempFSWorksheetLine.FINDSET THEN
                                  REPEAT
                                    FSWorksheetLine := TempFSWorksheetLine;
                                    IF FSWorksheetLine.FIND('=') THEN BEGIN
                                      FSWorksheetLine."Cost Plus Entry Created" := TRUE;
                                     FSWorksheetLine.MODIFY;
                                    END;
                                  UNTIL TempFSWorksheetLine.NEXT = 0;

                                COMMIT;
                                Window.CLOSE;
                              END;

               DataItemLink=Worksheet No.=FIELD(No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100528001;0;Container;
                  ContainerType=ContentArea }

      { 1100528000;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100528002;2;Field  ;
                  CaptionML=[DEU=RegieeintrÑge komprimieren;
                             ENU=Compress Cost Plus Entries;
                             NLD=Comprimeren regiemutaties;
                             NOR=Komprimer rapportert selvkost;
                             SVE=Komprimera redovisade sjÑlvkostnader];
                  SourceExpr=CompressCPE }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TempFSWorksheetLine@1100528012 : TEMPORARY Record 11125879;
      ServiceOrderCostPlusEntry@1100528001 : Record 11012825;
      CompressCPE@1100528000 : Boolean;
      Window@1100528002 : Dialog;
      CounterTotal@1100528006 : Integer;
      Counter@1100528005 : Integer;
      CounterOK@1100528004 : Integer;
      Text000@1100528011 : TextConst 'DEU=RegieeintrÑge werden erstellt...\\;ENU=Creating Cost Plus Entries....\\;NLD=Regiemutaties worden aangemaakt...\\;NOR=Oppretter rapporterte selvkosterˇ....\\;SVE=Skapar redovisade sjÑlvkostnaderˇ....\\';
      Text001@1100528010 : TextConst 'DEU=Prozentsatz fertig     @1@@@@@@@@@@@@@\;ENU=Percentage Finished     @1@@@@@@@@@@@@@\;NLD=Percentage gereed      @1@@@@@@@@@@@@@\;NOR=Prosent ferdig @1@@@@@@@@@@@@@\;SVE=Procent fÑrdigt  @1@@@@@@@@@@@@@\';
      Text007@1100528009 : TextConst 'DEU=Es sind keine RegieeintrÑge erstellt worden.;ENU=There are no Cost Plus Entries created.;NLD=Er zijn geen regiemutaties aangemaakt.;NOR=Ingen rapporterte selvkoster er opprettet.;SVE=Inga redovisade sjÑlvkostnader har skapats.';
      Text008@1100528008 : TextConst 'DEU=%1 Regieeintrag-Posten wurden fÅr %2 Arbeitsblattzeilen erstellt.;ENU=%1 Cost Plus Entries have been created for %2 Worksheet Lines.;NLD=%1 regiemutaties zijn aangemaakt voor %2 Werkstaatregels.';
      Text009@1100528007 : TextConst 'DEU=Serviceauftrag %1 wurde vom Log-Moment gesperrt.;ENU=Service Order %1 is blocked by its reference point.;NLD=Serviceorder %1 is geblokkeerd door het logmoment.;NOR=Serviceordre %1 er blokkert av sitt referansepunkt.;SVE=Serviceorder %1 har spÑrrats av referenspunkten.';
      TotalNumberOfCreatedCPE@1100528013 : Integer;

    PROCEDURE CheckCompressLines@1100525002();
    VAR
      ServiceOrderCostPlusEntry@1100530000 : Record 11012825;
      FieldServiceWorksheet@1100528000 : Record 11125878;
    BEGIN
      IF CompressCPE THEN BEGIN
        COMMIT;
        IF TempFSWorksheetLine.FINDFIRST THEN BEGIN
          REPEAT
           FieldServiceWorksheet.GET(TempFSWorksheetLine."Worksheet No.");
           ServiceOrderCostPlusEntry.SETRANGE("Service Order No.", FieldServiceWorksheet."Service Order No.");
           ServiceOrderCostPlusEntry.SETFILTER("Compression Id", '<>%1', '');
           IF ServiceOrderCostPlusEntry.FINDFIRST THEN BEGIN
             REPEAT
               ServiceOrderCostPlusEntry.MARK(TRUE);
             UNTIL ServiceOrderCostPlusEntry.NEXT = 0;
           END;
          UNTIL TempFSWorksheetLine.NEXT = 0;
        END;
        ServiceOrderCostPlusEntry.MARKEDONLY(TRUE);
        IF ServiceOrderCostPlusEntry.FINDFIRST THEN
          CompressServiceCostPlus(ServiceOrderCostPlusEntry);
      END;
    END;

    LOCAL PROCEDURE CompressServiceCostPlus@1210190001(VAR MainServiceOrderCostPlusEntry@1100528001 : Record 11012825);
    VAR
      ServiceOrderCostPlusEntry@1100528000 : Record 11012825;
    BEGIN
      //MainServiceOrderCostPlusEntry.SETCURRENTKEY("Service Order No.", "Line No.");
      MainServiceOrderCostPlusEntry.SETRANGE(Invoiced, FALSE);
      IF MainServiceOrderCostPlusEntry.FINDSET(TRUE, FALSE) THEN
        REPEAT
          ServiceOrderCostPlusEntry.SETRANGE("Service Order No.", MainServiceOrderCostPlusEntry."Service Order No.");
          ServiceOrderCostPlusEntry.SETFILTER("Line No.", '<>%1', MainServiceOrderCostPlusEntry."Line No.");

          ServiceOrderCostPlusEntry.SETRANGE("Compression Id", MainServiceOrderCostPlusEntry."Compression Id");
          ServiceOrderCostPlusEntry.SETRANGE("VAT Prod. Posting Group", MainServiceOrderCostPlusEntry."VAT Prod. Posting Group");

          IF ServiceOrderCostPlusEntry.FINDSET(TRUE, FALSE) THEN BEGIN
            REPEAT
              MainServiceOrderCostPlusEntry."Sales Price (LCY)" := MainServiceOrderCostPlusEntry."Sales Price (LCY)" + ServiceOrderCostPlusEntry."Sales Price (LCY)";
              MainServiceOrderCostPlusEntry."Invoice Price (LCY)" := MainServiceOrderCostPlusEntry."Invoice Price (LCY)" + ServiceOrderCostPlusEntry."Invoice Price (LCY)";
              ServiceOrderCostPlusEntry.DELETE(TRUE);
            UNTIL ServiceOrderCostPlusEntry.NEXT = 0;

            MainServiceOrderCostPlusEntry."Unit Price Code" := '';
            MainServiceOrderCostPlusEntry.VALIDATE("Sales Price (LCY)");
            MainServiceOrderCostPlusEntry.MODIFY(TRUE);
          END;
        UNTIL MainServiceOrderCostPlusEntry.NEXT = 0;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

