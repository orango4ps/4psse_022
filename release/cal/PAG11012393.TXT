OBJECT Page 11012393 Export CUF-XML
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Exportieren CUF-XML;
               ENU=Export CUF-XML;
               NLD=Exporteren CUF-XML;
               NOR=Eksporter CUF-XML;
               SVE=Exportera CUF-XML];
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table11012151;
    SourceTableView=WHERE(RAW Estimate=CONST(No));
    PageType=NavigatePage;
    OnOpenPage=BEGIN
                 //**4PS.sn
                 DPA_IsHardFilter := DPA_SetFilters(CurrPage.OBJECTID(FALSE));
                 //**4PS.en
                 UpdateVisible;
               END;

    OnNewRecord=BEGIN
                  //**4PS.sn
                  DPA_SetDefaultDepartment;
                  //**4PS.en
                END;

    OnInsertRecord=VAR
                     xRecRef@1100525000 : RecordRef;
                   BEGIN
                     //**4PS.sn
                     xRecRef.GETTABLE(xRec);
                     DPA_SetDefaultDpmtFromNoSeries(xRecRef);
                     //**4PS.en
                   END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1210190000;1 ;Action    ;
                      CaptionML=[DEU=E&xportieren;
                                 ENU=E&xport;
                                 NLD=E&xporteren;
                                 NOR=E&ksporter;
                                 SVE=&Exportera];
                      Promoted=No;
                      InFooterBar=Yes;
                      Image=Export;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 TempBlob@1210190003 : Record 99008535;
                                 ClientTypeManagement@1100528501 : Codeunit 4;
                                 InStream@1100528500 : InStream;
                                 ExportStream@1210190004 : OutStream;
                                 ExportCUFXML@1100528900 : XMLport 11012161;
                               BEGIN
                                 IF NOT ClientTypeManagement.IsCommonWebClientType THEN BEGIN
                                   IF ExportFileName = '' THEN
                                     ERROR(Text002);
                                   IF FileMgt.ClientFileExists(ExportFileName) THEN
                                     IF CONFIRM(Text004, TRUE, ExportFileName) THEN
                                       FileMgt.DeleteClientFile4PS(ExportFileName, TRUE)
                                     ELSE
                                       ERROR('');
                                 END;

                                 TempBlob.Blob.CREATEOUTSTREAM(ExportStream);
                                 Rec.SETRECFILTER;

                                 ExportCUFXML.SETTABLEVIEW(Rec);
                                 ExportCUFXML.SetSummarySheetNo(SummarySheetNo);
                                 ExportCUFXML.SETDESTINATION(ExportStream);  //db, 15-05-17: C037617
                                 ExportCUFXML.EXPORT;

                                 IF NOT ClientTypeManagement.IsCommonWebClientType THEN BEGIN
                                   FileMgt.BLOBExport4PS(TempBlob, ExportFileName, FALSE, TRUE);

                                   IF FileMgt.ClientFileExists(ExportFileName) THEN
                                     MESSAGE(Text000, ExportFileName)
                                   ELSE
                                     ERROR(Text001);
                                 END ELSE BEGIN
                                   TempBlob.Blob.CREATEINSTREAM(InStream);
                                   ExportFileName := STRSUBSTNO('%1_%2_CUF.xml', "No.", TODAY);
                                   DOWNLOADFROMSTREAM(InStream, '', '', '', ExportFileName);
                                 END;
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Field     ;
                CaptionML=[DEU=Exportieren CUF-XML;
                           ENU=Export CUF-XML;
                           NLD=Exporteren CUF-XML;
                           NOR=Eksporter CUF-XML;
                           SVE=Exportera CUF-XML];
                Style=Strong;
                StyleExpr=TRUE }

    { 1210190001;1;Field  ;
                CaptionML=[DEU=Kalkulationsnr.;
                           ENU=Estimate No.;
                           NLD=Calculatienr.;
                           NOR=Kalkylenr;
                           SVE=Kalkylnr];
                SourceExpr="No.";
                Editable=FALSE }

    { 1210190005;1;Field  ;
                CaptionML=[DEU=Endblatt;
                           ENU=Summary Sheet;
                           NLD=Staartblad;
                           NOR=Sammendragsark;
                           SVE=Summeringsark];
                SourceExpr=SummarySheetNo;
                OnValidate=VAR
                             SummarySheet@1100525000 : Record 11012172;
                           BEGIN
                             SummarySheet.GET("No.", SummarySheetNo);
                           END;

                OnLookup=VAR
                           SummarySheet@1210190000 : Record 11012172;
                         BEGIN
                           SummarySheet.SETRANGE("Estimate No.", "No.");
                           IF PAGE.RUNMODAL(0, SummarySheet) = ACTION::LookupOK THEN
                             SummarySheetNo := SummarySheet.Summary;
                         END;
                          }

    { 1210190003;1;Field  ;
                CaptionML=[DEU=Dateiname;
                           ENU=File Name;
                           NLD=Bestandsnaam;
                           NOR=Filnavn;
                           SVE=Filnamn];
                SourceExpr=ExportFileName;
                Visible=ExportFileNameVisible;
                OnValidate=BEGIN
                             ExportFileNameOnAfterValidate;
                           END;

                OnAssistEdit=VAR
                               FileManagement@1210190002 : Codeunit 419;
                             BEGIN
                               ExportFileName := FileManagement.SaveFileDialog(Text003, ExportFileName, '');
                             END;
                              }

  }
  CODE
  {
    VAR
      Text000@1210190001 : TextConst 'DEU=CUF-XML-Datei %1 ist erstellt worden;ENU=CUF-XML file %1 created;NLD=CUF-XML bestand %1 aangemaakt;NOR=CUF-XML-fil %1 er opprettet;SVE=CUF-XML-fil %1 har skapats';
      Text001@1210190002 : TextConst 'DEU=Datei ist nicht erstellt worden;ENU=File not created;NLD=Bestand niet aangemaakt;NOR=Fil ikke opprettet;SVE=Filen har inte skapats';
      FileMgt@1210190007 : Codeunit 419;
      ExportFileName@1210190003 : Text[250];
      SummarySheetNo@1210190000 : Code[10];
      Text002@1100528600 : TextConst 'DEU=Keinen Dateinamen angegeben;ENU=No File Name given;NLD=Geen bestandsnaam opgegeven;NOR=Inget filnavn er angitt;SVE=Inget filnamn har angetts';
      Text003@1210190005 : TextConst 'DEU=AuswÑhlen Datei...;ENU=Select file...;NLD=Selecteer bestand...;NOR=Velg fil...;SVE=VÑlj filˇ...';
      Text004@1210190006 : TextConst 'DEU=Die Datei %1 ist bereits vorhanden. Mîchten Sie diese Åberschreiben?;ENU=File %1 already exists, overwrite?;NLD=Bestand %1 bestaat reeds, overschrijven?;NOR=Filen %1 finnes allerede, vil du skrive over?;SVE=Filen %1 finns redan. Vill du skriva îver den?';
      DPA_IsHardFilter@1100525000 : Boolean;
      ExportFileNameVisible@1100528500 : Boolean INDATASET;

    LOCAL PROCEDURE ExportFileNameOnAfterValidate@19078403();
    VAR
      Position@1210190000 : Integer;
      FileExtension@1210190001 : Text[30];
    BEGIN
      FileExtension := '.XML';
      Position := STRPOS(UPPERCASE(ExportFileName), FileExtension);
      IF (Position = 0) OR (Position - 1 <> STRLEN(ExportFileName) - STRLEN(FileExtension)) THEN
        ExportFileName := ExportFileName + '.xml';
    END;

    LOCAL PROCEDURE UpdateVisible@1100528505();
    VAR
      ClientTypeManagement@1100528500 : Codeunit 4;
    BEGIN
      ExportFileNameVisible := NOT ClientTypeManagement.IsCommonWebClientType;
    END;

    BEGIN
    END.
  }
}

