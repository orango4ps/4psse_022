OBJECT Report 292 Copy Sales Document
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03,4PS14.00,4PSSE;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Copy Sales Document;
               NOR=Kopier salgsdokument;
               SVE=Kopiera fîrsÑljningsdokument];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  SalesSetup.GET;
                  CopyDocMgt.SetProperties(
                    //IncludeHeader,RecalculateLines,FALSE,FALSE,FALSE,SalesSetup."Exact Cost Reversing Mandatory",FALSE); //**4PS.o
                    IncludeHeader,RecalculateLines,FALSE,FALSE,FALSE,SalesSetup."Exact Cost Reversing Mandatory",FALSE,FALSE,IncludeOrgInvInfo); //**4PS.n
                  CopyDocMgt.SetArchDocVal(DocNoOccurrence,DocVersionNo);

                  OnPreReportOnBeforeCopySalesDoc(CopyDocMgt);
                  CopyDocMgt.SetIncludeOrgInvInfo(IncludeOrgInvInfo);

                  CopyDocMgt.CopySalesDoc(DocType,DocNo,SalesHeader);
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF DocNo <> '' THEN BEGIN
                     CASE DocType OF
                       DocType::Quote:
                         IF FromSalesHeader.GET(FromSalesHeader."Document Type"::Quote,DocNo) THEN;
                       DocType::"Blanket Order":
                         IF FromSalesHeader.GET(FromSalesHeader."Document Type"::"Blanket Order",DocNo) THEN;
                       DocType::Order:
                         IF FromSalesHeader.GET(FromSalesHeader."Document Type"::Order,DocNo) THEN;
                       DocType::Invoice:
                         IF FromSalesHeader.GET(FromSalesHeader."Document Type"::Invoice,DocNo) THEN;
                       DocType::"Return Order":
                         IF FromSalesHeader.GET(FromSalesHeader."Document Type"::"Return Order",DocNo) THEN;
                       DocType::"Credit Memo":
                         IF FromSalesHeader.GET(FromSalesHeader."Document Type"::"Credit Memo",DocNo) THEN;
                       DocType::"Posted Shipment":
                         IF FromSalesShptHeader.GET(DocNo) THEN
                           FromSalesHeader.TRANSFERFIELDS(FromSalesShptHeader);
                       DocType::"Posted Invoice":
                         IF FromSalesInvHeader.GET(DocNo) THEN
                           FromSalesHeader.TRANSFERFIELDS(FromSalesInvHeader);
                       DocType::"Posted Return Receipt":
                         IF FromReturnRcptHeader.GET(DocNo) THEN
                           FromSalesHeader.TRANSFERFIELDS(FromReturnRcptHeader);
                       DocType::"Posted Credit Memo":
                         IF FromSalesCrMemoHeader.GET(DocNo) THEN
                           FromSalesHeader.TRANSFERFIELDS(FromSalesCrMemoHeader);
                       DocType::"Arch. Order":
                         IF FromSalesHeaderArchive.GET(FromSalesHeaderArchive."Document Type"::Order,DocNo,DocNoOccurrence,DocVersionNo) THEN
                           FromSalesHeader.TRANSFERFIELDS(FromSalesHeaderArchive);
                       DocType::"Arch. Quote":
                         IF FromSalesHeaderArchive.GET(FromSalesHeaderArchive."Document Type"::Quote,DocNo,DocNoOccurrence,DocVersionNo) THEN
                           FromSalesHeader.TRANSFERFIELDS(FromSalesHeaderArchive);
                       DocType::"Arch. Blanket Order":
                         IF FromSalesHeaderArchive.GET(FromSalesHeaderArchive."Document Type"::"Blanket Order",DocNo,DocNoOccurrence,DocVersionNo) THEN
                           FromSalesHeader.TRANSFERFIELDS(FromSalesHeaderArchive);
                       DocType::"Arch. Return Order":
                         IF FromSalesHeaderArchive.GET(FromSalesHeaderArchive."Document Type"::"Return Order",DocNo,DocNoOccurrence,DocVersionNo) THEN
                           FromSalesHeader.TRANSFERFIELDS(FromSalesHeaderArchive);
                     END;
                     IF FromSalesHeader."No." = '' THEN
                       DocNo := '';
                   END;
                   ValidateDocNo;

                   OnAfterOpenPage;
                 END;

      OnQueryClosePage=BEGIN
                         IF CloseAction = ACTION::OK THEN
                           IF DocNo = '' THEN
                             ERROR(DocNoNotSerErr)
                       END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 3   ;2   ;Field     ;
                  Name=DocumentType;
                  CaptionML=[ENU=Document Type;
                             NOR=Bilagstype;
                             SVE=Dokumenttyp];
                  ToolTipML=[ENU=Specifies the type of document that is processed by the report or batch job.;
                             NOR=Angir typen dokument som behandles av rapporten eller kjõrselen.;
                             SVE=Anger dokumenttypen som rapporten eller buntjobbet bearbetar.];
                  OptionCaptionML=[ENU=Quote,Blanket Order,Order,Invoice,Return Order,Credit Memo,Posted Shipment,Posted Invoice,Posted Return Receipt,Posted Credit Memo,Arch. Quote,Arch. Order,Arch. Blanket Order,Arch. Return Order;
                                   NOR=Tilbud,Rammebestilling,Ordre,Faktura,Ordreretur,Kreditnota,Bokfõrt fõlgeseddel,Bokfõrt faktura,Bokfõrt returseddel,Bokfõrt kreditnota,Ark.tilbud,Ark.ordre,Ark.rammebestilling,Ark.ordreretur;
                                   SVE=Offert,Avropsorder,Order,Faktura,Returorder,Kreditnota,Bokfîrd inleverans,Bokfîrd faktura,Bokfîrd returutleverans,Bokfîrd kreditnota,Arkiverad offert,Arkiverad order,Arkiverad avropsorder,Arkiverad returorder];
                  ApplicationArea=#Suite;
                  SourceExpr=DocType;
                  OnValidate=BEGIN
                               DocNo := '';
                               ValidateDocNo;
                               IF DocType <> DocType::"Posted Invoice" THEN
                                 IncludeOrgInvInfo := FALSE;
                             END;
                              }

      { 8   ;2   ;Field     ;
                  Name=DocumentNo;
                  CaptionML=[ENU=Document No.;
                             NOR=Bilagsnr.;
                             SVE=Dokumentnr];
                  ToolTipML=[ENU=Specifies the number of the document that is processed by the report or batch job.;
                             NOR=Angir nummeret for dokumentet som behandles av rapporten eller kjõrselen.;
                             SVE=Anger antalet dokument som rapporten eller buntjobbet bearbetar.];
                  ApplicationArea=#Suite;
                  SourceExpr=DocNo;
                  OnValidate=BEGIN
                               ValidateDocNo;
                             END;

                  OnLookup=BEGIN
                             LookupDocNo;
                           END;

                  ShowMandatory=TRUE }

      { 9   ;2   ;Field     ;
                  CaptionML=[ENU=Doc. No. Occurrence;
                             NOR=Forekomst av bilagsnr.;
                             SVE=Dok.nr fîrekomst];
                  ToolTipML=[ENU=Specifies the number of times the No. value has been used in the number series.;
                             NOR=Angir hvor mange ganger verdien Nr. er brukt i nummerserien.];
                  ApplicationArea=#Suite;
                  BlankZero=Yes;
                  SourceExpr=DocNoOccurrence;
                  Editable=FALSE }

      { 6   ;2   ;Field     ;
                  CaptionML=[ENU=Version No.;
                             NOR=Versjonsnr.;
                             SVE=Versionsnr];
                  ToolTipML=[ENU=Specifies the version of the document to be copied.;
                             NOR=Angir versjonen av dokumentet som skal kopieres.];
                  ApplicationArea=#Suite;
                  BlankZero=Yes;
                  SourceExpr=DocVersionNo;
                  Editable=FALSE }

      { 5   ;2   ;Field     ;
                  Name=SellToCustNo;
                  CaptionML=[ENU=Sell-to Customer No.;
                             NOR=Salg til-kundenr.;
                             SVE=Fîrs.kundnr];
                  ToolTipML=[ENU=Specifies the sell-to customer number that will appear on the new sales document.;
                             NOR=Angir nummeret for selg til-kunden som skal vises pÜ det nye salgsdokumentet.;
                             SVE=Anger det fîrsÑljningskundnummer som ska visas i det nya fîrsÑljningsdokumentet.];
                  ApplicationArea=#Suite;
                  SourceExpr=FromSalesHeader."Sell-to Customer No.";
                  Editable=FALSE }

      { 7   ;2   ;Field     ;
                  Name=SellToCustName;
                  CaptionML=[ENU=Sell-to Customer Name;
                             NOR=Salg til-kundenavn;
                             SVE=Fîrs.kundnamn];
                  ToolTipML=[ENU=Specifies the sell-to customer name that will appear on the new sales document.;
                             NOR=Angir navnet pÜ selg til-kunden som skal vises pÜ det nye salgsdokumentet.;
                             SVE=Anger det fîrsÑljningskundnamn som ska visas i det nya fîrsÑljningsdokumentet.];
                  ApplicationArea=#Suite;
                  SourceExpr=FromSalesHeader."Sell-to Customer Name";
                  Editable=FALSE }

      { 1   ;2   ;Field     ;
                  Name=IncludeHeader_Options;
                  CaptionML=[ENU=Include Header;
                             NOR=Ta med hode;
                             SVE=Ta med huvud];
                  ToolTipML=[ENU=Specifies if you also want to copy the information from the document header. When you copy quotes, if the posting date field of the new document is empty, the work date is used as the posting date of the new document.;
                             NOR=Angir om du ogsÜ vil kopiere informasjonen fra dokumenthodet. NÜr du kopierer tilbud, brukes arbeidsdatoen som bokfõringsdato for det nye dokumentet hvis feltet Bokfõringsdato i det nye dokumentet er tomt.;
                             SVE=Anger om du ocksÜ vill kopiera informationen frÜn dokumenthuvudet. Om du kopierar en offert, och fÑltet Bokfîringsdatum i det nya dokumentet Ñr tomt, anvÑnds arbetsdatumet som bokfîringsdatum fîr det nya dokumentet.];
                  ApplicationArea=#Suite;
                  SourceExpr=IncludeHeader;
                  OnValidate=BEGIN
                               ValidateIncludeHeader;
                             END;
                              }

      { 4   ;2   ;Field     ;
                  Name=RecalculateLines;
                  CaptionML=[ENU=Recalculate Lines;
                             NOR=Gjenberegn linjer;
                             SVE=BerÑkna om rader];
                  ToolTipML=[ENU=Specifies that lines are recalculate and inserted on the sales document you are creating. The batch job retains the item numbers and item quantities but recalculates the amounts on the lines based on the customer information on the new document header. In this way, the batch job accounts for item prices and discounts that are specifically linked to the customer on the new header.;
                             NOR=Angir at linjer blir omberegnet og satt inn i salgsdokumentet du oppretter. Kjõrselen beholder varenumre og varemengde, men omberegner belõpene pÜ linjene pÜ bakgrunn av kundeinformasjonen i det nye dokumenthodet. PÜ denne mÜten tar kjõrselen med varepriser og -rabatter som er spesielt knyttet til kunden i det nye hodet.;
                             SVE=Anger att rader berÑknas om och infogas i fîrsÑljningsdokumentet som du skapar. Batch-jobbet behÜller artikelnumren och artikelantalen men berÑknar om beloppen pÜ raderna med hjÑlp av kundinformationen i det nya dokumenthuvudet. PÜ sÜ vis redovisar batch-jobbet artikelpriser och rabatter som Ñr knutna till kunden, i det nya huvudet.];
                  ApplicationArea=#Suite;
                  SourceExpr=RecalculateLines;
                  OnValidate=BEGIN
                               IF (DocType = DocType::"Posted Shipment") OR (DocType = DocType::"Posted Return Receipt") THEN
                                 RecalculateLines := TRUE;
                             END;
                              }

      { 1070001;2;Field     ;
                  Name=IncludeOrgInvInfo;
                  CaptionML=[ENU=Include Source Inv. Info.;
                             SVE=Inkludera kÑllfakt.info];
                  ToolTipML=SVE=Anger om du vill ta med information frÜn den ursprungliga fakturan.;
                  SourceExpr=IncludeOrgInvInfo;
                  OnValidate=BEGIN
                               IF DocType <> DocType::"Posted Invoice" THEN
                                 ERROR(Text11200);
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      SalesHeader@1007 : Record 36;
      FromSalesHeader@1010 : Record 36;
      FromSalesShptHeader@1012 : Record 110;
      FromSalesInvHeader@1014 : Record 112;
      FromReturnRcptHeader@1016 : Record 6660;
      FromSalesCrMemoHeader@1018 : Record 114;
      FromSalesHeaderArchive@1033 : Record 11072055;
      SalesSetup@1003 : Record 311;
      CopyDocMgt@1008 : Codeunit 6620;
      DocType@1026 : 'Quote,Blanket Order,Order,Invoice,Return Order,Credit Memo,Posted Shipment,Posted Invoice,Posted Return Receipt,Posted Credit Memo,Arch. Quote,Arch. Order,Arch. Blanket Order,Arch. Return Order';
      DocNo@1027 : Code[20];
      IncludeHeader@1028 : Boolean;
      RecalculateLines@1029 : Boolean;
      IncludeOrgInvInfo@1101100000 : Boolean;
      Text11200@1101100001 : TextConst 'ENU=Can only be used for posted invoices.;SVE=Kan bara anvÑndas fîr bokfîrda fakturor.';
      Text000@1000 : TextConst 'ENU=The price information may not be reversed correctly, if you copy a %1. If possible copy a %2 instead or use %3 functionality.;NOR=Prisinformasjonen tilbakefõres kanskje ikke pÜ riktig mÜte hvis du kopierer %1. Kopier om mulig %2 i stedet, eller bruk %3-funksjonalitet.;SVE=Prisinformationen kanske inte Üterfîrs korrekt om du kopierar en/ett %1. Om det Ñr mîjligt bîr du kopiera en/ett %2 i stÑllet eller anvÑnda funktionen %3.';
      Text001@1001 : TextConst 'ENU=Undo Shipment;NOR=Angre levering;SVE=èterstÑll utleverans';
      Text002@1002 : TextConst 'ENU=Undo Return Receipt;NOR=Angre returseddel;SVE=èterstÑll returinleverans';
      Text003@1004 : TextConst 'ENU=Quote,Blanket Order,Order,Invoice,Return Order,Credit Memo,Posted Shipment,Posted Invoice,Posted Return Receipt,Posted Credit Memo;NOR=Tilbud,Rammeordre,Ordre,Faktura,Ordreretur,Kreditnota,Bokfõrt fõlgeseddel,Bokfõrt faktura,Bokfõrt returseddel,Bokfõrt kreditnota;SVE=Offert,Avropsorder,Order,Faktura,Returorder,Kreditnota,Bokfîrd utleverans,Bokfîrd faktura,Bokfîrd returinleverans,Bokfîrd kreditnota';
      DocNoOccurrence@1031 : Integer;
      DocVersionNo@1032 : Integer;
      DocNoNotSerErr@1005 : TextConst 'ENU=Select a document number to continue, or choose Cancel to close the page.;NOR=Velg et dokumentnummer for Ü fortsette, eller velg Avbryt for Ü lukke siden.';

    [External]
    PROCEDURE SetSalesHeader@2(VAR NewSalesHeader@1000 : Record 36);
    BEGIN
      NewSalesHeader.TESTFIELD("No.");
      SalesHeader := NewSalesHeader;
    END;

    LOCAL PROCEDURE ValidateDocNo@10();
    VAR
      DocType2@1000 : 'Quote,Blanket Order,Order,Invoice,Return Order,Credit Memo,Posted Shipment,Posted Invoice,Posted Return Receipt,Posted Credit Memo';
    BEGIN
      IF DocNo = '' THEN BEGIN
        FromSalesHeader.INIT;
        DocNoOccurrence := 0;
        DocVersionNo := 0;
      END ELSE
        IF FromSalesHeader."No." = '' THEN BEGIN
          FromSalesHeader.INIT;
          CASE DocType OF
            DocType::Quote,
            DocType::"Blanket Order",
            DocType::Order,
            DocType::Invoice,
            DocType::"Return Order",
            DocType::"Credit Memo":
              FromSalesHeader.GET(CopyDocMgt.SalesHeaderDocType(DocType),DocNo);
            DocType::"Posted Shipment":
              BEGIN
                FromSalesShptHeader.GET(DocNo);
                FromSalesHeader.TRANSFERFIELDS(FromSalesShptHeader);
                IF SalesHeader."Document Type" IN
                   [SalesHeader."Document Type"::"Return Order",SalesHeader."Document Type"::"Credit Memo"]
                THEN BEGIN
                  DocType2 := DocType2::"Posted Invoice";
                  MESSAGE(Text000,SELECTSTR(1 + DocType,Text003),SELECTSTR(1 + DocType2,Text003),Text001);
                END;
              END;
            DocType::"Posted Invoice":
              BEGIN
                FromSalesInvHeader.GET(DocNo);
                FromSalesHeader.TRANSFERFIELDS(FromSalesInvHeader);
              END;
            DocType::"Posted Return Receipt":
              BEGIN
                FromReturnRcptHeader.GET(DocNo);
                FromSalesHeader.TRANSFERFIELDS(FromReturnRcptHeader);
                IF SalesHeader."Document Type" IN
                   [SalesHeader."Document Type"::Order,SalesHeader."Document Type"::Invoice]
                THEN BEGIN
                  DocType2 := DocType2::"Posted Credit Memo";
                  MESSAGE(Text000,SELECTSTR(1 + DocType,Text003),SELECTSTR(1 + DocType2,Text003),Text002);
                END;
              END;
            DocType::"Posted Credit Memo":
              BEGIN
                FromSalesCrMemoHeader.GET(DocNo);
                FromSalesHeader.TRANSFERFIELDS(FromSalesCrMemoHeader);
              END;
            DocType::"Arch. Quote",
            DocType::"Arch. Order",
            DocType::"Arch. Blanket Order",
            DocType::"Arch. Return Order":
              BEGIN
                IF NOT FromSalesHeaderArchive.GET(
                     CopyDocMgt.ArchSalesHeaderDocType(DocType),DocNo,DocNoOccurrence,DocVersionNo)
                THEN BEGIN
                  FromSalesHeaderArchive.SETRANGE("No.",DocNo);
                  IF FromSalesHeaderArchive.FINDLAST THEN BEGIN
                    DocNoOccurrence := FromSalesHeaderArchive."Doc. No. Occurrence";
                    DocVersionNo := FromSalesHeaderArchive."Version No.";
                  END;
                END;
                FromSalesHeader.TRANSFERFIELDS(FromSalesHeaderArchive);
              END;
          END;
        END;
      FromSalesHeader."No." := '';

      IncludeHeader :=
        (DocType IN [DocType::"Posted Invoice",DocType::"Posted Credit Memo"]) AND
        ((DocType = DocType::"Posted Credit Memo") <>
         (SalesHeader."Document Type" IN
          [SalesHeader."Document Type"::"Return Order",SalesHeader."Document Type"::"Credit Memo"])) AND
        (SalesHeader."Bill-to Customer No." IN [FromSalesHeader."Bill-to Customer No.",'']);

      OnBeforeValidateIncludeHeader(IncludeHeader);
      ValidateIncludeHeader;
      OnAfterValidateIncludeHeader(IncludeHeader,RecalculateLines);
    END;

    LOCAL PROCEDURE LookupDocNo@3();
    BEGIN
      CASE DocType OF
        DocType::Quote,
        DocType::"Blanket Order",
        DocType::Order,
        DocType::Invoice,
        DocType::"Return Order",
        DocType::"Credit Memo":
          LookupSalesDoc;
        DocType::"Posted Shipment":
          LookupPostedShipment;
        DocType::"Posted Invoice":
          LookupPostedInvoice;
        DocType::"Posted Return Receipt":
          LookupPostedReturn;
        DocType::"Posted Credit Memo":
          LookupPostedCrMemo;
        DocType::"Arch. Quote",
        DocType::"Arch. Order",
        DocType::"Arch. Blanket Order",
        DocType::"Arch. Return Order":
          LookupSalesArchive;
      END;
      ValidateDocNo;
    END;

    LOCAL PROCEDURE LookupSalesDoc@6();
    BEGIN
      FromSalesHeader.FILTERGROUP := 0;
      FromSalesHeader.SETRANGE("Document Type",CopyDocMgt.SalesHeaderDocType(DocType));
      IF SalesHeader."Document Type" = CopyDocMgt.SalesHeaderDocType(DocType) THEN
        FromSalesHeader.SETFILTER("No.",'<>%1',SalesHeader."No.");
      FromSalesHeader.FILTERGROUP := 2;
      FromSalesHeader."Document Type" := CopyDocMgt.SalesHeaderDocType(DocType);
      FromSalesHeader."No." := DocNo;
      IF (DocNo = '') AND (SalesHeader."Sell-to Customer No." <> '') THEN
        IF FromSalesHeader.SETCURRENTKEY("Document Type","Sell-to Customer No.") THEN BEGIN
          FromSalesHeader."Sell-to Customer No." := SalesHeader."Sell-to Customer No.";
          IF FromSalesHeader.FIND('=><') THEN;
        END;
      IF PAGE.RUNMODAL(0,FromSalesHeader) = ACTION::LookupOK THEN
        DocNo := FromSalesHeader."No.";
    END;

    LOCAL PROCEDURE LookupSalesArchive@13();
    BEGIN
      FromSalesHeaderArchive.RESET;
      FromSalesHeaderArchive.FILTERGROUP := 0;
      FromSalesHeaderArchive.SETRANGE("Document Type",CopyDocMgt.ArchSalesHeaderDocType(DocType));
      FromSalesHeaderArchive.FILTERGROUP := 2;
      FromSalesHeaderArchive."Document Type" := CopyDocMgt.ArchSalesHeaderDocType(DocType);
      FromSalesHeaderArchive."No." := DocNo;
      FromSalesHeaderArchive."Doc. No. Occurrence" := DocNoOccurrence;
      FromSalesHeaderArchive."Version No." := DocVersionNo;
      IF (DocNo = '') AND (SalesHeader."Sell-to Customer No." <> '') THEN
        IF FromSalesHeaderArchive.SETCURRENTKEY("Document Type","Sell-to Customer No.") THEN BEGIN
          FromSalesHeaderArchive."Sell-to Customer No." := SalesHeader."Sell-to Customer No.";
          IF FromSalesHeaderArchive.FIND('=><') THEN;
        END;
      IF PAGE.RUNMODAL(0,FromSalesHeaderArchive) = ACTION::LookupOK THEN BEGIN
        DocNo := FromSalesHeaderArchive."No.";
        DocNoOccurrence := FromSalesHeaderArchive."Doc. No. Occurrence";
        DocVersionNo := FromSalesHeaderArchive."Version No.";
        RequestOptionsPage.UPDATE(FALSE);
      END;
    END;

    LOCAL PROCEDURE LookupPostedShipment@8();
    BEGIN
      FromSalesShptHeader."No." := DocNo;
      IF (DocNo = '') AND (SalesHeader."Sell-to Customer No." <> '') THEN
        IF FromSalesShptHeader.SETCURRENTKEY("Sell-to Customer No.") THEN BEGIN
          FromSalesShptHeader."Sell-to Customer No." := SalesHeader."Sell-to Customer No.";
          IF FromSalesShptHeader.FIND('=><') THEN;
        END;
      IF PAGE.RUNMODAL(0,FromSalesShptHeader) = ACTION::LookupOK THEN
        DocNo := FromSalesShptHeader."No.";
    END;

    LOCAL PROCEDURE LookupPostedInvoice@9();
    BEGIN
      FromSalesInvHeader."No." := DocNo;
      IF (DocNo = '') AND (SalesHeader."Sell-to Customer No." <> '') THEN
        IF FromSalesInvHeader.SETCURRENTKEY("Sell-to Customer No.") THEN BEGIN
          FromSalesInvHeader."Sell-to Customer No." := SalesHeader."Sell-to Customer No.";
          IF FromSalesInvHeader.FIND('=><') THEN;
        END;
      FromSalesInvHeader.FILTERGROUP(2);
      FromSalesInvHeader.SETRANGE("Prepayment Invoice",FALSE);
      FromSalesInvHeader.FILTERGROUP(0);
      IF PAGE.RUNMODAL(0,FromSalesInvHeader) = ACTION::LookupOK THEN
        DocNo := FromSalesInvHeader."No.";
    END;

    LOCAL PROCEDURE LookupPostedCrMemo@12();
    BEGIN
      FromSalesCrMemoHeader."No." := DocNo;
      IF (DocNo = '') AND (SalesHeader."Sell-to Customer No." <> '') THEN
        IF FromSalesCrMemoHeader.SETCURRENTKEY("Sell-to Customer No.") THEN BEGIN
          FromSalesCrMemoHeader."Sell-to Customer No." := SalesHeader."Sell-to Customer No.";
          IF FromSalesCrMemoHeader.FIND('=><') THEN;
        END;
      FromSalesCrMemoHeader.FILTERGROUP(2);
      FromSalesCrMemoHeader.SETRANGE("Prepayment Credit Memo",FALSE);
      FromSalesCrMemoHeader.FILTERGROUP(0);
      IF PAGE.RUNMODAL(0,FromSalesCrMemoHeader) = ACTION::LookupOK THEN
        DocNo := FromSalesCrMemoHeader."No.";
    END;

    LOCAL PROCEDURE LookupPostedReturn@11();
    BEGIN
      FromReturnRcptHeader."No." := DocNo;
      IF (DocNo = '') AND (SalesHeader."Sell-to Customer No." <> '') THEN
        IF FromReturnRcptHeader.SETCURRENTKEY("Sell-to Customer No.") THEN BEGIN
          FromReturnRcptHeader."Sell-to Customer No." := SalesHeader."Sell-to Customer No.";
          IF FromReturnRcptHeader.FIND('=><') THEN;
        END;
      IF PAGE.RUNMODAL(0,FromReturnRcptHeader) = ACTION::LookupOK THEN
        DocNo := FromReturnRcptHeader."No.";
    END;

    LOCAL PROCEDURE ValidateIncludeHeader@4();
    BEGIN
      RecalculateLines :=
        (DocType IN [DocType::"Posted Shipment",DocType::"Posted Return Receipt"]) OR NOT IncludeHeader;
    END;

    [External]
    PROCEDURE InitializeRequest@1(NewDocType@1001 : Option;NewDocNo@1000 : Code[20];NewIncludeHeader@1002 : Boolean;NewRecalcLines@1003 : Boolean);
    BEGIN
      DocType := NewDocType;
      DocNo := NewDocNo;
      IncludeHeader := NewIncludeHeader;
      RecalculateLines := NewRecalcLines;
    END;

    [Integration(DEFAULT,TRUE)]
    LOCAL PROCEDURE OnAfterOpenPage@5();
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterValidateIncludeHeader@14(VAR IncludeHeader@1000 : Boolean;VAR RecalculateLines@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeValidateIncludeHeader@1016(VAR DoIncludeHeader@1000 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnPreReportOnBeforeCopySalesDoc@7(VAR CopyDocumentMgt@1000 : Codeunit 6620);
    BEGIN
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

