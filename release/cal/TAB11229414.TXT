OBJECT Table 11229414 Job Serv. Location Rental U.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnInsert=BEGIN
               TESTFIELD("Project No.");
               InitRecord;
             END;

    OnDelete=VAR
               JobServiceLocation@1100528800 : Record 11072219;
             BEGIN
               IF "Rental Unit No." = '' THEN BEGIN
                 JobServiceLocation.SETRANGE("Generated in Company", "Generated in Company");
                 JobServiceLocation.SETRANGE(Company, Company);
                 JobServiceLocation.SETRANGE("Project No.", "Project No.");
                 JobServiceLocation.SETFILTER("Service Location No.", '<>%1', '');
                 IF JobServiceLocation.FINDSET THEN
                   ERROR(Text000);

                 JobServiceLocation.DELETEALL;

               END;

               JobServiceLocation.RESET;

               IF "Rental Unit No." <> '' THEN BEGIN
                 JobServiceLocation.GET("Generated in Company", Company, "Project No.", "Rental Unit No.");
                 IF JobServiceLocation."Service Location No." <> '' THEN
                   ERROR(Text000);
               END;
             END;

    OnRename=VAR
               JobServiceLocation@1100528800 : Record 11072219;
             BEGIN
               JobServiceLocation.SETRANGE("Generated in Company", "Generated in Company");
               JobServiceLocation.SETRANGE(Company, xRec.Company);
               JobServiceLocation.SETRANGE("Project No.", xRec."Project No.");
               JobServiceLocation.SETFILTER("Service Location No.", '<>%1', '');
               IF JobServiceLocation.FINDSET THEN
                 ERROR(Text000, xRec."Project No.", xRec.Company);
             END;

    CaptionML=ENU=Job Service Location Rental Unit;
  }
  FIELDS
  {
    { 10  ;   ;Generated in Company;Text30        ;TableRelation=Company;
                                                   CaptionML=ENU=Generated in Company;
                                                   NotBlank=Yes }
    { 20  ;   ;Company             ;Text30        ;TableRelation=Company;
                                                   CaptionML=[ENU=Company;
                                                              NOR=Selskap;
                                                              SVE=F”retag] }
    { 30  ;   ;Project No.         ;Code20        ;OnValidate=VAR
                                                                Job@1100528800 : Record 11072003;
                                                              BEGIN
                                                                IF Company = '' THEN
                                                                  Job.CHANGECOMPANY(COMPANYNAME)
                                                                ELSE
                                                                  Job.CHANGECOMPANY(Company);
                                                                IF "Project No." <> '' THEN
                                                                  Job.GET("Project No.");
                                                              END;

                                                   OnLookup=VAR
                                                              Job@1100528800 : Record 11072003;
                                                            BEGIN
                                                              IF Company = '' THEN
                                                                Job.CHANGECOMPANY(COMPANYNAME)
                                                              ELSE
                                                                Job.CHANGECOMPANY(Company);
                                                              IF Job.GET("Project No.") THEN;
                                                              IF PAGE.RUNMODAL(PAGE::"Job List", Job) = ACTION::LookupOK THEN
                                                                "Project No." := Job."No.";
                                                            END;

                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr];
                                                   NotBlank=Yes }
    { 40  ;   ;Rental Unit No.     ;Code20        ;OnValidate=VAR
                                                                Plot@1100528800 : Record 11012940;
                                                              BEGIN
                                                                IF "Rental Unit No." = '' THEN
                                                                  EXIT;

                                                                IF Company = '' THEN
                                                                  Plot.CHANGECOMPANY(COMPANYNAME)
                                                                ELSE
                                                                  Plot.CHANGECOMPANY(Company);
                                                                Plot.GET("Project No.", "Rental Unit No.");
                                                              END;

                                                   OnLookup=VAR
                                                              RentalUnit@1100528800 : Record 11012940;
                                                            BEGIN
                                                              IF Company = '' THEN
                                                                RentalUnit.CHANGECOMPANY(COMPANYNAME)
                                                              ELSE
                                                                RentalUnit.CHANGECOMPANY(Company);

                                                              RentalUnit.SETRANGE("Project No.", "Project No.");
                                                              IF PAGE.RUNMODAL(PAGE::"Rental Unit List", RentalUnit) = ACTION::LookupOK THEN
                                                                "Rental Unit No." := RentalUnit."Rental Unit";
                                                            END;

                                                   CaptionML=[ENU=Plot No.;
                                                              NOR=Tegningnr;
                                                              SVE=Ritningnr] }
    { 50  ;   ;Service Location No.;Code20        ;TableRelation="Service Location".No.;
                                                   CaptionML=[ENU=Service Location No.;
                                                              NOR=Servicelokasjon Nr.;
                                                              SVE=Serviceplatsnr];
                                                   Editable=No }
    { 60  ;   ;Create Location for Project;Boolean;OnValidate=BEGIN
                                                                IF "Rental Unit No." <> '' THEN
                                                                  ERROR(Text001);
                                                                IF NOT "Create Location for Project" THEN
                                                                  "Use Proj. No. as Serv. Loc. No" := FALSE;
                                                              END;

                                                   CaptionML=ENU=Create Service Location for Project }
    { 70  ;   ;Use Proj. No. as Serv. Loc. No;Boolean;
                                                   OnValidate=BEGIN
                                                                IF "Use Proj. No. as Serv. Loc. No" THEN
                                                                  TESTFIELD("Create Location for Project");
                                                              END;

                                                   CaptionML=ENU=Use Project No. as Service Location No. }
    { 80  ;   ;Service Location Description;Text50;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location".Description WHERE (No.=FIELD(Service Location No.)));
                                                   CaptionML=ENU=Description (Service Location);
                                                   Editable=No }
    { 90  ;   ;Service Location Address;Text100   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location".Address WHERE (No.=FIELD(Service Location No.)));
                                                   CaptionML=ENU=Address (Service Location);
                                                   Editable=No }
    { 100 ;   ;Service Location Post Code;Code20  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location"."Post Code" WHERE (No.=FIELD(Service Location No.)));
                                                   CaptionML=ENU=Post Code (Service Location);
                                                   Editable=No }
    { 110 ;   ;Service Location City;Text30       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location".City WHERE (No.=FIELD(Service Location No.)));
                                                   CaptionML=ENU=City (Service Location);
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Generated in Company,Company,Project No.,Rental Unit No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528800 : TextConst 'ENU=Service Locations already created. Deleting not allowed.';
      Text001@1100528801 : TextConst 'ENU=Not allowed on Plot Number.';

    PROCEDURE InitRecord@1100528802();
    BEGIN
      IF "Generated in Company" = '' THEN
        "Generated in Company" := COMPANYNAME;

      IF Company = '' THEN
        Company := COMPANYNAME;
    END;

    PROCEDURE CopyPlotNumbers@1100528804();
    VAR
      RentalUnit@1100528803 : Record 11012940;
      JobServiceLocation@1100528802 : Record 11229414;
    BEGIN
      RentalUnit.CHANGECOMPANY(Company);
      RentalUnit.SETRANGE("Project No.", "Project No.");
      RentalUnit.SETFILTER("Rental Unit", '<>%1', '');

      IF RentalUnit.FINDSET THEN
        REPEAT
          IF NOT JobServiceLocation.GET("Generated in Company", Company, "Project No.", RentalUnit."Rental Unit") THEN BEGIN
            JobServiceLocation.INIT;
            JobServiceLocation.Company := Company;
            JobServiceLocation."Project No." := "Project No.";
            JobServiceLocation."Rental Unit No." := RentalUnit."Rental Unit";
            JobServiceLocation.INSERT(TRUE);
          END;
        UNTIL RentalUnit.NEXT = 0;
    END;

    PROCEDURE DeletePlotNumbers@1100528803();
    VAR
      JobServiceLocation@1100528800 : Record 11072219;
    BEGIN
      JobServiceLocation.SETRANGE("Generated in Company", "Generated in Company");
      JobServiceLocation.SETRANGE(Company, Company);
      JobServiceLocation.SETRANGE("Project No.", "Project No.");
      JobServiceLocation.SETRANGE("Service Location No.", '');
      JobServiceLocation.SETFILTER("Plot No.", '<>%1', '');
      JobServiceLocation.DELETEALL;
    END;

    PROCEDURE GenerateServiceLocations@1100528800(VAR TempServiceLocation@1100528805 : TEMPORARY Record 11012801;IPreferredEmployee@1100528806 : Code[20];ISubContractor@1100528807 : Code[20]);
    VAR
      ServiceLocation@1100528800 : Record 11012801;
      JobServiceLocation@1100528801 : Record 11229414;
      Job@1100528802 : Record 11072003;
      RentalUnit@1100528803 : Record 11012940;
      RecordRef@1100528804 : RecordRef;
    BEGIN
      Job.CHANGECOMPANY(Company);
      RentalUnit.CHANGECOMPANY(Company);
      Job.GET("Project No.");

      JobServiceLocation.SETRANGE("Generated in Company", "Generated in Company");
      JobServiceLocation.SETRANGE(Company, Company);
      JobServiceLocation.SETRANGE("Project No.", "Project No.");
      JobServiceLocation.SETRANGE("Service Location No.", '');
      IF NOT "Create Location for Project" THEN
        JobServiceLocation.SETFILTER("Rental Unit No.", '<>%1', '');
      IF JobServiceLocation.FINDSET THEN
        REPEAT
          ServiceLocation.INIT;
          ServiceLocation."No." := '';

          IF JobServiceLocation."Rental Unit No." <> '' THEN
            RentalUnit.GET("Project No.", JobServiceLocation."Rental Unit No.")
          ELSE
            IF "Use Proj. No. as Serv. Loc. No" THEN
              ServiceLocation."No." := "Project No.";

          IF Company <> COMPANYNAME THEN
            ServiceLocation."Supplying Company" := Company;
          ServiceLocation."Project No." := "Project No.";
          ServiceLocation.INSERT(TRUE);

          UpdateServiceLocationFromJob(ServiceLocation, JobServiceLocation, Job, RentalUnit, FALSE);

          ServiceLocation."Service Location Type" := TempServiceLocation."Service Location Type";
          ServiceLocation.Type := TempServiceLocation.Type;
          ServiceLocation."Territory Code" := TempServiceLocation."Territory Code";
          ServiceLocation."Location Manager" := TempServiceLocation."Location Manager";
          ServiceLocation."Travel Distance" := TempServiceLocation."Travel Distance";
          ServiceLocation."Travel Time" := TempServiceLocation."Travel Time";
          ServiceLocation."Travel Cost Code" := TempServiceLocation."Travel Cost Code";
          ServiceLocation."Global Dimension 1 Code" := TempServiceLocation."Global Dimension 1 Code";
          ServiceLocation."Remote Control" := TempServiceLocation."Remote Control";
          ServiceLocation."Warranty Company" := TempServiceLocation."Warranty Company";
          ServiceLocation."Warranty Project No." := TempServiceLocation."Warranty Project No.";
          ServiceLocation."Warranty Element No" := TempServiceLocation."Warranty Element No";
          ServiceLocation."Warranty Expires On" := TempServiceLocation."Warranty Expires On";
          ServiceLocation."Rental Unit" := JobServiceLocation."Rental Unit No.";

          CreateServiceLocationPreferredEmployee(ServiceLocation."No.", IPreferredEmployee);
          CreateServiceLocationSubContractor(ServiceLocation."No.", ISubContractor);
          ServiceLocation.MODIFY;

          JobServiceLocation."Service Location No." := ServiceLocation."No.";
          JobServiceLocation.MODIFY;

          RentalUnit."Service Location No." := ServiceLocation."No.";
          RentalUnit.MODIFY;

          CreateSubcontractorServiceByProjectAuthority(JobServiceLocation);

          IF JobServiceLocation."Rental Unit No." <> '' THEN
            RecordRef.GETTABLE(RentalUnit)
          ELSE
            RecordRef.GETTABLE(Job);

          CreateDocumentLink(ServiceLocation, RecordRef, Company);

        UNTIL JobServiceLocation.NEXT = 0;
    END;

    PROCEDURE CreateDocumentLink@1100525005(ServiceLocation@1100525007 : Record 11012801;RecRef@1100525006 : RecordRef;CompName@1100525008 : Text[50]);
    VAR
      DocumentProperties@1100525005 : Record 11012746;
      DocumentLink@1100525004 : Record 11012747;
      DocumentLink2@1100529900 : Record 11012747;
      DocumentLinkMgt@1100525002 : Codeunit 11012401;
      DocLinkRID@1100525000 : ARRAY [5] OF RecordID;
    BEGIN
      DocumentLink.RESET;
      DocumentLink.SETCURRENTKEY("Record ID"); //mg, 05-07-11: M27801
      DocumentLink.SETRANGE("Table No.", RecRef.NUMBER);
      DocumentLink.SETFILTER("Record ID", STRSUBSTNO('''%1''', RecRef.RECORDID()));
      DocumentLink.SETRANGE("Internal Company", CompName);
      IF DocumentLink.FINDSET THEN BEGIN
        REPEAT
          DocumentProperties.SETRANGE("No.", DocumentLink."Document No.");
          IF DocumentProperties.FINDFIRST THEN BEGIN
            RecRef.GETTABLE(ServiceLocation);
            DocLinkRID[2] := RecRef.RECORDID();
            DocumentLink2.SETRANGE("Document No.", DocumentLink."Document No.");
            DocumentLink2.SETRANGE("Table No.", RecRef.NUMBER);
            DocumentLink2.SETFILTER("Record ID", STRSUBSTNO('''%1''', RecRef.RECORDID()));
            DocumentLink2.SETRANGE("Internal Company", CompName);
            IF DocumentLink2.ISEMPTY THEN BEGIN
              DocumentLinkMgt.CreateDocumentLink(DocumentProperties, DocLinkRID);
              DocumentProperties.MODIFY(TRUE);
            END;
          END;
        UNTIL DocumentLink.NEXT = 0;
      END;
    END;

    PROCEDURE UpdateLocations@1100528801();
    VAR
      ServiceLocation@1100528804 : Record 11012801;
      JobServiceLocation@1100528803 : Record 11229414;
      Job@1100528802 : Record 11072003;
      Plot@1100528801 : Record 11012940;
      RecordRef@1100528800 : RecordRef;
    BEGIN
      Job.CHANGECOMPANY(Company);
      Plot.CHANGECOMPANY(Company);
      Job.GET("Project No.");

      JobServiceLocation.SETRANGE("Generated in Company", "Generated in Company");
      JobServiceLocation.SETRANGE(Company, Company);
      JobServiceLocation.SETRANGE("Project No.", "Project No.");
      JobServiceLocation.SETFILTER("Service Location No.", '<>%1', '');
      IF JobServiceLocation.FINDSET THEN
        REPEAT
          IF JobServiceLocation."Rental Unit No." <> '' THEN
            Plot.GET("Project No.", JobServiceLocation."Rental Unit No.");
          IF ServiceLocation.GET(JobServiceLocation."Service Location No.") THEN BEGIN
            UpdateServiceLocationFromJob(ServiceLocation, JobServiceLocation, Job, Plot, TRUE);
            ServiceLocation.MODIFY;

            IF JobServiceLocation."Rental Unit No." <> '' THEN
              RecordRef.GETTABLE(Plot)
            ELSE
              RecordRef.GETTABLE(Job);

            CreateDocumentLink(ServiceLocation, RecordRef, Company);
          END;
        UNTIL JobServiceLocation.NEXT = 0;
    END;

    LOCAL PROCEDURE UpdateServiceLocationFromJob@1100528600(VAR IOServiceLocation@1100528602 : Record 11012801;IJobServiceLocation@1100528600 : Record 11229414;IJob@1100528601 : Record 11072003;IPlot@1100528603 : Record 11012940;UpdateMode@1100529900 : Boolean);
    VAR
      Customer@1100528605 : Record 18;
      RentalPackage@1100528500 : Record 11012941;
      SkipCustomer@1100528501 : Boolean;
    BEGIN
      IF (NOT UpdateMode) OR (UpdateMode AND (IOServiceLocation.Description = '')) THEN
        IOServiceLocation.VALIDATE(Description, IJob.Description);
      IF (NOT UpdateMode) OR (UpdateMode AND (IOServiceLocation.Type = '')) THEN
        IOServiceLocation.Type := IJob.Type;
      IF IJobServiceLocation."Rental Unit No." <> '' THEN
        IF IPlot.GetActualRentalPackage(RentalPackage) THEN
          IF RentalPackage."Customer No." <> '' THEN BEGIN
            SkipCustomer := TRUE;
            IOServiceLocation."Customer No." := RentalPackage."Customer No.";
            IOServiceLocation."Bill-to Customer No." := RentalPackage."Customer No.";
          END;
      IF (IJobServiceLocation."Rental Unit No." <> '') AND (IPlot.Address + IPlot.City + IPlot."Post Code" + IPlot."BAG Id" <> '') THEN BEGIN
        IOServiceLocation.Address := IPlot.Address;
        IOServiceLocation.City := IPlot.City;
        IOServiceLocation."Post Code" := IPlot."Post Code";
        IOServiceLocation."BAG Id" := IPlot."BAG Id";
      END ELSE BEGIN
        IOServiceLocation.Address := IJob.Address;
        IOServiceLocation."Address 2" := IJob."Address 2";
        IOServiceLocation.City := IJob.City;
        IOServiceLocation."Post Code" := IJob."Post Code";
      END;
      IF NOT SkipCustomer THEN
        IF Customer.GET(IJob."Bill-to Customer No.") THEN BEGIN
          IOServiceLocation."Customer No." := Customer."No.";
          IOServiceLocation."Bill-to Customer No." := Customer."No.";
        END;
      IOServiceLocation.County := IJob.County;
      IOServiceLocation."Country/Region Code":= IJob."Country/Region Code";
      IOServiceLocation."Territory Code" := IJob."Territory Code";
    END;

    LOCAL PROCEDURE CreateSubcontractorServiceByProjectAuthority@1100528601(IJobServiceLocation@1100528600 : Record 11229414);
    VAR
      ProjectAuthority@1100528602 : Record 11020216;
      AuthorityType@1100528603 : Record 11020215;
      SubcontractorService@1100528604 : Record 11071789;
      Contact@1100528605 : Record 5050;
      Vendor@1100528606 : Record 23;
      EntryNo@1100527300 : Integer;
    BEGIN
      ProjectAuthority.CHANGECOMPANY(IJobServiceLocation.Company);
      AuthorityType.CHANGECOMPANY(IJobServiceLocation.Company);
      Contact.CHANGECOMPANY(IJobServiceLocation.Company);

      ProjectAuthority.SETRANGE("Project No.", IJobServiceLocation."Project No.");
      ProjectAuthority.SETRANGE("Use as Subc. on Serv. Location", TRUE);
      ProjectAuthority.SETFILTER("Contact No.", '<>%1', '');
      IF ProjectAuthority.FINDSET THEN
        REPEAT
          AuthorityType.GET(ProjectAuthority."Authority Type");
          CLEAR(SubcontractorService);
          SubcontractorService."Entry No." := EntryNo + 1;
          SubcontractorService."Service Location No." := IJobServiceLocation."Service Location No.";
          SubcontractorService.Discipline := AuthorityType.Discipline;
          SubcontractorService."Trade Speciality" := AuthorityType."Trade Speciality";
          SubcontractorService."Apply To" := SubcontractorService."Apply To"::Call;
          Contact.GET(ProjectAuthority."Contact No.");
          Contact.GetVendorByContact(Contact, Vendor);
          SubcontractorService.Subcontractor := Vendor."No.";
          SubcontractorService.INSERT(TRUE);
          EntryNo += 1;
        UNTIL ProjectAuthority.NEXT = 0;
    END;

    LOCAL PROCEDURE CreateServiceLocationPreferredEmployee@1100528805(IServiceLocation@1100528802 : Code[20];IResourceWOP@1100528801 : Code[20]);
    VAR
      PreferredEmployee@1100528800 : Record 11012843;
    BEGIN
      IF NOT PreferredEmployee.GET('','',IServiceLocation,'','','','',IResourceWOP) THEN BEGIN
        PreferredEmployee.VALIDATE("Service Location No.", IServiceLocation);
        PreferredEmployee.VALIDATE("Preferred Employee Code", IResourceWOP);
        PreferredEmployee.INSERT;
      END;
    END;

    LOCAL PROCEDURE CreateServiceLocationSubContractor@1100528806(IServiceLocationNo@1100528800 : Code[20];IVendorCode@1100528801 : Code[20]);
    VAR
      SubcontractorService@1100528802 : Record 11071789;
    BEGIN
      SubcontractorService.SETRANGE("Service Location No.", IServiceLocationNo);
      SubcontractorService.SETRANGE(Subcontractor, IVendorCode);
      IF NOT SubcontractorService.FINDFIRST THEN BEGIN
        SubcontractorService.INIT;
        SubcontractorService."Service Location No." := IServiceLocationNo;
        SubcontractorService.Subcontractor := IVendorCode;
        SubcontractorService.INSERT;
      END
    END;

    BEGIN
    {
      Modeled after 'Job Service Location', 11072219
    }
    END.
  }
}

