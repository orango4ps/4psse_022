OBJECT Codeunit 11012252 Completion Check Management
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1100525000 : TextConst 'DEU=Es wurden keine unvollstÑndigen Felder fÅr diesen Datensatz gefunden.;ENU=No incomplete fields found for this record.;NLD=Geen onvolledige velden gevonden voor dit record.;NOR=Ingen ufullstendige felt funnet for denne posten.;SVE=Det gick inte att hitta nÜgra ofullstÑndiga fÑlt fîr den hÑr posten.';
      Text001@1100525001 : TextConst 'DEU=Es wurde keine VollstÑndigkeitskontrolle fÅr Tabelle %1 eingerichtet.;ENU=No completeness check enabled for table %1.;NLD=Geen volledigheidscontrole ingesteld voor tabel %1.;NOR=Ingen ferdigstillelseskontroll er aktivert for tabellen %1.;SVE=Ingen fullstÑndighetskontroll har aktiverats fîr tabellen %1.';

    PROCEDURE ShowIncompleteFields@1100525004(RecRef@1100525000 : RecordRef);
    VAR
      FieldsToBeCheckedBuffer@1100525001 : TEMPORARY Record 11020567;
      IncompleteRecordLogBuffer@1100525002 : TEMPORARY Record 11020574;
    BEGIN
      IF NOT GetFieldListForCompletionChk(RecRef.NUMBER, FieldsToBeCheckedBuffer) THEN BEGIN
        MESSAGE(Text001, RecRef.CAPTION);
        EXIT;
      END;

      IF NOT IsRecordComplete(RecRef, FieldsToBeCheckedBuffer, IncompleteRecordLogBuffer) THEN
        PAGE.RUNMODAL(0, IncompleteRecordLogBuffer)
      ELSE
        MESSAGE(Text000);
    END;

    PROCEDURE ShowIncompleteFieldsNoMessages@1210190000(RecRef@1210190000 : RecordRef);
    VAR
      FieldsToBeCheckedBuffer@1210190002 : TEMPORARY Record 11020567;
      IncompleteRecordLogBuffer@1210190001 : TEMPORARY Record 11020574;
    BEGIN
      IF NOT GetFieldListForCompletionChk(RecRef.NUMBER, FieldsToBeCheckedBuffer) THEN
        EXIT;

      IF NOT IsRecordComplete(RecRef, FieldsToBeCheckedBuffer, IncompleteRecordLogBuffer) THEN
        PAGE.RUNMODAL(0, IncompleteRecordLogBuffer);
    END;

    PROCEDURE IsRecordComplete@1100525003(RecRef@1100525000 : RecordRef;VAR FieldsToBeChecked@1100525006 : Record 11020567;VAR IncompleteRecordLogBuffer@1100525007 : Record 11020574) IsComplete : Boolean;
    VAR
      FieldRef@1100525004 : FieldRef;
    BEGIN
      IsComplete := TRUE;
      IF NOT FieldsToBeChecked.FINDFIRST THEN
        EXIT;

      REPEAT
        FieldRef := RecRef.FIELD(FieldsToBeChecked."Field No.");
        IF (FORMAT(FieldRef.VALUE) = '') OR (FORMAT(FieldRef.VALUE) = '0') THEN BEGIN
          IncompleteRecordLogBuffer.AddIncompleteField(RecRef, FieldRef);
          IsComplete := FALSE;
        END;
      UNTIL FieldsToBeChecked.NEXT = 0;

      EXIT;
    END;

    PROCEDURE GetFieldListForCompletionChk@1100525002(TableNo@1100525000 : Integer;VAR FieldsToBeChecked@1100525006 : Record 11020567) : Boolean;
    VAR
      ExtendedFieldProperty@1100525001 : Record 11020567;
      ExtendedFieldPropertyDummy@1100525002 : TEMPORARY Record 11020567;
    BEGIN
      ExtendedFieldPropertyDummy.COPY(FieldsToBeChecked, TRUE); //chk if called with temp var
      FieldsToBeChecked.DELETEALL;

      ExtendedFieldProperty.SETRANGE("Table No.", TableNo);
      ExtendedFieldProperty.SETRANGE("Check Completion control", TRUE);
      IF NOT ExtendedFieldProperty.FINDFIRST THEN
        EXIT;

      REPEAT
        FieldsToBeChecked := ExtendedFieldProperty;
        FieldsToBeChecked.INSERT;
      UNTIL ExtendedFieldProperty.NEXT = 0;

      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

