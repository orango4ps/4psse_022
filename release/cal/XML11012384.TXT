OBJECT XMLport 11012384 Import Act. Wage Cost Payroll
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Import Actual Wage List Payr.;
    Direction=Import;
    DefaultFieldsValidation=No;
    TextEncoding=WINDOWS;
    OnInitXMLport=BEGIN
                    HumanResourcesSetup.GET;
                    HumanResourcesSetup.TESTFIELD("Salary Application",HumanResourcesSetup."Salary Application"::Payroll);

                    IF HumanResourcesSetup."Calculate Wage Costs" <> HumanResourcesSetup."Calculate Wage Costs"::"Actual Rate" THEN
                      IF NOT CONFIRM(Text001, TRUE) THEN
                        ERROR(Text002);

                    HumanResourcesSetup.TESTFIELD("Actual Wage Journal Template");
                    HumanResourcesSetup.TESTFIELD("Actual Wage Journal Batch");

                    currXMLport.FILENAME(HumanResourcesSetup."Pathname Files from Salary");
                  END;

    OnPreXMLport=BEGIN
                   JnlLineRec.LOCKTABLE;
                   JnlLineRec.SETRANGE("Journal Template Name",HumanResourcesSetup."Actual Wage Journal Template");
                   JnlLineRec.SETRANGE("Journal Batch Name",HumanResourcesSetup."Actual Wage Journal Batch");
                   IF JnlLineRec.FIND('+') THEN
                     LineCounter := JnlLineRec."Line No."
                   ELSE
                     LineCounter := 0;

                   CurrFact := 1.0;
                   IF TODAY < 020101D THEN
                     CurrDflt := 1
                   ELSE
                     CurrDflt := 2;
                   GLSetup.GET;
                 END;

    OnPostXMLport=BEGIN
                    OldFile := currXMLport.FILENAME;
                    NewFile := HumanResourcesSetup.DetermineBackupFile(OldFile);
                    IF NewFile <> OldFile THEN
                      FileMgt.RenameClientFile(OldFile, NewFile, TRUE);
                  END;

    Format=Fixed Text;
    UseRequestPage=No;
  }
  ELEMENTS
  {
    { [{DD5954B9-61F0-44B5-A241-51CBEC3C5A35}];  ;Root                ;Element ;Text     }

    { [{7464F36A-0776-4576-BE02-5184A7DEBE51}];1 ;GenJournalLine      ;Element ;Table   ;
                                                  SourceTable=Table81;
                                                  SourceTableView=SORTING(Field1,Field51,Field2);
                                                  Import::OnAfterInitRecord=BEGIN
                                                                              "Gen. Journal Line".INIT;
                                                                              CLEAR(WeekTxt);
                                                                              CLEAR(ProjectTxt);
                                                                              CLEAR(StructurePart);
                                                                              CLEAR(SupplyingCompany);
                                                                              CLEAR(ReceivingCompany);
                                                                              CLEAR(EmployeeNoLong);
                                                                            END;

                                                  Import::OnBeforeInsertRecord=VAR
                                                                                 JobsSetup@1100525000 : Record 315;
                                                                                 RecRef@1100528400 : RecordRef;
                                                                                 ServiceOrder@1100528701 : Record 11012823;
                                                                                 ServiceOrderNo@1100528700 : Code[20];
                                                                               BEGIN
                                                                                 WITH "Gen. Journal Line" DO BEGIN
                                                                                   IF "Employee No." = '0000' THEN
                                                                                     "Employee No." := EmployeeNoLong;
                                                                                   //
                                                                                   FinishedProject := '';
                                                                                   "Journal Template Name" := HumanResourcesSetup."Actual Wage Journal Template";
                                                                                   "Journal Batch Name" := HumanResourcesSetup."Actual Wage Journal Batch";
                                                                                   "Line No." := LineCounter + 10000;
                                                                                   LineCounter := LineCounter + 10000;
                                                                                   "Account Type" := "Account Type"::"G/L Account";

                                                                                   RecRef.GETTABLE("Gen. Journal Line");
                                                                                   XMLportStatusDialogMgt.ShowImportStatus(RecRef);

                                                                                   IF NOT JnlTemplRec.GET(HumanResourcesSetup."Actual Wage Journal Template") THEN
                                                                                     JnlTemplRec.INIT;
                                                                                   IF NOT JnlBatchRec.GET(HumanResourcesSetup."Actual Wage Journal Template", HumanResourcesSetup."Actual Wage Journal Batch") THEN
                                                                                     JnlBatchRec.INIT;

                                                                                   IF DocNo = '' THEN BEGIN
                                                                                     IF JnlBatchRec."No. Series" <> '' THEN
                                                                                       DocNo := NoSeriesMgt.GetNextNo(JnlBatchRec."No. Series",0D,FALSE)
                                                                                     ELSE
                                                                                       IF JnlTemplRec."No. Series" <> '' THEN
                                                                                         DocNo := NoSeriesMgt.GetNextNo(JnlTemplRec."No. Series",0D,FALSE);
                                                                                   END;
                                                                                   "Document No." := DocNo;

                                                                                   Year := DATE2DMY(TODAY,3);
                                                                                   cper := DATE2DWY(TODAY,2);
                                                                                   IF cper < week THEN
                                                                                     Year := Year - 1;
                                                                                   "Posting Date" := DWY2DATE(5, week, Year);
                                                                                   "Document Date" := "Posting Date";

                                                                                   sep := STRPOS(StructurePart, '-');
                                                                                   IF sep <> 0 THEN BEGIN
                                                                                     project := project + COPYSTR(StructurePart, sep, 1) + COPYSTR(StructurePart, 1, (sep-1));
                                                                                     StructurePart := COPYSTR(StructurePart, (sep+1), (STRLEN(StructurePart)-sep));
                                                                                   END;

                                                                                   CheckCompany;
                                                                                   "Receiving Company" := ReceivingCompany;
                                                                                   IF "Receiving Company" <> '' THEN BEGIN
                                                                                     JobsSetup.CHANGECOMPANY("Receiving Company");
                                                                                     ProjRec.CHANGECOMPANY("Receiving Company");
                                                                                     PtypRec.CHANGECOMPANY("Receiving Company");
                                                                                     EmplRec.CHANGECOMPANY("Receiving Company");
                                                                                     ServiceOrder.CHANGECOMPANY("Receiving Company");
                                                                                   END ELSE BEGIN
                                                                                     JobsSetup.CHANGECOMPANY(COMPANYNAME);
                                                                                     ProjRec.CHANGECOMPANY(COMPANYNAME);
                                                                                     PtypRec.CHANGECOMPANY(COMPANYNAME);
                                                                                     EmplRec.CHANGECOMPANY(COMPANYNAME);
                                                                                     ServiceOrder.CHANGECOMPANY(COMPANYNAME);
                                                                                   END;
                                                                                   JobsSetup.GET;

                                                                                   WHILE NOT EmplRec.GET("Employee No.") AND
                                                                                         StartsWithZero("Employee No.")
                                                                                   DO
                                                                                     StripFirstZero("Employee No.");

                                                                                   DimMgt.GetDimValueRec(1, "Shortcut Dimension 1 Code", DimVal1Rec, FALSE,'');
                                                                                   WHILE (DimVal1Rec.Code = '') AND
                                                                                         StartsWithZero("Shortcut Dimension 1 Code")
                                                                                   DO BEGIN
                                                                                     StripFirstZero("Shortcut Dimension 1 Code");
                                                                                     IF "Shortcut Dimension 1 Code" <> '' THEN
                                                                                       DimMgt.GetDimValueRec(1, "Shortcut Dimension 1 Code", DimVal1Rec, FALSE,'');
                                                                                   END;

                                                                                   DimMgt.GetDimValueRec(2, "Shortcut Dimension 2 Code", DimVal2Rec, FALSE,'');
                                                                                   WHILE (DimVal2Rec.Code = '') AND
                                                                                         StartsWithZero("Shortcut Dimension 2 Code")
                                                                                   DO BEGIN
                                                                                     StripFirstZero("Shortcut Dimension 2 Code");
                                                                                     IF "Shortcut Dimension 2 Code" <> '' THEN
                                                                                       DimMgt.GetDimValueRec(2, "Shortcut Dimension 2 Code", DimVal2Rec, FALSE,'');
                                                                                   END;

                                                                                   ServiceOrderNo := '';

                                                                                   IF project <> '' THEN BEGIN
                                                                                     IF UPPERCASE(COPYSTR(project, 1, 2)) = 'S~' THEN BEGIN
                                                                                       ServiceOrderNo := DELCHR(COPYSTR(project, 3), '>', ' ') + DELCHR(StructurePart, '<', ' ');
                                                                                     END ELSE BEGIN
                                                                                       WHILE NOT ProjRec.GET(project) AND
                                                                                             StartsWithZero(project)
                                                                                       DO
                                                                                         StripFirstZero(project);
                                                                                       ProjRec.GET(project);

                                                                                       IF (JobsSetup."Provisions at Closure") THEN BEGIN
                                                                                         IF ProjRec."Project Status" >= ProjRec."Project Status"::Archive THEN BEGIN
                                                                                           DetermineFinishedProjectCode;
                                                                                         END;
                                                                                       END ELSE BEGIN
                                                                                         IF ProjRec."Project Status" >= ProjRec."Project Status"::Finished THEN BEGIN
                                                                                           DetermineFinishedProjectCode;
                                                                                         END;
                                                                                       END;
                                                                                     END;
                                                                                   END;

                                                                                   IF ServiceOrderNo <> '' THEN BEGIN
                                                                                     WHILE NOT ServiceOrder.GET(ServiceOrderNo) AND
                                                                                           StartsWithZero(ServiceOrderNo)
                                                                                     DO
                                                                                       StripFirstZero(ServiceOrderNo);
                                                                                     ServiceOrder.GET(ServiceOrderNo);
                                                                                     VALIDATE("Service Order No.", ServiceOrderNo);
                                                                                   END ELSE BEGIN
                                                                                     VALIDATE("Job No.", project);
                                                                                     VALIDATE(Element, StructurePart);
                                                                                     IF NOT ProjRec.GET("Job No.") THEN ProjRec.INIT;
                                                                                     IF NOT PtypRec.GET(ProjRec."Project Type") THEN PtypRec.INIT;
                                                                                     VALIDATE("Account No.", WIPAcc(JobsSetup));
                                                                                   END;
                                                                                   IF NOT EmplRec.GET("Employee No.") THEN EmplRec.INIT;
                                                                                   IF NOT TradeGrpRec.GET(EmplRec."Trade Association") THEN TradeGrpRec.INIT;
                                                                                   VALIDATE("Bal. Account No.", TradeGrpRec."Wage Coverage Account");
                                                                                   Quantity := Quantity / 100;
                                                                                   Amount := (Amount * CurrFact) / 100;
                                                                                   VALIDATE(Amount);

                                                                                   IF NOT GLAccRec.GET("Account No.") THEN GLAccRec.INIT;
                                                                                   IF GLAccRec."Import Posting Information" = FALSE THEN GLAccRec.INIT;
                                                                                   "Gen. Posting Type" := GLAccRec."Gen. Posting Type";
                                                                                   "Gen. Bus. Posting Group" := GLAccRec."Gen. Bus. Posting Group";
                                                                                   "Gen. Prod. Posting Group" := GLAccRec."Gen. Prod. Posting Group";
                                                                                   "VAT Bus. Posting Group" := GLAccRec."VAT Bus. Posting Group";
                                                                                   "VAT Prod. Posting Group" := GLAccRec."VAT Prod. Posting Group";
                                                                                   "Tax Area Code" := GLAccRec."Tax Area Code";
                                                                                   "Tax Liable" := GLAccRec."Tax Liable";
                                                                                   "Tax Group Code" := GLAccRec."Tax Group Code";
                                                                                   VALIDATE("VAT Prod. Posting Group");

                                                                                   VALIDATE(Quantity);
                                                                                   "Source Code" := JnlTemplRec."Source Code";
                                                                                   IF JnlBatchRec."Reason Code" <> '' THEN
                                                                                     "Reason Code" := JnlBatchRec."Reason Code"
                                                                                   ELSE
                                                                                     "Reason Code" := JnlTemplRec."Reason Code";
                                                                                   "Unit of Measure Code" := DimVal2Rec."Unit of Measure";
                                                                                   IF FinishedProject <> '' THEN
                                                                                     Description := COPYSTR(STRSUBSTNO(Text006,Description,FinishedProject), 1, MAXSTRLEN(Description))
                                                                                   ELSE
                                                                                     Description := COPYSTR(Text007 + EmplRec.FullName, 1, MAXSTRLEN(Description)) ;
                                                                                 END;
                                                                               END;
                                                                                }

    { [{37E423EC-0093-498C-88A2-F5C8AE65884B}];2 ;Filler1             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=3 }

    { [{CDBE91F9-646C-4011-A767-91FEA5E87F02}];2 ;EmployeeNo          ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=Gen. Journal Line::Employee No.;
                                                  MinOccurs=Zero;
                                                  Width=4 }

    { [{ABC2D57D-63D9-4167-9977-1632EB0213F5}];2 ;WeekTxt             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  FillEmptyTextWithZero(WeekTxt);
                                                                                  EVALUATE(week, WeekTxt);
                                                                                END;

                                                  Width=2 }

    { [{10786E43-578A-42F4-A17B-8109F8BEBCB0}];2 ;Filler2             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=3 }

    { [{8D71AB28-A2ED-4F71-9454-4EFA12A5480F}];2 ;Amount              ;Element ;Field   ;
                                                  DataType=Decimal;
                                                  SourceField=Gen. Journal Line::Amount;
                                                  MinOccurs=Zero;
                                                  Width=9 }

    { [{57CBBB15-1A0A-4595-A8C3-BE21D7985818}];2 ;Quantity            ;Element ;Field   ;
                                                  DataType=Decimal;
                                                  SourceField=Gen. Journal Line::Quantity;
                                                  MinOccurs=Zero;
                                                  Width=9 }

    { [{0D21CD5F-AD06-43B4-A5D0-DE361C21B6C4}];2 ;ProjectTxt          ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  project := ProjectTxt;
                                                                                END;

                                                  Width=6 }

    { [{E227172D-8792-4756-89F7-51ADC399C90D}];2 ;StructurePart       ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=8 }

    { [{10117FEC-4261-4191-95B8-45EE5C37F93F}];2 ;ShortcutDimension2Code;Element;Field  ;
                                                  DataType=Code;
                                                  SourceField=Gen. Journal Line::Shortcut Dimension 2 Code;
                                                  MinOccurs=Zero;
                                                  Width=8 }

    { [{33DA26DD-6A39-4BC9-B8E1-28E1128C7723}];2 ;WageComponent       ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=Gen. Journal Line::Wage Component;
                                                  MinOccurs=Zero;
                                                  Width=3 }

    { [{701F40A7-8F65-4C62-9140-045A0AFAA7A0}];2 ;Filler3             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=1 }

    { [{FD8CFF78-DDDD-4D92-9186-071387BFCCBE}];2 ;SupplyingCompany    ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=3 }

    { [{D1C6BB35-2BE6-44A6-9AB1-D1EDE2D49071}];2 ;ShortcutDimension1Code;Element;Field  ;
                                                  DataType=Code;
                                                  SourceField=Gen. Journal Line::Shortcut Dimension 1 Code;
                                                  MinOccurs=Zero;
                                                  Width=6 }

    { [{533E3A2C-40F2-47F4-88C3-A7485D09F3BF}];2 ;ReceivingCompany    ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=3 }

    { [{77A1E00C-06B3-4BDB-AE87-D27872A0B6D2}];2 ;EmployeeNoLong      ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=6 }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      GLSetup@1100528430 : Record 98;
      HumanResourcesSetup@1100528429 : Record 5218;
      JnlLineRec@1100528427 : Record 81;
      JnlTemplRec@1100528426 : Record 80;
      JnlBatchRec@1100528425 : Record 232;
      ProjRec@1100528424 : Record 11072003;
      PtypRec@1100528423 : Record 11012009;
      EmplRec@1100528422 : Record 5200;
      TradeGrpRec@1100528421 : Record 11012015;
      DimVal1Rec@1100528420 : Record 349;
      DimVal2Rec@1100528419 : Record 349;
      GLAccRec@1100528418 : Record 15;
      NoSeriesMgt@1100528417 : Codeunit 396;
      DimMgt@1100528416 : Codeunit 408;
      FileMgt@1100528434 : Codeunit 419;
      XMLportStatusDialogMgt@1100528436 : Codeunit 11130006;
      DocNo@1100528415 : Code[20];
      project@1100528435 : Code[20];
      LineCounter@1100528412 : Integer;
      Year@1100528411 : Integer;
      week@1100528410 : Integer;
      cper@1100528409 : Integer;
      sep@1100528408 : Integer;
      CurrMenu@1100528407 : Integer;
      CurrDflt@1100528406 : Integer;
      CurrFact@1100528405 : Decimal;
      FinishedProject@1100528402 : Code[20];
      OldFile@1100528401 : Text[250];
      NewFile@1100528400 : Text[250];
      Text001@1100528432 : TextConst 'ENU=Do you want to import the Actual Wage List in spite of the Project Setup is "Fixed Rate" (i.o. "Actual Rate")?;SVE=Vill du importera den verkliga l”nelistan trots att projektinst„llnIngen „r "Fast avgift" (i st„llet f”r "Verklig avgift")?';
      Text002@1100528431 : TextConst 'ENU=Import of Actual Wage List aborted;SVE=Import av verklig l”nelista avbruten';
      Text003@1100528414 : TextConst 'ENU=Convert Amounts in Euro to NLG, Do not Convert';
      Text004@1100528413 : TextConst 'ENU=Convert amounts in NLG to EURO, Do not convert';
      Text006@1100528404 : TextConst 'ENU=%1 Project %2;SVE=%1 projekt %2';
      Text007@1100528403 : TextConst 'ENU="Hours ";SVE=Timmar';

    LOCAL PROCEDURE StripFirstZero@1(VAR Code@11012000 : Code[20]);
    BEGIN
      IF StartsWithZero(Code) THEN
        Code := DELSTR(Code, 1, 1);
    END;

    LOCAL PROCEDURE StartsWithZero@1100528400(Code@1100528400 : Code[20]) : Boolean;
    BEGIN
      IF STRLEN(Code) = 0 THEN
        EXIT(FALSE);

      IF COPYSTR(Code, 1, 1) = '0' THEN
        EXIT(TRUE);
    END;

    PROCEDURE CheckCompany@2();
    VAR
      IcRelRec@11012000 : Record 11012057;
      ProjElemRec@11012001 : Record 11012010;
      CompFound@11012002 : Boolean;
    BEGIN
      CompFound := FALSE;
      IF HumanResourcesSetup."Delivering Company (Navision)" <> ReceivingCompany THEN BEGIN
        IF IcRelRec.FIND('-') THEN BEGIN
          REPEAT
            HumanResourcesSetup.CHANGECOMPANY(IcRelRec."Receiving Company");
            HumanResourcesSetup.GET;
            IF HumanResourcesSetup."Delivering Company (Navision)" = ReceivingCompany THEN BEGIN
              CompFound := TRUE;
              IF COMPANYNAME = IcRelRec."Receiving Company" THEN
                ReceivingCompany := ''
              ELSE
                ReceivingCompany := IcRelRec."Receiving Company";
            END;
          UNTIL (IcRelRec.NEXT = 0) OR (CompFound = TRUE);
        END;
      END;

      IF CompFound = FALSE THEN
        ReceivingCompany := '';

      IF StructurePart = HumanResourcesSetup."Structure Part (Payroll)" THEN BEGIN
        IF ReceivingCompany <> '' THEN
          ProjElemRec.CHANGECOMPANY(ReceivingCompany);
        ProjElemRec.SETRANGE("Project No.", project);
        ProjElemRec.SETRANGE(Level, 0);
        IF ProjElemRec.FIND('-') THEN
          StructurePart := ProjElemRec.Element
        ELSE
          StructurePart := '';
        IF ReceivingCompany <> '' THEN
          ProjElemRec.CHANGECOMPANY(COMPANYNAME);
      END;

      HumanResourcesSetup.CHANGECOMPANY(COMPANYNAME);
      HumanResourcesSetup.GET;
    END;

    PROCEDURE WIPAcc@25(VAR JobsSetup@1100525001 : Record 315) : Code[20];
    BEGIN
      WITH PtypRec DO BEGIN
        IF (JobsSetup."Provisions at Closure") AND
           (ProjRec."Project Status" >= ProjRec."Project Status"::Finished) THEN BEGIN
          CASE DimVal2Rec."Cost Type" OF
            DimVal2Rec."Cost Type"::Labor:
              BEGIN
                TESTFIELD("Provision Account Labor");
                EXIT("Provision Account Labor");
              END;
            DimVal2Rec."Cost Type"::Material:
              BEGIN
                TESTFIELD("Provision Account Material");
                EXIT("Provision Account Material");
              END;
            DimVal2Rec."Cost Type"::Subcontracting:
              BEGIN
                TESTFIELD("Provision Account Subcontr.");
                EXIT("Provision Account Subcontr.");
              END;
            DimVal2Rec."Cost Type"::Plant:
              BEGIN
                TESTFIELD("Provision Account Plant");
                EXIT("Provision Account Plant");
              END;
            DimVal2Rec."Cost Type"::Sundry:
              BEGIN
                TESTFIELD("Provision Account Sundry");
                EXIT("Provision Account Sundry");
              END;
          END;
        END ELSE BEGIN
          CASE DimVal2Rec."Cost Type" OF
            DimVal2Rec."Cost Type"::Labor:
              BEGIN
                TESTFIELD("WIP Account Labor");
                EXIT("WIP Account Labor");
              END;
            DimVal2Rec."Cost Type"::Material:
              BEGIN
                TESTFIELD("WIP Account Material");
                EXIT("WIP Account Material");
              END;
            DimVal2Rec."Cost Type"::Subcontracting:
              BEGIN
                TESTFIELD("WIP Account Subcontracting");
                EXIT("WIP Account Subcontracting");
              END;
            DimVal2Rec."Cost Type"::Plant:
              BEGIN
                TESTFIELD("WIP Account Plant");
                EXIT("WIP Account Plant");
              END;
            DimVal2Rec."Cost Type"::Sundry:
              BEGIN
                TESTFIELD("WIP Account Sundry");
                EXIT("WIP Account Sundry");
              END;
          END;
        END;
      END;
    END;

    PROCEDURE DetermineFinishedProjectCode@1210190003();
    BEGIN
      FinishedProject := project;
      ProjRec.TESTFIELD("Project Type");
      PtypRec.GET(ProjRec."Project Type");
      PtypRec.TESTFIELD("Project No.After Closure Costs");
      project := PtypRec."Project No.After Closure Costs";
      ProjRec.GET(project);
    END;

    PROCEDURE FillEmptyTextWithZero@1100525013(VAR Text@1100525000 : Text[1024]);
    BEGIN
      IF DELCHR(Text,'<>=',' ') = '' THEN
        Text := '0';
    END;

    BEGIN
    END.
  }
}

