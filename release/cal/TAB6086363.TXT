OBJECT Table 6086363 EM Attachment Inbox
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.3.00;
  }
  PROPERTIES
  {
    Permissions=TableData 6086320=rimd,
                TableData 6086323=rimd;
    DataCaptionFields=Entry No.,File Name;
    OnInsert=VAR
               EMAttachmentInbox@1160040001 : Record 6086363;
             BEGIN
               EMAttachmentInbox.SETRANGE("Table ID","Table ID");
               EMAttachmentInbox.SETRANGE("Document Type","Document Type");
               EMAttachmentInbox.SETRANGE("Document No.","Document No.");
               EMAttachmentInbox.SETRANGE("Doc. Ref. No.","Doc. Ref. No.");
               IF EMAttachmentInbox.FINDLAST THEN
                 "Entry No." := EMAttachmentInbox."Entry No." + 1
               ELSE
                 "Entry No." := 1;

               "Attachment GUID" := CREATEGUID;
             END;

    OnDelete=VAR
               TempFile@1000 : TEMPORARY Record 6085608;
             BEGIN
               IF "Table ID" = DATABASE::Expense THEN BEGIN
                 GetAttachmentServerFile(TempFile);
                 TempFile.DeleteFile;
               END;
             END;

    CaptionML=[DEU=EM-Anhang-Eingangsfach;
               ENU=EM Attachment Inbox;
               NLD=EM bijlage inbox];
    LookupPageID=Page6086342;
    DrillDownPageID=Page6086342;
  }
  FIELDS
  {
    { 1   ;   ;Table ID            ;Integer       ;CaptionML=[DEU=Tabellen-ID;
                                                              ENU=Table ID;
                                                              NLD=Tabel-id;
                                                              NOR=Tabell-ID;
                                                              SVE=Tabell-ID] }
    { 2   ;   ;Document Type       ;Option        ;CaptionML=[DEU=Belegart;
                                                              ENU=Document Type;
                                                              NLD=Documenttype;
                                                              NOR=Dokumenttype;
                                                              SVE=Dokumenttyp];
                                                   OptionCaptionML=[DEU=Budget,Verrechnung;
                                                                    ENU=Budget,Settlement;
                                                                    NLD=Budget,Afboeking];
                                                   OptionString=Budget,Settlement }
    { 3   ;   ;Document No.        ;Code20        ;CaptionML=[DEU=Belegnr.;
                                                              ENU=Document No.;
                                                              NLD=Documentnr.;
                                                              NOR=Bilagsnr.;
                                                              SVE=Dokumentnr] }
    { 4   ;   ;Doc. Ref. No.       ;Integer       ;TableRelation=IF (Table ID=CONST(6086323)) "Expense Inbox"
                                                                 ELSE IF (Table ID=CONST(6086353)) "Mileage Inbox";
                                                   CaptionML=[DEU=Bel. Referenznr.;
                                                              ENU=Doc. Ref. No.;
                                                              NLD=Doc. Ref. nr.] }
    { 10  ;   ;Entry No.           ;Integer       ;CaptionML=[DEU=Lfd. Nr.;
                                                              ENU=Entry No.;
                                                              NLD=Volgnr.;
                                                              NOR=L›penr.;
                                                              SVE=L”pnr];
                                                   Editable=No }
    { 12  ;   ;Attachment GUID     ;GUID          ;CaptionML=[DEU=Anhang-GUID;
                                                              ENU=Attachment GUID;
                                                              NLD=Bijlage GUID] }
    { 13  ;   ;File Name           ;Text250       ;CaptionML=[DEU=Dateiname;
                                                              ENU=File Name;
                                                              NLD=Bestandsnaam;
                                                              NOR=Filnavn;
                                                              SVE=Filnamn] }
    { 14  ;   ;Attachment          ;BLOB          ;CaptionML=[DEU=Dateianhang;
                                                              ENU=Attachment;
                                                              NLD=Bijlage;
                                                              SVE=Bilaga] }
  }
  KEYS
  {
    {    ;Table ID,Document Type,Document No.,Doc. Ref. No.,Entry No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      EMSetup@1160040002 : Record 6086300;
      ContCompSetup@1160040005 : Record 6192773;
      EMDocFileInterface@1160040003 : Codeunit 6086396;
      GotCompSetup@1160040004 : Boolean;
      GotEMSetup@1160040001 : Boolean;
      FileMove@1160040000 : TextConst 'DEU=Die Datei wurde verschoben und kann nicht mehr visualisiert werden.;ENU=The file was moved and it cannot be visualized anymore.;NLD=Het bestand is verplaatst en kan niet langer ingezien worden.';

    PROCEDURE GetCreatedDate@1160040000() : Date;
    VAR
      ExpenseInbox@1160040002 : Record 6086323;
      MileageInbox@1160040000 : Record 6086353;
    BEGIN
      CASE "Table ID" OF
        DATABASE::"Expense Inbox":
          IF ExpenseInbox.GET("Doc. Ref. No.") THEN
            EXIT(DT2DATE(ExpenseInbox."Imported Date/Time"));
        DATABASE::"Mileage Inbox":
          IF MileageInbox.GET("Doc. Ref. No.") THEN
            EXIT(MileageInbox."Date Created");
      END;

      EXIT(TODAY);
    END;

    PROCEDURE OpenFile@1160040002();
    VAR
      TempFile@1000 : TEMPORARY Record 6085608;
    BEGIN
      IF InboxAccepted THEN
        ERROR(FileMove);

      IF GetAttachmentServerFile(TempFile) THEN
        TempFile.Open;
    END;

    PROCEDURE SetSetup@1160040006(VAR NewEMSetup@1160040000 : Record 6086300;VAR NewContCompSetup@1160040001 : Record 6192773);
    BEGIN
      EMSetup := NewEMSetup;
      GotEMSetup := TRUE;

      ContCompSetup := NewContCompSetup;
      GotCompSetup := TRUE;

      EMDocFileInterface.SetSetup(NewEMSetup,NewContCompSetup);
    END;

    PROCEDURE HasAttachment@13() : Boolean;
    BEGIN
      EXIT(EMDocFileInterface.HasInboxAttachmentFile(Rec));
    END;

    PROCEDURE SetAttachment@6(VAR TempFile@1000 : TEMPORARY Record 6085608) : Boolean;
    BEGIN
      EXIT(EMDocFileInterface.SetInboxAttachmentFile(Rec,TempFile));
    END;

    PROCEDURE GetAttachmentServerFile@10(VAR TempFile@1000 : TEMPORARY Record 6085608) : Boolean;
    BEGIN
      EXIT(EMDocFileInterface.GetInboxAttachFile(Rec,TempFile));
    END;

    PROCEDURE ClearAttachment@14() : Boolean;
    BEGIN
      EXIT(EMDocFileInterface.ClearInboxAttachment(Rec));
    END;

    PROCEDURE InboxAccepted@1() : Boolean;
    VAR
      ExpenseInbox@1001 : Record 6086323;
      MileageInbox@1000 : Record 6086353;
    BEGIN
      CASE "Table ID" OF
        DATABASE::"Expense Inbox":
          BEGIN
            ExpenseInbox.GET("Doc. Ref. No.");
            EXIT(ExpenseInbox.Status = ExpenseInbox.Status::Accepted);
          END;

        DATABASE::"Mileage Inbox":
          BEGIN
            MileageInbox.GET("Doc. Ref. No.");
            EXIT(MileageInbox.Status = MileageInbox.Status::Accepted);
          END;
      END;
    END;

    BEGIN
    END.
  }
}

