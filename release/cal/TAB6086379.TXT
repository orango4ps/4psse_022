OBJECT Table 6086379 EM User Responsibility
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.3.00;
  }
  PROPERTIES
  {
    Permissions=TableData 6086379=rimd;
    CaptionML=[DEU=EM Benutzerverantwortlichkeit;
               ENU=EM User Responsibility;
               NLD=EM gebruikersverantwoordelijkheid];
    LookupPageID=Page6086418;
  }
  FIELDS
  {
    { 1   ;   ;Responsible User ID ;Code50        ;TableRelation="Continia User Setup";
                                                   CaptionML=[DEU=Benutzer-ID verantwortlicher Person;
                                                              ENU=Responsible User ID;
                                                              NLD=User-id verantwoordelijke] }
    { 2   ;   ;User ID sharing responsibility;Code50;
                                                   TableRelation="Continia User Setup";
                                                   CaptionML=[DEU=Benutzer-ID mit geteilter Verantwortlichkeit;
                                                              ENU=User ID sharing responsibility;
                                                              NLD=User-id gedeelde verantwoordelijkheid] }
  }
  KEYS
  {
    {    ;Responsible User ID,User ID sharing responsibility;
                                                   Clustered=Yes }
    {    ;User ID sharing responsibility           }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      UserIDBlank@1160040001 : TextConst 'DEU=Continia-Benutzer-ID darf nicht leer sein.;ENU=Continia User ID must not be blank.;NLD=Continia gebruikers-id mag niet leeg zijn.';
      UserNotSelecteable@1160040000 : TextConst 'DEU=Sie haben nicht die Verantwortlichkeit fÅr %1. Nehmen Sie Kontakt mit Ihrem Systemverwalter auf.;ENU=You do not have responsibility for %1. Please contact your system administrator.;NLD=U heeft niet de verantwoordelijkheid voor %1. Neem contact op met uw systeembeheerder.';

    PROCEDURE GetResponsibilityFilter@1160040000() FilterTxt : Text[1024];
    VAR
      UserResponsibility@1160040000 : Record 6086379;
    BEGIN
      IF NOT IsNAVUser THEN
        EXIT;

      FilterTxt := USERID;

      UserResponsibility.SETRANGE("Responsible User ID",USERID);
      IF UserResponsibility.FINDSET THEN
        REPEAT
          FilterTxt := FilterTxt + '|' + UserResponsibility."User ID sharing responsibility";
        UNTIL UserResponsibility.NEXT = 0;
    END;

    PROCEDURE LookupUser@1160040002(VAR Text@1160040000 : Text[1024]) : Boolean;
    VAR
      ContiniaUserSetup@1160040001 : Record 6086002;
      UserResp@1160040002 : Record 6086379;
    BEGIN
      ContiniaUserSetup.SETFILTER("Continia User ID",UserResp.GetResponsibilityFilter);
      IF ContiniaUserSetup.GET(Text) THEN;
      IF PAGE.RUNMODAL(0,ContiniaUserSetup) = ACTION::LookupOK THEN BEGIN
        Text := ContiniaUserSetup."Continia User ID";
        EXIT(TRUE);
      END;
    END;

    PROCEDURE VerifyUser@1160040001(VAR RespUserID@1160040000 : Code[50]);
    VAR
      ContiniaUserSetup@1160040002 : Record 6086002;
    BEGIN
      IF RespUserID = '' THEN
        ERROR(UserIDBlank);

      IF GetResponsibilityFilter = '' THEN
        EXIT;

      ContiniaUserSetup.SETFILTER("Continia User ID",GetResponsibilityFilter);
      IF ContiniaUserSetup.FINDSET THEN
        REPEAT
          IF ContiniaUserSetup."Continia User ID" = RespUserID THEN
            EXIT;
        UNTIL ContiniaUserSetup.NEXT = 0
      ELSE
        EXIT;

      ERROR(UserNotSelecteable,RespUserID);
    END;

    LOCAL PROCEDURE IsNAVUser@161024028() : Boolean;
    VAR
      EMSetup@161024019 : Record 6086300;
      NAVVersionMgt@6086300 : Codeunit 6086319;
    BEGIN
      IF NOT EMSetup.GET THEN
        EXIT(FALSE);

      IF EMSetup."Limited Role ID" = '' THEN
        EXIT(FALSE);

      EXIT(NAVVersionMgt.HasPermission(EMSetup."Limited Role ID"));
    END;

    BEGIN
    END.
  }
}

