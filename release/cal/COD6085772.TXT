OBJECT Codeunit 6085772 Sales Doc. - Identification
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.5.00;
  }
  PROPERTIES
  {
    TableNo=6085590;
    OnRun=VAR
            Template@1000000000 : Record 6085579;
            Field@1000000001 : Record 6085580;
            Cust@1000000002 : Record 18;
            SourceExcl@161024012 : Record 6085598;
            DocCat@161024015 : Record 6085575;
            CaptureEngine@1000000004 : Codeunit 6085575;
            RecIDMgt@161024014 : Codeunit 6085604;
            CaptureMgt@6085573 : Codeunit 6085576;
            VATRegNo@1000000003 : Code[20];
            Found@161024013 : Boolean;
            RecIDTreeID@161024016 : Integer;
          BEGIN
            // *********************************************************************************************************************************
            // SOURCE NO ALREADY IDENTIFIED. VALIDATING THE NO. WILL MAKE THE SYSTEM INSERT THE DEFAULT TEMPLATE
            // *********************************************************************************************************************************
            IF "Source Record ID Tree ID" <> 0 THEN BEGIN
              VALIDATE("Source Record ID Tree ID","Source Record ID Tree ID");
              MODIFY(TRUE);
              EXIT;
            END;

            // *********************************************************************************************************************************
            // FIND THE IDENTIFICATION TEMPLATE
            // *********************************************************************************************************************************
            Template.RESET;
            Template.SETCURRENTKEY("Category Code",Type);
            Template.SETRANGE("Category Code","Document Category Code");
            Template.SETRANGE(Type,Template.Type::Identification);
            IF NOT Template.FINDFIRST THEN
              EXIT;

            Field.SETRANGE("Template No.",Template."No.");
            IF NOT Field.FINDFIRST THEN
              EXIT;

            DocCat.GET("Document Category Code");

            // *********************************************************************************************************************************
            // CAPTURE THE VAT REG. NO FROM THE DOCUMENT VALUES
            // *********************************************************************************************************************************
            VATRegNo := COPYSTR(CaptureEngine.CaptureField(Rec,0,Field,FALSE),1,MAXSTRLEN(Cust."VAT Registration No."));

            // *********************************************************************************************************************************
            // IF REGISTRATION NO. WAS FOUND THEN TRY TO FIND A CUSTOMER WITH THE NO.
            // *********************************************************************************************************************************
            VATRegNo := CaptureMgt.ReplaceIllegalFilterCharacters(VATRegNo,FALSE,MAXSTRLEN(VATRegNo));
            IF VATRegNo <> '' THEN BEGIN
              Cust.SETCURRENTKEY("VAT Registration No.");
              Cust.SETRANGE("VAT Registration No.",VATRegNo);
              IF Cust.FINDSET THEN
                REPEAT
                  RecIDTreeID := RecIDMgt.GetRecIDTreeID2(
                    DocCat."Source Table No.",Cust.FIELDNO("No."),DocCat."Document Category GUID",Cust."No.");
                  Found := NOT SourceExcl.GET("Document Category Code",RecIDTreeID);
                UNTIL Found OR (Cust.NEXT = 0);

              IF NOT Found THEN BEGIN
                VATRegNo := RemoveLeadingLetters(VATRegNo);
                IF (STRLEN(VATRegNo) > 6) THEN BEGIN
                  Cust.SETRANGE("VAT Registration No.",VATRegNo);

                  IF Cust.FINDSET THEN
                    REPEAT
                      RecIDTreeID := RecIDMgt.GetRecIDTreeID2(
                        DocCat."Source Table No.",Cust.FIELDNO("No."),DocCat."Document Category GUID",Cust."No.");
                      Found := NOT SourceExcl.GET("Document Category Code",RecIDTreeID);
                    UNTIL Found OR (Cust.NEXT = 0);


                  IF NOT Found THEN BEGIN
                    Cust.SETFILTER("VAT Registration No.",'%1','*' + VATRegNo + '*');
                    IF Cust.FINDSET THEN
                      REPEAT
                        RecIDTreeID := RecIDMgt.GetRecIDTreeID2(
                          DocCat."Source Table No.",Cust.FIELDNO("No."),DocCat."Document Category GUID",Cust."No.");
                        Found := NOT SourceExcl.GET("Document Category Code",RecIDTreeID);
                      UNTIL Found OR (Cust.NEXT = 0);
                  END;
                END;
              END;
            END;

            IF NOT Found THEN
              EXIT;

            VALIDATE("Source Record ID Tree ID",RecIDMgt.GetRecIDTreeID2(DATABASE::Customer,Cust.FIELDNO("No."),
              DocCat."Document Category GUID",Cust."No."));
            MODIFY(TRUE);
          END;

  }
  CODE
  {

    PROCEDURE CaptureVATNo@161024012(VAR CaptureFieldVal@161024012 : Record 6085597);
    VAR
      CompInfo@161024013 : Record 79;
    BEGIN
      // *********************************************************************************************************************************
      // THIS FUNCTION IS CALLED DURING IDENTIFICATION. IF THE SYSTEM FINDS OUR OWN VAT REG. NO, IT WILL CONTINUE SEARCHING THE DOCUMENT
      // FOR THE CUSTOMER VAT NO.
      // *********************************************************************************************************************************
      CompInfo.GET;
      WITH CaptureFieldVal DO BEGIN
        // WE ONLY WAN'T TO VALIDATE THE VALUE IF THE VALUE ALREADY CONFORMS TO THE SPECIFIED REGEX-RULES
        IF "File Rule Entry No." = 0 THEN
          EXIT;

        // REMOVE ALL LETTERS FROM THE VAT NO. AND COMPARE ONLY THE NUMBER-SEQUENCE
        "Is Valid" := RemoveLeadingLetters(Value) <> RemoveLeadingLetters(CompInfo."VAT Registration No.")
      END;
    END;

    PROCEDURE RemoveLeadingLetters@1000000000(Text@1000000000 : Text[1024]) : Text[1024];
    BEGIN
      WHILE Text <> '' DO BEGIN
        IF (Text[1] >= 'A') AND (Text[1] <= 'Z') THEN
          Text := COPYSTR(Text,2)
        ELSE
          EXIT(Text);
      END;
      EXIT(Text);
    END;

    BEGIN
    END.
  }
}

