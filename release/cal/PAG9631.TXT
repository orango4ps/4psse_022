OBJECT Page 9631 Page Inspection
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.01;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[ENU=Page Inspection;
               NOR=Sideinspeksjon;
               SVE=Sidinspektion];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table2000000204;
    PageType=List;
    InstructionalTextML=[ENU=See information about the page, its different elements, and the source behind the data it displays.;
                         NOR=Se informasjon om siden, dens ulike elementer og kilden bak dataene som vises.;
                         SVE=Visa information om sidan, dess olika element och k„llan bakom de data som visas.];
    OnOpenPage=BEGIN
                 SetInitialVisibilities;
               END;

    OnAfterGetRecord=BEGIN
                       SetElementsVisibilities;

                       CurrPage.Fields.PAGE.UpdatePage("Current Form ID");
                       CurrPage.Fields.PAGE.SetFieldListVisbility(PageHasSourceTable);

                       CurrPage.Extensions.PAGE.FilterForExtAffectingPage("Page ID","Source Table No.");
                       CurrPage.Extensions.PAGE.SetExtensionListVisbility(NOT PageIsReportRequest AND NOT PageIsReportViewer AND NOT PageIsSystem);

                       CurrPage.Filters.PAGE.UpdatePage("Current Form ID");
                       CurrPage.Filters.PAGE.SetFilterListVisbility(PageHasSourceTable);
                     END;

  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 5   ;1   ;Group     ;
                GroupType=Group }

    { 16  ;2   ;Field     ;
                CaptionML=[ENU=Page;
                           NOR=Side;
                           SVE=Sida];
                ToolTipML=[ENU=Specifies the selected page's name, number, and type.;
                           NOR=Angir den valgte sidens navn, nummer og type.;
                           SVE=Anger den valda sidans namn, nummer och typ.];
                ApplicationArea=#All;
                SourceExpr=PageInfo }

    { 12  ;1   ;Group     ;
                Visible=PageIsReportRequest;
                GroupType=Group }

    { 8   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies that the page is related to a report.;
                           NOR=Angir at siden er relatert til en rapport.;
                           SVE=Anger att sidan „r relaterad till en rapport.];
                ApplicationArea=#All;
                SourceExpr=PageIsReportTextLbl }

    { 20  ;1   ;Group     ;
                Visible=PageIsRoleCenter;
                GroupType=Group }

    { 18  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies that the page has a Role Center type page.;
                           NOR=Angir at siden har en typeside for rollesenter.;
                           SVE=Anger att sidan „r en sida av typen rollcenter.];
                ApplicationArea=#All;
                SourceExpr=PageIsRoleCenterTextLbl }

    { 24  ;1   ;Group     ;
                Visible=IsViewQueryPage;
                GroupType=Group }

    { 23  ;2   ;Field     ;
                Name=QueryInfo;
                CaptionML=[ENU=Query;
                           NOR=Sp›rring;
                           SVE=Fr†ga];
                ToolTipML=[ENU=Specifies the name and number of the inspected query.;
                           NOR=Angir navn og nummer p† kontrollerte sp›rring.;
                           SVE=Anger namn och nummer f”r den inspekterade fr†gan.];
                ApplicationArea=#All;
                SourceExpr=QueryInfo }

    { 10  ;1   ;Group     ;
                Visible=NOT PageIsSystem AND NOT PageIsReportRequest AND NOT PageIsReportViewer AND NOT PageIsRoleCenter AND NOT IsViewQueryPage;
                GroupType=Group }

    { 6   ;2   ;Field     ;
                CaptionML=[ENU=Table;
                           NOR=Tabell;
                           SVE=Tabell];
                ToolTipML=[ENU=Specifies the name and number of the selected page's source table.;
                           NOR=Angir navn og nummer for den valgte sidens kildetabellen.;
                           SVE=Anger namn och nummer p† den valda sidans k„lltabell.];
                ApplicationArea=#All;
                SourceExpr=TableInfo }

    { 27  ;1   ;Group     ;
                Visible=PageIsReportViewer;
                GroupType=Group }

    { 28  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies that the page is related to a report.;
                           NOR=Angir at siden er relatert til en rapport.;
                           SVE=Anger att sidan „r relaterad till en rapport.];
                ApplicationArea=#All;
                SourceExpr=PageIsReportViewerTextLbl }

    { 29  ;1   ;Group     ;
                Visible=PageIsSystem;
                GroupType=Group }

    { 30  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies that the page is a system page.;
                           NOR=Angir at siden er en systemside.;
                           SVE=Anger att sidan „r en systemsida.];
                ApplicationArea=#All;
                SourceExpr=PageIsSystemTextLbl }

    { 11  ;1   ;Group     ;
                Visible=NOT PageIsSystem AND NOT PageIsReportRequest AND NOT PageIsReportViewer AND NOT PageIsRoleCenter AND NOT IsViewTablePage AND PageHasSourceTable AND NOT PageSourceTableIsTemporary AND NOT IsViewQueryPage;
                GroupType=Group }

    { 9   ;2   ;Field     ;
                ExtendedDatatype=URL;
                AccessByPermission=System 5330=X;
                DrillDown=Yes;
                ToolTipML=[ENU=Specifies the URL to view all records and fields of the page's source table in a separate browser window. Requires permission to run tables.;
                           NOR=Angir URL-adressen for † vise alle poster og felter for sidens kildetabell i et eget nettleservindu. Krever tillatelse til † kj›re tabeller.;
                           SVE=Anger URL:en om du vill visa alla poster och f„lt f”r sidans k„lltabell i ett nytt webbl„sarf”nster. Beh”righet att k”ra tabeller kr„vs.];
                ApplicationArea=#All;
                SourceExpr=ViewTableLbl;
                OnDrillDown=BEGIN
                              HYPERLINK(ViewFullTableURL);
                            END;
                             }

    { 7   ;1   ;Group     ;
                Visible=NOT PageIsSystem AND NOT PageIsReportRequest AND NOT PageIsReportViewer AND NOT PageIsRoleCenter AND NOT PageIsSystemPart AND NOT IsViewQueryPage AND PageHasSourceTable AND PageSourceTableIsTemporary;
                GroupType=Group }

    { 4   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies that the table is a temporary table.;
                           NOR=Angir at tabellen er en midlertidig tabell.;
                           SVE=Anger att tabellen „r en tempor„r tabell.];
                ApplicationArea=#All;
                SourceExpr=SourceTableIsTemporaryLbl }

    { 13  ;1   ;Group     ;
                GroupType=GridLayout }

    { 14  ;2   ;Field     ;
                AccessByPermission=System 5330=X;
                DrillDown=Yes;
                ToolTipML=[ENU=Specifies all fields for the current record, including those not shown on the page. The information includes the field's name, number, data type, value, and if it is a primary key (PK).;
                           NOR=Angir alle felter for gjeldende post, inkludert de som ikke vises p† siden. Informasjonen inkluderer feltets navn, nummer, datatype, verdi, og om det er en prim‘rn›kkel (PN).;
                           SVE=Anger alla f„lt f”r den aktuella posten, inklusive dem som inte visas p† sidan. Informationen omfattar f„ltets namn, nummer, datatyp, v„rde och om det „r en prim„rnyckel.];
                ApplicationArea=#All;
                SourceExpr=ShowFieldsTextLbl;
                Style=Strong;
                StyleExpr=ShowFields;
                OnDrillDown=BEGIN
                              ShowFields := TRUE;
                              ShowExtensions := FALSE;
                              ShowFilters := FALSE;

                              // hide message about access in Fields are active tab
                              ShowNoPermissionForExtensions := FALSE;

                              CurrPage.Fields.PAGE.UpdatePage("Current Form ID");
                            END;
                             }

    { 15  ;2   ;Field     ;
                AccessByPermission=System 5330=X;
                DrillDown=Yes;
                ToolTipML=[ENU=Specifies which installed extensions add or modify the page or source table.;
                           NOR=Angir hvilke installerte utvidelser som legges til eller endrer siden eller kildetabellen.;
                           SVE=Anger vilka installerade till„gg som ska l„ggas till eller „ndra sidan eller k„lltabellen.];
                ApplicationArea=#All;
                SourceExpr=ShowExtensionsTextLbl;
                Style=Strong;
                StyleExpr=ShowExtensions;
                OnDrillDown=VAR
                              NavAppObjectMetadata@1000 : Record 2000000150;
                              NavAppInstalledApp@1001 : Record 2000000153;
                            BEGIN
                              ShowExtensions := TRUE;
                              ShowFields := FALSE;
                              ShowFilters := FALSE;

                              IF NavAppObjectMetadata.READPERMISSION AND NavAppInstalledApp.READPERMISSION THEN
                                ShowNoPermissionForExtensions := FALSE
                              ELSE
                                ShowNoPermissionForExtensions := TRUE;

                              CurrPage.Extensions.PAGE.FilterForExtAffectingPage("Page ID","Source Table No.");
                            END;
                             }

    { 17  ;2   ;Field     ;
                AccessByPermission=System 5330=X;
                ToolTipML=[ENU=Specifies the filters that are applied to the page to refine the data.;
                           NOR=Angir filtrene som er brukt p† siden for † begrense dataene.;
                           SVE=Anger filtren som anv„nds p† sidan f”r att finjustera data.];
                ApplicationArea=#All;
                SourceExpr=ShowFiltersTextLbl;
                Style=Strong;
                StyleExpr=ShowFilters;
                OnDrillDown=BEGIN
                              ShowFilters := TRUE;
                              ShowFields := FALSE;
                              ShowExtensions := FALSE;

                              // hide message about access in Fields are active tab
                              ShowNoPermissionForExtensions := FALSE;

                              CurrPage.Filters.PAGE.UpdatePage("Current Form ID");
                            END;
                             }

    { 3   ;1   ;Part      ;
                Name=Fields;
                AccessByPermission=System 5330=X;
                ApplicationArea=#All;
                PagePartID=Page9632;
                Visible=ShowFields;
                PartType=Page }

    { 19  ;1   ;Part      ;
                Name=Extensions;
                AccessByPermission=System 5330=X;
                ApplicationArea=#All;
                PagePartID=Page9633;
                Visible=ShowExtensions;
                Enabled=FALSE;
                Editable=FALSE;
                PartType=Page }

    { 2   ;1   ;Part      ;
                Name=Filters;
                AccessByPermission=System 5330=X;
                ApplicationArea=#All;
                PagePartID=Page9634;
                Visible=ShowFilters;
                PartType=Page }

    { 22  ;1   ;Group     ;
                Visible=ShowNoPermissionForExtensions;
                GroupType=Group }

    { 21  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies that you do not have permission to view the tables that list the extensions affecting this page.;
                           NOR=Angir at du ikke har tillatelse til † vise tabellene som lister opp utvidelsene som p†virker denne siden.;
                           SVE=Anger att du saknar beh”righet att visa tabellerna som anger vilka till„gg som p†verkar den h„r sidan.];
                ApplicationArea=#All;
                SourceExpr=NoViewExtensionsPermissionLbl;
                MultiLine=Yes }

  }
  CODE
  {
    VAR
      PageInfo@1000 : Text;
      TableInfo@1001 : Text;
      QueryInfo@1024 : Text;
      ViewFullTableURL@1002 : Text;
      ViewTableLbl@1003 : TextConst 'ENU=View table;NOR=Vis tabell;SVE=Visa tabell';
      ShowFields@1006 : Boolean;
      ShowExtensions@1007 : Boolean;
      ShowFilters@1008 : Boolean;
      ShowFieldsTextLbl@1014 : TextConst 'ENU=Table Fields;NOR=Tabellfelt;SVE=Tabellf„lt';
      ShowExtensionsTextLbl@1013 : TextConst 'ENU=Extensions;NOR=Utvidelser;SVE=Till„gg';
      ShowFiltersTextLbl@1012 : TextConst 'ENU=Page Filters;NOR=Sidefiltre;SVE=Sidfilter';
      NoViewExtensionsPermissionLbl@1021 : TextConst 'ENU=You do not have permission to view the tables that list the extensions affecting this page.;NOR=Du har ikke tillatelse til † vise tabellene som lister opp utvidelsene som p†virker denne siden.;SVE=Du saknar beh”righet att visa tabellerna som anger vilka till„gg som p†verkar den h„r sidan.';
      NoSourceTableLbl@1004 : TextConst 'ENU=This page does not have a source table.;NOR=Denne siden har ikke en kildetabell.;SVE=Den h„r sidan har inte n†gon k„lltabell.';
      ShowNoPermissionForExtensions@1020 : Boolean;
      PageHasSourceTable@1005 : Boolean;
      PageSourceTableIsTemporary@1009 : Boolean;
      SourceTableIsTemporaryLbl@1010 : TextConst 'ENU=The source table of this page is temporary.;NOR=Kildetabellen for denne siden er midlertidig.;SVE=K„lltabellen p† sidan „r tillf„llig.';
      PageIsSystemPart@1011 : Boolean;
      PageIsReportRequest@1015 : Boolean;
      PageIsReportTextLbl@1016 : TextConst 'ENU=This is a report request page.;NOR=Dette er en rapportforesp›rselsside.;SVE=Det h„r „r en rapportbeg„ransida.';
      PageIsReportViewer@1027 : Boolean;
      PageIsReportViewerTextLbl@1028 : TextConst 'ENU=This is a report viewer page.;NOR=Dette er en side for rapportvisningsprogram.;SVE=Det h„r „r en rapportvisningssida.';
      PageIsRoleCenter@1017 : Boolean;
      PageIsRoleCenterTextLbl@1018 : TextConst 'ENU=This is a role center page.;NOR=Dette er en rollesenterside.;SVE=Det h„r „r en rollcentersida.';
      IsViewTablePage@1019 : Boolean;
      ViewTablePageLbl@1022 : TextConst 'ENU=View Table page.;NOR=Vis tabellside.;SVE=Visa tabellsida.';
      IsViewQueryPage@1023 : Boolean;
      PageIsSystem@1029 : Boolean;
      PageIsSystemTextLbl@1030 : TextConst 'ENU=This is a system page.;NOR=Dette er en systemside.;SVE=Det h„r „r en systemsida.';

    LOCAL PROCEDURE SetInitialVisibilities@3();
    BEGIN
      ShowFields := TRUE;
      ShowExtensions := FALSE;
      ShowFilters := FALSE;
      ShowNoPermissionForExtensions := FALSE;
      PageHasSourceTable := TRUE;
      PageSourceTableIsTemporary := FALSE;

      PageIsSystemPart := FALSE;
      PageIsReportRequest := FALSE;
      PageIsReportViewer := FALSE;
      PageIsRoleCenter := FALSE;
      PageIsSystem := FALSE;
      IsViewTablePage := FALSE;
      IsViewQueryPage := FALSE;
    END;

    LOCAL PROCEDURE SetElementsVisibilities@1();
    VAR
      PageMetadata@1000 : Record 2000000138;
      BaseUrlTxt@1001 : Text;
    BEGIN
      // for some reason that helps to reset range in data provider
      // when going from form without a source table to form with a source table
      SetInitialVisibilities;

      IF "Source Data Type" = 'Query' THEN BEGIN
        IsViewQueryPage := TRUE;
        QueryInfo := STRSUBSTNO('%1 (%2)',"Source Table Name","Source Table No.");
      END;

      PageMetadata.RESET;
      PageMetadata.SETFILTER(ID,'%1',"Page ID");
      IF PageMetadata.FINDFIRST THEN
        PageSourceTableIsTemporary := PageMetadata.SourceTableTemporary;

      IF "Page ID" = 0 THEN
        IF "Page Name" = '' THEN
          PageInfo := STRSUBSTNO('(%1)',"Page Type")
        ELSE
          PageInfo := STRSUBSTNO('%1 (%2)',"Page Name","Page Type")
      ELSE
        PageInfo := STRSUBSTNO('%1 (%2, %3)',"Page Name","Page ID","Page Type");

      IF "Page Name" = ViewTablePageLbl THEN BEGIN
        IsViewTablePage := TRUE;
        PageInfo := STRSUBSTNO('%1 (%2)',"Page Name","Page Type");
      END;

      IF "Source Table No." <= 0 THEN BEGIN
        TableInfo := NoSourceTableLbl;
        PageHasSourceTable := FALSE;
      END ELSE BEGIN
        TableInfo := STRSUBSTNO('%1 (%2)',"Source Table Name","Source Table No.");
        PageHasSourceTable := TRUE;
        BaseUrlTxt := GETURL(CLIENTTYPE::Current,COMPANYNAME);
        IF STRPOS(BaseUrlTxt,'?') = 0 THEN
          ViewFullTableURL := STRSUBSTNO('%1?table=%2',BaseUrlTxt,"Source Table No.")
        ELSE
          ViewFullTableURL := STRSUBSTNO('%1&table=%2',BaseUrlTxt,"Source Table No.");
      END;

      PageIsRoleCenter := ("Current Form ID" = '00000000-0000-0000-0000-000000000001');

      PageIsReportRequest := ("Current Form ID" = '00000000-0000-0000-0000-000000000002');

      PageIsSystemPart := ("Current Form ID" = '00000000-0000-0000-0000-000000000003') OR
        ("Current Form ID" = '00000000-0000-0000-0000-000000000004');

      PageIsReportViewer := ("Current Form ID" = '00000000-0000-0000-0000-000000000005');

      PageIsSystem := ("Current Form ID" = '00000000-0000-0000-0000-000000000006');
    END;

    BEGIN
    END.
  }
}

