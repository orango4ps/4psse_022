OBJECT Table 11125540 Plant Need (SMF)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "Project No." <> '' THEN BEGIN
                 FillDefaultPlantCompany();
                 IF "Plant Company" <> '' THEN
                   VALIDATE("Plant Location", GetProjectDefaultPlantLoc());
               END;
             END;

    CaptionML=[ENU=Plant Need (Site Manager Form);
               NOR=Maskinbehov (Byggelederskjema)];
  }
  FIELDS
  {
    { 10  ;   ;Primary Key         ;Code10        ;CaptionML=[ENU=Primary Key;
                                                              NOR=Prim‘rn›kkel;
                                                              SVE=Prim„rnyckel] }
    { 20  ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr];
                                                   Editable=No }
    { 21  ;   ;Project Description ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Job.Description WHERE (No.=FIELD(Project No.)));
                                                   CaptionML=[ENU=Project Description;
                                                              NOR=Prosjektbeskrivelse;
                                                              SVE=Projektbeskrivning];
                                                   Editable=No }
    { 30  ;   ;Plant Company       ;Text30        ;TableRelation=Company;
                                                   OnValidate=VAR
                                                                PlantMgtComp@1100525001 : Record 11020586;
                                                                TmpPlantMgtComp@1100525002 : TEMPORARY Record 11020586;
                                                              BEGIN
                                                                IF "Plant Company" <> '' THEN BEGIN
                                                                  IF NOT PlantMgtComp.GET("Plant Company") THEN
                                                                    ERROR(Text001, "Plant Company", PlantMgtComp.TABLECAPTION)
                                                                  ELSE BEGIN
                                                                    IF "Project No." <> '' THEN BEGIN
                                                                      PlantMgtComp.GetPlantCompaniesForProject("Project No.", TmpPlantMgtComp, '', FALSE);
                                                                      IF NOT TmpPlantMgtComp.GET("Plant Company") THEN
                                                                        ERROR(Text002, "Project No.", FIELDCAPTION("Plant Company"), "Plant Company");
                                                                    END;
                                                                  END;
                                                                END;

                                                                IF "Plant Company" <> xRec."Plant Company" THEN
                                                                  VALIDATE("Plant Location", GetProjectDefaultPlantLoc());
                                                              END;

                                                   OnLookup=VAR
                                                              PlantCompany@1100409000 : Text[30];
                                                            BEGIN
                                                              PlantCompany := LookupPlantCompany();
                                                              IF (PlantCompany <> '') AND (PlantCompany <> "Plant Company") THEN
                                                                VALIDATE("Plant Company", PlantCompany);
                                                            END;

                                                   CaptionML=[ENU=Plant Company;
                                                              NOR=Maskinselskap;
                                                              SVE=Maskinf”retag];
                                                   NotBlank=Yes }
    { 40  ;   ;Plant Location      ;Code20        ;OnValidate=VAR
                                                                PlantLoc@1100409001 : Record 11012554;
                                                                Company@1100409000 : Text[30];
                                                              BEGIN
                                                                IF "Plant Location" = '' THEN
                                                                  "Plant Location Description" := ''
                                                                ELSE BEGIN
                                                                  TESTFIELD("Project No.");
                                                                  TESTFIELD("Plant Company");
                                                                  PlantLoc.CHANGECOMPANY("Plant Company");
                                                                  PlantLoc.GET("Plant Location");
                                                                  PlantLoc.TESTFIELD(Blocked, FALSE);
                                                                  PlantLoc.TESTFIELD(Depot, FALSE);
                                                                  PlantLoc.TESTFIELD("Project No.", "Project No.");
                                                                  IF "Plant Company" <> COMPANYNAME THEN
                                                                    Company := COMPANYNAME;
                                                                  PlantLoc.TESTFIELD("Company Name", Company);
                                                                  "Plant Location Description" := PlantLoc.Description;
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              PlantLocation@1100409000 : Code[20];
                                                            BEGIN
                                                              PlantLocation := LookupPlantLocation();
                                                              IF (PlantLocation <> '') AND (PlantLocation <> "Plant Location") THEN
                                                                VALIDATE("Plant Location", PlantLocation);
                                                            END;

                                                   CaptionML=[ENU=Plant Location;
                                                              NOR=Maskinplassering;
                                                              SVE=Maskinutplacering] }
    { 41  ;   ;Plant Location Description;Text100 ;CaptionML=[ENU=Plant Location Description;
                                                              NOR=Beskrivelse av maskinplassering;
                                                              SVE=Beskrivning av Maskinsplats];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Primary Key                             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100409002 : TextConst 'ENU="''%1'' is not setup as %2. ";NOR=%1'' er ikke satt opp som %2.;SVE="''%1'' har inte st„llts in som %2. "';
      Text002@1100409001 : TextConst 'ENU=There is no plant location of project ''%1'' present in %2 ''%3''.;NOR=Det finnes ingen maskinlokasjon for prosjekt ''%1'' i %2 ''%3''.;SVE=Det finns ingen Maskinsplats f”r projektet ''%1'' i %2 ''%3''.';

    PROCEDURE FillDefaultPlantCompany@1100525004();
    VAR
      PlantMgtComp@1100525000 : Record 11020586;
      TmpPlantMgtComp@1100525002 : TEMPORARY Record 11020586;
      NoOfPlantComps@1100525001 : Integer;
    BEGIN
      PlantMgtComp."Plant Company" := PlantMgtComp.GetPlantCompany(TRUE, NoOfPlantComps, '');
      IF (PlantMgtComp."Plant Company" <> '') AND (NoOfPlantComps = 1) THEN
        "Plant Company" := PlantMgtComp."Plant Company";

      IF (NoOfPlantComps > 1) AND ("Project No." <> '') THEN BEGIN
        IF PlantMgtComp.GetPlantCompaniesForProject("Project No.", TmpPlantMgtComp, '', FALSE) = 1 THEN
          "Plant Company" := TmpPlantMgtComp."Plant Company";
        IF "Plant Company" = '' THEN BEGIN
          TmpPlantMgtComp.SETRANGE(Default, TRUE);
          IF TmpPlantMgtComp.FINDFIRST THEN
            "Plant Company" := TmpPlantMgtComp."Plant Company";
        END;
      END;
    END;

    PROCEDURE GetProjectDefaultPlantLoc@1100525006() : Code[20];
    VAR
      PlantLoc@1100525001 : Record 11012554;
      DefPlantLoc@1100525002 : Code[20];
      Company@1100525000 : Text[30];
    BEGIN
      //* Default location only when there is only 1 plant location of the project.
      IF ("Project No." = '') OR ("Plant Company" = '') THEN
        EXIT('');
      IF "Plant Company" <> COMPANYNAME THEN
        Company := COMPANYNAME;
      PlantLoc.CHANGECOMPANY("Plant Company");
      PlantLoc.SETCURRENTKEY("Company Name", "Project No.", "Service Order No.", "Employee No.");
      PlantLoc.SETRANGE(Depot, FALSE);
      PlantLoc.SETRANGE("Project No.", "Project No.");
      PlantLoc.SETRANGE("Company Name", Company);
      IF PlantLoc.FINDFIRST THEN BEGIN
        DefPlantLoc := PlantLoc.Code;
        IF PlantLoc.NEXT <> 0 THEN
          DefPlantLoc := '';
      END;
      EXIT(DefPlantLoc);
    END;

    PROCEDURE LookupPlantCompany@1100525005() PlantCompany : Text[30];
    VAR
      PlantMgtComp@1100525001 : Record 11020586;
      TmpPlantMgtComp@1100525002 : TEMPORARY Record 11020586;
      NoOfPlantComps@1100525000 : Integer;
    BEGIN
      PlantCompany := "Plant Company";
      PlantMgtComp.GetPlantCompany(TRUE, NoOfPlantComps, '');
      IF PlantMgtComp.GetPlantCompaniesForProject("Project No.", TmpPlantMgtComp, '', FALSE) = 0 THEN BEGIN
        PlantMgtComp."Plant Company" := "Plant Company";
        PAGE.RUNMODAL(0, PlantMgtComp);
      END ELSE BEGIN
        TmpPlantMgtComp."Plant Company" := "Plant Company";
        IF PAGE.RUNMODAL(0, TmpPlantMgtComp) = ACTION::LookupOK THEN
          PlantCompany := TmpPlantMgtComp."Plant Company";
      END;
    END;

    PROCEDURE LookupPlantLocation@1100525000() PlantLocation : Code[20];
    VAR
      PlantLoc@1100525001 : Record 11012554;
      Company@1100525000 : Text[30];
    BEGIN
      PlantLocation := "Plant Location";
      TESTFIELD("Project No.");
      TESTFIELD("Plant Company");
      IF "Plant Company" <> COMPANYNAME THEN
        Company := COMPANYNAME;
      PlantLoc.CHANGECOMPANY("Plant Company");
      PlantLoc.SETCURRENTKEY("Company Name", "Project No.", "Service Order No.", "Employee No.");
      PlantLoc.FILTERGROUP(9);
      PlantLoc.SETRANGE(Depot, FALSE);
      PlantLoc.SETRANGE("Project No.", "Project No.");
      PlantLoc.SETRANGE("Company Name", Company);
      PlantLoc.FILTERGROUP(0);
      PlantLoc.Code := "Plant Location";
      PlantLoc."Project No." := "Project No.";
      PlantLoc."Company Name" := Company;
      IF PAGE.RUNMODAL(0, PlantLoc) = ACTION::LookupOK THEN
        PlantLocation := PlantLoc.Code;
    END;

    BEGIN
    {
      This table may only be used as temporary!
    }
    END.
  }
}

