OBJECT XMLport 11012394 Import Duncan Budget
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Import Duncan Budget;
               SVE=Import Duncan Budget];
    Direction=Import;
    DefaultFieldsValidation=No;
    PreserveWhiteSpace=Yes;
    TextEncoding=WINDOWS;
    OnInitXMLport=BEGIN
                    ElemLength := 9;
                    LaborLength := 6;
                    MatLength := 5;
                    SubcLength := 5;
                    PlantLength := 5;
                    SunLength := 5;
                    ImportElemLabor := FALSE;
                    ImportElemMat := TRUE;
                    ImportElemSubc := TRUE;
                    ImportElemPlant := TRUE;
                    ImportElemSundry := TRUE;
                  END;

    OnPreXMLport=BEGIN
                   LineCounter := 0;

                   BudgetRec.SETRANGE("Project No.", ProjectCde);
                   BudgetRec.SETRANGE("Extension Contract", ExtensionCde);
                   BudgetRec.SETRANGE(Adjustment,  AdjustmentCde);
                   BudgetRec.SETRANGE(Option, OptionCde);
                   BudgetRec.SETRANGE("Settlement Quantity Code", SettlementQuantityCde);
                   IF BudgetRec.FINDLAST THEN
                     LineCounter := BudgetRec."Line No.";

                   IF (OptionCde = '') OR (ProjectCde <> '') THEN
                     ProjRec.GET(ProjectCde);
                 END;

    OnPostXMLport=VAR
                    lvSumSheetRec@1100525000 : Record 11012064;
                  BEGIN
                    WITH "Budget Line" DO BEGIN
                      IF OptionCde <> '' THEN BEGIN
                        BudgetRec.SETRANGE("Project No.", ProjectCde);
                        BudgetRec.SETRANGE(Adjustment, '');
                        BudgetRec.SETRANGE("Extension Contract", '');
                        BudgetRec.SETRANGE(Option, OptionCde);
                        BudgetRec.SETRANGE("Settlement Quantity Code", '');
                        BudgetRec.SETRANGE("Extension Contract Status", "Extension Contract Status"::Expired);
                        BudgetRec.DELETEALL(TRUE);
                      END;
                        IF SettlementQuantityCde <> '' THEN BEGIN
                        BudgetRec.SETRANGE("Project No.", ProjectCde);
                        BudgetRec.SETRANGE(Adjustment, '');
                        BudgetRec.SETRANGE("Extension Contract", '');
                        BudgetRec.SETRANGE(Option, '');
                        BudgetRec.SETRANGE("Settlement Quantity Code", SettlementQuantityCde);
                        BudgetRec.SETRANGE("Extension Contract Status", "Extension Contract Status"::Expired);
                        BudgetRec.DELETEALL(TRUE);
                      END;
                      lvSumSheetRec."Project No." := ProjectCde;
                      lvSumSheetRec."Contract No." := ExtensionCde;
                      lvSumSheetRec."Plot No." := PlotCde;
                      lvSumSheetRec."House Model" := HouseModelCde;
                      lvSumSheetRec."Main Group" := MainGroupCde;
                      lvSumSheetRec.Group := GroupCde;
                      lvSumSheetRec."Sub Group" := SubGroupCde;
                      lvSumSheetRec.Option := OptionCde;
                      lvSumSheetRec."Settlement Quantity Code" := SettlementQuantityCde;
                      IF OptionCde <> '' THEN BEGIN
                        lvSumSheetRec.ReCalcOptionSummaryLines();
                        lvSumSheetRec.UpdateOptBudgetLines();
                      END ELSE BEGIN
                        IF SettlementQuantityCde <> '' THEN BEGIN
                          lvSumSheetRec.ReCalcSettlQuanSummaryLines();
                          lvSumSheetRec.UpdateSettlQuanBudgLines();
                        END ELSE BEGIN
                          lvSumSheetRec.ReCalcSummaryLines(AdjustmentCde);
                          lvSumSheetRec.UpdateBudgetLines(TRUE, AdjustmentCde);
                        END;
                      END;
                      CheckUpdateOption();
                      CheckUpdateSettlQuantity();
                    END;
                  END;

    Format=Variable Text;
    FieldSeparator=[;];
  }
  ELEMENTS
  {
    { [{FE3DF8A8-CB7D-4FB2-8196-0EFFEBEE40B3}];  ;Root                ;Element ;Text     }

    { [{17092E14-9C83-4161-912B-7E4A83DF70D4}];1 ;BudgetLine          ;Element ;Table   ;
                                                  SourceTable=Table11012001;
                                                  SourceTableView=SORTING(Field1,Field31,Field33,Field38,Field14);
                                                  Import::OnAfterInitRecord=BEGIN
                                                                              "Budget Line".INIT;
                                                                              CLEAR(LineTypeTxt);
                                                                              CLEAR(ElementTxt);
                                                                              CLEAR(CostObjectLaborTxt);
                                                                              CLEAR(CostObjectMaterialTxt);
                                                                              CLEAR(CostObjectSubcontrTxt);
                                                                              CLEAR(CostObjectPlantTxt);
                                                                              CLEAR(CostObjectSundryTxt);
                                                                              CLEAR(QuantityTxt);
                                                                              CLEAR(NormTxt);
                                                                              CLEAR(RateTxt);
                                                                              CLEAR(MaterialPriceTxt);
                                                                              CLEAR(SubcPriceTxt);
                                                                              CLEAR(PlantPriceTxt);
                                                                              CLEAR(SundryPriceTxt);
                                                                              CLEAR(LineTotalTxt);
                                                                              CLEAR(StatusTxt);
                                                                            END;

                                                  Import::OnBeforeInsertRecord=BEGIN
                                                                                 WITH "Budget Line" DO BEGIN
                                                                                   FileLineCounter := FileLineCounter + 1;
                                                                                   IF LineTypeTxt = 'Begroting' THEN BEGIN
                                                                                     LineCounter := LineCounter + 10000;
                                                                                     "Line No." := LineCounter;
                                                                                     "Project No." := ProjectCde;
                                                                                     FillMainProject;
                                                                                     "Extension Contract" := ExtensionCde;
                                                                                     Adjustment := AdjustmentCde;
                                                                                     Option := OptionCde;
                                                                                     "Settlement Quantity Code" := SettlementQuantityCde;
                                                                                     "Time Quantity" := 1;
                                                                                     "Department Code" := ProjRec."Global Dimension 1 Code";

                                                                                     RecRef.GETTABLE("Budget Line");
                                                                                     XMLportStatusDialogMgt.ShowImportStatus(RecRef);

                                                                                     GetVersionDate;
                                                                                     UpdateExtContractStatus;

                                                                                     IF Option <> '' THEN BEGIN
                                                                                       "Extension Contract Status" := "Extension Contract Status"::Expired;
                                                                                       OptionRec.SETRANGE("Project No.","Project No.");
                                                                                       OptionRec.SETRANGE("Plot No.","Plot No.");
                                                                                       OptionRec.SETRANGE("House Model",HouseModelCde);
                                                                                       OptionRec.SETRANGE("Main Group",MainGroupCde);
                                                                                       OptionRec.SETRANGE(Group,GroupCde);
                                                                                       OptionRec.SETRANGE("Sub Group",SubGroupCde);
                                                                                       OptionRec.SETRANGE(Option,Option);
                                                                                       IF OptionRec.FINDFIRST THEN
                                                                                         "Version Date" := OptionRec."Version Date";
                                                                                     END;

                                                                                     IF "Settlement Quantity Code" <> '' THEN BEGIN
                                                                                       "Extension Contract Status" := "Extension Contract Status"::Expired;
                                                                                       SettlementQuantityRec.SETRANGE("Project No.","Project No.");
                                                                                       SettlementQuantityRec.SETRANGE("House Model",HouseModelCde);
                                                                                       SettlementQuantityRec.SETRANGE(Code, "Settlement Quantity Code");
                                                                                       IF SettlementQuantityRec.FINDFIRST THEN
                                                                                         "Version Date" := SettlementQuantityRec."Version Date";
                                                                                     END;

                                                                                     "Project No."  := ProjectCde;
                                                                                     IF CostObjectLaborTxt <> '' THEN BEGIN
                                                                                       TestCostObject(COPYSTR(CostObjectLaborTxt,1,LaborLength));
                                                                                       IF ImportElemLabor THEN
                                                                                         FillElement
                                                                                       ELSE BEGIN
                                                                                         Element := '';
                                                                                         Paragraph := '';
                                                                                         Chapter := '';
                                                                                       END;
                                                                                     END;
                                                                                     IF CostObjectMaterialTxt <> '' THEN BEGIN
                                                                                       TestCostObject(COPYSTR(CostObjectMaterialTxt,1,MatLength));
                                                                                       "Price (LCY)" := MaterialPrice;
                                                                                       IF ImportElemMat THEN
                                                                                         FillElement
                                                                                       ELSE BEGIN
                                                                                         Element := '';
                                                                                         Paragraph := '';
                                                                                         Chapter := '';
                                                                                       END;
                                                                                     END;
                                                                                     IF CostObjectSubcontrTxt <> '' THEN BEGIN
                                                                                       TestCostObject(COPYSTR(CostObjectSubcontrTxt,1,SubcLength));
                                                                                       "Price (LCY)" := SubcPrice;
                                                                                       IF ImportElemSubc THEN
                                                                                         FillElement
                                                                                       ELSE BEGIN
                                                                                         Element := '';
                                                                                         Paragraph := '';
                                                                                         Chapter := '';
                                                                                       END;
                                                                                     END;
                                                                                     IF CostObjectPlantTxt <> '' THEN BEGIN
                                                                                       TestCostObject(COPYSTR(CostObjectPlantTxt,1,PlantLength));
                                                                                       "Price (LCY)" := PlantPrice;
                                                                                       IF ImportElemPlant THEN
                                                                                         FillElement
                                                                                       ELSE BEGIN
                                                                                         Element := '';
                                                                                         Paragraph := '';
                                                                                         Chapter := '';
                                                                                       END;
                                                                                     END;
                                                                                     IF CostObjectSundryTxt <> '' THEN BEGIN
                                                                                       TestCostObject(COPYSTR(CostObjectSundryTxt,1,SunLength));
                                                                                       "Price (LCY)" := SundryPrice;
                                                                                       IF ImportElemSundry THEN
                                                                                         FillElement
                                                                                       ELSE BEGIN
                                                                                         Element := '';
                                                                                         Paragraph := '';
                                                                                         Chapter := '';
                                                                                       END;
                                                                                     END;
                                                                                     CalcTotals;
                                                                                     "Cost Component" := DefaultCostComponent("Cost Object", "Project No.");
                                                                                     FillDefaultFscType();
                                                                                     FillElementPhaseCodeBasedOnElement;
                                                                                     IF (OptionCde = '') AND (SettlementQuantityCde = '') THEN
                                                                                       INSERT
                                                                                     ELSE
                                                                                       AssignOptionBudget;
                                                                                     IF (OptionCde = '') AND (SettlementQuantityCde = '') THEN UpdateCostControlStatus(0);
                                                                                   END ELSE BEGIN
                                                                                     EVALUATE(Element,COPYSTR(ElementTxt,1,ElemLength));
                                                                                     TestElement(Element);

                                                                                     RecRef.GETTABLE("Budget Line");
                                                                                     XMLportStatusDialogMgt.ShowImportStatus(RecRef);
                                                                                   END;
                                                                                   IF "Unit of Measure" <> '' THEN
                                                                                     IF NOT UnitRec.GET("Unit of Measure") THEN
                                                                                       StoreMessage(Description, STRSUBSTNO(Text999, FIELDCAPTION("Unit of Measure"), "Unit of Measure"));
                                                                                 END;
                                                                                 currXMLport.SKIP;
                                                                               END;
                                                                                }

    { [{8A562F1A-92EC-41B2-9EBF-06601D49A1EF}];2 ;LineTypeTxt         ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{03A20FC9-C1B4-4121-84C8-A7C8FEF88B75}];2 ;ElementTxt          ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{860689E0-A4B0-4E93-A57B-30FD46D7E2BE}];2 ;CostObjectLaborTxt  ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{42C4F407-7A49-4D31-B629-652953A25BC4}];2 ;CostObjectMaterialTxt;Element;Text    ;
                                                  MinOccurs=Zero }

    { [{828D7F8B-42F4-4D91-B221-363B8281ABAF}];2 ;CostObjectSubcontrTxt;Element;Text    ;
                                                  MinOccurs=Zero }

    { [{F8327C27-E4A3-4F15-B173-0491964ED815}];2 ;CostObjectPlantTxt  ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{D4669F94-C65D-4D49-862E-44D8EFDC952F}];2 ;CostObjectSundryTxt ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{3EF04790-C08E-4ECF-A676-B029B0170D14}];2 ;Description         ;Element ;Field   ;
                                                  DataType=Text;
                                                  SourceField=Budget Line::Description;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignField=BEGIN
                                                                               IF STRLEN("Budget Line".Description) > 50 THEN
                                                                                 "Budget Line".Description := COPYSTR("Budget Line".Description,1,50);
                                                                             END;
                                                                              }

    { [{74D86113-8AD9-4D2C-A489-5756EF364FB8}];2 ;QuantityTxt         ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  FillEmptyTextWithZero(QuantityTxt);
                                                                                  QuantityTxt := CONVERTSTR(QuantityTxt, '.', ',');
                                                                                  EVALUATE("Budget Line".Quantity, QuantityTxt);
                                                                                END;
                                                                                 }

    { [{30534575-8765-492E-AFA9-5256A53D99C6}];2 ;UnitOfMeasure       ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=Budget Line::Unit of Measure;
                                                  MinOccurs=Zero }

    { [{492AD2D7-3A49-437D-91BA-86F408C667DE}];2 ;NormTxt             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  FillEmptyTextWithZero(NormTxt);
                                                                                  NormTxt := CONVERTSTR(NormTxt, '.', ',');
                                                                                  EVALUATE("Budget Line".Norm, NormTxt);
                                                                                END;
                                                                                 }

    { [{620238A9-39F1-4F07-BD1C-8D19314F932B}];2 ;RateTxt             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  FillEmptyTextWithZero(RateTxt);
                                                                                  RateTxt := CONVERTSTR(RateTxt, '.', ',');
                                                                                  EVALUATE("Budget Line"."Rate (LCY)", RateTxt);
                                                                                END;
                                                                                 }

    { [{C150BE6D-9EC4-4DA1-B1A9-3EEBE3B91FEA}];2 ;MaterialPriceTxt    ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  FillEmptyTextWithZero(MaterialPriceTxt);
                                                                                  MaterialPriceTxt := CONVERTSTR(MaterialPriceTxt, '.', ',');
                                                                                  EVALUATE(MaterialPrice, MaterialPriceTxt);
                                                                                END;
                                                                                 }

    { [{F79E7E36-E0D8-4744-B85A-BE8023203AF3}];2 ;SubcPriceTxt        ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  FillEmptyTextWithZero(SubcPriceTxt);
                                                                                  SubcPriceTxt := CONVERTSTR(SubcPriceTxt, '.', ',');
                                                                                  EVALUATE(SubcPrice, SubcPriceTxt);
                                                                                END;
                                                                                 }

    { [{89D5378C-8B0A-4F54-B8E1-24F657798008}];2 ;PlantPriceTxt       ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  FillEmptyTextWithZero(PlantPriceTxt);
                                                                                  PlantPriceTxt := CONVERTSTR(PlantPriceTxt, '.', ',');
                                                                                  EVALUATE(PlantPrice, PlantPriceTxt);
                                                                                END;
                                                                                 }

    { [{9AABA3AB-86FC-461E-ABE0-BA9C0796A210}];2 ;SundryPriceTxt      ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  FillEmptyTextWithZero(SundryPriceTxt);
                                                                                  SundryPriceTxt := CONVERTSTR(SundryPriceTxt, '.', ',');
                                                                                  EVALUATE(SundryPrice, SundryPriceTxt);
                                                                                END;
                                                                                 }

    { [{AD2EE81B-9841-404A-B698-E95A4115DDA7}];2 ;LineTotalTxt        ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  FillEmptyTextWithZero(LineTotalTxt);
                                                                                  LineTotalTxt := CONVERTSTR(LineTotalTxt, '.', ',');
                                                                                END;
                                                                                 }

    { [{8F627BA6-541E-4EF9-814E-2CBC077AD056}];2 ;StatusTxt           ;Element ;Text    ;
                                                  MinOccurs=Zero }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1100525000;0;Container;
                  ContainerType=ContentArea }

      { 1100525001;1;Group  ;
                  CaptionML=[ENU=Options;
                             SVE=Alternativ] }

      { 1100525002;2;Field  ;
                  CaptionML=[ENU=Element Length;
                             SVE=Elementl„ngd];
                  SourceExpr=ElemLength }

      { 1100525009;2;Field  ;
                  CaptionML=ENU=Import Element Labor;
                  SourceExpr=ImportElemLabor }

      { 1100525003;2;Field  ;
                  CaptionML=[ENU=Labour Length Cost Object;
                             SVE=Arbetskraft l„ngd kostnadsobjekt];
                  SourceExpr=LaborLength }

      { 1100525010;2;Field  ;
                  CaptionML=ENU=Import Element Material;
                  SourceExpr=ImportElemMat }

      { 1100525004;2;Field  ;
                  CaptionML=[ENU=Material Length Cost Object;
                             SVE=Materiall„ngd kostnadsobjekt];
                  SourceExpr=MatLength }

      { 1100525011;2;Field  ;
                  CaptionML=ENU=Import Element Subcontracting;
                  SourceExpr=ImportElemSubc }

      { 1100525005;2;Field  ;
                  CaptionML=[ENU=Subcontracting Length Cost Object;
                             SVE=L„ngd kostnadsobjekt underentreprenad];
                  SourceExpr=SubcLength }

      { 1100525012;2;Field  ;
                  CaptionML=ENU=Import Element Plant;
                  SourceExpr=ImportElemPlant }

      { 1100525006;2;Field  ;
                  CaptionML=[ENU=Plant Length Cost Object;
                             SVE=Maskinl„ngd kostnadsobjekt];
                  SourceExpr=PlantLength }

      { 1100525013;2;Field  ;
                  CaptionML=ENU=Import Element Sundry;
                  SourceExpr=ImportElemSundry }

      { 1100525007;2;Field  ;
                  CaptionML=[ENU=Remaining Length Cost Object;
                             SVE=terst†ende l„ngd kostnadsobjekt];
                  SourceExpr=SunLength }

    }
  }
  CODE
  {
    VAR
      BudgetRec@1100525045 : Record 11012001;
      ProjRec@1100525044 : Record 11072003;
      OptionBudgetRec@1100525043 : Record 11012503;
      OptionRec@1100525042 : Record 11012502;
      DimValRec@1100525041 : Record 349;
      UnitRec@1100525040 : Record 204;
      ProjElemRec@1100525039 : Record 11012010;
      RecRef@1100525028 : RecordRef;
      XMLportStatusDialogMgt@1100525029 : Codeunit 11130006;
      FormatElement@1100525038 : Codeunit 11012022;
      DimMgt@1100525037 : Codeunit 408;
      MaterialPrice@1100525024 : Decimal;
      SubcPrice@1100525025 : Decimal;
      PlantPrice@1100525026 : Decimal;
      SundryPrice@1100525027 : Decimal;
      ElemLength@1100525022 : Integer;
      LaborLength@1100525021 : Integer;
      MatLength@1100525020 : Integer;
      SubcLength@1100525019 : Integer;
      PlantLength@1100525018 : Integer;
      SunLength@1100525017 : Integer;
      ProjectCde@1100525015 : Code[20];
      AdjustmentCde@1100525014 : Code[10];
      ExtensionCde@1100525013 : Code[10];
      HouseModelCde@1100525012 : Code[20];
      MainGroupCde@1100525011 : Code[20];
      GroupCde@1100525010 : Code[20];
      SubGroupCde@1100525009 : Code[20];
      OptionCde@1100525008 : Code[20];
      LineCounter@1100525007 : Integer;
      FileLineCounter@1100525006 : Integer;
      ImportElemLabor@1100525005 : Boolean;
      ImportElemMat@1100525004 : Boolean;
      ImportElemSubc@1100525003 : Boolean;
      ImportElemPlant@1100525002 : Boolean;
      ImportElemSundry@1100525001 : Boolean;
      PlotCde@1100525000 : Code[20];
      Text013@1210190000 : TextConst 'ENU=Cost Object %1 is blocked;SVE=Kostnadsobjekt %1 „r blockerad';
      Text999@1100525023 : TextConst 'ENU=%1 %2 not present;SVE=%1 %2 finns inte';
      SettlementQuantityCde@1100528203 : Code[20];
      FromSettlementQuantity@1100528202 : Boolean;
      SettlementQuantityRec@1100528201 : Record 11072243;
      SettlementQuantityBudgetRec@1100528200 : Record 11072244;

    PROCEDURE DetermineSelection@1(lvProject@11012000 : Code[20];lvAdjustment@11012001 : Code[10];lvExtension@11012002 : Code[10];lvHouseModel@1210190003 : Code[20];lvMainGroup@1210190002 : Code[20];lvGroup@1210190001 : Code[20];lvSubGroup@1210190000 : Code[20];lvOption@11012003 : Code[20];lvPlot@1100485000 : Code[20]);
    BEGIN
      ProjectCde := lvProject;
      AdjustmentCde := lvAdjustment;
      ExtensionCde := lvExtension;
      PlotCde := lvPlot;
      HouseModelCde := lvHouseModel;
      MainGroupCde := lvMainGroup;
      GroupCde := lvGroup;
      SubGroupCde := lvSubGroup;
      OptionCde := lvOption;
    END;

    PROCEDURE TestCostObject@1210190007(lCostObject@1210190000 : Code[10]);
    BEGIN
      DimMgt.GetDimValueRec(2,lCostObject, DimValRec, TRUE,'');
      IF DimValRec.Blocked THEN
        StoreMessage("Budget Line".Description, STRSUBSTNO(Text013,lCostObject));

      "Budget Line"."Cost Object" := DimValRec.Code;
      IF NOT DimValRec.Blocked THEN
        "Budget Line"."Purchase Action" := DimValRec."Purchase Action";
      "Budget Line"."Cost Type" := DimValRec."Cost Type";
    END;

    PROCEDURE TestElement@1210190000(VAR lElemCde@1210190000 : Code[20]);
    VAR
      Chapter@1210190009 : Code[20];
      Paragraph@1210190010 : Code[20];
      Level@1210190011 : Integer;
    BEGIN
      IF lElemCde = '' THEN
        EXIT;

      FormatElement.FormatElemExtended(2,lElemCde,Chapter,Paragraph,Level);

        IF NOT ProjElemRec.GET(ProjectCde,lElemCde) THEN BEGIN
          CreateProjElem(lElemCde, Level, Chapter, Paragraph, "Budget Line".Description);
          FormatElement.CreateProjElem(ProjectCde, lElemCde, Chapter, Paragraph, Level);
        END ELSE
        IF Level > 2 THEN BEGIN
          ProjElemRec.Description := COPYSTR("Budget Line".Description, 1, MAXSTRLEN(ProjElemRec.Description));
          ProjElemRec.MODIFY;
        END;
    END;

    PROCEDURE CreateProjElem@1210190010(lElemCde@1210190000 : Code[20];lLevelInt@1210190001 : Integer;lChapter@1210190002 : Code[20];lParagraph@1210190003 : Code[20];lDescription@1210190004 : Text[100]);
    BEGIN
      IF lElemCde = '' THEN
        EXIT;
      ProjElemRec.INIT;
      ProjElemRec."Project No." := ProjectCde;
      ProjElemRec.Element := lElemCde;
      ProjElemRec.Description := COPYSTR(lDescription, 1, MAXSTRLEN(ProjElemRec.Description));
      ProjElemRec.Chapter := lChapter;
      ProjElemRec.Paragraph := lParagraph;
      ProjElemRec.Level := lLevelInt;
      ProjElemRec.Quantity := "Budget Line".Quantity;
      ProjElemRec."Unit of Measure" := "Budget Line"."Unit of Measure";
      ProjElemRec.INSERT;
    END;

    PROCEDURE FillElement@1210190002();
    BEGIN
      IF ProjElemRec.GET(ProjectCde,COPYSTR(ElementTxt,1,ElemLength)) THEN BEGIN
        "Budget Line".Element := COPYSTR(ElementTxt,1,ElemLength);
        "Budget Line".Paragraph := ProjElemRec.Paragraph;
        "Budget Line".Chapter := ProjElemRec.Chapter;
      END;
    END;

    PROCEDURE AssignOptionBudget@3();
    VAR
      LastLino@11012000 : Integer;
    BEGIN
      BudgetRec.COPY("Budget Line");

      OptionBudgetRec.SETRANGE("Project No.",ProjectCde);
      IF PlotCde <> '' THEN
        OptionBudgetRec.SETRANGE("Plot No.",PlotCde)
      ELSE
        OptionBudgetRec.SETFILTER("Plot No.",'%1','');
      OptionBudgetRec.SETRANGE("House Model",HouseModelCde);
      OptionBudgetRec.SETRANGE("Main Group",MainGroupCde);
      OptionBudgetRec.SETRANGE(Group,GroupCde);
      OptionBudgetRec.SETRANGE("Sub Group",SubGroupCde);
      OptionBudgetRec.SETRANGE(Option,OptionCde);
      IF NOT OptionBudgetRec.FINDLAST THEN
        LastLino := 10000
      ELSE
        LastLino := OptionBudgetRec."Line No." + 10000;

      OptionBudgetRec.INIT;
      OptionBudgetRec."Project No." := BudgetRec."Project No.";
      OptionBudgetRec."Plot No." := PlotCde;
      OptionBudgetRec."House Model" := HouseModelCde;
      OptionBudgetRec."Main Group" := MainGroupCde;
      OptionBudgetRec.Group := GroupCde;
      OptionBudgetRec."Sub Group" := SubGroupCde;
      OptionBudgetRec.Option := BudgetRec.Option;
      OptionBudgetRec.Element := BudgetRec.Element;
      OptionBudgetRec."Cost Object" := BudgetRec."Cost Object";
      OptionBudgetRec."Cost Component" := BudgetRec."Cost Component";
      OptionBudgetRec.Description := BudgetRec.Description;
      OptionBudgetRec.Quantity := BudgetRec.Quantity;
      OptionBudgetRec."Unit of Measure" := BudgetRec."Unit of Measure";
      OptionBudgetRec.Norm := BudgetRec.Norm;
      OptionBudgetRec."Rate Code" := BudgetRec."Rate Code";
      OptionBudgetRec.Rate := BudgetRec."Rate (LCY)";
      OptionBudgetRec.Hours := BudgetRec.Hours;
      OptionBudgetRec.Amount := BudgetRec."Amount (LCY)";
      OptionBudgetRec."Line No." := LastLino;
      OptionBudgetRec."Total Amount" := BudgetRec."Total Amount";
      OptionBudgetRec."Total Hours" := BudgetRec."Total Hours";
      OptionBudgetRec."Cost Type" := BudgetRec."Cost Type";
      OptionBudgetRec.Price := BudgetRec."Price (LCY)";
      OptionBudgetRec."Time Quantity" := BudgetRec."Time Quantity";
      OptionBudgetRec."Unit of Time" := BudgetRec."Unit of Time";
      OptionBudgetRec.Chapter := BudgetRec.Chapter;
      OptionBudgetRec.Paragraph := BudgetRec.Paragraph;
      OptionBudgetRec.VALIDATE(Quantity);

      OptionBudgetRec.CheckInsertSummarySheet(FALSE);  //* Must be done before INSERT, because of test 'only when first'.
      OptionBudgetRec.INSERT;
    END;

    PROCEDURE AssignSettlQuantityBudget@1100528205();
    VAR
      LastLino@11012000 : Integer;
    BEGIN
      BudgetRec.COPY("Budget Line");

      SettlementQuantityBudgetRec.SETRANGE("Project No.",ProjectCde);
      SettlementQuantityBudgetRec.SETRANGE("House Model",HouseModelCde);
      SettlementQuantityBudgetRec.SETRANGE("Settlement Quantity Code", SettlementQuantityCde);
      IF NOT SettlementQuantityBudgetRec.FINDLAST THEN
        LastLino := 10000
      ELSE
        LastLino := SettlementQuantityBudgetRec."Line No." + 10000;

      SettlementQuantityBudgetRec.INIT;
      SettlementQuantityBudgetRec."Project No." := BudgetRec."Project No.";
      SettlementQuantityBudgetRec."Settlement Quantity Code":= BudgetRec."Settlement Quantity Code";
      SettlementQuantityBudgetRec."House Model" := HouseModelCde;
      SettlementQuantityBudgetRec."Line No." := LastLino;
      SettlementQuantityBudgetRec.Element := BudgetRec.Element;
      SettlementQuantityBudgetRec."Cost Type" := BudgetRec."Cost Type";
      SettlementQuantityBudgetRec."Cost Object" := BudgetRec."Cost Object";
      SettlementQuantityBudgetRec.Description := BudgetRec.Description;
      SettlementQuantityBudgetRec."Cost Component" := BudgetRec."Cost Component";
      SettlementQuantityBudgetRec.Quantity := BudgetRec.Quantity;
      SettlementQuantityBudgetRec."Unit of Measure" := BudgetRec."Unit of Measure";
      SettlementQuantityBudgetRec.Norm := BudgetRec.Norm;
      SettlementQuantityBudgetRec."Rate Code" := BudgetRec."Rate Code";
      SettlementQuantityBudgetRec.Rate := BudgetRec."Rate (LCY)";
      SettlementQuantityBudgetRec.Hours := BudgetRec.Hours;
      SettlementQuantityBudgetRec.Amount := BudgetRec."Amount (LCY)";
      SettlementQuantityBudgetRec."Total Amount" := BudgetRec."Total Amount";
      SettlementQuantityBudgetRec."Total Hours" := BudgetRec."Total Hours";
      SettlementQuantityBudgetRec.Price := BudgetRec."Price (LCY)";
      SettlementQuantityBudgetRec."Time Quantity" := BudgetRec."Time Quantity";
      SettlementQuantityBudgetRec."Unit of Time" := BudgetRec."Unit of Time";
      SettlementQuantityBudgetRec.Chapter := BudgetRec.Chapter;
      SettlementQuantityBudgetRec.Paragraph := BudgetRec.Paragraph;

      SettlementQuantityBudgetRec.CheckInsertSummarySheet(FALSE);  //* Must be done before INSERT, because of test 'only when first'.
      SettlementQuantityBudgetRec.INSERT;
    END;

    PROCEDURE StoreMessage@5(lvDesc@11012000 : Text[100];lvMess@11012001 : Text[80]);
    VAR
      ErrorRec@1210190000 : Record 11012051;
      lvError@11012002 : Integer;
    BEGIN
      WITH ErrorRec DO BEGIN
        SETRANGE("User ID", USERID);
        SETRANGE("Source Type", "Source Type"::"Import Budget");
        IF FINDLAST THEN
          lvError := "Line No." + 1
        ELSE
          lvError := 1;

        INIT;
        "User ID" := USERID;
        "Source Type" := "Source Type"::"Import Budget";
        "Project No." := ProjRec."No.";
        "Line No." := lvError;
        "Error message" := lvMess;
        Description := lvDesc;
        INSERT;
      END;
    END;

    PROCEDURE CheckUpdateOption@1100485000();
    VAR
      lvDummyOptionSurchRec@1100485001 : Record 11012512;
      lvBuyerMgtCU@1100485000 : Codeunit 11012500;
    BEGIN
      IF OptionCde = '' THEN
        EXIT;

      lvBuyerMgtCU.BmCalcSurchargesUpdateOption(
        ProjectCde, PlotCde, HouseModelCde, MainGroupCde, GroupCde, SubGroupCde, OptionCde, FALSE, lvDummyOptionSurchRec);
    END;

    PROCEDURE DefaultCostComponent@1100485001(lvCostObject@1100485000 : Code[20];lvProject@1100485001 : Code[20]) lvCostComponent : Code[20];
    VAR
      lvDimValRec@1100485002 : Record 349;
    BEGIN
      lvCostComponent := '';
      IF lvCostObject <> '' THEN BEGIN
        DimMgt.GetDimValueRec(2, lvCostObject, lvDimValRec, FALSE, lvProject);
        IF NOT lvDimValRec.Blocked THEN
          lvCostComponent := lvDimValRec."Cost Component";
      END;
    END;

    LOCAL PROCEDURE FillEmptyTextWithZero@1100525000(VAR Text@1100525000 : Text[1024]);
    BEGIN
      IF DELCHR(Text,'<>=',' ') = '' THEN
        Text := '0';
    END;

    PROCEDURE SetDefaultsSettlQuantity@1100528203(ISettlementQuantityCde@11012000 : Code[20]);
    BEGIN
      IF SettlementQuantityRec.WRITEPERMISSION AND SettlementQuantityBudgetRec.WRITEPERMISSION THEN BEGIN
        FromSettlementQuantity := TRUE;
        SettlementQuantityCde := ISettlementQuantityCde;
      END ELSE BEGIN
        FromSettlementQuantity := FALSE;
        SettlementQuantityCde := '';
      END;
    END;

    PROCEDURE CheckUpdateSettlQuantity@1100528200();
    VAR
      lvDummySettlementQuantitySurcharge@1100485001 : Record 11072247;
      lvSettlementQuantityManagement@1100485000 : Codeunit 11071690;
    BEGIN
      IF SettlementQuantityCde = '' THEN
        EXIT;

      lvSettlementQuantityManagement.MMRCalcSurchargesUpdateSettlQuan(
        ProjectCde, HouseModelCde, SettlementQuantityCde, FALSE,lvDummySettlementQuantitySurcharge)
    END;

    PROCEDURE SetDefaults@1100528602(IElemLength@1100528600 : Integer;IImportElemLabor@1100528601 : Boolean;ILaborLength@1100528602 : Integer;IImportElemMat@1100528603 : Boolean;IMatLength@1100528604 : Integer;IImportElemSubc@1100528605 : Boolean;ISubcLength@1100528606 : Integer;IImportElemPlant@1100528607 : Boolean;IPlantLength@1100528608 : Integer;IImportElemSundry@1100528609 : Boolean;ISunLength@1100528610 : Integer);
    BEGIN
      ElemLength := IElemLength;
      ImportElemLabor := IImportElemLabor;
      LaborLength := ILaborLength;
      ImportElemMat := IImportElemMat;
      MatLength := IMatLength;
      ImportElemSubc := IImportElemSubc;
      SubcLength := ISubcLength;
      ImportElemPlant := IImportElemPlant;
      PlantLength := IPlantLength;
      ImportElemSundry := IImportElemSundry;
      SunLength := ISunLength;
    END;

    BEGIN
    END.
  }
}

