OBJECT Table 5717 Item Cross Reference
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF ("Cross-Reference Type No." <> '') AND
                  ("Cross-Reference Type" = "Cross-Reference Type"::" ")
               THEN
                 ERROR(Text000,FIELDCAPTION("Cross-Reference Type No."));

               Item.GET("Item No.");
               IF "Unit of Measure" = '' THEN
                 VALIDATE("Unit of Measure",Item."Base Unit of Measure");
               CreateItemVendor;
             END;

    OnDelete=BEGIN
               IF "Cross-Reference Type" = "Cross-Reference Type"::Vendor THEN
                 DeleteItemVendor(Rec)
             END;

    OnRename=BEGIN
               IF ("Cross-Reference Type No." <> '') AND
                  ("Cross-Reference Type" = "Cross-Reference Type"::" ")
               THEN
                 ERROR(Text000,FIELDCAPTION("Cross-Reference Type No."));

               IF ("Cross-Reference Type" = "Cross-Reference Type"::Vendor) AND NOT ItemVendorResetRequired(xRec,Rec) THEN
                 UpdateItemVendorNo(xRec,"Cross-Reference No.")
               ELSE BEGIN
                 IF xRec."Cross-Reference Type" = "Cross-Reference Type"::Vendor THEN
                   DeleteItemVendor(xRec);
                 IF "Cross-Reference Type" = "Cross-Reference Type"::Vendor THEN
                   CreateItemVendor;
               END;
             END;

    CaptionML=[ENU=Item Cross Reference;
               NOR=Varekryssreferanse];
    LookupPageID=Page5724;
  }
  FIELDS
  {
    { 1   ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   CaptionML=[ENU=Item No.;
                                                              NOR=Varenr.;
                                                              SVE=Artikelnr] }
    { 2   ;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=[ENU=Variant Code;
                                                              NOR=Variantkode;
                                                              SVE=Variantkod] }
    { 3   ;   ;Unit of Measure     ;Code10        ;TableRelation="Item Unit of Measure".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=[ENU=Unit of Measure;
                                                              NOR=Enhet;
                                                              SVE=M†ttenhet] }
    { 4   ;   ;Cross-Reference Type;Option        ;OnValidate=BEGIN
                                                                IF ("Cross-Reference Type" <> xRec."Cross-Reference Type") AND
                                                                   (xRec."Cross-Reference Type" <> xRec."Cross-Reference Type"::" ") OR
                                                                   ("Cross-Reference Type" = "Cross-Reference Type"::"Bar Code")
                                                                THEN
                                                                  "Cross-Reference Type No." := '';
                                                              END;

                                                   CaptionML=[ENU=Cross-Reference Type;
                                                              NOR=Kryssreferansetype;
                                                              SVE=Tv„rreferens typ];
                                                   OptionCaptionML=[ENU=" ,Customer,Vendor,Bar Code";
                                                                    NOR=" ,Kunde,Leverand›r,Strekkode";
                                                                    SVE=" ,Kund,Leverant”r,Streckkod"];
                                                   OptionString=[ ,Customer,Vendor,Bar Code] }
    { 5   ;   ;Cross-Reference Type No.;Code30    ;TableRelation=IF (Cross-Reference Type=CONST(Customer)) Customer.No.
                                                                 ELSE IF (Cross-Reference Type=CONST(Vendor)) Vendor.No.;
                                                   CaptionML=[ENU=Cross-Reference Type No.;
                                                              NOR=Kryssreferansetypenr.;
                                                              SVE=Tv„rreferens typnr] }
    { 6   ;   ;Cross-Reference No. ;Code20        ;CaptionML=[ENU=Cross-Reference No.;
                                                              NOR=Kryssreferansenr.;
                                                              SVE=Tv„rreferensnr];
                                                   NotBlank=Yes }
    { 7   ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 8   ;   ;Discontinue Bar Code;Boolean       ;OnValidate=BEGIN
                                                                IF "Discontinue Bar Code" AND
                                                                   ("Cross-Reference Type" <> "Cross-Reference Type"::"Bar Code")
                                                                THEN
                                                                  ERROR(Text001,TABLECAPTION);
                                                              END;

                                                   CaptionML=[ENU=Discontinue Bar Code;
                                                              NOR=Strekkode utg†r] }
    { 9   ;   ;Description 2       ;Text50        ;CaptionML=[ENU=Description 2;
                                                              NOR=Beskrivelse 2;
                                                              SVE=Beskrivning 2] }
  }
  KEYS
  {
    {    ;Item No.,Variant Code,Unit of Measure,Cross-Reference Type,Cross-Reference Type No.,Cross-Reference No.;
                                                   Clustered=Yes }
    {    ;Cross-Reference No.                      }
    {    ;Cross-Reference No.,Cross-Reference Type,Cross-Reference Type No.,Discontinue Bar Code }
    {    ;Cross-Reference Type,Cross-Reference No. }
    {    ;Item No.,Variant Code,Unit of Measure,Cross-Reference Type,Cross-Reference No.,Discontinue Bar Code }
    {    ;Cross-Reference Type,Cross-Reference Type No. }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You cannot enter a %1 for a blank Cross-Reference Type.;NOR=Du kan ikke angi %1 for en tom kryssreferansetype.';
      Text001@1001 : TextConst 'ENU=This %1 is not a bar code.;NOR=Denne %1 er ikke en strekkode.';
      Item@1002 : Record 27;
      ItemVend@1003 : Record 99;

    LOCAL PROCEDURE CreateItemVendor@1();
    BEGIN
      IF ("Cross-Reference Type" = "Cross-Reference Type"::Vendor) AND
         ItemVend.WRITEPERMISSION
      THEN BEGIN
        ItemVend.RESET;
        ItemVend.SETRANGE("Item No.","Item No.");
        ItemVend.SETRANGE("Vendor No.","Cross-Reference Type No.");
        ItemVend.SETRANGE("Variant Code","Variant Code");
        IF ItemVend.ISEMPTY THEN BEGIN
          ItemVend."Item No." := "Item No.";
          ItemVend."Vendor No." := "Cross-Reference Type No.";
          ItemVend.VALIDATE("Vendor No.");
          ItemVend."Variant Code" := "Variant Code";
          ItemVend."Vendor Item No." := "Cross-Reference No.";
          ItemVend.INSERT;
          OnAfterCreateItemVendor(Rec,ItemVend);
        END;
      END;
    END;

    LOCAL PROCEDURE DeleteItemVendor@2(ItemCrossReference@1000 : Record 5717);
    BEGIN
      IF NOT MultipleCrossReferencesExist(ItemCrossReference) THEN
        IF ItemVend.GET(ItemCrossReference."Cross-Reference Type No.",ItemCrossReference."Item No.",ItemCrossReference."Variant Code") THEN
          IF UPPERCASE(DELCHR(ItemVend."Vendor Item No.",'<',' ')) = ItemCrossReference."Cross-Reference No." THEN BEGIN
            OnBeforeItemVendorDelete(ItemVend,ItemCrossReference);
            ItemVend.DELETE;
          END;
    END;

    LOCAL PROCEDURE UpdateItemVendorNo@3(ItemCrossReference@1000 : Record 5717;NewCrossRefNo@1002 : Code[20]);
    BEGIN
      IF NOT MultipleCrossReferencesExist(ItemCrossReference) THEN
        IF ItemVend.GET(ItemCrossReference."Cross-Reference Type No.",ItemCrossReference."Item No.",ItemCrossReference."Variant Code") THEN BEGIN
          ItemVend.VALIDATE("Vendor Item No.",NewCrossRefNo);
          ItemVend.MODIFY;
        END;
    END;

    LOCAL PROCEDURE ItemVendorResetRequired@5(OldItemCrossRef@1000 : Record 5717;NewItemCrossRef@1001 : Record 5717) : Boolean;
    BEGIN
      EXIT(
        (OldItemCrossRef."Item No." <> NewItemCrossRef."Item No.") OR
        (OldItemCrossRef."Variant Code" <> NewItemCrossRef."Variant Code") OR
        (OldItemCrossRef."Cross-Reference Type" <> NewItemCrossRef."Cross-Reference Type") OR
        (OldItemCrossRef."Cross-Reference Type No." <> NewItemCrossRef."Cross-Reference Type No."));
    END;

    LOCAL PROCEDURE MultipleCrossReferencesExist@8(ItemCrossReference@1000 : Record 5717) : Boolean;
    VAR
      ItemCrossReference2@1001 : Record 5717;
    BEGIN
      ItemCrossReference2.SETRANGE("Item No.",ItemCrossReference."Item No.");
      ItemCrossReference2.SETRANGE("Variant Code",ItemCrossReference."Variant Code");
      ItemCrossReference2.SETRANGE("Cross-Reference Type",ItemCrossReference."Cross-Reference Type");
      ItemCrossReference2.SETRANGE("Cross-Reference Type No.",ItemCrossReference."Cross-Reference Type No.");
      ItemCrossReference2.SETRANGE("Cross-Reference No.",ItemCrossReference."Cross-Reference No.");
      ItemCrossReference2.SETFILTER("Unit of Measure",'<>%1',ItemCrossReference."Unit of Measure");

      EXIT(NOT ItemCrossReference2.ISEMPTY);
    END;

    [External]
    PROCEDURE GetItemDescription@4(VAR ItemDescription@1006 : Text;VAR ItemDescription2@1007 : Text;ItemNo@1000 : Code[20];VariantCode@1001 : Code[10];UnitOfMeasureCode@1002 : Code[10];CrossRefType@1003 : Option;CrossRefTypeNo@1004 : Code[20]) : Boolean;
    VAR
      ItemCrossReference@1005 : Record 5717;
    BEGIN
      ItemCrossReference.SETRANGE("Item No.",ItemNo);
      ItemCrossReference.SETRANGE("Variant Code",VariantCode);
      ItemCrossReference.SETRANGE("Unit of Measure",UnitOfMeasureCode);
      ItemCrossReference.SETRANGE("Cross-Reference Type",CrossRefType);
      ItemCrossReference.SETRANGE("Cross-Reference Type No.",CrossRefTypeNo);
      IF ItemCrossReference.FINDFIRST THEN BEGIN
        IF (ItemCrossReference.Description = '') AND (ItemCrossReference."Description 2" = '') THEN
          EXIT(FALSE);
        ItemDescription := ItemCrossReference.Description;
        ItemDescription2 := ItemCrossReference."Description 2";
        EXIT(TRUE);
      END;

      EXIT(FALSE);
    END;

    [External]
    PROCEDURE HasValidUnitOfMeasure@38() : Boolean;
    VAR
      ItemUnitOfMeasure@1000 : Record 5404;
    BEGIN
      IF "Unit of Measure" = '' THEN
        EXIT(TRUE);
      ItemUnitOfMeasure.SETRANGE("Item No.","Item No.");
      ItemUnitOfMeasure.SETRANGE(Code,"Unit of Measure");
      EXIT(ItemUnitOfMeasure.FINDFIRST);
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCreateItemVendor@6(VAR ItemCrossReference@1000 : Record 5717;ItemVendor@1001 : Record 99);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeItemVendorDelete@7(ItemVendor@1000 : Record 99;ItemCrossReference@1001 : Record 5717);
    BEGIN
    END;

    BEGIN
    END.
  }
}

