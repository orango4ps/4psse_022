OBJECT Codeunit 6085710 Purch./Sale Doc.-Transl.Templ.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.4.50;
  }
  PROPERTIES
  {
    TableNo=6085579;
    OnRun=VAR
            Field@1000000001 : Record 6085580;
            DataTransl@1000000003 : Record 6085584;
            DocCat@1160040001 : Record 6085575;
            LineTransl@1000000000 : Page 6085701;
            OurContToPurchSalespers@1160040000 : Page 6086059;
            Selection@1000000002 : Integer;
          BEGIN
            Selection := STRMENU(Text001,1);
            IF Selection = 0 THEN
              EXIT;

            CASE Selection OF
              1:
                BEGIN
                  DataTransl.FILTERGROUP := 2;

                  Field.SETRANGE("Template No.",Rec."No.");
                  Field.SETRANGE(Type,Field.Type::Header);
                  Field.SETRANGE("Data Type",Field."Data Type"::Number);
                  Field.SETFILTER("Transfer Amount to Document",'<>%1',Field."Transfer Amount to Document"::" ");
                  IF Field.FINDSET THEN
                    REPEAT
                      DataTransl.SETRANGE("Template No.","No.");
                      DataTransl.SETRANGE(Type,Field.Type);
                      DataTransl.SETRANGE("Field Code",Field.Code);

                      IF DataTransl.ISEMPTY THEN BEGIN
                        DataTransl.INIT;
                        DataTransl."Template No." := "No.";
                        DataTransl.Type := Field.Type;
                        DataTransl."Field Code" := Field.Code;
                        DataTransl."Translate From" := '';
                        DataTransl.CALCFIELDS("Field Description");
                        DataTransl.INSERT;
                      END;
                    UNTIL Field.NEXT = 0;
                  COMMIT;

                  DataTransl.SETRANGE("Field Code");
                  DataTransl.FILTERGROUP := 0;
                  IF DataTransl.FINDFIRST THEN;

                  PAGE.RUNMODAL(PAGE::"Header Amount Posting",DataTransl);

                  DataTransl.SETRANGE("Translate to Type",DataTransl."Translate to Type"::" ");
                  DataTransl.DELETEALL(TRUE);
                END;

              2:
                BEGIN
                  LineTransl.SetTemplate(Rec);
                  Field.GET(Rec."No.",Field.Type::Line,'NO');
                  LineTransl.SetField(Field);
                  LineTransl.RUN;
                END;

              3:
                BEGIN
                  DocCat.GET("Category Code");
                  OurContToPurchSalespers.SetVars(DocCat."Source Table No.",GetSourceID);
                  OurContToPurchSalespers.RUN;
                END;
            END;
          END;

  }
  CODE
  {
    VAR
      Text001@161024012 : TextConst 'DEU=Konten fÅr BetrÑge anzeigen, Zeilenumrechnungen anzeigen, Umrechnungen Abnehmer/VerkÑufer anzeigen;ENU=Show Accounts for Amounts,Show Line Translations,Show Purchaser/Salesperson Translations;NLD=Toon rekeningen voor bedragen,Toon regelvertalingen,Toon Verkoper/Inkoperovertalingen';

    BEGIN
    END.
  }
}

