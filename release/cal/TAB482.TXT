OBJECT Table 482 Reclas. Dimension Set Buffer
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    ReplicateData=No;
    CaptionML=[ENU=Reclas. Dimension Set Buffer;
               NOR=Reklass. dimensjonssettbuffer;
               SVE=Gruppera dimensionsupps„ttningsbuffert];
    LookupPageID=Page479;
    DrillDownPageID=Page479;
  }
  FIELDS
  {
    { 1   ;   ;Dimension Code      ;Code20        ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                IF "Dimension Code" <> xRec."Dimension Code" THEN BEGIN
                                                                  "Dimension Value Code" := '';
                                                                  "Dimension Value ID" := 0;
                                                                  "New Dimension Value Code" := '';
                                                                  "New Dimension Value ID" := 0;
                                                                END;
                                                              END;

                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Dimension Code;
                                                              NOR=Dimensjonskode;
                                                              SVE=Dimensionskod];
                                                   NotBlank=Yes }
    { 2   ;   ;Dimension Value Code;Code20        ;TableRelation="Dimension Value".Code WHERE (Dimension Code=FIELD(Dimension Code));
                                                   OnValidate=BEGIN
                                                                "Dimension Value ID" := GetDimValID("Dimension Code","Dimension Value Code");
                                                              END;

                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Dimension Value Code;
                                                              NOR=Dimensjonsverdikode;
                                                              SVE=Dimensionsv„rdekod] }
    { 3   ;   ;Dimension Value ID  ;Integer       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Dimension Value ID;
                                                              NOR=Dimensjonsverdi-ID;
                                                              SVE=Dimensionsv„rde-ID] }
    { 4   ;   ;New Dimension Value Code;Code20    ;TableRelation="Dimension Value".Code WHERE (Dimension Code=FIELD(Dimension Code));
                                                   OnValidate=BEGIN
                                                                "New Dimension Value ID" := GetDimValID("Dimension Code","New Dimension Value Code");
                                                              END;

                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=New Dimension Value Code;
                                                              NOR=Ny dimensjonsverdikode;
                                                              SVE=Ny dimensionsv„rdekod] }
    { 5   ;   ;New Dimension Value ID;Integer     ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=New Dimension Value ID;
                                                              NOR=Ny dimensjonsverdi-ID;
                                                              SVE=Nytt dimensionsv„rde-ID] }
    { 6   ;   ;Dimension Name      ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Dimension.Name WHERE (Code=FIELD(Dimension Code)));
                                                   CaptionML=[ENU=Dimension Name;
                                                              NOR=Dimensjonsnavn;
                                                              SVE=Dimensionsnamn];
                                                   Editable=No }
    { 7   ;   ;Dimension Value Name;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Dimension Value".Name WHERE (Dimension Code=FIELD(Dimension Code),
                                                                                                    Code=FIELD(Dimension Value Code)));
                                                   CaptionML=[ENU=Dimension Value Name;
                                                              NOR=Navn p† dimensjonsverdi;
                                                              SVE=Dimensionsv„rdesnamn];
                                                   Editable=No }
    { 8   ;   ;New Dimension Value Name;Text50    ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Dimension Value".Name WHERE (Dimension Code=FIELD(Dimension Code),
                                                                                                    Code=FIELD(New Dimension Value Code)));
                                                   CaptionML=[ENU=New Dimension Value Name;
                                                              NOR=Nytt navn p† dimensjonsverdi;
                                                              SVE=Nytt dimensionsv„rdesnamn];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Dimension Code                          ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    [External]
    PROCEDURE GetDimSetID@5(VAR ReclasDimSetBuf@1000 : Record 482) : Integer;
    BEGIN
      EXIT(GetDimSetID2(ReclasDimSetBuf,FALSE));
    END;

    [External]
    PROCEDURE GetNewDimSetID@7(VAR ReclasDimSetBuf@1000 : Record 482) : Integer;
    BEGIN
      EXIT(GetDimSetID2(ReclasDimSetBuf,TRUE));
    END;

    LOCAL PROCEDURE GetDimSetID2@1(VAR ReclasDimSetBuf@1000 : Record 482;NewVal@1003 : Boolean) : Integer;
    VAR
      TempDimSetEntry@1002 : TEMPORARY Record 480;
      DimMgt@1001 : Codeunit 408;
    BEGIN
      ReclasDimSetBuf.RESET;
      ReclasDimSetBuf.SETFILTER("Dimension Code",'<>%1','');
      IF NewVal THEN
        ReclasDimSetBuf.SETFILTER("New Dimension Value Code",'<>%1','')
      ELSE
        ReclasDimSetBuf.SETFILTER("Dimension Value Code",'<>%1','');
      IF NOT ReclasDimSetBuf.FINDSET THEN
        EXIT(0);
      REPEAT
        TempDimSetEntry."Dimension Set ID" := 0;
        TempDimSetEntry."Dimension Code" := ReclasDimSetBuf."Dimension Code";
        IF NewVal THEN BEGIN
          TempDimSetEntry."Dimension Value Code" := ReclasDimSetBuf."New Dimension Value Code";
          TempDimSetEntry."Dimension Value ID" := ReclasDimSetBuf."New Dimension Value ID";
        END ELSE BEGIN
          TempDimSetEntry."Dimension Value Code" := ReclasDimSetBuf."Dimension Value Code";
          TempDimSetEntry."Dimension Value ID" := ReclasDimSetBuf."Dimension Value ID";
        END;
        TempDimSetEntry.INSERT;
      UNTIL ReclasDimSetBuf.NEXT = 0;
      EXIT(DimMgt.GetDimensionSetID(TempDimSetEntry));
    END;

    LOCAL PROCEDURE GetDimValID@2(DimCode@1000 : Code[20];DimValCode@1001 : Code[20]) : Integer;
    VAR
      DimVal@1002 : Record 349;
    BEGIN
      IF DimValCode = '' THEN
        EXIT(0);

      DimVal.GET(DimCode,DimValCode);
      EXIT(DimVal."Dimension Value ID");
    END;

    BEGIN
    END.
  }
}

