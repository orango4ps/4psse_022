OBJECT Report 11012630 Modify Plant Resp. Employee
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=;
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  lvNewEmplComp@1210190000 : Code[80];
                BEGIN
                  IF NewEmplComp = COMPANYNAME THEN
                    lvNewEmplComp := '';

                  IF NewEmplNo = '' THEN BEGIN
                    IF lvNewEmplComp <> '' THEN
                      ERROR(Text001)  //* Leeg alleen toegestaan als ook 'Company' leeg is
                    ELSE BEGIN
                      IF NOT CONFIRM(Text002, FALSE) THEN
                        EXIT;
                    END;
                  END ELSE BEGIN
                    IF NOT CONFIRM(Text003, TRUE, NewEmplNo) THEN
                      EXIT;
                  END;
                END;

  }
  DATASET
  {
    { 5444;    ;DataItem;                    ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number)
                                 WHERE(Number=CONST(1));
               OnAfterGetRecord=BEGIN
                                  HandleSelection;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1210190004;;Container;
                  ContainerType=ContentArea }

      { 1210190003;1;Group  ;
                  CaptionML=[DEU=Neu;
                             ENU=" New";
                             NLD=" Nieuw";
                             NOR=Nytt;
                             SVE=" Nytt"] }

      { 1210190002;2;Field  ;
                  Name=NewEmplComp;
                  CaptionML=[DEU=Mandantenname;
                             ENU=Company Name;
                             NLD=Bedrijfsnaam;
                             NOR=Navn p† selskap;
                             SVE=F”retagsnamn];
                  SourceExpr=NewEmplComp;
                  TableRelation=Company;
                  OnValidate=BEGIN
                               OnValidateNewEmployee(NewEmplNo);

                               IF NewEmplComp = COMPANYNAME THEN
                                 NewEmplComp := '';
                             END;
                              }

      { 1210190001;2;Field  ;
                  Name=NewEmplNo;
                  CaptionML=[DEU=Mitarbeiternr.;
                             ENU=Employee No.;
                             NLD=Werknemer;
                             NOR=Ansattnr;
                             SVE=Anst„llningsnr];
                  SourceExpr=NewEmplNo;
                  OnValidate=BEGIN
                               OnValidateNewEmployee(NewEmplNo);
                             END;

                  OnLookup=BEGIN
                             LookupNewEmployee;
                           END;
                            }

      { 1210190000;2;Field  ;
                  CaptionML=[DEU=Name;
                             ENU=Name;
                             NLD=Naam;
                             NOR=Navn;
                             SVE=Namn];
                  SourceExpr=GetNewEmployeeName() }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      NewEmplNo@1210190001 : Code[20];
      NewEmplComp@1210190000 : Text[30];
      Text001@1210190004 : TextConst 'DEU=Neuer Mitarbeiter muss eingegeben werden.;ENU=New Employee No. must be filled.;NLD=Nieuwe werknemer moet worden ingegeven.;NOR=Det nye Ansattnr. m† fylles ut.;SVE=Det nya anst„llningsnumret m†ste fyllas i.';
      Text002@1210190003 : TextConst 'DEU=Neuer Mitarbeiter wurde nicht eingegeben, Mitarbeiter wird leer gemacht. Sind Sie sich sicher?;ENU=New Employee No. is not filled, Employee NO. will be made empty. Are you sure?;NLD=Nieuwe werknemer is niet gevuld, werknemer zal leeg gemaakt worden. Weet u het zeker?;NOR=Det nye Ansattnr er ikke fylt ut. Ansattnr. gj›res tomt. Er du sikker?;SVE=Det nya anst„llningsnumret har inte fyllts i. Anst„llningsnr kommer att vara tomt. Žr du s„ker?';
      Text003@1210190002 : TextConst 'DEU=Mitarbeiter mit neuer Mitarbeiternr. ''%1'' eingeben?;ENU=Fill Employee No. with new Employee No. ''%1''?;NLD=Werknemer vullen met nieuwe werknemersnr. ''''%1''?;NOR=Vil du fylle ut Ansattnr med nytt Ansattnr ''%1''?;SVE=Vill du fylla i anst„llningsnr med nytt anst„llningsnr ''%1''?';
      TempPlantInvRec@1210190005 : TEMPORARY Record 11012555;

    PROCEDURE OnValidateNewEmployee@1100485000(IEmplNo@1100485001 : Code[20]);
    VAR
      lvEmplRec@1100485000 : Record 5200;
    BEGIN
      IF IEmplNo <> '' THEN BEGIN
        IF NewEmplComp <> '' THEN
          lvEmplRec.CHANGECOMPANY(NewEmplComp);
        lvEmplRec.GET(IEmplNo);
      END;
      NewEmplNo := IEmplNo;
    END;

    PROCEDURE LookupNewEmployee@1100485011();
    VAR
      lvEmplRec@1100485000 : Record 5200;
    BEGIN
      IF NewEmplComp <> '' THEN
        lvEmplRec.CHANGECOMPANY(NewEmplComp);

      lvEmplRec.SETRANGE(Status, lvEmplRec.Status::Active);

      lvEmplRec."No." := NewEmplNo;

      IF PAGE.RUNMODAL(0,lvEmplRec) = ACTION::LookupOK THEN
        OnValidateNewEmployee(lvEmplRec."No.");
    END;

    PROCEDURE GetNewEmployeeName@1100485001() : Text[200];
    VAR
      lvEmplRec@1100485000 : Record 5200;
    BEGIN
      IF NewEmplNo <> '' THEN BEGIN
        IF NewEmplComp <> '' THEN
          lvEmplRec.CHANGECOMPANY(NewEmplComp);
        IF lvEmplRec.GET(NewEmplNo) THEN
          EXIT(lvEmplRec."Full Name");
      END;
      EXIT('');
    END;

    PROCEDURE HandleSelection@1210190000();
    VAR
      lvNewEmplComp@1210190000 : Code[80];
      PlantInvRec@1210190001 : Record 11012555;
    BEGIN
      IF NewEmplComp = COMPANYNAME THEN
        lvNewEmplComp := '';

      IF TempPlantInvRec.FINDSET THEN BEGIN
      REPEAT
        PlantInvRec := TempPlantInvRec;
        PlantInvRec.FIND('=');
        IF (lvNewEmplComp <> PlantInvRec."Employee Company") OR
           (NewEmplNo <> PlantInvRec."Employee No.") THEN BEGIN
          PlantInvRec."Employee Company" := lvNewEmplComp;  //* Eerst beide velden vulden, daarna pas valideren!
          PlantInvRec."Employee No." := NewEmplNo;
          PlantInvRec.VALIDATE("Employee Company");
          PlantInvRec.VALIDATE("Employee No.");
          PlantInvRec.MODIFY(FALSE);  //* FALSE!
        END;
        UNTIL TempPlantInvRec.NEXT =0;
      END;
    END;

    PROCEDURE SetSelection@1210190001(VAR IPlantInvRec@1210190000 : Record 11012555);
    BEGIN
      IF IPlantInvRec.COUNT = 0 THEN
        ERROR('');

      IF IPlantInvRec.FINDSET THEN BEGIN
        REPEAT
          TempPlantInvRec := IPlantInvRec;
          TempPlantInvRec.INSERT;
        UNTIL IPlantInvRec.NEXT =0;
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

