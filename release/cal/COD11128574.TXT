OBJECT Codeunit 11128574 OIOUBL Check Reminder
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVDK9.00 (13604);
  }
  PROPERTIES
  {
    TableNo=295;
    OnRun=BEGIN
            IF "EAN No." <> '' THEN BEGIN
              ReadCompanyInfo;
              ReadGLSetup;

              CompanyInfo.TESTFIELD("VAT Registration No.");
              CompanyInfo.TESTFIELD(Name);
              CompanyInfo.TESTFIELD(Address);
              CompanyInfo.TESTFIELD(City);
              CompanyInfo.TESTFIELD("Post Code");
              CompanyInfo.TESTFIELD("Country/Region Code");
              IF CompanyInfo.IBAN = '' THEN
                CompanyInfo.TESTFIELD("Bank Account No.");
              CompanyInfo.TESTFIELD("Bank Branch No.");
              IF "Payment Channel" = "Payment Channel"::" " THEN
                FIELDERROR("Payment Channel",MusthaveavalueErr);
              IF NOT OIOUBLDocumentEncode.IsValidEANNo("EAN No.") THEN
                FIELDERROR("EAN No.",Text13606);
              OIOUBLDocumentEncode.GetOIOUBLCountryRegionCode("Country/Region Code");
              OIOUBLDocumentEncode.GetOIOUBLCountryRegionCode(CompanyInfo."Country/Region Code");
              OIOUBLDocumentEncode.IsValidCountryCode("Country/Region Code");
              TESTFIELD(Name);
              TESTFIELD(Address);
              TESTFIELD(City);
              TESTFIELD("Post Code");
              TESTFIELD(Contact);
              "Currency Code" := OIOUBLDocumentEncode.GetOIOUBLCurrencyCode("Currency Code");
              CheckReminderLines(Rec);
            END;
          END;

  }
  CODE
  {
    VAR
      CompanyInfo@1101100003 : Record 79;
      GLSetup@1101100004 : Record 98;
      OIOUBLDocumentEncode@1101100002 : Codeunit 11128570;
      GLSetupRead@1101100001 : Boolean;
      CompanyInfoRead@1101100000 : Boolean;
      Text13606@1101100005 : TextConst 'ENU=does not contain a valid, 13-digit EAN no.;NOR=inneholder ikke et gyldig 13-tegns Gtin nr.;SVE=inneh†ller inte ett giltigt 13-teckens EAN nr.';
      EmptyDescriptionErr@13607 : TextConst 'ENU=The Reminder %1 contains lines in which the Type and the No. are specified, but the Description is empty. This is not allowed for an OIOUBL document which might be created from the posted document.;NOR=P†minnelsen %1 inneholder rader der %2 og %3 er spesifisert, men ikke %4. Dette er ikke tillatt i ett OIOXML dokument som kanskje vil opprettes fra det bokf›rte dokumentet.';
      EmptyFieldsQst@13608 : TextConst 'ENU=The Reminder %1 contains lines in which either the Type or the No. is empty. Such lines will not be taken into account when creating an OIOUBL document.\Do you want to continue?;NOR=P†minnelsen %1 inneholder rader hvor enten %2 eller %3 er tomme. V‘r klar over at slike rader ikke regnes med n†r OIOXML dokument opprettes.';
      Text13610@13610 : TextConst 'ENU=The issuing has been interrupted to respect the warning.;NOR=Tilldelingen ble avbrutt pga advarselen.;SVE=Tilldelningen har avbrutits pga varningen.';
      MusthaveavalueErr@1060000 : TextConst 'ENU=must have a value';

    LOCAL PROCEDURE ReadCompanyInfo@1101100003();
    BEGIN
      IF NOT CompanyInfoRead THEN BEGIN
        CompanyInfo.GET;
        CompanyInfoRead := TRUE;
      END;
    END;

    LOCAL PROCEDURE ReadGLSetup@1101100011();
    BEGIN
      IF NOT GLSetupRead THEN BEGIN
        GLSetup.GET;
        GLSetupRead := TRUE;
      END;
    END;

    LOCAL PROCEDURE CheckReminderLines@1060000(ReminderHeader@1060000 : Record 295);
    VAR
      ReminderLine@1060001 : Record 296;
      EmptyLineFound@1060002 : Boolean;
    BEGIN
      EmptyLineFound := FALSE;
      WITH ReminderLine DO BEGIN
        RESET;
        SETRANGE("Reminder No.",ReminderHeader."No.");
        IF FINDSET THEN
          REPEAT
            IF Description = '' THEN
              IF (Type <> Type::" ") AND ("No." <> '') THEN
                ERROR(EmptyDescriptionErr,"Reminder No.");
            IF (Type = Type::" ") OR ("No." = '')
            THEN
              EmptyLineFound := TRUE;
          UNTIL (NEXT = 0);

        IF EmptyLineFound THEN
          IF NOT CONFIRM(EmptyFieldsQst,TRUE,"Reminder No.") THEN
            ERROR(Text13610);
      END;
    END;

    BEGIN
    END.
  }
}

