OBJECT Table 11072662 RFID Plant Order Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=RFID Plant Order Line;
               NOR=RFID maskinordrerad;
               SVE=RFID maskinorderad];
  }
  FIELDS
  {
    { 10  ;   ;Order No.           ;Code20        ;TableRelation="RFID Plant Order";
                                                   CaptionML=[ENU=Order No.;
                                                              NOR=Ordrenr.;
                                                              SVE=Ordernr] }
    { 20  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 30  ;   ;RFID Plant Code     ;Code10        ;CaptionML=[ENU=RFID Plant Code;
                                                              NOR=RFID-maskinkode;
                                                              SVE=RFID maskinkod];
                                                   Editable=No }
    { 40  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF Type <> Type::Item THEN
                                                                  TESTFIELD("Item No.", '')
                                                                ELSE BEGIN
                                                                  TESTFIELD("Plant Type", '');
                                                                  TESTFIELD("Plant No.", '');
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Typ];
                                                   OptionCaptionML=[ENU=Plant,Item;
                                                                    NOR=Maskin,Artikkel;
                                                                    SVE=Maskin,Artikel];
                                                   OptionString=Plant,Item }
    { 50  ;   ;Plant Type          ;Code20        ;TableRelation="Plant Type";
                                                   OnValidate=BEGIN
                                                                IF "Plant Type" <> '' THEN
                                                                  TESTFIELD(Type, Type::Plant);
                                                                CALCFIELDS(Bulk);
                                                                IF "Plant Type" <> xRec."Plant Type" THEN
                                                                  "Plant No.":= '';
                                                              END;

                                                   CaptionML=[ENU=Plant Type;
                                                              NOR=Maskintype;
                                                              SVE=Maskintyp] }
    { 60  ;   ;Plant No.           ;Code10        ;TableRelation="Plant Number".No. WHERE (Plant Type=FIELD(Plant Type));
                                                   OnValidate=BEGIN
                                                                IF "Plant No." <> '' THEN BEGIN
                                                                  TESTFIELD(Type, Type::Plant);
                                                                  TESTFIELD("Plant Type");
                                                                  CALCFIELDS(Bulk);
                                                                  IF Bulk THEN BEGIN
                                                                    PlantInvent.RESET;
                                                                    PlantInvent.SETCURRENTKEY(Removed, "Plant Type", "Plant No.");
                                                                    PlantInvent.SETRANGE(Removed, FALSE);
                                                                    PlantInvent.SETRANGE("Plant Type", "Plant Type");
                                                                    PlantInvent.SETRANGE("Plant No.", "Plant No.");
                                                                    IF PlantInvent.ISEMPTY THEN
                                                                      TESTFIELD("Plant No.", '');
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Plant No.;
                                                              NOR=Maskinnr.;
                                                              SVE=Maskinnr] }
    { 70  ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   OnValidate=BEGIN
                                                                IF "Item No." <> '' THEN
                                                                  TESTFIELD(Type, Type::Item);
                                                              END;

                                                   CaptionML=[ENU=Item No.;
                                                              NOR=Artikkelnr;
                                                              SVE=Artikelnr] }
    { 80  ;   ;Bulk                ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Type".Bulk WHERE (Code=FIELD(Plant Type)));
                                                   CaptionML=[ENU=Bulk;
                                                              NOR=Bulk;
                                                              SVE=Bulk];
                                                   Editable=No }
    { 90  ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                IF (Quantity <> 0) AND (Type = Type::Plant) THEN BEGIN
                                                                  CALCFIELDS(Bulk);
                                                                  IF (NOT Bulk) THEN BEGIN
                                                                    Quantity := ROUND(Quantity, 1);
                                                                    IF "Plant No." <> '' THEN
                                                                      TESTFIELD(Quantity, 1);
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   BlankZero=Yes }
  }
  KEYS
  {
    {    ;Order No.,Line No.                      ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PlantInvent@1210190000 : Record 11012555;

    PROCEDURE GetLineDescription@1210190001() Descr : Text[100];
    VAR
      PlantNumber@1210190001 : Record 11012552;
      Item@1210190000 : Record 27;
    BEGIN
      IF Type <> Type::Item THEN
        PlantNumber.GetPlantDescription("Plant Type", "Plant No.", Descr)
      ELSE BEGIN
        IF Item.GET("Item No.") THEN
          Descr := Item.Description;
      END;
    END;

    BEGIN
    {
      DP00130
    }
    END.
  }
}

