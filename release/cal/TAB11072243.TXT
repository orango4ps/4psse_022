OBJECT Table 11072243 Settlement Quantity
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               JobRec@1100528200 : Record 11072003;
               BudgetSummarySheet@1100528201 : Record 11012064;
             BEGIN
               JobRec.INIT;
               IF "Project No." <> '' THEN BEGIN
                 JobRec.GET("Project No.");
                 JobRec.CheckProjectStatus(0);
                 //"Main Project No." := ProjRec."Main Project";
               END;
                 //"Main Project No." := '';

               IF JobRec."Summary Sheet Option" <> '' THEN BEGIN
                 //TODO Still need to add copy + Default in Job table
                 //BudgetSummarySheet.CopyTemplate2(ProjRec."Summary Sheet Option",
                 //  "Project No.", '', "Plot No.",  "House Model", "Main Group", Group, "Sub Group", Option);
                 BudgetSummarySheet.SETRANGE("Project No.", "Project No.");
                 BudgetSummarySheet.SETRANGE("House Model", "House Model");
                 BudgetSummarySheet.SETFILTER("Contract No.", '%1', '');
                 BudgetSummarySheet.SETFILTER(Option, '%1', '');
                 BudgetSummarySheet.SETRANGE("Settlement Quantity Code", Code);
                 IF BudgetSummarySheet.FIND('-') THEN BEGIN
                   BudgetSummarySheet.ReCalcSettlQuanSummaryLines;
                   BudgetSummarySheet.UpdateSettlQuanBudgLines;
                 END;
               END;
             END;

    OnModify=VAR
               PlotRecordingCrossList@1100527400 : Record 11072253;
             BEGIN
               PlotRecordingCrossList.SETRANGE("Project No.", "Project No.");
               PlotRecordingCrossList.SETRANGE("House Model", "House Model");
               PlotRecordingCrossList.SETRANGE(Type, PlotRecordingCrossList.Type::Settlement);
               PlotRecordingCrossList.SETRANGE(Code, Code);
               IF PlotRecordingCrossList.FINDSET THEN BEGIN
                 REPEAT
                   BEGIN
                     PlotRecordingCrossList.Description := Rec.Description;
                     PlotRecordingCrossList.MODIFY;
                   END;
                 UNTIL PlotRecordingCrossList.NEXT = 0;
               END;
             END;

    OnDelete=VAR
               BudgetSummarySheet@1100528200 : Record 11012064;
               SettlementQuantityBudget@1100528201 : Record 11072244;
               SettlementQuantitySurcharge@1100525000 : Record 11072247;
               PlotRecordingCrossList@1100527400 : Record 11072253;
             BEGIN
               SettlementQuantityBudget.SETRANGE("Project No.", "Project No.");
               SettlementQuantityBudget.SETRANGE("House Model", "House Model");
               SettlementQuantityBudget.SETRANGE("Settlement Quantity Code", Code);
               SettlementQuantityBudget.DELETEALL;

               BudgetSummarySheet.SETRANGE("Project No.", "Project No.");
               BudgetSummarySheet.SETRANGE("House Model", "House Model");
               BudgetSummarySheet.SETRANGE("Settlement Quantity Code", Code);
               BudgetSummarySheet.SETFILTER("Contract No.", '%1', '');
               BudgetSummarySheet.SETFILTER(Option, '%1', '');
               BudgetSummarySheet.DELETEALL;

               SettlementQuantitySurcharge.SETRANGE("Project No.", "Project No.");
               SettlementQuantitySurcharge.SETRANGE("House Model", "House Model");
               SettlementQuantitySurcharge.SETRANGE("Settlement Quantity Code", Code);
               SettlementQuantitySurcharge.DELETEALL;

               PlotRecordingCrossList.SETRANGE("Project No.", "Project No.");
               PlotRecordingCrossList.SETRANGE("House Model", "House Model");
               PlotRecordingCrossList.SETRANGE(Type, PlotRecordingCrossList.Type::Settlement);
               PlotRecordingCrossList.SETRANGE(Code, Code);
               PlotRecordingCrossList.DELETEALL;
             END;

    OnRename=VAR
               BudgetSummarySheet@1100525002 : Record 11012064;
               SettlementQuantityBudget@1100525001 : Record 11072244;
               SettlementQuantitySurcharge@1100525000 : Record 11072247;
               PlotRecordingCrossList@1100525003 : Record 11072253;
             BEGIN
               PlotRecordingCrossList.SETRANGE("Project No.", xRec."Project No.");
               PlotRecordingCrossList.SETRANGE("House Model", xRec."House Model");
               PlotRecordingCrossList.SETRANGE(Type, PlotRecordingCrossList.Type::Settlement);
               PlotRecordingCrossList.SETRANGE(Code, xRec.Code);
               IF NOT PlotRecordingCrossList.ISEMPTY THEN
                 ERROR(Text001, TABLECAPTION, PlotRecordingCrossList.TABLECAPTION);

               SettlementQuantityBudget.SETRANGE("Project No.", xRec."Project No.");
               SettlementQuantityBudget.SETRANGE("House Model", xRec."House Model");
               SettlementQuantityBudget.SETRANGE("Settlement Quantity Code", xRec.Code);
               IF SettlementQuantityBudget.FINDSET(TRUE,TRUE) THEN
                 REPEAT
                   SettlementQuantityBudget.RENAME("Project No.","House Model" , Code, SettlementQuantityBudget."Line No.");
                 UNTIL SettlementQuantityBudget.NEXT = 0;

               BudgetSummarySheet.SETRANGE("Project No.", xRec."Project No.");
               BudgetSummarySheet.SETRANGE("House Model", xRec."House Model");
               BudgetSummarySheet.SETRANGE("Settlement Quantity Code", xRec.Code);
               BudgetSummarySheet.SETFILTER("Contract No.", '%1', '');
               BudgetSummarySheet.SETFILTER(Option, '%1', '');
               IF BudgetSummarySheet.FINDSET(TRUE,TRUE) THEN
                 REPEAT
                   BudgetSummarySheet.RENAME(
                     "Project No.",
                     BudgetSummarySheet."Contract No.",
                     BudgetSummarySheet."Plot No.",
                     "House Model",
                     BudgetSummarySheet."Main Group",
                     BudgetSummarySheet.Group,
                     BudgetSummarySheet."Sub Group",
                     BudgetSummarySheet.Option,
                     Code,
                     BudgetSummarySheet."Row No.");
                 UNTIL BudgetSummarySheet.NEXT = 0;

               SettlementQuantitySurcharge.SETRANGE("Project No.", xRec."Project No.");
               SettlementQuantitySurcharge.SETRANGE("House Model", xRec."House Model");
               SettlementQuantitySurcharge.SETRANGE("Settlement Quantity Code", xRec.Code);
               IF SettlementQuantitySurcharge.FINDSET(TRUE,TRUE) THEN
                 REPEAT
                   SettlementQuantitySurcharge.RENAME("Project No.","House Model" , Code, SettlementQuantitySurcharge."Line No.");
                 UNTIL SettlementQuantitySurcharge.NEXT = 0;
             END;

    CaptionML=ENU=Settlement Quantity;
    LookupPageID=Page11229511;
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation=Job.No.;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr] }
    { 15  ;   ;House Model         ;Code20        ;TableRelation="House Model".Model WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=[ENU=House Model;
                                                              NOR=Husmodell;
                                                              SVE=Husmodell];
                                                   NotBlank=Yes }
    { 20  ;   ;Code                ;Code20        ;AltSearchField=Description;
                                                   OnValidate=BEGIN
                                                                IF STRLEN(Code) > 10 THEN
                                                                  FIELDERROR(Code);
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 30  ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 40  ;   ;Settlement Price    ;Decimal       ;OnValidate=BEGIN
                                                                CalcTotals();
                                                              END;

                                                   CaptionML=[ENU=Settlement Price;
                                                              NOR=Oppgj›rspris;
                                                              SVE=Avr„kningspris];
                                                   AutoFormatType=1 }
    { 50  ;   ;Budget Price        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Settlement Quantity Budget".Amount WHERE (Project No.=FIELD(Project No.),
                                                                                                              House Model=FIELD(House Model),
                                                                                                              Settlement Quantity Code=FIELD(Code)));
                                                   CaptionML=ENU=Budget Price;
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 55  ;   ;Surcharges          ;Decimal       ;FieldClass=FlowField;
                                                   InitValue=0;
                                                   CalcFormula=Sum("Settlement Quantity Surcharge"."Surcharge Amount" WHERE (Project No.=FIELD(Project No.),
                                                                                                                             House Model=FIELD(House Model),
                                                                                                                             Settlement Quantity Code=FIELD(Code)));
                                                   CaptionML=[ENU=Surcharges;
                                                              NOR=Tilleggsgebyrer;
                                                              SVE=Till„ggsavgifter];
                                                   DecimalPlaces=2:2;
                                                   Editable=No }
    { 60  ;   ;Number of Plots     ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Job."Plots Involved" WHERE (No.=FIELD(Project No.)));
                                                   CaptionML=[ENU=Number of Plots;
                                                              NOR=Antall tegninger;
                                                              SVE=Antal Ritningar];
                                                   Editable=No }
    { 70  ;   ;Unit of Measure     ;Code10        ;TableRelation="Unit of Measure".Code;
                                                   CaptionML=[ENU=Unit of Measure;
                                                              NOR=M†leenhet;
                                                              SVE=Enhet] }
    { 80  ;   ;Quantity per Plot   ;Decimal       ;OnValidate=BEGIN
                                                                CalcTotalQuantity(FALSE);
                                                              END;

                                                   CaptionML=ENU=Quantity per Plot }
    { 90  ;   ;Total Quantity      ;Decimal       ;OnValidate=BEGIN
                                                                CalcTotalQuantity(TRUE);
                                                              END;

                                                   CaptionML=[ENU=Total Quantity;
                                                              NOR=Totalt antall;
                                                              SVE=Totalt antal] }
    { 100 ;   ;Forecast %          ;Decimal       ;OnValidate=BEGIN
                                                                CalcForecastQuantity(TRUE, FALSE);
                                                              END;

                                                   CaptionML=ENU=Forecast %;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 105 ;   ;Forecast Quantity   ;Integer       ;OnValidate=BEGIN
                                                                CalcForecastQuantity(FALSE, FALSE);
                                                              END;

                                                   CaptionML=[ENU=Forecast Quantity;
                                                              NOR=Prognoseantall;
                                                              SVE=Prognosantal] }
    { 110 ;   ;Forecast Total Quantity;Decimal    ;OnValidate=BEGIN
                                                                IF ("Forecast Total Quantity" < 0 ) OR  ("Forecast Total Quantity" > "Total Quantity") THEN
                                                                  FIELDERROR("Forecast Total Quantity");

                                                                CalcForecastQuantity(FALSE, TRUE);
                                                              END;

                                                   CaptionML=ENU=Forecast Total Quantity }
    { 120 ;   ;Recorded Quantity   ;Decimal       ;CaptionML=ENU=Recorded Quantity;
                                                   Editable=No }
    { 130 ;   ;Production Quantity ;Decimal       ;CaptionML=[ENU=Production Quantity;
                                                              NOR=Produksjon antall;
                                                              SVE=Produktion antal];
                                                   Editable=No }
    { 140 ;   ;Total Budget        ;Decimal       ;CaptionML=[ENU=Total Budget;
                                                              NOR=Budsjett Total;
                                                              SVE=Total budget];
                                                   Editable=No }
    { 150 ;   ;Version Date        ;Date          ;CaptionML=[ENU=Version Date;
                                                              NOR=Versjonsdato;
                                                              SVE=Versionsdatum] }
    { 270 ;   ;Modified by         ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Modified by;
                                                              NOR=Endret av;
                                                              SVE=Žndrad av];
                                                   Editable=No }
    { 280 ;   ;Last Date Modified  ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              NOR=Oppdateringsdato;
                                                              SVE=Uppdateringsdatum];
                                                   Editable=No }
    { 300 ;   ;Net Price           ;Decimal       ;CaptionML=[ENU=Net Price;
                                                              NOR=Nettopris;
                                                              SVE=Nettopris];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 310 ;   ;Net Amount          ;Decimal       ;CaptionML=[ENU=Net Amount;
                                                              NOR=Nettobel›p;
                                                              SVE=Nettobelopp];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 320 ;   ;Settlement Amount   ;Decimal       ;CaptionML=ENU=Settlement Amount;
                                                   Editable=No }
    { 400 ;   ;Type                ;Code20        ;TableRelation="Settlement Quantity Type";
                                                   CaptionML=[ENU=Type;
                                                              NOR=Type;
                                                              SVE=Typ] }
    { 410 ;   ;Total Quantity 1 House Model;Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plot SQ Recording Line"."Delta Recording Quantity" WHERE (Project No.=FIELD(Project No.),
                                                                                                                                 Phase=FILTER(Cold Recording)));
                                                   CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal] }
    { 420 ;   ;Total Quantity 2 House Mode;Decimal;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plot SQ Recording Line"."Delta Recording Quantity" WHERE (Project No.=FIELD(Project No.),
                                                                                                                                 Phase=FILTER(Warm Recording)));
                                                   CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal] }
    { 430 ;   ;Total Quantity 3House Mode;Decimal ;CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal] }
    { 450 ;   ;Total Quantity 1 Total;Decimal     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plot SQ Recording Line"."Delta Recording Quantity" WHERE (Project No.=FIELD(Project No.),
                                                                                                                                 Phase=FILTER(Cold Recording)));
                                                   CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal] }
    { 460 ;   ;Total Quantity 2 Total;Decimal     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plot SQ Recording Line"."Delta Recording Quantity" WHERE (Project No.=FIELD(Project No.),
                                                                                                                                 Phase=FILTER(Warm Recording)));
                                                   CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal] }
    { 470 ;   ;Total Quantity 3 Total;Decimal     ;CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal] }
  }
  KEYS
  {
    {    ;Project No.,House Model,Code            ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100525000 : TextConst 'ENU=Rename not allowed, %1 is present in %2.';
      Text002@1100527400 : TextConst 'ENU=Delete is not possible, %1 is present in %2.';

    PROCEDURE CalcTotalQuantity@1100528200(FromTotal@1100528200 : Boolean);
    BEGIN
      IF FromTotal THEN BEGIN
        CALCFIELDS("Number of Plots");
        IF "Total Quantity" = 0 THEN BEGIN
          "Quantity per Plot" := 0;
        END ELSE BEGIN
          IF "Number of Plots" <> 0 THEN BEGIN
            "Quantity per Plot" := ROUND("Total Quantity"/"Number of Plots",0.01);
          END ELSE BEGIN
            "Quantity per Plot" := 0;
          END;
        END;
      END ELSE BEGIN
        "Total Quantity" := "Number of Plots" * "Quantity per Plot"
      END;

      CalcForecastQuantity(FALSE, FALSE);
    END;

    PROCEDURE CalcForecastQuantity@1100528201(Percentage@1100528201 : Boolean;FromTotalQuantity@1100528200 : Boolean);
    BEGIN
      // Percentage TRUE --> Call from ForeCast %
      // Percentage FALSE --> Call from Forecast Quantity
      // FromTotalQuantity --> Call from Forecast Total Quantity
      CALCFIELDS("Number of Plots");
      IF FromTotalQuantity THEN BEGIN  //Forecast Total Quantity is not editable anymore!
        IF "Total Quantity" <> 0 THEN BEGIN
          "Forecast %" := ROUND("Forecast Total Quantity"/"Total Quantity"*100,1);
          "Forecast Quantity" := ROUND("Forecast %" * "Number of Plots" / 100,1);
        END ELSE BEGIN
          "Forecast %" := 0;
          "Forecast Quantity" := 0;
        END;
      END ELSE BEGIN
        IF Percentage THEN BEGIN
          "Forecast Quantity" := ROUND("Forecast %" * "Number of Plots" / 100,1);
        END ELSE  BEGIN
          IF "Number of Plots" <> 0 THEN BEGIN
            "Forecast %" := ROUND("Forecast Quantity" / "Number of Plots" * 100,1);
          END ELSE BEGIN
            "Forecast %" := 0;
          END;
        END;
        "Forecast Total Quantity" := "Forecast Quantity" * "Quantity per Plot";
      END;

      CalcTotals;
    END;

    PROCEDURE CalcTotals@1100528202();
    VAR
      SettlementQuantityManagement@1100528200 : Codeunit 11071690;
    BEGIN
      SettlementQuantityManagement.MMRCalcTotals(Rec);
    END;

    PROCEDURE SetModifiedByAndDate@1100528203();
    BEGIN
      "Modified by" := USERID;
      "Last Date Modified" := TODAY;
    END;

    BEGIN
    END.
  }
}

