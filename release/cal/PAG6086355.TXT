OBJECT Page 6086355 EM Approval Entries
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.3.00;
  }
  PROPERTIES
  {
    Permissions=TableData 6086320=r,
                TableData 6086338=r;
    Editable=No;
    CaptionML=[DEU=Genehmigungsposten;
               ENU=Approval Entries;
               NLD=Goedkeuringsposten;
               SVE=Godk„nnandetransaktioner];
    SourceTable=Table6086319;
    DataCaptionExpr="Continia User ID";
    SourceTableView=SORTING(Approver ID,Status)
                    WHERE(Table ID=FILTER(6086320|6086338));
    DataCaptionFields=Document No.,Status,Approver ID;
    PageType=List;
    UsageCategory=Lists;
    SourceTableTemporary=Yes;
    TimerUpdate=1;
    RefreshOnActivate=Yes;
    PromotedActionCategoriesML=[DEU=Neu, Prozess, Bericht, Ausgabe;
                                ENU=New,Process,Report,Document,Approval Entry;
                                NLD=Nieuw,Verwerk,Rapport,Goedkeuringspost];
    OnOpenPage=VAR
                 EmOnlineMgt@1160040000 : Codeunit 6086305;
               BEGIN
                 EmOnlineMgt.IsCompanyActive(FALSE);
                 EnableFields;
                 IF NOT EVALUATE(CurrZoom,DELCHR(CurrentZoomText,'=','%')) THEN;
                 IF CurrZoom = 0 THEN
                   CurrZoom := 40;

                 IF GetIsOutOfOffice THEN
                   MESSAGE(Text003);

                 UpdateList;
               END;

    OnAfterGetRecord=VAR
                       ExpAllocation@1160040001 : Record 6086321;
                       EMAppMgt@1160040002 : Codeunit 6086312;
                     BEGIN
                       CLEAR(ShortcutFieldValues);
                       CASE "Table ID" OF
                         DATABASE::Expense,DATABASE::Mileage:
                           ShortcutFieldFunctions.ShowShortcutApprovalValues(ShortcutFieldValues,"Table ID",0,'',EMAppMgt.Code2Int("Document No."));
                         DATABASE::"Expense Header":
                           ShortcutFieldFunctions.ShowShortcutApprovalValues(
                             ShortcutFieldValues,"Table ID",ExpHeader."Document Type"::Settlement,"Document No.",0);
                       END;

                       IsHeading := FALSE;
                       Indent := 0;
                       CLEAR(HasComments);

                       GetRecordFromApprovalEntry;

                       CASE "Table ID" OF
                         DATABASE::Expense:
                           BEGIN
                             ExpAllocation.RESET;
                             ExpAllocation.SETCURRENTKEY("Expense Entry No.");
                             ExpAllocation.SETRANGE("Expense Entry No.",EMAppMgt.Code2Int("Document No."));
                             IsAllocated := NOT ExpAllocation.ISEMPTY;

                             HasComments := Expense.HasApprovalComment OR Expense.HasExpenseComment;
                           END;

                         DATABASE::Mileage:
                           HasComments := Mileage.HasApprovalComment OR Mileage.HasMileageComment;

                         DATABASE::"Expense Header":
                           HasComments := ExpHeader.HasApprovalComment OR ExpHeader.HasSettlementComment;
                       END;

                       IF "Document Type" = 6 THEN
                         IsHeading := TRUE;

                       IF ("Temp. Entry Type" <> "Temp. Entry Type"::Normal) AND ("Document No." <> '') THEN
                         Indent := 1;
                     END;

    OnNewRecord=BEGIN
                  GetRecordFromApprovalEntry;
                END;

    ActionList=ACTIONS
    {
      { 1900000003;  ;ActionContainer;
                      Name=RelatedInformation;
                      CaptionML=;
                      ActionContainerType=RelatedInformation }
      { 1000000013;1 ;ActionGroup;
                      Name=Show;
                      CaptionML=[DEU=An&zeigen;
                                 ENU=&Show;
                                 NLD=Toon;
                                 SVE=V&isa] }
      { 1160040041;2 ;Action    ;
                      Name=View;
                      ShortCutKey=Return;
                      CaptionML=[DEU=Anzeigen;
                                 ENU=View;
                                 NLD=Weergave];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=View;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 GetRecordFromApprovalEntry;
                                 CASE "Table ID" OF
                                   DATABASE::Expense:
                                     PAGE.RUN(PAGE::"Expense Card",Expense);
                                   DATABASE::Mileage:
                                     PAGE.RUN(PAGE::"Mileage Card",Mileage);
                                   DATABASE::"Expense Header":
                                     PAGE.RUN(PAGE::"Expense Settlement Card",ExpHeader);
                                 END;
                               END;
                                }
      { 1160040033;2 ;Action    ;
                      Name=Comments;
                      CaptionML=[DEU=Bemerkungen;
                                 ENU=Comments;
                                 NLD=Opmerkingen;
                                 SVE=Kommentar];
                      RunObject=Page 6085713;
                      RunPageView=SORTING(Table ID,Document Type,Document No.)
                                  ORDER(Descending);
                      RunPageLink=Table ID=FIELD(Table ID),
                                  Document Type=FIELD(Document Type),
                                  Document No.=FIELD(Document No.);
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ViewComments;
                      PromotedCategory=Process }
      { 106     ;1   ;ActionGroup;
                      Name=Expense;
                      CaptionML=[DEU=Erstattung;
                                 ENU=Expense;
                                 NLD=Uitgave;
                                 SVE=Utl„gg] }
      { 114     ;2   ;Action    ;
                      Name=Dimensions;
                      ShortCutKey=Shift+Ctrl+I;
                      CaptionML=[DEU=Dimensionen;
                                 ENU=Dimensions;
                                 NLD=Dimensies;
                                 SVE=Dimensioner];
                      Image=Dimensions;
                      OnAction=VAR
                                 EMDim@1160040000 : Record 6086360;
                                 ExtraFields@1160040001 : Page 6086356;
                               BEGIN
                                 IF ("Document No." = '') OR ISEMPTY THEN
                                   EXIT;

                                 CASE "Table ID" OF
                                   DATABASE::Expense,DATABASE::Mileage:
                                     BEGIN
                                       EMDim.SETCURRENTKEY("Table ID","Document Type","Document No.","Doc. Ref. No.");
                                       EMDim.SETRANGE("Table ID","Table ID");
                                       EMDim.SETRANGE("Document Type",0);
                                       EMDim.SETRANGE("Document No.",'');
                                       EMDim.SETRANGE("Doc. Ref. No.",EMApprovalMgt.Code2Int("Document No."));
                                       EMDim.SETRANGE("Field Code",'');
                                     END;

                                   DATABASE::"Expense Header":
                                     BEGIN
                                       EMDim.SETCURRENTKEY("Table ID","Document Type","Document No.","Doc. Ref. No.");
                                       EMDim.SETRANGE("Table ID","Table ID");
                                       EMDim.SETRANGE("Document Type",ExpHeader."Document Type"::Settlement);
                                       EMDim.SETRANGE("Document No.","Document No.");
                                       EMDim.SETRANGE("Doc. Ref. No.",0);
                                       EMDim.SETRANGE("Field Code",'');
                                     END;
                                 END;

                                 ExtraFields.SETTABLEVIEW(EMDim);
                                 ExtraFields.SETRECORD(EMDim);
                                 ExtraFields.SetReadOnly;
                                 ExtraFields.RUNMODAL;
                               END;
                                }
      { 1160040002;2 ;Action    ;
                      Name=Extra Fields;
                      CaptionML=[DEU=Zus„tzliche Felder;
                                 ENU=Extra Fields;
                                 NLD=Extra velden];
                      Image=SelectField;
                      OnAction=VAR
                                 EMDim@1160040000 : Record 6086360;
                                 ExtraFields@1160040001 : Page 6086358;
                               BEGIN
                                 IF ("Document No." = '') OR ISEMPTY THEN
                                   EXIT;

                                 CASE "Table ID" OF
                                   DATABASE::Expense,DATABASE::Mileage:
                                     BEGIN
                                       EMDim.SETCURRENTKEY("Table ID","Document Type","Document No.","Doc. Ref. No.");
                                       EMDim.SETRANGE("Table ID","Table ID");
                                       EMDim.SETRANGE("Document Type",0);
                                       EMDim.SETRANGE("Document No.",'');
                                       EMDim.SETRANGE("Doc. Ref. No.",EMApprovalMgt.Code2Int("Document No."));
                                     END;

                                   DATABASE::"Expense Header":
                                     BEGIN
                                       EMDim.SETCURRENTKEY("Table ID","Document Type","Document No.","Doc. Ref. No.");
                                       EMDim.SETRANGE("Table ID","Table ID");
                                       EMDim.SETRANGE("Document Type",ExpHeader."Document Type"::Settlement);
                                       EMDim.SETRANGE("Document No.","Document No.");
                                       EMDim.SETRANGE("Doc. Ref. No.",0);
                                     END;
                                 END;

                                 ExtraFields.SETTABLEVIEW(EMDim);
                                 ExtraFields.SETRECORD(EMDim);
                                 ExtraFields.SetReadOnly;
                                 ExtraFields.RUNMODAL;
                               END;
                                }
      { 161024032;2  ;Action    ;
                      Name=Attachments;
                      CaptionML=[DEU=&Anh„nge;
                                 ENU=&Attachments;
                                 NLD=Bijlagen];
                      Image=Attachments;
                      OnAction=VAR
                                 EMAttachment@1160040000 : Record 6086359;
                               BEGIN
                                 IF ("Document No." = '') OR ISEMPTY THEN
                                   EXIT;

                                 CASE "Table ID" OF
                                   DATABASE::Expense,DATABASE::Mileage:
                                     BEGIN
                                       EMAttachment.SETRANGE("Table ID","Table ID");
                                       EMAttachment.SETRANGE("Document Type",0);
                                       EMAttachment.SETRANGE("Document No.",'');
                                       EMAttachment.SETRANGE("Doc. Ref. No.",EMApprovalMgt.Code2Int("Document No."));

                                       PAGE.RUN(0,EMAttachment);
                                     END;

                                   DATABASE::"Expense Header":
                                     ERROR(UseSettlementCard);
                                 END;
                               END;
                                }
      { 1160040046;2 ;Action    ;
                      Name=Attendees;
                      CaptionML=[DEU=Teilnehmer;
                                 ENU=A&ttendees;
                                 NLD=Deelnemers];
                      Image=Users;
                      OnAction=BEGIN
                                 IF ("Document No." = '') OR ISEMPTY THEN
                                   EXIT;

                                 CASE "Table ID" OF
                                   DATABASE::Expense:
                                     IF Expense.GET(EMApprovalMgt.Code2Int("Document No.")) THEN
                                       Expense.DrillDownAttendees;

                                   DATABASE::Mileage:
                                     ERROR(MileageAttendessErr);

                                   DATABASE::"Expense Header":
                                     ERROR(UseSettlementCard);
                                 END;

                                 UpdateList;
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 1160040029;2 ;Separator  }
      { 1160040028;2 ;Action    ;
                      Name=Allocation;
                      ShortCutKey=Shift+Ctrl+L;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Verteilung;
                                 ENU=Allocation;
                                 NLD=Allocatie;
                                 SVE=F”rdelning];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Split;
                      PromotedCategory=Category4;
                      OnAction=VAR
                                 ExpAllocation@1160040000 : Record 6086321;
                                 ExpSplitAndAllocation@1160040001 : Page 6086320;
                               BEGIN
                                 IF ("Document No." = '') OR ISEMPTY THEN
                                   EXIT;

                                 CASE "Table ID" OF
                                   DATABASE::Expense:
                                     BEGIN
                                       ExpAllocation.SETRANGE("Expense Entry No.",EMApprovalMgt.Code2Int("Document No."));
                                       ExpSplitAndAllocation.SETTABLEVIEW(ExpAllocation);
                                       ExpSplitAndAllocation.RUN;
                                     END;

                                   DATABASE::Mileage:
                                     ERROR(MileageAllocationErr);

                                   DATABASE::"Expense Header":
                                     ERROR(UseSettlementCard);
                                 END;
                               END;
                                }
      { 1160040034;  ;ActionContainer;
                      Name=ActionItems;
                      CaptionML=;
                      ActionContainerType=ActionItems }
      { 1160040037;1 ;Action    ;
                      Name=Approve;
                      CaptionML=[DEU=&Genehmigen;
                                 ENU=&Approve;
                                 NLD=Goedkeuren;
                                 SVE=&Godk„nn];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Approve;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 IF ("Document No." = '') OR ISEMPTY THEN
                                   EXIT;

                                 EMApprovalsBridge.Approve("Table ID","Document No.");

                                 IF MARKEDONLY THEN
                                   MARK(FALSE);

                                 UpdateList;

                                 CurrPage.UPDATE(FALSE);

                                 IF ("Document No." = '') OR ISEMPTY THEN
                                   CurrPage.CaptureUI.PAGE.ClearImage;
                               END;
                                }
      { 1160040038;1 ;Action    ;
                      Name=Reject;
                      CaptionML=[DEU=Abl&ehnen;
                                 ENU=&Reject;
                                 NLD=Weigeren;
                                 SVE=A&vvisa];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Reject;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 IF ("Document No." = '') OR ISEMPTY THEN
                                   EXIT;

                                 EMApprovalsBridge.Reject("Table ID","Document No.");

                                 IF MARKEDONLY THEN
                                   MARK(FALSE);

                                 UpdateList;

                                 CurrPage.UPDATE(FALSE);

                                 IF ("Document No." = '') OR ISEMPTY THEN
                                   CurrPage.CaptureUI.PAGE.ClearImage;
                               END;
                                }
      { 1160040035;1 ;Action    ;
                      Name=Forward;
                      CaptionML=[DEU=Versenden;
                                 ENU=&Forward;
                                 NLD=Doorsturen;
                                 SVE=&Vidarebefordra];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Delegate;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 IF ("Document No." = '') OR ISEMPTY THEN
                                   EXIT;

                                 EMApprovalsBridge.Forward("Table ID","Document No.");

                                 IF MARKEDONLY THEN
                                   MARK(FALSE);

                                 UpdateList;

                                 CurrPage.UPDATE(FALSE);

                                 IF ("Document No." = '') OR ISEMPTY THEN
                                   CurrPage.CaptureUI.PAGE.ClearImage;
                               END;
                                }
      { 1160040036;1 ;Action    ;
                      Name=On Hold;
                      CaptionML=[DEU=&Gesperrt;
                                 ENU=&On Hold;
                                 NLD=Wachten;
                                 SVE=S&toppad];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Lock;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 SetOnHold@1160040001 : Boolean;
                               BEGIN
                                 IF ("Document No." = '') OR ISEMPTY THEN
                                   EXIT;

                                 GetRecordFromApprovalEntry;
                                 CASE "Table ID" OF
                                   DATABASE::Expense:
                                     SetOnHold := Expense."On Hold" = '';
                                   DATABASE::Mileage:
                                     SetOnHold := Mileage."On Hold" = '';
                                   DATABASE::"Expense Header":
                                     SetOnHold := ExpHeader."On Hold" = '';
                                 END;

                                 IF SetOnHold THEN
                                   EMApprovalMgt.PutOnHold("Table ID","Document No.")
                                 ELSE
                                   EMApprovalMgt.RemoveOnHold("Table ID","Document No.");

                                 IF MARKEDONLY THEN
                                   MARK(FALSE);

                                 UpdateList;

                                 CurrPage.UPDATE(FALSE);

                                 IF ("Document No." = '') OR ISEMPTY THEN
                                   CurrPage.CaptureUI.PAGE.ClearImage;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;;Container;
                Name=ContentArea;
                ContainerType=ContentArea }

    { 1160040001;1;Group  ;
                Editable=false;
                IndentationColumnName=Indent;
                IndentationControls=Description;
                GroupType=Repeater }

    { 1160040045;2;Field  ;
                Name=HasComments;
                CaptionML=[DEU=Bemerkung;
                           ENU=Comment;
                           NLD=Opmerking;
                           SVE=Kommentar];
                SourceExpr=HasComments;
                Editable=FALSE }

    { 1160040044;2;Field  ;
                Name=Approval Entry Type;
                CaptionML=[DEU=Genehmigungspostenart;
                           ENU=Approval Entry Type;
                           NLD=Goedkeuringsposttype];
                SourceExpr="Approval Entry Type" }

    { 1160040005;2;Field  ;
                Name=Continia User ID;
                SourceExpr="Continia User ID";
                CaptionClass=Expense.FIELDCAPTION("Continia User ID") }

    { 1160040019;2;Field  ;
                Name=Continia User Name;
                SourceExpr="Continia User Name";
                CaptionClass=Expense.FIELDCAPTION("Continia User Name") }

    { 1160040039;2;Field  ;
                Name=No.;
                SourceExpr="No.";
                CaptionClass=Expense.FIELDCAPTION("Expense Type") }

    { 1160040007;2;Field  ;
                Name=Description;
                CaptionML=[DEU=Beschreibung;
                           ENU=Description;
                           NLD=Omschrijving;
                           SVE=Beskrivning];
                SourceExpr=Description;
                Style=Strong;
                StyleExpr=IsHeading }

    { 1160040009;2;Field  ;
                Name=Description 2;
                SourceExpr="Description 2";
                CaptionClass=Expense.FIELDCAPTION("Description 2");
                Visible=FALSE }

    { 1160040013;2;Field  ;
                Name=Details;
                SourceExpr=Details }

    { 1160040011;2;Field  ;
                Name=Document Date;
                SourceExpr="Document Date";
                CaptionClass=Expense.FIELDCAPTION("Document Date") }

    { 1160040003;2;Field  ;
                Name=On Hold;
                SourceExpr="On Hold";
                CaptionClass=Expense.FIELDCAPTION("On Hold") }

    { 1160040015;2;Field  ;
                Name=Currency Code;
                CaptionML=[DEU=W„hrungscode;
                           ENU=Currency Code;
                           NLD=Valutacode;
                           SVE=Valutakod];
                SourceExpr="Currency Code";
                CaptionClass=Expense.FIELDCAPTION("Currency Code");
                Editable=false }

    { 1160040023;2;Field  ;
                Name=Amount;
                CaptionML=[DEU=Betrag;
                           ENU=Amount;
                           NLD=Bedrag;
                           SVE=Belopp];
                SourceExpr=Amount }

    { 1160040024;2;Field  ;
                Name=Amount (LCY);
                CaptionML=[DEU=Betrag (MW);
                           ENU=Amount (LCY);
                           NLD=Bedrag (LV);
                           SVE=Belopp (BVA)];
                SourceExpr="Amount (LCY)" }

    { 1160040032;2;Field  ;
                Name=IsAllocated;
                CaptionML=[DEU=Zugeordnet;
                           ENU=Allocated;
                           NLD=Gealloceerd;
                           SVE=F”rdelat];
                SourceExpr=IsAllocated }

    { 1160040030;2;Field  ;
                Name=Salespers./Purch. Code;
                SourceExpr="Salespers./Purch. Code";
                Visible=false }

    { 1160040021;2;Field  ;
                Name=Global Dimension 1 Code;
                SourceExpr="Global Dimension 1 Code";
                CaptionClass='1,2,1';
                Editable=false;
                OnLookup=BEGIN
                           EXIT(Expense.LookupGlobalDim(Text,1));
                         END;
                          }

    { 1160040025;2;Field  ;
                Name=Global Dimension 2 Code;
                SourceExpr="Global Dimension 2 Code";
                CaptionClass='1,2,2';
                Editable=false;
                OnLookup=BEGIN
                           EXIT(Expense.LookupGlobalDim(Text,2));
                         END;
                          }

    { 1160040026;2;Field  ;
                Name=Job No.;
                SourceExpr="Job No.";
                CaptionClass=Expense.FIELDCAPTION("Job No.");
                Visible=FALSE;
                Editable=false }

    { 1160040027;2;Field  ;
                Name=Job Task No.;
                SourceExpr="Job Task No.";
                CaptionClass=Expense.FIELDCAPTION("Job Task No.");
                Visible=FALSE;
                Editable=FALSE }

    { 1160040040;2;Field  ;
                Name=Admin Comment;
                SourceExpr="Admin Comment";
                Visible=false;
                Editable=false }

    { 1160040000;2;Field  ;
                Name=ShortcutField1;
                SourceExpr=ShortcutFieldValues[1];
                CaptionClass=GetShortcutFieldCaption(1);
                Visible=CustomFieldVisible1;
                Editable=false }

    { 1160040008;2;Field  ;
                Name=ShortcutField2;
                SourceExpr=ShortcutFieldValues[2];
                CaptionClass=GetShortcutFieldCaption(2);
                Visible=CustomFieldVisible2;
                Editable=false }

    { 1160040010;2;Field  ;
                Name=ShortcutField3;
                SourceExpr=ShortcutFieldValues[3];
                CaptionClass=GetShortcutFieldCaption(3);
                Visible=CustomFieldVisible3;
                Editable=false }

    { 1160040012;2;Field  ;
                Name=ShortcutField4;
                SourceExpr=ShortcutFieldValues[4];
                CaptionClass=GetShortcutFieldCaption(4);
                Visible=CustomFieldVisible4;
                Editable=false }

    { 1160040014;2;Field  ;
                Name=ShortcutField5;
                SourceExpr=ShortcutFieldValues[5];
                CaptionClass=GetShortcutFieldCaption(5);
                Visible=CustomFieldVisible5;
                Editable=false }

    { 1160040016;2;Field  ;
                Name=ShortcutField6;
                SourceExpr=ShortcutFieldValues[6];
                CaptionClass=GetShortcutFieldCaption(6);
                Visible=CustomFieldVisible6;
                Editable=false }

    { 1160040017;2;Field  ;
                Name=ShortcutField7;
                SourceExpr=ShortcutFieldValues[7];
                CaptionClass=GetShortcutFieldCaption(7);
                Visible=CustomFieldVisible7;
                Editable=false }

    { 1160040018;2;Field  ;
                Name=ShortcutField8;
                SourceExpr=ShortcutFieldValues[8];
                CaptionClass=GetShortcutFieldCaption(8);
                Visible=CustomFieldVisible8;
                Editable=false }

    { 1160040020;2;Field  ;
                Name=ShortcutField9;
                SourceExpr=ShortcutFieldValues[9];
                CaptionClass=GetShortcutFieldCaption(9);
                Visible=CustomFieldVisible9;
                Editable=false }

    { 1160040022;2;Field  ;
                Name=ShortcutField10;
                SourceExpr=ShortcutFieldValues[10];
                CaptionClass=GetShortcutFieldCaption(10);
                Visible=CustomFieldVisible10;
                Editable=false }

    { 1160040031;1;Part   ;
                Name=EM Comment App. Subpage;
                CaptionML=[DEU=Bemerkungen;
                           ENU=Comments;
                           NLD=Opmerkingen;
                           SVE=Kommentarer];
                SubPageLink=Doc. Ref. No. (Code)=FIELD(Document No.),
                            Table ID=FIELD(Table ID);
                PagePartID=Page6086370;
                PartType=Page }

    { 1160040006;;Container;
                Name=FactBoxArea;
                ContainerType=FactBoxArea }

    { 1160040004;1;Part   ;
                Name=CaptureUI;
                CaptionML=[DEU=Dateianhang;
                           ENU=Attachments;
                           NLD=Bijlagen;
                           SVE=Bilagor];
                SubPageLink=Table ID=FIELD(Table ID),
                            Document Type=FIELD(Document Type),
                            Document No.=FIELD(Document No.),
                            Sequence No.=FIELD(Sequence No.);
                PagePartID=Page6086366 }

  }
  CODE
  {
    VAR
      Expense@1160040003 : Record 6086320;
      Mileage@1160040001 : Record 6086338;
      ExpHeader@1160040011 : Record 6086339;
      ShortcutFieldFunctions@1160040002 : Codeunit 6086304;
      EMApprovalMgt@1160040004 : Codeunit 6086312;
      EMApprovalsBridge@1000 : Codeunit 6086369;
      CurrentZoomText@1000000002 : Text[30];
      CustomFieldNames@1160040025 : ARRAY [10] OF Text[50];
      ShortcutFieldCaptions@1160040026 : ARRAY [10] OF Text[250];
      ShortcutFieldValues@1160040027 : ARRAY [10] OF Text[250];
      CurrZoom@1000000001 : Decimal;
      Indent@1160040029 : Integer;
      CustomFieldVisible1@1160040024 : Boolean INDATASET;
      CustomFieldVisible10@1160040015 : Boolean INDATASET;
      CustomFieldVisible2@1160040023 : Boolean INDATASET;
      CustomFieldVisible3@1160040022 : Boolean INDATASET;
      CustomFieldVisible4@1160040021 : Boolean INDATASET;
      CustomFieldVisible5@1160040020 : Boolean INDATASET;
      CustomFieldVisible6@1160040019 : Boolean INDATASET;
      CustomFieldVisible7@1160040018 : Boolean INDATASET;
      CustomFieldVisible8@1160040017 : Boolean INDATASET;
      CustomFieldVisible9@1160040016 : Boolean INDATASET;
      HasComments@1160040009 : Boolean;
      IsAllocated@1160040000 : Boolean;
      IsHeading@1160040007 : Boolean INDATASET;
      MileageEnabled@1160040008 : Boolean INDATASET;
      MileageAllocationErr@1160040012 : TextConst 'DEU=Zuweisungen sind nicht zul„ssig bei Kilometerleistung.;ENU=Allocations are not supported on mileage.;NLD=Toewijzingen zijn niet toegestaan bij kilometrage.';
      MileageAttendessErr@1160040010 : TextConst 'DEU=Teilnehmer sind nicht zul„ssig bei Kilometerleistung.;ENU=Attendees are not supported on mileage.;NLD=Deelnemers zijn niet toegestaan bij kilometrage.';
      Text003@1160040005 : TextConst 'DEU=Out of Office ist derzeit aktiviert.;ENU=Out of Office is currently activated.;NLD=Out of Office momenteel actief.;SVE=Fr†nvaro „r aktiv';
      UseSettlementCard@1160040006 : TextConst 'DEU=™ffnen Sie die Ausgleichskarte, um die Details anzuzeigen.;ENU=Please open the Settlement Card to see the details.;NLD=Open de afrekeningkaart om de details te zien.';

    LOCAL PROCEDURE UpdateList@161024013();
    VAR
      EmApprovalMgnt@161024012 : Codeunit 6086312;
    BEGIN
      EmApprovalMgnt.BuildApprovalEntriesForUserExp(Rec,TRUE,TRUE);
      SETCURRENTKEY("Temp. Entry Type","Approver ID","Temp. Display Sorting");
      IF (xRec."Document No." = '') AND (xRec."Temp. Entry Type" = xRec."Temp. Entry Type"::Normal) THEN BEGIN
        IF FINDFIRST THEN;
      END ELSE
        IF NOT GET(xRec."Entry No.") THEN;
    END;

    LOCAL PROCEDURE GetIsOutOfOffice@161024014() : Boolean;
    VAR
      DCApprovalMgnt@161024015 : Codeunit 6085722;
      FromDate@161024013 : Date;
      ToDate@161024014 : Date;
      ForwardToApproverId@161024012 : Code[50];
    BEGIN
      IF DCApprovalMgnt.GetOutOfOffice(USERID,ForwardToApproverId,FromDate,ToDate) THEN
        IF ((FromDate = 0D) OR (FromDate <= TODAY)) AND ((ToDate = 0D) OR (ToDate >= TODAY)) THEN
          EXIT(TRUE);
    END;

    LOCAL PROCEDURE GetShortcutFieldCaption@1160040009(ShortcutFieldNo@1160040000 : Integer) : Text[250];
    BEGIN
      IF ShortcutFieldCaptions[1] = '' THEN
        ShortcutFieldFunctions.GetShortcutAppCaptionsArray(ShortcutFieldCaptions);

      EXIT(ShortcutFieldCaptions[ShortcutFieldNo]);
    END;

    LOCAL PROCEDURE EnableFields@1160040010();
    VAR
      EMSetup@1160040000 : Record 6086300;
    BEGIN
      EMSetup.GET;
      CustomFieldNames[1] := EMSetup."Shortcut Field 1 Code (App.)";
      CustomFieldNames[2] := EMSetup."Shortcut Field 2 Code (App.)";
      CustomFieldNames[3] := EMSetup."Shortcut Field 3 Code (App.)";
      CustomFieldNames[4] := EMSetup."Shortcut Field 4 Code (App.)";
      CustomFieldNames[5] := EMSetup."Shortcut Field 5 Code (App.)";
      CustomFieldNames[6] := EMSetup."Shortcut Field 6 Code (App.)";
      CustomFieldNames[7] := EMSetup."Shortcut Field 7 Code (App.)";
      CustomFieldNames[8] := EMSetup."Shortcut Field 8 Code (App.)";
      CustomFieldNames[9] := EMSetup."Shortcut Field 9 Code (App.)";
      CustomFieldNames[10] := EMSetup."Shortcut Field 10 Code (App.)";

      CustomFieldVisible1 := CustomFieldNames[1] <> '';
      CustomFieldVisible2 := CustomFieldNames[2] <> '';
      CustomFieldVisible3 := CustomFieldNames[3] <> '';
      CustomFieldVisible4 := CustomFieldNames[4] <> '';
      CustomFieldVisible5 := CustomFieldNames[5] <> '';
      CustomFieldVisible6 := CustomFieldNames[6] <> '';
      CustomFieldVisible7 := CustomFieldNames[7] <> '';
      CustomFieldVisible8 := CustomFieldNames[8] <> '';
      CustomFieldVisible9 := CustomFieldNames[9] <> '';
      CustomFieldVisible10 := CustomFieldNames[10] <> '';

      MileageEnabled := EMSetup."Enable Mileage";
    END;

    LOCAL PROCEDURE GetRecordFromApprovalEntry@1160040002();
    BEGIN
      CASE "Table ID" OF
        DATABASE::Expense:
          IF NOT Expense.GET(EMApprovalMgt.Code2Int("Document No.")) THEN
            CLEAR(Expense);

        DATABASE::Mileage:
          IF NOT Mileage.GET(EMApprovalMgt.Code2Int("Document No.")) THEN
            CLEAR(Mileage);

        DATABASE::"Expense Header":
          IF NOT ExpHeader.GET(ExpHeader."Document Type"::Settlement,"Document No.") THEN
            CLEAR(ExpHeader);

      END;
    END;

    BEGIN
    END.
  }
}

