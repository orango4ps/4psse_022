OBJECT Report 5300 Outlook Synch. Change Log Set.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Outlook-Synch.-énderungsprotokolleinr.;
               ENU=Outlook Synch. Change Log Set.;
               NLD=Wijzingslogbestandinstel. Outlook-synch.;
               NOR=Synkroniser endringsloggsett med Outlook];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF NOT OSynchEntity.FINDFIRST THEN
                    ERROR(Text004);
                END;

    OnPostReport=VAR
                   ChangeLogSetup@1000 : Record 402;
                 BEGIN
                   IF NOT ChangeLogSetup.GET THEN
                     ChangeLogSetup.INSERT;
                   IF NOT ChangeLogSetup."Change Log Activated" THEN BEGIN
                     ChangeLogSetup."Change Log Activated" := TRUE;
                     ChangeLogSetup.MODIFY;
                     MESSAGE(Text002);
                   END ELSE
                     MESSAGE(Text003);
                 END;

  }
  DATASET
  {
    { 6770;    ;DataItem;OSynchEntity        ;
               DataItemTable=Table5300;
               DataItemTableView=SORTING(Code);
               OnAfterGetRecord=BEGIN
                                  RegisterChangeLogPrimaryKey("Table No.");
                                END;

               ReqFilterFields=Code }

    { 4710;1   ;DataItem;OSynchEntityElement ;
               DataItemTable=Table5301;
               DataItemTableView=SORTING(Synch. Entity Code,Element No.)
                                 ORDER(Ascending)
                                 WHERE(Element No.=FILTER(<>0));
               OnAfterGetRecord=BEGIN
                                  RegisterChangeLogPrimaryKey("Table No.");
                                END;

               DataItemLink=Synch. Entity Code=FIELD(Code) }

    { 6868;2   ;DataItem;OSynchFilterElement ;
               DataItemTable=Table5303;
               DataItemTableView=SORTING(Record GUID,Filter Type,Line No.)
                                 ORDER(Ascending)
                                 WHERE(Filter Type=CONST(Table Relation));
               OnAfterGetRecord=BEGIN
                                  RegisterChangeLogFilter(OSynchFilterElement);
                                END;

               DataItemLink=Record GUID=FIELD(Record GUID) }

    { 2890;1   ;DataItem;OSynchField         ;
               DataItemTable=Table5304;
               DataItemTableView=SORTING(Synch. Entity Code,Element No.,Line No.);
               OnAfterGetRecord=VAR
                                  OSynchFilter@1000 : Record 5303;
                                  OSynchEntityElement@1001 : Record 5301;
                                BEGIN
                                  IF "Table No." <> 0 THEN BEGIN
                                    OSynchFilter.RESET;
                                    OSynchFilter.SETRANGE("Record GUID","Record GUID");
                                    OSynchFilter.SETRANGE("Filter Type",OSynchFilter."Filter Type"::"Table Relation");
                                    OSynchFilter.SETRANGE(Type,OSynchFilter.Type::FIELD);
                                    IF NOT OSynchFilter.FINDFIRST THEN BEGIN
                                      CALCFIELDS("Table Caption");
                                      IF "Element No." = 0 THEN
                                        ERROR(Text001,"Table Caption",OSynchEntity.TABLECAPTION,OSynchEntity.Code);

                                      OSynchEntityElement.GET("Synch. Entity Code","Element No.");
                                      OSynchEntityElement.CALCFIELDS("Table Caption");
                                      ERROR(
                                        Text005,
                                        "Table Caption",
                                        OSynchEntityElement."Table Caption",
                                        OSynchEntityElement."Outlook Collection",
                                        OSynchEntity.Code);
                                    END;

                                    IF TypeHelper.GetField("Table No.","Field No.",Field) THEN BEGIN
                                      Field.TESTFIELD(Enabled,TRUE);
                                      RegisterChangeLogField("Table No.","Field No.");
                                    END;

                                    FieldID := OSynchFilter."Master Table Field No.";
                                  END ELSE
                                    FieldID := "Field No.";

                                  IF TypeHelper.GetField("Master Table No.",FieldID,Field) THEN BEGIN
                                    Field.TESTFIELD(Enabled,TRUE);
                                    RegisterChangeLogField("Master Table No.",FieldID);
                                  END;
                                END;

               DataItemLink=Synch. Entity Code=FIELD(Code) }

    { 7744;    ;DataItem;OSynchFilterEntity  ;
               DataItemTable=Table5303;
               DataItemTableView=SORTING(Record GUID,Filter Type,Line No.)
                                 ORDER(Ascending);
               OnAfterGetRecord=BEGIN
                                  RegisterChangeLogFilter(OSynchFilterEntity);
                                END;

               DataItemLinkReference=OSynchEntity;
               DataItemLink=Record GUID=FIELD(Record GUID) }

    { 9398;    ;DataItem;OSynchUserSetup     ;
               DataItemTable=Table5305;
               DataItemTableView=SORTING(User ID,Synch. Entity Code)
                                 ORDER(Ascending);
               DataItemLinkReference=OSynchEntity;
               DataItemLink=Synch. Entity Code=FIELD(Code) }

    { 3638;1   ;DataItem;OSynchFilterUserSetup;
               DataItemTable=Table5303;
               DataItemTableView=SORTING(Record GUID,Filter Type,Line No.)
                                 ORDER(Ascending);
               OnAfterGetRecord=BEGIN
                                  RegisterChangeLogFilter(OSynchFilterUserSetup);
                                END;

               DataItemLink=Record GUID=FIELD(Record GUID) }

    { 1658;    ;DataItem;OSynchDependency    ;
               DataItemTable=Table5311;
               DataItemTableView=SORTING(Synch. Entity Code,Element No.,Depend. Synch. Entity Code)
                                 ORDER(Ascending);
               DataItemLinkReference=OSynchEntity;
               DataItemLink=Synch. Entity Code=FIELD(Code) }

    { 7267;1   ;DataItem;OSynchFilterDependency;
               DataItemTable=Table5303;
               DataItemTableView=SORTING(Record GUID,Filter Type,Line No.)
                                 ORDER(Ascending);
               OnAfterGetRecord=BEGIN
                                  RegisterChangeLogFilter(OSynchFilterDependency);
                                END;

               DataItemLink=Record GUID=FIELD(Record GUID) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Field@1004 : Record 2000000041;
      TypeHelper@1007 : Codeunit 10;
      FieldID@1000 : Integer;
      Text001@1002 : TextConst 'DEU=Die Relation zwischen der %1-Tabelle und der %2-Tabelle in der %3-Einheit kann nicht bestimmt werden. öberprÅfen Sie die Synchronisierungseinstellungen fÅr diese Einheit.;ENU=The relation between the %1 table and %2 table in the %3 entity cannot be determined. Please verify your synchronization settings for this entity.;NLD=De relatie tussen de tabellen %1 en %2 in de entiteit %3 kan niet worden vastgesteld. Controleer de synchronisatie-instellingen voor deze entiteit.;NOR=Kan ikke fastsette relasjonen mellom tabell %1 og tabell %2 i enheten %3. Kontroller synkroniseringsinnstillingene for denne enheten.';
      Text002@1001 : TextConst 'DEU=Die énderungsprotokolleinstellungen wurden erfolgreich registriert. Sie mÅssen den Mandanten schlie·en und neu îffnen, damit die neuen Einstellungen des énderungsprotokolls wirksam werden.;ENU=The change log settings have been registered successfully. You must close and reopen the company for the new change log settings to take effect.;NLD=De wijzigingslogbestandinstellingen zijn geregistreerd. U moet het bedrijf opnieuw openen om de nieuwe instellingen van kracht te laten worden.;NOR=Endringslogginnstillingene er registrert. Du mÜ lukke selskapet og Üpne det pÜ nytt for at de nye endringslogginnstillingene skal tre i kraft.';
      Text003@1003 : TextConst 'DEU=Die énderungsprotokolleinstellungen wurden erfolgreich registriert.;ENU=The change log settings have been registered successfully.;NLD=De wijzigingslogbestandinstellingen zijn geregistreerd.;NOR=Endringslogginnstillingene er registrert.';
      Text004@1005 : TextConst 'DEU=Die Einheit wurde nicht gefunden. Stellen Sie sicher, dass Sie den Namen richtig eingegeben haben.;ENU=The entity cannot be found. Make sure that you have typed its name correctly.;NLD=De entiteit kan niet worden gevonden. Controleer of u de naam juist hebt getypt.;NOR=Finner ikke enheten. Kontroller at du har skrevet navnet riktig.';
      Text005@1006 : TextConst 'DEU=Die Relation zwischen der %1-Tabelle und der %2-Tabelle in der %3-Sammlung der %4-Einheit kann nicht bestimmt werden. öberprÅfen Sie die Synchronisierungseinstellungen fÅr diese Einheit.;ENU=The relation between the %1 table and %2 table in the %3 collection from the %4 entity cannot be determined. Verify your synchronization settings for this entity.;NLD=De relatie tussen de tabellen %1 en %2 in de verzameling %3 van de entiteit %4 kan niet worden vastgesteld. Controleer de synchronisatie-instellingen voor deze entiteit.;NOR=Kan ikke fastsette relasjonen mellom tabell %1 og tabell %2 i samlingen %3 fra enheten %4. Kontroller synkroniseringsinnstillingene for denne enheten.';

    LOCAL PROCEDURE RegisterChangeLogField@10(TableID@1000 : Integer;FieldID@1001 : Integer);
    VAR
      ChangeLogSetupTable@1002 : Record 403;
      ChangeLogSetupField@1003 : Record 404;
      NeedToBeUpdated@1004 : Boolean;
    BEGIN
      WITH ChangeLogSetupTable DO BEGIN
        RESET;
        IF NOT GET(TableID) THEN BEGIN
          INIT;
          "Table No." := TableID;
          VALIDATE("Log Insertion","Log Insertion"::"Some Fields");
          VALIDATE("Log Modification","Log Modification"::"Some Fields");
          VALIDATE("Log Deletion","Log Modification"::"Some Fields");
          INSERT;
          NeedToBeUpdated := TRUE;
        END ELSE BEGIN
          NeedToBeUpdated :=
            ("Log Insertion" <> "Log Insertion"::"All Fields") OR
            ("Log Modification" <> "Log Modification"::"All Fields") OR
            ("Log Deletion" <> "Log Deletion"::"All Fields");

          IF "Log Insertion" <> "Log Insertion"::"All Fields" THEN
            "Log Insertion" := "Log Insertion"::"Some Fields";

          IF "Log Modification" <> "Log Modification"::"All Fields" THEN
            "Log Modification" := "Log Insertion"::"Some Fields";

          IF "Log Deletion" <> "Log Deletion"::"All Fields" THEN
            "Log Deletion" := "Log Deletion"::"Some Fields";

          IF NeedToBeUpdated THEN
            MODIFY;
        END;
      END;

      IF NOT NeedToBeUpdated THEN
        EXIT;

      WITH ChangeLogSetupField DO BEGIN
        RESET;
        IF NOT GET(TableID,FieldID) THEN BEGIN
          INIT;
          "Table No." := TableID;
          "Field No." := FieldID;
          "Log Insertion" := TRUE;
          "Log Modification" := TRUE;
          "Log Deletion" := TRUE;
          INSERT;
        END ELSE BEGIN
          "Log Insertion" := TRUE;
          "Log Modification" := TRUE;
          "Log Deletion" := TRUE;
          MODIFY;
        END;
      END;
    END;

    LOCAL PROCEDURE RegisterChangeLogPrimaryKey@1(TableID@1000 : Integer);
    VAR
      RecRef@1001 : RecordRef;
      I@1002 : Integer;
    BEGIN
      RecRef.OPEN(TableID,TRUE);
      FOR I := 1 TO RecRef.KEYINDEX(1).FIELDCOUNT DO
        RegisterChangeLogField(TableID,RecRef.KEYINDEX(1).FIELDINDEX(I).NUMBER);
      RecRef.CLOSE;
    END;

    LOCAL PROCEDURE RegisterChangeLogFilter@8(OSynchFilter1@1000 : Record 5303);
    BEGIN
      IF TypeHelper.GetField(OSynchFilter1."Table No.",OSynchFilter1."Field No.",Field) THEN BEGIN
        Field.TESTFIELD(Enabled,TRUE);
        RegisterChangeLogField(OSynchFilter1."Table No.",OSynchFilter1."Field No.");
      END;
      IF TypeHelper.GetField(OSynchFilter1."Master Table No.",OSynchFilter1."Master Table Field No.",Field) THEN BEGIN
        Field.TESTFIELD(Enabled,TRUE);
        RegisterChangeLogField(OSynchFilter1."Master Table No.",OSynchFilter1."Master Table Field No.");
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

