OBJECT Page 11012659 Request - Rail
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Anforderung;
               ENU=Request;
               NLD=Aanvraag;
               NOR=Forespõrsel;
               SVE=FîrfrÜgan];
    SourceTable=Table11012619;
    OnInit=BEGIN
             NoEDITABLE := TRUE;
             DateEDITABLE := TRUE;
             StatusEDITABLE := TRUE;
             ProjectNoEDITABLE := TRUE;
             DescriptionEDITABLE := TRUE;
             SubFormEDITABLE := TRUE;
           END;

    OnOpenPage=BEGIN
                 UserSetupRec.GET(USERID);
                 UserSetupRec.TESTFIELD("Employee No.");
                 SETRANGE("Requested by", UserSetupRec."Employee No.");
               END;

    OnAfterGetRecord=BEGIN
                       SETRANGE("No.");
                     END;

    OnNewRecord=BEGIN
                  "Requested by" := UserSetupRec."Employee No.";
                END;

    OnDeleteRecord=BEGIN
                     IF ("Requested by" <> UserSetupRec."Employee No.") THEN
                       ERROR(Text001);
                     IF NOT CheckAllowedForUser(TRUE) THEN
                       ERROR(Text002);
                   END;

    OnAfterGetCurrRecord=BEGIN
                           SetEditable();
                         END;

  }
  CONTROLS
  {
    { 1100529100;;Container;
                ContainerType=ContentArea }

    { 1100529101;1;Group  ;
                Name=Algemeen;
                GroupType=Group }

    { 1100529102;2;Field  ;
                SourceExpr="No.";
                OnAssistEdit=BEGIN
                               IF AssistEdit(xRec) THEN
                                 CurrPage.UPDATE;
                             END;
                              }

    { 1100529103;2;Field  ;
                SourceExpr=Date }

    { 1100529104;2;Field  ;
                SourceExpr="Requested by" }

    { 1100529109;2;Field  ;
                CaptionML=[DEU=Name;
                           ENU=Name;
                           NLD=Naam;
                           NOR=Navn;
                           SVE=Namn];
                SourceExpr=EmplRec.CompleteEmployeeName("Requested by") }

    { 1100529105;2;Field  ;
                SourceExpr="Project No." }

    { 1100529106;2;Field  ;
                SourceExpr="Project Description" }

    { 1100529107;2;Field  ;
                SourceExpr=Description }

    { 1100529108;2;Field  ;
                SourceExpr=Status;
                OnValidate=BEGIN
                             IF (NOT (Status IN [Status::Requested, Status::Expired])) THEN
                               ERROR(Text000, Status);

                             IF (Status = Status::Expired) AND HasAssignedLines() THEN
                               ERROR(Text003, Status);
                           END;
                            }

    { 1100529110;1;Part   ;
                SubPageView=SORTING(Request No.,Line No.);
                SubPageLink=Request No.=FIELD(No.);
                PagePartID=Page11012653;
                PartType=Page }

    { 1100529113;;Container;
                ContainerType=FactBoxArea }

    { 1100529112;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1100529111;1;Part   ;
                Visible=TRUE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      EmplRec@1100529102 : Record 5200;
      UserSetupRec@1100529101 : Record 91;
      AssEmplRec@1100529100 : Record 11012621;
      Text000@1100529106 : TextConst 'DEU=Sie besitzen keine Berechtigungen fÅr das Festlegen des Status in''%1''.;ENU=You do not have permission to set the status on ''%1''.;NLD=U heeft geen permissie om de status op ''%1'' te zetten.';
      Text001@1100529105 : TextConst 'DEU=Sie besitzen keine Berechtigungen fÅr das Lîschen dieser Anforderung.;ENU=You do not have permission to delete this request.;NLD=U heeft geen permissie om deze aanvraag te verwijderen.';
      Text002@1100529104 : TextConst 'DEU=Sie kînnen diese Anforderung nicht mehr lîschen.;ENU=Delete this request is not allowed anymore for you.;NLD=Deze aanvraag verwijderen is voor u niet meer toegestaan.';
      Text003@1100529103 : TextConst 'DEU=Status ''%1'' nicht zulÑssig, dieser Anforderung sind bereits Mitarbeiter zugeordnet.;ENU=Status ''%1'' not allowed, there are already employees assigned to this request.;NLD=Status ''%1'' niet toegestaan, er zijn al werknemers toegekend aan deze aanvraag.';
      NoEDITABLE@1100529107 : Boolean INDATASET;
      DateEDITABLE@1100529108 : Boolean INDATASET;
      StatusEDITABLE@1100529109 : Boolean INDATASET;
      ProjectNoEDITABLE@1100529110 : Boolean INDATASET;
      DescriptionEDITABLE@1100529111 : Boolean INDATASET;
      SubFormEDITABLE@1100529112 : Boolean INDATASET;

    PROCEDURE SetEditable@1100485003();
    BEGIN
      NoEDITABLE := CheckAllowedForUser(TRUE);
      DateEDITABLE := CheckAllowedForUser(TRUE);
      StatusEDITABLE := CheckAllowedForUser(TRUE);
      ProjectNoEDITABLE := CheckAllowedForUser(TRUE);
      DescriptionEDITABLE := CheckAllowedForUser(FALSE);
      SubFormEDITABLE := CheckAllowedForUser(TRUE);
    END;

    PROCEDURE CheckAllowedForUser@1100485000(IModeCheckAssigned@1100485000 : Boolean) : Boolean;
    BEGIN
      IF ("Requested by" <> UserSetupRec."Employee No.") THEN
        EXIT(FALSE);
      IF (NOT (Status IN [Status::Requested, Status::Expired])) THEN
        EXIT(FALSE);

      IF IModeCheckAssigned THEN BEGIN
        IF HasAssignedLines() THEN
          EXIT(FALSE);
      END;

      EXIT(TRUE);
    END;

    PROCEDURE HasAssignedLines@1100485001() : Boolean;
    BEGIN
      AssEmplRec.RESET;
      AssEmplRec.SETRANGE("Request No.", "No.");
      IF AssEmplRec.FIND('-') THEN
        EXIT(TRUE);

      EXIT(FALSE);
    END;

    BEGIN
    END.
  }
}

