OBJECT Table 11012503 Option Budget
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 11072003=rm;
    DataCaptionFields=Project No.,Plot No.,Option,Option Description;
    OnInsert=BEGIN
               CheckOptionStatus();
               IF "Plot No." <> '' THEN BEGIN
                 PlotRec.GET("Project No.", "Plot No.");
                 PlotRec.TESTFIELD("Plot No. Fixed", FALSE);
               END;
               CheckInsertSummarySheet(TRUE);
               UpdateOtherTables(Trigger::Insert);
             END;

    OnModify=BEGIN
               CheckOptionStatus();
               IF "Plot No." <> '' THEN BEGIN
                 PlotRec.GET("Project No.", "Plot No.");
                 PlotRec.TESTFIELD("Plot No. Fixed", FALSE);
               END;
               UpdateOtherTables(Trigger::Modify);
             END;

    OnDelete=VAR
               OptionLineRec@1100525000 : Record 11012503;
             BEGIN
               CheckOptionStatus();
               IF "Plot No." <> '' THEN BEGIN
                 PlotRec.GET("Project No.", "Plot No.");
                 PlotRec.TESTFIELD("Plot No. Fixed", FALSE);
               END;
               UpdateOtherTables(Trigger::Delete);

               IF "Line No." <> 0 THEN BEGIN   //db, 02-02-09
                 IF ("Source Type" = "Source Type"::"Recipe Header") AND (Recipe <> '') THEN BEGIN
                   OptionLineRec.SETRANGE("Project No.","Project No.");
                   OptionLineRec.SETFILTER("Plot No.", '%1', "Plot No.");
                   OptionLineRec.SETFILTER("House Model", '%1', "House Model");
                   OptionLineRec.SETFILTER("Main Group", '%1', "Main Group");
                   OptionLineRec.SETFILTER(Group, '%1', Group);
                   OptionLineRec.SETFILTER("Sub Group", '%1', "Sub Group");
                   OptionLineRec.SETFILTER(Option, '%1', Option);
                   OptionLineRec.SETRANGE("Attached to Recipe Line","Line No.");
                   OptionLineRec.DELETEALL(TRUE);
                 END;
               END;
             END;

    CaptionML=[ENU=Option Budget;
               NOR=Alternativt budsjett;
               SVE=Alternativbudget];
    DrillDownPageID=Page11012519;
  }
  FIELDS
  {
    { 1   ;   ;Project No.         ;Code20        ;TableRelation=Job.No.;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr] }
    { 2   ;   ;Element             ;Code20        ;TableRelation=IF (Project No.=FILTER(<>'')) "Project Element".Element WHERE (Project No.=FIELD(Project No.))
                                                                 ELSE IF (Project No.=FILTER('')) "Base Element".Code;
                                                   OnValidate=BEGIN
                                                                ElementFormat.FormatElem(Element,2,'');
                                                                IF Element <> '' THEN BEGIN
                                                                  IF "Project No." = '' THEN BEGIN
                                                                    IF NOT ElemRec.GET(Element) THEN
                                                                      FIELDERROR(Element, STRSUBSTNO(Text007, ElemRec.TABLECAPTION + ' ' + Element));
                                                                    Chapter := ElemRec.Chapter;
                                                                    Paragraph := ElemRec.Paragraph;
                                                                  END ELSE BEGIN
                                                                    IF NOT ProjElemRec.GET("Project No.",Element) THEN
                                                                      FIELDERROR(Element, STRSUBSTNO(Text007, ProjElemRec.TABLECAPTION + ' ' + "Project No." + ' - ' + Element));
                                                                    Chapter := ProjElemRec.Chapter;
                                                                    Paragraph := ProjElemRec.Paragraph;
                                                                  END;
                                                                END ELSE BEGIN
                                                                  CLEAR(Chapter);
                                                                  CLEAR(Paragraph);
                                                                END;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Element;
                                                              NOR=Element;
                                                              SVE=Element] }
    { 3   ;   ;Cost Object         ;Code20        ;TableRelation=IF (Cost Type=CONST(Labor)) "Dimension Value".Code WHERE (Cost Type=FILTER(Labor),
                                                                                                                           Global Dimension No.=CONST(2),
                                                                                                                           Project Filter=FIELD(Project No.))
                                                                                                                           ELSE IF (Cost Type=CONST(Material)) "Dimension Value".Code WHERE (Cost Type=FILTER(Material),
                                                                                                                                                                                             Global Dimension No.=CONST(2),
                                                                                                                                                                                             Project Filter=FIELD(Project No.))
                                                                                                                                                                                             ELSE IF (Cost Type=CONST(Plant)) "Dimension Value".Code WHERE (Cost Type=FILTER(Plant),
                                                                                                                                                                                                                                                            Global Dimension No.=CONST(2),
                                                                                                                                                                                                                                                            Project Filter=FIELD(Project No.))
                                                                                                                                                                                                                                                            ELSE IF (Cost Type=CONST(Sundry)) "Dimension Value".Code WHERE (Cost Type=FILTER(Sundry),
                                                                                                                                                                                                                                                                                                                            Global Dimension No.=CONST(2),
                                                                                                                                                                                                                                                                                                                            Project Filter=FIELD(Project No.))
                                                                                                                                                                                                                                                                                                                            ELSE IF (Cost Type=CONST(Subcontracting)) "Dimension Value".Code WHERE (Cost Type=FILTER(Subcontracting),
                                                                                                                                                                                                                                                                                                                                                                                                    Global Dimension No.=CONST(2),
                                                                                                                                                                                                                                                                                                                                                                                                    Project Filter=FIELD(Project No.));
                                                   OnValidate=VAR
                                                                Selection@1100525000 : Integer;
                                                              BEGIN
                                                                IF "Cost Object" <> '' THEN
                                                                  IF xRec."Cost Object" = '' THEN
                                                                    GetDefaultsCostObject(FALSE)
                                                                  ELSE
                                                                    IF "Cost Object" <> xRec."Cost Object" THEN
                                                                      IF "Item No." + "Basic Item" + "Trade Item" = '' THEN BEGIN
                                                                        Selection := STRMENU(Text006,3);
                                                                        IF Selection < 3 THEN
                                                                          GetDefaultsCostObject(Selection=2);
                                                                      END;
                                                              END;

                                                   CaptionML=[ENU=Global Dimension 2 Code;
                                                              NOR=Kostnadsobjekt;
                                                              SVE=Kostnadsobjekt];
                                                   CaptionClass='1,1,2' }
    { 4   ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 5   ;   ;Description 2       ;Text50        ;CaptionML=[ENU=Description 2;
                                                              NOR=Beskrivelse 2;
                                                              SVE=Beskrivning 2] }
    { 6   ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                CalcTotals;
                                                                CheckRecipeTotals;
                                                              END;

                                                   CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   DecimalPlaces=0:5 }
    { 7   ;   ;Unit of Measure     ;Code10        ;TableRelation="Unit of Measure".Code;
                                                   OnValidate=BEGIN
                                                                IF CurrFieldNo = FIELDNO("Unit of Measure") THEN
                                                                  ValidateUnitOfMeasure("Unit of Measure");
                                                              END;

                                                   OnLookup=BEGIN
                                                              HandleLookupUnit;
                                                            END;

                                                   CaptionML=[ENU=Unit of Measure;
                                                              NOR=M†leenhet;
                                                              SVE=M†ttenhet] }
    { 8   ;   ;Norm                ;Decimal       ;OnValidate=BEGIN
                                                                IF ("Cost Type" = "Cost Type"::Labor) THEN
                                                                  CalcTotals
                                                                ELSE BEGIN
                                                                  MESSAGE(Text001, "Cost Type");
                                                                  Norm := 0;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Norm;
                                                              NOR=Norm;
                                                              SVE=Norm];
                                                   DecimalPlaces=2: }
    { 10  ;Yes;Rate Code           ;Code10        ;TableRelation="Budget Rate".Code WHERE (Project Filter=FIELD(Project No.));
                                                   OnValidate=VAR
                                                                BudgetRateManagement@1100528600 : Codeunit 11012358;
                                                                BudgetRateFoundAt@1100528601 : Text[250];
                                                              BEGIN
                                                                IF ("Cost Type" = "Cost Type"::Labor) THEN BEGIN
                                                                  Rate := BudgetRateManagement.GetBudgetRate(
                                                                    0, "Project No.", '', '', ProjectRec."Global Dimension 1 Code", "Rate Code", GetVersionDate, BudgetRateFoundAt);  //C062294
                                                                  CalcTotals;
                                                                END ELSE BEGIN
                                                                  IF CurrFieldNo = FIELDNO("Rate Code") THEN
                                                                    MESSAGE(Text002, "Cost Type");
                                                                  "Rate Code" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Rate Code;
                                                              NOR=Avgiftskode;
                                                              SVE=Avgiftskod] }
    { 11  ;   ;Rate                ;Decimal       ;OnValidate=BEGIN
                                                                IF ("Cost Type" = "Cost Type"::Labor) THEN
                                                                  CalcTotals;
                                                              END;

                                                   CaptionML=[ENU=Rate;
                                                              NOR=Avgift;
                                                              SVE=Avgift];
                                                   BlankZero=Yes;
                                                   AutoFormatType=2 }
    { 12  ;   ;Hours               ;Decimal       ;FieldClass=Normal;
                                                   CaptionML=[ENU=Hours;
                                                              NOR="Timer ";
                                                              SVE=Timmar];
                                                   Editable=No }
    { 13  ;   ;Amount              ;Decimal       ;CaptionML=[ENU=Amount;
                                                              NOR=Bel›p;
                                                              SVE=Belopp];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 14  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 15  ;   ;Project Description ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Job.Description WHERE (No.=FIELD(Project No.)));
                                                   CaptionML=[ENU=Project Description;
                                                              NOR=Prosjektbeskrivelse;
                                                              SVE=Projektbeskrivning];
                                                   Editable=No }
    { 17  ;   ;Cost Object Description;Text50     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Dimension Value".Name WHERE (Code=FIELD(Cost Object),
                                                                                                    Global Dimension No.=CONST(2)));
                                                   CaptionML=[ENU=Cost Object Description;
                                                              NOR=Kostnadsobjekt, Beskrivelse;
                                                              SVE=Kostnadsobjekt, beskrivning];
                                                   Editable=No;
                                                   CaptionClass='1,11012000,2, , ' + Text000 }
    { 18  ;   ;Element Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Base Element".Description WHERE (Code=FIELD(Element)));
                                                   CaptionML=[ENU=Element Description;
                                                              NOR=Elementbeskrivelse;
                                                              SVE=Elementbeskrivning];
                                                   Editable=No }
    { 19  ;   ;Total Amount        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Option Budget".Amount WHERE (Project No.=FIELD(Project No.),
                                                                                                 Plot No.=FIELD(Plot No.),
                                                                                                 House Model=FIELD(House Model),
                                                                                                 Main Group=FIELD(Main Group),
                                                                                                 Group=FIELD(Group),
                                                                                                 Sub Group=FIELD(Sub Group),
                                                                                                 Option=FIELD(Option),
                                                                                                 Budget Summary Line=FIELD(Summary Filter)));
                                                   CaptionML=[ENU=Total Amount;
                                                              NOR=Totalt bel›p;
                                                              SVE=Totalt belopp];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 20  ;   ;Total Hours         ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Option Budget".Hours WHERE (Project No.=FIELD(Project No.),
                                                                                                Plot No.=FIELD(Plot No.),
                                                                                                House Model=FIELD(House Model),
                                                                                                Main Group=FIELD(Main Group),
                                                                                                Group=FIELD(Group),
                                                                                                Sub Group=FIELD(Sub Group),
                                                                                                Option=FIELD(Option),
                                                                                                Cost Type=CONST(Labor),
                                                                                                Budget Summary Line=FIELD(Summary Filter)));
                                                   CaptionML=[ENU=Total Hours;
                                                              NOR=Timer totalt;
                                                              SVE=Totalt timmar];
                                                   Editable=No }
    { 21  ;   ;Project Related Name;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project - Cost Object".Name WHERE (Project No.=FIELD(Project No.),
                                                                                                          Cost Object=FIELD(Cost Object)));
                                                   CaptionML=[ENU=Project Related Name;
                                                              NOR=Prosjektrelatert navn;
                                                              SVE=Projektrelaterat namn];
                                                   Description=Name Cost Object;
                                                   Editable=No }
    { 22  ;   ;Cost Type           ;Option        ;OnValidate=VAR
                                                                BudgetRateManagement@1100528600 : Codeunit 11012358;
                                                                BudgetRateFoundAt@1100528601 : Text[250];
                                                              BEGIN
                                                                IF "Cost Object" <> '' THEN BEGIN
                                                                  IF "Cost Type" <> xRec."Cost Type" THEN
                                                                    IF CONFIRM(Text003,TRUE) THEN BEGIN
                                                                      "Cost Object" := '';
                                                                      //Only fill fields below for Cost Type Plant
                                                                      "Item No." := '';
                                                                      "Basic Item" := '';
                                                                      "Trade Item" := '';
                                                                      Manufacturer := '';
                                                                    END ELSE BEGIN
                                                                      "Cost Type" := xRec."Cost Type";
                                                                      EXIT;
                                                                    END;
                                                                END;
                                                                IF ("Cost Type" = "Cost Type"::Labor) THEN BEGIN
                                                                  IF "Project No." <> '' THEN BEGIN
                                                                    ProjectRec.GET("Project No.");
                                                                    Rate := BudgetRateManagement.GetBudgetRate(
                                                                      0, "Project No.", '', '', ProjectRec."Global Dimension 1 Code", ProjectRec."Default Rate Code",
                                                                        TODAY, BudgetRateFoundAt);
                                                                    IF Rate <> 0 THEN
                                                                      "Rate Code" := ProjectRec."Default Rate Code"
                                                                    ELSE
                                                                      IF Price <> 0 THEN
                                                                        ERROR(Text005);
                                                                //    IF NOT RateRec.ProjectRate("Project No.", ProjectRec."Default Rate Code", TODAY, Rate) THEN BEGIN
                                                                //      IF RateRec.StandardRate(ProjectRec."Default Rate Code", TODAY, Rate) THEN BEGIN
                                                                //        "Rate Code" := ProjectRec."Default Rate Code";
                                                                //      END ELSE BEGIN
                                                                //        IF Price <> 0 THEN
                                                                //          ERROR(Text005);
                                                                //      END;
                                                                //    END ELSE BEGIN
                                                                //      "Rate Code" := ProjectRec."Default Rate Code";
                                                                //    END;
                                                                    IF (Rate <> 0) AND (Price <> 0) THEN
                                                                      Norm := Price / Rate;
                                                                    Price := 0;
                                                                  END;
                                                                  Price := 0;
                                                                END ELSE BEGIN
                                                                  IF (Rate <> 0) THEN
                                                                    Price := Norm * Rate;
                                                                  "Rate Code" := '';
                                                                  Rate := 0;
                                                                  Norm := 0;
                                                                END;
                                                                CalcTotals;
                                                              END;

                                                   CaptionML=[ENU=Cost Type;
                                                              NOR=Kosttype;
                                                              SVE=Kostnadstyp];
                                                   OptionCaptionML=[ENU=Labor,Material,Subcontracting,Plant,Sundry;
                                                                    NOR=Arbeidskraft,Materiale,Underentrepen›r,Maskin,Diverse;
                                                                    SVE=Arbetskraft,Material,Underentreprenad,Maskin,Diverse];
                                                   OptionString=Labor,Material,Subcontracting,Plant,Sundry }
    { 23  ;   ;Price               ;Decimal       ;OnValidate=BEGIN
                                                                IF ("Cost Type" = "Cost Type"::Labor) THEN BEGIN
                                                                  MESSAGE(Text004);
                                                                  Price := 0;
                                                                END ELSE BEGIN
                                                                  CalculateDiscount;  //db, 10-12-13: C007216
                                                                  CalcTotals;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Price;
                                                              NOR=Pris;
                                                              SVE=Pris];
                                                   AutoFormatType=2 }
    { 24  ;   ;Time Quantity       ;Decimal       ;InitValue=1;
                                                   OnValidate=BEGIN
                                                                CalcTotals;
                                                              END;

                                                   CaptionML=[ENU=Time Quantity;
                                                              NOR=Tidskvantitet;
                                                              SVE=Tidskvantitet] }
    { 25  ;   ;Unit of Time        ;Code10        ;TableRelation="Unit of Measure".Code;
                                                   CaptionML=[ENU=Unit of Time;
                                                              NOR=Tidsenhet;
                                                              SVE=Tidsenhet] }
    { 26  ;   ;Total Labor         ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Option Budget".Amount WHERE (Project No.=FIELD(Project No.),
                                                                                                 Plot No.=FIELD(Plot No.),
                                                                                                 House Model=FIELD(House Model),
                                                                                                 Main Group=FIELD(Main Group),
                                                                                                 Group=FIELD(Group),
                                                                                                 Sub Group=FIELD(Sub Group),
                                                                                                 Option=FIELD(Option),
                                                                                                 Cost Type=CONST(Labor),
                                                                                                 Budget Summary Line=FIELD(Summary Filter)));
                                                   CaptionML=[ENU=Total Labor;
                                                              NOR=Total arbeidskraft;
                                                              SVE=Total arbetskraft];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 27  ;   ;Total Material      ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Option Budget".Amount WHERE (Project No.=FIELD(Project No.),
                                                                                                 Plot No.=FIELD(Plot No.),
                                                                                                 House Model=FIELD(House Model),
                                                                                                 Main Group=FIELD(Main Group),
                                                                                                 Group=FIELD(Group),
                                                                                                 Sub Group=FIELD(Sub Group),
                                                                                                 Option=FIELD(Option),
                                                                                                 Cost Type=CONST(Material),
                                                                                                 Budget Summary Line=FIELD(Summary Filter)));
                                                   CaptionML=[ENU=Total Material;
                                                              NOR=Totalt materiale;
                                                              SVE=Totalt material];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 28  ;   ;Total Subcontracting;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Option Budget".Amount WHERE (Project No.=FIELD(Project No.),
                                                                                                 Plot No.=FIELD(Plot No.),
                                                                                                 House Model=FIELD(House Model),
                                                                                                 Main Group=FIELD(Main Group),
                                                                                                 Group=FIELD(Group),
                                                                                                 Sub Group=FIELD(Sub Group),
                                                                                                 Option=FIELD(Option),
                                                                                                 Cost Type=CONST(Subcontracting),
                                                                                                 Budget Summary Line=FIELD(Summary Filter)));
                                                   CaptionML=[ENU=Total Subcontracting;
                                                              NOR=Underleverand›r Total;
                                                              SVE=Underentreprenad totalt];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 29  ;   ;Total Plant         ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Option Budget".Amount WHERE (Project No.=FIELD(Project No.),
                                                                                                 Plot No.=FIELD(Plot No.),
                                                                                                 House Model=FIELD(House Model),
                                                                                                 Main Group=FIELD(Main Group),
                                                                                                 Group=FIELD(Group),
                                                                                                 Sub Group=FIELD(Sub Group),
                                                                                                 Option=FIELD(Option),
                                                                                                 Cost Type=CONST(Plant),
                                                                                                 Budget Summary Line=FIELD(Summary Filter)));
                                                   CaptionML=[ENU=Total Plant;
                                                              NOR=Maskin total;
                                                              SVE=Maskin totalt];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 30  ;   ;Total Sundry        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Option Budget".Amount WHERE (Project No.=FIELD(Project No.),
                                                                                                 Plot No.=FIELD(Plot No.),
                                                                                                 House Model=FIELD(House Model),
                                                                                                 Main Group=FIELD(Main Group),
                                                                                                 Group=FIELD(Group),
                                                                                                 Sub Group=FIELD(Sub Group),
                                                                                                 Option=FIELD(Option),
                                                                                                 Cost Type=CONST(Sundry),
                                                                                                 Budget Summary Line=FIELD(Summary Filter)));
                                                   CaptionML=[ENU=Total Sundry;
                                                              NOR=Totalt diverse;
                                                              SVE=Totalt diverse];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 31  ;   ;Total Summary       ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Option Budget".Amount WHERE (Project No.=FIELD(Project No.),
                                                                                                 Plot No.=FIELD(Plot No.),
                                                                                                 House Model=FIELD(House Model),
                                                                                                 Main Group=FIELD(Main Group),
                                                                                                 Group=FIELD(Group),
                                                                                                 Sub Group=FIELD(Sub Group),
                                                                                                 Option=FIELD(Option),
                                                                                                 Budget Summary Line=CONST(Yes)));
                                                   CaptionML=[ENU=Total Summary;
                                                              NOR=Totalt Sammendrag;
                                                              SVE=Totalt sammandrag];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 38  ;   ;Option              ;Code20        ;TableRelation=Option.Option WHERE (Project No.=FIELD(Project No.),
                                                                                      Plot No.=FIELD(Plot No.),
                                                                                      House Model=FIELD(House Model),
                                                                                      Main Group=FIELD(Main Group),
                                                                                      Group=FIELD(Group),
                                                                                      Sub Group=FIELD(Sub Group));
                                                   CaptionML=[ENU=Option;
                                                              NOR=Alternativ;
                                                              SVE=Alternativ] }
    { 40  ;   ;Chapter             ;Code20        ;CaptionML=[ENU=Chapter;
                                                              NOR=Kapittel;
                                                              SVE=Kapitel] }
    { 50  ;   ;Paragraph           ;Code20        ;CaptionML=[ENU=Paragraph;
                                                              NOR=Avsnitt;
                                                              SVE=Stycke] }
    { 85  ;   ;Element Filter      ;Code250       ;FieldClass=FlowFilter;
                                                   TableRelation="Project Element".Element WHERE (Project No.=FIELD(Project No.));
                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Element Filter;
                                                              NOR=Elementfilter;
                                                              SVE=Elementfilter] }
    { 100 ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   OnValidate=BEGIN
                                                                IF ("Item No." <> xRec."Item No.") AND ("Item No." <> '') THEN BEGIN
                                                                  ValidateItem(ItemTypeOpt::Item);
                                                                  ItemRec.TESTFIELD(Blocked,FALSE);           //jth, 15-02-05
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Item No.;
                                                              NOR=Artikkelnr;
                                                              SVE=Artikelnr] }
    { 101 ;   ;Basic Item          ;Code20        ;TableRelation="Basic Item"."Product Code" WHERE (Manufacturer=FIELD(Manufacturer));
                                                   OnValidate=BEGIN
                                                                IF ("Basic Item" <> xRec."Basic Item") AND ("Basic Item" <> '') THEN
                                                                  ValidateItem(ItemTypeOpt::"Basic Item");
                                                              END;

                                                   CaptionML=[ENU=Basic Item;
                                                              NOR=Fabrikantartikkel;
                                                              SVE=Basartikel] }
    { 102 ;   ;Trade Item          ;Code20        ;TableRelation="Trade Item"."Item Code" WHERE (Vendor=FIELD("Vendor (Trade Item)"));
                                                   OnValidate=BEGIN
                                                                IF ("Trade Item" <> xRec."Trade Item") AND ("Trade Item" <> '') THEN
                                                                  ValidateItem(ItemTypeOpt::"Trade Item");
                                                              END;

                                                   CaptionML=[ENU=Trade Item;
                                                              NOR=Katalogartikkel;
                                                              SVE=Anskaffningsartikel] }
    { 104 ;   ;Vendor (Trade Item) ;Code15        ;TableRelation="ETIM Vendor - Central Mgt";
                                                   OnValidate=VAR
                                                                TradeItemRec@1100525000 : Record 11012317;
                                                                lvExecValidate@1100485000 : Boolean;
                                                              BEGIN
                                                                IF ("Vendor (Trade Item)" <> xRec."Vendor (Trade Item)") AND ("Vendor (Trade Item)" <> '') THEN BEGIN
                                                                  IF "Basic Item" <> '' THEN BEGIN
                                                                    "Trade Item" := '';
                                                                    lvExecValidate := TRUE;
                                                                  END;
                                                                  IF "Trade Item" <> '' THEN BEGIN
                                                                    VALIDATE("Trade Item");
                                                                    lvExecValidate := TRUE;
                                                                  END;
                                                                  //db.sn, 17-12-10: M18021
                                                                  IF lvExecValidate THEN BEGIN
                                                                    IF TradeItemRec.CheckReplaceVendor(
                                                                      xRec."Vendor (Trade Item)", xRec."Trade Item",
                                                                      "Vendor (Trade Item)", "Trade Item") THEN
                                                                      ValidateItem(ItemTypeOpt::"Trade Item")
                                                                    ELSE
                                                                      ValidateItem(ItemTypeOpt::"Trade Vendor");
                                                                  END;
                                                                  //db.en, 17-12-10: M18021
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Vendor (Trade Item);
                                                              NOR=Leverand›r (katalogartikkel);
                                                              SVE=Leverant”r (skaffningsartikel)] }
    { 105 ;   ;Manufacturer        ;Code15        ;TableRelation="ETIM Vendor - Central Mgt";
                                                   CaptionML=[ENU=Manufacturer;
                                                              NOR=Produsent;
                                                              SVE=Tillverkare] }
    { 110 ;   ;Text                ;Text250       ;OnValidate=BEGIN
                                                                "Text Block" := Text <> '';
                                                              END;

                                                   CaptionML=[ENU=Text;
                                                              NOR=Tekst;
                                                              SVE=Ben„mning] }
    { 111 ;   ;Text Block          ;Boolean       ;CaptionML=[ENU=Text Block;
                                                              NOR=Tekstblokk;
                                                              SVE=Textblock];
                                                   Editable=No }
    { 120 ;   ;Recipe Group        ;Code10        ;TableRelation="Recipe Group".No.;
                                                   OnValidate=BEGIN
                                                                IF ("Recipe Group" = '') AND (Recipe <> '') THEN
                                                                  Recipe := '';
                                                              END;

                                                   CaptionML=[ENU=Recipe Group;
                                                              NOR=Maltype;
                                                              SVE=Malltyp];
                                                   Description=DP00381 }
    { 125 ;   ;Recipe              ;Code20        ;TableRelation="Base Recipe".No. WHERE (Recipe Group=FIELD(Recipe Group));
                                                   OnValidate=VAR
                                                                RecipeRec@1210190000 : Record 11020581;
                                                              BEGIN
                                                                IF (Recipe <> xRec.Recipe) AND (Recipe <> '') THEN BEGIN
                                                                  RecipeRec.GET("Recipe Group", Recipe);
                                                                  RecipeRec.TESTFIELD(Blocked, FALSE);
                                                                  Description := COPYSTR(RecipeRec.Description, 1, MAXSTRLEN(Description));
                                                                  "Description 2" := COPYSTR(RecipeRec.Description, MAXSTRLEN(Description) + 1);
                                                                  "Unit of Measure" := RecipeRec."Unit of Measure";
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Recipe;
                                                              NOR=Mal;
                                                              SVE=Mall];
                                                   Description=DP00381 }
    { 130 ;   ;Plot No.            ;Code10        ;TableRelation=Plot."Plot No." WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=[ENU=Plot No.;
                                                              NOR=Tegningnr;
                                                              SVE=Ritningnr] }
    { 150 ;   ;Budget Summary Line ;Boolean       ;CaptionML=[ENU=Budget Summary Line;
                                                              NOR=Budsjettsammendragsrad;
                                                              SVE=Budgetsammandragsrad] }
    { 160 ;   ;Gross Price         ;Decimal       ;OnValidate=BEGIN
                                                                CalculateNetPrice;
                                                                CalcTotals;
                                                              END;

                                                   CaptionML=[ENU=Gross Price;
                                                              NOR=Bruttopris;
                                                              SVE=Bruttopris];
                                                   BlankZero=Yes;
                                                   AutoFormatType=2 }
    { 170 ;   ;Discount %          ;Decimal       ;OnValidate=BEGIN
                                                                CalculateNetPrice;
                                                                CalcTotals;
                                                              END;

                                                   CaptionML=[ENU=Discount %;
                                                              NOR=Rabatt-%;
                                                              SVE=Rabatt %];
                                                   DecimalPlaces=0:5;
                                                   BlankZero=Yes }
    { 180 ;   ;House Model         ;Code20        ;TableRelation="House Model".Model WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=[ENU=House Model;
                                                              NOR=Husmodell;
                                                              SVE=Husmodell];
                                                   NotBlank=Yes }
    { 190 ;   ;Main Group          ;Code20        ;TableRelation="Option Main Group".Code WHERE (Project No.=FIELD(Project No.),
                                                                                                 Plot No.=FIELD(Plot No.));
                                                   CaptionML=[ENU=Main Group;
                                                              NOR=Hovedgruppe;
                                                              SVE=Huvudgrupp];
                                                   NotBlank=Yes }
    { 200 ;   ;Group               ;Code20        ;TableRelation="Option Group".Code WHERE (Project No.=FIELD(Project No.),
                                                                                            Plot No.=FIELD(Plot No.));
                                                   CaptionML=[ENU=Group;
                                                              NOR=Gruppe;
                                                              SVE=Grupp];
                                                   NotBlank=Yes }
    { 210 ;   ;Sub Group           ;Code20        ;TableRelation="Option Sub Group".Code WHERE (Project No.=FIELD(Project No.),
                                                                                                Plot No.=FIELD(Plot No.));
                                                   CaptionML=[ENU=Sub Group;
                                                              NOR=Undergruppe;
                                                              SVE=Undergrupp];
                                                   NotBlank=Yes }
    { 250 ;   ;Source Type         ;Option        ;CaptionML=[ENU=Source Type;
                                                              NOR=Kildetype;
                                                              SVE=Ursprungstyp];
                                                   OptionCaptionML=[ENU=" ,Recipe Header,Recipe Line";
                                                                    NOR=" ,Maltittel,Malrad";
                                                                    SVE=" ,Mallrubrik,Mallrad"];
                                                   OptionString=[ ,Recipe Header,Recipe Line] }
    { 255 ;   ;Attached to Recipe Line;Integer    ;TableRelation="Option Budget"."Line No." WHERE (Project No.=FIELD(Project No.),
                                                                                                   Plot No.=FIELD(Plot No.),
                                                                                                   House Model=FIELD(House Model),
                                                                                                   Main Group=FIELD(Main Group),
                                                                                                   Group=FIELD(Group),
                                                                                                   Sub Group=FIELD(Sub Group),
                                                                                                   Option=FIELD(Option));
                                                   CaptionML=[ENU=Attached to Recipe Line;
                                                              NOR=Tilknyttet Malrad;
                                                              SVE=Bifogad till mallrad];
                                                   Editable=No }
    { 260 ;   ;Cost Component      ;Code20        ;TableRelation="Cost Component".Code;
                                                   CaptionML=[ENU=Cost Component;
                                                              NOR=Kostnadskomponent;
                                                              SVE=Kostnadsdel] }
    { 270 ;   ;Summary Filter      ;Boolean       ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Summary Filter;
                                                              NOR=Sammendragfilter;
                                                              SVE=Sammandrag filter] }
    { 300 ;   ;Option Description  ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Option.Description WHERE (Project No.=FIELD(Project No.),
                                                                                                Plot No.=FIELD(Plot No.),
                                                                                                House Model=FIELD(House Model),
                                                                                                Main Group=FIELD(Main Group),
                                                                                                Group=FIELD(Group),
                                                                                                Sub Group=FIELD(Sub Group),
                                                                                                Option=FIELD(Option)));
                                                   CaptionML=[ENU=Option Description;
                                                              NOR=Alternativbeskrivelse;
                                                              SVE=Alternativbeskrivning];
                                                   Editable=No }
    { 540 ;   ;Surcharge Amnt from Summary;Decimal;CaptionML=[ENU=Overhead Surcharge Amount from Summary;
                                                              NOR=Overheadkostbel›p fra sammendrag;
                                                              SVE=Overheadkostnadsbelopp fr†n sammandrag] }
    { 550 ;   ;Overhead Surcharge from Summ.;Boolean;
                                                   CaptionML=[ENU=Overhead Surcharge from Summary;
                                                              NOR=Overheadkost fra sammendrag;
                                                              SVE=Overheadkostnad fr†n sammandrag] }
    { 560 ;   ;Part of Line        ;Integer       ;TableRelation="Option Budget"."Line No." WHERE (Project No.=FIELD(Project No.),
                                                                                                   Plot No.=FIELD(Plot No.),
                                                                                                   House Model=FIELD(House Model),
                                                                                                   Main Group=FIELD(Main Group),
                                                                                                   Group=FIELD(Group),
                                                                                                   Sub Group=FIELD(Sub Group),
                                                                                                   Option=FIELD(Option));
                                                   OnValidate=BEGIN
                                                                DetermineBOMLevel("Part of Line","Level BOM");
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Part of Line;
                                                              SVE=Del av rad] }
    { 565 ;   ;Original Quantity   ;Decimal       ;DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Original Quantity;
                                                              SVE=Ursprungligt antal];
                                                   DecimalPlaces=0:5;
                                                   BlankZero=Yes }
    { 570 ;   ;Level BOM           ;Integer       ;DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Level BOM;
                                                              SVE=Niv†struktur];
                                                   Description=Level after exploding BOM-Item;
                                                   Editable=No }
    { 575 ;   ;BOM Item No.        ;Code20        ;TableRelation=Item;
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=BOM Item No.;
                                                              SVE=Strukturartikelnr];
                                                   Description=Original Item before exploding BOM }
    { 680 ;   ;Location Code (CUF) ;Code20        ;DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Location Code (CUF) }
  }
  KEYS
  {
    {    ;Project No.,Plot No.,House Model,Main Group,Group,Sub Group,Option,Line No.;
                                                   SumIndexFields=Hours,Amount;
                                                   MaintainSIFTIndex=No;
                                                   Clustered=Yes }
    {    ;Project No.,Option,Cost Type            ;SumIndexFields=Hours,Amount }
    {    ;Project No.,Plot No.,House Model,Main Group,Group,Sub Group,Option,Budget Summary Line,Line No.;
                                                   SumIndexFields=Hours,Amount }
    {    ;Project No.,Plot No.,House Model,Main Group,Group,Sub Group,Option,Budget Summary Line,Cost Type,Line No.;
                                                   SumIndexFields=Hours,Amount }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@11012014 : TextConst 'ENU=Cost Type of Line is %1, input of norm not allowed;NOR=Kostnadstypen for raden er %1, Norm kan ikke angis;SVE=Kostnadstypen f”r raden „r %1, norm kan inte anges';
      Text002@11012015 : TextConst 'ENU=Cost Type of line is %1, input of rate code not allowed.;NOR=Kostnadstypen for raden er %1, satskode kan ikke angis.;SVE=Kostnadstypen f”r raden „r %1, avgiftskod kan inte anges';
      Text003@11012016 : TextConst 'ENU=Change Cost Type ?;NOR=Endre kostnadstype?;SVE=Žndra kostnadstyp?';
      Text004@11012017 : TextConst 'ENU=Unit Price not allowed because Cost Type of this record is Labor !;NOR=Enhetsprisen er ikke tillatt siden Kostnadstypen for denne posten er arbeidskraft!;SVE=Enhetspriset „r inte till†tet eftersom kostnadstypen f”r denna post „r arbetskraft!';
      DimValRec@11012002 : Record 349;
      ProjectRec@11012003 : Record 11072003;
      PlotRec@1100525001 : Record 11012500;
      ElemRec@11012006 : Record 11012060;
      ProjElemRec@11012007 : Record 11012010;
      ItemRec@1210190001 : Record 27;
      DummyOptionSurchRec@1210190004 : Record 11012512;
      ElementFormat@11012010 : Codeunit 11012022;
      ValidateCU@11012011 : Codeunit 11012033;
      DimMgtCU@1210190003 : Codeunit 408;
      BuyerMgtCU@1210190002 : Codeunit 11012500;
      ItemTypeOpt@11012012 : 'Item,Basic Item,Trade Item,Trade Vendor';
      Trigger@1210190000 : 'Insert,Modify,Delete';
      Text005@1100485000 : TextConst 'ENU=Switch to Cost Type Labor requires default Rate Code per Project (or there is no Budget Rate of this Budget Rate present before the current date).;NOR=Bytting til Kostnadstypen arbeidskraft krever Standard satskode per prosjekt (eller det mangler budsjettsats for denne budsjettsatsen f›r den aktuelle datoen).;SVE=V„xling till kostnadstypen arbetskraft kr„ver en standardavgiftskod per projekt (annars finns det ingen budgetavgift f”r denna budgetavgift f”re det aktuella datumet).';
      Text006@1100525000 : TextConst 'ENU=&Complete with Cost Object Data,Co&py Cost Object Data,&Don''''t Copy Cost Object Data;NOR=&Komplett med Kostnadsobjektdata,Ko&piere Kostnadsobjektdata,&Ikke kopier Kostnadsobjektdata;SVE=&Komplett med kostnadsobjektdata,Ko&piera kostnadsobjektdata,&Kopiera inte kostnadsobjektdata';
      Text007@1100529400 : TextConst 'ENU=(%1) does not exist';
      Text020@1100525501 : TextConst 'ENU=First select Budget Lines;NOR=Velg f›rst Budsjettrader;SVE=V„lj f”rst budgetrader';
      Text021@1100525500 : TextConst 'ENU=Copied %1 Lines;NOR=%1 Rader er kopiert.;SVE=%1 rader har kopierats.';
      Text099@1100529500 : TextConst 'ENU=Update Summary #1###############;NOR=Oppdater sammendrag #1###############;SVE=Uppdatera sammandrag #1###############';
      TmpProjOptionSummaryRec@1100529501 : TEMPORARY Record 11012064;
      Text000@1100527350 : TextConst 'ENU=Description;SVE=Beskrivning';

    PROCEDURE CalcTotals@1();
    BEGIN
      CASE "Cost Type" OF
        "Cost Type"::Labor:
          BEGIN
            Hours := ROUND("Time Quantity" * Quantity * Norm);
      //      Amount := Hours * Rate;  jth 09-03-2005 Round hours and amount. (made identical to Budget Line.)
            IF (Quantity <> 0) OR // Amount can be entered manually when Quantity = 0 and Norm = 0;
               (Norm <> 0)
            THEN
              Amount := ROUND("Time Quantity" * Quantity * Norm * Rate);
          END;
        ELSE
          BEGIN
            Hours := 0;
            Amount := ROUND("Time Quantity" * Quantity * Price);
          END;
      END;
      CALCFIELDS("Total Amount","Total Hours","Total Labor","Total Material","Total Subcontracting","Total Plant","Total Sundry");
      UpdateControlStatus;
    END;

    PROCEDURE CalculateNetPrice@13();
    BEGIN
      //IF "Gross Price" <> 0 THEN  //db, 10-12-13: C007216
        Price := "Gross Price" - (("Gross Price"/100) * "Discount %");
    END;

    PROCEDURE CalculateDiscount@1100525024();
    BEGIN
      //db, 10-12-13: C007216
      IF "Gross Price" <> 0 THEN BEGIN
        IF ("Item No." + "Basic Item" + "Trade Item" = '') AND ("Discount %" = 0) THEN
          "Gross Price" := Price
        ELSE
          "Discount %" := (("Gross Price" - Price) / "Gross Price") * 100;
      END ELSE BEGIN
        IF "Discount %" <> 100 THEN
          "Gross Price" := Price / ((100 - "Discount %") / 100)
        ELSE
          "Gross Price" := Price;
      END;
    END;

    PROCEDURE GetDefaultsCostObject@3(Replace@1100525001 : Boolean);
    VAR
      BudgetRateManagement@1100528600 : Codeunit 11012358;
      BudgetRateFoundAt@1100528601 : Text[250];
    BEGIN
      //Function can be used in 2 ways: 1. only add data, 2: replace data
      DimMgtCU.GetDimValueRec(2, "Cost Object", DimValRec, TRUE, "Project No.");
      DimValRec.TESTFIELD(Blocked, FALSE);

      IF (Description = '') OR Replace THEN
        Description := DimValRec.Name;
      IF ("Unit of Measure" = '') OR Replace  THEN
        VALIDATE("Unit of Measure", DimValRec."Unit of Measure");
      IF ("Unit of Time" = '') OR Replace  THEN
        VALIDATE("Unit of Time", DimValRec."Unit of Time");
      IF ("Cost Component" = '') OR Replace  THEN
        VALIDATE("Cost Component", DimValRec."Cost Component");

      CASE "Cost Type" OF
        "Cost Type"::Labor:
          BEGIN
            IF (Norm = 0) OR Replace  THEN
              Norm := DimValRec.Norm;
            IF NOT ProjectRec.GET("Project No.") THEN  //*C-025993.n
              ProjectRec."Default Rate Code" := '';
            //IF (("Rate Code" = '') AND (Rate = 0)) OR Replace THEN BEGIN  //*C-025993.o
            IF (("Rate Code" = '') AND (Rate = 0)) OR (Replace AND (ProjectRec."Default Rate Code" = '')) THEN BEGIN //*C-025993.n
              "Rate Code" := DimValRec."Rate Code";
              Rate := BudgetRateManagement.GetBudgetRate(
                0, "Project No.", '', '', ProjectRec."Global Dimension 1 Code", "Rate Code", TODAY, BudgetRateFoundAt);
            END;
          END;
        ELSE
          IF (Price = 0) OR Replace  THEN
            Price := DimValRec.Price;
      END;
      CalcTotals;
    END;

    PROCEDURE CheckOptionStatus@1100525000();
    VAR
      lvOptionRec@1100525000 : Record 11012502;
      lvText000@1100525001 : TextConst 'ENU=has the status %1'', modify option budget not allowed;NOR=har status %1'', † endre Budsjettjustering er ikke tillatt;SVE=har status %1'', att „ndra alternativbudget „r inte till†tet';
    BEGIN
      IF ("Project No." = '') OR ("Plot No." = '') OR (Option = '') THEN
        EXIT;

      IF lvOptionRec.GET("Project No.","Plot No.","House Model","Main Group",Group,"Sub Group",Option) THEN BEGIN
        IF (lvOptionRec.Status >= lvOptionRec.Status::Order) THEN BEGIN
          FIELDERROR(Option, STRSUBSTNO(lvText000, lvOptionRec.Status));
        END;
      END;
    END;

    PROCEDURE UpdateControlStatus@10();
    BEGIN
      IF ("Project No." = '') OR ("Plot No." = '') OR (Option = '') OR ("Line No." = 0) THEN
        EXIT;
      IF ProjectRec.GET("Project No.") THEN BEGIN
        IF ProjectRec."Cost Control Up to Date" THEN BEGIN
          ProjectRec."Cost Control Up to Date" := FALSE;
          ProjectRec.MODIFY;
        END;
      END;
    END;

    PROCEDURE ValidateItem@31(LTypeArtOpt@11012000 : 'Item,Basic Item,Trade Item');
    VAR
      DummyDat@11012001 : Date;
      lvRefDate@1100485002 : Date;
      lvDiscRef1@1100485001 : Code[20];
      lvDiscRef2@1100485000 : Code[20];
    BEGIN
      IF NOT ProjectRec.GET("Project No.") THEN ProjectRec.INIT;
      lvRefDate := ValidateCU.GetRefDateProject(ProjectRec);
      lvDiscRef1 := ProjectRec."Purch Discount Term Group 1";
      lvDiscRef2 := ProjectRec."Purch Discount Term Group 2";

      ValidateCU.ValidateItem(LTypeArtOpt,"Trade Item","Item No.",Manufacturer,"Vendor (Trade Item)",
                              "Basic Item","Cost Object",Description,"Unit of Measure",Price,
                              "Discount %",DummyDat,"Gross Price","Description 2","Cost Component",
                              lvDiscRef1,lvDiscRef2,lvRefDate,'');
      CalcTotals;
      IF ItemRec.GET("Item No.") THEN
        VALIDATE(Text, ItemRec.Text);
    END;

    PROCEDURE UpdateOtherTables@6(ITrigger@1210190000 : 'Insert,Modify,Delete');
    BEGIN
      CASE ITrigger OF
        ITrigger::Insert: INSERT;
        ITrigger::Modify: MODIFY;
        ITrigger::Delete: DELETE;
      END;

      BuyerMgtCU.BmCalcSurchargesUpdateOption(
        "Project No.", "Plot No.", "House Model", "Main Group", Group, "Sub Group", Option, FALSE, DummyOptionSurchRec);

      CASE ITrigger OF
        ITrigger::Insert: DELETE;
        ITrigger::Delete: INSERT;
      END;
    END;

    PROCEDURE HandleLookupUnit@1210190008();
    VAR
      ItemUnitRec@1210190000 : Record 5404;
      lvUnit@1210190001 : Code[10];
    BEGIN
      lvUnit := ItemUnitRec.HandleLookupUnit("Item No.", "Basic Item", "Trade Item", Manufacturer, "Vendor (Trade Item)");
      IF (lvUnit <> '') AND (lvUnit <> "Unit of Measure") THEN BEGIN
        xRec."Unit of Measure" := "Unit of Measure";  //db, 01-07-04: standard bug
        VALIDATE("Unit of Measure", lvUnit);
      END;
    END;

    PROCEDURE CheckItemUnitConversion@1210190010(VAR MainNewRec@1210190006 : Record 11012503;MainOldRec@1210190007 : Record 11012503);
    VAR
      ItemRec@1210190004 : Record 27;
      UnitFactor@1210190005 : Decimal;
      UnitFactorOld@1210190003 : Decimal;
      UnitFactorNew@1210190002 : Decimal;
      UnitControl@1210190001 : Codeunit 5402;
    BEGIN
      WITH MainNewRec DO BEGIN
        UnitFactorOld := 1;
        UnitFactorNew := 1;
        IF "Item No." <> '' THEN BEGIN
          ItemRec.GET("Item No.");
          UnitFactorOld := UnitControl.GetQtyPerUnitOfMeasure(ItemRec, MainOldRec."Unit of Measure");
          UnitFactorNew := UnitControl.GetQtyPerUnitOfMeasure(ItemRec, "Unit of Measure");
          UnitFactor := UnitFactorOld;
          ValidateCU.ValidateUnit(ItemTypeOpt::Item,
            "Item No.","Basic Item","Trade Item",Manufacturer,"Vendor (Trade Item)",
            Quantity,"Unit of Measure",UnitFactor);
        END ELSE BEGIN
          IF "Trade Item" <> '' THEN BEGIN
            ValidateCU.ValidateUnit(ItemTypeOpt::"Trade Item",
              "Item No.","Basic Item","Trade Item",Manufacturer,"Vendor (Trade Item)",
              Quantity,"Unit of Measure",UnitFactorNew);
          END ELSE BEGIN
            IF "Basic Item" <> '' THEN BEGIN
              ValidateCU.ValidateUnit(ItemTypeOpt::"Basic Item",
                "Item No.","Basic Item","Trade Item",Manufacturer,"Vendor (Trade Item)",
                Quantity,"Unit of Measure",UnitFactorNew);
            END;
          END;
        END;
        IF (UnitFactorNew * UnitFactorOld) <> 0 THEN BEGIN
          IF UnitFactorNew <> UnitFactorOld THEN BEGIN //db, 20-04-11: M26826
            IF "Gross Price" <> 0 THEN
              VALIDATE("Gross Price", ("Gross Price" * UnitFactorNew / UnitFactorOld))
            ELSE
              VALIDATE(Price, (Price * UnitFactorNew / UnitFactorOld));
          END;
        END;
      END;
    END;

    PROCEDURE CheckRecipeTotals@1210190002();
    VAR
      OptionBudget@1210190000 : Record 11012503;
      OldQuan@1210190001 : Decimal;
      NewQuan@1210190002 : Decimal;
    BEGIN
      IF "Source Type" <> "Source Type"::"Recipe Header" THEN EXIT;

      //DP02339.sn
      OldQuan := xRec.Quantity;
      NewQuan := Quantity;
      //DP02339.en

      OptionBudget.SETRANGE("Project No.", "Project No.");
      OptionBudget.SETRANGE("Plot No.", "Plot No.");
      OptionBudget.SETRANGE("House Model", "House Model");
      OptionBudget.SETRANGE("Main Group", "Main Group");
      OptionBudget.SETRANGE(Group, Group);
      OptionBudget.SETRANGE("Sub Group", "Sub Group");
      OptionBudget.SETRANGE(Option, Option);

      OptionBudget.SETRANGE("Source Type", OptionBudget."Source Type"::"Recipe Line");
      OptionBudget.SETRANGE("Recipe Group", "Recipe Group");
      OptionBudget.SETRANGE(Recipe, Recipe);
      OptionBudget.SETRANGE("Attached to Recipe Line", "Line No.");
      IF OptionBudget.FINDSET(TRUE, FALSE) THEN BEGIN
        REPEAT
          IF OldQuan = 0 THEN
            OptionBudget.Quantity := OptionBudget."Original Quantity" * NewQuan  //DP02339
          ELSE
            OptionBudget.Quantity := OptionBudget.Quantity * NewQuan / OldQuan;
          OptionBudget.CalcTotals;
          OptionBudget.MODIFY(TRUE);
        UNTIL OptionBudget.NEXT = 0;
      END;
    END;

    PROCEDURE CheckInsertSummarySheet@1100525001(iRecalcMode@1100525002 : Boolean);
    VAR
      lvSummarySheetLineRec@1100525001 : Record 11012064;
      lvOptionBudgetLineRec@1100525000 : Record 11012503;
    BEGIN
      IF NOT "Budget Summary Line" AND ("Project No." <> '' ) THEN BEGIN
        ProjectRec.GET("Project No.");
        //IF ProjectRec."Summary Sheet Project" <> '' THEN BEGIN  //*C-026075.o
        IF ProjectRec."Summary Sheet Option" <> '' THEN BEGIN  //*C-026075.n
          lvOptionBudgetLineRec.SETFILTER("Project No.", '%1', "Project No.");
          lvOptionBudgetLineRec.SETFILTER("Plot No.", '%1', "Plot No.");
          lvOptionBudgetLineRec.SETFILTER("House Model", '%1', "House Model");
          lvOptionBudgetLineRec.SETFILTER("Main Group", '%1', "Main Group");
          lvOptionBudgetLineRec.SETFILTER(Group, '%1', Group);
          lvOptionBudgetLineRec.SETFILTER("Sub Group", '%1', "Sub Group");
          lvOptionBudgetLineRec.SETFILTER(Option, '%1', Option);
          IF NOT lvOptionBudgetLineRec.FINDFIRST THEN BEGIN
            // Add default summary sheet only at inserting of first line of a budget
            lvSummarySheetLineRec.SETRANGE("Project No.", "Project No.");
            lvSummarySheetLineRec.SETRANGE("Contract No.", '');
            lvSummarySheetLineRec.SETRANGE("Plot No.", "Plot No.");
            lvSummarySheetLineRec.SETRANGE("House Model", "House Model");
            lvSummarySheetLineRec.SETRANGE("Main Group", "Main Group");
            lvSummarySheetLineRec.SETRANGE(Group, Group);
            lvSummarySheetLineRec.SETRANGE("Sub Group", "Sub Group");
            lvSummarySheetLineRec.SETRANGE(Option, Option);
            IF NOT lvSummarySheetLineRec.FINDFIRST THEN BEGIN
              lvSummarySheetLineRec.RESET;

              lvSummarySheetLineRec.CopyTemplate2(ProjectRec."Summary Sheet Option", "Project No.", '', "Plot No.",
                                                  "House Model", "Main Group", Group, "Sub Group", Option);

              IF iRecalcMode THEN BEGIN
                lvSummarySheetLineRec.SETRANGE("Project No.", "Project No.");
                lvSummarySheetLineRec.SETRANGE("Contract No.", '');
                lvSummarySheetLineRec.SETRANGE("Plot No.", "Plot No.");
                lvSummarySheetLineRec.SETRANGE("House Model", "House Model");
                lvSummarySheetLineRec.SETRANGE("Main Group", "Main Group");
                lvSummarySheetLineRec.SETRANGE(Group, Group);
                lvSummarySheetLineRec.SETRANGE("Sub Group", "Sub Group");
                lvSummarySheetLineRec.SETRANGE(Option, Option);
                IF lvSummarySheetLineRec.FINDFIRST THEN BEGIN
                  lvSummarySheetLineRec.ReCalcOptionSummaryLines;
                  lvSummarySheetLineRec.UpdateOptBudgetLines;
                END;
              END;
            END;
          END;
        END;
      END;
    END;

    PROCEDURE CopyAndPasteRTC@1100525008(ToProjectNo@1100525506 : Code[20];ToPlotNo@1100525505 : Code[20];ToHouseModel@1100525504 : Code[20];ToMainGroup@1100525503 : Code[20];ToGroup@1100525502 : Code[20];ToSubGroup@1100525501 : Code[20];ToOption@1100525500 : Code[20]);
    VAR
      OptionBudget@1100525000 : Record 11012503;
      OptionBudget2@1100525009 : Record 11012503;
      BaseElement@1100528501 : Record 11012060;
      ProjectElement@1100528500 : Record 11012010;
      ElementExist@1100528502 : Boolean;
      Counter@1100525003 : Integer;
    BEGIN
      IF NOT FINDSET(FALSE,FALSE) THEN
        ERROR(Text020);
      REPEAT;
        OptionBudget := Rec;
        OptionBudget."Project No." := ToProjectNo;
        OptionBudget."Plot No." := ToPlotNo;
        OptionBudget."House Model" := ToHouseModel;
        OptionBudget."Main Group" := ToMainGroup;
        OptionBudget.Group := ToGroup;
        OptionBudget."Sub Group" := ToSubGroup;
        OptionBudget.Option := ToOption;
        IF OptionBudget.Element <> '' THEN BEGIN
          IF OptionBudget."Project No." = '' THEN
            ElementExist := BaseElement.GET(OptionBudget.Element)
          ELSE
            ElementExist := ProjectElement.GET(OptionBudget."Project No.", OptionBudget.Element);
          IF NOT ElementExist THEN BEGIN
            OptionBudget.Element := '';
            OptionBudget.Chapter := '';
            OptionBudget.Paragraph := '';
          END;
        END;
        OptionBudget2.SETRANGE("Project No.", OptionBudget."Project No.");
        OptionBudget2.SETRANGE("Plot No.", OptionBudget."Plot No.");
        OptionBudget2.SETRANGE("House Model", OptionBudget."House Model");
        OptionBudget2.SETRANGE("Main Group", OptionBudget."Main Group");
        OptionBudget2.SETRANGE(Group, OptionBudget.Group);
        OptionBudget2.SETRANGE("Sub Group", OptionBudget."Sub Group");
        OptionBudget2.SETRANGE(Option, OptionBudget.Option);
        IF NOT OptionBudget2.FINDLAST THEN
          OptionBudget2."Line No." := 0;
        OptionBudget."Line No." := OptionBudget2."Line No." + 10000;
        OptionBudget.INSERT(TRUE);
        Counter := Counter + 1;
      UNTIL NEXT = 0;
      MESSAGE(Text021, Counter);
    END;

    PROCEDURE LookupRecipe@1100525002(VAR SaveRec@1100529600 : Boolean);
    VAR
      RecipeFrm@1100525001 : Page 11125073;
      RecipeRec@1100525000 : Record 11020581;
      CountOld@1100529601 : Integer;
    BEGIN
      //DP00381
      CountOld := COUNT;
      RecipeFrm.LOOKUPMODE(TRUE);
      RecipeFrm.SetSelection(
        DATABASE::"Option Budget","Project No.","Plot No.",Option,Element,'',"House Model", "Main Group",Group,"Sub Group",TRUE,"Line No.");  //C027336
      IF "Recipe Group" <> '' THEN
        RecipeRec.SETRANGE("Recipe Group","Recipe Group");
      RecipeFrm.SETTABLEVIEW(RecipeRec);
      IF RecipeFrm.RUNMODAL = ACTION::LookupOK THEN BEGIN
        RecipeFrm.GETRECORD(RecipeRec);
        "Recipe Group" := RecipeRec."Recipe Group";
        VALIDATE(Recipe, RecipeRec."No.");
      END;
      SaveRec := CountOld = COUNT;
    END;

    PROCEDURE DrillDownOnTotals@1100525003(FieldNumber@1100525000 : Integer;InclSummary@1100525002 : Boolean);
    VAR
      OptionBudget@1100525001 : Record 11012503;
    BEGIN
      //C024496
      OptionBudget.SETRANGE("Project No.", "Project No.");
      OptionBudget.SETRANGE("Plot No.", "Plot No.");
      OptionBudget.SETRANGE("House Model", "House Model");
      OptionBudget.SETRANGE("Main Group", "Main Group");
      OptionBudget.SETRANGE(Group, Group);
      OptionBudget.SETRANGE("Sub Group", "Sub Group");
      OptionBudget.SETRANGE(Option, Option);
      IF NOT InclSummary THEN
          OptionBudget.SETRANGE("Budget Summary Line", FALSE);
      CASE FieldNumber OF
        FIELDNO("Total Labor"):
          OptionBudget.SETRANGE("Cost Type", OptionBudget."Cost Type"::Labor);
        FIELDNO("Total Material"):
          OptionBudget.SETRANGE("Cost Type", OptionBudget."Cost Type"::Material);
        FIELDNO("Total Subcontracting"):
          OptionBudget.SETRANGE("Cost Type", OptionBudget."Cost Type"::Subcontracting);
        FIELDNO("Total Plant"):
          OptionBudget.SETRANGE("Cost Type", OptionBudget."Cost Type"::Plant);
        FIELDNO("Total Sundry"):
          OptionBudget.SETRANGE("Cost Type", OptionBudget."Cost Type"::Sundry);
        FIELDNO("Total Amount"):
          OptionBudget.SETRANGE("Cost Type");
        FIELDNO("Total Hours"):
          OptionBudget.SETRANGE("Cost Type", OptionBudget."Cost Type"::Labor);
        FIELDNO("Total Summary"):
          OptionBudget.SETRANGE("Budget Summary Line", TRUE);
      END;
      PAGE.RUNMODAL(PAGE::"Option Budget Lines", OptionBudget);
    END;

    PROCEDURE ValidateUnitOfMeasure@1100528400(UnitOfMeasureCode@1100528400 : Code[10]);
    BEGIN
      "Unit of Measure" := UnitOfMeasureCode;
      IF ("Cost Type" = "Cost Type"::Material) THEN
        IF "Unit of Measure" <>  xRec."Unit of Measure" THEN  //db, 10-02-05
          CheckItemUnitConversion(Rec, xRec);
      CalcTotals;
    END;

    PROCEDURE ProcessSummarySheet@1100529402(OptionBudget@1100529401 : Record 11012503);
    BEGIN
      //DP01269
      FillChangedSummarySheet;
      RecalcSummarySheet;
    END;

    PROCEDURE FillChangedSummarySheet@1100485002();
    VAR
      lvProjOptionSummaryRec@1100485001 : Record 11012064;
    BEGIN
      //DP01269 (moved from page 11012513)
      lvProjOptionSummaryRec.SETRANGE("Project No.", "Project No.");
      lvProjOptionSummaryRec.SETRANGE("Contract No.", '');
      lvProjOptionSummaryRec.SETRANGE("Plot No.", "Plot No.");  //* 13303
      lvProjOptionSummaryRec.SETRANGE("House Model", "House Model");
      lvProjOptionSummaryRec.SETRANGE("Main Group", "Main Group");
      lvProjOptionSummaryRec.SETRANGE(Group, Group);
      lvProjOptionSummaryRec.SETRANGE("Sub Group", "Sub Group");
      lvProjOptionSummaryRec.SETRANGE(Option, Option);
      IF NOT lvProjOptionSummaryRec.FIND('-') THEN
        EXIT;

      TmpProjOptionSummaryRec.RESET;
      TmpProjOptionSummaryRec.SETRANGE("Project No.", "Project No.");
      TmpProjOptionSummaryRec.SETRANGE("Contract No.", '');
      TmpProjOptionSummaryRec.SETRANGE("Plot No.", "Plot No.");  //* 13303
      TmpProjOptionSummaryRec.SETRANGE("House Model", "House Model");
      TmpProjOptionSummaryRec.SETRANGE("Main Group", "Main Group");
      TmpProjOptionSummaryRec.SETRANGE(Group, Group);
      TmpProjOptionSummaryRec.SETRANGE("Sub Group", "Sub Group");
      TmpProjOptionSummaryRec.SETRANGE(Option, Option);
      IF NOT TmpProjOptionSummaryRec.FIND('-') THEN BEGIN
        TmpProjOptionSummaryRec.INIT;
        TmpProjOptionSummaryRec."Project No." := "Project No.";
        TmpProjOptionSummaryRec."Contract No." := '';
        TmpProjOptionSummaryRec."Plot No." := "Plot No.";  //* 13303
        TmpProjOptionSummaryRec."House Model" := "House Model";
        TmpProjOptionSummaryRec."Main Group" := "Main Group";
        TmpProjOptionSummaryRec.Group := Group;
        TmpProjOptionSummaryRec."Sub Group" := "Sub Group";
        TmpProjOptionSummaryRec.Option := Option;
        TmpProjOptionSummaryRec.INSERT;
      END;
      TmpProjOptionSummaryRec.RESET;
    END;

    PROCEDURE RecalcSummarySheet@1100485006() : Boolean;
    VAR
      lvProjOptionSummaryRec@1100485000 : Record 11012064;
      CalcSummaryWindow@1100529400 : Dialog;
    BEGIN
      //DP01269 (moved from page 11012513)
      IF NOT TmpProjOptionSummaryRec.FIND('-') THEN
        EXIT(FALSE);

      CalcSummaryWindow.OPEN(Text099);

      REPEAT
        CalcSummaryWindow.UPDATE(1, "Project No.");

        lvProjOptionSummaryRec.SETRANGE("Project No.", TmpProjOptionSummaryRec."Project No.");
        lvProjOptionSummaryRec.SETRANGE("Contract No.", '');
        lvProjOptionSummaryRec.SETRANGE("Plot No.", TmpProjOptionSummaryRec."Plot No.");  //* 13303
        lvProjOptionSummaryRec.SETRANGE("House Model", TmpProjOptionSummaryRec."House Model");
        lvProjOptionSummaryRec.SETRANGE("Main Group", TmpProjOptionSummaryRec."Main Group");
        lvProjOptionSummaryRec.SETRANGE(Group, TmpProjOptionSummaryRec.Group);
        lvProjOptionSummaryRec.SETRANGE("Sub Group", TmpProjOptionSummaryRec."Sub Group");
        lvProjOptionSummaryRec.SETRANGE(Option, TmpProjOptionSummaryRec.Option);
        IF lvProjOptionSummaryRec.FIND('-') THEN BEGIN
          lvProjOptionSummaryRec.ReCalcOptionSummaryLines();
          lvProjOptionSummaryRec.UpdateOptBudgetLines();
        END;
        TmpProjOptionSummaryRec.DELETE;
      UNTIL TmpProjOptionSummaryRec.NEXT = 0;

      CalcSummaryWindow.CLOSE;

      EXIT(TRUE);
    END;

    PROCEDURE LineIsEmpty@1100529600() : Boolean;
    BEGIN
      IF (Quantity <> 0) OR (Rate <> 0) OR (Price <> 0) OR (Hours <> 0) OR (Amount <> 0) THEN
        EXIT(FALSE);
      IF (Recipe <> '') OR ("Cost Object" <> '') OR (Element <> '') OR
         (Description <> '') OR ("Description 2" <> '')
      THEN
        EXIT(FALSE);
      IF ("Item No." <> '') OR ("Vendor (Trade Item)" <> '') OR
         (Manufacturer <> '') OR ("Basic Item" <> '') OR ("Trade Item" <> '')
      THEN
        EXIT(FALSE);
      EXIT(TRUE);
    END;

    PROCEDURE DetermineBOMLevel@1100525007(LineNo@1100525000 : Integer;VAR BOMLevel@1100525002 : Integer);
    VAR
      OptionBudget@1100525001 : Record 11012503;
    BEGIN
      OptionBudget.SETRANGE("Project No.","Project No.");
      OptionBudget.SETRANGE("Plot No.","Plot No.");
      OptionBudget.SETRANGE("House Model","House Model");
      OptionBudget.SETRANGE("Main Group","Main Group");
      OptionBudget.SETRANGE(Group,Group);
      OptionBudget.SETRANGE("Sub Group","Sub Group");
      OptionBudget.SETRANGE(Option,Option);
      OptionBudget.SETRANGE("Line No.",LineNo);
      IF OptionBudget.FINDFIRST THEN
        BOMLevel := OptionBudget."Level BOM" + 1;
    END;

    [Internal]
    PROCEDURE GetVersionDate@1100528900() VersionDate : Date;
    VAR
      OptionRec@1100528900 : Record 11012502;
    BEGIN
      //C062294
      IF Option <> '' THEN BEGIN
        IF "Project No." = '' THEN
          OptionRec.SETFILTER("Project No.", '%1', '')
        ELSE
          OptionRec.SETRANGE("Project No.","Project No.");
        IF "Plot No." = '' THEN
          OptionRec.SETFILTER("Plot No.", '%1', '')
        ELSE
          OptionRec.SETRANGE("Plot No.", "Plot No.");
        OptionRec.SETRANGE(Option,Option);
        IF OptionRec.FINDFIRST THEN
          VersionDate := OptionRec."Version Date";
      END;

      IF VersionDate = 0D THEN
        VersionDate := TODAY;

      EXIT(VersionDate);
    END;

    BEGIN
    END.
  }
}

