OBJECT Report 751 Save as Standard Item Journal
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Als Standard Artikel Buch.-Blatt speichern;
               ENU=Save as Standard Item Journal;
               NLD=Opslaan als standaardartikeldagboek;
               NOR=Lagre som standard varekladd];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF Code = '' THEN
                    ERROR(Text000);

                  StdJournalCreated := FALSE;
                  SaveItemJnlAsStandardJnl;
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 2   ;2   ;Field     ;
                  Name=Code;
                  CaptionML=[DEU=Code;
                             ENU=Code;
                             NLD=Code;
                             NOR=Kode;
                             SVE=Kod];
                  ToolTipML=[DEU=Gibt den Code fÅr das Buch.-Blatt an.;
                             ENU=Specifies the code for the journal.;
                             NLD=Hiermee wordt de code voor het dagboek opgegeven.;
                             NOR=Angir koden for kladden.];
                  ApplicationArea=#Suite;
                  NotBlank=Yes;
                  SourceExpr=Code;
                  OnLookup=VAR
                             StdItemJnl@1000 : Record 752;
                             StdItemJnls@1001 : Page 753;
                           BEGIN
                             StdItemJnl.SETRANGE("Journal Template Name",ItemJnlBatch."Journal Template Name");
                             StdItemJnls.SETTABLEVIEW(StdItemJnl);

                             StdItemJnls.LOOKUPMODE := TRUE;
                             StdItemJnls.EDITABLE := FALSE;
                             IF StdItemJnls.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               StdItemJnls.GETRECORD(StdItemJnl);
                               Code := StdItemJnl.Code;
                               Description := StdItemJnl.Description;
                             END;
                           END;
                            }

      { 8   ;2   ;Field     ;
                  CaptionML=[DEU=Beschreibung;
                             ENU=Description;
                             NLD=Omschrijving;
                             NOR=Beskrivelse;
                             SVE=Beskrivning];
                  ToolTipML=[DEU=Gibt eine Beschreibung des Buch.-Blatts an.;
                             ENU=Specifies a description of the journal.;
                             NLD=Hiermee wordt een omschrijving van het dagboek opgegeven.;
                             NOR=Angir en beskrivelse av kladden.];
                  ApplicationArea=#Suite;
                  SourceExpr=Description }

      { 5   ;2   ;Field     ;
                  CaptionML=[DEU=StÅckpreis speichern;
                             ENU=Save Unit Amount;
                             NLD=Eenheidsprijs opslaan;
                             NOR=Lagre enhetsbelõp];
                  ToolTipML=[DEU=Legt fest, ob das Programm den Wert/die Werte im Feld "StÅckpreis" des von Ihnen gespeicherten Artikel Buch.-Blatts speichern soll.;
                             ENU=Specifies if you want the program to save the value(s) in the Unit Amount field of the item journal you are saving.;
                             NLD=Hiermee wordt opgegeven of u de waarden wilt vastleggen in het veld Eenheidsprijs van het artikeldagboek dat u opslaat.;
                             NOR=Angir om du vil lagre verdien(e) i feltet Enhetsbelõp i varekladden som lagres.];
                  ApplicationArea=#Suite;
                  SourceExpr=SaveUnitAmount }

      { 7   ;2   ;Field     ;
                  CaptionML=[DEU=Menge speichern;
                             ENU=Save Quantity;
                             NLD=Aantal opslaan;
                             NOR=Lagre mengde];
                  ToolTipML=[DEU=Legt fest, ob das Programm den Wert/die Werte im Feld "Menge" des von Ihnen gespeicherten Artikel Buch.-Blatts speichern soll.;
                             ENU=Specifies if you want the program to save the value(s) in the Quantity field of the item journal you are saving.;
                             NLD=Hiermee wordt opgegeven of u de waarden wilt vastleggen in het veld Aantal van het artikeldagboek dat u opslaat.;
                             NOR=Angir om du vil lagre verdien(e) i feltet Antall i varekladden som lagres.];
                  ApplicationArea=#Suite;
                  SourceExpr=SaveQuantity }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1001 : TextConst 'DEU=Geben Sie einen Code fÅr das Standard-Artikel Buch.-Blatt ein.;ENU=Enter a code for Standard Item Journal.;NLD=Geef een code op voor het standaardartikeldagboek.;NOR=Angi en kode for standard varekladd.';
      Text001@1000 : TextConst 'DEU=Das Standard-Artikel Buch ''%1'' ist bereits vorhanden. Mîchten Sie es Åberschreiben?;ENU=Standard Item Journal %1 already exists. Do you want to overwrite?;NLD=Standaardartikeldagboek %1 bestaat reeds. Wilt u dit overschrijven?;NOR=Standard varekladd %1 finnes allerede. Vil du overskrive den?';
      ItemJnlLine@1007 : Record 83;
      ItemJnlBatch@1006 : Record 233;
      StdItemJnl@1010 : Record 752;
      Code@1005 : Code[10];
      Description@1004 : Text[100];
      SaveUnitAmount@1003 : Boolean;
      SaveQuantity@1008 : Boolean;
      StdJournalCreated@1002 : Boolean;

    [External]
    PROCEDURE Initialise@2(VAR SelectedItemJnlLines@1000 : Record 83;SelectedItemJnlBatch@1001 : Record 233);
    BEGIN
      ItemJnlLine.COPY(SelectedItemJnlLines);
      ItemJnlBatch := SelectedItemJnlBatch;
    END;

    [External]
    PROCEDURE InitializeRequest@3(NewCode@1003 : Code[10];NewDescription@1002 : Text[50];NewSaveUnitAmount@1001 : Boolean;NewSaveQuantity@1000 : Boolean);
    BEGIN
      Code := NewCode;
      Description := NewDescription;
      SaveUnitAmount := NewSaveUnitAmount;
      SaveQuantity := NewSaveQuantity;
    END;

    LOCAL PROCEDURE SaveItemJnlAsStandardJnl@6();
    VAR
      StdItemJnlLine@1000 : Record 753;
      NextLineNo@1001 : Integer;
      LineNo@1100525000 : Integer;
    BEGIN
      StdItemJnl.INIT;
      StdItemJnl."Journal Template Name" := ItemJnlBatch."Journal Template Name";
      StdItemJnl.Code := Code;
      StdItemJnl.Description := Description;

      IF StdItemJnlExists THEN
        IF NOT CONFIRM(Text001,FALSE,StdItemJnl.Code) THEN
          EXIT;

      StdItemJnlLine.LOCKTABLE;
      StdItemJnl.LOCKTABLE;

      IF StdItemJnlExists THEN BEGIN
        StdItemJnl.MODIFY(TRUE);
        StdItemJnlLine.SETRANGE("Journal Template Name",StdItemJnl."Journal Template Name");
        StdItemJnlLine.SETRANGE("Standard Journal Code",StdItemJnl.Code);
        StdItemJnlLine.DELETEALL(TRUE);
      END ELSE
        StdItemJnl.INSERT(TRUE);

      NextLineNo := 10000;
      IF ItemJnlLine.FINDSET THEN
        REPEAT
          StdItemJnlLine."Line No." := NextLineNo;
          NextLineNo := NextLineNo + 10000;
          StdItemJnlLine.INIT;
          StdItemJnlLine."Journal Template Name" := StdItemJnl."Journal Template Name";
          StdItemJnlLine."Standard Journal Code" := StdItemJnl.Code;
          StdItemJnlLine.TRANSFERFIELDS(ItemJnlLine,FALSE);
          //**4PS.sn: KB914980
          LineNo := LineNo + 10000;
          StdItemJnlLine."Line No." := LineNo;
          //**4PS.en
          IF NOT SaveUnitAmount THEN BEGIN
            StdItemJnlLine."Unit Amount" := 0;
            StdItemJnlLine.Amount := 0;
            StdItemJnlLine."Unit Cost" := 0;
            StdItemJnlLine."Indirect Cost %" := 0;
          END;
          IF NOT SaveQuantity THEN
            StdItemJnlLine.VALIDATE(Quantity,0);
          //StdItemJnlLine.INSERT(TRUE); //**4PS.o jhoek: zie KB 914980
          StdItemJnlLine.INSERT; //**4PS.n
        UNTIL ItemJnlLine.NEXT = 0;

      StdJournalCreated := TRUE;
    END;

    LOCAL PROCEDURE StdItemJnlExists@10() : Boolean;
    VAR
      StdItemJnl@1000 : Record 752;
    BEGIN
      StdItemJnl.SETRANGE("Journal Template Name",ItemJnlBatch."Journal Template Name");
      StdItemJnl.SETRANGE(Code,Code);

      EXIT(StdItemJnl.FINDFIRST);
    END;

    [External]
    PROCEDURE GetStdItemJournal@1(VAR StdItemJnl1@1000 : Record 752) : Boolean;
    BEGIN
      IF StdJournalCreated THEN
        StdItemJnl1.COPY(StdItemJnl);

      EXIT(StdJournalCreated);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

