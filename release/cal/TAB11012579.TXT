OBJECT Table 11012579 Plant Need
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CheckPlantLocationStatus;
               CheckDepartmentCodeFilled('');

               Finished := FALSE;
               "Quantity To Copy" := Quantity;
               "Delete Remainder":= FALSE;
             END;

    OnModify=BEGIN
               CheckPlantLocationStatus;
             END;

    OnDelete=BEGIN
               ResetCopiedNeedPlantOrderLines('');
             END;

    CaptionML=[ENU=Plant Need;
               NOR=Maskinbehov;
               SVE=Maskinbehov];
    LookupPageID=Page11012606;
    DrillDownPageID=Page11012606;
  }
  FIELDS
  {
    { 10  ;   ;Location            ;Code20        ;TableRelation="Plant Location".Code;
                                                   OnValidate=BEGIN
                                                                FillDefaultFromDepot;
                                                              END;

                                                   CaptionML=[ENU=Location;
                                                              NOR=Plassering;
                                                              SVE=Lagerst„lle] }
    { 20  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 25  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                CASE Type OF
                                                                  Type::Plant:
                                                                    BEGIN
                                                                      TESTFIELD("Item No.", '');
                                                                    END;
                                                                  Type::Item:
                                                                    BEGIN
                                                                      TESTFIELD("Plant Type", '');
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Typ];
                                                   OptionCaptionML=[ENU=Plant,Item;
                                                                    NOR=Maskin,Artikkel;
                                                                    SVE=Maskin,Artikel];
                                                   OptionString=Plant,Item }
    { 30  ;   ;Plant Type          ;Code20        ;TableRelation="Plant Type".Code;
                                                   OnValidate=BEGIN
                                                                IF "Plant Type" <> '' THEN
                                                                  TESTFIELD(Type, Type::Plant);
                                                                CALCFIELDS("Plant Type Description", "Unit of Measure", "Cost Object (Plant)");
                                                              END;

                                                   CaptionML=[ENU=Plant Type;
                                                              NOR=Maskintype;
                                                              SVE=Maskintyp];
                                                   NotBlank=Yes }
    { 32  ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   OnValidate=BEGIN
                                                                IF "Item No." <> '' THEN
                                                                  TESTFIELD(Type, Type::Item);
                                                                CALCFIELDS("Item Description", "Unit of Measure (Item)");
                                                              END;

                                                   CaptionML=[ENU=Item No.;
                                                              NOR=Varenr.;
                                                              SVE=Artikelnr] }
    { 35  ;   ;Plant Type Description;Text50      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Type".Description WHERE (Code=FIELD(Plant Type)));
                                                   CaptionML=[ENU=Plant Description;
                                                              NOR=Maskinbeskrivelse;
                                                              SVE=Maskinbeskrivning];
                                                   Editable=No }
    { 37  ;   ;Item Description    ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Item.Description WHERE (No.=FIELD(Item No.)));
                                                   CaptionML=[ENU=Item Description;
                                                              NOR=Artikkelbeskrivelse;
                                                              SVE=Artikelbeskrivning];
                                                   Editable=No }
    { 40  ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                IF ("Ending Date" <> 0D) AND ("Starting Date" <> xRec."Starting Date") THEN BEGIN
                                                                  IF CONFIRM(Text001,TRUE) THEN
                                                                    "Ending Date" := "Ending Date" + ("Starting Date" - xRec."Starting Date");
                                                                END;
                                                                VALIDATE("Ending Date")
                                                              END;

                                                   CaptionML=[ENU=Starting Date;
                                                              NOR=Startdato;
                                                              SVE=Startdatum];
                                                   NotBlank=Yes }
    { 50  ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                IF ("Ending Date" <> 0D) AND ("Ending Date" < "Starting Date") THEN
                                                                  "Ending Date" := "Starting Date";
                                                              END;

                                                   CaptionML=[ENU=Ending Date;
                                                              NOR=Sluttdato;
                                                              SVE=Slutdatum] }
    { 60  ;   ;Quantity            ;Decimal       ;CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 65  ;   ;Unit of Measure     ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Type"."Unit of Measure" WHERE (Code=FIELD(Plant Type)));
                                                   CaptionML=[ENU=Unit of Measure;
                                                              NOR=M†leenhet;
                                                              SVE=M†ttenhet];
                                                   Description=Plant;
                                                   Editable=No }
    { 66  ;   ;Unit of Measure (Item);Code10      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Item."Sales Unit of Measure" WHERE (No.=FIELD(Item No.)));
                                                   CaptionML=ENU=Unit of Measure (Item);
                                                   Editable=No }
    { 80  ;   ;Quantity To Copy    ;Decimal       ;CaptionML=[ENU=To Copy;
                                                              NOR=Til kopi;
                                                              SVE=Till kopia];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 85  ;   ;Delete Remainder    ;Boolean       ;CaptionML=[ENU=Delete Remainder;
                                                              NOR=Fjerne Resten;
                                                              SVE=Ta bort resten] }
    { 90  ;   ;Finished            ;Boolean       ;OnValidate=BEGIN
                                                                IF Finished THEN BEGIN
                                                                  "Quantity To Copy" := 0;
                                                                  "Delete Remainder" := FALSE;
                                                                END ELSE BEGIN
                                                                  "Quantity To Copy" := Quantity - QuantityCopiedLineTotal('');
                                                                  IF "Quantity To Copy" < 0 THEN
                                                                    "Quantity To Copy" := 0;
                                                                  "Delete Remainder" := FALSE;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Finished;
                                                              NOR=Ferdig;
                                                              SVE=Avslutad] }
    { 100 ;   ;Document Type       ;Option        ;CaptionML=[ENU=Document Type;
                                                              NOR=Dokumenttype;
                                                              SVE=Dokumenttyp];
                                                   OptionCaptionML=[ENU=" ,Quote,Order";
                                                                    NOR=" ,Tilbud,Ordre";
                                                                    SVE=" ,Offert,Order"];
                                                   OptionString=[ ,SalesQuote,SalesOrder];
                                                   Description=Only for granule 'Sales Rental Contract';
                                                   Editable=No }
    { 101 ;   ;Document No.        ;Code20        ;TableRelation=IF (Document Type=CONST(SalesQuote)) "Sales Header".No. WHERE (Document Type=CONST(Quote))
                                                                 ELSE IF (Document Type=CONST(SalesOrder)) "Sales Header".No. WHERE (Document Type=CONST(Order));
                                                   CaptionML=[ENU=Document No.;
                                                              NOR=Dokumentnr.;
                                                              SVE=Dokumentnr];
                                                   Description=Only for granule 'Sales Rental Contract';
                                                   Editable=No }
    { 102 ;   ;Document Line No.   ;Integer       ;TableRelation=IF (Document Type=CONST(SalesQuote)) "Sales Line"."Line No." WHERE (Document Type=CONST(Quote),
                                                                                                                                     Document No.=FIELD(Document No.))
                                                                                                                                     ELSE IF (Document Type=CONST(SalesOrder)) "Sales Line"."Line No." WHERE (Document Type=CONST(Order),
                                                                                                                                                                                                              Document No.=FIELD(Document No.));
                                                   CaptionML=[ENU=Document Line No.;
                                                              NOR=Dokumentlinjenr.;
                                                              SVE=Dokumentradnr];
                                                   Description=Only for granule 'Sales Rental Contract';
                                                   Editable=No }
    { 120 ;   ;Transf. from Estimate;Code20       ;TableRelation=Estimate;
                                                   CaptionML=[ENU=Transf. from Estimate;
                                                              NOR=Overf›r fra Kalkyle;
                                                              SVE=™verf. fr†n kalkyl];
                                                   Editable=No }
    { 130 ;   ;Cost Object (Plant) ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Type"."Cost Object" WHERE (Code=FIELD(Plant Type)));
                                                   TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Cost Object (Plant);
                                                              NOR=Kostnadsobjekt (Maskin);
                                                              SVE=Kostnadsobjekt (Maskin)];
                                                   Editable=No }
    { 140 ;   ;From Depot          ;Code20        ;TableRelation="Plant Location" WHERE (Depot=CONST(Yes));
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=From Depot;
                                                              SVE=Fr†n dep†] }
  }
  KEYS
  {
    {    ;Location,Line No.                       ;Clustered=Yes }
    {    ;Finished,Plant Type,Starting Date        }
    {    ;Plant Type,Location                      }
    {    ;Location,Starting Date,Ending Date,Plant Type }
    {    ;Document Type,Document No.,Document Line No. }
    {    ;Transf. from Estimate                    }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1210190000 : TextConst 'ENU=Also shift the End Dat;NOR=Endre ogs† Sluttdato;SVE=Žndra „ven slutdatum';
      PlantSetupRec@1100525000 : Record 11012550;
      PlantLocRec@1100485000 : Record 11012554;
      NextLino@1100525001 : Integer;

    PROCEDURE CheckPlantLocationStatus@1100485000();
    BEGIN
      PlantLocRec.GET(Location);
      PlantLocRec.CheckBlock(0,FALSE,FALSE);
    END;

    PROCEDURE CopyLinesToPlantOrder@1210190002(VAR PlantNeedRec2@1100525000 : Record 11012579;PlantOrderNo@1100525001 : Code[20]);
    VAR
      PlantNeedRec@1210190004 : Record 11012579;
      PlantOrderRec@1100525002 : Record 11012556;
      PlantOrderLineRec@1100525003 : Record 11012557;
    BEGIN
      // Copy the selected lines to the Plant Order
      PlantNeedRec.COPY(PlantNeedRec2);
      IF NOT PlantNeedRec.FINDSET(TRUE,FALSE) THEN
        EXIT;

      PlantSetupRec.GET;
      PlantOrderRec.GET(PlantOrderNo);

      PlantOrderLineRec.SETRANGE("Plant Order No.", PlantOrderNo);
      IF PlantOrderLineRec.FINDLAST THEN
        NextLino :=  PlantOrderLineRec."Line No." + 10000
      ELSE
        NextLino := 10000;

      REPEAT
        IF PlantNeedRec."Quantity To Copy" > 0 THEN BEGIN
          PlantOrderLineRec.INIT;
          PlantOrderLineRec."Plant Order No." := PlantOrderNo;
          PlantOrderLineRec.InitRecord();  //* First fill order number
          PlantOrderLineRec."Line No." := NextLino;
          NextLino :=NextLino + 10000;
          IF PlantNeedRec.Type = PlantNeedRec.Type::Item THEN BEGIN
            PlantOrderLineRec.VALIDATE(Type, PlantOrderLineRec.Type::Item);
            PlantOrderLineRec.VALIDATE("Item No.", PlantNeedRec."Item No.");
          END ELSE BEGIN
            PlantOrderLineRec.Type := PlantOrderLineRec.Type::Plant;
            PlantOrderLineRec.VALIDATE("Plant Type", PlantNeedRec."Plant Type");
            PlantOrderLineRec."Expected return on" := PlantNeedRec."Ending Date";
            IF (PlantOrderLineRec."Expected return on" <> 0D) AND
               (PlantOrderLineRec."Expected return on" < PlantOrderLineRec."Transfer Date")
            THEN
              PlantOrderLineRec."Expected return on" := PlantOrderLineRec."Transfer Date";
          END;
          PlantOrderLineRec."Transfer Date" := PlantOrderRec."Transfer Date";
          PlantOrderLineRec."From Location" := PlantOrderRec."From Location";
          IF PlantSetupRec."Extended Picking Procedure" AND (PlantOrderRec.Type = PlantOrderRec.Type::Arrival) THEN
            PlantOrderLineRec."Asked Quantity" := PlantNeedRec."Quantity To Copy"
          ELSE
            PlantOrderLineRec.Quantity := PlantNeedRec."Quantity To Copy";
          PlantOrderLineRec."Location Copied Need" := PlantNeedRec.Location;
          PlantOrderLineRec."Line No. Copied Need" := PlantNeedRec."Line No.";
          PlantOrderLineRec.INSERT;
        END;
        CalculateQuantityToCopy(PlantNeedRec, TRUE, '');
        PlantNeedRec.MODIFY;
      UNTIL (PlantNeedRec.NEXT = 0);
      COMMIT;
    END;

    PROCEDURE CopyLinesToPlantRequest@1100525000(VAR PlantNeedRec2@1100525000 : Record 11012579;PlantRequestNo@1100525001 : Code[20]);
    VAR
      PlantNeedRec@1210190004 : Record 11012579;
      PlantRequestRec@1100525002 : Record 11020520;
      PlantReqLineRec@1100525003 : Record 11020521;
    BEGIN
      // Copy the selected lines to the Plant Request
      PlantNeedRec.COPY(PlantNeedRec2);
      IF NOT PlantNeedRec.FINDSET(FALSE,FALSE) THEN
        EXIT;

      PlantRequestRec.GET(PlantRequestNo);

      PlantReqLineRec.SETRANGE("Plant Request No.", PlantRequestNo);
      IF PlantReqLineRec.FINDLAST THEN
        NextLino :=  PlantReqLineRec."Line No." + 10000
      ELSE
        NextLino := 10000;

      REPEAT
        IF PlantNeedRec."Quantity To Copy" > 0 THEN BEGIN
          PlantReqLineRec.INIT;
          PlantReqLineRec."Plant Request No." := PlantRequestNo;
          PlantReqLineRec.InitRecord;
          PlantReqLineRec."Line No." := NextLino;
          NextLino :=NextLino + 10000;
          IF PlantNeedRec.Type = PlantNeedRec.Type::Item THEN BEGIN
            PlantReqLineRec.VALIDATE(Type, PlantReqLineRec.Type::Item);
            PlantReqLineRec.VALIDATE("Item No.", PlantNeedRec."Item No.");
          END ELSE BEGIN
            PlantNeedRec.CALCFIELDS("Plant Type Description");
            PlantReqLineRec.Type := PlantReqLineRec.Type::Plant;
            PlantReqLineRec."Plant Type" := PlantNeedRec."Plant Type";
            PlantReqLineRec."Plant No." := '';
            PlantReqLineRec."Item No." := '';
            PlantReqLineRec."Set Code" := '';
            PlantReqLineRec.Description := PlantNeedRec."Plant Type Description";
            PlantReqLineRec."Expected return on" := PlantNeedRec."Ending Date";
            IF (PlantReqLineRec."Expected return on" <> 0D) AND
               (PlantReqLineRec."Expected return on" < PlantRequestRec."Transfer Date")
            THEN
              PlantReqLineRec."Expected return on" := PlantRequestRec."Transfer Date";
          END;
          PlantReqLineRec.Quantity := PlantNeedRec."Quantity To Copy";
          PlantReqLineRec."Location Copied Need" := PlantNeedRec.Location;
          PlantReqLineRec."Line No. Copied Need" := PlantNeedRec."Line No.";
          PlantReqLineRec.INSERT;
        END;
        CalculateQuantityToCopy(PlantNeedRec, FALSE, '');
        PlantNeedRec.MODIFY;
      UNTIL (PlantNeedRec.NEXT = 0);
      COMMIT;
    END;

    PROCEDURE CalculateQuantityToCopy@1100525005(VAR PlantNeedRec@1100525000 : Record 11012579;Fin@1100525002 : Boolean;PlantCompany@1100409000 : Text[30]);
    BEGIN
      IF PlantNeedRec."Delete Remainder" THEN BEGIN
        PlantNeedRec."Quantity To Copy" := 0;
        PlantNeedRec."Delete Remainder" := FALSE;
      END ELSE BEGIN
        PlantNeedRec."Quantity To Copy" := PlantNeedRec.Quantity - PlantNeedRec.QuantityCopiedLineTotal(PlantCompany);
      END;
      IF PlantNeedRec."Quantity To Copy" <= 0 THEN BEGIN
        PlantNeedRec."Quantity To Copy" := 0;
        IF Fin THEN
          PlantNeedRec.Finished := TRUE;
      END;
    END;

    PROCEDURE LookupPlantType@1100525001(xRecPlantTypeCode@1210190000 : Code[20];Editable@1100525004 : Boolean;VAR MultiCopyDone@1100525005 : Boolean) : Code[20];
    VAR
      PlantType@1100525000 : Record 11012551;
      TmpPlantType@1100525002 : TEMPORARY Record 11012551;
      MultiCopyMgt@1100525003 : Codeunit 11012068;
      PlantTypeList@1100525001 : Page 11012552;
    BEGIN
      MultiCopyDone := FALSE;
      TESTFIELD(Location);

      PlantType.Code := "Plant Type";
      IF NOT Editable THEN BEGIN
        PlantTypeList.SETRECORD(PlantType);
        PlantTypeList.RUNMODAL;
      END ELSE BEGIN
        IF PlantType.Code = '' THEN
          PlantType.Code := xRecPlantTypeCode;
        PlantTypeList.SETRECORD(PlantType);
        PlantTypeList.LOOKUPMODE(TRUE);
        PlantTypeList.SetMultiCopyAllowed(TRUE);
        IF PlantTypeList.RUNMODAL = ACTION::LookupOK THEN BEGIN
          PlantTypeList.GETRECORD(PlantType);
          IF PlantType.Code <> '' THEN
            EXIT(PlantType.Code);
        END ELSE BEGIN
          IF PlantTypeList.GetMultiCopyRec(TmpPlantType) THEN BEGIN
            MultiCopyMgt.CopyPlantTypeToPlantNeedLine(TmpPlantType, Location, "Line No.");
            MultiCopyDone := TRUE;
          END;
        END;
      END;
      EXIT("Plant Type");
    END;

    PROCEDURE ResetCopiedNeedPlantOrderLines@1100409002(PlantCompany@1100409002 : Text[30]);
    VAR
      PlantOrderLine@1100409001 : Record 11012557;
      PlantOrderLine2@1100409000 : Record 11012557;
      PlantRequestLine@1100529000 : Record 11020521;
      PlantRequestLine2@1100529001 : Record 11020521;
    BEGIN
      IF (Location = '') AND ("Line No." = 0) THEN
        EXIT;
      IF (PlantCompany <> '') AND (PlantCompany <> COMPANYNAME) THEN BEGIN
        PlantOrderLine.CHANGECOMPANY(PlantCompany);
        PlantOrderLine2.CHANGECOMPANY(PlantCompany);
        PlantRequestLine2.CHANGECOMPANY(PlantCompany);
        PlantRequestLine.CHANGECOMPANY(PlantCompany);
      END;
      PlantOrderLine.SETCURRENTKEY("Location Copied Need", "Line No. Copied Need");
      PlantOrderLine.SETRANGE("Location Copied Need", Location);
      PlantOrderLine.SETRANGE("Line No. Copied Need", "Line No.");
      IF PlantOrderLine.FINDSET(TRUE, TRUE) THEN BEGIN
        REPEAT
          PlantOrderLine2.GET(PlantOrderLine."Plant Order No.", PlantOrderLine."Line No.");
          PlantOrderLine2."Location Copied Need" := '';
          PlantOrderLine2."Line No. Copied Need" := 0;
          PlantOrderLine2.MODIFY;
        UNTIL (PlantOrderLine.NEXT = 0);
      END;
      PlantRequestLine.SETCURRENTKEY("Location Copied Need", "Line No. Copied Need");
      PlantRequestLine.SETRANGE("Location Copied Need", Location);
      PlantRequestLine.SETRANGE("Line No. Copied Need", "Line No.");
      IF PlantRequestLine.FINDSET(TRUE, TRUE) THEN BEGIN
        REPEAT
          PlantRequestLine2.GET(PlantRequestLine."Plant Request No.", PlantRequestLine."Line No.");
          PlantRequestLine2."Location Copied Need" := '';
          PlantRequestLine2."Line No. Copied Need" := 0;
          PlantRequestLine2.MODIFY;
        UNTIL PlantRequestLine.NEXT = 0;
      END;
    END;

    PROCEDURE ShiftSelectedLines@1100409000(VAR SelectedPlantNeed@1100409000 : Record 11012579;FactorLeftRight@1210190002 : Integer;TimeUnits@1210190004 : Integer;PeriodType@1210190005 : 'Day,Week,Month';DateType@1210190003 : 'Both,StartDate,EndDate');
    VAR
      TotalDays@1210190001 : Integer;
      ShiftExpression@1210190006 : Text[30];
      NewDate@1210190007 : Date;
    BEGIN
      //*RTC Function copied from form/page to table
      IF TimeUnits = 0 THEN
        EXIT;
      SelectedPlantNeed.SETRANGE(Finished, FALSE);
      IF NOT SelectedPlantNeed.FINDSET(TRUE, FALSE) THEN
        EXIT;

      ShiftExpression := STRSUBSTNO('%1', (FactorLeftRight * TimeUnits));
      CASE PeriodType OF
        PeriodType::Day:
          ShiftExpression := ShiftExpression + 'D';
        PeriodType::Week:
          ShiftExpression := ShiftExpression + 'W';
        PeriodType::Month:
          ShiftExpression := ShiftExpression + 'M';
        ELSE
          EXIT;
      END;
      ShiftExpression := '<'+ ShiftExpression + '>';

      REPEAT
        CASE DateType OF
          DateType::Both:
            BEGIN
              IF SelectedPlantNeed."Ending Date" <> 0D THEN
                TotalDays := SelectedPlantNeed."Ending Date" - SelectedPlantNeed."Starting Date";
              SelectedPlantNeed."Starting Date" := CALCDATE(ShiftExpression , SelectedPlantNeed."Starting Date");
              IF SelectedPlantNeed."Ending Date" <> 0D THEN
                SelectedPlantNeed."Ending Date" := SelectedPlantNeed."Starting Date" + TotalDays;
            END;
          DateType::StartDate:
            BEGIN
              NewDate := CALCDATE(ShiftExpression , SelectedPlantNeed."Starting Date");
              IF (SelectedPlantNeed."Ending Date" = 0D) OR
                 ((SelectedPlantNeed."Ending Date" <> 0D) AND (NewDate <= SelectedPlantNeed."Ending Date")) THEN
                SelectedPlantNeed."Starting Date" := NewDate;
            END;
          DateType::EndDate:
            BEGIN
              IF SelectedPlantNeed."Ending Date" <> 0D THEN BEGIN
                NewDate := CALCDATE(ShiftExpression , SelectedPlantNeed."Ending Date");
                IF (NewDate >= SelectedPlantNeed."Starting Date") THEN
                  SelectedPlantNeed."Ending Date" := NewDate;
              END;
            END;
        END;
        SelectedPlantNeed.MODIFY;
      UNTIL (SelectedPlantNeed.NEXT = 0);
    END;

    PROCEDURE PlantOrItemDescription@1100529001() : Text;
    BEGIN
      CASE Type OF
        Type::Plant:
          BEGIN
            CALCFIELDS("Plant Type Description");
            EXIT("Plant Type Description");
          END;
        Type::Item:
          BEGIN
            CALCFIELDS("Item Description");
            EXIT("Item Description");
          END;
      END;
      EXIT('');
    END;

    PROCEDURE UnitOfMeasureCode@1100529002() : Code[10];
    BEGIN
      IF Type <> Type::Item THEN BEGIN
        CALCFIELDS("Unit of Measure");
        EXIT("Unit of Measure");
      END ELSE BEGIN
        CALCFIELDS("Unit of Measure (Item)");
        EXIT("Unit of Measure (Item)");
      END;
    END;

    PROCEDURE GetTransfFromEstimateNo@1100529006(PlantNeedLocation@1100529000 : Code[20];PlantNeedLineNo@1100529001 : Integer) : Code[20];
    VAR
      PlantNeed@1100529002 : Record 11012579;
    BEGIN
      IF (PlantNeedLocation <> '') AND (PlantNeedLineNo <> 0) THEN BEGIN
        IF PlantNeed.GET(PlantNeedLocation, PlantNeedLineNo) THEN
          EXIT(PlantNeed."Transf. from Estimate");
      END;
      EXIT('');
    END;

    PROCEDURE QuantityCopiedLineTotal@1100529003(PlantCompany@1100529000 : Text) : Decimal;
    BEGIN
      EXIT(QuantityCopiedOnPlantOrders(PlantCompany) + QuantityCopiedOnPlantRequests(PlantCompany));
    END;

    PROCEDURE QuantityCopiedOnPlantRequests@1100529004(PlantCompany@1100529000 : Text) CopiedQuantity : Decimal;
    VAR
      PlantRequest@1100529002 : Record 11020520;
      PlantRequestLine@1100529001 : Record 11020521;
    BEGIN
      CopiedQuantity := 0;
      IF (Location = '') OR ("Line No." = 0) THEN
        EXIT;
      IF (PlantCompany <> '') AND (PlantCompany <> COMPANYNAME) THEN BEGIN
        PlantRequest.CHANGECOMPANY(PlantCompany);
        PlantRequestLine.CHANGECOMPANY(PlantCompany);
      END;
      PlantRequestLine.SETCURRENTKEY("Location Copied Need", "Line No. Copied Need");
      PlantRequestLine.SETRANGE("Location Copied Need", Location);
      PlantRequestLine.SETRANGE("Line No. Copied Need", "Line No.");
      IF PlantRequestLine.FINDSET THEN BEGIN
        REPEAT
          IF PlantRequest.GET(PlantRequestLine."Plant Request No.") THEN BEGIN
            IF (NOT PlantRequest.Posted) AND (PlantRequest.Status < PlantRequest.Status::Approved) THEN  //Approved counted on PO, do not count Disapproved/Canceled
              CopiedQuantity := CopiedQuantity + PlantRequestLine.Quantity;
          END;
        UNTIL PlantRequestLine.NEXT = 0;
      END;
    END;

    PROCEDURE QuantityCopiedOnPlantOrders@1100529000(PlantCompany@1100529000 : Text) CopiedQuantity : Decimal;
    VAR
      PlantSetup@1100529003 : Record 11012550;
      PlantOrder@1100529002 : Record 11012556;
      PlantOrderLine@1100529001 : Record 11012557;
      LineQuantity@1100529004 : Decimal;
    BEGIN
      CopiedQuantity := 0;
      IF (Location = '') OR ("Line No." = 0) THEN
        EXIT;
      IF (PlantCompany <> '') AND (PlantCompany <> COMPANYNAME) THEN BEGIN
        PlantSetup .CHANGECOMPANY(PlantCompany);
        PlantOrder.CHANGECOMPANY(PlantCompany);
        PlantOrderLine.CHANGECOMPANY(PlantCompany);
      END;
      PlantOrderLine.SETCURRENTKEY("Location Copied Need", "Line No. Copied Need");
      PlantOrderLine.SETRANGE("Location Copied Need", Location);
      PlantOrderLine.SETRANGE("Line No. Copied Need", "Line No.");
      IF PlantOrderLine.FINDSET THEN BEGIN
        PlantSetup.GET;
        REPEAT
          IF PlantOrder.GET(PlantOrderLine."Plant Order No.") THEN BEGIN
            LineQuantity := PlantOrderLine.Quantity;
            IF (PlantOrderLine.Quantity = 0) AND (PlantOrderLine."Asked Quantity" <> 0) AND PlantSetup."Extended Picking Procedure" AND
               (NOT PlantOrder.Posted) AND (PlantOrder.Status < PlantOrder.Status::Released) AND (PlantOrder.Type = PlantOrder.Type::Arrival)
            THEN
              LineQuantity := PlantOrderLine."Asked Quantity";
            CopiedQuantity := CopiedQuantity + LineQuantity;
          END;
        UNTIL PlantOrderLine.NEXT = 0;
      END;
    END;

    PROCEDURE QuantityCopiedOnPostedPlantOrders@1100529005(PlantCompany@1100529000 : Text) CopiedQuantity : Decimal;
    VAR
      PlantOrderLine@1100529001 : Record 11012557;
    BEGIN
      CopiedQuantity := 0;
      IF (Location = '') OR ("Line No." = 0) THEN
        EXIT;
      IF (PlantCompany <> '') AND (PlantCompany <> COMPANYNAME) THEN
        PlantOrderLine.CHANGECOMPANY(PlantCompany);
      PlantOrderLine.SETCURRENTKEY("Location Copied Need", "Line No. Copied Need");
      PlantOrderLine.SETRANGE("Location Copied Need", Location);
      PlantOrderLine.SETRANGE("Line No. Copied Need", "Line No.");
      PlantOrderLine.SETRANGE(Posted, TRUE);
      IF PlantOrderLine.FINDSET THEN BEGIN
        REPEAT
          CopiedQuantity := CopiedQuantity + PlantOrderLine.Quantity;
        UNTIL PlantOrderLine.NEXT = 0;
      END;
    END;

    PROCEDURE CheckDepartmentCodeFilled@1100525002(PlantCompany@1100525000 : Text);
    VAR
      PlantSetup@1100525001 : Record 11012550;
      PlantLocation@1100525002 : Record 11012554;
    BEGIN
      IF (PlantCompany <> '') AND (PlantCompany <> COMPANYNAME) THEN BEGIN
        PlantSetup .CHANGECOMPANY(PlantCompany);
        PlantLocation.CHANGECOMPANY(PlantCompany);
      END;
      PlantLocation.GET(Location);
      IF PlantLocation."Department Code" = '' THEN BEGIN
        PlantSetup.GET;
        IF PlantSetup."Plant Need Based on Department" THEN
          PlantLocation.TESTFIELD("Department Code");
      END;
    END;

    PROCEDURE FillDefaultFromDepot@1100529007();
    VAR
      PlantLocation@1100529000 : Record 11012554;
    BEGIN
      IF (Location <> '') AND ("From Depot" = '') THEN BEGIN
        IF CURRENTCOMPANY <> COMPANYNAME THEN
          PlantLocation.CHANGECOMPANY(CURRENTCOMPANY);
        IF PlantLocation.GET(Location) THEN BEGIN
          IF PlantLocation."From Depot (Plant Need)" <> '' THEN BEGIN
            IF PlantLocation.GET(PlantLocation."From Depot (Plant Need)") THEN BEGIN
              IF PlantLocation.Depot THEN
                "From Depot" := PlantLocation.Code;
            END;
          END;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

