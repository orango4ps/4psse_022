OBJECT Codeunit 11126005 Accident Mail Management
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    TableNo=11126005;
    OnRun=VAR
            Accident@1100525000 : Record 11126005;
          BEGIN
            Accident.COPY(Rec);
            SendMailToQAEmployee(Accident);
          END;

  }
  CODE
  {
    VAR
      Text001@1100525000 : TextConst 'DEU=Unfall %1 ist zugewiesen worden;ENU=Accident %1 is assigned;NLD=Ongeval %1 is toegewezen;SVE=Olycksfall %1 har tilldelats';
      Text002@1100525001 : TextConst 'DEU=Unfall %1;ENU=Accident %1;NLD=Ongeval %1;SVE=Olycksfall %1';
      Text003@1100525002 : TextConst 'DEU=%1 von %2 ''%3'' nicht ausgefÅllt.;ENU=%1 of %2 ''%3'' not filled.;NLD=%1 van %2 ''%3'' niet gevuld.;SVE=%1 av %2 ''%3'' Ñr inte ifyllt.';
      Text004@1100525003 : TextConst 'DEU=Absender (Benutzer/Mitarbeiternr.);ENU=Sender (User/Employee No.);NLD=Afzender (gebruiker/werknemernr.);SVE=AvsÑndare (anvÑndare/anstÑllningsnr)';

    PROCEDURE SendMailToQAEmployee@1100525000(Accident@1100525000 : Record 11126005) : Boolean;
    VAR
      UserSetup@1100525002 : Record 91;
      Empl@1100525004 : Record 5200;
      SMTP_Mail@1100525006 : Codeunit 400;
      SenderName@1100525005 : Text[100];
      SenderAddress@1100525007 : Text[100];
      Subject@1100525009 : Text[100];
    BEGIN
      IF (Accident."No." = '') OR (Accident."QA Employee" = '') THEN
        EXIT(FALSE);

      SenderAddress := '';
      IF UserSetup.GET(USERID) THEN BEGIN
        IF UserSetup."Employee No." <> '' THEN BEGIN
          IF Empl.GET(UserSetup."Employee No.") THEN BEGIN
            SenderAddress := Empl."E-Mail";
            SenderName := Empl."Full Name";
          END;
        END;
      END;
      IF SenderAddress = '' THEN BEGIN
        MESSAGE(Text003, Empl.FIELDCAPTION("E-Mail"), Text004, USERID + ' (' + Empl."No." + ')');
        EXIT(FALSE);
      END;

      IF NOT Accident."E-Mail Sent to QA" THEN
        Subject := STRSUBSTNO(Text001, Accident."No.")
      ELSE
        Subject := STRSUBSTNO(Text002, Accident."No.");
      IF NOT Empl.GET(Accident."QA Employee") THEN
        Empl.INIT;
      IF Empl."E-Mail" = '' THEN BEGIN
        MESSAGE(Text003, Empl.FIELDCAPTION("E-Mail"), Accident.FIELDCAPTION("QA Employee"), Accident."QA Employee");
        EXIT(FALSE);
      END;
      SMTP_Mail.CreateMessage(
        SenderName,
        SenderAddress,
        Empl."E-Mail",
        Subject,
        '',
        FALSE);

      SMTP_Mail.AppendBody(Empl."First Name" + ',' + CrLf);
      SMTP_Mail.AppendBody(Subject + CrLf);
      IF Accident."Employee Company" <> '' THEN
        SMTP_Mail.AppendBody(Accident.FIELDCAPTION("Employee Company") + ': ' + Accident."Employee Company" + Cr);
      IF Accident."Employee No." <> '' THEN
        SMTP_Mail.AppendBody(Accident.FIELDCAPTION("Employee No.") + ': ' + Accident."Employee No." + Cr);
      SMTP_Mail.AppendBody(Accident.FIELDCAPTION(Name) + ': ' + Accident.Name + Cr);
      SMTP_Mail.AppendBody(Accident.FIELDCAPTION(Description) + ': ' + Accident.Description + Cr);
      SMTP_Mail.AppendBody(Accident.FIELDCAPTION("Date/Time") + ': ' + FORMAT(Accident."Date/Time") + Cr);

      SMTP_Mail.Send;
      //IF SMTP_Mail.RUN THEN
      //  EXIT(TRUE)
      //ELSE
      //  MESSAGE(Text005, Accident.FIELDCAPTION("QA Employee"), GETLASTERRORTEXT);
      //EXIT(FALSE);
    END;

    LOCAL PROCEDURE CrLf@1100525009() retChars : Text[2];
    BEGIN
      retChars[1] := 10;
      retChars[2] := 13;
    END;

    LOCAL PROCEDURE Cr@1100525001() retChars : Text[1];
    BEGIN
      retChars[1] := 10;
    END;

    BEGIN
    END.
  }
}

