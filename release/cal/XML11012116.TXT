OBJECT XMLport 11012116 Imp. Open Entries Vendors
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Imp. Open Entries Vendors;
    DefaultFieldsValidation=No;
    PreserveWhiteSpace=Yes;
    TextEncoding=WINDOWS;
    OnInitXMLport=BEGIN
                    XmlPortID := 11012116;
                    MainTableID := 81;
                    XMLPortRequestPageID := 11127328;
                  END;

    OnPreXMLport=VAR
                   ConversionXMLPort4PS@1100527700 : Record 11020640;
                 BEGIN
                   IF NOT currXMLport.IMPORTFILE THEN BEGIN
                     AddConversionFileFormatLine;
                     ConversionXMLPort4PS.UpdateFromXMLPort(XmlPortID,MainTableID,0,XMLPortRequestPageID,TRUE,ConversionXMLPort."Journal Template Type"::Purche);
                     COMMIT;
                     ERROR('');
                   END;

                   ConversionXMLPort.GET(XmlPortID);

                   ConvMgt.DeleteConComment(XmlPortID);
                   ConvMgt.CreateConComment(XmlPortID,STRSUBSTNO('Import File: %1.',currXMLport.FILENAME));
                   ConvMgt.CreateConComment(XmlPortID,'Start');
                 END;

    Format=Variable Text;
    FieldDelimiter=<None>;
    FieldSeparator=[;];
  }
  ELEMENTS
  {
    { [{7B539476-C20A-46BC-A552-62FED55766AD}];  ;Root                ;Element ;Text     }

    { [{A667E51D-CE85-4A0D-946E-DE2CCC52ED51}];1 ;GeneralJournalLine  ;Element ;Table   ;
                                                  SourceTable=Table81;
                                                  SourceTableView=SORTING(Field1,Field51,Field2);
                                                  Import::OnBeforeInsertRecord=VAR
                                                                                 VendCde@1100409000 : Code[20];
                                                                                 GenJournalTemplate@1100409001 : Record 80;
                                                                               BEGIN
                                                                                 IF AllInputFieldsAreEmpty THEN
                                                                                   currXMLport.SKIP;

                                                                                 CLEAR("Gen. Journal Line");

                                                                                 WITH "Gen. Journal Line" DO BEGIN

                                                                                   IF UseImportDate AND (LDate <> 0D) THEN
                                                                                     PostingDate := LDate
                                                                                   ELSE
                                                                                      PostingDate := ConversionXMLPort."Posting Date";

                                                                                   VendCde := ConvMgt.DetermineNewCode(
                                                                                     LookupVendorNo,
                                                                                     CommentRec."Table Name"::Vendor,
                                                                                     VendCommentCde,
                                                                                     B);

                                                                                   ConvMgt.GenerateGenJnlLine(
                                                                                     ConversionXMLPort."Journal Template Name",
                                                                                     ConversionXMLPort."Journal Batch Name",
                                                                                     PostingDate,
                                                                                     A,
                                                                                     ConversionXMLPort."Balance Account No.",
                                                                                     "Gen. Journal Line");

                                                                                   XmlShowStatus.ShowImportCounter;
                                                                                   ImpLineCounter := ImpLineCounter + 1;

                                                                                   VALIDATE("Document Type","Document Type"::Invoice);
                                                                                   IF FDec > 0 THEN
                                                                                     VALIDATE("Document Type","Document Type"::"Credit Memo");

                                                                                   IF EDec = 0 THEN BEGIN
                                                                                     IF FDec > 0 THEN
                                                                                       VALIDATE("Document Type","Document Type"::Payment)
                                                                                     ELSE
                                                                                       VALIDATE("Document Type","Document Type"::Refund)
                                                                                   END;

                                                                                   VALIDATE("Document No.",A);
                                                                                   VALIDATE("Account Type","Account Type"::Vendor);

                                                                                   IF (VendCde <> '') AND ConvMgt.IsVendorPresent(VendCde) THEN
                                                                                     VALIDATE("Account No.",VendCde)
                                                                                   ELSE
                                                                                     ConvMgt.CreateConComment(XmlPortID,ConvMgt.CreateFieldNotFilledCommentTxt(
                                                                                     23,
                                                                                     B,
                                                                                     FIELDCAPTION("Account No."),
                                                                                     MainTableID,
                                                                                     STRSUBSTNO('%1-%2-%3',"Journal Template Name","Journal Batch Name","Line No.")));

                                                                                   VALIDATE("Document Date",CDate);
                                                                                   VALIDATE("Due Date",DDate);
                                                                                   "Source Code" := GenJournalTemplate."Source Code";
                                                                                   VALIDATE(Amount,FDec);
                                                                                   IF (I <> '') AND (JDec <> 0) THEN BEGIN
                                                                                     VALIDATE("Currency Code",I);
                                                                                     "Gen. Journal Line"."Currency Factor" := JDec/FDec;
                                                                                     VALIDATE(Amount,JDec);
                                                                                   END;
                                                                                   //C037921
                                                                                   IF (I = '') AND ("Currency Code" <> '') THEN
                                                                                     VALIDATE("Currency Code",I);
                                                                                   VALIDATE(Description,STRSUBSTNO(Text20,B,E));
                                                                                   VALIDATE("External Document No.",G);
                                                                                   VALIDATE("Shortcut Dimension 1 Code",H);
                                                                                   VALIDATE("On Hold",K);
                                                                                   VALIDATE(Approved,TRUE);

                                                                                   ConvMgt.ClearGJNLPostingFields("Gen. Journal Line");

                                                                                   MODIFY(TRUE);
                                                                                 END;

                                                                                 ClearFields;
                                                                                 currXMLport.SKIP;
                                                                               END;
                                                                                }

    { [{3AC77EC7-9326-4B76-BA5F-EA1F70D4DA67}];2 ;A                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=20 }

    { [{1B889F9B-B9E2-4749-96B0-D22BA1117049}];2 ;B                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=50 }

    { [{D76B46B3-243B-4FF7-8D1D-C7B64CCFA492}];2 ;C                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  IF C <> '' THEN
                                                                                    EVALUATE(CDate,C);
                                                                                END;

                                                  Width=50 }

    { [{11AAC24C-6612-4FE6-9973-FE5EF2EF2C43}];2 ;D                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  IF D <> '' THEN
                                                                                    EVALUATE(DDate,D);
                                                                                END;

                                                  Width=50 }

    { [{F0633544-507F-4C5E-A35B-A0DD95F11E63}];2 ;E                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  IF E <> '' THEN
                                                                                    EVALUATE(EDec,E);
                                                                                END;

                                                  Width=50 }

    { [{98E7308F-19D5-4AF6-89FB-48DB78E52124}];2 ;F                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  IF F <> '' THEN
                                                                                    EVALUATE(FDec,F);
                                                                                END;

                                                  Width=50 }

    { [{23B2F6EB-78F9-48FB-BC70-858F6A53A74C}];2 ;G                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=50 }

    { [{007CC416-7129-43EA-90B0-193FFE5FCA68}];2 ;H                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=50 }

    { [{8F9BF873-E7E2-450A-B9C5-33703DAFE6BE}];2 ;I                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=50 }

    { [{17D83AA6-D251-4A7A-8475-309C7FF2822C}];2 ;J                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  IF J <> '' THEN
                                                                                    EVALUATE(JDec,J);
                                                                                END;
                                                                                 }

    { [{96548088-F8F5-4882-AA70-31D319F2CC43}];2 ;K                   ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{9FDD90B1-DE37-4078-BECC-993793FD52B6}];2 ;L                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  IF L <> '' THEN
                                                                                    EVALUATE(LDate,L);
                                                                                END;
                                                                                 }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100409000;;Container;
                  Name=Options;
                  ContainerType=ContentArea }

      { 1100409001;1;Field  ;
                  Name=LookupVendorNo;
                  CaptionML=ENU=Find Vendor by Old Vendor No;
                  SourceExpr=LookupVendorNo;
                  OnValidate=BEGIN
                               IF NOT LookupVendorNo THEN
                                 VendCommentCde := '';
                             END;
                              }

      { 1100409002;1;Field  ;
                  Name=VendCommentCde;
                  CaptionML=ENU=Comment Code old Vendor;
                  SourceExpr=VendCommentCde;
                  TableRelation="Comment Code" }

      { 1100525001;1;Field  ;
                  Name=UseImportDate;
                  CaptionML=ENU=Use Import Date;
                  SourceExpr=UseImportDate }

    }
  }
  CODE
  {
    VAR
      ConversionXMLPort@1100409001 : Record 11020640;
      XmlPortID@1100409000 : Integer;
      MainTableID@1100409002 : Integer;
      XMLPortRequestPageID@1100527700 : Integer;
      CommentRec@1100525061 : Record 97;
      ConvMgt@1100525059 : Codeunit 11020212;
      ImpLineCounter@1100409007 : Integer;
      XmlShowStatus@1100409006 : Codeunit 11130006;
      LookupVendorNo@1100409005 : Boolean;
      VendCommentCde@1100409004 : Code[20];
      UseImportDate@1100525001 : Boolean;
      PostingDate@1100409003 : Date;
      CDate@1100409014 : Date;
      DDate@1100409015 : Date;
      FDec@1100525000 : Decimal;
      EDec@1100409008 : Decimal;
      JDec@1100409009 : Decimal;
      LDate@1100409017 : Date;
      Text20@1100409013 : TextConst 'ENU=Vendor/Amount: %1/%2;SVE=Leverantîr/Belopp: %1/%2';
      Text001@1100409012 : TextConst 'ENU=Invoice Amount in Description;SVE=Fakturabelopp i beskrivning';
      Text002@1100409011 : TextConst 'ENU=Remaining Amount Local Currency;SVE=èterstÜende belopp i lokal valuta';
      Text003@1100409010 : TextConst 'ENU=Remaining Amount LCY;SVE=èterstÜende belopp BVA';

    PROCEDURE AddConversionFileFormatLine@1100529901();
    VAR
      ConversionFileFormat@1100409000 : Record 11020691;
    BEGIN
      // This function stores the file format into table [Conversion File Format].
      // There is no other standard documentation; so please maintain this function in case of customization.
      //
      // Parameters: 1: [XMLport No.],
      //             2: [csv-Column],
      //             3: [NAV Table ID],
      //             4: [NAV Field Name],
      //             5: [Data Type of dataport field],
      //             6: [extra Comment].
      //

      WITH ConversionFileFormat DO BEGIN
        ConversionFileFormat.DeleteColumnLines(XmlPortID);
        InsertColumnLine(XmlPortID,'A',MainTableID,"Gen. Journal Line".FIELDNAME("Document No."),'Code20','');
        InsertColumnLine(XmlPortID,'B',MainTableID,"Gen. Journal Line".FIELDNAME("Account No."),'Code20','OldTxt');
        InsertColumnLine(XmlPortID,'C',MainTableID,"Gen. Journal Line".FIELDNAME("Document Date"),'Date','');
        InsertColumnLine(XmlPortID,'D',MainTableID,"Gen. Journal Line".FIELDNAME("Due Date"),'Date','');
        InsertColumnLine(XmlPortID,'E',0,Text001,'Decimal',Text001);
        InsertColumnLine(XmlPortID,'F',MainTableID,"Gen. Journal Line".FIELDNAME(Amount),'Decimal',Text002);
        InsertColumnLine(XmlPortID,'G',MainTableID,"Gen. Journal Line".FIELDNAME("External Document No."),'Code20','');
        InsertColumnLine(XmlPortID,'H',MainTableID,"Gen. Journal Line".FIELDNAME("Shortcut Dimension 1 Code"),'Code20','');
        InsertColumnLine(XmlPortID,'I',MainTableID,"Gen. Journal Line".FIELDNAME("Currency Code"),'Code10','');
        InsertColumnLine(XmlPortID,'J',MainTableID,"Gen. Journal Line".FIELDNAME(Amount),'Decimal',Text003);
        InsertColumnLine(XmlPortID,'K',MainTableID,"Gen. Journal Line".FIELDNAME("On Hold"),'Code3','');
        InsertColumnLine(XmlPortID,'L',MainTableID,"Gen. Journal Line".FIELDNAME("Posting Date"),'Date','');
      END;
    END;

    PROCEDURE StripData@1100485000();
    BEGIN
    END;

    PROCEDURE ClearFields@1100529902();
    BEGIN
      CLEAR("Gen. Journal Line");

      CLEAR(A);
      CLEAR(B);
      CLEAR(C);
      CLEAR(D);
      CLEAR(E);
      CLEAR(F);
      CLEAR(G);
      CLEAR(H);
      CLEAR(I);
      CLEAR(J);
      CLEAR(K);
      CLEAR(L);

      CLEAR(CDate);
      CLEAR(DDate);
      CLEAR(FDec);
      CLEAR(EDec);
      CLEAR(JDec);
      CLEAR(LDate);
    END;

    LOCAL PROCEDURE AllInputFieldsAreEmpty@1100525000() : Boolean;
    BEGIN
      EXIT(
        (A = '') AND
        (B = '') AND
        (C = '') AND
        (D = '') AND
        (E = '') AND
        (F = '') AND
        (G = '') AND
        (H = '') AND
        (I = '') AND
        (J = '') AND
        (K = '') AND
        (L = ''));
    END;

    PROCEDURE InitXMLport@1100529500(iLookupVendorNo@1100529502 : Boolean;iVendCommentCde@1100529501 : Code[20];iUseImportDate@1100529500 : Boolean);
    BEGIN
      LookupVendorNo := iLookupVendorNo;
      VendCommentCde := iVendCommentCde;
      UseImportDate := iUseImportDate;
    END;

    BEGIN
    END.
  }
}

