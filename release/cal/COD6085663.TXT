OBJECT Codeunit 6085663 Online Client Resource Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW110.00.00.5.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            DownloadAndImportClientAddins;
          END;

  }
  CODE
  {

    LOCAL PROCEDURE DownloadAndImportClientAddins@6();
    VAR
      Addin@1000 : Record 2000000069;
      TempAddInFile@1006 : Record 6085608;
      TempScannerAddInFile@1007 : Record 6085608;
      TempServerAssemblyFile@1008 : Record 6085608;
      COMgt@1001 : Codeunit 6192772;
      WriteStream@1009 : OutStream;
      ReadStream@1010 : InStream;
    BEGIN
      IF NOT Addin.WRITEPERMISSION THEN
        EXIT;

      COMgt.DownloadFile(GetClientAddInResourceUrl,'',TempAddInFile,FALSE);
      COMgt.DownloadFile(GetScannerAddInResourceUrl,'',TempScannerAddInFile,FALSE);
      COMgt.DownloadFile(GetServerAssembliesResourceUrl,'',TempServerAssemblyFile,FALSE);

      Addin.SETFILTER("Add-in Name",'Continia.DocumentCapture.Dynamics.*|Akeyto.DocumentCapture.Dynamics.*');
      Addin.SETFILTER(Category,'<>%1',Addin.Category::"JavaScript Control Add-in");
      IF Addin.FINDSET(TRUE,FALSE) THEN
        REPEAT
          CASE Addin."Add-in Name" OF
            'Akeyto.DocumentCapture.Dynamics.Nav.EventNotifier',
            'Akeyto.DocumentCapture.Dynamics.Nav.MatchInfoBar',
            'Akeyto.DocumentCapture.Dynamics.Nav.UI',
            'Continia.DocumentCapture.Dynamics.Nav.ClientEventNotifier',
            'Continia.DocumentCapture.Dynamics.Nav.DocDragAndDrop110',
            'Continia.DocumentCapture.Dynamics.Nav.DocDragAndDrop220',
            'Continia.DocumentCapture.Dynamics.Nav.Page.Controls.RadioButton',
            'Continia.DocumentCapture.Dynamics.Nav.Page.EventListener':
              BEGIN
                TempAddInFile.Data.CREATEINSTREAM(ReadStream);
                Addin.Resource.CREATEOUTSTREAM(WriteStream);
                COPYSTREAM(WriteStream,ReadStream);
                Addin.MODIFY;
              END;
            'Akeyto.DocumentCapture.Dynamics.Nav.Scanner':
              BEGIN
                TempScannerAddInFile.Data.CREATEINSTREAM(ReadStream);
                Addin.Resource.CREATEOUTSTREAM(WriteStream);
                COPYSTREAM(WriteStream,ReadStream);
                Addin.MODIFY;
              END;
             'Akeyto.DocumentCapture.Dynamics.Nav.Shared',
             'Continia.DocumentCapture.Dynamics.Nav.MessageBus':
               BEGIN
                 TempServerAssemblyFile.Data.CREATEINSTREAM(ReadStream);
                 Addin.Resource.CREATEOUTSTREAM(WriteStream);
                 COPYSTREAM(WriteStream,ReadStream);
                 Addin.MODIFY;
               END;
          END;
        UNTIL Addin.NEXT = 0;
    END;

    LOCAL PROCEDURE GetClientAddInResourceUrl@2() : Text[1024];
    VAR
      CODC@1000 : Codeunit 6085617;
      AboutDC@1001 : Codeunit 6085589;
      ContiniaVersionMgt@6085573 : Codeunit 6192775;
    BEGIN
      EXIT(STRSUBSTNO('%1/addins/%2/%3/clientaddins.zip',CODC.GetResourceUrl,AboutDC.ControlAddInVersion,ContiniaVersionMgt.NAVVersion));
    END;

    LOCAL PROCEDURE GetScannerAddInResourceUrl@16() : Text[1024];
    VAR
      CODC@1001 : Codeunit 6085617;
      AboutDC@1000 : Codeunit 6085589;
      ContiniaVersionMgt@6085573 : Codeunit 6192775;
    BEGIN
      EXIT(STRSUBSTNO('%1/addins/%2/%3/scanneraddin.zip',CODC.GetResourceUrl,AboutDC.ControlAddInVersion,ContiniaVersionMgt.NAVVersion));
    END;

    LOCAL PROCEDURE GetServerAssembliesResourceUrl@7() : Text[1024];
    VAR
      CODC@1001 : Codeunit 6085617;
      AboutDC@1000 : Codeunit 6085589;
      ContiniaVersionMgt@6085573 : Codeunit 6192775;
    BEGIN
      EXIT(STRSUBSTNO('%1/addins/%2/%3/serverassemblies.zip',CODC.GetResourceUrl,AboutDC.ControlAddInVersion,ContiniaVersionMgt.NAVVersion));
    END;

    BEGIN
    END.
  }
}

