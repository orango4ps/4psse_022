OBJECT Page 11130364 Copy Document Parts
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Belegteile kopieren;
               ENU=Copy Document Parts;
               NLD=Documentdelen kopiâren];
    PageType=StandardDialog;
    OnQueryClosePage=BEGIN
                       CloseActionOK := CloseAction IN [ACTION::OK,ACTION::LookupOK];
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100525000;1;Group  ;
                CaptionML=[DEU=Kopieren von;
                           ENU=Copy from;
                           NLD=Kopiâren van;
                           NOR=Kopier fra;
                           SVE=Kopiera frÜn];
                GroupType=Group }

    { 1100525002;2;Field  ;
                CaptionML=[DEU=Ebene;
                           ENU=Level;
                           NLD=Niveau;
                           NOR=NivÜ;
                           SVE=NivÜ];
                OptionCaptionML=[DEU=Allgemein,,Kreditor,,,,,Bestellung;
                                 ENU=General,,Vendor,,,,,Purchase Order;
                                 NLD=Algemeen,,Leverancier,,,,,Inkooporder;
                                 SVE=AllmÑn,,Leverantîr,,,,,Inkîpsorder];
                SourceExpr=gFromChoice }

    { 1100525004;2;Field  ;
                CaptionML=[DEU=Code;
                           ENU=Code;
                           NLD=Code;
                           NOR=Kode;
                           SVE=Kod];
                SourceExpr=gFromCode;
                OnValidate=BEGIN
                             DocumentPartsSet.GET(gFromChoice, gFromCode);
                           END;

                OnLookup=BEGIN
                           DocumentPartsSet.RESET;
                           DocumentPartsSet.SETRANGE(Level, gFromChoice) ;
                           IF PAGE.RUNMODAL(0, DocumentPartsSet) = ACTION::LookupOK THEN
                             gFromCode := DocumentPartsSet.Code;
                           DocumentPartsSet.RESET;
                         END;
                          }

    { 1100525001;1;Group  ;
                CaptionML=[DEU=Kopieren in;
                           ENU=Copy to;
                           NLD=Kopiâren naar;
                           NOR=Kopier til;
                           SVE=Kopiera till];
                GroupType=Group }

    { 1100528200;2;Field  ;
                CaptionML=[DEU=Ebene;
                           ENU=Level;
                           NLD=Niveau;
                           NOR=NivÜ;
                           SVE=NivÜ];
                OptionCaptionML=[DEU=Allgemein,,Kreditor,,,,,Bestellung;
                                 ENU=General,,Vendor,,,,,Purchase Order;
                                 NLD=Algemeen,,Leverancier,,,,,Inkooporder;
                                 SVE=AllmÑn,,Leverantîr,,,,,Inkîpsorder];
                SourceExpr=gToChoice }

    { 1100525005;2;Field  ;
                CaptionML=[DEU=Code;
                           ENU=Code;
                           NLD=Code;
                           NOR=Kode;
                           SVE=Kod];
                SourceExpr=gToCode;
                OnValidate=VAR
                             Vendor@1100528201 : Record 23;
                             PurchaseHeader@1100528200 : Record 38;
                           BEGIN
                             CASE gToChoice OF
                               gToChoice::General:
                                 BEGIN
                                   DocumentPartsSet.GET(gToChoice, gToCode );
                                 END;
                               gToChoice::Vendor:
                                 BEGIN
                                   Vendor.GET(gToCode);
                                 END;
                               gToChoice::"Purchase Order":
                                 BEGIN
                                   PurchaseHeader.GET(PurchaseHeader."Document Type"::Order, gToCode);
                                 END;
                             END;
                           END;

                OnLookup=VAR
                           Vendor@1100528201 : Record 23;
                           PurchaseHeader@1100528202 : Record 38;
                         BEGIN
                           CASE gToChoice OF
                             gToChoice::General:
                               BEGIN
                                 DocumentPartsSet.RESET;
                                 DocumentPartsSet.SETRANGE(Level, gToChoice);
                                 IF PAGE.RUNMODAL(0, DocumentPartsSet) = ACTION::LookupOK THEN
                                   gToCode := DocumentPartsSet.Code;
                                 DocumentPartsSet.RESET;
                               END;
                             gToChoice::Vendor:
                               BEGIN
                                 IF PAGE.RUNMODAL(0, Vendor) = ACTION::LookupOK THEN
                                   gToCode := Vendor."No.";
                               END;
                             gToChoice::"Purchase Order":
                               BEGIN
                                 PurchaseHeader.SETRANGE("Document Type", PurchaseHeader."Document Type"::Order);
                                   IF PAGE.RUNMODAL(0, PurchaseHeader) = ACTION::LookupOK THEN
                                 gToCode := PurchaseHeader."No.";
                               END;
                           END;
                         END;
                          }

    { 1100528400;1;Group  ;
                CaptionML=[DEU=Ausgleichen;
                           ENU=Apply;
                           NLD=Toepassen;
                           NOR=Koble;
                           SVE=VerkstÑll];
                GroupType=Group }

    { 1100525008;2;Field  ;
                CaptionML=[DEU=Vorhandene Belegteile entfernen;
                           ENU=Remove existing document parts;
                           NLD=Verwijder bestaande document delen];
                SourceExpr=gOverwriteExisting }

  }
  CODE
  {
    VAR
      DocumentPartsSet@1100528202 : Record 11012402;
      gFromChoice@1100525000 : 'General,,Vendor,,,,,Purchase Order';
      gFromCode@1100525001 : Code[20];
      gToCode@1100525002 : Code[20];
      gToChoice@1100528200 : 'General,,Vendor,,,,,Purchase Order';
      gOverwriteExisting@1100525004 : Boolean;
      Text001@1100525012 : TextConst 'DEU=Sie mÅssen eine Bis-Codenr. angeben;ENU=You have to specify a To Code No.;NLD=U moet een Naar Code invullen.';
      Text002@1100525011 : TextConst 'DEU=Sie mÅssen eine Ab-Codenr. angeben;ENU=You have to specify a From Coder No.;NLD=U moet een Van Code invullen.';
      CloseActionOK@1100528400 : Boolean;
      Text003@1100528203 : TextConst 'DEU=Ab %1 ist nicht vorhanden.;ENU=From %1 does not exist.;NLD=Van %1 bestaat niet.';
      Text004@1100528204 : TextConst 'DEU=Belegteile kopieren\;ENU=Copy Document Parts\;NLD=Kopieren documentdelen\';
      Text005@1100528205 : TextConst 'DEU=Fortschritt: @1@@@@;ENU=Progress: @1@@@@;NLD=Voortgang: @1@@@@';
      ProgressBar@1100528206 : Dialog;
      ProgressPercentage@1100528207 : Integer;
      TotalCount@1100528208 : Integer;
      Counter@1100528209 : Integer;

    PROCEDURE SetSelection@1100525001(lDocumentPartsSet@1100525000 : Record 11012402;EmptySet@1100528200 : Boolean);
    VAR
      PurchaseHeader@1100528201 : Record 38;
      lDocumentPartsSet2@1100528203 : Record 11012402;
    BEGIN
      CASE lDocumentPartsSet.Level OF
        lDocumentPartsSet.Level::General:
          BEGIN
            IF EmptySet THEN BEGIN
              gFromChoice := gFromChoice::General;
              gFromCode := '';
              gToChoice := gToChoice::General;
              gToCode := lDocumentPartsSet.Code;
            END ELSE BEGIN
              gFromChoice := gFromChoice::General;
              gFromCode := lDocumentPartsSet.Code;
              gToChoice := gToChoice::General;
              gToCode := '';
            END;
          END;
        lDocumentPartsSet.Level::"Purchase Order":
          BEGIN
            IF EmptySet THEN BEGIN
              gFromChoice := gFromChoice::General;
              gFromCode := '';
              IF PurchaseHeader.GET(PurchaseHeader."Document Type"::Order,  lDocumentPartsSet.Code) THEN BEGIN
                IF lDocumentPartsSet2.GET(lDocumentPartsSet2.Level::Vendor, PurchaseHeader."Buy-from Vendor No.") THEN BEGIN
                  gFromChoice := gFromChoice::Vendor;
                  gFromCode := PurchaseHeader."Buy-from Vendor No.";
                END;
              END;
              gToChoice := gToChoice::"Purchase Order";
              gToCode := lDocumentPartsSet.Code;
            END ELSE BEGIN
              PurchaseHeader.INIT;
              IF PurchaseHeader.GET(PurchaseHeader."Document Type"::Order,  lDocumentPartsSet.Code) THEN;
              gFromChoice := gFromChoice::"Purchase Order";
              gFromCode := lDocumentPartsSet.Code;
              gToChoice := gToChoice::Vendor;
              gToCode := PurchaseHeader."Buy-from Vendor No.";
            END;
          END;
        lDocumentPartsSet.Level::Vendor:
          BEGIN
            IF EmptySet THEN BEGIN
              gFromChoice := gFromChoice::General;
              gFromCode := '';
              gToChoice := gToChoice::Vendor;
              gToCode := lDocumentPartsSet.Code;
            END ELSE BEGIN
              gFromChoice := gFromChoice::Vendor;
              gFromCode := lDocumentPartsSet.Code;
              gToChoice := gToChoice::Vendor;
              gToCode := '';
            END;
          END;
      ELSE
        ERROR('');
      END;
    END;

    PROCEDURE StartProcess@1100485002();
    VAR
      FromDocumentPart@1100525000 : Record 11012403;
      ToDocumentPart@1100525001 : Record 11012403;
      FromDocumentPartsSet@1100528201 : Record 11012402;
      ToDocumentPartsSet@1100528200 : Record 11012402;
    BEGIN
      IF gToCode = '' THEN
        ERROR(Text001);

      IF gFromCode = '' THEN
        ERROR(Text002);

      IF NOT FromDocumentPartsSet.GET(gFromChoice,gFromCode) THEN
        ERROR(Text003,FromDocumentPartsSet.TABLECAPTION);

      IF NOT ToDocumentPartsSet.GET(gToChoice,gToCode) THEN BEGIN
        ToDocumentPartsSet.Level := gToChoice;
        ToDocumentPartsSet.Code := gToCode;
        ToDocumentPartsSet.INSERT(TRUE);
      END;

      Counter := 0;
      ProgressPercentage := 0;
      ProgressBar.OPEN(Text004+Text005,ProgressPercentage);

      FromDocumentPart.SETRANGE(Level, gFromChoice);
      FromDocumentPart.SETRANGE("Set Code", gFromCode);
      TotalCount := FromDocumentPart.COUNT;
      IF FromDocumentPart.FIND('-') THEN BEGIN
        IF gOverwriteExisting THEN BEGIN
          ToDocumentPart.SETRANGE(Level, gToChoice);
          ToDocumentPart.SETRANGE("Set Code", gToCode);
          ToDocumentPart.DELETEALL(TRUE);
        END;
        REPEAT
          Counter += 1;
          ProgressPercentage := ROUND(10000*(Counter/TotalCount),1);
          ProgressBar.UPDATE();
          ToDocumentPart.INIT;
          ToDocumentPart.Level := gToChoice;
          ToDocumentPart."Set Code" := gToCode;
          ToDocumentPart.Code := FromDocumentPart.Code;
          IF NOT ToDocumentPart.FIND('=') THEN BEGIN
            ToDocumentPart.INSERT(TRUE);
            ToDocumentPart.CopyFileFromCodeToCode(FromDocumentPart);
            ToDocumentPart.Description := FromDocumentPart.Description;
            //DP01292.sn
            ToDocumentPart."Source Type (Level)" := FromDocumentPart.Level+1;
            ToDocumentPart."Source Type (Set)" := FromDocumentPart."Set Code";
            //DP01292.en
            ToDocumentPart.MODIFY;
          END;
        UNTIL FromDocumentPart.NEXT = 0;
      END;
      ProgressBar.CLOSE;
    END;

    LOCAL PROCEDURE StdDialogConfirm@1100528400() : Boolean;
    BEGIN
      CurrPage.RUNMODAL;
      EXIT(CloseActionOK);
    END;

    LOCAL PROCEDURE StdDialogDoAction@1100528401();
    BEGIN
      StartProcess;
    END;

    PROCEDURE StdDialogRunModal@1100528404();
    BEGIN
      IF StdDialogConfirm THEN
        StdDialogDoAction;
    END;

    BEGIN
    END.
  }
}

