OBJECT Codeunit 16 Gen. Jnl.-Show CT Entries
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    TableNo=81;
    OnRun=BEGIN
            IF NOT ("Document Type" IN ["Document Type"::Payment,"Document Type"::Refund,"Document Type"::" "]) THEN
              EXIT;
            IF NOT ("Account Type" IN ["Account Type"::Customer,"Account Type"::Vendor,"Account Type"::Employee]) THEN
              EXIT;

            SetFiltersOnCreditTransferEntry(Rec,CreditTransferEntry);

            PAGE.RUN(PAGE::"Credit Transfer Reg. Entries",CreditTransferEntry);
          END;

  }
  CODE
  {
    VAR
      CreditTransferEntry@1000 : Record 1206;

    [External]
    PROCEDURE SetFiltersOnCreditTransferEntry@86(VAR GenJournalLine@1005 : Record 81;VAR CreditTransferEntry@1000 : Record 1206);
    VAR
      GeneralLedgerSetup@1001 : Record 98;
      CustLedgerEntry@1002 : Record 21;
      VendorLedgerEntry@1003 : Record 25;
      EmployeeLedgerEntry@1006 : Record 5222;
      FoundCorrespondingLedgerEntry@1004 : Boolean;
    BEGIN
      WITH GenJournalLine DO BEGIN
        CreditTransferEntry.RESET;
        FoundCorrespondingLedgerEntry := FALSE;
        CASE "Account Type" OF
          "Account Type"::Vendor:
            BEGIN
              CreditTransferEntry.SETRANGE("Account Type",CreditTransferEntry."Account Type"::Vendor);
              IF "Applies-to Doc. No." <> '' THEN BEGIN
                VendorLedgerEntry.SETRANGE("Document Type","Applies-to Doc. Type");
                VendorLedgerEntry.SETRANGE("Document No.","Applies-to Doc. No.");
                IF VendorLedgerEntry.FINDFIRST THEN BEGIN
                  CreditTransferEntry.SETRANGE("Applies-to Entry No.",VendorLedgerEntry."Entry No.");
                  FoundCorrespondingLedgerEntry := TRUE;
                END;
              END;
            END;
          "Account Type"::Customer:
            BEGIN
              CreditTransferEntry.SETRANGE("Account Type",CreditTransferEntry."Account Type"::Customer);
              IF "Applies-to Doc. No." <> '' THEN BEGIN
                CustLedgerEntry.SETRANGE("Document Type","Applies-to Doc. Type");
                CustLedgerEntry.SETRANGE("Document No.","Applies-to Doc. No.");
                IF CustLedgerEntry.FINDFIRST THEN BEGIN
                  CreditTransferEntry.SETRANGE("Applies-to Entry No.",CustLedgerEntry."Entry No.");
                  FoundCorrespondingLedgerEntry := TRUE;
                END;
              END;
            END;
          "Account Type"::Employee:
            BEGIN
              CreditTransferEntry.SETRANGE("Account Type",CreditTransferEntry."Account Type"::Employee);
              IF "Applies-to Doc. No." <> '' THEN BEGIN
                EmployeeLedgerEntry.SETRANGE("Document Type","Applies-to Doc. Type");
                EmployeeLedgerEntry.SETRANGE("Document No.","Applies-to Doc. No.");
                IF EmployeeLedgerEntry.FINDFIRST THEN BEGIN
                  CreditTransferEntry.SETRANGE("Applies-to Entry No.",EmployeeLedgerEntry."Entry No.");
                  FoundCorrespondingLedgerEntry := TRUE;
                END;
              END;
            END;
        END;
        CreditTransferEntry.SETRANGE("Account No.","Account No.");
        IF NOT FoundCorrespondingLedgerEntry THEN
          CreditTransferEntry.SETRANGE("Applies-to Entry No.",0);
        GeneralLedgerSetup.GET;
        CreditTransferEntry.SETFILTER(
          "Currency Code",'''%1''|''%2''',"Currency Code",GeneralLedgerSetup.GetCurrencyCode("Currency Code"));
        CreditTransferEntry.SETRANGE(Canceled,FALSE);
      END;
    END;

    BEGIN
    END.
  }
}

