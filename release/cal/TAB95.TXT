OBJECT Table 95 G/L Budget Name
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW19.00,4PS14.00;
  }
  PROPERTIES
  {
    OnModify=BEGIN
               IF ("Budget Dimension 1 Code" <> xRec."Budget Dimension 1 Code") OR
                  ("Budget Dimension 2 Code" <> xRec."Budget Dimension 2 Code") OR
                  ("Budget Dimension 3 Code" <> xRec."Budget Dimension 3 Code") OR
                  ("Budget Dimension 4 Code" <> xRec."Budget Dimension 4 Code")
               THEN
                 UpdateGLBudgetEntryDim;
             END;

    OnDelete=VAR
               GLBudgetEntry@1000 : Record 96;
               AnalysisViewBudgetEntry@1001 : Record 366;
             BEGIN
               GLBudgetEntry.SETCURRENTKEY("Budget Name");
               GLBudgetEntry.SETRANGE("Budget Name",Name);
               GLBudgetEntry.DELETEALL(TRUE);

               AnalysisViewBudgetEntry.SETRANGE("Budget Name",Name);
               AnalysisViewBudgetEntry.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=G/L Budget Name;
               NOR=Budsjettnavn;
               SVE=Redov.budgetnamn];
    LookupPageID=Page121;
  }
  FIELDS
  {
    { 1   ;   ;Name                ;Code10        ;CaptionML=[ENU=Name;
                                                              NOR=Navn;
                                                              SVE=Namn];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text80        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 3   ;   ;Blocked             ;Boolean       ;CaptionML=[ENU=Blocked;
                                                              NOR=Sperret;
                                                              SVE=Sp„rrad] }
    { 4   ;   ;Budget Dimension 1 Code;Code20     ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                IF "Budget Dimension 1 Code" <> xRec."Budget Dimension 1 Code" THEN
                                                                  IF Dim.CheckIfDimUsed("Budget Dimension 1 Code",9,Name,'',0) THEN
                                                                    ERROR(Text000,Dim.GetCheckDimErr);
                                                              END;

                                                   CaptionML=[ENU=Budget Dimension 1 Code;
                                                              NOR=Budsjettdim. 1 - kode;
                                                              SVE=Budget dimension 1 kod] }
    { 5   ;   ;Budget Dimension 2 Code;Code20     ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                IF "Budget Dimension 2 Code" <> xRec."Budget Dimension 2 Code" THEN
                                                                  IF Dim.CheckIfDimUsed("Budget Dimension 2 Code",10,Name,'',0) THEN
                                                                    ERROR(Text000,Dim.GetCheckDimErr);
                                                              END;

                                                   CaptionML=[ENU=Budget Dimension 2 Code;
                                                              NOR=Budsjettdim. 2 - kode;
                                                              SVE=Budget dimension 2 kod] }
    { 6   ;   ;Budget Dimension 3 Code;Code20     ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                IF "Budget Dimension 3 Code" <> xRec."Budget Dimension 3 Code" THEN
                                                                  IF Dim.CheckIfDimUsed("Budget Dimension 3 Code",11,Name,'',0) THEN
                                                                    ERROR(Text000,Dim.GetCheckDimErr);
                                                              END;

                                                   CaptionML=[ENU=Budget Dimension 3 Code;
                                                              NOR=Budsjettdim. 3 - kode;
                                                              SVE=Budget dimension 3 kod] }
    { 7   ;   ;Budget Dimension 4 Code;Code20     ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                IF "Budget Dimension 4 Code" <> xRec."Budget Dimension 4 Code" THEN
                                                                  IF Dim.CheckIfDimUsed("Budget Dimension 4 Code",12,Name,'',0) THEN
                                                                    ERROR(Text000,Dim.GetCheckDimErr);
                                                              END;

                                                   CaptionML=[ENU=Budget Dimension 4 Code;
                                                              NOR=Budsjettdim. 4 - kode;
                                                              SVE=Budget dimension 4 kod] }
    { 11012000;;Budget Responsible ;Code50        ;TableRelation=User."User Name";
                                                   OnValidate=BEGIN
                                                                UserMgt.ValidateUserID("Budget Responsible");
                                                              END;

                                                   OnLookup=BEGIN
                                                              UserMgt.LookupUserID("Budget Responsible");
                                                            END;

                                                   CaptionML=[ENU=Budget Responsible;
                                                              SVE=Budgetansvarig] }
  }
  KEYS
  {
    {    ;Name                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1\You cannot use the same dimension twice in the same budget.;NOR=%1\Du kan ikke bruke samme dimensjon to ganger i samme budsjett.;SVE=%1\Du kan inte anv„nda samma dimension tv† g†nger i samma budget.';
      Text001@1001 : TextConst 'ENU=Updating budget entries @1@@@@@@@@@@@@@@@@@@;NOR=Oppdatere budsjettposter@1@@@@@@@@@@@@@@@@@@;SVE=Uppdaterar budgettrans. @1@@@@@@@@@@@@@@@@@@';
      Dim@1002 : Record 348;
      DimSetEntry@1004 : Record 480;
      TempDimSetEntry@1003 : TEMPORARY Record 480;
      UserMgt@1100485000 : Codeunit 418;

    LOCAL PROCEDURE UpdateGLBudgetEntryDim@1();
    VAR
      GLBudgetEntry@1007 : Record 96;
      Window@1005 : Dialog;
      TotalCount@1004 : Integer;
      i@1003 : Integer;
      T0@1006 : Time;
    BEGIN
      GLBudgetEntry.SETCURRENTKEY("Budget Name");
      GLBudgetEntry.SETRANGE("Budget Name",Name);
      GLBudgetEntry.SETFILTER("Dimension Set ID",'<>%1',0);
      TotalCount := COUNT;
      Window.OPEN(Text001);
      T0 := TIME;
      GLBudgetEntry.LOCKTABLE;
      IF GLBudgetEntry.FINDSET THEN
        REPEAT
          i := i + 1;
          IF TIME > T0 + 750 THEN BEGIN
            Window.UPDATE(1,10000 * i DIV TotalCount);
            T0 := TIME;
          END;
          GLBudgetEntry."Budget Dimension 1 Code" := GetDimValCode(GLBudgetEntry."Dimension Set ID","Budget Dimension 1 Code");
          GLBudgetEntry."Budget Dimension 2 Code" := GetDimValCode(GLBudgetEntry."Dimension Set ID","Budget Dimension 2 Code");
          GLBudgetEntry."Budget Dimension 3 Code" := GetDimValCode(GLBudgetEntry."Dimension Set ID","Budget Dimension 3 Code");
          GLBudgetEntry."Budget Dimension 4 Code" := GetDimValCode(GLBudgetEntry."Dimension Set ID","Budget Dimension 4 Code");
          GLBudgetEntry.MODIFY;
        UNTIL GLBudgetEntry.NEXT = 0;
      Window.CLOSE;
    END;

    LOCAL PROCEDURE GetDimValCode@3(DimSetID@1000 : Integer;DimCode@1001 : Code[20]) : Code[20];
    BEGIN
      IF DimCode = '' THEN
        EXIT('');
      IF TempDimSetEntry.GET(DimSetID,DimCode) THEN
        EXIT(TempDimSetEntry."Dimension Value Code");
      IF DimSetEntry.GET(DimSetID,DimCode) THEN
        TempDimSetEntry := DimSetEntry
      ELSE BEGIN
        TempDimSetEntry.INIT;
        TempDimSetEntry."Dimension Set ID" := DimSetID;
        TempDimSetEntry."Dimension Code" := DimCode;
      END;
      TempDimSetEntry.INSERT;
      EXIT(TempDimSetEntry."Dimension Value Code")
    END;

    BEGIN
    END.
  }
}

