OBJECT Table 11072343 DPT Authority Perm ServLoc
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnModify=BEGIN
               CALCFIELDS("Subsite Structure");
               IF ("Subsite Structure" < "Subsite Structure"::PermissionPerDocType) AND (Type = Type::Access) THEN
                 "Document Type" := '';
               CheckPermissions;
             END;

    OnDelete=BEGIN
               CheckRecordChangeAllowed;
             END;

    OnRename=BEGIN
               CheckRecordChangeAllowed;
             END;

    CaptionML=ENU=DPT Authority Permission (Service);
  }
  FIELDS
  {
    { 10  ;   ;Type                ;Option        ;CaptionML=SVE=Radtyp;
                                                   OptionCaptionML=ENU=,Access;
                                                   OptionString=,Access;
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 20  ;   ;Service Location No.;Code20        ;TableRelation="Service Location".No.;
                                                   CaptionML=[ENU=Service Location No.;
                                                              NOR=Servicelokasjon Nr.;
                                                              SVE=Serviceplatsnr];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 30  ;   ;Authority Type      ;Code20        ;TableRelation="Service Authority DPT"."Authority Type" WHERE (Source Type=CONST(ServLocation),
                                                                                                                 Source Code=FIELD(Service Location No.));
                                                   CaptionML=[ENU=Authority Type;
                                                              NOR=Myndighetstype;
                                                              SVE=Beh”righetstyp];
                                                   NotBlank=Yes }
    { 31  ;   ;Authority Type Description;Text50  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Authority Type".Description WHERE (Code=FIELD(Authority Type)));
                                                   CaptionML=[ENU=Authority Type Description;
                                                              NOR=Beskrivelse av myndighetstype;
                                                              SVE=Beskrivning av beh”righetstyp];
                                                   Editable=No }
    { 40  ;   ;Contact No.         ;Code20        ;TableRelation="Service Authority DPT"."Contact No." WHERE (Source Type=CONST(ServLocation),
                                                                                                              Source Code=FIELD(Service Location No.),
                                                                                                              Authority Type=FIELD(Authority Type));
                                                   CaptionML=[ENU=Contact No.;
                                                              NOR=Kontaktnr;
                                                              SVE=Kontaktnr];
                                                   NotBlank=Yes }
    { 41  ;   ;Contact Name        ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=[ENU=Contact Name;
                                                              NOR=Kontaktnavn;
                                                              SVE=Kontaktnamn];
                                                   Editable=No }
    { 50  ;   ;Contact Person No.  ;Code20        ;TableRelation="Service Authority DPT"."Contact No." WHERE (Source Type=CONST(ServLocation),
                                                                                                              Source Code=FIELD(Service Location No.),
                                                                                                              Authority Type=FIELD(Authority Type),
                                                                                                              Contact No.=FIELD(Contact No.));
                                                   CaptionML=[ENU=Contact Person No.;
                                                              NOR=Kontaktperson;
                                                              SVE=Kontaktperson] }
    { 51  ;   ;Contact Person Name ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Contact Person No.)));
                                                   CaptionML=[ENU=Contact Person Name;
                                                              NOR=Kontaktpersonens navn;
                                                              SVE=Kontaktpersonens namn];
                                                   Editable=No }
    { 60  ;   ;Subsite Code        ;Code20        ;TableRelation="DPT Subsite ServLoc".Code WHERE (Service Location No.=FIELD(Service Location No.));
                                                   CaptionML=ENU=Subsite Code }
    { 61  ;   ;Subsite Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Subsite ServLoc".Description WHERE (Service Location No.=FIELD(Service Location No.),
                                                                                                               Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Subsite Description;
                                                   Editable=No }
    { 62  ;   ;Subsite Structure   ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Subsite ServLoc"."Subsite Structure" WHERE (Service Location No.=FIELD(Service Location No.),
                                                                                                                       Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Subsite Structure;
                                                   OptionCaptionML=ENU=1 Document Library,Document Library per Document Type,Permission per Document Type;
                                                   OptionString=OneDocLibrary,DocLibraryPerDocType,PermissionPerDocType;
                                                   Editable=No }
    { 70  ;   ;Document Type       ;Code20        ;TableRelation=IF (Type=CONST(Access),
                                                                     Subsite Structure=FILTER(>OneDocLibrary)) "DPT Subsite Doc Type ServLoc"."Document Type" WHERE (Service Location No.=FIELD(Service Location No.),
                                                                                                                                                                     Subsite Code=FIELD(Subsite Code));
                                                   CaptionML=[ENU=Document Type;
                                                              NOR=Dokumenttype;
                                                              SVE=Dokumenttyp] }
    { 71  ;   ;Document Type Description;Text50   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Document Type".Description WHERE (Type=FIELD(Document Type)));
                                                   CaptionML=[ENU=Document Type Description;
                                                              NOR=Dokumenttype, Beskrivelse;
                                                              SVE=Dokumenttyp, beskrivning];
                                                   Editable=No }
    { 90  ;   ;Permission Level    ;Option        ;OnValidate=BEGIN
                                                                CheckPermissions;
                                                              END;

                                                   CaptionML=ENU=Permission Level;
                                                   OptionCaptionML=ENU=None,Visitor,Member;
                                                   OptionString=None,Visitor,Member }
    { 100 ;   ;Show in View OLD    ;Boolean       ;CaptionML=;
                                                   Editable=No }
    { 110 ;   ;Subsite Synced On   ;DateTime      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Subsite ServLoc"."Synchronized On" WHERE (Service Location No.=FIELD(Service Location No.),
                                                                                                                     Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Subsite Synchronized On;
                                                   Editable=No }
    { 120 ;   ;Subsite Doc Type Synced On;DateTime;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Subsite Doc Type ServLoc"."Synchronized On" WHERE (Service Location No.=FIELD(Service Location No.),
                                                                                                                              Subsite Code=FIELD(Subsite Code),
                                                                                                                              Document Type=FIELD(Document Type)));
                                                   CaptionML=ENU=Subsite Document Type Synchronized On;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Type,Service Location No.,Authority Type,Contact No.,Contact Person No.,Subsite Code,Document Type;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      AddCounter@1100528301 : Integer;
      ModifyCounter@1100528300 : Integer;
      Text000@1100528303 : TextConst 'ENU=%1 Permission Records added and %2 Permission Records modified.';

    PROCEDURE CheckPermissions@1100409001();
    BEGIN
      CheckModifyPermission;
      CheckPermissionLevel;
    END;

    LOCAL PROCEDURE CheckPermissionLevel@1100528302();
    VAR
      DPTAuthorityPermServLoc@1100528300 : Record 11072343;
    BEGIN
      IF (Type <> Type::Access) THEN
        EXIT;
      IF (("Subsite Code" = '') AND ("Document Type" = '') AND ("Permission Level" = "Permission Level"::None)) THEN BEGIN
        DPTAuthorityPermServLoc.SETRANGE(Type , DPTAuthorityPermServLoc.Type::Access);
        DPTAuthorityPermServLoc.SETRANGE("Service Location No.", "Service Location No.");
        DPTAuthorityPermServLoc.SETRANGE("Authority Type", "Authority Type");
        DPTAuthorityPermServLoc.SETRANGE("Contact No.", "Contact No.");
        DPTAuthorityPermServLoc.SETRANGE("Contact Person No.", "Contact Person No.");
        DPTAuthorityPermServLoc.SETFILTER("Subsite Code", '<>%1', "Subsite Code");
        DPTAuthorityPermServLoc.MODIFYALL("Permission Level", "Permission Level"::None);
        EXIT;
      END;
      CALCFIELDS("Subsite Structure");
      IF ("Subsite Structure" < "Subsite Structure"::PermissionPerDocType) THEN
        EXIT;

      IF ("Document Type" = '') THEN BEGIN
        DPTAuthorityPermServLoc.SETRANGE(Type, Type);
        DPTAuthorityPermServLoc.SETRANGE("Service Location No.", "Service Location No.");
        DPTAuthorityPermServLoc.SETRANGE("Authority Type", "Authority Type");
        DPTAuthorityPermServLoc.SETRANGE("Contact No.", "Contact No.");
        DPTAuthorityPermServLoc.SETRANGE("Contact Person No.", "Contact Person No.");
        DPTAuthorityPermServLoc.SETRANGE("Subsite Code", "Subsite Code");
        DPTAuthorityPermServLoc.SETFILTER("Document Type", '<>%1', '');
        DPTAuthorityPermServLoc.MODIFYALL("Permission Level", "Permission Level");
      END ELSE BEGIN
        IF (DPTAuthorityPermServLoc.GET(Type, "Authority Type", "Contact No.", "Contact Person No.", "Subsite Code", '')) THEN BEGIN
          IF (DPTAuthorityPermServLoc."Permission Level" < "Permission Level") THEN BEGIN
            DPTAuthorityPermServLoc."Permission Level" := "Permission Level";
            DPTAuthorityPermServLoc.MODIFY;
          END;
        END;
      END;
    END;

    LOCAL PROCEDURE CheckModifyPermission@1100409000();
    VAR
      DPTRespPersPermServLoc@1100409000 : Record 11072340;
    BEGIN
      DPTRespPersPermServLoc."Service Location No." := "Service Location No.";
      DPTRespPersPermServLoc.CheckModifyPermission;
    END;

    PROCEDURE AddPortalPermissions@1100528301(ServiceLocationNo@1100528300 : Code[20];AuthorityTypeFilter@1100528307 : Code[20];ContactNoFilter@1100528308 : Code[20];ContactPersonNoFilter@1100528309 : Code[20];SubsiteCodeFilter@1100528306 : Code[20];ShowMessage@1100528310 : Boolean);
    VAR
      ServiceAuthorityDPT@1100528301 : Record 11072298;
      DPTSubsiteServLoc@1100528303 : Record 11072344;
      DPTSubsiteDocTypeServLoc@1100528302 : Record 11072345;
      ServiceLocation@1100528304 : Record 11012801;
    BEGIN
      AddCounter := 0;
      ModifyCounter := 0;
      ServiceLocation.GET(ServiceLocationNo);

      ServiceAuthorityDPT.SETRANGE("Source Type", ServiceAuthorityDPT."Source Type"::ServLocation);
      ServiceAuthorityDPT.SETRANGE("Source Code", ServiceLocationNo);
      IF ((AuthorityTypeFilter <> '') OR (ContactNoFilter <> '') OR (ContactPersonNoFilter <> '')) THEN BEGIN
        ServiceAuthorityDPT.SETRANGE("Authority Type", AuthorityTypeFilter);
        ServiceAuthorityDPT.SETRANGE("Contact No.", ContactNoFilter);
        ServiceAuthorityDPT.SETRANGE("Contact Person No.", ContactPersonNoFilter);
      END;
      IF (NOT ServiceAuthorityDPT.FINDSET) THEN
        EXIT;

      REPEAT
        InsertRecord(ServiceAuthorityDPT, 1, '', '');
        DPTSubsiteServLoc.SETRANGE("Service Location No.", ServiceLocationNo);
        IF (SubsiteCodeFilter <> '') THEN
          DPTSubsiteServLoc.SETRANGE(Code, SubsiteCodeFilter);
        DPTSubsiteServLoc.SETRANGE("Subsite Structure", DPTSubsiteServLoc."Subsite Structure"::PermissionPerDocType);
        IF (DPTSubsiteServLoc.FINDSET) THEN BEGIN
          REPEAT
            DPTSubsiteDocTypeServLoc.SETRANGE("Service Location No.", ServiceLocationNo);
            DPTSubsiteDocTypeServLoc.SETRANGE("Subsite Code", DPTSubsiteServLoc.Code);
            IF (DPTSubsiteDocTypeServLoc.FINDSET) THEN BEGIN
              InsertRecord(ServiceAuthorityDPT, 1, DPTSubsiteServLoc.Code, ''); //1=Access
              REPEAT
                InsertRecord(ServiceAuthorityDPT, 1, DPTSubsiteServLoc.Code, DPTSubsiteDocTypeServLoc."Document Type");
              UNTIL(DPTSubsiteDocTypeServLoc.NEXT = 0);
            END;
          UNTIL (DPTSubsiteServLoc.NEXT = 0);
        END;

        DPTSubsiteServLoc.SETFILTER("Subsite Structure", '<%1', DPTSubsiteServLoc."Subsite Structure"::PermissionPerDocType);
        IF (DPTSubsiteServLoc.FINDSET) THEN BEGIN
          REPEAT
            InsertRecord(ServiceAuthorityDPT, 1, DPTSubsiteServLoc.Code, ''); //1=Access
          UNTIL (DPTSubsiteServLoc.NEXT = 0);
        END;
      UNTIL (ServiceAuthorityDPT.NEXT = 0);
      IF (ShowMessage) THEN
        MESSAGE(Text000, AddCounter, ModifyCounter - AddCounter);
    END;

    LOCAL PROCEDURE InsertRecord@1100528300(ServiceAuthorityDPT@1100528306 : Record 11072298;PermissionType@1100528300 : Option;SubsiteCode@1100528304 : Code[20];DocumentType@1100528305 : Code[20]);
    VAR
      DPTAuthorityPermServLoc@1100528301 : Record 11072343;
      DPTDefAuthorityPermServLoc@1100528303 : Record 11072342;
      ServiceLocation@1100528302 : Record 11012801;
    BEGIN
      ServiceLocation.GET(ServiceAuthorityDPT."Source Code");
      IF (NOT DPTDefAuthorityPermServLoc.GET(PermissionType, ServiceAuthorityDPT."Authority Type", SubsiteCode, DocumentType))
      THEN BEGIN
        DPTDefAuthorityPermServLoc.INIT;
      END;

      IF (NOT DPTAuthorityPermServLoc.GET(PermissionType, ServiceAuthorityDPT."Source Code", ServiceAuthorityDPT."Authority Type",
          ServiceAuthorityDPT."Contact No.", ServiceAuthorityDPT."Contact Person No.", SubsiteCode, DocumentType))
      THEN BEGIN
        DPTAuthorityPermServLoc.INIT;
        DPTAuthorityPermServLoc.Type := PermissionType;
        DPTAuthorityPermServLoc."Service Location No." := ServiceAuthorityDPT."Source Code";
        DPTAuthorityPermServLoc."Authority Type" := ServiceAuthorityDPT."Authority Type";
        DPTAuthorityPermServLoc."Contact No." := ServiceAuthorityDPT."Contact No.";
        DPTAuthorityPermServLoc."Contact Person No." := ServiceAuthorityDPT."Contact Person No.";
        DPTAuthorityPermServLoc."Subsite Code" := SubsiteCode;
        DPTAuthorityPermServLoc."Document Type" := DocumentType;
        DPTAuthorityPermServLoc.INSERT;
        AddCounter += 1;
      END;
      DPTAuthorityPermServLoc."Permission Level" := DPTDefAuthorityPermServLoc."Permission Level";
      DPTAuthorityPermServLoc.MODIFY;
      ModifyCounter += 1;
    END;

    LOCAL PROCEDURE CheckRecordChangeAllowed@1100528307();
    VAR
      ServiceLocation@1100528300 : Record 11012801;
    BEGIN
      IF (Type <> Type::Access) OR (NOT ServiceLocation.GET("Service Location No.")) THEN
        EXIT;
    END;

    BEGIN
    END.
  }
}

