OBJECT Page 5478 Journal Lines Entity
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[@@@={Locked};
               ENU=journalLines;
               NOR=journalLines;
               SVE=journalLines];
    SourceTable=Table81;
    DelayedInsert=Yes;
    PageType=API;
    EntitySetName=journalLines;
    EntityName=journalLine;
    OnOpenPage=BEGIN
                 GraphMgtJournalLines.SetJournalLineFilters(Rec);
               END;

    OnAfterGetRecord=BEGIN
                       SetCalculatedFields;
                     END;

    OnNewRecord=BEGIN
                  CheckFilters;

                  ClearCalculatedFields;

                  "Document Type" := "Document Type"::" ";
                  "Account Type" := "Account Type"::"G/L Account";
                END;

    OnInsertRecord=VAR
                     TempGenJournalLine@1002 : TEMPORARY Record 81;
                   BEGIN
                     TempGenJournalLine.RESET;
                     TempGenJournalLine.COPY(Rec);

                     CLEAR(Rec);
                     GraphMgtJournalLines.SetJournalLineTemplateAndBatch(
                       Rec,LibraryAPIGeneralJournal.GetBatchNameFromId(TempGenJournalLine.GETFILTER("Journal Batch Id")));
                     LibraryAPIGeneralJournal.InitializeLine(
                       Rec,TempGenJournalLine."Line No.",TempGenJournalLine."Document No.",TempGenJournalLine."External Document No.");

                     GraphMgtJournalLines.SetJournalLineValues(Rec,TempGenJournalLine);

                     UpdateDimensions(FALSE);
                     SetCalculatedFields;
                   END;

    OnModifyRecord=VAR
                     GenJournalLine@1000 : Record 81;
                   BEGIN
                     GenJournalLine.SETRANGE(Id,Id);
                     GenJournalLine.FINDFIRST;

                     IF "Line No." = GenJournalLine."Line No." THEN
                       MODIFY(TRUE)
                     ELSE BEGIN
                       GenJournalLine.TRANSFERFIELDS(Rec,FALSE);
                       GenJournalLine.RENAME("Journal Template Name","Journal Batch Name","Line No.");
                       TRANSFERFIELDS(GenJournalLine,TRUE);
                     END;

                     UpdateDimensions(TRUE);
                     SetCalculatedFields;

                     EXIT(FALSE);
                   END;

    OnAfterGetCurrRecord=BEGIN
                           IF NOT FiltersChecked THEN BEGIN
                             CheckFilters;
                             FiltersChecked := TRUE;
                           END;
                         END;

    ODataKeyFields=Id;
  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                Name=<Control1>;
                CaptionML=[ENU=<Control1>;
                           NOR=<Control1>;
                           SVE=<Control1>];
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                GroupType=Repeater }

    { 8   ;2   ;Field     ;
                Name=id;
                CaptionML=[@@@={Locked};
                           ENU=Id;
                           NOR=Id;
                           SVE=Id];
                ApplicationArea=#All;
                SourceExpr=Id;
                Editable=FALSE }

    { 14  ;2   ;Field     ;
                Name=journalDisplayName;
                CaptionML=[@@@={Locked};
                           ENU=JournalDisplayName;
                           NOR=JournalDisplayName;
                           SVE=JournalDisplayName];
                ToolTipML=[ENU=Specifies the Journal Batch Name of the Journal Line;
                           NOR=Angir kladdenavnet for kladdelinjen;
                           SVE=Anger journalnamnet p† journalraden];
                ApplicationArea=#All;
                SourceExpr=GlobalJournalDisplayNameTxt;
                OnValidate=BEGIN
                             ERROR(CannotEditBatchNameErr);
                           END;
                            }

    { 11  ;2   ;Field     ;
                Name=lineNumber;
                CaptionML=[@@@={Locked};
                           ENU=LineNumber;
                           NOR=LineNumber;
                           SVE=LineNumber];
                ApplicationArea=#All;
                SourceExpr="Line No." }

    { 10  ;2   ;Field     ;
                Name=accountId;
                CaptionML=[@@@={Locked};
                           ENU=AccountId;
                           NOR=AccountId;
                           SVE=AccountId];
                ApplicationArea=#All;
                SourceExpr="Account Id";
                OnValidate=BEGIN
                             IF "Account Id" = BlankGUID THEN BEGIN
                               "Account No." := '';
                               EXIT;
                             END;

                             GLAccount.SETRANGE(Id,"Account Id");
                             IF NOT GLAccount.FINDFIRST THEN
                               ERROR(AccountIdDoesNotMatchAnAccountErr);

                             "Account No." := GLAccount."No.";
                           END;
                            }

    { 3   ;2   ;Field     ;
                Name=accountNumber;
                CaptionML=[@@@={Locked};
                           ENU=AccountNumber;
                           NOR=AccountNumber;
                           SVE=AccountNumber];
                ApplicationArea=#All;
                SourceExpr="Account No.";
                OnValidate=BEGIN
                             IF GLAccount."No." <> '' THEN BEGIN
                               IF GLAccount."No." <> "Account No." THEN
                                 ERROR(AccountValuesDontMatchErr);
                               EXIT;
                             END;

                             IF "Account No." = '' THEN BEGIN
                               "Account Id" := BlankGUID;
                               EXIT;
                             END;

                             IF NOT GLAccount.GET("Account No.") THEN
                               ERROR(AccountNumberDoesNotMatchAnAccountErr);

                             "Account Id" := GLAccount.Id;
                           END;
                            }

    { 12  ;2   ;Field     ;
                Name=postingDate;
                CaptionML=[@@@={Locked};
                           ENU=PostingDate;
                           NOR=PostingDate;
                           SVE=PostingDate];
                ApplicationArea=#All;
                SourceExpr="Posting Date" }

    { 6   ;2   ;Field     ;
                Name=documentNumber;
                CaptionML=[@@@={Locked};
                           ENU=DocumentNumber;
                           NOR=DocumentNumber;
                           SVE=DocumentNumber];
                ApplicationArea=#All;
                SourceExpr="Document No." }

    { 4   ;2   ;Field     ;
                Name=externalDocumentNumber;
                CaptionML=[@@@={Locked};
                           ENU=ExternalDocumentNumber;
                           NOR=ExternalDocumentNumber;
                           SVE=ExternalDocumentNumber];
                ApplicationArea=#All;
                SourceExpr="External Document No." }

    { 5   ;2   ;Field     ;
                Name=amount;
                CaptionML=[@@@={Locked};
                           ENU=Amount;
                           NOR=Amount;
                           SVE=Amount];
                ApplicationArea=#All;
                SourceExpr=Amount }

    { 7   ;2   ;Field     ;
                Name=description;
                CaptionML=[@@@={Locked};
                           ENU=Description;
                           NOR=Description;
                           SVE=Description];
                ApplicationArea=#All;
                SourceExpr=Description }

    { 13  ;2   ;Field     ;
                Name=comment;
                CaptionML=[@@@={Locked};
                           ENU=Comment;
                           NOR=Comment;
                           SVE=Comment];
                ApplicationArea=#All;
                SourceExpr=Comment }

    { 16  ;2   ;Field     ;
                Name=dimensions;
                CaptionML=[@@@={Locked};
                           ENU=Dimensions;
                           NOR=Dimensions;
                           SVE=Dimensions];
                ToolTipML=[ENU=Specifies Journal Line Dimensions.;
                           NOR=Angir kladdelinjedimensjoner.;
                           SVE=Anger dimensioner f”r journalrader.];
                ApplicationArea=#All;
                SourceExpr=DimensionsJSON;
                OnValidate=BEGIN
                             DimensionsSet := PreviousDimensionsJSON <> DimensionsJSON;
                           END;

                ODataEDMType=Collection(DIMENSION) }

    { 9   ;2   ;Field     ;
                Name=lastModifiedDateTime;
                CaptionML=[@@@={Locked};
                           ENU=LastModifiedDateTime;
                           NOR=LastModifiedDateTime;
                           SVE=LastModifiedDateTime];
                ApplicationArea=#All;
                SourceExpr="Last Modified DateTime";
                Editable=FALSE }

  }
  CODE
  {
    VAR
      GLAccount@1002 : Record 15;
      GraphMgtJournalLines@1000 : Codeunit 5478;
      LibraryAPIGeneralJournal@1001 : Codeunit 5469;
      FiltersNotSpecifiedErr@1007 : TextConst '@@@={Locked};ENU=You must specify a journal batch ID or a journal ID to get a journal line.;NOR=You must specify a journal batch ID or a journal ID to get a journal line.;SVE=You must specify a journal batch ID or a journal ID to get a journal line.';
      CannotEditBatchNameErr@1009 : TextConst '@@@={Locked};ENU=The Journal Batch Display Name isn''t editable.;NOR=The Journal Batch Display Name isn''t editable.;SVE=The Journal Batch Display Name isn''t editable.';
      AccountValuesDontMatchErr@1005 : TextConst '@@@={Locked};ENU=The account values do not match to a specific Account.;NOR=The account values do not match to a specific Account.;SVE=The account values do not match to a specific Account.';
      AccountIdDoesNotMatchAnAccountErr@1004 : TextConst '@@@={Locked};ENU=The "accountId" does not match to an Account.;NOR=The "accountId" does not match to an Account.;SVE=The "accountId" does not match to an Account.';
      AccountNumberDoesNotMatchAnAccountErr@1003 : TextConst '@@@={Locked};ENU=The "accountNumber" does not match to an Account.;NOR=The "accountNumber" does not match to an Account.;SVE=The "accountNumber" does not match to an Account.';
      DimensionsJSON@1011 : Text;
      PreviousDimensionsJSON@1013 : Text;
      GlobalJournalDisplayNameTxt@1006 : Code[10];
      FiltersChecked@1008 : Boolean;
      DimensionsSet@1012 : Boolean;
      BlankGUID@1010 : GUID;

    LOCAL PROCEDURE SetCalculatedFields@6();
    VAR
      GraphMgtComplexTypes@1000 : Codeunit 5468;
    BEGIN
      GlobalJournalDisplayNameTxt := "Journal Batch Name";
      DimensionsJSON := GraphMgtComplexTypes.GetDimensionsJSON("Dimension Set ID");
      PreviousDimensionsJSON := DimensionsJSON;
    END;

    LOCAL PROCEDURE ClearCalculatedFields@10();
    BEGIN
      CLEAR(GlobalJournalDisplayNameTxt);
      CLEAR(DimensionsJSON);
      CLEAR(PreviousDimensionsJSON);
      CLEAR(DimensionsSet);
    END;

    LOCAL PROCEDURE CheckFilters@3();
    BEGIN
      IF (GETFILTER("Journal Batch Id") = '') AND
         (GETFILTER(Id) = '')
      THEN
        ERROR(FiltersNotSpecifiedErr);
    END;

    LOCAL PROCEDURE UpdateDimensions@14(LineExists@1001 : Boolean);
    VAR
      GraphMgtComplexTypes@1000 : Codeunit 5468;
      DimensionManagement@1003 : Codeunit 408;
      NewDimensionSetId@1002 : Integer;
    BEGIN
      IF NOT DimensionsSet THEN
        EXIT;

      GraphMgtComplexTypes.GetDimensionSetFromJSON(DimensionsJSON,"Dimension Set ID",NewDimensionSetId);
      IF "Dimension Set ID" <> NewDimensionSetId THEN BEGIN
        "Dimension Set ID" := NewDimensionSetId;
        DimensionManagement.UpdateGlobalDimFromDimSetID(NewDimensionSetId,"Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
        IF LineExists THEN
          MODIFY;
      END;
    END;

    BEGIN
    END.
  }
}

