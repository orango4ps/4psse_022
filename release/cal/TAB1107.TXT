OBJECT Table 1107 Cost Allocation Target
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               // Default value according to previous line
               "Allocation Target Type" := xRec."Allocation Target Type";
               IF xRec."Target Cost Type" <> '' THEN
                 "Target Cost Type" := xRec."Target Cost Type";

               Base := xRec.Base;
               "Last Date Modified" := TODAY;
               "User ID" := USERID;
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;
               "User ID" := USERID;
             END;

    CaptionML=[ENU=Cost Allocation Target;
               NOR=Kostfordelingsm.;
               SVE=MÜl fîr kostnadsfîrdelning];
    LookupPageID=Page1114;
    DrillDownPageID=Page1114;
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;Code10        ;TableRelation="Cost Allocation Source";
                                                   CaptionML=[ENU=ID;
                                                              NOR=ID;
                                                              SVE=ID];
                                                   Editable=No }
    { 2   ;   ;Line No.            ;Integer       ;InitValue=0;
                                                   CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 5   ;   ;Target Cost Type    ;Code20        ;TableRelation="Cost Type";
                                                   CaptionML=[ENU=Target Cost Type;
                                                              NOR=MÜlkosttype;
                                                              SVE=MÜlkostnadstyp] }
    { 6   ;   ;Target Cost Center  ;Code20        ;TableRelation="Cost Center";
                                                   OnValidate=BEGIN
                                                                IF ("Target Cost Center" <> '') AND ("Target Cost Object" <> '') THEN
                                                                  ERROR(Text000);
                                                              END;

                                                   CaptionML=[ENU=Target Cost Center;
                                                              NOR=MÜlkostsenter;
                                                              SVE=MÜlkostnadsstÑlle] }
    { 7   ;   ;Target Cost Object  ;Code20        ;TableRelation="Cost Object";
                                                   OnValidate=BEGIN
                                                                VALIDATE("Target Cost Center")
                                                              END;

                                                   CaptionML=[ENU=Target Cost Object;
                                                              NOR=MÜlkostobjekt;
                                                              SVE=MÜlkostnadsobjekt] }
    { 8   ;   ;Static Base         ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Static Weighting");
                                                              END;

                                                   CaptionML=[ENU=Static Base;
                                                              NOR=Statisk grunnlag;
                                                              SVE=Statisk bas];
                                                   DecimalPlaces=0:2;
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 9   ;   ;Static Weighting    ;Decimal       ;OnValidate=BEGIN
                                                                IF Base = Base::Static THEN BEGIN
                                                                  Share := "Static Base" * "Static Weighting";
                                                                  VALIDATE(Share);
                                                                END ELSE BEGIN
                                                                  IF "Static Base" > 0 THEN
                                                                    ERROR(Text001,"Static Base");
                                                                  IF "Static Weighting" > 0 THEN
                                                                    ERROR(Text001,"Static Weighting");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Static Weighting;
                                                              NOR=Statisk vekting;
                                                              SVE=Fast viktning];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 10  ;   ;Share               ;Decimal       ;OnValidate=BEGIN
                                                                CalcPercent;
                                                              END;

                                                   CaptionML=[ENU=Share;
                                                              NOR=Andel;
                                                              SVE=Andel];
                                                   DecimalPlaces=2:5;
                                                   MinValue=0 }
    { 11  ;   ;Percent             ;Decimal       ;CaptionML=[ENU=Percent;
                                                              NOR=Prosent;
                                                              SVE=Procent];
                                                   DecimalPlaces=2:5;
                                                   MinValue=0;
                                                   MaxValue=100;
                                                   Editable=No }
    { 20  ;   ;Comment             ;Text50        ;CaptionML=[ENU=Comment;
                                                              NOR=Merknad;
                                                              SVE=Kommentar] }
    { 30  ;   ;Base                ;Option        ;OnValidate=BEGIN
                                                                IF Base <> xRec.Base THEN BEGIN
                                                                  Share := 0;
                                                                  Percent := 0;
                                                                  "No. Filter" := '';
                                                                  "Group Filter" := '';
                                                                END;

                                                                IF Base = Base::Static THEN BEGIN
                                                                  "No. Filter" := '';
                                                                  "Cost Center Filter" := '';
                                                                  "Cost Object Filter" := '';
                                                                  "Date Filter Code" := 0;
                                                                  "Group Filter" := '';
                                                                END ELSE BEGIN
                                                                  "Static Base" := 0;
                                                                  "Static Weighting" := 0;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Base;
                                                              NOR=Grunnlag;
                                                              SVE=Nettobelopp];
                                                   OptionCaptionML=[ENU=Static,G/L Entries,G/L Budget Entries,Cost Type Entries,Cost Budget Entries,,,,,No of Employees,,Items Sold (Qty.),Items Purchased (Qty.),Items Sold (Amount),Items Purchased (Amount);
                                                                    NOR=Statisk,Finansposter,Budsjettposter,Kosttypeposter,Kostbudsjettposter,,,,,Antall ansatte,,Solgte varer (antall),Kjõpte varer (antall),Solgte varer (belõp),Kjõpte varer (belõp);
                                                                    SVE=Statisk,Redovisningstransaktioner,Redovisningsbudgettransaktioner,Kostnadstyptransaktioner,Kostnadsbudgettransaktioner,,,,,Antal anstÑllda,,SÜlda artiklar(ant.),Kîpta artiklar (ant.),SÜlda artiklar (belopp),Kîpta artiklar (belopp)];
                                                   OptionString=Static,G/L Entries,G/L Budget Entries,Cost Type Entries,Cost Budget Entries,,,,,No of Employees,,Items Sold (Qty.),Items Purchased (Qty.),Items Sold (Amount),Items Purchased (Amount) }
    { 31  ;   ;No. Filter          ;Text250       ;OnLookup=VAR
                                                              SelectionFilter@1000 : Text[1024];
                                                            BEGIN
                                                              IF LookupNoFilter(SelectionFilter) THEN
                                                                VALIDATE("No. Filter",COPYSTR(SelectionFilter,1,MAXSTRLEN("No. Filter")));
                                                            END;

                                                   CaptionML=[ENU=No. Filter;
                                                              NOR=Nr.filter;
                                                              SVE=Nummerfilter] }
    { 32  ;   ;Cost Center Filter  ;Text250       ;TableRelation="Cost Center";
                                                   OnLookup=VAR
                                                              CostCenter@1000 : Record 1112;
                                                              SelectionFilter@1001 : Text[1024];
                                                            BEGIN
                                                              IF CostCenter.LookupCostCenterFilter(SelectionFilter) THEN
                                                                VALIDATE("Cost Center Filter",COPYSTR(SelectionFilter,1,MAXSTRLEN("Cost Center Filter")));
                                                            END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Cost Center Filter;
                                                              NOR=Kostsenterfilter;
                                                              SVE=Filter fîr kostnadsstÑlle] }
    { 33  ;   ;Cost Object Filter  ;Text250       ;TableRelation="Cost Object";
                                                   OnLookup=VAR
                                                              CostObject@1001 : Record 1113;
                                                              SelectionFilter@1000 : Text[1024];
                                                            BEGIN
                                                              IF CostObject.LookupCostObjectFilter(SelectionFilter) THEN
                                                                VALIDATE("Cost Object Filter",COPYSTR(SelectionFilter,1,MAXSTRLEN("Cost Object Filter")));
                                                            END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Cost Object Filter;
                                                              NOR=Kostobjektfilter;
                                                              SVE=Filter fîr kostnadsbÑrare] }
    { 34  ;   ;Date Filter Code    ;Option        ;CaptionML=[ENU=Date Filter Code;
                                                              NOR=Datofilterkode;
                                                              SVE=Kod fîr datumfilter];
                                                   OptionCaptionML=[ENU=" ,Week,Last Week,Month,Last Month,Month of Last Year,Year,Last Year,Period,Last Period,Period of Last Year,Fiscal Year,Last Fiscal Year";
                                                                    NOR=" ,Uke,Forrige uke,MÜned,Forrige mÜned,MÜned i fjor,èr,I fjor,Periode,Forrige periode,Periode i fjor,RegnskapsÜr,Forrige regnskapsÜr";
                                                                    SVE=" ,Vecka,Senaste veckan,MÜnad,Senaste mÜnaden,MÜnad under senaste Üret,èr,Senaste Üret,Period,Senaste perioden,Period under senaste Üret,RÑkenskapsÜr,Senaste rÑkenskapsÜret"];
                                                   OptionString=[ ,Week,Last Week,Month,Last Month,Month of Last Year,Year,Last Year,Period,Last Period,Period of Last Year,Fiscal Year,Last Fiscal Year] }
    { 35  ;   ;Group Filter        ;Text250       ;OnLookup=VAR
                                                              SelectionFilter@1000 : Text[1024];
                                                            BEGIN
                                                              IF LookupGroupFilter(SelectionFilter) THEN
                                                                VALIDATE("Group Filter",COPYSTR(SelectionFilter,1,MAXSTRLEN("Group Filter")));
                                                            END;

                                                   CaptionML=[ENU=Group Filter;
                                                              NOR=Gruppefilter;
                                                              SVE=Gruppfilter] }
    { 38  ;   ;Allocation Target Type;Option      ;OnValidate=BEGIN
                                                                IF "Allocation Target Type" <> xRec."Allocation Target Type" THEN BEGIN
                                                                  "Percent per Share" := 0;
                                                                  "Amount per Share" := 0;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Allocation Target Type;
                                                              NOR=FordelingsmÜltype;
                                                              SVE=Typ av fîrdelningsmÜl];
                                                   OptionCaptionML=[ENU=All Costs,Percent per Share,Amount per Share;
                                                                    NOR=All kost,Prosent per andel,Belõp per andel;
                                                                    SVE=Alla kostnader,Procent per andel,Belopp per andel];
                                                   OptionString=All Costs,Percent per Share,Amount per Share }
    { 40  ;   ;Percent per Share   ;Decimal       ;OnValidate=BEGIN
                                                                IF "Percent per Share" <> 0 THEN BEGIN
                                                                  "Allocation Target Type" := "Allocation Target Type"::"Percent per Share";
                                                                  "Amount per Share" := 0;
                                                                END ELSE
                                                                  "Allocation Target Type" := "Allocation Target Type"::"All Costs";
                                                              END;

                                                   CaptionML=[ENU=Percent per Share;
                                                              NOR=Prosent per andel;
                                                              SVE=Procent per andel];
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 41  ;   ;Amount per Share    ;Decimal       ;OnValidate=BEGIN
                                                                IF "Amount per Share" <> 0 THEN BEGIN
                                                                  "Allocation Target Type" := "Allocation Target Type"::"Amount per Share";
                                                                  "Percent per Share" := 0;
                                                                END ELSE
                                                                  "Allocation Target Type" := "Allocation Target Type"::"All Costs";
                                                              END;

                                                   CaptionML=[ENU=Amount per Share;
                                                              NOR=Belõp per andel;
                                                              SVE=Belopp per andel];
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 50  ;   ;Share Updated on    ;Date          ;CaptionML=[ENU=Share Updated on;
                                                              NOR=Andel oppdatert;
                                                              SVE=Andel uppdaterad den];
                                                   Editable=No }
    { 60  ;   ;Last Date Modified  ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              NOR=Endret den;
                                                              SVE=Uppdateringsdatum];
                                                   Editable=No }
    { 61  ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit 418;
                                                              TempUserID@1001 : Code[50];
                                                            BEGIN
                                                              TempUserID := "User ID";
                                                              UserMgt.LookupUserID(TempUserID);
                                                            END;

                                                   TestTableRelation=No;
                                                   DataClassification=EndUserIdentifiableInformation;
                                                   CaptionML=[ENU=User ID;
                                                              NOR=Bruker-ID;
                                                              SVE=AnvÑndar-ID];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;ID,Line No.                             ;Clustered=Yes }
    {    ;Target Cost Type                         }
    {    ;Target Cost Center                       }
    {    ;Target Cost Object                       }
    {    ;ID,Allocation Target Type                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You cannot define both cost center and cost object.;NOR=Du kan ikke definere bÜde kostsenter og kostobjekt.;SVE=Det gÜr inte att definiera bÜde kostnadsstÑlle och kostnadsbÑrare.';
      Text001@1001 : TextConst 'ENU=%1 can only be used with static allocations.;NOR=%1 kan bare brukes med statiske fordelinger.;SVE=%1 gÜr bara att anvÑnda med fasta fîrdelningar.';

    LOCAL PROCEDURE CalcPercent@1();
    VAR
      CostAllocationTarget@1002 : Record 1107;
      PctTotal@1000 : Decimal;
      TotalShare@1001 : Decimal;
    BEGIN
      WITH CostAllocationTarget DO BEGIN
        SETRANGE(ID,Rec.ID);
        SETFILTER("Line No.",'<>%1',Rec."Line No.");
        CALCSUMS(Share);
        TotalShare := Share + Rec.Share;

        IF TotalShare = 0 THEN BEGIN
          MODIFYALL(Percent,0);
          MODIFYALL("Share Updated on",TODAY);
          Rec.Percent := 0;
          Rec."Share Updated on" := TODAY;
          EXIT;
        END;

        IF FINDSET THEN
          REPEAT
            Percent := ROUND(100 * Share / TotalShare,0.00001);
            "Share Updated on" := TODAY;
            MODIFY;
          UNTIL NEXT = 0;
        Rec.Percent := ROUND(100 * Rec.Share / TotalShare,0.00001);
        Rec."Share Updated on" := TODAY;
        Rec.MODIFY;
        // distribute rounding error:
        CALCSUMS(Percent);
        PctTotal := Percent + Rec.Percent;
        IF PctTotal <> 100 THEN BEGIN
          IF (Rec."Line No." <> 0) AND FINDLAST AND ("Line No." > Rec."Line No.") THEN BEGIN
            Percent := Percent + (100 - PctTotal);
            MODIFY;
          END ELSE
            Rec.Percent := Rec.Percent + (100 - PctTotal);
        END;
      END;
    END;

    LOCAL PROCEDURE LookupNoFilter@2(VAR SelectionFilter@1000 : Text) : Boolean;
    VAR
      CostType@1001 : Record 1103;
    BEGIN
      CASE Base OF
        Base::"G/L Entries",
        Base::"G/L Budget Entries":
          EXIT(CostType.LookupGLAccFilter(SelectionFilter));
        Base::"Cost Type Entries",
        Base::"Cost Budget Entries":
          EXIT(CostType.LookupCostTypeFilter(SelectionFilter));
        Base::"Items Sold (Qty.)",
        Base::"Items Purchased (Qty.)",
        Base::"Items Sold (Amount)",
        Base::"Items Purchased (Amount)":
          EXIT(LookupItemFilter(SelectionFilter));
      END;
    END;

    LOCAL PROCEDURE LookupItemFilter@14(VAR SelectionFilter@1000 : Text) : Boolean;
    VAR
      ItemList@1001 : Page 31;
    BEGIN
      ItemList.LOOKUPMODE(TRUE);
      IF ItemList.RUNMODAL = ACTION::LookupOK THEN BEGIN
        SelectionFilter := ItemList.GetSelectionFilter;
        EXIT(TRUE);
      END;
      EXIT(FALSE)
    END;

    LOCAL PROCEDURE LookupGroupFilter@4(VAR SelectionFilter@1000 : Text) : Boolean;
    VAR
      IsHandled@1001 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeLookupGroupFilter(Base,SelectionFilter,IsHandled);
      IF IsHandled THEN
        EXIT;

      CASE Base OF
        Base::"G/L Budget Entries":
          EXIT(LookupGLBudgetFilter(SelectionFilter));
        Base::"Cost Budget Entries":
          EXIT(LookupCostBudgetFilter(SelectionFilter));
        Base::"Items Sold (Qty.)",
        Base::"Items Purchased (Qty.)",
        Base::"Items Sold (Amount)",
        Base::"Items Purchased (Amount)":
          EXIT(LookupInvtPostingGrFilter(SelectionFilter));
      END;
    END;

    LOCAL PROCEDURE LookupGLBudgetFilter@6(VAR SelectionFilter@1000 : Text) : Boolean;
    VAR
      GLBudgetNames@1001 : Page 121;
    BEGIN
      GLBudgetNames.LOOKUPMODE(TRUE);
      IF GLBudgetNames.RUNMODAL = ACTION::LookupOK THEN BEGIN
        SelectionFilter := GLBudgetNames.GetSelectionFilter;
        EXIT(TRUE);
      END;
      EXIT(FALSE)
    END;

    LOCAL PROCEDURE LookupCostBudgetFilter@7(VAR SelectionFilter@1000 : Text) : Boolean;
    VAR
      CostBudgetNames@1001 : Page 1116;
    BEGIN
      CostBudgetNames.LOOKUPMODE(TRUE);
      IF CostBudgetNames.RUNMODAL = ACTION::LookupOK THEN BEGIN
        SelectionFilter := CostBudgetNames.GetSelectionFilter;
        EXIT(TRUE);
      END;
      EXIT(FALSE)
    END;

    LOCAL PROCEDURE LookupInvtPostingGrFilter@8(VAR SelectionFilter@1000 : Text) : Boolean;
    VAR
      InvtPostingGroups@1001 : Page 112;
    BEGIN
      InvtPostingGroups.LOOKUPMODE(TRUE);
      IF InvtPostingGroups.RUNMODAL = ACTION::LookupOK THEN BEGIN
        SelectionFilter := InvtPostingGroups.GetSelectionFilter;
        EXIT(TRUE);
      END;
      EXIT(FALSE)
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeLookupGroupFilter@3(SourceBase@1000 : Integer;VAR SelectionFilter@1001 : Text;VAR IsHandled@1002 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

