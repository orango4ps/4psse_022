OBJECT Page 11125052 Create Contact Person
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Kontaktperson erstellen;
               ENU=Create Contact Person;
               NLD=Contactpersoon aanmaken;
               NOR=Opprette kontaktperson;
               SVE=Skapa kontaktperson];
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table5050;
    DataCaptionExpr='';
    PageType=NavigatePage;
    OnInit=BEGIN
             SelectionPersonEditable := TRUE;
             CompanyNoEditable := TRUE;
             PersonNoEditable := TRUE;
           END;

    OnOpenPage=BEGIN
                 MarketingSetup.GET;
                 MarketingSetup.TESTFIELD("Person Nos.");
                 MarketingSetup.TESTFIELD("Contact Nos.");

                 FILTERGROUP(10);
                 IF GETFILTER("Person No.") <> '' THEN BEGIN
                   SelectionPerson := SelectionPerson::"Select an existing person";
                   SelectionPersonEditable := FALSE;
                   PersonNoEditable := FALSE;
                 END ELSE
                   SelectionPerson := SelectionPerson::"Create a new person";
                 IF GETFILTER("Company No.") <> '' THEN
                   CompanyNoEditable := FALSE;
                 FILTERGROUP(0);

                 CompanyNo := "Company No.";
                 PersonNo := "Person No.";
               END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 10      ;1   ;Action    ;
                      Name=Finish;
                      CaptionML=[DEU=B&eenden;
                                 ENU=&Finish;
                                 NLD=Voltooie&n;
                                 NOR=&Avslutt;
                                 SVE=&Slutf”r];
                      InFooterBar=Yes;
                      Image=Completed;
                      OnAction=BEGIN
                                 InsertingPersonIsAllowed(TRUE);

                                 IF SelectionPerson = SelectionPerson::"Create a new person" THEN BEGIN
                                   IF PersonNos <> '' THEN
                                     NoSeriesMgt1.SaveNoSeries;
                                   PersonNo := CreatePerson(PersonNo);
                                 END;

                                 IF ContactNos <> '' THEN
                                   NoSeriesMgt2.SaveNoSeries;
                                 ContactNo := CreateContactPerson(CompanyNo, PersonNo, ContactNo);

                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100528700;1;Field  ;
                CaptionML=[DEU=Wie m”chten Sie eine neue Kontaktperson erstellen?;
                           ENU=How do you want to create a new contact person?;
                           NLD=Hoe wilt u een nieuwe contactpersoon aanmaken?;
                           NOR=Hvordan vil du opprette en ny kontaktperson?;
                           SVE=Vill du skapa en ny kontaktperson?];
                OptionCaptionML=[DEU=Zun„chst eine neue Person erstellen, Eine vorhandene Person w„hlen;
                                 ENU=First create a new person,Select an existing person;
                                 NLD=Eerst een nieuwe persoon aanmaken,Bestaande persoon selecteren];
                SourceExpr=SelectionPerson;
                Editable=SelectionPersonEditable;
                OnValidate=BEGIN
                             PersonNo := '';
                             PersonNos := '';
                           END;
                            }

    { 1100528702;1;Field  ;
                CaptionML=[DEU=Unternehmensnr.;
                           ENU=Company No.;
                           NLD=Bedrijfsnr.;
                           NOR=Selskapsnr.;
                           SVE=F”retagsnummer];
                SourceExpr=CompanyNo;
                TableRelation=Contact WHERE (Type=CONST(Company));
                Editable=CompanyNoEditable;
                OnLookup=VAR
                           Contact@1100528700 : Record 5050;
                           ContactList@1100528701 : Page 5052;
                         BEGIN
                           IF NOT CompanyNoEditable THEN
                             EXIT;
                           Contact.SETRANGE(Type, Type::Company);
                           IF Contact.GET(CompanyNo) THEN
                             ContactList.SETRECORD(Contact);
                           ContactList.SETTABLEVIEW(Contact);
                           ContactList.LOOKUPMODE(TRUE);
                           IF ContactList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             ContactList.GETRECORD(Contact);
                             CompanyNo := Contact."No.";
                           END;
                         END;
                          }

    { 1100528704;1;Field  ;
                CaptionML=[DEU=Personennr.;
                           ENU=Person No.;
                           NLD=Persoonsnr.;
                           NOR=Personnummer;
                           SVE=Personens nr];
                SourceExpr=PersonNo;
                Editable=PersonNoEditable;
                OnValidate=VAR
                             Contact@1100528700 : Record 5050;
                             PersonNo2@1100528702 : Code[20];
                           BEGIN
                             IF PersonNo = '' THEN
                               EXIT;

                             CASE SelectionPerson OF
                               SelectionPerson::"Select an existing person":
                                 BEGIN
                                   Contact.SETRANGE("No.", PersonNo);
                                   Contact.SETRANGE(Type, Type::Person);
                                   Contact.SETFILTER("Company No.", '%1', '');
                                   Contact.FINDFIRST;
                                 END;
                               SelectionPerson::"Create a new person":
                                 BEGIN
                                   NoSeriesMgt1.TestManual(MarketingSetup."Person Nos.");
                                   IF Contact.GET(PersonNo) THEN
                                     MESSAGE(Text001, TABLECAPTION, PersonNo);

                                   IF PersonNos <> '' THEN BEGIN
                                     PersonNo2 := NoSeriesMgt1.GetNextNo(PersonNos, 0D, FALSE);
                                     IF PersonNo2 <> PersonNo THEN
                                       PersonNos := ''; // Manual
                                   END;
                                 END;
                             END;
                           END;

                OnLookup=VAR
                           Contact@1100528700 : Record 5050;
                           ContactListPerson@1100528701 : Page 11130210;
                         BEGIN
                           IF SelectionPerson <> SelectionPerson::"Select an existing person" THEN
                             EXIT;
                           IF NOT PersonNoEditable THEN
                             EXIT;

                           Contact.SETRANGE(Type, Type::Person);
                           Contact.SETFILTER("Company No.", '%1', '');
                           IF Contact.GET(PersonNo) THEN
                             ContactListPerson.SETRECORD(Contact);
                           ContactListPerson.SETTABLEVIEW(Contact);
                           ContactListPerson.LOOKUPMODE(TRUE);
                           IF ContactListPerson.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             ContactListPerson.GETRECORD(Contact);
                             PersonNo := Contact."No.";
                           END;
                         END;

                OnAssistEdit=VAR
                               Contact@1100528701 : Record 5050;
                             BEGIN
                               IF SelectionPerson <> SelectionPerson::"Create a new person" THEN
                                 EXIT;
                               IF NOT PersonNoEditable THEN
                                 EXIT;

                               IF NoSeriesMgt1.SelectSeries(MarketingSetup."Person Nos.", '', PersonNos) THEN BEGIN
                                 PersonNo := NoSeriesMgt1.GetNextNo(PersonNos, 0D, FALSE);
                                 IF Contact.GET(PersonNo) THEN
                                   MESSAGE(Text001, TABLECAPTION, PersonNo);
                               END;
                             END;
                              }

    { 1100528701;1;Field  ;
                CaptionML=[DEU=Kontaktnr.;
                           ENU=Contact Person No.;
                           NLD=Contactpersoonnr.;
                           NOR=Kontaktperson;
                           SVE=Kontaktperson];
                SourceExpr=ContactNo;
                OnValidate=VAR
                             Contact@1100528700 : Record 5050;
                             ContactNo2@1100528702 : Code[20];
                           BEGIN
                             IF ContactNo = '' THEN
                               EXIT;

                             NoSeriesMgt2.TestManual(MarketingSetup."Contact Nos.");
                             IF Contact.GET(ContactNo) THEN
                               MESSAGE(Text001, TABLECAPTION, ContactNo);

                             IF ContactNos <> '' THEN BEGIN
                               ContactNo2 := NoSeriesMgt2.GetNextNo(ContactNos, 0D, FALSE);
                               IF ContactNo2 <> ContactNo THEN
                                 ContactNos := ''; // Manual
                             END;
                           END;

                OnAssistEdit=VAR
                               Contact@1100528700 : Record 5050;
                             BEGIN
                               IF NoSeriesMgt2.SelectSeries(MarketingSetup."Contact Nos.", '', ContactNos) THEN BEGIN
                                 ContactNo := NoSeriesMgt2.GetNextNo(ContactNos, 0D, FALSE);
                                 IF Contact.GET(ContactNo) THEN
                                   MESSAGE(Text001, TABLECAPTION, ContactNo);
                               END;
                             END;
                              }

  }
  CODE
  {
    VAR
      MarketingSetup@1100528709 : Record 5079;
      NoSeriesMgt1@1100528708 : Codeunit 396;
      NoSeriesMgt2@1100528701 : Codeunit 396;
      CompanyNo@1100528707 : Code[20];
      PersonNo@1100528706 : Code[20];
      ContactNo@1100528705 : Code[20];
      SelectionPerson@1100528702 : 'Create a new person,Select an existing person';
      SelectionPersonEditable@1100528710 : Boolean INDATASET;
      Text001@1100528700 : TextConst 'DEU=%1 %2 ist bereits vorhanden.;ENU=%1 %2 already exists.;NLD=%1 %2  is reeds aanwezig.;NOR=%1 %2 finnes allerede.;SVE=%1 %2 finns redan.';
      CompanyNoEditable@1100528711 : Boolean INDATASET;
      PersonNoEditable@1100528712 : Boolean INDATASET;
      PersonNos@1100528704 : Code[10];
      ContactNos@1100528703 : Code[10];

    PROCEDURE GetContactNo@1100528700() : Code[20];
    BEGIN
      EXIT(ContactNo);
    END;

    PROCEDURE GetSelectionPerson@1100528704() : Integer;
    BEGIN
      EXIT(SelectionPerson);
    END;

    BEGIN
    END.
  }
}

