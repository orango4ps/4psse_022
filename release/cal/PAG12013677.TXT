OBJECT Page 12013677 Export File
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF400000;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Export ExFlow Setup File;
               SVE=Exportera ExFlow inst„llningsfil];
    OnOpenPage=BEGIN
                 CompanyList.RESET;
                 IF CompanyList.FINDSET THEN
                   REPEAT
                     TempCompanySelectList."Entry No." := TempCompanySelectList."Entry No." + 1;
                     TempCompanySelectList."Company Name" := CompanyList.Name;
                     TempCompanySelectList."New Company Name" := CompanyList.Name;
                     TempCompanySelectList.Selected := TRUE;
                     TempCompanySelectList.INSERT;
                   UNTIL CompanyList.NEXT = 0;

                 FromCompanyName := COMPANYNAME;
                 ToCompanyName := FromCompanyName;

                 ExportAllExFlowTables := TRUE;
                 ExFlowDataMgt.CreateListofTables(TempTableSelection);
               END;

  }
  CONTROLS
  {
    { 1100285001;0;Container;
                ContainerType=ContentArea }

    { 1100285000;1;Group  ;
                CaptionML=[ENU=General;
                           SVE=Allm„nt] }

    { 1100285002;2;Field  ;
                CaptionML=[ENU=XML File Name;
                           SVE=XML Filnamn];
                SourceExpr=FileName;
                OnAssistEdit=BEGIN
                               ExFlowFileMgt.FileNameFromDialog(FileName,Text001,4,Text002,1);
                               IF (FileName <> '') AND (STRPOS(FileName,'.xml') = 0) THEN
                                 FileName := FileName + '.xml';
                             END;
                              }

    { 1100285003;2;Field  ;
                CaptionML=[ENU=From Company Name;
                           SVE=Fr†n F”retagsnamn];
                SourceExpr=FromCompanyName;
                TableRelation=Company;
                Editable=False;
                LookupPageID=Companies;
                OnValidate=BEGIN
                             ToCompanyName := FromCompanyName;
                           END;
                            }

    { 1100285004;2;Field  ;
                CaptionML=[ENU=To Company Name;
                           SVE=Till F”retagsnamn];
                SourceExpr=ToCompanyName;
                OnValidate=BEGIN
                             TempCompanySelectList.SETRANGE("Company Name", FromCompanyName);
                             TempCompanySelectList.FIND('-');
                             TempCompanySelectList."New Company Name" := ToCompanyName;
                             TempCompanySelectList.MODIFY;
                           END;
                            }

    { 1100285005;2;Field  ;
                CaptionML=[ENU=Export all ExFlow Setup tables;
                           SVE=Exportera alla ExFlow tabeller];
                SourceExpr=ExportAllExFlowTables;
                OnValidate=BEGIN
                             IF NOT ExportAllExFlowTables THEN BEGIN
                               CLEAR(ExFlowTableSelection);
                               ExFlowTableSelection.LOOKUPMODE := TRUE;
                               ExFlowTableSelection.EDITABLE := TRUE;
                               ExFlowTableSelection.SetTempTable(TempTableSelection);
                               IF ExFlowTableSelection.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                 TempTableSelection.RESET;
                                 TempTableSelection.DELETEALL;
                                 ExFlowTableSelection.GetTempTable(TempTableSelection);
                               END
                               ELSE
                                 ExportAllExFlowTables := TRUE;
                             END
                             ELSE BEGIN
                               TempTableSelection.RESET;
                               TempTableSelection.DELETEALL;

                               ExFlowDataMgt.CreateListofTables(TempTableSelection);
                             END;
                           END;
                            }

  }
  CODE
  {
    VAR
      CompanyList@1100285010 : Record 2000000006;
      TempCompanySelectList@1100285009 : TEMPORARY Record 12013666;
      TempTableSelection@1100285008 : TEMPORARY Record 12013666;
      ExFlowFileMgt@1100285007 : Codeunit 12013602;
      ExFlowDataMgt@1100285006 : Codeunit 12013621;
      ExFlowTableSelection@1100285005 : Page 12057077;
      ExportAllExFlowTables@1100285004 : Boolean;
      FileName@1100285003 : Text[250];
      FromCompanyName@1100285001 : Text[50];
      ToCompanyName@1100285000 : Text[50];
      Text001@1100285012 : TextConst 'ENU=XML File;SVE=XML Fil';
      Text002@1100285011 : TextConst 'ENU=XML Files (*.xml*)|*.xml*|All Files (*.*)|*.*;SVE=XML Filer (*.xml*)|*.xml*|Alla Filer (*.*)|*.*';

    PROCEDURE GetFileInfo@12013587(VAR _FileName@1240150001 : Text[250];VAR _TempCompanyList@1100285000 : Record 12013666;VAR _TempTableSelection@1100285001 : TEMPORARY Record 12013666);
    BEGIN
      _FileName := FileName;

      TempCompanySelectList.RESET;
      TempCompanySelectList.SETRANGE("Company Name", FromCompanyName);
      TempCompanySelectList.SETRANGE(Selected, TRUE);
      TempCompanySelectList.FIND('-');
      REPEAT
        _TempCompanyList.INIT;
        _TempCompanyList.TRANSFERFIELDS(TempCompanySelectList);
        _TempCompanyList.INSERT;
      UNTIL TempCompanySelectList.NEXT = 0;

      TempTableSelection.RESET;
      TempTableSelection.SETRANGE(Selected, TRUE);
      TempTableSelection.FIND('-');
      REPEAT
        _TempTableSelection.INIT;
        _TempTableSelection.TRANSFERFIELDS(TempTableSelection);
        _TempTableSelection.INSERT;
      UNTIL TempTableSelection.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

