OBJECT Page 11229225 Change Request Hours Chart
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Stundendiagramm énderungsanfragen;
               ENU=Change Request Hours Chart;
               NLD=Wijzigingsverzoek uren grafiek;
               SVE=éndringsbegÑran tider diagram];
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table485;
    PageType=ListPlus;
    UsageCategory=Tasks;
    OnInit=BEGIN
             PeriodType := PeriodType::Month;
             NoOfPeriods := 12;
             ShowTotal := TRUE;
             DetermineMeasurementIndexes;
           END;

    OnOpenPage=VAR
                 Date@1100409000 : Record 2000000007;
               BEGIN
                 Date."Period Start" := WORKDATE;
                 PeriodFormManagement.FindDate('=<', Date, PeriodType);
                 BaseDate := Date."Period Start";
               END;

    ActionList=ACTIONS
    {
      { 1100528615;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100528614;1 ;Action    ;
                      CaptionML=[DEU=ZurÅck;
                                 ENU=Previous;
                                 NLD=Vorige;
                                 NOR=Forrige;
                                 SVE=FîregÜende];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=PreviousRecord;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 Date@1100409000 : Record 2000000007;
                               BEGIN
                                 Date."Period Start" := BaseDate;
                                 PeriodFormManagement.FindDate('<', Date, PeriodType);
                                 BaseDate := Date."Period Start";
                                 UpdateChart;
                               END;
                                }
      { 1100528613;1 ;Action    ;
                      CaptionML=[DEU=Weiter;
                                 ENU=Next;
                                 NLD=Volgende;
                                 NOR=Neste;
                                 SVE=NÑsta];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=NextRecord;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 Date@1100409000 : Record 2000000007;
                               BEGIN
                                 Date."Period Start" := BaseDate;
                                 PeriodFormManagement.FindDate('>', Date, PeriodType);
                                 BaseDate := Date."Period Start";
                                 UpdateChart;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100528612;;Container;
                ContainerType=ContentArea }

    { 1100528611;1;Group  ;
                CaptionML=[DEU=Allgemein;
                           ENU=General;
                           NLD=Algemeen;
                           NOR=Generell;
                           SVE=AllmÑnt];
                GroupType=Group }

    { 1100409002;2;Field  ;
                CaptionML=[DEU=Debitorenfilter;
                           ENU=Customer Filter;
                           NLD=Klant filter;
                           NOR=Kundefilter;
                           SVE=Kundfilter];
                SourceExpr=CustomerFilter;
                TableRelation=Customer;
                OnValidate=BEGIN
                             UpdateChart;
                           END;
                            }

    { 1100528610;2;Field  ;
                CaptionML=[DEU=énderungsanfragenfilter;
                           ENU=Change Request Filter;
                           NLD=Wijzigingsverzoek filter;
                           SVE=éndringsbegÑransfilter];
                SourceExpr=ChangeRequestFilter;
                TableRelation="Change Request";
                OnValidate=BEGIN
                             UpdateChart;
                           END;
                            }

    { 1100409001;2;Field  ;
                CaptionML=[DEU=Filter fÅr verantwortliche Mitarbeiter;
                           ENU=Responsible Employee Filter;
                           NLD=Verantwoordelijk medewerker filter];
                SourceExpr=ResponsibleEmployeeFilter;
                TableRelation=Employee;
                OnValidate=BEGIN
                             UpdateChart;
                           END;
                            }

    { 1100528603;1;Group  ;
                CaptionML=[DEU=Diagramm;
                           ENU=Diagram;
                           NLD=Grafiek];
                GroupType=Group }

    { 1100528604;2;Field  ;
                Name=Chart;
                ControlAddIn=[Microsoft.Dynamics.Nav.Client.BusinessChart;PublicKeyToken=31bf3856ad364e35] }

    { 1100528602;1;Group  ;
                CaptionML=[DEU=Optionen;
                           ENU=Options;
                           NLD=Opties;
                           NOR=Alternativer;
                           SVE=Alternativ];
                GroupType=Group }

    { 1100528601;2;Field  ;
                Name=<Control1100528602>;
                CaptionML=[DEU=Anzeigen nach;
                           ENU=View by;
                           NLD=Weergeven per;
                           NOR=Vis per;
                           SVE=Visa per];
                OptionCaptionML=[DEU=Tag,Woche,Monat,Quartal,Jahr,Periode;
                                 ENU=Day,Week,Month,Quarter,Year,Period;
                                 NLD=Dag,Week,Maand,Kwartaal,Jaar,Periode;
                                 NOR=Dag,Uke,MÜned,Kvartal,èr,Periode;
                                 SVE=Dag,Vecka,MÜnad,Kvartal,èr,Period];
                SourceExpr=PeriodType;
                OnValidate=VAR
                             Date@1100409000 : Record 2000000007;
                           BEGIN
                             PeriodFormManagement.FindDate('=<>', Date, PeriodType);
                             UpdateChart;
                           END;
                            }

    { 1100528600;2;Field  ;
                CaptionML=[DEU=Anz. der Perioden;
                           ENU=No. Of Periods;
                           NLD=Aantal perioden];
                SourceExpr=NoOfPeriods;
                MinValue=1;
                MaxValue=12;
                OnValidate=BEGIN
                             UpdateChart;
                           END;
                            }

    { 1100409000;2;Field  ;
                CaptionML=[DEU=Summen Stunden;
                           ENU=Total Hours;
                           NLD=Totaal uren;
                           NOR=Timer totalt;
                           SVE=Timmar totalt];
                SourceExpr=ShowTotal;
                OnValidate=BEGIN
                             UpdateChart;
                           END;
                            }

  }
  CODE
  {
    VAR
      PeriodFormManagement@1100528600 : Codeunit 359;
      PeriodType@1100528603 : ',Week,Month,Quarter,Year';
      Text000@1100528614 : TextConst 'DEU=Tag,Woche,Monat,Quartal,Jahr,Periode;ENU=Day,Week,Month,Quarter,Year,Period;NLD=Dag,Week,Maand,Kwartaal,Jaar,Periode;NOR=Dag,Uke,MÜned,Kvartal,èr,Periode;SVE=Dag,Vecka,MÜnad,Kvartal,èr,Period';
      BaseDate@1100528610 : Date;
      IndexNoReceived@1100409001 : Integer;
      IndexNoInProcess@1100409000 : Integer;
      IndexNoFinished@1100528609 : Integer;
      IndexNoTotal@1100409007 : Integer;
      IndexNoLeadTime@1100409004 : Integer;
      NoOfPeriods@1100528611 : Integer;
      ChangeRequestFilter@1100528617 : Text;
      ResponsibleEmployeeFilter@1100409002 : Text;
      CustomerFilter@1100409008 : Text;
      Text001@1100409003 : TextConst 'DEU=Gesamt;ENU=Total;NLD=Totaal;NOR=I alt;SVE=Totalt';
      ShowTotal@1100409006 : Boolean;

    LOCAL PROCEDURE UpdateChart@1100528601();
    VAR
      ChangeRequestTaskHourAcc@1100409001 : Record 11072222;
      ChangeRequestTask@1100409000 : Record 11072214;
      Date@1100528604 : Record 2000000007;
      Offset@1100528602 : Integer;
      ColumnName@1100528601 : Text[30];
    BEGIN
      DetermineMeasurementIndexes;
      Initialize;
      SetXAxis(GetPeriodTypeText(PeriodType), "Data Type"::String);
      IF (NOT ShowTotal) THEN BEGIN
        AddMeasure(ChangeRequestTaskHourAcc.FIELDCAPTION(Hours) + ' ('+ FORMAT(ChangeRequestTaskHourAcc."Task Status"::Received) + ')',
          IndexNoReceived, "Data Type"::Decimal, "Chart Type"::StackedColumn);
        AddMeasure(ChangeRequestTaskHourAcc.FIELDCAPTION(Hours) + ' ('+ FORMAT(ChangeRequestTaskHourAcc."Task Status"::"In Process") + ')',
          IndexNoInProcess, "Data Type"::Decimal, "Chart Type"::StackedColumn);
        AddMeasure(ChangeRequestTaskHourAcc.FIELDCAPTION(Hours) + ' ('+ FORMAT(ChangeRequestTaskHourAcc."Task Status"::Finished) + ')',
          IndexNoFinished, "Data Type"::Decimal, "Chart Type"::StackedColumn);
      END ELSE
        AddMeasure(Text001 + ' ' + ChangeRequestTaskHourAcc.FIELDCAPTION(Hours), IndexNoTotal, "Data Type"::Decimal, "Chart Type"::Column);
      AddMeasure(ChangeRequestTask.FIELDCAPTION("Lead Time (Hrs)"), IndexNoLeadTime, "Data Type"::Decimal, "Chart Type"::Line);

      Date."Period Start" := BaseDate;
      PeriodFormManagement.FindDate('=<>', Date, PeriodType);

      FOR Offset := 0 TO NoOfPeriods -1 DO BEGIN
        IF (Offset > 0) THEN
          PeriodFormManagement.NextDate(1, Date, PeriodType);
        ColumnName := PeriodFormManagement.CreatePeriodFormat(PeriodType, Date."Period Start");
        AddColumn(ColumnName);

        ChangeRequestTaskHourAcc.RESET;
        ChangeRequestTask.RESET;
        IF (CustomerFilter <> '') THEN BEGIN
          ChangeRequestTaskHourAcc.SETFILTER("Customer No.", CustomerFilter);
          ChangeRequestTask.SETFILTER("Customer No.", CustomerFilter);
        END;
        IF (ChangeRequestFilter <> '') THEN BEGIN
          ChangeRequestTaskHourAcc.SETFILTER("Change Request No.", ChangeRequestFilter);
          ChangeRequestTask.SETFILTER("Change Request No.", ChangeRequestFilter);
        END;
        IF (ResponsibleEmployeeFilter <> '') THEN BEGIN
          ChangeRequestTaskHourAcc.SETFILTER("Employee No.", ResponsibleEmployeeFilter);
          ChangeRequestTask.SETFILTER("Responsible Employee", ResponsibleEmployeeFilter);
        END;
        ChangeRequestTaskHourAcc.SETRANGE("Hour Line Date", Date."Period Start", Date."Period End");
        ChangeRequestTask.SETRANGE("Due Date", Date."Period Start", Date."Period End");

        IF (NOT ShowTotal) THEN BEGIN
          ChangeRequestTaskHourAcc.SETRANGE("Task Status", ChangeRequestTaskHourAcc."Task Status"::Received);
          ChangeRequestTaskHourAcc.CALCSUMS(Hours);
          SetValueByIndex(IndexNoReceived -1, Offset, ChangeRequestTaskHourAcc.Hours);

          ChangeRequestTaskHourAcc.SETRANGE("Task Status", ChangeRequestTaskHourAcc."Task Status"::"In Process");
          ChangeRequestTaskHourAcc.CALCSUMS(Hours);
          SetValueByIndex(IndexNoInProcess -1, Offset, ChangeRequestTaskHourAcc.Hours);

          ChangeRequestTaskHourAcc.SETRANGE("Task Status", ChangeRequestTaskHourAcc."Task Status"::Finished);
          ChangeRequestTaskHourAcc.CALCSUMS(Hours);
          SetValueByIndex(IndexNoFinished -1, Offset, ChangeRequestTaskHourAcc.Hours);
        END ELSE BEGIN
          ChangeRequestTaskHourAcc.SETRANGE("Task Status");
          ChangeRequestTaskHourAcc.CALCSUMS(Hours);
          SetValueByIndex(IndexNoTotal - 1, Offset, ChangeRequestTaskHourAcc.Hours);
        END;
        ChangeRequestTask.CALCSUMS("Lead Time (Hrs)");
        SetValueByIndex(IndexNoLeadTime -1, Offset, ChangeRequestTask."Lead Time (Hrs)");
      END;
      Update(CurrPage.Chart);
    END;

    LOCAL PROCEDURE GetPeriod@1100528604(IOffset@1100528600 : Integer;VAR ODate@1100528601 : Record 2000000007);
    BEGIN
      ODate."Period Start" := BaseDate;
      PeriodFormManagement.FindDate('=<>', ODate, PeriodType);
      IF (IOffset <> 0) THEN
        PeriodFormManagement.NextDate(IOffset, ODate, PeriodType);
    END;

    LOCAL PROCEDURE GetPeriodTypeText@1100528610(IPeriodType@1100528600 : Option) PeriodTypeText : Text;
    VAR
      OptionFieldTools@1100528601 : Codeunit 11012271;
    BEGIN
      OptionFieldTools.OptionValueToReadableOption(Text000, IPeriodType, PeriodTypeText);
    END;

    LOCAL PROCEDURE DetermineMeasurementIndexes@1100528611();
    BEGIN
      IndexNoReceived := 1;
      IndexNoInProcess := 2;
      IndexNoFinished := 3;
      IndexNoLeadTime := 4;
      IndexNoTotal := 1;
      IF (ShowTotal) THEN
        IndexNoLeadTime := 2;
    END;

    LOCAL PROCEDURE ShowHours@1100528602(XIndex@1100528600 : Integer;MeasureIndex@1100409002 : Integer);
    VAR
      ChangeRequestTaskHourAcc@1100528602 : Record 11072222;
      ChangeRequestTask@1100409003 : Record 11072214;
      Date@1100409000 : Record 2000000007;
      ChangeRequestTaskHourAccPage@1100409001 : Page 11130543;
      ChangeRequestTasksPage@1100409004 : Page 11126267;
    BEGIN
      IF (CustomerFilter <> '') THEN BEGIN
        ChangeRequestTaskHourAcc.SETFILTER("Customer No.", CustomerFilter);
        ChangeRequestTask.SETFILTER("Customer No.", CustomerFilter);
      END;
      IF (ChangeRequestFilter <> '') THEN BEGIN
        ChangeRequestTaskHourAcc.SETFILTER("Change Request No.", ChangeRequestFilter);
        ChangeRequestTask.SETFILTER("Change Request No.", ChangeRequestFilter);
      END;
      IF (ResponsibleEmployeeFilter <> '') THEN BEGIN
        ChangeRequestTaskHourAcc.SETFILTER("Employee No.", ResponsibleEmployeeFilter);
        ChangeRequestTask.SETFILTER("Responsible Employee", ResponsibleEmployeeFilter);
      END;

      GetPeriod(XIndex, Date);
      ChangeRequestTaskHourAcc.SETRANGE("Hour Line Date", Date."Period Start", Date."Period End");
      ChangeRequestTask.SETRANGE("Due Date", Date."Period Start", Date."Period End");
      IF (NOT ShowTotal) THEN BEGIN
        CASE MeasureIndex OF
          1: ChangeRequestTaskHourAcc.SETRANGE("Task Status", ChangeRequestTaskHourAcc."Task Status"::Received);
          2: ChangeRequestTaskHourAcc.SETRANGE("Task Status", ChangeRequestTaskHourAcc."Task Status"::"In Process");
          3: ChangeRequestTaskHourAcc.SETRANGE("Task Status", ChangeRequestTaskHourAcc."Task Status"::Finished);
        END;
        CASE MeasureIndex OF
          1,2,3: BEGIN
            ChangeRequestTaskHourAccPage.SetKeyFieldsVisible(TRUE);
            ChangeRequestTaskHourAccPage.EDITABLE(FALSE);
            ChangeRequestTaskHourAccPage.SETTABLEVIEW(ChangeRequestTaskHourAcc);
            ChangeRequestTaskHourAccPage.RUNMODAL;
          END;
          4: BEGIN
            ChangeRequestTasksPage.SetKeyFieldsVisible(TRUE);
            ChangeRequestTasksPage.EDITABLE(FALSE);
            ChangeRequestTasksPage.SETTABLEVIEW(ChangeRequestTask);
            ChangeRequestTasksPage.RUNMODAL;
          END;
        END;
      END ELSE BEGIN
        CASE MeasureIndex OF
          1: BEGIN
            ChangeRequestTaskHourAccPage.SetKeyFieldsVisible(TRUE);
            ChangeRequestTaskHourAccPage.EDITABLE(FALSE);
            ChangeRequestTaskHourAccPage.SETTABLEVIEW(ChangeRequestTaskHourAcc);
            ChangeRequestTaskHourAccPage.RUNMODAL;
          END;
          2: BEGIN
            ChangeRequestTasksPage.SetKeyFieldsVisible(TRUE);
            ChangeRequestTasksPage.EDITABLE(FALSE);
            ChangeRequestTasksPage.SETTABLEVIEW(ChangeRequestTask);
            ChangeRequestTasksPage.RUNMODAL;
          END;
        END;
      END;
    END;

    PROCEDURE SetChangeRequestFilter@1100409002(NewValue@1100409000 : Code[20]);
    BEGIN
      ChangeRequestFilter := NewValue;
    END;

    EVENT Chart@-1100528604::DataPointClicked@1(point@1100525000 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartDataPoint");
    BEGIN
      SetDrillDownIndexes(point);
      ShowHours("Drill-Down X Index", "Drill-Down Measure Index" + 1);
    END;

    EVENT Chart@-1100528604::DataPointDoubleClicked@2(point@1100525000 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartDataPoint");
    BEGIN
    END;

    EVENT Chart@-1100528604::AddInReady@3();
    BEGIN
      UpdateChart;
    END;

    EVENT Chart@-1100528604::Refresh@4();
    BEGIN
    END;

    BEGIN
    END.
  }
}

