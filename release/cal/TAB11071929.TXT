OBJECT Table 11071929 SC Budget Line Version - Calls
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Service Contract No.");
               TESTFIELD("Index Date");
             END;

    CaptionML=ENU=Service Contract Budget Line Version - Calls;
  }
  FIELDS
  {
    { 10  ;   ;Service Contract No.;Code20        ;TableRelation=Service-Contract;
                                                   CaptionML=[ENU=Service Contract No.;
                                                              NOR=Servicekontraktnr;
                                                              SVE=Servicekontraktnr] }
    { 20  ;   ;Index Date          ;Date          ;CaptionML=[ENU=Index Date;
                                                              NOR=Indeksdato;
                                                              SVE=Indexdatum] }
    { 30  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 40  ;   ;Cost Type           ;Option        ;OnValidate=BEGIN
                                                                IF "Cost Type" <> xRec."Cost Type" THEN BEGIN
                                                                  IF "Cost Type" = "Cost Type"::Labor THEN BEGIN
                                                                    "Cost Price" := 0;
                                                                  END ELSE BEGIN
                                                                    "Rate Code" := '';
                                                                    Rate := 0;
                                                                    Norm := 0;
                                                                  END;
                                                                  CalcTotals;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Cost Type;
                                                              NOR=Kosttype;
                                                              SVE=Kostnadstyp];
                                                   OptionCaptionML=[ENU=Labor,Material,Subcontracting,Plant,Sundry;
                                                                    NOR=Arbeidskraft,Materiale,Underentrepen›r,Maskin,Diverse;
                                                                    SVE=Arbetskraft,Material,Underentreprenad,Maskin,Diverse];
                                                   OptionString=Labor,Material,Subcontracting,Plant,Sundry }
    { 50  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 60  ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                CalcTotals;
                                                              END;

                                                   CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal] }
    { 70  ;   ;Norm                ;Decimal       ;OnValidate=BEGIN
                                                                IF ("Cost Type" = "Cost Type"::Labor) OR ("Cost Type" = "Cost Type"::Subcontracting) THEN
                                                                  CalcTotals
                                                                ELSE
                                                                  ERROR(Text001, "Cost Type", FIELDCAPTION(Description));
                                                              END;

                                                   CaptionML=[ENU=Norm;
                                                              NOR=Norm;
                                                              SVE=Norm] }
    { 80  ;   ;Rate Code           ;Code10        ;TableRelation="Budget Rate".Code;
                                                   OnValidate=VAR
                                                                ServiceContract@1100528602 : Record 11012812;
                                                                BudgetRateManagement@1100528600 : Codeunit 11012358;
                                                                IndexDate@1100528601 : Date;
                                                                BudgetRateFoundAt@1100528603 : Text[250];
                                                              BEGIN
                                                                IF ("Cost Type" = "Cost Type"::Labor) OR ("Cost Type" = "Cost Type"::Subcontracting) THEN BEGIN
                                                                  IF "Index Date" = 0D THEN
                                                                    IndexDate := TODAY
                                                                  ELSE
                                                                    IndexDate := "Index Date";

                                                                  ServiceContract.GET("Service Contract No.");
                                                                  Rate := BudgetRateManagement.GetBudgetRate(
                                                                    1, '', '', "Service Contract No.", ServiceContract."Global Dimension 1 Code", "Rate Code", IndexDate, BudgetRateFoundAt);
                                                                  IF "Cost Type" = "Cost Type"::Subcontracting THEN BEGIN
                                                                    "Cost Price" := Norm * Rate;
                                                                    Rate := 0;
                                                                  END;
                                                                  CalcTotals;
                                                                END ELSE
                                                                  ERROR(Text001, "Cost Type", FIELDCAPTION("Cost Type"));
                                                              END;

                                                   OnLookup=VAR
                                                              BudgetRate@1100528601 : Record 11012000;
                                                              IndexDate@1100528600 : Date;
                                                            BEGIN
                                                              IF "Index Date" = 0D THEN
                                                                IndexDate := TODAY
                                                              ELSE
                                                                IndexDate := "Index Date";

                                                              BudgetRate.SETRANGE("Starting Date", 0D, IndexDate);
                                                              BudgetRate.SETFILTER("Ending Date", '%1|>=%2', 0D, IndexDate);
                                                              IF PAGE.RUNMODAL(0, BudgetRate) = ACTION::LookupOK THEN
                                                                VALIDATE("Rate Code", BudgetRate.Code);
                                                            END;

                                                   CaptionML=[ENU=Rate Code;
                                                              NOR=Avgiftskode;
                                                              SVE=Avgiftskod] }
    { 90  ;   ;Rate                ;Decimal       ;OnValidate=BEGIN
                                                                IF ("Cost Type" = "Cost Type"::Labor) THEN
                                                                  CalcTotals
                                                                ELSE
                                                                  IF Rate <> 0 THEN
                                                                    ERROR(Text001, "Cost Type", FIELDCAPTION(Rate));
                                                              END;

                                                   CaptionML=[ENU=Rate;
                                                              NOR=Avgift;
                                                              SVE=Avgift] }
    { 100 ;   ;Cost Price          ;Decimal       ;OnValidate=BEGIN
                                                                IF "Cost Type" = "Cost Type"::Labor THEN BEGIN
                                                                  IF "Cost Price" <> 0 THEN
                                                                    ERROR(Text001, "Cost Type", FIELDCAPTION("Cost Price"));
                                                                END ELSE
                                                                  CalcTotals;
                                                              END;

                                                   CaptionML=[ENU=Cost Price;
                                                              NOR=Selvkost;
                                                              SVE=Sj„lvkostnadspris] }
    { 110 ;   ;Cost Amount         ;Decimal       ;CaptionML=[ENU=Amount;
                                                              NOR=Bel›p;
                                                              SVE=Belopp];
                                                   Editable=No }
    { 120 ;   ;Hours               ;Decimal       ;CaptionML=[ENU=Hours;
                                                              NOR="Timer ";
                                                              SVE=Timmar];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Service Contract No.,Index Date,Line No.;Clustered=Yes }
    {    ;Service Contract No.,Index Date,Cost Type }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100528602 : TextConst 'ENU=Cost Type of Line is %1, input of %2 not allowed;NOR=Kostnadstype for rader %1, innmating av %2 ikke tillatt;SVE=Kostnadstypen f”r raden „r %1, inmatning av %2 inte till†ten';

    PROCEDURE CalcTotals@1100528600();
    BEGIN
      IF "Cost Type" = "Cost Type"::Labor THEN BEGIN
        "Cost Amount" := ROUND(Quantity * Norm * Rate);
        Hours := Quantity * Norm;
      END ELSE BEGIN
        "Cost Amount" := ROUND(Quantity * "Cost Price");
        Hours := 0;
      END;
    END;

    BEGIN
    END.
  }
}

