OBJECT Page 6085628 Activ. Wizard Reg. SubPage - O
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW17.00.00.5.00.02;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[DEU=Subseite vom Archivierungsassistenten;
               ENU=Activation Wizard SubPage;
               NLD=Subpagina van activeringswizard];
    SaveValues=No;
    LinksAllowed=No;
    SourceTable=Table6192775;
    PageType=ListPart;
    SourceTableTemporary=Yes;
    RefreshOnActivate=Yes;
    OnAfterGetRecord=BEGIN
                       IF "Company GUID" = ContiniaCompanySetup."Company GUID" THEN BEGIN
                         IF "Company Name" = COMPANYNAME THEN
                           StyleCodeCompanyName := 'Favorable'
                         ELSE
                           StyleCodeCompanyName := 'Attention';

                         IF "Company Code" = CompanyCode THEN
                           StyleCodeCompanyCode := 'Favorable'
                         ELSE
                           StyleCodeCompanyCode := 'Attention';

                         StyleCodeCompanyGUID := 'Favorable';
                       END ELSE BEGIN
                         IF "Company Name" = NewActivationTxt THEN
                           StyleCodeCompanyName := 'Strong'
                         ELSE
                           StyleCodeCompanyName := 'None';

                         IF ("Company Code" = CompanyCode) AND ("Company Name" <> NewActivationTxt) THEN
                           StyleCodeCompanyCode := 'Favorable'
                         ELSE
                           StyleCodeCompanyCode := 'None';

                         StyleCodeCompanyGUID := 'None';
                       END;
                     END;

    OnQueryClosePage=BEGIN
                       IF CloseAction = ACTION::LookupOK THEN BEGIN
                         IF ("Company GUID" = NullGUID) AND ("Company Name" = '') THEN
                           EXIT(FALSE);

                         IF ("Company GUID" <> NullGUID) AND ("Company GUID" <> ContiniaCompanySetup."Company GUID") THEN
                           IF NOT CONFIRM(UpdateWarningQst) THEN
                             EXIT(FALSE);
                       END;

                       EXIT(TRUE);
                     END;

    OnAfterGetCurrRecord=BEGIN
                           RemClientActivationBuffer := Rec;
                         END;

  }
  CONTROLS
  {
    { 1160040020;;Container;
                Name=ContentArea;
                ContainerType=ContentArea }

    { 6085573;1;Group     ;
                Name=Control6085573;
                CaptionML=[DEU=Zeilen;
                           ENU=Lines;
                           NLD=Regels];
                GroupType=Repeater }

    { 6085574;2;Field     ;
                Name=CompanyName;
                SourceExpr="Company Name";
                StyleExpr=StyleCodeCompanyName }

    { 6085575;2;Field     ;
                Name=CompanyGUID;
                SourceExpr=GetCompanyGUIDAsText("Company GUID");
                CaptionClass=FIELDCAPTION("Company GUID");
                Visible=FALSE;
                StyleExpr=StyleCodeCompanyGUID }

    { 6085577;2;Field     ;
                Name=ProductCode;
                SourceExpr="Product Code";
                Visible=FALSE }

    { 6085578;2;Field     ;
                Name=CompanyCode;
                SourceExpr="Company Code";
                StyleExpr=StyleCodeCompanyCode }

  }
  CODE
  {
    VAR
      ContiniaCompanySetup@6085579 : Record 6192773;
      ContiniaOnlineLicenseMgt@6085584 : Codeunit 6192773;
      NullGUID@6085576 : GUID;
      StyleCodeCompanyName@6085578 : Text[30];
      StyleCodeCompanyCode@6085577 : Text[30];
      StyleCodeCompanyGUID@6085574 : Text[30];
      ProductCode@6085586 : Code[10];
      CompanyCode@6085587 : Code[10];
      NewActivationTxt@6085581 : TextConst 'DEU=<< Neue Aktivierung >>;ENU=<< New activation >>;NLD=<< Nieuwe activatie >>';
      UpdateWarningQst@6085580 : TextConst 'DEU=Dies wird die vorhandene Aktivierung bei Continia Online aktualisieren.\\Es ist sehr wichtig, dass Sie dafÅr sorgen, dass kein anderes Unternehmen in einer NAV-Datenbank mit Ihren Client Credentials diese Aktivierung verwendet.\\Mîchten Sie fortfahren?;ENU=This will update the existing activation in Continia Online.\\It is very important that you make sure no other company in any NAV database with your client credentials are using this activation.\\Do you want to continue?;NLD=Dit zal de bestaande activatie bij Continia Online bijwerken.\\Het is zeer belangrijk dat u ervoor zorgt dat geen ander bedrijf in een NAV-database met uw client credentials deze activatie gebruikt.\\Wilt u doorgaan?';
      RemClientActivationBuffer@6085573 : Record 6192775;

    PROCEDURE InitCard@6085580(NewProductCode@6085573 : Code[10];NewCompanyCode@6085574 : Code[10]);
    BEGIN
      ProductCode := NewProductCode;
      CompanyCode := NewCompanyCode;
    END;

    PROCEDURE IsNewCompany@6085578() : Boolean;
    BEGIN
      EXIT(RemClientActivationBuffer."Company GUID" = NullGUID);
    END;

    PROCEDURE GetGuid@6085577() : GUID;
    BEGIN
      EXIT(RemClientActivationBuffer."Company GUID");
    END;

    LOCAL PROCEDURE GetCompanyGUIDAsText@6085576(CompanyGuid@6085573 : GUID) : Text[50];
    BEGIN
      IF CompanyGuid = NullGUID THEN
        EXIT('')
      ELSE
        EXIT(FORMAT(CompanyGuid));
    END;

    PROCEDURE UpdateTables@6085574();
    VAR
      CurrEntryNo@6085573 : Integer;
    BEGIN
      RESET;
      DELETEALL;

      ContiniaCompanySetup.GET;
      ContiniaOnlineLicenseMgt.GetCompanyActivations(Rec,FALSE);
      IF FINDLAST THEN BEGIN
        CurrEntryNo := "Entry No." + 1;
        INIT;
        "Entry No." := CurrEntryNo;
        "Product Code" := '';
        INSERT;

        CurrEntryNo := CurrEntryNo + 1;
      END ELSE
        EXIT;

      INIT;
      "Entry No." := CurrEntryNo;
      "Company GUID" := NullGUID;
      "Company Code" := CompanyCode;
      "Company Name" := NewActivationTxt;
      "Product Code" := ProductCode;
      INSERT;

      RESET;
      SETRANGE("Company GUID",ContiniaCompanySetup."Company GUID");
      IF NOT FINDFIRST THEN BEGIN
        SETRANGE("Company GUID",NullGUID);
        FINDFIRST;
      END;

      SETRANGE("Company GUID");

      RemClientActivationBuffer := Rec;
    END;

    PROCEDURE ClearTempTable@6085575();
    BEGIN
      RESET;
      DELETEALL;

      CurrPage.UPDATE(FALSE);
    END;

    PROCEDURE GetSelectedRecord@6085573(VAR NewRegistration@6085576 : Boolean;VAR CompanyName@6085574 : Text[50];VAR CompanyCode@6085573 : Code[10];VAR CompanyGUID@6085575 : GUID);
    BEGIN
      IF RemClientActivationBuffer."Company Name" = NewActivationTxt THEN BEGIN
        NewRegistration := TRUE;
        CompanyName := '';
        CompanyCode := '';
        CompanyGUID := NullGUID;
      END ELSE BEGIN
        NewRegistration := FALSE;
        CompanyName := RemClientActivationBuffer."Company Name";
        CompanyCode := RemClientActivationBuffer."Company Code";
        CompanyGUID := RemClientActivationBuffer."Company GUID";
      END;
    END;

    BEGIN
    END.
  }
}

