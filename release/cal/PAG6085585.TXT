OBJECT Page 6085585 Template Subpage
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW17.00.00.5.00.07;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[DEU=Vorlagen-Unterseite;
               ENU=Template Subpage;
               NLD=Sjabloon subpagina];
    SourceTable=Table6085580;
    SourceTableView=SORTING(Template No.,Type,Sort Order);
    PageType=ListPart;
    SourceTableTemporary=Yes;
    CardPageID=Template Field Card;
    RefreshOnActivate=Yes;
    OnOpenPage=VAR
                 ContiniaVerMgt@6085573 : Codeunit 6192775;
               BEGIN
                 CardActionVisible := ContiniaVerMgt.NAVVersion < 100000;
               END;

    OnAfterGetRecord=BEGIN
                       IF "Sort Order" = 0 THEN BEGIN
                         StrongColumn := TRUE;
                         DataType := '';
                       END ELSE BEGIN
                         StrongColumn := FALSE;
                         DataType := FORMAT("Data Type");
                       END;
                       UpdateTempCode;
                     END;

    OnNewRecord=BEGIN
                  Type := xRec.Type;
                END;

    OnAfterGetCurrRecord=VAR
                           SingleInstanceStorage@6085573 : Codeunit 6085789;
                         BEGIN
                           SingleInstanceStorage.SetActiveTemplFieldForP6085585("Sort Order");
                         END;

    ActionList=ACTIONS
    {
      { 1000000012;  ;ActionContainer;
                      Name=ActionItems;
                      CaptionML=;
                      ActionContainerType=ActionItems }
      { 1000000013;1 ;Action    ;
                      Name=Card;
                      ShortCutKey=Shift+F5;
                      CaptionML=[DEU=Karte;
                                 ENU=Card;
                                 NLD=Kaart;
                                 SVE=Kort];
                      Visible=CardActionVisible;
                      Image=Edit;
                      OnAction=VAR
                                 TemplateField@6085573 : Record 6085580;
                               BEGIN
                                 IF "Sort Order" = 0 THEN
                                   EXIT;

                                 TemplateField.FILTERGROUP(4);
                                 TemplateField.SETRANGE("Template No.","Template No.");
                                 TemplateField.SETRANGE(Code,Code);
                                 TemplateField.FILTERGROUP(0);
                                 TemplateField.FINDFIRST;

                                 PAGE.RUNMODAL(PAGE::"Template Field Card",TemplateField);

                                 UpdateList;
                               END;
                                }
      { 6085574 ;1   ;Action    ;
                      Name=MoveUp;
                      CaptionML=[DEU=Nach oben;
                                 ENU=Move Up;
                                 NLD=Naar boven];
                      Image=MoveUp;
                      OnAction=VAR
                                 TemplateField@6085573 : Record 6085580;
                                 TemplateField2@6085574 : Record 6085580;
                                 i@6085575 : Integer;
                                 xSortOrder@6085576 : Integer;
                               BEGIN
                                 IF "Sort Order" <= 1 THEN
                                   EXIT;

                                 TemplateField2.GET("Template No.",Type,Code);
                                 NEXT(-1);
                                 TemplateField.GET("Template No.",Type,Code);
                                 i := TemplateField."Sort Order";

                                 TemplateField."Sort Order" := TemplateField2."Sort Order";
                                 TemplateField.MODIFY;

                                 TemplateField2."Sort Order" := i;
                                 TemplateField2.MODIFY;

                                 UpdateList;

                                 GET(TemplateField2."Template No.",TemplateField2.Type,TemplateField2.Code);
                               END;
                                }
      { 6085573 ;1   ;Action    ;
                      Name=MoveDown;
                      CaptionML=[DEU=Nach unten;
                                 ENU=Move Down;
                                 NLD=Naar beneden];
                      Image=MoveDown;
                      OnAction=VAR
                                 TemplateField@6085574 : Record 6085580;
                                 TemplateField2@6085575 : Record 6085580;
                                 i@6085573 : Integer;
                                 xSortOrder@6085576 : Integer;
                               BEGIN
                                 IF "Sort Order" = 0 THEN
                                   EXIT;

                                 TemplateField2.GET("Template No.",Type,Code);

                                 IF NEXT = 0 THEN
                                   EXIT;

                                 TemplateField.GET("Template No.",Type,Code);
                                 i := TemplateField."Sort Order";
                                 TemplateField."Sort Order" := TemplateField2."Sort Order";
                                 TemplateField.MODIFY;

                                 "Sort Order" := TemplateField2."Sort Order";
                                 MODIFY;

                                 TemplateField2."Sort Order" := i;
                                 TemplateField2.MODIFY;

                                 UpdateList;

                                 GET(TemplateField2."Template No.",TemplateField2.Type,TemplateField2.Code);
                               END;
                                }
      { 6085575 ;1   ;Action    ;
                      Name=Delete;
                      CaptionML=[DEU=L”schen;
                                 ENU=Delete;
                                 NLD=Verwijder];
                      Image=Delete;
                      OnAction=VAR
                                 TemplateField@6085573 : Record 6085580;
                                 TemplateField2@6085574 : Record 6085580;
                                 FieldName@6085575 : Text[80];
                               BEGIN
                                 IF "Sort Order" = 0 THEN
                                   EXIT;

                                 FieldName := DELCHR("Field Name",'<',' ');
                                 IF NOT CONFIRM(STRSUBSTNO(DeleteFieldMsg,FieldName)) THEN
                                   EXIT;

                                 TemplateField.GET("Template No.",Type,Code);
                                 TemplateField2.SETCURRENTKEY("Template No.",Type,"Sort Order");
                                 TemplateField2.SETRANGE("Template No.","Template No.");
                                 TemplateField2.SETRANGE(Type,Type);
                                 TemplateField2.SETRANGE(Code,Code);
                                 TemplateField2.FINDLAST;
                                 TemplateField2.SETRANGE(Code);
                                 IF TemplateField2.NEXT = 0 THEN
                                   TemplateField2.NEXT(-1);

                                 TemplateField.DELETE(TRUE);
                                 UpdateList;
                                 IF GET(TemplateField2."Template No.",TemplateField2.Type,TemplateField2.Code) THEN;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1000000000;;Container;
                Name=ContentArea;
                ContainerType=ContentArea }

    { 1000000001;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1000000003;2;Field  ;
                Name=Code;
                SourceExpr=TempCode;
                CaptionClass=FIELDCAPTION(Code);
                Visible=FALSE }

    { 1000000005;2;Field  ;
                Name=Field Name;
                SourceExpr="Field Name";
                Style=Strong;
                StyleExpr=StrongColumn }

    { 1000000006;2;Field  ;
                Name=Data Type;
                SourceExpr=DataType;
                CaptionClass=FIELDCAPTION("Data Type") }

    { 1000000009;2;Field  ;
                Name=Required;
                SourceExpr=Required;
                Visible=FALSE }

    { 161024012;2;Field   ;
                Name=Insert on new Templates;
                SourceExpr="Insert on new Templates";
                Visible=IsMasterTemplate }

  }
  CODE
  {
    VAR
      StrongColumn@6085573 : Boolean;
      TemplateNo@6085575 : Code[20];
      HeaderText@6085576 : TextConst 'DEU=Kopffelder;ENU=Header Fields;NLD=Kopvelden';
      LineText@6085577 : TextConst 'DEU=Zeilenfelder;ENU=Line Fields;NLD=Regelvelden';
      DataType@6085574 : Text[250] INDATASET;
      IsMasterTemplate@6085578 : Boolean;
      DeleteFieldMsg@6085579 : TextConst 'DEU=%1 l”schen?;ENU=Delete %1?;NLD=%1 verwijderen?';
      CardActionVisible@6085580 : Boolean INDATASET;
      TempCode@6085581 : Code[20];

    PROCEDURE SetTemplateNo@6085574(NewTemplateNo@6085573 : Code[20]);
    VAR
      Template@6085574 : Record 6085579;
    BEGIN
      Template.GET(NewTemplateNo);
      IsMasterTemplate := Template.Type = Template.Type::Master;

      TemplateNo := NewTemplateNo;
      UpdateList;
    END;

    LOCAL PROCEDURE UpdateList@6085573();
    VAR
      TemplateField@6085574 : Record 6085580;
      LastType@6085575 : Text[50];
    BEGIN
      DELETEALL;
      TemplateField.SETCURRENTKEY("Template No.",Type,"Sort Order");
      TemplateField.SETRANGE("Template No.",TemplateNo);
      IF TemplateField.FINDSET THEN
        REPEAT
          IF LastType <> FORMAT(TemplateField.Type) THEN BEGIN
            INIT;
            "Template No." := TemplateNo;
            Type := TemplateField.Type;
            IF TemplateField.Type = 0 THEN BEGIN
              Code := '#%&HEADERTYPE#%&';
              "Field Name" := HeaderText;
            END ELSE BEGIN
              Code := '#%&LINETYPE#%&';
              "Field Name" := LineText;
            END;
            "Sort Order" := 0;
            INSERT;
            LastType := FORMAT(TemplateField.Type);
          END;
          TRANSFERFIELDS(TemplateField,TRUE);
          "Field Name" := '     '  + TemplateField."Field Name";
          INSERT;
        UNTIL TemplateField.NEXT = 0;

      FILTERGROUP(2);
      SETRANGE("Template No.",TemplateNo);
      FILTERGROUP(0);

      SETCURRENTKEY("Template No.",Type,"Sort Order");
      IF FINDFIRST THEN;
    END;

    LOCAL PROCEDURE UpdateTempCode@6085577();
    BEGIN
      TempCode := '';
      IF (Rec.Code <> '#%&HEADERTYPE#%&') AND (Rec.Code <> '#%&LINETYPE#%&') THEN
        TempCode := Rec.Code;
    END;

    BEGIN
    END.
  }
}

