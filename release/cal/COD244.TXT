OBJECT Codeunit 244 Item Jnl.-B.Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04;
  }
  PROPERTIES
  {
    TableNo=233;
    OnRun=BEGIN
            ItemJnlBatch.COPY(Rec);
            Code;
            Rec := ItemJnlBatch;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to post the journals and print the posting report?;NOR=Vil du bokfre kladdene og skrive ut bokfringsrapporten?;SVE=Vill du bokfra journalerna och skriva ut bokfringsrapporten?';
      Text001@1001 : TextConst 'ENU=The journals were successfully posted.;NOR=Kladdene ble bokfrt p riktig mte.;SVE=Journalerna r bokfrda.';
      Text002@1002 : TextConst 'ENU="It was not possible to post all of the journals. ";NOR="Det er ikke mulig  bokfre alle kladdene. ";SVE="Det var inte mjligt att bokfra alla journaler. "';
      Text003@1003 : TextConst 'ENU=The journals that were not successfully posted are now marked.;NOR=Kladdene som ikke ble bokfrt, er merket.;SVE=Journalerna som inte blev bokfrda r nu markerade.';
      ItemJnlTemplate@1004 : Record 82;
      ItemJnlBatch@1005 : Record 233;
      ItemJnlLine@1006 : Record 83;
      ItemReg@1007 : Record 46;
      WhseReg@1010 : Record 7313;
      ItemJnlPostBatch@1008 : Codeunit 23;
      JnlWithErrors@1009 : Boolean;

    LOCAL PROCEDURE Code@1();
    VAR
      HideDialog@1000 : Boolean;
    BEGIN
      WITH ItemJnlBatch DO BEGIN
        ItemJnlTemplate.GET("Journal Template Name");
        ItemJnlTemplate.TESTFIELD("Posting Report ID");

        HideDialog := FALSE;
        OnBeforePostJournalBatch(ItemJnlBatch,HideDialog);
        IF NOT HideDialog THEN
          IF NOT CONFIRM(Text000,FALSE) THEN
            EXIT;

        FIND('-');
        REPEAT
          ItemJnlLine."Journal Template Name" := "Journal Template Name";
          ItemJnlLine."Journal Batch Name" := Name;
          ItemJnlLine."Line No." := 1;
          CLEAR(ItemJnlPostBatch);
          IF ItemJnlPostBatch.RUN(ItemJnlLine) THEN BEGIN
            OnAfterPostJournalBatch(ItemJnlBatch);
            MARK(FALSE);
            IF ItemReg.GET(ItemJnlPostBatch.GetItemRegNo) THEN BEGIN
              ItemReg.SETRECFILTER;
              REPORT.RUN(ItemJnlTemplate."Posting Report ID",FALSE,FALSE,ItemReg);
            END;

            IF WhseReg.GET(ItemJnlPostBatch.GetWhseRegNo) THEN BEGIN
              WhseReg.SETRECFILTER;
              REPORT.RUN(ItemJnlTemplate."Whse. Register Report ID",FALSE,FALSE,WhseReg);
            END;
          END ELSE BEGIN
            MARK(TRUE);
            JnlWithErrors := TRUE;
          END;
        UNTIL NEXT = 0;

        IF NOT JnlWithErrors THEN
          MESSAGE(Text001)
        ELSE
          MESSAGE(
            Text002 +
            Text003);

        IF NOT FIND('=><') THEN BEGIN
          RESET;
          Name := '';
        END;
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPostJournalBatch@3(VAR ItemJournalBatch@1000 : Record 233);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostJournalBatch@2(VAR ItemJournalBatch@1000 : Record 233;VAR HideDialog@1001 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

