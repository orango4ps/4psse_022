OBJECT Codeunit 143 ECSL Report Validate
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    TableNo=740;
    OnRun=BEGIN
            ErrorMessage.SetContext(RECORDID);
            ErrorMessage.ClearLog;

            IF NOT IsPeriodValid THEN BEGIN
              ErrorMessage.LogMessage(Rec,FIELDNO("No."),ErrorMessage."Message Type"::Error,InvalidPeriodErr);
              EXIT;
            END;

            ValidateVATReportHasLine(Rec);
            CheckVATRegNoOnLines(Rec);
            CheckZeroAmountOnLines(Rec);
          END;

  }
  CODE
  {
    VAR
      NoLineFoundErr@1000 : TextConst 'ENU=You cannot release a blank VAT report.;NOR=Du kan ikke frigi en tom mva-rapport.';
      ErrorMessage@1042 : Record 700;
      ZeroTotalValueErr@1040 : TextConst '@@@=Placeholder 1 holds the line no that cause the error;ENU=You cannot release the report because line No. %1 has zero as the Total Value Of Supplies.;NOR=Du kan ikke frigi rapporten fordi linjenummer %1 har null som verdi for Totalverdi for forsyninger.';
      NoVatRegNoErr@1041 : TextConst '@@@=Placeholder 1 holds the line no that cause the error;ENU=You cannot release the report because line No. %1 is missing a VAT Registration Number. You need to correct this on the sales document and post it again.;NOR=Du kan ikke frigi rapporten fordi linjenummer %1 mangler et organisasjonsnummer. Du m† korrigere dette p† salgsdokumentet og bokf›re rapporten p† nytt.';
      InvalidPeriodErr@1043 : TextConst 'ENU=The period is not valid.;NOR=Perioden er ugyldig.';

    LOCAL PROCEDURE ValidateVATReportHasLine@7(VATReportHeader@1040 : Record 740);
    VAR
      ECSLVATReportLine@1041 : Record 362;
    BEGIN
      ECSLVATReportLine.SETRANGE("Report No.",VATReportHeader."No.");
      IF ECSLVATReportLine.COUNT = 0 THEN
        ErrorMessage.LogMessage(VATReportHeader,VATReportHeader.FIELDNO("No."),ErrorMessage."Message Type"::Error,NoLineFoundErr);
    END;

    LOCAL PROCEDURE CheckVATRegNoOnLines@1040(VATReportHeader@1040 : Record 740);
    VAR
      ECSLVATReportLine@1041 : Record 362;
    BEGIN
      ECSLVATReportLine.SETRANGE("Report No.",VATReportHeader."No.");
      ECSLVATReportLine.SETRANGE("Customer VAT Reg. No.",'');
      IF ECSLVATReportLine.FINDSET THEN
        REPEAT
          ErrorMessage.LogMessage(ECSLVATReportLine,ECSLVATReportLine.FIELDNO("Customer VAT Reg. No."),
            ErrorMessage."Message Type"::Error,STRSUBSTNO(NoVatRegNoErr,ECSLVATReportLine."Line No."));
        UNTIL ECSLVATReportLine.NEXT = 0;
    END;

    LOCAL PROCEDURE CheckZeroAmountOnLines@1041(VATReportHeader@1040 : Record 740);
    VAR
      ECSLVATReportLine@1041 : Record 362;
    BEGIN
      ECSLVATReportLine.SETRANGE("Report No.",VATReportHeader."No.");
      ECSLVATReportLine.SETRANGE("Total Value Of Supplies",0);

      IF ECSLVATReportLine.FINDSET THEN
        REPEAT
          ErrorMessage.LogMessage(ECSLVATReportLine,ECSLVATReportLine.FIELDNO("Total Value Of Supplies"),
            ErrorMessage."Message Type"::Error,STRSUBSTNO(ZeroTotalValueErr,ECSLVATReportLine."Line No."));
        UNTIL ECSLVATReportLine.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

