OBJECT Table 11229900 Risk Opportunity Schedule
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               GetSourceDescription;
             END;

    OnDelete=VAR
               RiskLine@1100528800 : Record 11229903;
               OpportunityLine@1100528801 : Record 11229904;
             BEGIN
               RiskLine.SETRANGE(Source, Source);
               RiskLine.SETRANGE("Source Code", "Source Code");
               RiskLine.SETRANGE("Schedule Date", Date);
               RiskLine.DELETEALL(TRUE);

               OpportunityLine.SETRANGE(Source, Source);
               OpportunityLine.SETRANGE("Source Code", "Source Code");
               OpportunityLine.SETRANGE("Schedule Date", Date);
               OpportunityLine.DELETEALL(TRUE);

               DeleteCommentLine;
               DeleteDocumentLinks;
             END;

    CaptionML=ENU=Risk Opportunity Schedule;
    LookupPageID=Page11126561;
    DrillDownPageID=Page11126561;
  }
  FIELDS
  {
    { 10  ;   ;Source              ;Option        ;CaptionML=[ENU=Source;
                                                              NOR=Source;
                                                              SVE=K„lla];
                                                   OptionCaptionML=ENU=Project,Try-Out,Service Contract;
                                                   OptionString=Project,Try-Out,Service Contract;
                                                   Editable=No }
    { 20  ;   ;Source Code         ;Code20        ;TableRelation=IF (Source=CONST(Project)) Job.No.
                                                                 ELSE IF (Source=CONST(Try-Out)) Try-out.Code
                                                                 ELSE IF (Source=CONST(Service Contract)) Service-Contract.No.;
                                                   OnValidate=BEGIN
                                                                IF xRec."Source Code" <> "Source Code" THEN
                                                                  GetSourceDescription;
                                                              END;

                                                   CaptionML=[ENU=Source Code;
                                                              NOR=Kildespor;
                                                              SVE=Ursprungskod];
                                                   Editable=No }
    { 30  ;   ;Source Code Description;Text100    ;CaptionML=ENU=Source Code Description;
                                                   Editable=No }
    { 40  ;   ;Date                ;Date          ;CaptionML=[ENU=Date;
                                                              NOR=Dato;
                                                              SVE=Datum];
                                                   NotBlank=Yes }
    { 50  ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 60  ;   ;Fixed               ;Boolean       ;CaptionML=[ENU=Fixed;
                                                              NOR=Fast;
                                                              SVE=Fast] }
    { 100 ;   ;Previous Schedule Date;Date        ;TableRelation="Risk Opportunity Schedule".Date WHERE (Source=FIELD(Source),
                                                                                                         Source Code=FIELD(Source Code));
                                                   CaptionML=ENU=Previous Schedule Date }
    { 200 ;   ;Total Risk Cost     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Risk Line"."Total Risk Cost Amount" WHERE (Source=FIELD(Source),
                                                                                                               Source Code=FIELD(Source Code),
                                                                                                               Schedule Date=FIELD(Date)));
                                                   CaptionML=[ENU=Total Risk Cost;
                                                              SVE=Total riskkostnad, m†l] }
    { 210 ;   ;Total Risk Sales    ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Risk Line"."Total Risk Sales Amount" WHERE (Source=FIELD(Source),
                                                                                                                Source Code=FIELD(Source Code),
                                                                                                                Schedule Date=FIELD(Date)));
                                                   CaptionML=[ENU=Total Risk Sales;
                                                              SVE=Total riskkostnad, f”rs„ljning] }
    { 220 ;   ;Target Risk Cost    ;Decimal       ;CaptionML=[ENU=Target Risk Cost;
                                                              SVE=Riskkostnad, m†l] }
    { 230 ;   ;Target Risk Sales   ;Decimal       ;CaptionML=[ENU=Target Risk Sales;
                                                              SVE=Risk, f”rs„ljningsm†l] }
    { 300 ;   ;Total Opportunity Cost;Decimal     ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Opportunity Line"."Total Opportunity Cost Amt." WHERE (Source=FIELD(Source),
                                                                                                                           Source Code=FIELD(Source Code),
                                                                                                                           Schedule Date=FIELD(Date)));
                                                   CaptionML=[ENU=Total Opportunity Cost;
                                                              SVE=Total aff„rsm”jlighetskostnad] }
    { 310 ;   ;Total Opportunity Sales;Decimal    ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Opportunity Line"."Total Opportunity Sales Amt." WHERE (Source=FIELD(Source),
                                                                                                                            Source Code=FIELD(Source Code),
                                                                                                                            Schedule Date=FIELD(Date)));
                                                   CaptionML=[ENU=Total Opportunity Sales;
                                                              SVE=Total aff„rsm”jlighet, f”rs„ljning] }
    { 320 ;   ;Target Opportunity Cost;Decimal    ;CaptionML=[ENU=Target Opportunity Cost;
                                                              SVE=Aff„rsm”jlighetskostnad, m†l] }
    { 330 ;   ;Target Opportunity Sales;Decimal   ;CaptionML=[ENU=Target Opportunity Sales;
                                                              SVE=Aff„rsm”jlighet f”rs„ljningsm†l] }
    { 400 ;   ;Original Source     ;Option        ;CaptionML=ENU=Original Source;
                                                   OptionCaptionML=ENU=Project,Try-Out,Service Contract;
                                                   OptionString=Project,Try-Out,Service Contract;
                                                   Editable=No }
    { 410 ;   ;Original Source Code;Code20        ;TableRelation=IF (Original Source=CONST(Project)) Job.No.
                                                                 ELSE IF (Original Source=CONST(Try-Out)) Try-out.Code
                                                                 ELSE IF (Original Source=CONST(Service Contract)) Service-Contract.No.;
                                                   CaptionML=ENU=Original Source Code;
                                                   Editable=No }
    { 1000;   ;Main Project No.    ;Code20        ;CaptionML=[ENU=Main Project No.;
                                                              NOR=Hovedprosjektnr.;
                                                              SVE=Huvudprojektnr] }
  }
  KEYS
  {
    {    ;Source,Source Code,Date                 ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528800 : TextConst 'ENU=Schedule date %1, does not exist for %2 %3.';
      FPSLicenseManagement@1100527400 : Codeunit 11229289;

    PROCEDURE DeleteCommentLine@1100529800();
    VAR
      CommentLine@1100529800 : Record 11020634;
      RecRef@1100529801 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);

      CommentLine.RESET;
      CommentLine.SETRANGE("Table Name", CommentLine."Table Name"::"Risk Opportunity Schedule");
      CommentLine.SETFILTER("Record ID", FORMAT(RecRef.RECORDID()));
      CommentLine.DELETEALL(TRUE);
    END;

    PROCEDURE CommentLineExist@1210190011() : Boolean;
    VAR
      RecRef@1210190000 : RecordRef;
      CommentLine@1210190001 : Record 11020634;
    BEGIN
      RecRef.GETTABLE(Rec);
      CommentLine.SETRANGE("Table Name", CommentLine."Table Name"::"Risk Opportunity Schedule");
      CommentLine.SETFILTER("Record ID", FORMAT(RecRef.RECORDID()));
      IF NOT CommentLine.ISEMPTY THEN
        EXIT(TRUE);

      EXIT(FALSE);
    END;

    PROCEDURE DocumentsExist@1100528801() : Boolean;
    VAR
      RecRef@1210190000 : RecordRef;
      DocumentLink@1210190001 : Record 11012747;
    BEGIN
      IF NOT FPSLicenseManagement.LicenseAndReadPermissionForTable(DocumentLink) THEN
        EXIT;

      RecRef.GETTABLE(Rec);

      DocumentLink.SETCURRENTKEY("Record ID");
      DocumentLink.SETFILTER("Record ID", STRSUBSTNO('''%1''', RecRef.RECORDID));
      DocumentLink.SETRANGE("Internal Company", COMPANYNAME);

      IF NOT DocumentLink.ISEMPTY THEN
        EXIT(TRUE);

      EXIT(FALSE);
    END;

    LOCAL PROCEDURE DeleteDocumentLinks@1100528400();
    VAR
      DocumentLink@1100528402 : Record 11012747;
      DocumentLinkMgt@1100528400 : Codeunit 11012401;
      RecRef@1100528401 : RecordRef;
    BEGIN
      IF NOT FPSLicenseManagement.LicenseAndReadPermissionForTable(DocumentLink) THEN
        EXIT;

      RecRef.GETTABLE(Rec);
      DocumentLinkMgt.DeleteDocumentLinksFromObject(RecRef);
    END;

    PROCEDURE TestRiskOpportunityScheduleDate@1100528800(ISource@1100528800 : 'Project,Try-Out,Service Contract';ISourceCode@1100528801 : Code[20];IScheduleDate@1100528802 : Date);
    VAR
      RiskOpportunitySchedule@1100528803 : Record 11229900;
    BEGIN
      RiskOpportunitySchedule.SETRANGE(Source ,ISource);
      RiskOpportunitySchedule.SETRANGE("Source Code", ISourceCode);
      RiskOpportunitySchedule.SETRANGE(Date, IScheduleDate);
      IF RiskOpportunitySchedule.ISEMPTY THEN
        ERROR(Text000, IScheduleDate, ISource, ISourceCode);
    END;

    LOCAL PROCEDURE GetSourceDescription@1100525000();
    VAR
      Job@1100525000 : Record 11072003;
      TryOut@1100525001 : Record 11012750;
      ServiceContract@1100525002 : Record 11012812;
    BEGIN
      CASE Source OF
        Source::Project:
          BEGIN
            IF Job.GET("Source Code") THEN
              "Source Code Description" := Job.Description
            ELSE
              "Source Code Description" := ''
          END;
        Source::"Try-Out":
          BEGIN
            IF TryOut.GET("Source Code") THEN
              "Source Code Description" := TryOut.Description
            ELSE
              "Source Code Description" := ''
          END;
        Source::"Service Contract":
          BEGIN
            IF ServiceContract.GET("Source Code") THEN
              "Source Code Description" := ServiceContract.Description
            ELSE
              "Source Code Description" := ''
          END;
       END;
    END;

    PROCEDURE CopyScheduleToProjectOrServiceContract@1100529700();
    VAR
      TryoutSuccessor@1100529701 : Record 11012761;
      RiskOpportunityManagement@1100529700 : Codeunit 11229282;
    BEGIN
      TryoutSuccessor.SETRANGE("Try-out No.", "Source Code");
      TryoutSuccessor.FILTERGROUP(-1);
      TryoutSuccessor.SETFILTER("Project No.", '<>''''');
      TryoutSuccessor.SETFILTER("Service Contract No.", '<>''''');
      TryoutSuccessor.FILTERGROUP(0);
      IF TryoutSuccessor.FINDSET THEN
        IF PAGE.RUNMODAL(0, TryoutSuccessor) = ACTION::LookupOK THEN
          IF TryoutSuccessor."Project No."<>'' THEN
            RiskOpportunityManagement.CopyScheduleBetweenSources(1, "Source Code", Date,0,TryoutSuccessor."Project No.", Date)
          ELSE
            IF TryoutSuccessor."Service Contract No."<>'' THEN
              RiskOpportunityManagement.CopyScheduleBetweenSources(1, "Source Code", Date,2,TryoutSuccessor."Service Contract No.", Date);
    END;

    BEGIN
    END.
  }
}

