OBJECT Table 750 Standard General Journal
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               StdGenJnlLine@1000 : Record 751;
             BEGIN
               StdGenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
               StdGenJnlLine.SETRANGE("Standard Journal Code",Code);

               StdGenJnlLine.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=Standard General Journal;
               NOR=Standard finanskladd;
               SVE=Standardredovisningsjournal];
    LookupPageID=Page750;
  }
  FIELDS
  {
    { 1   ;   ;Journal Template Name;Code10       ;TableRelation="Gen. Journal Template";
                                                   CaptionML=[ENU=Journal Template Name;
                                                              NOR=Kladdemalnavn;
                                                              SVE=Mallnamn f”r journal];
                                                   NotBlank=Yes }
    { 2   ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 3   ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
  }
  KEYS
  {
    {    ;Journal Template Name,Code              ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      GenJnlTemplate@1210190000 : Record 80;
      GenJnlBatch@1008 : Record 232;
      LastGenJnlLine@1007 : Record 81;
      GenJnlLine@1006 : Record 81;
      Window@1003 : Dialog;
      WindowUpdateDateTime@1002 : DateTime;
      NoOfJournalsToBeCreated@1001 : Integer;
      Text000@1004 : TextConst 'ENU=Getting Standard General Journal Lines @1@@@@@@@;NOR=Henter standard finanskladdelinjer @1@@@@@@@;SVE=H„mtar rader f”r standardredovisningsjournal @1@@@@@@@';
      NoOfJournalsCreated@1005 : Integer;

    [External]
    PROCEDURE CreateGenJnlFromStdJnl@1(StdGenJnl@1005 : Record 750;JnlBatchName@1003 : Code[10]);
    VAR
      DocumentNo@1001 : Code[20];
    BEGIN
      IF StdGenJnl.IsZeroAmountJournal THEN
        DocumentNo := TryGetNextDocumentNo(StdGenJnl."Journal Template Name",JnlBatchName);

      CreateGenJnl(StdGenJnl,JnlBatchName,DocumentNo);
    END;

    [External]
    PROCEDURE CreateGenJnlFromStdJnlWithDocNo@5(StdGenJnl@1005 : Record 750;JnlBatchName@1003 : Code[10];DocumentNo@1001 : Code[20]);
    BEGIN
      IF DocumentNo = '' THEN
        CreateGenJnl(StdGenJnl,JnlBatchName,'')
      ELSE
        CreateGenJnl(StdGenJnl,JnlBatchName,DocumentNo);
    END;

    [External]
    PROCEDURE Initialize@15(VAR StdGenJnl@1001 : Record 750;JnlBatchName@1000 : Code[10]);
    BEGIN
      IF GenJnlTemplate.GET("Journal Template Name") THEN;  //M26743

      GenJnlLine."Journal Template Name" := StdGenJnl."Journal Template Name";
      GenJnlLine."Journal Batch Name" := JnlBatchName;
      GenJnlLine.SETRANGE("Journal Template Name",StdGenJnl."Journal Template Name");
      GenJnlLine.SETRANGE("Journal Batch Name",JnlBatchName);

      LastGenJnlLine.SETRANGE("Journal Template Name",StdGenJnl."Journal Template Name");
      LastGenJnlLine.SETRANGE("Journal Batch Name",JnlBatchName);

      IF LastGenJnlLine.FINDLAST THEN;

      GenJnlBatch.SETRANGE("Journal Template Name",StdGenJnl."Journal Template Name");
      GenJnlBatch.SETRANGE(Name,JnlBatchName);

      IF GenJnlBatch.FINDFIRST THEN;
    END;

    LOCAL PROCEDURE CopyGenJnlFromStdJnl@3(StdGenJnlLine@1001 : Record 751;DocumentNo@1002 : Code[20]);
    VAR
      GenJnlManagement@1000 : Codeunit 230;
      Balance@1007 : Decimal;
      TotalBalance@1006 : Decimal;
      ShowBalance@1005 : Boolean;
      ShowTotalBalance@1004 : Boolean;
    BEGIN
      GenJnlLine.INIT;
      GenJnlLine."Line No." := 0;
      GenJnlManagement.CalcBalance(GenJnlLine,LastGenJnlLine,Balance,TotalBalance,ShowBalance,ShowTotalBalance);
      GenJnlLine.SetUpNewLine(LastGenJnlLine,Balance,TRUE);
      IF LastGenJnlLine."Line No." <> 0 THEN
        GenJnlLine."Line No." := LastGenJnlLine."Line No." + 10000
      ELSE
        GenJnlLine."Line No." := 10000;

      GenJnlLine.TRANSFERFIELDS(StdGenJnlLine,FALSE);

      IF GenJnlLine."Source Code" = '' THEN                       //M26743
        GenJnlLine."Source Code" := GenJnlTemplate."Source Code"; //M26743
      IF GenJnlLine."Reason Code" = '' THEN                       //M26743
        GenJnlLine."Reason Code" := GenJnlBatch."Reason Code";    //M26743

      GenJnlLine.UpdateLineBalance;
      GenJnlLine."Currency Factor" := 0;
      GenJnlLine.VALIDATE("Currency Code");

      IF GenJnlLine."VAT Prod. Posting Group" <> '' THEN
        GenJnlLine.VALIDATE("VAT Prod. Posting Group");
      IF (GenJnlLine."VAT %" <> 0) AND GenJnlBatch."Allow VAT Difference" THEN
        GenJnlLine.VALIDATE("VAT Amount",StdGenJnlLine."VAT Amount");
      GenJnlLine.VALIDATE("Bal. VAT Prod. Posting Group");
      GenJnlLine."Dimension Set ID" := StdGenJnlLine."Dimension Set ID";
      IF GenJnlBatch."Allow VAT Difference" THEN
        GenJnlLine.VALIDATE("Bal. VAT Amount",StdGenJnlLine."Bal. VAT Amount");
      IF DocumentNo <> '' THEN
        GenJnlLine."Document No." := DocumentNo;
      OnAfterCopyGenJnlFromStdJnl(GenJnlLine,StdGenJnlLine);
      GenJnlLine.INSERT(TRUE);

      LastGenJnlLine := GenJnlLine;
    END;

    LOCAL PROCEDURE CreateGenJnl@6(StdGenJnl@1002 : Record 750;JnlBatchName@1001 : Code[10];DocumentNo@1003 : Code[20]);
    VAR
      StdGenJnlLine@1000 : Record 751;
    BEGIN
      Initialize(StdGenJnl,JnlBatchName);

      StdGenJnlLine.SETRANGE("Journal Template Name",StdGenJnl."Journal Template Name");
      StdGenJnlLine.SETRANGE("Standard Journal Code",StdGenJnl.Code);
      OpenWindow(Text000,StdGenJnlLine.COUNT);
      IF StdGenJnlLine.FIND('-') THEN
        REPEAT
          UpdateWindow;
          CopyGenJnlFromStdJnl(StdGenJnlLine,DocumentNo);
        UNTIL StdGenJnlLine.NEXT = 0;
    END;

    PROCEDURE IsZeroAmountJournal@8() : Boolean;
    VAR
      StandardGeneralJournalLine@1001 : Record 751;
    BEGIN
      StandardGeneralJournalLine.SETRANGE("Journal Template Name","Journal Template Name");
      StandardGeneralJournalLine.SETRANGE("Standard Journal Code",Code);
      StandardGeneralJournalLine.CALCSUMS("Debit Amount","Credit Amount");
      EXIT((StandardGeneralJournalLine."Debit Amount" = 0) AND (StandardGeneralJournalLine."Credit Amount" = 0));
    END;

    LOCAL PROCEDURE TryGetNextDocumentNo@7(TemplateName@1000 : Code[10];BatchName@1001 : Code[10]) : Code[20];
    VAR
      GenJournalBatch@1002 : Record 232;
      NoSeriesMgt@1003 : Codeunit 396;
    BEGIN
      GenJournalBatch.GET(TemplateName,BatchName);
      IF GenJournalBatch."No. Series" = '' THEN
        EXIT('');

      EXIT(NoSeriesMgt.TryGetNextNo(GenJournalBatch."No. Series",WORKDATE));
    END;

    LOCAL PROCEDURE OpenWindow@12(DisplayText@1001 : Text[250];NoOfJournalsToBeCreated2@1000 : Integer);
    BEGIN
      NoOfJournalsCreated := 0;
      NoOfJournalsToBeCreated := NoOfJournalsToBeCreated2;
      WindowUpdateDateTime := CURRENTDATETIME;
      Window.OPEN(DisplayText);
    END;

    LOCAL PROCEDURE UpdateWindow@4();
    BEGIN
      NoOfJournalsCreated := NoOfJournalsCreated + 1;
      IF CURRENTDATETIME - WindowUpdateDateTime >= 300 THEN BEGIN
        WindowUpdateDateTime := CURRENTDATETIME;
        Window.UPDATE(1,ROUND(NoOfJournalsCreated / NoOfJournalsToBeCreated * 10000,1));
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyGenJnlFromStdJnl@2(VAR GenJournalLine@1000 : Record 81;StdGenJournalLine@1001 : Record 751);
    BEGIN
    END;

    BEGIN
    END.
  }
}

