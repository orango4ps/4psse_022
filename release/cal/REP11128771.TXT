OBJECT Report 11128771 Create SO from Defect
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Serviceauftrag von Defekt erstellen;
               ENU=Create Service Order from Defect;
               NLD=Serviceorder vanuit gebrek aanmaken];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 1100528500;;DataItem;                  ;
               DataItemTable=Table11128785;
               DataItemTableView=SORTING(Header Entry ID,Structure Line Code,Line No.);
               OnPreDataItem=BEGIN
                               LOCKTABLE;
                             END;

               OnAfterGetRecord=BEGIN
                                  TESTFIELD("Service Order No.",'');
                                  TESTFIELD("Location Type","Location Type"::"Service Location");
                                  "Service Order No." := CreateServiceOrder;
                                  MODIFY;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TempServiceOrder@1100528500 : TEMPORARY Record 11012823;

    LOCAL PROCEDURE CreateServiceOrder@1100528500() : Code[20];
    VAR
      ServiceOrder@1100528500 : Record 11012823;
      ServiceSourceTypeSetup@1100528501 : Record 11071726;
      ServiceOrderObject@1100528502 : Record 11071724;
      MaintenanceManagement@1100528503 : Codeunit 11012829;
      ActualServiceContract@1100528504 : Code[20];
    BEGIN
      ServiceSourceTypeSetup.GET(ServiceSourceTypeSetup."Source Type"::Direct);

      ActualServiceContract := MaintenanceManagement.GetActualContractForObject("NEN2767/LTMP Line"."Object No.",TODAY);

      IF CheckNewServiceOrderNeeded(ActualServiceContract) THEN BEGIN
        ServiceSourceTypeSetup.TESTFIELD("No. Series");
        ServiceOrder."No. Series" := ServiceSourceTypeSetup."No. Series";
        ServiceOrder."Source Type" := ServiceOrder."Source Type"::Direct;
        ServiceOrder.INSERT(TRUE);
      END ELSE BEGIN
        ServiceOrder := TempServiceOrder;
        ServiceOrder.FIND;
      END;

      IF "NEN2767/LTMP Line"."Location Code" <> '' THEN
        ServiceOrder.VALIDATE("Service Location No.","NEN2767/LTMP Line"."Location Code");
      IF ActualServiceContract <> '' THEN
        ServiceOrder.VALIDATE("Service Contract No.",ActualServiceContract);

      ServiceOrder.MODIFY;

      TempServiceOrder := ServiceOrder;
      IF TempServiceOrder.INSERT THEN;

      IF "NEN2767/LTMP Line"."Object No." <> '' THEN BEGIN
        ServiceOrderObject.INIT;
        ServiceOrderObject.VALIDATE("Service Order No.",ServiceOrder."No.");
        ServiceOrderObject.VALIDATE("Object No.","NEN2767/LTMP Line"."Object No.");
        ServiceOrderObject.VALIDATE("Service Contract No.",ServiceOrder."Service Contract No.");
        ServiceOrderObject.Source := ServiceOrderObject.Source::Other;
        IF ServiceOrderObject.INSERT(TRUE) THEN; //IF cause object can exist multiple times
      END;

      EXIT(ServiceOrder."No.");
    END;

    LOCAL PROCEDURE CheckNewServiceOrderNeeded@1100528501(ActualServiceContract@1100528500 : Code[20]) : Boolean;
    BEGIN
      IF "NEN2767/LTMP Line"."Location Type" = "NEN2767/LTMP Line"."Location Type"::"Service Location" THEN
        TempServiceOrder.SETRANGE("Service Location No.","NEN2767/LTMP Line"."Location Code")
      ELSE
        TempServiceOrder.SETRANGE("Service Location No.",'');
      IF NOT TempServiceOrder.FINDFIRST THEN
        EXIT(TRUE);

      EXIT(ActualServiceContract <> TempServiceOrder."Service Contract No.");
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

