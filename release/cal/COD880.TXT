OBJECT Codeunit 880 OCR - Send to Service
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    TableNo=472;
    OnRun=BEGIN
            SendAllReadyToOcr;
          END;

  }
  CODE
  {
    VAR
      SendMsg@1000 : TextConst 'ENU=Sending to the OCR Service @1@@@@@@@@@@@@@@@@@@@.;NOR=Sender til OCR-tjenesten @1@@@@@@@@@@@@@@@@@@@.;SVE=Skickar till OCR-service @1@@@@@@@@@@@@@@@@@@@.';
      SendDoneMsg@1001 : TextConst '@@@=%1 is a number, e.g. 1;ENU=%1 documents have been sent to the OCR service.;NOR=%1 dokumenter er sendt til OCR-tjenesten.;SVE=%1 dokument har skickats till OCR-service.';

    [Internal]
    PROCEDURE SendAllReadyToOcr@1();
    VAR
      IncomingDocument@1001 : Record 130;
      IncomingDocumentAttachment@1002 : Record 133;
      TempIncomingDocumentAttachment@1006 : TEMPORARY Record 133;
      OCRServiceMgt@1003 : Codeunit 1294;
      IncDocAttsReadyforOCR@1000 : Query 133;
      Window@1008 : Dialog;
      NoOfDocuments@1005 : Integer;
      i@1007 : Integer;
    BEGIN
      IF NOT IncDocAttsReadyforOCR.OPEN THEN
        EXIT;  // empty

      IF GUIALLOWED THEN
        Window.OPEN(SendMsg);

      // Find Document Count and lock records
      IncomingDocument.LOCKTABLE;
      IncomingDocumentAttachment.LOCKTABLE;
      WHILE IncDocAttsReadyforOCR.READ DO BEGIN
        NoOfDocuments += 1;
        IncomingDocumentAttachment.GET(IncDocAttsReadyforOCR.Incoming_Document_Entry_No,IncDocAttsReadyforOCR.Line_No);
        IncomingDocument.GET(IncomingDocumentAttachment."Incoming Document Entry No.");  // lock
        TempIncomingDocumentAttachment := IncomingDocumentAttachment;
        TempIncomingDocumentAttachment.INSERT;
      END;
      IncDocAttsReadyforOCR.CLOSE;
      // Release locks
      COMMIT;

      IF NoOfDocuments = 0 THEN
        EXIT;

      OCRServiceMgt.StartUpload(NoOfDocuments);

      TempIncomingDocumentAttachment.FINDSET;
      REPEAT
        IF GUIALLOWED THEN BEGIN
          i += 1;
          Window.UPDATE(1,10000 * i DIV NoOfDocuments);
        END;
        IncomingDocument.GET(TempIncomingDocumentAttachment."Incoming Document Entry No.");
        IncomingDocument.SendToOCR(FALSE);
      UNTIL TempIncomingDocumentAttachment.NEXT = 0;

      COMMIT;

      IF GUIALLOWED THEN BEGIN
        Window.CLOSE;
        MESSAGE(SendDoneMsg,NoOfDocuments);
      END;
    END;

    BEGIN
    END.
  }
}

