OBJECT Table 11071705 Service Contract Arrangement
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               VisitingScheme@1100526201 : Record 11071705;
             BEGIN
               IF ("Line No." = 0) OR VisitingScheme.GET("Service Contract No.", "Object No.", "Maintenance Scheme", "Line No.") THEN
                 "Line No." := GetNextLineNo();

               IF "Maintenance Year" = 0 THEN
                 FIELDERROR("Maintenance Year");

               CheckUniqueDWY;
               UpdateControlPeriods("Theoretical Maintenance Date");
             END;

    OnModify=BEGIN
               CheckModifyAllowed();
               CheckUniqueDWY;
               UpdateDatesOnContractObjMaint;
             END;

    OnDelete=VAR
               ServContrArrCommentLine@1100529600 : Record 11072560;
             BEGIN
               CheckModifyAllowed();

               ServContrArrCommentLine.SETRANGE("Service Contract No.", "Service Contract No.");
               ServContrArrCommentLine.SETRANGE("Object No.", "Object No.");
               ServContrArrCommentLine.SETRANGE("Maintenance Scheme", "Maintenance Scheme");
               ServContrArrCommentLine.SETRANGE("Serv. Contr. Arrang. Line No.", "Line No.");
               ServContrArrCommentLine.DELETEALL;
             END;

    CaptionML=[ENU=Service Contract Arrangement;
               NOR=Servicekontraktavtale;
               SVE=Servicekontraktsarrangemang];
    LookupPageID=Page11125309;
    DrillDownPageID=Page11125309;
  }
  FIELDS
  {
    { 10  ;   ;Service Contract No.;Code20        ;TableRelation=Service-Contract.No.;
                                                   CaptionML=[ENU=Service Contract No.;
                                                              NOR=Servicekontraktnr;
                                                              SVE=Servicekontraktnr];
                                                   Editable=No }
    { 20  ;   ;Object No.          ;Code20        ;TableRelation=Service-Object.No.;
                                                   OnValidate=VAR
                                                                ServiceObject@1100528600 : Record 11071691;
                                                              BEGIN
                                                                IF "Object No." <> '' THEN
                                                                  IF ServiceObject.GET("Object No.") THEN
                                                                    "Service Location No." := ServiceObject."Service Location No.";
                                                              END;

                                                   CaptionML=[ENU=Object No.;
                                                              NOR=Objektnr.;
                                                              SVE=Objektnr];
                                                   Editable=No }
    { 25  ;   ;Object Description  ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object.Description WHERE (No.=FIELD(Object No.)));
                                                   CaptionML=[ENU=Object Description;
                                                              NOR=Objektbeskrivelse;
                                                              SVE=Objektbeskrivning];
                                                   Editable=No }
    { 27  ;   ;Object Description 2;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object."Description 2" WHERE (No.=FIELD(Object No.)));
                                                   CaptionML=ENU=Object Description 2;
                                                   Editable=No }
    { 40  ;   ;Maintenance Scheme  ;Code20        ;TableRelation="Maintenance Scheme".Code;
                                                   CaptionML=[ENU=Maintenance Scheme;
                                                              NOR=Vedlikeholdsskjema;
                                                              SVE=UnderhÜllsschema];
                                                   Editable=No }
    { 45  ;   ;Maintenance Scheme Description;Text50;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Maintenance Scheme".Description WHERE (Code=FIELD(Maintenance Scheme)));
                                                   CaptionML=[ENU=Maintenance Scheme Description;
                                                              NOR=Beskrivelse av vedlikeholdskjema;
                                                              SVE=Beskrivning av underhÜllsschema];
                                                   Editable=No }
    { 50  ;   ;Service Package     ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Contract Object"."Service Package" WHERE (Service Contract No.=FIELD(Service Contract No.),
                                                                                                                 Service Object No.=FIELD(Object No.)));
                                                   CaptionML=[ENU=Service Package;
                                                              NOR=Servicepakke;
                                                              SVE=Servicepaket];
                                                   Editable=No }
    { 55  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 60  ;   ;Maintenance Year    ;Integer       ;OnValidate=BEGIN
                                                                IF "Maintenance Week" <> 0 THEN BEGIN
                                                                  CheckWeekNo("Maintenance Day", "Maintenance Week", "Maintenance Year");
                                                                  IF xRec."Maintenance Year" <> "Maintenance Year" THEN
                                                                    RecalcTheoreticalMaintDate();
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Maintenance Year;
                                                              NOR=VedlikeholdsÜr;
                                                              SVE=UnderhÜll, Ür] }
    { 70  ;   ;Maintenance Week    ;Integer       ;OnValidate=BEGIN
                                                                CheckWeekNo("Maintenance Day", "Maintenance Week", "Maintenance Year");

                                                                IF xRec."Maintenance Week" <> "Maintenance Week" THEN
                                                                  RecalcTheoreticalMaintDate();
                                                              END;

                                                   CaptionML=[ENU=Maintenance Week;
                                                              NOR=Vedlikeholdsuke;
                                                              SVE=UnderhÜll, vecka] }
    { 80  ;   ;Maintenance Day     ;Option        ;OnValidate=BEGIN
                                                                IF (xRec."Maintenance Day" <> "Maintenance Day") AND ("Maintenance Day" IN [1..7]) THEN
                                                                  RecalcTheoreticalMaintDate();
                                                              END;

                                                   CaptionML=[ENU=Maintenance Day;
                                                              NOR=Vedlikeholdsdag;
                                                              SVE=UnderhÂllsdag];
                                                   OptionCaptionML=[ENU=" ,Monday,Tuesday,Wednesday,Thursday,Friday,Saturday,Sunday,Weekday,Weekend";
                                                                    NOR=" ,Mandag,Tirsdag,Onsdag,Torsdag,Fredag,Lõrdag,Sõndag,Ukedag,Helg";
                                                                    SVE=" ,MÂndag,Tisdag,Onsdag,Torsdag,Fredag,Lˆrdag,Sˆndag,Veckodag,Helg"];
                                                   OptionString=[ ,Monday,Tuesday,Wednesday,Thursday,Friday,Saturday,Sunday,Weekday,Weekend] }
    { 90  ;   ;Planned Maintenance Date;Date      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Maintenance Proposal Line"."Starting Date Maintenance" WHERE (Service Contract No.=FIELD(Service Contract No.),
                                                                                                                                     Object No.=FIELD(Object No.),
                                                                                                                                     Maintenance Scheme=FIELD(Maintenance Scheme),
                                                                                                                                     Service Contract Arr. Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=Planned Maintenance Date;
                                                              NOR=Planlagt vedlikeholdsdato;
                                                              SVE=Planerat underhÜllsdatum];
                                                   Editable=No }
    { 100 ;   ;Service Order Starting Date;Date   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Order"."Starting Date" WHERE (No.=FIELD(Service Order No.)));
                                                   CaptionML=[ENU=Execute Maintenance Date;
                                                              NOR=Iverksett vedlikeholdsdato;
                                                              SVE=VerkstÑll underhÜllsdatum];
                                                   Editable=No }
    { 110 ;   ;Maintenance Interval;DateFormula   ;CaptionML=[ENU=Maintenance Interval;
                                                              NOR=Vedlikeholdsintervall;
                                                              SVE=UnderhÜllsintervall];
                                                   Editable=No }
    { 120 ;   ;Maintenance Proposal No.;Code20    ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Maintenance Proposal Line"."Maintenance Proposal" WHERE (Service Contract No.=FIELD(Service Contract No.),
                                                                                                                                Object No.=FIELD(Object No.),
                                                                                                                                Maintenance Scheme=FIELD(Maintenance Scheme),
                                                                                                                                Service Contract Arr. Line No.=FIELD(Line No.),
                                                                                                                                Expired=CONST(No)));
                                                   OnLookup=VAR
                                                              MaintenanceProposalConsObj@1100526200 : Record 11071695;
                                                            BEGIN
                                                              IF MaintenanceProposalConsObj.GET("Maintenance Proposal No.") THEN
                                                                PAGE.RUN(PAGE::"Maintenance Proposal List", MaintenanceProposalConsObj);
                                                            END;

                                                   CaptionML=[ENU=Maintenance Proposal;
                                                              NOR=Vedlikeholdsforslag;
                                                              SVE=UnderhÜllsfîrslag];
                                                   Editable=No }
    { 130 ;   ;Service Order No.   ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Order Object"."Service Order No." WHERE (Service Contract No.=FIELD(Service Contract No.),
                                                                                                                        Object No.=FIELD(Object No.),
                                                                                                                        Source=CONST(Maintenance),
                                                                                                                        Maintenance Scheme=FIELD(Maintenance Scheme),
                                                                                                                        Source Line No.=FIELD(Line No.)));
                                                   OnLookup=VAR
                                                              ServiceOrder@1100526200 : Record 11012823;
                                                            BEGIN
                                                              IF ServiceOrder.GET("Service Order No.") THEN
                                                                PAGE.RUN(PAGE::"Service-Order List", ServiceOrder);
                                                            END;

                                                   CaptionML=[ENU=Service Order No.;
                                                              NOR=Serviceordrenr.;
                                                              SVE=Serviceordernr];
                                                   Editable=No }
    { 140 ;   ;Firm Planned        ;Boolean       ;CaptionML=[ENU=Firm Planned;
                                                              NOR=Fast planlagt;
                                                              SVE=Fast planerad] }
    { 150 ;   ;Theoretical Maintenance Date;Date  ;CaptionML=[ENU=Theoretical Maintenance Date;
                                                              NOR=Teoretisk vedlikeholdsdato;
                                                              SVE=Teoretiskt underhÜllsdatum];
                                                   Editable=No }
    { 160 ;   ;Service Order Execution Date;Date  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Order Object"."Execution Date" WHERE (Service Contract No.=FIELD(Service Contract No.),
                                                                                                                     Object No.=FIELD(Object No.),
                                                                                                                     Maintenance Scheme=FIELD(Maintenance Scheme),
                                                                                                                     Source Line No.=FIELD(Line No.),
                                                                                                                     Source=CONST(Maintenance)));
                                                   CaptionML=[ENU=Service Order Execution Date;
                                                              NOR=Serviceordre iverksettingsdato;
                                                              SVE=ServiceorderverkstÑllandedatum];
                                                   Editable=No }
    { 170 ;   ;Finished            ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Order Object".Finished WHERE (Service Order No.=FIELD(Service Order No.),
                                                                                                             Service Contract No.=FIELD(Service Contract No.),
                                                                                                             Object No.=FIELD(Object No.),
                                                                                                             Source=CONST(Maintenance),
                                                                                                             Code=FIELD(Maintenance Scheme),
                                                                                                             Source Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=Finished;
                                                              NOR=Ferdig;
                                                              SVE=Avslutad];
                                                   Editable=No }
    { 180 ;   ;Service Order Status;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Order".Status WHERE (No.=FIELD(Service Order No.)));
                                                   CaptionML=[ENU=Service Order Status;
                                                              NOR=Serviceordrestatus;
                                                              SVE=Serviceorderstatus];
                                                   OptionCaptionML=[ENU=Open,,In Process,Technical Finished,Administrative Finished,Finished,Cancelled;
                                                                    NOR=èpne,,Under behandling,Teknisk ferdig,Administrativt ferdig,Ferdig,Avbrutt;
                                                                    SVE=ôppna,,Under behandling,Tekniskt fÑrdig,Administrativt fÑrdig,FÑrdig,Avbrutet];
                                                   OptionString=Open,,In Process,Technical Finished,Administrative Finished,Finished,Cancelled;
                                                   Editable=No }
    { 190 ;   ;Cluster             ;Code20        ;TableRelation="Service Cluster".Code WHERE (Type=FILTER(' '|Contract));
                                                   OnValidate=BEGIN
                                                                IF xRec.Cluster <> Cluster THEN
                                                                  "Firm Planned" := TRUE;
                                                                ValidateServiceCluster;
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupServiceCluster;
                                                            END;

                                                   CaptionML=[ENU=Cluster;
                                                              NOR=Klynge;
                                                              SVE=Kluster] }
    { 200 ;   ;Maintenance Bandwidth;DateFormula  ;CaptionML=[ENU=Maintenance Bandwidth;
                                                              NOR=Vedlikehold, bÜndbredde;
                                                              SVE=BandbreddsunderhÜll];
                                                   Editable=No }
    { 210 ;   ;Starting Time Maint. Window;Time   ;OnValidate=BEGIN
                                                                TestMaintenanceTimes;
                                                              END;

                                                   CaptionML=[ENU=Starting Time Maintenance Window;
                                                              NOR=Vinduet Startid for Vedlikehold;
                                                              SVE=UnderhÜllsfînster starttid] }
    { 220 ;   ;Ending Time Maint. Window;Time     ;OnValidate=BEGIN
                                                                TestMaintenanceTimes;
                                                              END;

                                                   CaptionML=[ENU=Ending Time Maintenance Window;
                                                              NOR=Vinduet Sluttid for Vedlikehold;
                                                              SVE=Fînstret sluttid fîr underhÜll] }
    { 230 ;   ;Service Location No.;Code20        ;TableRelation="Service Location";
                                                   CaptionML=[ENU=Service Location No.;
                                                              NOR=Servicelokasjon Nr.;
                                                              SVE=Serviceplatsnr];
                                                   Editable=No }
    { 240 ;   ;Service Location Description;Text50;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location".Description WHERE (No.=FIELD(Service Location No.)));
                                                   CaptionML=[ENU=Service Location Description;
                                                              NOR=Servicelokasjonbeskrivelse;
                                                              SVE=Serviceplatsbeskrivning];
                                                   Editable=No }
    { 250 ;   ;Address             ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location".Address WHERE (No.=FIELD(Service Location No.)));
                                                   CaptionML=[ENU=Address;
                                                              NOR=Adresse;
                                                              SVE=Adress];
                                                   Editable=No }
    { 260 ;   ;Address 2           ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location"."Address 2" WHERE (No.=FIELD(Service Location No.)));
                                                   CaptionML=[ENU=Address 2;
                                                              NOR=Adresse 2;
                                                              SVE=Adress 2];
                                                   Editable=No }
    { 270 ;   ;City                ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location".City WHERE (No.=FIELD(Service Location No.)));
                                                   CaptionML=[ENU=City;
                                                              NOR=Sted;
                                                              SVE=Ort];
                                                   Editable=No }
    { 280 ;   ;County              ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location".County WHERE (No.=FIELD(Service Location No.)));
                                                   CaptionML=[ENU=County;
                                                              NOR=Delstat;
                                                              SVE=Delstat];
                                                   Editable=No }
    { 290 ;   ;Post Code           ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location"."Post Code" WHERE (No.=FIELD(Service Location No.)));
                                                   TableRelation="Post Code";
                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Post Code;
                                                              NOR=Postnr;
                                                              SVE=Postnr];
                                                   Editable=No }
    { 300 ;   ;Comment OLD         ;Text50        ;CaptionML=[ENU=Comment;
                                                              NOR=Merknad;
                                                              SVE=Kommentar] }
    { 310 ;   ;Cancelled           ;Boolean       ;OnValidate=BEGIN
                                                                IF Cancelled THEN BEGIN
                                                                  CALCFIELDS("Maintenance Proposal No.", "Service Order No.");
                                                                  IF "Maintenance Proposal No." <> '' THEN
                                                                    ERROR(Text001);
                                                                  IF "Service Order No." <> '' THEN
                                                                    ERROR(Text002);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Cancelled;
                                                              NOR=Annullert;
                                                              SVE=Avbruten] }
    { 320 ;   ;Discipline          ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Maintenance Scheme".Discipline WHERE (Code=FIELD(Maintenance Scheme)));
                                                   TableRelation=Discipline;
                                                   CaptionML=[ENU=Discipline;
                                                              NOR=Disiplin;
                                                              SVE=Disciplin];
                                                   Editable=No }
    { 330 ;   ;Trade Speciality    ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Maintenance Scheme"."Trade Speciality" WHERE (Code=FIELD(Maintenance Scheme)));
                                                   TableRelation="Trade Speciality".Code WHERE (Discipline=FIELD(Discipline));
                                                   CaptionML=[ENU=Trade Speciality;
                                                              NOR=SNI-nummer;
                                                              SVE=SNI-nummer];
                                                   Editable=No }
    { 340 ;   ;Service Category    ;Code10        ;TableRelation="Service Category".Code WHERE (Service Contract No.=FIELD(Service Contract No.));
                                                   CaptionML=[ENU=Service Category;
                                                              SVE=Servicekategori] }
    { 350 ;   ;Object Brand Code   ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object."Brand Code" WHERE (No.=FIELD(Object No.)));
                                                   CaptionML=ENU=Object Brand Code;
                                                   Editable=No }
    { 360 ;   ;Object Model Code   ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object."Model Code" WHERE (No.=FIELD(Object No.)));
                                                   CaptionML=ENU=Object Model Code;
                                                   Editable=No }
    { 370 ;   ;Object Type Code    ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object."Type Code" WHERE (No.=FIELD(Object No.)));
                                                   CaptionML=ENU=Object Type Code;
                                                   Editable=No }
    { 380 ;   ;Comment Line Present;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Serv. Contr. Arr. Comment Line" WHERE (Service Contract No.=FIELD(Service Contract No.),
                                                                                                             Object No.=FIELD(Object No.),
                                                                                                             Maintenance Scheme=FIELD(Maintenance Scheme),
                                                                                                             Serv. Contr. Arrang. Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=Comment Line Present;
                                                              SVE=Kommentarsrad finns];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Service Contract No.,Object No.,Maintenance Scheme,Line No.;
                                                   Clustered=Yes }
    {    ;Service Contract No.,Object No.,Maintenance Scheme,Maintenance Year,Maintenance Week,Maintenance Day }
    {    ;Service Contract No.,Maintenance Year,Maintenance Week,Maintenance Day }
    {    ;Service Contract No.,Object No.,Maintenance Scheme,Theoretical Maintenance Date }
    {    ;Object No.                               }
    {    ;Service Location No.,Maintenance Year,Maintenance Week,Maintenance Day }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100526200 : TextConst 'ENU=Invalid Week No.;NOR=Ugyldig ukenr.;SVE=Ogiltigt veckonr';
      Text001@1100526201 : TextConst 'ENU=Maintenance Proposal already created, modification prohibited.;NOR=Vedlikeholdsforslag er allerede opprettet, endring ikke mulig.;SVE=UnderhÜllsfîrslaget har redan skapats, Ñndring fîrbjuden.';
      Text002@1100526202 : TextConst 'ENU=Service Order No. already created, modification prohibited.;NOR=Serviceordrenr. allerede opprettet, endring ikke mulig;SVE=Serviceordernr har redan skapats, Ñndring fîrbjuden.';
      Text003@1100526203 : TextConst 'ENU=Contract Arrangement Line already exists;NOR=Kontraktavtalerad finnes allerede;SVE=Rad fîr kontraktsarrangemang finns redan';
      Text004@1100528600 : TextConst 'ENU=Must occur after  %1;NOR=mÜ forekomme etter %1;SVE=MÜste intrÑffa efter %1';

    PROCEDURE GetMaintenanceDate@1100526200() : Date;
    VAR
      Day@1100526200 : Integer;
      MaintenanceDate@1100526202 : Date;
      MaintenanceDay@1100526201 : Option;
    BEGIN
      IF "Theoretical Maintenance Date" = 0D THEN
        EXIT(0D);
      Day := DATE2DWY("Theoretical Maintenance Date", 1);
      MaintenanceDate := "Theoretical Maintenance Date";
      CASE "Maintenance Day" OF
        "Maintenance Day"::Monday.."Maintenance Day"::Sunday:
          MaintenanceDay := "Maintenance Day";
        "Maintenance Day"::Weekday:
          BEGIN
            IF Day = 6 THEN
              MaintenanceDay := 5;
            IF Day = 7 THEN BEGIN
              MaintenanceDay := 1;
              MaintenanceDate := CALCDATE('<+1W>', MaintenanceDate);
            END;
          END;
        "Maintenance Day"::Weekend:
          BEGIN
            IF Day IN [1..3] THEN BEGIN
              MaintenanceDate := CALCDATE('<-1W>', MaintenanceDate);
              MaintenanceDay := 7;
            END;
            IF Day IN [4..5] THEN BEGIN
              MaintenanceDay := 6;
            END;
          END;
      END;
      IF MaintenanceDay <> "Maintenance Day"::" " THEN BEGIN
      //  Week := DATE2DWY(MaintenanceDate, 2);
      //  Year := DATE2DWY(MaintenanceDate, 3);
        MaintenanceDate := DWY2DATE(MaintenanceDay, "Maintenance Week", "Maintenance Year");
      END;
      EXIT(MaintenanceDate);
    END;

    PROCEDURE GetNextLineNo@1100526201() : Integer;
    VAR
      VisitingScheme@1100526200 : Record 11071705;
    BEGIN
      VisitingScheme.SETRANGE("Service Contract No.", "Service Contract No.");
      VisitingScheme.SETRANGE("Object No.", "Object No.");
      VisitingScheme.SETRANGE("Maintenance Scheme", "Maintenance Scheme");
      IF VisitingScheme.FINDLAST THEN
        EXIT(VisitingScheme."Line No." + 10000)
      ELSE
        EXIT(10000);
    END;

    PROCEDURE CheckWeekNo@1100526203(IDay@1100526202 : Integer;IWeek@1100526208 : Integer;IYear@1100526207 : Integer);
    VAR
      lvDate@1100526203 : Date;
      lvYear@1100526204 : Integer;
      lvWeek@1100526205 : Integer;
    BEGIN
      IF IWeek = 0 THEN
        ERROR(Text000);
      IF NOT (IDay IN [1..7]) THEN
        IDay := 1;
      lvDate := DWY2DATE(IDay, IWeek, IYear);
      lvWeek := DATE2DWY(lvDate, 2);
      lvYear := DATE2DWY(lvDate, 3);
      IF (lvWeek <> IWeek) OR (lvYear <> IYear) THEN
        ERROR(Text000);
    END;

    PROCEDURE CheckModifyAllowed@1100526205();
    BEGIN
      CALCFIELDS("Maintenance Proposal No.", "Service Order No.");
      IF "Maintenance Proposal No." <> '' THEN
        ERROR(Text001);
      IF "Service Order No." <> '' THEN
        ERROR(Text002);
    END;

    PROCEDURE CheckUniqueDWY@1100526202();
    VAR
      ServiceContractArrangement@1100526203 : Record 11071705;
    BEGIN
      ServiceContractArrangement.SETCURRENTKEY("Service Contract No.", "Object No.", "Maintenance Scheme", "Maintenance Year", "Maintenance Week", "Maintenance Day");
      ServiceContractArrangement.SETRANGE("Service Contract No.", "Service Contract No.");
      ServiceContractArrangement.SETRANGE("Object No.", "Object No.");
      ServiceContractArrangement.SETRANGE("Maintenance Scheme", "Maintenance Scheme");
      ServiceContractArrangement.SETRANGE("Maintenance Year", "Maintenance Year");
      ServiceContractArrangement.SETRANGE("Maintenance Week", "Maintenance Week");
      ServiceContractArrangement.SETRANGE("Maintenance Day", "Maintenance Day");
      ServiceContractArrangement.SETFILTER("Line No.", '<>%1', "Line No.");
      IF ServiceContractArrangement.FINDFIRST THEN
        FIELDERROR("Maintenance Week", Text003);
    END;

    PROCEDURE IsUniqueDWY@1100528606() : Boolean;
    VAR
      ServiceContractArrangement@1100526203 : Record 11071705;
    BEGIN
      ServiceContractArrangement.SETCURRENTKEY("Service Contract No.", "Object No.", "Maintenance Scheme", "Maintenance Year", "Maintenance Week", "Maintenance Day");
      ServiceContractArrangement.SETRANGE("Service Contract No.", "Service Contract No.");
      ServiceContractArrangement.SETRANGE("Object No.", "Object No.");
      ServiceContractArrangement.SETRANGE("Maintenance Scheme", "Maintenance Scheme");
      ServiceContractArrangement.SETRANGE("Maintenance Year", "Maintenance Year");
      ServiceContractArrangement.SETRANGE("Maintenance Week", "Maintenance Week");
      ServiceContractArrangement.SETRANGE("Maintenance Day", "Maintenance Day");
      ServiceContractArrangement.SETFILTER("Line No.", '<>%1', "Line No.");
      EXIT(ServiceContractArrangement.ISEMPTY);
    END;

    PROCEDURE RecalcTheoreticalMaintDate@1100526204();
    BEGIN
      "Firm Planned" := TRUE;
      IF "Maintenance Day" IN ["Maintenance Day"::Monday.."Maintenance Day"::Sunday] THEN
        "Theoretical Maintenance Date" := DWY2DATE("Maintenance Day", "Maintenance Week", "Maintenance Year")
      ELSE
        "Theoretical Maintenance Date" := DWY2DATE(1, "Maintenance Week", "Maintenance Year");
    END;

    PROCEDURE UpdateControlPeriods@1100528600(IUntil@1100528600 : Date);
    VAR
      ContractControlPeriod@1100528601 : Record 11071746;
    BEGIN
      ContractControlPeriod.UpdateControlPeriodsUntil("Service Contract No.", IUntil, FALSE);
    END;

    LOCAL PROCEDURE TestMaintenanceTimes@1100528602();
    BEGIN
      IF ("Starting Time Maint. Window" = 0T) OR ("Ending Time Maint. Window" = 0T) THEN
        EXIT;
      IF "Ending Time Maint. Window" <= "Starting Time Maint. Window" THEN
        FIELDERROR("Ending Time Maint. Window", STRSUBSTNO(Text004, FIELDCAPTION("Starting Time Maint. Window")));
    END;

    PROCEDURE ValidateServiceCluster@1100528603();
    VAR
      ServiceContract@1100528601 : Record 11012812;
      ServiceCluster@1100528600 : Record 11071742;
    BEGIN
      IF Cluster <> '' THEN BEGIN
        ServiceContract.GET("Service Contract No.");
        IF ServiceContract."Use Contract Clusters" THEN
          ServiceCluster.GET(ServiceCluster.Type::Contract, "Service Contract No.", ServiceCluster."Source Type"::" " , Cluster)
        ELSE
          ServiceCluster.GET(ServiceCluster.Type::" ", '', ServiceCluster."Source Type"::" ", Cluster);
      END;
    END;

    PROCEDURE LookupServiceCluster@1100528601();
    VAR
      ServiceContract@1100528600 : Record 11012812;
      ServiceCluster@1100528601 : Record 11071742;
    BEGIN
      ServiceContract.GET("Service Contract No.");
      IF ServiceContract."Use Contract Clusters" THEN BEGIN
        ServiceCluster.SETRANGE(Type, ServiceCluster.Type::Contract);
        ServiceCluster.SETRANGE("Type Code", "Service Contract No.");
        IF Cluster <> '' THEN
          ServiceCluster.GET(ServiceCluster.Type::Contract, "Service Contract No.", ServiceCluster."Source Type"::" " , Cluster);
      END ELSE BEGIN
        ServiceCluster.SETRANGE(Type, ServiceCluster.Type::" ");
        ServiceCluster.SETRANGE("Type Code", '');
        IF Cluster <> '' THEN
          ServiceCluster.GET(ServiceCluster.Type::" ", '', ServiceCluster."Source Type"::" " , Cluster);
      END;
      IF PAGE.RUNMODAL(0, ServiceCluster) = ACTION::LookupOK THEN
        VALIDATE(Cluster, ServiceCluster.Code);
    END;

    PROCEDURE IsMaintPropLineExpired@1100528604() : Boolean;
    VAR
      MaintenanceProposalLine@1100528600 : Record 11071696;
    BEGIN
      CALCFIELDS("Maintenance Proposal No.");
      MaintenanceProposalLine.SETRANGE("Maintenance Proposal", "Maintenance Proposal No.");
      MaintenanceProposalLine.SETRANGE("Service Contract No.", "Service Contract No.");
      MaintenanceProposalLine.SETRANGE("Object No.", "Object No.");
      MaintenanceProposalLine.SETRANGE("Maintenance Scheme", "Maintenance Scheme");
      MaintenanceProposalLine.SETRANGE("Service Contract Arr. Line No.", "Line No.");
      IF MaintenanceProposalLine.FINDFIRST THEN
        EXIT(MaintenanceProposalLine.Expired);
    END;

    PROCEDURE GetPrefServiceLocationNo@1100528301(IWithErrorCheck@1100528300 : Boolean) : Code[20];
    VAR
      ServiceObject@1100528301 : Record 11071691;
    BEGIN
      IF ServiceObject.GET("Object No.") THEN
        EXIT(ServiceObject.GetPrefServiceLocationNo(IWithErrorCheck));
    END;

    PROCEDURE GetPreferredBuildingCode@1100528605() : Code[20];
    VAR
      ServiceObject@1100528600 : Record 11071691;
    BEGIN
      IF ServiceObject.GET("Object No.") THEN
        EXIT(ServiceObject.GetPreferredBuildingCode);
    END;

    PROCEDURE GetDisciplineTradeSpeciality@1100528636(VAR ODiscipline@1100528604 : Code[10];VAR OTradeSpeciality@1100528605 : Code[10]);
    VAR
      ServiceOrder@1100528601 : Record 11012823;
      MaintenanceProposalLine@1100528602 : Record 11071696;
      MaintenanceManagement@1100528600 : Codeunit 11012829;
    BEGIN
      CALCFIELDS("Service Order No.", "Maintenance Proposal No.");
      CASE TRUE OF
        "Service Order No." <> '':
          BEGIN
            IF ServiceOrder.GET("Service Order No.") THEN BEGIN
              ODiscipline := ServiceOrder.Discipline;
              OTradeSpeciality := ServiceOrder."Trade Speciality";
            END;
          END;
        "Maintenance Proposal No." <> '':
          BEGIN
            MaintenanceProposalLine.SETCURRENTKEY("Service Contract No.", "Object No.");
            MaintenanceProposalLine.SETRANGE("Service Contract No.", "Service Contract No.");
            MaintenanceProposalLine.SETRANGE("Object No.", "Object No.");
            MaintenanceProposalLine.SETRANGE("Maintenance Scheme", "Maintenance Scheme");
            MaintenanceProposalLine.SETRANGE("Service Contract Arr. Line No.", "Line No.");
            IF MaintenanceProposalLine.FINDFIRST THEN BEGIN
              ODiscipline := MaintenanceProposalLine.Discipline;
              OTradeSpeciality := MaintenanceProposalLine."Trade Speciality";
            END;
          END;
      ELSE
        MaintenanceManagement.DetermineDisciplineAndTradeSpeciality("Object No.", "Maintenance Scheme", ODiscipline, OTradeSpeciality);
      END;
    END;

    LOCAL PROCEDURE UpdateDatesOnContractObjMaint@1100528607();
    VAR
      ContractObjectMaintenance@1100528900 : Record 11071692;
    BEGIN
      IF ContractObjectMaintenance.GET("Service Contract No.", "Object No.", "Maintenance Scheme") THEN BEGIN
        IF MODIFY THEN;
        ContractObjectMaintenance.UpdateLastAndNextMaintDate;
        ContractObjectMaintenance.MODIFY;
      END;
    END;

    PROCEDURE EditCommentLines@1100529601();
    VAR
      ServContrArrCommentLine@1100529600 : Record 11072560;
    BEGIN
      ServContrArrCommentLine.FILTERGROUP(2);
      ServContrArrCommentLine.SETRANGE("Service Contract No.", "Service Contract No.");
      ServContrArrCommentLine.SETRANGE("Object No.", "Object No.");
      ServContrArrCommentLine.SETRANGE("Maintenance Scheme", "Maintenance Scheme");
      ServContrArrCommentLine.SETRANGE("Serv. Contr. Arrang. Line No.", "Line No.");
      ServContrArrCommentLine.FILTERGROUP(0);
      PAGE.RUNMODAL(0, ServContrArrCommentLine);
    END;

    PROCEDURE CopyCancelCommentLines@1100529602(VAR SourceServContrArrCommentLine@1100529601 : Record 11072560);
    VAR
      NewServContrArrCommentLine@1100529600 : Record 11072560;
      LastLineNo@1100529602 : Integer;
    BEGIN
      IF NOT SourceServContrArrCommentLine.ISTEMPORARY THEN
        EXIT;
      IF SourceServContrArrCommentLine.ISEMPTY THEN
        EXIT;

      NewServContrArrCommentLine.SETRANGE("Service Contract No.", "Service Contract No.");
      NewServContrArrCommentLine.SETRANGE("Object No.", "Object No.");
      NewServContrArrCommentLine.SETRANGE("Maintenance Scheme", "Maintenance Scheme");
      NewServContrArrCommentLine.SETRANGE("Serv. Contr. Arrang. Line No.", "Line No.");
      IF NewServContrArrCommentLine.FINDLAST THEN
        LastLineNo := NewServContrArrCommentLine."Line No.";

      SourceServContrArrCommentLine.FINDSET;
      REPEAT
        LastLineNo += 10000;
        NewServContrArrCommentLine := SourceServContrArrCommentLine;
        NewServContrArrCommentLine."Service Contract No." := "Service Contract No.";
        NewServContrArrCommentLine."Object No." := "Object No.";
        NewServContrArrCommentLine."Maintenance Scheme" := "Maintenance Scheme";
        NewServContrArrCommentLine."Serv. Contr. Arrang. Line No." := "Line No.";
        NewServContrArrCommentLine."Line No." := LastLineNo;
        NewServContrArrCommentLine.INSERT;
      UNTIL SourceServContrArrCommentLine.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

