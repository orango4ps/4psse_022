OBJECT Table 11072288 Service Loc. Contact Debrief
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnModify=BEGIN
               TESTFIELD("Processed Date", 0D);
             END;

    OnDelete=BEGIN
               TESTFIELD("Processed Date", 0D);
             END;

    CaptionML=ENU=Service Location Contact Debrief;
    LookupPageID=Page11229430;
    DrillDownPageID=Page11229430;
  }
  FIELDS
  {
    { 10  ;   ;Service Order No.   ;Code20        ;TableRelation="Service Order";
                                                   CaptionML=[ENU=Service Order No.;
                                                              NOR=Serviceordrenr.;
                                                              SVE=Serviceordernr];
                                                   Editable=No }
    { 20  ;   ;Table Type          ;Option        ;OnValidate=BEGIN
                                                                CASE "Table Type" OF
                                                                  "Table Type"::"Service Location":
                                                                    "Table No." := DATABASE::"Service Location";
                                                                END;
                                                                VALIDATE("Field No.", 0);
                                                              END;

                                                   CaptionML=[ENU=Table Type;
                                                              NOR=Tabelltype;
                                                              SVE=Tabelltyp];
                                                   OptionCaptionML=[ENU=Service Location;
                                                                    NOR=Servicelokasjon;
                                                                    SVE=Serviceplats];
                                                   OptionString=Service Location;
                                                   Editable=No }
    { 30  ;   ;Table No.           ;Integer       ;InitValue=11012802;
                                                   CaptionML=[ENU=Table No.;
                                                              NOR=Tabellnr.;
                                                              SVE=Tabellnr];
                                                   Editable=No }
    { 40  ;   ;Field No.           ;Integer       ;CaptionML=[ENU=Field No.;
                                                              NOR=Feltnr.;
                                                              SVE=F„ltnr];
                                                   Editable=No }
    { 50  ;   ;Old Value           ;Text80        ;CaptionML=[ENU=Old Value;
                                                              NOR=Gammel verdi;
                                                              SVE=Gammalt v„rde];
                                                   Editable=No }
    { 60  ;   ;New Value           ;Text80        ;OnValidate=BEGIN
                                                                EvaluateValue;
                                                              END;

                                                   CaptionML=[ENU=New Value;
                                                              NOR=Ny verdi;
                                                              SVE=Nytt v„rde] }
    { 70  ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=[ENU=" ,Received,Approved,Disapproved";
                                                                    NOR=" ,Mottatt,Godkjent,Ikke godkjent";
                                                                    SVE=" ,Mottaget,Godk„nt,Inte godk„nt"];
                                                   OptionString=[ ,Received,Approved,Disapproved] }
    { 80  ;   ;Processed Date      ;Date          ;CaptionML=[ENU=Processed Date;
                                                              NOR=Behandlingsdato;
                                                              SVE=Bearbetad den];
                                                   Editable=No }
    { 90  ;   ;Field Name          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field.FieldName WHERE (TableNo=FIELD(Table No.),
                                                                                             No.=FIELD(Field No.)));
                                                   CaptionML=[ENU=Field Name;
                                                              NOR=Feltnavn;
                                                              SVE=F„ltnamn];
                                                   Editable=No }
    { 110 ;   ;Field Caption       ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Table No.),
                                                                                                   No.=FIELD(Field No.)));
                                                   CaptionML=[ENU=Field Caption;
                                                              NOR=Felttekst;
                                                              SVE=F„ltrubrik];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Service Order No.,Table No.,Field No.   ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100528600 : TextConst 'ENU=Processed Location Contact Debrief Lines cannot change Status';

    PROCEDURE EvaluateValue@1100528603();
    VAR
      RecRef@1100528600 : RecordRef;
      FieldRef@1100528601 : FieldRef;
    BEGIN
      IF "New Value" = '' THEN EXIT;

      RecRef.OPEN("Table No.");
      FieldRef := RecRef.FIELD("Field No.");
      FieldRef.VALUE := "New Value";
      "New Value" := FieldRef.VALUE;
    END;

    PROCEDURE ProcessContactDebriefLines@1100528601(VAR IServiceLocContactDebrief@1100528600 : Record 11072288);
    VAR
      ServiceLocContactDebrief@1100528601 : Record 11072288;
      ServiceLocation@1100528602 : Record 11012801;
      ServiceOrder@1100528603 : Record 11012823;
    BEGIN
      ServiceLocContactDebrief.COPY(IServiceLocContactDebrief);
      ServiceLocContactDebrief.SETRANGE(Status, ServiceLocContactDebrief.Status::Approved);
      ServiceLocContactDebrief.SETRANGE("Processed Date", 0D);
      IF ServiceLocContactDebrief.FINDSET(TRUE) THEN
        REPEAT
          ServiceOrder.GET(ServiceLocContactDebrief."Service Order No.");
          CASE ServiceLocContactDebrief."Table Type" OF
            ServiceLocContactDebrief."Table Type"::"Service Location":
              BEGIN
                IF ServiceLocation.GET(ServiceOrder."Service Location No.") THEN BEGIN
                  CASE ServiceLocContactDebrief."Field No." OF
                    ServiceLocation.FIELDNO("Phone No."):
                      ServiceLocation."Phone No." := ServiceLocContactDebrief."New Value";
                    ServiceLocation.FIELDNO("Phone No. 2"):
                      ServiceLocation."Phone No. 2" := ServiceLocContactDebrief."New Value";
                    ServiceLocation.FIELDNO("Mobile Phone No."):
                      ServiceLocation."Mobile Phone No." := ServiceLocContactDebrief."New Value";
                    ServiceLocation.FIELDNO("E-Mail"):
                      ServiceLocation."E-Mail" := ServiceLocContactDebrief."New Value";
                  END;
                  ServiceLocation.MODIFY(TRUE);
                END;
             END;
          END;
          ServiceLocContactDebrief."Processed Date" := TODAY;
          ServiceLocContactDebrief.MODIFY;
        UNTIL ServiceLocContactDebrief.NEXT = 0;
    END;

    PROCEDURE SetStatusCollectively@1100528605(VAR IOServiceLocContactDebrief@1100528601 : Record 11072288;INewStatus@1100528600 : ' ,Received,Approved,Disapproved');
    VAR
      TotalNoOfSpecLines@1100528602 : Integer;
    BEGIN
      TotalNoOfSpecLines := IOServiceLocContactDebrief.COUNT;
      IOServiceLocContactDebrief.SETRANGE("Processed Date", 0D);
      IF TotalNoOfSpecLines <> IOServiceLocContactDebrief.COUNT THEN
        ERROR(Text001);
      IF IOServiceLocContactDebrief.FINDSET THEN
        REPEAT
          IOServiceLocContactDebrief.VALIDATE(Status, INewStatus);
          IOServiceLocContactDebrief.MODIFY(TRUE);
        UNTIL IOServiceLocContactDebrief.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

