OBJECT Codeunit 11012940 RentalUnitInvoicing
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text001@1100529003 : TextConst 'DEU=%1 ''%2'' unbekannt;ENU=%1 ''%2'' unknown;NLD=%1 ''%2'' onbekend';
      Text002@1100529004 : TextConst 'DEU=%1: %2 nicht ausgefllt;ENU=%1: %2 not filled;NLD=%1: %2 niet gevuld';
      Text003@1100529005 : TextConst 'DEU=Felder von %1 ''%2'' nicht ausgefllt:;ENU=Fields of %1 ''%2'' not filled:;NLD=Veld(en) van %1 ''%2'' niet gevuld:';
      Text004@1100529002 : TextConst 'DEU=%1 muss in Projekt oder Projektauftraggeber (%2-%3) ausgefllt werden.;ENU=%1 must be filled on project or project-principal (%2-%3).;NLD=%1 moet worden ingeven bij project of project-opdrachtgever (%2-%3).;NOR=%1 m† fylles ut for prosjektet eller prosjektets oppdragsgiver (%2-%3).;SVE=%1 m†ste fyllas i f”r projektet eller projektets uppdragsgivare (%2-%3).';
      Text005@1100529006 : TextConst 'DEU=Zeile(n) mit %1 aktuell nicht ausgefllt;ENU=Line(s) with %1 not filled present;NLD="Regel(s) met %1 niet gevuld aanwezig "';
      Text006@1100529007 : TextConst 'DEU=Zeile(n) mit %1 aktuell leer, %2 aber Standard-%1 kann nicht festgelegt werden;ENU=Line(s) with %1 empty present, %2 but Default %1 can not be determined;NLD=Regel(s) met %1 leeg aanwezig, %2 maar Default %1 kan niet bepaald worden';
      Text007@1100529001 : TextConst 'DEU=%1, muss auf nicht bearbeitbar eingestellt werden, bevor fakturiert werden kann;ENU=%1, must be set to not editable before it can be invoiced;NLD=%1, moet op niet muteerbaar gezet worden voordat gefactureerd kan worden';
      Text008@1100529000 : TextConst 'DEU=%1 ''%2'' hat Status ''%3'', muss zuerst gebucht werden;ENU=%1 ''%2'' has status ''%3'', this must be posted first;NLD=%1 ''%2'' heeft status ''%3'', deze moet eerst geboekt worden';
      Text012@1100529011 : TextConst 'DEU=%1: %2 vor Ende der Rechnungsperiode (%3) aber %4 nicht ausgefllt, zuerst beenden oder verl„ngern;ENU=%1: %2 before end of invoice period (%3) but %4 not filled, first terminate or prolong;NLD=%1: %2 voor einde van factuurperiode (%3) maar %4 niet gevuld, eerst opzeggen of verlengen';
      Text013@1100529012 : TextConst 'DEU=Zeile(n);ENU=Line(s);NLD=Regel(s)';
      Text014@1100529013 : TextConst 'DEU=Fakturierung an ''%1'' nicht zul„ssig, indizieren Sie zuerst: %2 ''%3'';ENU=Invoicing to ''%1'' not allowed, first index: %2 ''%3'';NLD=Factureren t/m ''%1'' niet toegestaan, eerst indexeren: %2 ''%3''';
      Text014b@1100529017 : TextConst 'DEU=(Fakturierung mit versp„teter Indexierung zul„ssig an ''%1'');ENU=(invoicing with delayed indexation allowed to ''%1'');NLD=(factureren met uitgestelde indexering toegestaan t/m ''%1'')';
      Text015@1100529015 : TextConst 'DEU=Keine Rechnungen erstellt.;ENU=No Invoices created.;NLD=Er zijn geen facturen aangemaakt;NOR=Ingen fakturaer er opprettet.;SVE=Inga fakturor har skapats.';
      Text016@1100529014 : TextConst 'DEU=%1 Rechnungen, %2 Gutschriften und %3 Rechnungszeilen erstellt.;ENU=%1 Invoices, %2 Credit Memos and %3 Invoice Lines created.;NLD=Er zijn %1 facturen, %2 creditnota''s en %3 factuurregels aangemaakt;NOR=%1 fakturaer, %2 kreditnotaer og %3 fakturarader er opprettet.;SVE=%1 fakturor, %2 kreditnotor och %3 fakturarader har skapats.';
      Text017@1100529016 : TextConst 'DEU=Kein Tarif gefunden vor %1 %2: %3 %4, %5 %6;ENU=No Rate found before %1 %2: %3 %4, %5 %6;NLD=Geen tarief gevonden voor %1 %2: %3 %4, %5 %6';
      Text018@1100529018 : TextConst 'DEU=N„chster Monat, voriger Monat;ENU=Next month,Previous month;NLD=Volgende maand,Vorige maand';
      Text019@1100529019 : TextConst 'DEU=N„chste Woche, vorige Woche;ENU=Next week,Previous week;NLD=Volgende week,Vorige week';
      ProjSetup@1100529020 : Record 315;
      TmpInvoicePeriods@1100529021 : TEMPORARY Record 2000000007;
      Text020@1100529022 : TextConst 'DEU=Rechnungsperiode bis ''%1'' zu weit in der Zukunft, Periodenanzahl kann nicht bestimmt werden;ENU=Invoice period until ''%1'' too far in future, number of periods can not be determined;NLD=Factuurperiode t/m ''%1'' te ver in toekomst, aantal periodes kan niet bepaald worden';
      Text021@1100529400 : TextConst 'DEU=%1 ist ''%2'', Erstellung Rechnung nicht zul„ssig;ENU=%1 is ''%2'', create invoice not allowed;NLD=%1 is ''%2'', aanmaken factuur niet toegestaan';

    PROCEDURE DeterminePeriodDateFromPeriodNo@1100529008(PeriodYear@1100529001 : Integer;PeriodNo@1100529000 : Integer;VAR PeriodDate@1100529003 : Date);
    VAR
      Period@1100529004 : Record 2000000007;
    BEGIN
      IF (PeriodYear = 0) OR (PeriodNo = 0) THEN BEGIN
        PeriodDate := 0D;
        EXIT;
      END;
      ProjSetup.GET;
      Period.GET(Period."Period Type"::Month, DMY2DATE(1, PeriodNo, PeriodYear));
      IF (ProjSetup."Invoice Period Rental Units" = ProjSetup."Invoice Period Rental Units"::Date) AND
         (PeriodDate >= Period."Period Start") AND (PeriodDate <= Period."Period End")
      THEN
        EXIT;
      PeriodDate := NORMALDATE(Period."Period End");  //Last date of month
    END;

    PROCEDURE DeterminePeriodNoFromPeriodDate@1100529007(VAR PeriodDate@1100529003 : Date;VAR PeriodYear@1100529001 : Integer;VAR PeriodNo@1100529000 : Integer);
    VAR
      Period@1100529004 : Record 2000000007;
    BEGIN
      IF PeriodDate = 0D THEN BEGIN
        PeriodNo := 0;  //Only periodNo, so not year
        EXIT;
      END;
      ProjSetup.GET;
      PeriodYear := DATE2DMY(PeriodDate, 3);
      PeriodNo := DATE2DMY(PeriodDate, 2);
      IF ProjSetup."Invoice Period Rental Units" = ProjSetup."Invoice Period Rental Units"::Calendar THEN BEGIN
        Period.GET(Period."Period Type"::Month, DMY2DATE(1, PeriodNo, PeriodYear));
        PeriodDate := NORMALDATE(Period."Period End");  //Must be last date of month
      END;
    END;

    PROCEDURE PeriodSelectionShift@1100529006(CurrField@1100529004 : Code[20];VAR PeriodYear@1100529001 : Integer;VAR PeriodNo@1100529000 : Integer;VAR PeriodDate@1100529002 : Date) : Boolean;
    VAR
      Period@1100529008 : Record 2000000007;
      OptionNo@1100529003 : Integer;
      Steps@1100529005 : Integer;
    BEGIN
      CASE CurrField OF
        'PERIODNO':
          IF (PeriodYear = 0) OR (PeriodNo = 0) THEN
            EXIT(FALSE);
        'PERIODDATE':
          IF (PeriodDate = 0D) OR (PeriodNo = 0) THEN
            EXIT(FALSE);
      ELSE
        EXIT(FALSE);
      END;

      ProjSetup.GET;
      IF (ProjSetup."Invoice Period Rental Units" = ProjSetup."Invoice Period Rental Units"::Date) AND (CurrField = 'PERIODDATE') THEN
        OptionNo := STRMENU(Text018 + ',' + Text019, 1)
      ELSE
        OptionNo := STRMENU(Text018, 1);
      CASE OptionNo OF
        1:
          BEGIN
            IF PeriodNo < 12 THEN
              PeriodNo := PeriodNo + 1
            ELSE BEGIN
              PeriodYear := PeriodYear + 1;
              PeriodNo := 1;
            END;
            DeterminePeriodDateFromPeriodNo(PeriodYear, PeriodNo, PeriodDate);
            EXIT(TRUE);
          END;
        2:
          BEGIN
            IF PeriodNo > 1 THEN
              PeriodNo := PeriodNo - 1
            ELSE BEGIN
              PeriodYear := PeriodYear - 1;
              PeriodNo := 12;
            END;
            DeterminePeriodDateFromPeriodNo(PeriodYear, PeriodNo, PeriodDate);
            EXIT(TRUE);
          END;
        3, 4:
          BEGIN
            Steps := 1;
            Period.SETRANGE("Period Type", Period."Period Type"::Week);
            Period.SETFILTER("Period Start", '<=%1', PeriodDate);
            IF Period.FINDLAST THEN BEGIN
              Period.SETRANGE("Period Start"); //Reset filter
              IF OptionNo = 4 THEN
                Steps := -Steps;
              Period.NEXT(Steps);
              PeriodDate := NORMALDATE(Period."Period End");
              DeterminePeriodNoFromPeriodDate(PeriodDate, PeriodYear, PeriodNo);
              EXIT(TRUE);
            END;
          END;
      END;
      EXIT(FALSE);
    END;

    PROCEDURE DetermineRentalPeriodInfo@1100525000(Forewards@1100529004 : Boolean;YearNo@1100529000 : Integer;MonthNo@1100529001 : Integer;PeriodDate@1100529002 : Date;VAR PeriodToInfo@1100525003 : Text;VAR Attention@1100529005 : Boolean;VAR ReferenceDate@1100525009 : Date);
    BEGIN
      PeriodToInfo := '';
      Attention := FALSE;
      ReferenceDate := 0D;
      ProjSetup.GET;
      CASE ProjSetup."Invoice Period Rental Units" OF
        ProjSetup."Invoice Period Rental Units"::Calendar:
          DetermineRentalPeriodInfoBasedOnCal(Forewards, YearNo, MonthNo, PeriodToInfo, ReferenceDate);
        ProjSetup."Invoice Period Rental Units"::Date:
          DetermineRentalPeriodInfoBasedOnDate(Forewards, PeriodDate, PeriodToInfo, ReferenceDate);
      END;
      Attention := ((ReferenceDate <> 0D) AND (ReferenceDate >= CALCDATE('<1M>', TODAY)));
    END;

    LOCAL PROCEDURE DetermineRentalPeriodInfoBasedOnCal@1100529009(Forewards@1100529004 : Boolean;YearNo@1100529000 : Integer;MonthNo@1100529001 : Integer;VAR PeriodToInfo@1100525003 : Text;VAR ReferenceDate@1100525009 : Date);
    VAR
      RentalPackage@1100525004 : Record 11012941;
      MonthStartDate@1100525001 : Date;
      MonthEndDate@1100525005 : Date;
      QuarterEndDate@1100525006 : Date;
      HalfyearEndDate@1100525007 : Date;
      YearEndDate@1100525008 : Date;
      YearNo2@1100525002 : Integer;
      PeriodNo@1100525000 : Integer;
    BEGIN
      IF (YearNo = 0) OR (MonthNo < 1) OR (MonthNo > 12) THEN
        EXIT;

      MonthStartDate := DMY2DATE(1, MonthNo, YearNo);
      MonthEndDate := CALCDATE('<1M-1D>', MonthStartDate);
      IF Forewards THEN BEGIN
        ReferenceDate := MonthStartDate;
        CASE MonthNo OF
          1,2,3:
            PeriodNo := 3;
          4,5,6:
            PeriodNo := 6;
          7,8,9:
            PeriodNo := 9;
          10,11,12:
            PeriodNo := 12;
        END;
        QuarterEndDate := CALCDATE('<1M-1D>', DMY2DATE(1, PeriodNo, YearNo));
        IF MonthNo <= 6 THEN
          PeriodNo := 6
        ELSE
          PeriodNo := 12;
        HalfyearEndDate := CALCDATE('<1M-1D>', DMY2DATE(1, PeriodNo, YearNo));
        YearEndDate := DMY2DATE(31, 12, YearNo);
      END ELSE BEGIN
        ReferenceDate := MonthEndDate;
        YearNo2 := YearNo;
        IF MonthNo <= 2 THEN
          YearNo2 := YearNo2 - 1;
        CASE MonthNo OF
          1,2,12:
            PeriodNo := 12;
          3,4,5:
            PeriodNo := 3;
          6,7,8:
            PeriodNo := 6;
          9,10,11:
            PeriodNo := 9;
        END;
        QuarterEndDate := CALCDATE('<1M-1D>', DMY2DATE(1, PeriodNo, YearNo2));
        YearNo2 := YearNo;
        IF MonthNo <= 5 THEN
          YearNo2 := YearNo2 - 1;
        IF (MonthNo <= 5) OR (MonthNo = 12) THEN
          PeriodNo := 12
        ELSE
          PeriodNo := 6;
        HalfyearEndDate := CALCDATE('<1M-1D>', DMY2DATE(1, PeriodNo, YearNo2));
        YearNo2 := YearNo;
        IF MonthNo <= 11 THEN
          YearNo2 := YearNo2 - 1;
        YearEndDate := DMY2DATE(31, 12, YearNo2);
      END;
      PeriodToInfo := STRSUBSTNO('%1: %2 / %3: %4 / %5: %6 / %7: %8',
        RentalPackage."Invoice Frequency"::Month, MonthEndDate,
        RentalPackage."Invoice Frequency"::Quarter, QuarterEndDate,
        RentalPackage."Invoice Frequency"::HalfYear, HalfyearEndDate,
        RentalPackage."Invoice Frequency"::Year, YearEndDate
        );
    END;

    LOCAL PROCEDURE DetermineRentalPeriodInfoBasedOnDate@1100529010(Forewards@1100529004 : Boolean;PeriodDate@1100529000 : Date;VAR PeriodToInfo@1100525003 : Text;VAR ReferenceDate@1100525009 : Date);
    VAR
      RentalPackage@1100525004 : Record 11012941;
      MonthEndDate@1100525005 : Date;
      QuarterEndDate@1100525006 : Date;
      HalfyearEndDate@1100525007 : Date;
      YearEndDate@1100525008 : Date;
    BEGIN
      IF (PeriodDate = 0D) THEN
        EXIT;

      IF Forewards THEN BEGIN
        ReferenceDate := CalcdateWithEndMonthCorrection('<-1M>', PeriodDate, 0D);
        MonthEndDate := PeriodDate;
        QuarterEndDate := CalcdateWithEndMonthCorrection('<2M>', PeriodDate, 0D);
        HalfyearEndDate := CalcdateWithEndMonthCorrection('<5M>', PeriodDate, 0D);
        YearEndDate := CalcdateWithEndMonthCorrection('<11M>', PeriodDate, 0D);
      END ELSE BEGIN
        ReferenceDate := PeriodDate;
        MonthEndDate := PeriodDate;
        QuarterEndDate := PeriodDate;
        HalfyearEndDate := PeriodDate;
        YearEndDate := PeriodDate;
      END;
      PeriodToInfo := STRSUBSTNO('%1: %2 / %3: %4 / %5: %6 / %7: %8',
        RentalPackage."Invoice Frequency"::Month, MonthEndDate,
        RentalPackage."Invoice Frequency"::Quarter, QuarterEndDate,
        RentalPackage."Invoice Frequency"::HalfYear, HalfyearEndDate,
        RentalPackage."Invoice Frequency"::Year, YearEndDate
        );
    END;

    PROCEDURE GetPeriodEndForInvoiceFreq@1100525001(Forewards@1100525002 : Boolean;InvoiceFreq@1100529007 : Option;YearNo@1100529000 : Integer;MonthNo@1100529001 : Integer;PeriodDate@1100529002 : Date) : Date;
    BEGIN
      ProjSetup.GET;
      CASE ProjSetup."Invoice Period Rental Units" OF
        ProjSetup."Invoice Period Rental Units"::Calendar:
          EXIT(GetPeriodEndForInvoiceFreqBasedOnCal(Forewards, InvoiceFreq, YearNo, MonthNo));
        ProjSetup."Invoice Period Rental Units"::Date:
          EXIT(GetPeriodEndForInvoiceFreqBasedOnDate(Forewards, InvoiceFreq, PeriodDate));
      END;
      EXIT(0D);
    END;

    LOCAL PROCEDURE GetPeriodEndForInvoiceFreqBasedOnCal@1100529012(Forewards@1100525002 : Boolean;InvoiceFreq@1100529007 : Option;YearNo@1100529000 : Integer;MonthNo@1100529001 : Integer) EndDate : Date;
    VAR
      RentalPackage@1100529008 : Record 11012941;
      YearNumber@1100525000 : Integer;
      PeriodNumber@1100525001 : Integer;
    BEGIN
      YearNumber := YearNo;
      IF Forewards THEN BEGIN
        CASE InvoiceFreq OF
          RentalPackage."Invoice Frequency"::Month:
            BEGIN
              PeriodNumber := MonthNo;
            END;
          RentalPackage."Invoice Frequency"::Quarter:
            BEGIN
              CASE MonthNo OF
                1,2,3:
                  PeriodNumber := 3;
                4,5,6:
                  PeriodNumber := 6;
                7,8,9:
                  PeriodNumber := 9;
                10,11,12:
                  PeriodNumber := 12;
              END;
            END;
          RentalPackage."Invoice Frequency"::HalfYear:
            IF MonthNo <= 6 THEN
              PeriodNumber := 6
            ELSE
              PeriodNumber := 12;
          RentalPackage."Invoice Frequency"::Year:
            PeriodNumber := 12;
        END;
      END ELSE BEGIN
        CASE InvoiceFreq OF
          RentalPackage."Invoice Frequency"::Month:
            PeriodNumber := MonthNo;
          RentalPackage."Invoice Frequency"::Quarter:
            CASE MonthNo OF
              1,2:
                BEGIN
                  YearNumber := YearNumber - 1;
                  PeriodNumber := 12;
                END;
              3,4,5:
                PeriodNumber := 3;
              6,7,8:
                PeriodNumber := 6;
              9,10,11:
                PeriodNumber := 9;
              12:
                PeriodNumber := 12;
              END;
          RentalPackage."Invoice Frequency"::HalfYear:
            CASE MonthNo OF
              1,2,3,4,5:
                BEGIN
                  YearNumber := YearNumber - 1;
                  PeriodNumber := 12;
                END;
              6,7,8,9,10,11:
                PeriodNumber := 6;
              12:
                PeriodNumber := 12;
            END;
          RentalPackage."Invoice Frequency"::Year:
            BEGIN
              IF MonthNo < 12 THEN
                YearNumber := YearNumber - 1;
              PeriodNumber := 12;
            END;
        END;
      END;
      EndDate := CALCDATE('<1M-1D>', DMY2DATE(1, PeriodNumber, YearNumber));
    END;

    LOCAL PROCEDURE GetPeriodEndForInvoiceFreqBasedOnDate@1100529013(Forewards@1100529001 : Boolean;InvoiceFreq@1100529000 : Option;PeriodDate@1100529002 : Date) : Date;
    VAR
      RentalPackage@1100529008 : Record 11012941;
      EndDate@1100529003 : Date;
    BEGIN
      EndDate := PeriodDate;
      IF Forewards THEN BEGIN
        CASE InvoiceFreq OF
          RentalPackage."Invoice Frequency"::Quarter:
            EndDate := CalcdateWithEndMonthCorrection('<2M>', PeriodDate, 0D);
          RentalPackage."Invoice Frequency"::HalfYear:
            EndDate := CalcdateWithEndMonthCorrection('<5M>', PeriodDate, 0D);
          RentalPackage."Invoice Frequency"::Year:
            EndDate := CalcdateWithEndMonthCorrection('<11M>', PeriodDate, 0D);
        END;
      END;
      WITH TmpInvoicePeriods DO BEGIN
        TmpInvoicePeriods.RESET;
        TmpInvoicePeriods.SETRANGE("Period Type", TmpInvoicePeriods."Period Type"::Date);
        TmpInvoicePeriods.SETFILTER("Period Start", '<=%1', EndDate);
        TmpInvoicePeriods.SETFILTER("Period End", '<=%1', EndDate);
        IF TmpInvoicePeriods.FINDLAST THEN
          EXIT(TmpInvoicePeriods."Period End");
        TmpInvoicePeriods.SETRANGE("Period End");
        IF TmpInvoicePeriods.FINDLAST THEN BEGIN
          IF EndDate > "Period End" THEN
            ERROR(Text020, PeriodDate);
        END;
      END;
      EXIT(0D);
    END;

    PROCEDURE GetPeriodStartForInvoiceFreqFromPeriodEnd@1100525002(InvoiceFreq@1100529007 : Option;PeriodEndDate@1100529000 : Date) : Date;
    VAR
      RentalPackage@1100529008 : Record 11012941;
      YearNo@1100525000 : Integer;
      MonthNo@1100525001 : Integer;
    BEGIN
      ProjSetup.GET;
      IF ProjSetup."Invoice Period Rental Units" = ProjSetup."Invoice Period Rental Units"::Calendar THEN BEGIN
        MonthNo := DATE2DMY(PeriodEndDate, 2);
        YearNo := DATE2DMY(PeriodEndDate, 2);
        CASE InvoiceFreq OF
          RentalPackage."Invoice Frequency"::Month:
            EXIT(DMY2DATE(1, MonthNo, YearNo));
          RentalPackage."Invoice Frequency"::Quarter:
            EXIT(DMY2DATE(1, MonthNo-2, YearNo));
          RentalPackage."Invoice Frequency"::HalfYear:
            EXIT(DMY2DATE(1, MonthNo-5, YearNo));
          RentalPackage."Invoice Frequency"::Year:
            EXIT(DMY2DATE(1, 1, YearNo));
        END;
      END ELSE BEGIN
        TmpInvoicePeriods.RESET;
        TmpInvoicePeriods.SETRANGE("Period Type", TmpInvoicePeriods."Period Type"::Date);
        TmpInvoicePeriods.SETFILTER("Period Start", '<=%1', PeriodEndDate);
        IF TmpInvoicePeriods.FINDLAST THEN
          EXIT(TmpInvoicePeriods."Period Start");
      END;
    END;

    PROCEDURE CheckPackageAndReadRelatedData@1100529002(RentalPackage@1100529004 : Record 11012941;RentalPeriodEnd@1100529012 : Date;DelayedIndexInvRun@1100529016 : Boolean;VAR Project@1100529000 : Record 11072003;VAR ProjectType@1100529001 : Record 11012009;VAR ProjectTypeExtension@1100529602 : Record 11229469;VAR ProjPrincipal@1100529003 : Record 11012005;VAR DefaultElement@1100529010 : Code[20];VAR RentalUnit@1100529013 : Record 11012940;VAR TmpError@1100529008 : TEMPORARY Record 11012051) RetOK : Boolean;
    VAR
      RentalRateLine@1100529009 : Record 11012942;
      RentalPackageLineRate@1100529021 : Record 11229856;
      ProjectElement@1100529011 : Record 11012010;
      ShiptoAddress@1100529500 : Record 222;
      Customer@1100529600 : Record 18;
      LedgerbyProjTypeCustGr@1100529601 : Record 11229378;
      ProvisionAcc@1100529005 : Boolean;
      IsCredit@1100529020 : Boolean;
      LimitDate@1100529014 : Date;
      RentalStartDate@1100529017 : Date;
      RentalEndDate@1100529018 : Date;
      LineQuantity@1100529019 : Decimal;
      Comma@1100529007 : Text[2];
      ErrorMess@1100529002 : Text;
    BEGIN
      CLEAR(Project);
      CLEAR(ProjectType);
      CLEAR(ProjectTypeExtension);
      CLEAR(LedgerbyProjTypeCustGr);
      CLEAR(ProjPrincipal);
      CLEAR(RentalUnit);
      DefaultElement := '';
      RetOK := TRUE;
      WITH RentalPackage DO BEGIN
        IF "Package Editable" THEN BEGIN
          ErrorMess := STRSUBSTNO(Text007, FIELDCAPTION("Package Editable"));
          WriteError(RentalPackage, ErrorMess, TmpError);
          RetOK := FALSE;
        END;
        IF "Invoice Status" = "Invoice Status"::"Temporary" THEN BEGIN
          ErrorMess := STRSUBSTNO(Text008, FIELDCAPTION("Last Invoice No."), "Last Invoice No.", "Invoice Status");
          WriteError(RentalPackage, ErrorMess, TmpError);
          RetOK := FALSE;
        END;
        IF NOT ("Invoice Frequency" IN
          ["Invoice Frequency"::Month, "Invoice Frequency"::Quarter, "Invoice Frequency"::HalfYear, "Invoice Frequency"::Year])
        THEN BEGIN
          ErrorMess := STRSUBSTNO(Text001, FIELDCAPTION("Invoice Frequency"), "Invoice Frequency");
          WriteError(RentalPackage, ErrorMess, TmpError);
          RetOK := FALSE;
        END;
        IF "Alternative Bill-to Address" <> '' THEN BEGIN
          ShiptoAddress.SETRANGE("Customer No.", DetermineBillToCustomerNo);
          ShiptoAddress.SETRANGE(Code, "Alternative Bill-to Address");
          ShiptoAddress.SETRANGE("Billing Address", TRUE);
          IF NOT ShiptoAddress.FINDFIRST THEN BEGIN
            ErrorMess := STRSUBSTNO(Text001, FIELDCAPTION("Alternative Bill-to Address"), "Alternative Bill-to Address");
            WriteError(RentalPackage, ErrorMess, TmpError);
            RetOK := FALSE;
          END;
        END;

        IF NOT Project.GET("Project No.") THEN BEGIN
          ErrorMess := STRSUBSTNO(Text001, FIELDCAPTION("Project No."), "Project No.");
          WriteError(RentalPackage, ErrorMess, TmpError);
          RetOK := FALSE;
        END ELSE BEGIN
          ProjSetup.GET;
          IF (Project."Project Status" < Project."Project Status"::Production) OR
             ((ProjSetup."Provisions at Closure") AND (Project."Project Status" > Project."Project Status"::Finished)) OR
             ((NOT ProjSetup."Provisions at Closure") AND (Project."Project Status" > Project."Project Status"::"Technical Finished"))
          THEN BEGIN
            ErrorMess := STRSUBSTNO(Text021, Project.FIELDCAPTION("Project Status"), Project."Project Status");
            WriteError(RentalPackage, ErrorMess, TmpError);
            RetOK := FALSE;
          END;
          IF NOT ProjPrincipal.GET("Project No.", "Customer No.") THEN   //Not Mandatory
            ProjPrincipal.INIT;
          IF (ProjPrincipal."VAT Bus. Posting Group" = '') AND (Project."VAT Bus. Posting Group" = '') THEN BEGIN
            ErrorMess := STRSUBSTNO(Text004, Project.FIELDCAPTION("VAT Bus. Posting Group"), "Project No.", "Customer No.");
            WriteError(RentalPackage, ErrorMess, TmpError);
            RetOK := FALSE;
          END;
          IF Project."Project Type" = '' THEN BEGIN
            ErrorMess := STRSUBSTNO(Text002, FIELDCAPTION("Project No."), Project.FIELDCAPTION("Project Type"));
            WriteError(RentalPackage, ErrorMess, TmpError);
            RetOK := FALSE;
          END ELSE BEGIN
            IF NOT ProjectType.GET(Project."Project Type") THEN BEGIN
              ErrorMess := STRSUBSTNO(Text001, Project.FIELDCAPTION("Project Type"), Project."Project Type");
              WriteError(RentalPackage, ErrorMess, TmpError);
              RetOK := FALSE;
            END ELSE BEGIN
              IF Project."Project Status" >= Project."Project Status"::Finished THEN BEGIN
                ProjSetup.GET;
                ProvisionAcc := ProjSetup."Provisions at Closure";
              END;
              ProjectTypeExtension.GetProjectTypeExtension(ProjectType.Code);
              IF Customer.GET("Customer No.") THEN
                IF Customer."Customer Posting Group" <> '' THEN
                  IF LedgerbyProjTypeCustGr.GET(ProjectType.Code, Customer."Customer Posting Group") THEN;
              IF (ProjectType."Sales Invoice Nos." = '') OR (ProjectType."Credit Memo Nos." = '') OR
                 (ProvisionAcc AND (ProjectType."Provision Account Rental UR" = '')) OR
                 ((NOT ProvisionAcc) AND
                  (((ProjectType."Rental Unit Account Revenue" = '') AND (LedgerbyProjTypeCustGr."Rental Unit Account Revenue" = '')) OR
                   ((ProjectType."Rental Unit Service Cost Rev." = '') AND (LedgerbyProjTypeCustGr."Rental Unit Service Cost Rev." = '')) OR
                   ((ProjectType."Rental Unit Acc Utilities Rev." = '') AND (LedgerbyProjTypeCustGr."Rental Unit Acc Utilities Rev." = '')) OR
                   (ProjectTypeExtension."Rental Unit One-off Cost Rev." = '') OR
                   (ProjectTypeExtension."Rental Unit Deposit Revenue" = '')))
              THEN BEGIN
                ErrorMess := STRSUBSTNO(Text003, Project.FIELDCAPTION("Project Type"), Project."Project Type");
                Comma := ' ';
                IF ProjectType."Sales Invoice Nos." = '' THEN BEGIN
                  ErrorMess := ErrorMess + Comma + ProjectType.FIELDCAPTION("Sales Invoice Nos.");
                  Comma := ', ';
                END;
                IF ProjectType."Credit Memo Nos." = '' THEN BEGIN
                  ErrorMess := ErrorMess + Comma + ProjectType.FIELDCAPTION("Credit Memo Nos.");
                  Comma := ', ';
                END;
                IF ProvisionAcc THEN BEGIN
                  IF ProjectType."Provision Account Rental UR" = '' THEN BEGIN
                    ErrorMess := ErrorMess + Comma + ProjectType.FIELDCAPTION("Provision Account Rental UR");
                    Comma := ', ';
                  END;
                END ELSE BEGIN
                  IF (ProjectType."Rental Unit Account Revenue" = '') AND (LedgerbyProjTypeCustGr."Rental Unit Account Revenue" = '') THEN BEGIN
                    ErrorMess := ErrorMess + Comma + ProjectType.FIELDCAPTION("Rental Unit Account Revenue");
                    Comma := ', ';
                  END;
                  IF (ProjectType."Rental Unit Service Cost Rev." = '') AND (LedgerbyProjTypeCustGr."Rental Unit Service Cost Rev." = '') THEN BEGIN
                    ErrorMess := ErrorMess + Comma + ProjectType.FIELDCAPTION("Rental Unit Service Cost Rev.");
                    Comma := ', ';
                  END;
                  IF (ProjectType."Rental Unit Acc Utilities Rev." = '') AND (LedgerbyProjTypeCustGr."Rental Unit Acc Utilities Rev." = '') THEN BEGIN
                    ErrorMess := ErrorMess + Comma + ProjectType.FIELDCAPTION("Rental Unit Acc Utilities Rev.");
                    Comma := ', ';
                  END;
                  IF ProjectTypeExtension."Rental Unit One-off Cost Rev." = '' THEN BEGIN
                    ErrorMess := ErrorMess + Comma + ProjectTypeExtension.FIELDCAPTION("Rental Unit One-off Cost Rev.");
                    Comma := ', ';
                  END;
                  IF ProjectTypeExtension."Rental Unit Deposit Revenue" = '' THEN BEGIN
                    ErrorMess := ErrorMess + Comma + ProjectTypeExtension.FIELDCAPTION("Rental Unit Deposit Revenue");
                    Comma := ', ';
                  END;
                END;
                WriteError(RentalPackage, ErrorMess, TmpError);
                RetOK := FALSE;
              END;
            END;
          END;
        END;
        IF NOT RentalUnit.GET("Project No.", "Rental Unit") THEN BEGIN
          ErrorMess := STRSUBSTNO(Text001, FIELDCAPTION("Rental Unit"), "Rental Unit");
          WriteError(RentalPackage, ErrorMess, TmpError);
          RetOK := FALSE;
        END;
        IF NOT DelayedIndexInvRun THEN BEGIN
          IF ("Indexation Date" <> 0D) AND ("Indexation Date" <= RentalPeriodEnd) THEN BEGIN
            LimitDate := 0D;
            IF RentalPackage."Max. Delay Indexation (Mth)" > 0 THEN
              LimitDate := AllowedInvoiceEndDateWithDelayIndex();
            IF (RentalPeriodEnd > LimitDate) OR (LimitDate = 0D) THEN BEGIN
              ErrorMess := STRSUBSTNO(Text014, RentalPeriodEnd, FIELDCAPTION("Indexation Date"), "Indexation Date");
              IF LimitDate <> 0D THEN
                ErrorMess := ErrorMess + ' ' + STRSUBSTNO(Text014b, LimitDate);
              WriteError(RentalPackage, ErrorMess, TmpError);
              RetOK := FALSE;
            END;
          END;
          IF ("Ending Date" <> 0D) AND ("Ending Date" < RentalPeriodEnd) AND ("Termination Reason" = '') THEN BEGIN
            ErrorMess := STRSUBSTNO(Text012, TABLECAPTION, FIELDCAPTION("Ending Date"), RentalPeriodEnd, FIELDCAPTION("Termination Reason"));
            WriteError(RentalPackage, ErrorMess, TmpError);
            RetOK := FALSE;
          END;
        END;

        IF NOT DelayedIndexInvRun THEN BEGIN
          RentalRateLine.SETRANGE("Project No.", "Project No.");
          RentalRateLine.SETRANGE("Rental Unit", "Rental Unit");
          RentalRateLine.SETRANGE("Starting Date Package", "Starting Date");
          //
          IF RentalRateLine.FINDSET THEN BEGIN
            REPEAT
              CalcRentalRateLineData(
                RentalPackage, RentalRateLine, RentalPeriodEnd, RentalStartDate, RentalEndDate, LineQuantity, IsCredit);
              IF LineQuantity <> 0 THEN BEGIN
                IF NOT RentalRateLine.GetLineRateOnRefDate(RentalRateLine, RentalStartDate, RentalPackageLineRate) THEN BEGIN
                  ErrorMess := STRSUBSTNO(Text017,
                    RentalPackageLineRate.FIELDCAPTION(Date), RentalStartDate,
                    RentalRateLine.FIELDCAPTION("Line No."), RentalRateLine."Line No.",
                    RentalRateLine.FIELDCAPTION(Description), RentalRateLine.Description);
                  WriteError(RentalPackage, ErrorMess, TmpError);
                  RetOK := FALSE;
                END;
              END;
            UNTIL RentalRateLine.NEXT = 0;
          END;
          //
          RentalRateLine.SETFILTER("Ending Date Line", '<>%1&<%2', 0D, RentalPeriodEnd);
          RentalRateLine.SETRANGE("Termination Reason", '');
          IF NOT RentalRateLine.ISEMPTY THEN BEGIN
            ErrorMess := STRSUBSTNO(Text012, Text013, FIELDCAPTION("Ending Date"), RentalPeriodEnd, FIELDCAPTION("Termination Reason"));
            WriteError(RentalPackage, ErrorMess, TmpError);
            RetOK := FALSE;
          END;
          RentalRateLine.SETRANGE("Ending Date Line");    //Reset filters
          RentalRateLine.SETRANGE("Termination Reason");  //
          //
          RentalRateLine.SETRANGE("VAT Prod. Posting Group", '');
          IF NOT RentalRateLine.ISEMPTY THEN BEGIN
            ErrorMess := STRSUBSTNO(Text005, RentalRateLine.FIELDCAPTION("VAT Prod. Posting Group"));
            WriteError(RentalPackage, ErrorMess, TmpError);
            RetOK := FALSE;
          END;
          RentalRateLine.SETRANGE("VAT Prod. Posting Group");  //Reset filter
        END;
        //
        IF Project."Posting Element Mandatory" THEN BEGIN
          ProjectElement.SETRANGE("Project No.", "Project No.");
          ProjectElement.SETFILTER(Element, '<>%1', '');
          ProjectElement.SETRANGE(Level, 0);
          IF ProjectElement.FINDFIRST THEN
            DefaultElement := ProjectElement.Element;
          IF NOT DelayedIndexInvRun THEN BEGIN
            RentalRateLine.SETRANGE(Element, '');
            IF (NOT RentalRateLine.ISEMPTY) AND (DefaultElement = '') THEN BEGIN
              ErrorMess := STRSUBSTNO(Text006, RentalRateLine.FIELDCAPTION(Element), Project.FIELDCAPTION("Posting Element Mandatory"));
              WriteError(RentalPackage, ErrorMess, TmpError);
              RetOK := FALSE;
            END;
            RentalRateLine.SETRANGE(Element);  //Reset filter
          END;
        END;
      END;

      EXIT(RetOK);
    END;

    PROCEDURE CalcRentalRateLineData@1100528401(RentalPackage@1100528400 : Record 11012941;RentalRateLine@1100528401 : Record 11012942;RentalPeriodEnd@1100528404 : Date;VAR RentalStartDate@1100528402 : Date;VAR RentalEndDate@1100528408 : Date;VAR LineQuantity@1100528406 : Decimal;VAR IsCredit@1100529002 : Boolean);
    VAR
      StartDateLine@1100529000 : Date;
      EndDateLine@1100529001 : Date;
    BEGIN
      RentalStartDate := 0D;
      RentalEndDate := 0D;
      LineQuantity := 0;
      IsCredit := FALSE;

      StartDateLine := RentalRateLine."Starting Date Line";
      IF (StartDateLine = 0D) OR (StartDateLine < RentalPackage."Starting Date") THEN
        StartDateLine := RentalPackage."Starting Date";
      EndDateLine := RentalRateLine."Ending Date Line";
      IF (EndDateLine = 0D) AND (RentalPackage."Ending Date" <> 0D) THEN
        EndDateLine := RentalPackage."Ending Date";

      IF (RentalRateLine."Invoiced Until" = 0D) OR (EndDateLine = 0D) OR (RentalRateLine."Invoiced Until" < EndDateLine) THEN BEGIN
        IF RentalRateLine."Invoiced Until" = 0D THEN
          RentalStartDate := StartDateLine
        ELSE BEGIN
          RentalStartDate := RentalRateLine."Invoiced Until" + 1;
          IF RentalStartDate < StartDateLine THEN
            RentalStartDate := StartDateLine;
        END;
        //
        RentalEndDate := RentalPeriodEnd;
        IF (EndDateLine <> 0D) AND (EndDateLine < RentalEndDate) THEN
          RentalEndDate := EndDateLine;
        IF (RentalEndDate < RentalStartDate) OR (RentalStartDate > RentalPeriodEnd) THEN
          EXIT;
      END ELSE BEGIN
        IF (RentalRateLine."Invoiced Until" <> 0D) AND (RentalRateLine."Invoiced Until" > EndDateLine) THEN BEGIN
          RentalStartDate := EndDateLine + 1;
          RentalEndDate := RentalRateLine."Invoiced Until";
          IF (RentalEndDate < RentalStartDate) OR (RentalEndDate > RentalPeriodEnd) THEN
            EXIT;
          IsCredit := TRUE;
        END;
        IF NOT IsCredit THEN
          EXIT;
      END;
      LineQuantity := CalculateNoOfPeriods(RentalPackage."Invoice Frequency", RentalStartDate, RentalEndDate);
      IF IsCredit THEN
        LineQuantity := -LineQuantity;
    END;

    PROCEDURE DetermineUnitOfMeasure@1210190015(RentalPackage@1210190001 : Record 11012941) RetUnitOfMeasure@1210190000 : Code[10];
    BEGIN
      ProjSetup.GET;
      WITH RentalPackage DO BEGIN
        CASE "Invoice Frequency" OF
          "Invoice Frequency"::Month:
            RetUnitOfMeasure := ProjSetup."Unit of Measure Month";
          "Invoice Frequency"::Quarter:
            RetUnitOfMeasure := ProjSetup."Unit of Measure Quarter";
          "Invoice Frequency"::HalfYear:
            RetUnitOfMeasure := ProjSetup."Unit of Measure Half a Year";
          "Invoice Frequency"::Year:
            RetUnitOfMeasure := ProjSetup."Unit of Measure Year";
          ELSE
            RetUnitOfMeasure := '';
        END;
      END;
    END;

    PROCEDURE CalculateNoOfPeriods@1100529001(InvoiceFreq@1210190002 : Option;RentalStartDate@1210190003 : Date;RentalEndDate@1210190004 : Date) @1210190005 : Decimal;
    BEGIN
      IF (RentalEndDate < RentalStartDate) OR (RentalStartDate = 0D) OR (RentalEndDate = 0D) THEN
        EXIT(0);

      ProjSetup.GET;
      CASE ProjSetup."Invoice Period Rental Units" OF
        ProjSetup."Invoice Period Rental Units"::Calendar:
          EXIT(CalculateNoOfPeriodsBasedOnCal(InvoiceFreq, RentalStartDate, RentalEndDate));
        ProjSetup."Invoice Period Rental Units"::Date:
          EXIT(CalculateNoOfPeriodsBasedOnDate( RentalStartDate, RentalEndDate));
      END;
      EXIT(0);
    END;

    LOCAL PROCEDURE CalculateNoOfPeriodsBasedOnCal@1100529015(InvoiceFreq@1210190002 : Option;RentalStartDate@1210190003 : Date;RentalEndDate@1210190004 : Date) @1210190005 : Decimal;
    VAR
      RentalPackage@1100529012 : Record 11012941;
      Date1@1100529000 : Record 2000000007;
      Date2@1100529001 : Record 2000000007;
      DayNo1@1100529002 : Integer;
      DayNo2@1100529007 : Integer;
      MonthNo1@1100529003 : Integer;
      MonthNo2@1100529006 : Integer;
      YearNo1@1100529004 : Integer;
      YearNo2@1100529005 : Integer;
      LastDayNoMonth1@1100529009 : Integer;
      LastDayNoMonth2@1100529010 : Integer;
      FreqFactor@1100529011 : Integer;
      NoOfMonths@1100529008 : Decimal;
    BEGIN
      NoOfMonths := 0;
      DayNo1 := DATE2DMY(RentalStartDate, 1);
      MonthNo1 := DATE2DMY(RentalStartDate, 2);
      YearNo1 := DATE2DMY(RentalStartDate, 3);
      DayNo2 := DATE2DMY(RentalEndDate, 1);
      MonthNo2 := DATE2DMY(RentalEndDate, 2);
      YearNo2 := DATE2DMY(RentalEndDate, 3);

      Date1.SETRANGE("Period Type", Date1."Period Type"::Month);
      Date1.SETFILTER("Period Start", '<=%1', RentalStartDate);
      Date1.FINDLAST;
      LastDayNoMonth1 := DATE2DMY(NORMALDATE(Date1."Period End"), 1);
      IF (YearNo1 <> YearNo2) OR (MonthNo1 <> MonthNo2) THEN BEGIN
        Date2.SETRANGE("Period Type", Date2."Period Type"::Month);
        Date2.SETFILTER("Period Start", '<=%1', RentalEndDate);
        Date2.FINDLAST;
        LastDayNoMonth2 := DATE2DMY(NORMALDATE(Date2."Period End"), 1);
      END;

      IF (YearNo1 = YearNo2) AND (MonthNo1 = MonthNo2) THEN BEGIN
        IF (DayNo1 = 1) AND (DayNo2 = LastDayNoMonth1) THEN
          NoOfMonths := 1
        ELSE
          NoOfMonths := (DayNo2 - DayNo1 + 1) / LastDayNoMonth1;
      END ELSE BEGIN
        //First month
        IF DayNo1 = 1 THEN
          NoOfMonths := NoOfMonths + 1
        ELSE
          NoOfMonths := NoOfMonths + (LastDayNoMonth1 - DayNo1 + 1) / LastDayNoMonth1;
        //Last month
        IF DayNo2 = LastDayNoMonth2 THEN
          NoOfMonths := NoOfMonths + 1
        ELSE
          NoOfMonths := NoOfMonths + (DayNo2 / LastDayNoMonth2);
        //Months between
        IF MonthNo1 < 12 THEN
          MonthNo1 := MonthNo1 + 1
        ELSE BEGIN
          MonthNo1 := 1;
          YearNo1 := YearNo1 + 1;
        END;
        IF MonthNo2 > 1 THEN
          MonthNo2 := MonthNo2 - 1
        ELSE BEGIN
          MonthNo2 := 12;
          YearNo2 := YearNo2 - 1;
        END;
        IF (YearNo2 > YearNo1) THEN BEGIN
          NoOfMonths := NoOfMonths + (12 - MonthNo1 + 1) + MonthNo2;  // Plus months in first and last year
          IF YearNo2 > YearNo1 + 1 THEN
            NoOfMonths := NoOfMonths + ((YearNo2 - YearNo1 - 1) * 12);
        END ELSE BEGIN
          IF (YearNo1 = YearNo2) AND (MonthNo2 >= MonthNo1) THEN
            NoOfMonths := NoOfMonths + (MonthNo2 - MonthNo1 + 1);
        END;
      END;

      CASE InvoiceFreq OF
        RentalPackage."Invoice Frequency"::Month:
          FreqFactor := 1;
        RentalPackage."Invoice Frequency"::Quarter:
          FreqFactor := 3;
        RentalPackage."Invoice Frequency"::HalfYear:
          FreqFactor := 6;
        RentalPackage."Invoice Frequency"::Year:
          FreqFactor := 12;
      END;
      EXIT(NoOfMonths / FreqFactor);
    END;

    LOCAL PROCEDURE CalculateNoOfPeriodsBasedOnDate@1100529016(RentalStartDate@1100529001 : Date;RentalEndDate@1100529000 : Date) : Decimal;
    VAR
      NoOfPeriods@1100529002 : Decimal;
      StartDate@1100529003 : Date;
      EndDate@1100529004 : Date;
    BEGIN
      WITH TmpInvoicePeriods DO BEGIN
        RESET;
        SETRANGE("Period Type", "Period Type"::Date);
        SETFILTER("Period Start", '<=%1', RentalEndDate);
        SETFILTER("Period End", '>=%1', RentalStartDate);
        IF TmpInvoicePeriods.FINDSET THEN BEGIN
          REPEAT
            IF ("Period Start" >= RentalStartDate) AND ("Period End" <= RentalEndDate) THEN
              NoOfPeriods := NoOfPeriods + 1
            ELSE BEGIN
              StartDate := "Period Start";
              EndDate := "Period End";
              IF StartDate < RentalStartDate THEN
                StartDate := RentalStartDate;
              IF EndDate > RentalEndDate THEN
                EndDate := RentalEndDate;
              IF EndDate >= StartDate THEN
                NoOfPeriods := NoOfPeriods + ((EndDate - StartDate +1) / "Period No.");  // "Period No." contains number of days in Invoice Period
            END;
          UNTIL TmpInvoicePeriods.NEXT = 0;
          IF RentalEndDate > "Period End" THEN
            ERROR(Text020, RentalEndDate);
        END;
      END;
      EXIT(NoOfPeriods);
    END;

    PROCEDURE NextRateDateInInvoicePeriod@1100529005(RentalPackageLineRate@1100529000 : Record 11229856;RentalEndDate@1100529001 : Date) : Date;
    VAR
      RentalRateLine@1100529003 : Record 11012942;
      RentalPackageLineRate2@1100529002 : Record 11229856;
    BEGIN
      IF RentalRateLine.GetNextLineRate(RentalPackageLineRate, RentalPackageLineRate2) THEN BEGIN
        IF RentalPackageLineRate2.Date <= RentalEndDate THEN
          EXIT(RentalPackageLineRate2.Date);
      END;
      EXIT(0D);
    END;

    LOCAL PROCEDURE CalcdateWithEndMonthCorrection@1100529011(DateExpr@1100529001 : Code[20];OrgDate@1100529000 : Date;RefDate@1100529005 : Date) RetDate : Date;
    VAR
      Period@1100529002 : Record 2000000007;
      YearNo@1100529003 : Integer;
      MonthNo@1100529004 : Integer;
    BEGIN
      RetDate := CALCDATE(DateExpr, OrgDate);

      IF RefDate = 0D THEN
        RefDate := OrgDate;
      YearNo := DATE2DMY(RefDate, 3);
      MonthNo := DATE2DMY(RefDate, 2);
      Period.GET(Period."Period Type"::Month, DMY2DATE(1, MonthNo, YearNo));
      IF NORMALDATE(RefDate) = NORMALDATE(Period."Period End") THEN BEGIN
        YearNo := DATE2DMY(RetDate, 3);
        MonthNo := DATE2DMY(RetDate, 2);
        Period.GET(Period."Period Type"::Month, DMY2DATE(1, MonthNo, YearNo));
        RetDate := NORMALDATE(Period."Period End");
      END;
    END;

    PROCEDURE BuildPackageInvoicePeriods@1100529014(RentalPackage@1100529000 : Record 11012941);
    VAR
      DateExpr@1100529002 : Code[20];
      InvoicedUntil@1100529003 : Date;
      FoundUntil@1100529004 : Boolean;
      Counter@1100529005 : Integer;
      FreqFactor@1100529006 : Integer;
    BEGIN
      //Invoice (Rental) Periods based on Date
      IF RentalPackage."Starting Date" = 0D THEN
        EXIT;
      IF (RentalPackage."Invoiced Until" <> 0D) AND (RentalPackage."Invoiced Until" >= RentalPackage."Starting Date") THEN
        InvoicedUntil := RentalPackage."Invoiced Until"
      ELSE
        FoundUntil := TRUE;
      CASE RentalPackage."Invoice Frequency" OF
        RentalPackage."Invoice Frequency"::Month:
          FreqFactor := 1;
        RentalPackage."Invoice Frequency"::Quarter:
          FreqFactor := 3;
        RentalPackage."Invoice Frequency"::HalfYear:
          FreqFactor := 6;
        RentalPackage."Invoice Frequency"::Year:
          FreqFactor := 12;
      END;

      WITH TmpInvoicePeriods DO BEGIN
        RESET;
        DELETEALL;
        INIT;
        "Period Type" := "Period Type"::Date;
        "Period Start" := RentalPackage."Starting Date";
        Counter := 0;
        REPEAT
          Counter := Counter + 1;
          DateExpr := '<' + FORMAT(Counter * FreqFactor) + 'M>';
          "Period End" := CalcdateWithEndMonthCorrection(DateExpr, RentalPackage."Starting Date", RentalPackage."Starting Date") - 1;
          "Period No." := "Period End" - "Period Start" + 1;  // Number of days in Invoice Period
          INSERT;
          IF (NOT FoundUntil) AND ("Period End" = InvoicedUntil) THEN
            FoundUntil := TRUE;
          "Period Start" := "Period End" + 1;
        UNTIL ("Period Start" > TODAY + 735);
        IF FoundUntil OR (InvoicedUntil = 0D) THEN
          EXIT;

        //If Invoice Freq. is modified or setup "Invoice Period Rental Units" set from Caledar->Date, then may be not passibel
        //base the invoice periods on Start Date. Then they must be based on Invoiced Until.
        DELETEALL;
        "Period Start" := InvoicedUntil + 1;
        Counter := 0;
        REPEAT
          Counter := Counter + 1;
          DateExpr := '<' + FORMAT(Counter * FreqFactor) + 'M>';
          "Period End" := CalcdateWithEndMonthCorrection(DateExpr, InvoicedUntil + 1, InvoicedUntil + 1) - 1;
          "Period No." := "Period End" - "Period Start" + 1;
          INSERT;
          "Period Start" := "Period End" + 1;
        UNTIL ("Period Start" > TODAY + 735);

        DateExpr := '<-'+ COPYSTR(DateExpr, 2);
        "Period Start" := RentalPackage."Invoiced Until" + 1;
        Counter := 0;
        REPEAT
          Counter := Counter + 1;
          DateExpr := '<' + FORMAT(-Counter * FreqFactor) + 'M>';
          "Period End" := "Period Start" - 1;
          "Period Start" := CalcdateWithEndMonthCorrection(DateExpr, InvoicedUntil + 1, InvoicedUntil + 1);
          "Period No." := "Period End" - "Period Start" + 1;
          INSERT;
        UNTIL ("Period Start" <= RentalPackage."Starting Date");
      END;
    END;

    LOCAL PROCEDURE WriteError@1100529003(RentalPackage@1100529001 : Record 11012941;ErrorMess@1100529000 : Text;VAR TmpError@1100529002 : TEMPORARY Record 11012051);
    BEGIN
      TmpError.INIT;
      TmpError."User ID" := USERID;
      TmpError."Source Type" := TmpError."Source Type"::RentalUnit;
      TmpError."Line No." := TmpError."Line No." + 1;
      TmpError."Project No." := RentalPackage."Project No.";
      TmpError."Rental Unit" := RentalPackage."Rental Unit";
      TmpError."Starting Date Package" := RentalPackage."Starting Date";
      TmpError."Error message" := COPYSTR(ErrorMess, 1, MAXSTRLEN(TmpError."Error message"));
      TmpError.INSERT;
    END;

    PROCEDURE CreateRentalInvoices@1210190006(VAR SalesHeadTmp@1100529000 : TEMPORARY Record 36;VAR SalesLineTmp@1100529001 : TEMPORARY Record 37;DelayedIndexInvoice@1100529002 : Boolean);
    VAR
      SalesHead@1100529003 : Record 36;
      Window@1100529007 : Dialog;
      Factor@1210190000 : Integer;
      InvCounter@1100529006 : Integer;
      CreditMemoCounter@1100529005 : Integer;
      LineCounter@1100529004 : Integer;
    BEGIN
      SalesHeadTmp.RESET;
      IF SalesHeadTmp.FINDSET THEN BEGIN
        Window.OPEN(
          SalesLineTmp.FIELDCAPTION("Job No.") + '/' + SalesLineTmp.FIELDCAPTION("Rental Unit") + '#1############################');
        REPEAT
          SalesLineTmp.RESET;
          SalesLineTmp.SETRANGE("Document Type", SalesHeadTmp."Document Type");
          SalesLineTmp.SETRANGE("Document No.", SalesHeadTmp."No.");
          SalesLineTmp.CALCSUMS(Amount);
          IF SalesLineTmp.Amount >= 0 THEN BEGIN
            InvCounter := InvCounter + 1;
            Factor := 1;
          END ELSE BEGIN
            CreditMemoCounter :=CreditMemoCounter + 1;
            Factor := -1;
          END;
          InsertSalesHeader(SalesHeadTmp, Factor, SalesHead);
          SalesLineTmp.RESET;
          SalesLineTmp.SETRANGE("Document Type", SalesHeadTmp."Document Type");
          SalesLineTmp.SETRANGE("Document No.", SalesHeadTmp."No.");
          IF SalesLineTmp.FINDFIRST THEN BEGIN
            Window.UPDATE(1, SalesLineTmp."Job No." + '/' + SalesLineTmp."Rental Unit");
            REPEAT
              LineCounter := LineCounter + 1;
              IF NOT DelayedIndexInvoice THEN
                InsertSalesLine(SalesLineTmp, SalesHead, Factor, 1)
              ELSE
                InsertSalesLine(SalesLineTmp, SalesHead, 1, Factor);
              UpdateRentalPackageLastInvoice(SalesHead, SalesLineTmp);
            UNTIL SalesLineTmp.NEXT = 0;
          END;
        UNTIL SalesHeadTmp.NEXT = 0;
        Window.CLOSE;
      END;
      IF (InvCounter = 0) AND (CreditMemoCounter = 0) THEN
        MESSAGE(Text015)
      ELSE
        MESSAGE(Text016, InvCounter, CreditMemoCounter, LineCounter);
    END;

    LOCAL PROCEDURE InsertSalesHeader@1210190007(SalesHeadTmp@1100529000 : Record 36;Factor@1210190000 : Integer;VAR SalesHeader@1100529001 : Record 36);
    VAR
      NoSeriesManagement@1100529002 : Codeunit 396;
    BEGIN
      SalesHeader.INIT;
      IF Factor = 1 THEN BEGIN
        SalesHeader."Document Type" := SalesHeadTmp."Document Type";
        SalesHeader."No. Series" := SalesHeadTmp."No. Series";
        SalesHeader."Posting No. Series" := SalesHeadTmp."Posting No. Series";
      END ELSE BEGIN
        SalesHeader."Document Type" := SalesHeader."Document Type"::"Credit Memo";
        SalesHeader."No. Series" := SalesHeadTmp."Last Shipping No.";         // Credit Memo Series No
        SalesHeader."Posting No. Series" := SalesHeadTmp."Last Posting No.";  // Posting Credit Memo Series No
      END;
      SalesHeader."No." := NoSeriesManagement.GetNextNo(SalesHeader."No. Series",WORKDATE,TRUE);
      SalesHeader.INSERT(TRUE);
      SalesHeader."Job No." := SalesHeadTmp."Job No.";
      SalesHeader.VALIDATE("Sell-to Customer No.", SalesHeadTmp."Sell-to Customer No.");
      IF SalesHeadTmp."Alternative Bill-to Address" <> '' THEN
        SalesHeader.VALIDATE("Alternative Bill-to Address", SalesHeadTmp."Alternative Bill-to Address");
      SalesHeader."Posting Date" := SalesHeadTmp."Posting Date";
      SalesHeader.VALIDATE("Document Date", SalesHeadTmp."Document Date");
      SalesHeader."Your Reference" := SalesHeadTmp."Your Reference";
      IF SalesHeadTmp."Principal Reference" <> '' THEN
        SalesHeader."Principal Reference" := SalesHeadTmp."Principal Reference";
      SalesHeader."Project Invoice" := TRUE;
      SalesHeader."Rental Unit Invoice" := TRUE;
      SalesHeader.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE InsertSalesLine@1210190008(SalesLineTmp@1100529000 : Record 37;SalesHeader@1100529001 : Record 36;FactorQty@1210190000 : Integer;FactorAmt@1100529003 : Integer);
    VAR
      SalesLine@1100529002 : Record 37;
    BEGIN
      SalesLine.INIT;
      SalesLine."Document Type" := SalesHeader."Document Type";
      SalesLine."Document No." := SalesHeader."No.";
      SalesLine."Line No." := SalesLineTmp."Line No.";
      SalesLine."Project Invoice" := TRUE;
      SalesLine."Rental Unit Invoice" := TRUE;
      SalesLine."System-Created Entry" := SalesLineTmp."System-Created Entry";
      SalesLine.VALIDATE("Sell-to Customer No.",SalesHeader."Sell-to Customer No.");
      SalesLine."Job No." := SalesLineTmp."Job No.";
      SalesLine.Type := SalesLineTmp.Type;
      IF SalesLine.Type = SalesLine.Type::" " THEN BEGIN  // Text Line
        SalesLine.Description := SalesLineTmp.Description;
        SalesLine.INSERT;
        EXIT;
      END;
      SalesLine.Element := SalesLineTmp.Element;
      SalesLine.VALIDATE("No.", SalesLineTmp."No.");
      // Rental Unit fields must be filled before validate Dim2, else "No." (account no.) will be modified
      SalesLine."Rental Unit" := SalesLineTmp."Rental Unit";
      SalesLine."Rental Package Date" := SalesLineTmp."Rental Package Date";
      SalesLine."Rental Unit Line Type" := SalesLineTmp."Rental Unit Line Type";
      SalesLine.VALIDATE("Shortcut Dimension 2 Code", SalesLineTmp."Shortcut Dimension 2 Code");
      SalesLine.VALIDATE(Quantity, (FactorQty * SalesLineTmp.Quantity));
      SalesLine.VALIDATE("Unit Price", (FactorAmt * SalesLineTmp."Unit Price"));
      SalesLine.VALIDATE("Line Discount %", SalesLineTmp."Line Discount %");
      SalesLine."Amount (LCY)" := SalesLine.Amount;
      SalesLine.VALIDATE("Unit of Measure Code", SalesLineTmp."Unit of Measure Code");
      SalesLine.VALIDATE("VAT Prod. Posting Group", SalesLineTmp."VAT Prod. Posting Group");
      SalesLine.VALIDATE("VAT Bus. Posting Group", SalesLineTmp."VAT Bus. Posting Group");
      SalesLine.Description := SalesLineTmp.Description;
      SalesLine."Relate to" := SalesLineTmp."Relate to";
      SalesLine."Rental Period" := SalesLineTmp."Rental Period";
      SalesLine."Rental Unit" := SalesLineTmp."Rental Unit";
      SalesLine."Rental Package Date" := SalesLineTmp."Rental Package Date";
      SalesLine."Rental Unit Line Type" := SalesLineTmp."Rental Unit Line Type";
      SalesLine."Plant Rental Split No." := SalesLineTmp."Plant Rental Split No.";  //Rental Package Rate Line: "Line No."
      SalesLine.UpdateUnitPrice(0);  //db, 12-09-03: extra argument CalledByFieldNo
      SalesLine.INSERT;
    END;

    LOCAL PROCEDURE UpdateRentalPackageLastInvoice@1210190010(SalesHeader@1100529003 : Record 36;SalesLineTmp@1100529002 : Record 37);
    VAR
      RentalPackage@1210190000 : Record 11012941;
      RentalRateLine@1100529000 : Record 11012942;
      HistRentalPackage@1100529001 : Record 11229848;
    BEGIN
      // SalesLineTmpRec."Plant Rental Split No." is used for "Line No." of the Rental Package Rate Line
      // SalesLineTmpRec."Alternative No." is used for "Invoice Run No." of the History
      IF RentalPackage.GET(SalesLineTmp."Job No.", SalesLineTmp."Rental Unit", SalesLineTmp."Rental Package Date") THEN BEGIN
        IF RentalPackage."Last Invoice No." <> SalesHeader."No." THEN BEGIN
          RentalPackage."Last Invoice No." := SalesHeader."No.";
          IF  SalesHeader."Document Type" = SalesHeader."Document Type"::"Credit Memo" THEN
            RentalPackage."Invoice Type" := RentalPackage."Invoice Type"::CreditMemo;
          RentalPackage.MODIFY;
          //
          IF HistRentalPackage.GET(
            RentalPackage."Project No.", RentalPackage."Rental Unit",
            RentalPackage."Starting Date", SalesLineTmp."Alternative No.")
          THEN BEGIN
            HistRentalPackage."Invoice No." := SalesHeader."No.";
            HistRentalPackage."Invoiced Until" := RentalPackage."Invoiced Until";
            IF SalesHeader."Document Type" = SalesHeader."Document Type"::"Credit Memo" THEN
              HistRentalPackage."Invoice Type" := HistRentalPackage."Invoice Type"::CreditMemo;
            HistRentalPackage.MODIFY;
          END
        END;
        IF RentalRateLine.GET(
          RentalPackage."Project No.", RentalPackage."Rental Unit",
          RentalPackage."Starting Date", SalesLineTmp."Plant Rental Split No.")
        THEN BEGIN
          RentalRateLine."Last Invoice No." := SalesHeader."No.";
          IF  SalesHeader."Document Type" = SalesHeader."Document Type"::"Credit Memo" THEN
            RentalRateLine."Invoice Type" := RentalRateLine."Invoice Type"::CreditMemo;
          RentalRateLine.MODIFY;
        END
      END;
    END;

    PROCEDURE WriteHistoryHead@1100529004(ProjectNo@1100529000 : Code[20];RentalUnit@1100529001 : Code[20];StartDate@1100529002 : Date;DelayedIndexInvoice@1100529004 : Boolean) : Integer;
    VAR
      RentalPackage@1100529003 : Record 11012941;
      HistRentalPackage@1100529005 : Record 11229848;
      InvoiceRunNo@1100529007 : Integer;
    BEGIN
      RentalPackage.GET(ProjectNo, RentalUnit, StartDate);

      HistRentalPackage.SETRANGE("Project No.", ProjectNo);
      HistRentalPackage.SETRANGE("Rental Unit", RentalUnit);
      HistRentalPackage.SETRANGE("Starting Date", StartDate);
      IF HistRentalPackage.FINDLAST THEN
        InvoiceRunNo := HistRentalPackage."Invoice Run No." + 1
      ELSE
        InvoiceRunNo := 1;

      HistRentalPackage.RESET;
      HistRentalPackage.INIT;
      HistRentalPackage.TRANSFERFIELDS(RentalPackage, TRUE);
      HistRentalPackage."Invoice Run No." := InvoiceRunNo;
      HistRentalPackage."Date/Time Created" := CURRENTDATETIME;
      HistRentalPackage."Invoice Status" := HistRentalPackage."Invoice Status"::"Temporary";
      HistRentalPackage."Invoice Type" :=  HistRentalPackage."Invoice Type"::Invoice;
      IF DelayedIndexInvoice THEN BEGIN
        HistRentalPackage."Delayed Index. Invoice" := TRUE;
        HistRentalPackage."Invoiced Until" := 0D;
      END;
      HistRentalPackage.INSERT;

      EXIT(InvoiceRunNo);
    END;

    PROCEDURE WriteHistoryLine@1100529000(RentalRateLine@1100529000 : Record 11012942;InvoiceRunNo@1100529001 : Integer;InvoicedQuantity@1100529002 : Decimal;UnitOfMeasure@1100529003 : Code[10];InvoicedAmount@1100529004 : Decimal;StartInvoicedPeriod@1100529005 : Date;EndInvoicedPeriod@1100529007 : Date;SplitNo@1100529008 : Integer;PeriodAmount@1100529009 : Decimal;YearAmount@1100529010 : Decimal);
    VAR
      HistRentalRateLine@1100529006 : Record 11229849;
    BEGIN
      HistRentalRateLine.INIT;
      HistRentalRateLine.TRANSFERFIELDS(RentalRateLine, TRUE);  //RentalRateLine is already update, so also new "Invoiced until"
      HistRentalRateLine."Invoice Run No." := InvoiceRunNo;
      HistRentalRateLine."Split No." := SplitNo;
      HistRentalRateLine."Invoiced Quantity" := InvoicedQuantity;
      HistRentalRateLine."Unit of Measure" := UnitOfMeasure;
      HistRentalRateLine."Invoiced Amount" := InvoicedAmount;
      HistRentalRateLine."Rental Starting Date" := StartInvoicedPeriod;
      HistRentalRateLine."Rental Ending Date" := EndInvoicedPeriod;
      HistRentalRateLine."Period Amount" := PeriodAmount;
      HistRentalRateLine."Year Amount" := YearAmount;
      HistRentalRateLine.INSERT;
    END;

    BEGIN
    {
      Functions for Rental Unit Invoicing.
      DP00617: Several modifications
    }
    END.
  }
}

