OBJECT Table 11125535 Inspection SMF
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               "Sequence No." := GetFirstFreeSequenceNo;

               ProjectSiteMgt.LogInspectionSMF(1, Rec); //**4PS.n
             END;

    OnDelete=VAR
               lvSiteMgtInspecTextRec@1100525000 : Record 11125536;
             BEGIN
               lvSiteMgtInspecTextRec.SETRANGE("Project No.", "Project No.");
               lvSiteMgtInspecTextRec.SETRANGE("Inspection Code", "Inspection Code");
               lvSiteMgtInspecTextRec.SETRANGE(Date, Date);
               lvSiteMgtInspecTextRec.SETRANGE("Sequence No.", "Sequence No.");
               lvSiteMgtInspecTextRec.DELETEALL;

               ProjectSiteMgt.LogInspectionSMF(2, Rec); //**4PS.n
             END;

    OnRename=BEGIN
               ProjectSiteMgt.LogInspectionSMF(3, Rec); //**4PS.n
             END;

    CaptionML=[ENU=Inspection (Site Manager Form);
               NOR=Inspeksjon (prosjektlederskjema);
               SVE=Inspektion (byggledarformul„r)];
    LookupPageID=Page11125543;
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr];
                                                   NotBlank=Yes }
    { 20  ;   ;Inspection Code     ;Code10        ;TableRelation="Project Inspection SMF"."Inspection Code" WHERE (Project No.=FIELD(Project No.));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS(Description, "Inspect on", Frequence, "Way of Inspection", "Inspection by", Norm, Tolerance);
                                                              END;

                                                   CaptionML=[ENU=Inspection Code;
                                                              NOR=Inspeksjonskode;
                                                              SVE=Inspektionskod];
                                                   NotBlank=Yes }
    { 30  ;   ;Date                ;Date          ;CaptionML=[ENU=Date;
                                                              NOR=Dato;
                                                              SVE=Datum];
                                                   NotBlank=Yes }
    { 40  ;   ;Sequence No.        ;Integer       ;CaptionML=[ENU=Sequence No.;
                                                              NOR=Sekvensnummer;
                                                              SVE=Sekvensnr] }
    { 100 ;   ;Description         ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Inspection SMF".Description WHERE (Project No.=FIELD(Project No.),
                                                                                                                  Inspection Code=FIELD(Inspection Code)));
                                                   CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning];
                                                   Editable=No }
    { 110 ;   ;Inspect on          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Inspection SMF"."Inspect on" WHERE (Project No.=FIELD(Project No.),
                                                                                                                   Inspection Code=FIELD(Inspection Code)));
                                                   CaptionML=[ENU=Inspect on;
                                                              NOR=Inspiseksjon den;
                                                              SVE=Inspekterad den];
                                                   Editable=No }
    { 120 ;   ;Frequence           ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Inspection SMF".Frequence WHERE (Project No.=FIELD(Project No.),
                                                                                                                Inspection Code=FIELD(Inspection Code)));
                                                   CaptionML=[ENU=Frequence;
                                                              NOR=Frekvens;
                                                              SVE=F”rekomst];
                                                   Editable=No }
    { 130 ;   ;Way of Inspection   ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Inspection SMF"."Way of Inspection" WHERE (Project No.=FIELD(Project No.),
                                                                                                                          Inspection Code=FIELD(Inspection Code)));
                                                   CaptionML=[ENU=Way of Inspection;
                                                              NOR=Type inspeksjon;
                                                              SVE=Inspektionss„tt];
                                                   Editable=No }
    { 140 ;   ;Inspection by       ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Inspection SMF"."Inspection by" WHERE (Project No.=FIELD(Project No.),
                                                                                                                      Inspection Code=FIELD(Inspection Code)));
                                                   CaptionML=[ENU=Inspection by;
                                                              NOR=Inspisert av;
                                                              SVE=Inspekterad av];
                                                   Editable=No }
    { 150 ;   ;Norm                ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Inspection SMF".Norm WHERE (Project No.=FIELD(Project No.),
                                                                                                           Inspection Code=FIELD(Inspection Code)));
                                                   CaptionML=[ENU=Norm;
                                                              NOR=Norm/toleranse;
                                                              SVE=Norm/tolerans];
                                                   Editable=No }
    { 160 ;   ;Tolerance           ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Inspection SMF".Tolerance WHERE (Project No.=FIELD(Project No.),
                                                                                                                Inspection Code=FIELD(Inspection Code)));
                                                   CaptionML=ENU=Tolerance;
                                                   Editable=No }
    { 200 ;   ;Entry Number        ;Text50        ;CaptionML=[ENU=Entry Number;
                                                              NOR=Transaksjonsnr;
                                                              SVE=Transaktionsnummer] }
    { 210 ;   ;Comment             ;Text80        ;CaptionML=[ENU=Comment;
                                                              NOR=Merknad;
                                                              SVE=Kommentar] }
    { 230 ;   ;Inspection Text     ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("InspectionText Line (SMF)" WHERE (Project No.=FIELD(Project No.),
                                                                                                        Inspection Code=FIELD(Inspection Code),
                                                                                                        Date=FIELD(Date),
                                                                                                        Sequence No.=FIELD(Sequence No.)));
                                                   CaptionML=[ENU=Inspection Text;
                                                              NOR=Inspeksjonstekst;
                                                              SVE=Inspektionstext];
                                                   Editable=No }
    { 240 ;   ;Place of Measuring  ;Text80        ;CaptionML=[ENU=Place of Measuring;
                                                              NOR=Sted for m†ling;
                                                              SVE=Plats f”r m†tt] }
    { 250 ;   ;Result Measuring    ;Text80        ;CaptionML=[ENU=Result Measuring;
                                                              NOR=Resultat av m†ling;
                                                              SVE=Resultatm„tning] }
    { 260 ;   ;Measuring Instrument;Text80        ;CaptionML=[ENU=Measuring Instrument;
                                                              NOR=M†leinstrument;
                                                              SVE=M„tinstrument] }
    { 270 ;   ;In Accordance with Demand;Option   ;CaptionML=[ENU=In Accordance with Demand;
                                                              NOR=I samsvar med ettersp›rsel;
                                                              SVE=I enlighet med efterfr†gan];
                                                   OptionCaptionML=[ENU=Not Applicable,Yes,No;
                                                                    NOR=Ikke tilgjengelig,Ja,Nei;
                                                                    SVE=Ej till„mpligt,Ja,Nej];
                                                   OptionString=Not Applicable,Yes,No }
  }
  KEYS
  {
    {    ;Project No.,Inspection Code,Date,Sequence No.;
                                                   Clustered=Yes }
    {    ;Project No.,Date,Inspection Code         }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ProjectSiteMgt@1100525000 : Codeunit 11012282;

    PROCEDURE EmptyLine@1100525000() : Boolean;
    BEGIN
      IF ("Entry Number" = '') AND
         (Comment = '') AND
         ("In Accordance with Demand" = "In Accordance with Demand"::"Not Applicable") AND
         ("Place of Measuring" = '') AND
         ("Result Measuring" = '') AND
         ("Measuring Instrument" = '')
      THEN BEGIN
        CALCFIELDS("Inspection Text");
        IF NOT "Inspection Text" THEN
          EXIT(TRUE);
      END;

      EXIT(FALSE);
    END;

    PROCEDURE CreateProjectInspections@1100525008(ProjectNo@1100525000 : Code[20];WorkingDate@1100525003 : Date;AddIfInspectionsExist@1100528400 : Boolean);
    VAR
      Job@1100528401 : Record 11072003;
    BEGIN
      IF (ProjectNo = '') OR (WorkingDate = 0D) THEN
        EXIT;

      IF NOT Job.GET(ProjectNo) THEN
        EXIT;

      IF Job."Autom. Get Project Inspections" AND
         (Job."Project Status" = Job."Project Status"::Production)
      THEN
        CreateProjectInspectionsForPeriod(ProjectNo, WorkingDate, AddIfInspectionsExist)
      ELSE
        CreateProjectInspectionsForOneDate(ProjectNo, WorkingDate, AddIfInspectionsExist);
    END;

    LOCAL PROCEDURE CreateProjectInspectionsForPeriod@1100528401(ProjectNo@1100525000 : Code[20];WorkingDate@1100528402 : Date;AddIfInspectionsExist@1100528401 : Boolean);
    VAR
      SiteManagerFormSetup@1100528403 : Record 11125525;
      StartDate@1100525003 : Date;
      EndDate@1100525002 : Date;
      Date@1100528400 : Record 2000000007;
    BEGIN
      IF NOT SiteManagerFormSetup.GET THEN
        EXIT;

      StartDate := CALCDATE(SiteManagerFormSetup."Period before Work Date", WorkingDate);
      EndDate := CALCDATE(SiteManagerFormSetup."Period after Work Date", WorkingDate);
      Date.SETRANGE("Period Type", Date."Period Type"::Date);
      Date.SETRANGE("Period Start", StartDate, EndDate);
      IF Date.FINDSET THEN
        REPEAT
          CreateProjectInspectionsForOneDate(ProjectNo, Date."Period Start", AddIfInspectionsExist);
        UNTIL Date.NEXT = 0;
    END;

    LOCAL PROCEDURE CreateProjectInspectionsForOneDate@1100528402(ProjectNo@1100525000 : Code[20];InspectionDate@1100525003 : Date;AddIfInspectionsExist@1100528400 : Boolean);
    VAR
      ProjectInspectionSMF@1100525001 : Record 11125534;
      InspectionSMF@1100525002 : Record 11125535;
    BEGIN
      IF InspectionDate = 0D THEN
        EXIT;

      InspectionSMF.SETCURRENTKEY("Project No.", Date);
      InspectionSMF.SETRANGE("Project No.", ProjectNo);
      InspectionSMF.SETRANGE(Date, InspectionDate);
      IF (NOT InspectionSMF.ISEMPTY) AND
         (NOT AddIfInspectionsExist)
      THEN
        EXIT;

      ProjectInspectionSMF.RESET;
      ProjectInspectionSMF.SETRANGE("Project No.", ProjectNo);
      IF ProjectInspectionSMF.FINDSET THEN BEGIN
        InspectionSMF.SETCURRENTKEY("Project No.", "Inspection Code", Date, "Sequence No."); //Key1
        REPEAT
          InspectionSMF.SETRANGE("Inspection Code", ProjectInspectionSMF."Inspection Code");
          IF InspectionSMF.ISEMPTY THEN BEGIN
            InspectionSMF.INIT;
            InspectionSMF."Project No." := ProjectNo;
            InspectionSMF.Date := InspectionDate;
            InspectionSMF."Inspection Code" := ProjectInspectionSMF."Inspection Code";
            InspectionSMF.INSERT(TRUE);
          END;
        UNTIL ProjectInspectionSMF.NEXT = 0;
      END;
    END;

    PROCEDURE HasDocumentLinks@1100525004() : Boolean;
    VAR
      DocumentLinkMgt@1100525000 : Codeunit 11012401;
      RecRef@1100525001 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      EXIT(DocumentLinkMgt.EntityHasDocumentLinks(RecRef));
    END;

    LOCAL PROCEDURE GetFirstFreeSequenceNo@1100528400() : Integer;
    VAR
      InspectionSMF@1100528400 : Record 11125535;
    BEGIN
      InspectionSMF.SETRANGE("Project No.", "Project No.");
      InspectionSMF.SETRANGE("Inspection Code", "Inspection Code");
      InspectionSMF.SETRANGE(Date, Date);
      IF InspectionSMF.FINDLAST THEN
        EXIT(InspectionSMF."Sequence No." + 1);
      EXIT(1);
    END;

    BEGIN
    END.
  }
}

