OBJECT Page 5072 Rlshp. Mgt. Comment Sheet
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW111.00,4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 5050=m;
    CaptionML=[ENU=Rlshp. Mgt. Comment Sheet;
               NOR=Forbindelser - merknadsark;
               SVE=Aff„rsst”d kommentarsark];
    LinksAllowed=No;
    SourceTable=Table5061;
    DelayedInsert=Yes;
    DataCaptionFields=No.;
    PageType=List;
    AutoSplitKey=Yes;
    OnOpenPage=BEGIN
                 CurrPage.EDITABLE(CommentIsEditable);//**4PS.n
               END;

    OnClosePage=BEGIN
                  //**4PS.sn
                  IF UpdateMain = TRUE THEN BEGIN
                    IF "Table Name" = "Table Name"::Contact THEN BEGIN
                      ContactRec.GET("No.");
                      ContactRec.MODIFY(TRUE);
                    END;
                  END;
                  //**4PS.en
                END;

    OnNewRecord=BEGIN
                  SetUpNewLine;
                END;

    OnInsertRecord=BEGIN
                     //**4PS.sn
                     IF NOT CommentIsEditable THEN
                       EXIT(FALSE);
                     UpdateMain := TRUE;
                     //**4PS.en
                   END;

    OnModifyRecord=BEGIN
                     //**4PS.sn
                     IF NOT CommentIsEditable THEN
                       EXIT(FALSE);
                     UpdateMain := TRUE;
                     //**4PS.en
                   END;

    OnDeleteRecord=BEGIN
                     //**4PS.sn
                     IF NOT CommentIsEditable THEN
                       EXIT(FALSE);
                     UpdateMain := TRUE;
                     //**4PS.en
                   END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1210190000;1 ;Action    ;
                      CaptionML=[ENU=&Text Editor;
                                 SVE=&Texteditor];
                      Promoted=Yes;
                      Image=SNInfo;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 TextEditor@1210190001 : Codeunit 11012001;
                               BEGIN
                                 //**4PS
                                 IF CurrPage.EDITABLE THEN
                                   TextEditor.RlshpMgtComment(Rec);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the date the comment was created.;
                           NOR=Angir dato for oppretting av merknaden.;
                           SVE=Anger det datum d† kommentaren skapades.];
                ApplicationArea=#RelationshipMgmt;
                SourceExpr=Date }

    { 1100525000;2;Field  ;
                SourceExpr="Comment Code";
                Visible=FALSE }

    { 6   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the comment itself.;
                           NOR=Angir selve merknaden.;
                           SVE=Anger sj„lva kommentaren.];
                ApplicationArea=#RelationshipMgmt;
                SourceExpr=Comment }

    { 4   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the code for the comment.;
                           NOR=Angir koden for merknaden.;
                           SVE=Anger koden f”r kommentaren.];
                ApplicationArea=#RelationshipMgmt;
                SourceExpr=Code;
                Visible=FALSE;
                OnValidate=BEGIN
                             CodeOnAfterValidate;
                           END;

                OnLookup=VAR
                           TxtRec@1210190000 : Record 7;
                         BEGIN
                           //**4PS
                           TxtRec.Code := Code;
                           IF PAGE.RUNMODAL(PAGE::"Standard Text Codes", TxtRec) = ACTION::LookupOK THEN BEGIN
                             IF  (TxtRec.Code <> Code) AND (TxtRec.Code <> '') THEN BEGIN
                               Code := TxtRec.Code;
                               GetStandardText;
                             END;
                           END;
                         END;
                          }

    { 1100485000;2;Field  ;
                SourceExpr="Created by";
                Visible=FALSE }

    { 1100528700;2;Field  ;
                SourceExpr="Line Break";
                Visible=FALSE }

  }
  CODE
  {
    VAR
      ContactRec@1100485012 : Record 5050;
      UpdateMain@1100485000 : Boolean;

    LOCAL PROCEDURE CommentIsEditable@1100528400() : Boolean;
    VAR
      CentralDataMgt@1100528400 : Codeunit 11012010;
      CompanyRelationsCentralMgt@1100528401 : Record 11012059;
      FPSLicenseManagement@1100527400 : Codeunit 11229289;
    BEGIN
      //**4PS.sn
      IF NOT FPSLicenseManagement.LicenseAndReadPermissionForTable(CompanyRelationsCentralMgt) THEN
        EXIT(FALSE);

      IF "Table Name" = "Table Name"::Contact THEN
        EXIT(CentralDataMgt.ContactCommentIsEditable("No."))
      ELSE
        EXIT(TRUE);
      //**4PS.en
    END;

    PROCEDURE GetStandardText@1210190000();
    VAR
      TxtRec@1210190001 : Record 7;
      LineTxtRec@1210190002 : Record 280;
      RlshpMgtCommentLine@1210190003 : Record 5061;
      CurrLino@1210190000 : Integer;
      lvText11012000@1210190004 : TextConst 'ENU=Not enough space to insert text lines, not all text lines are added.;SVE=Det finns inte tillr„ckligt med utrymme f”r att infoga textrader. Inte alla textrader l„ggs till.';
    BEGIN
      //**4PS
      IF Code = '' THEN
        EXIT;

      CurrPage.SAVERECORD;
      CurrLino := "Line No.";
      IF TxtRec.GET(Code) THEN BEGIN
        IF (TxtRec.Description <> '') THEN BEGIN
          Comment := COPYSTR(TxtRec.Description, 1, MAXSTRLEN(Comment));
          MODIFY;
        END;
        LineTxtRec.SETRANGE("Table Name", LineTxtRec."Table Name"::"Standard Text");
        LineTxtRec.SETRANGE("No.", Code);
        IF LineTxtRec.FIND('-') THEN BEGIN
          REPEAT
            RlshpMgtCommentLine := Rec;
            RlshpMgtCommentLine.Comment := COPYSTR(LineTxtRec.Text, 1, MAXSTRLEN(RlshpMgtCommentLine.Comment));
            RlshpMgtCommentLine."Line No." := CurrLino + 1;
            RlshpMgtCommentLine.Date := 0D;
            RlshpMgtCommentLine.Code := '';
            IF NOT RlshpMgtCommentLine.INSERT THEN BEGIN
              MESSAGE(lvText11012000);
              EXIT;
            END;
            CurrLino := RlshpMgtCommentLine."Line No.";
          UNTIL LineTxtRec.NEXT = 0;
        END;
      END;
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE CodeOnAfterValidate@19050293();
    BEGIN
      //**4PS
      IF Code <> xRec.Code THEN
        GetStandardText;
    END;

    BEGIN
    {
      ¸ 4PS
      Button Text Editor added
      4PS, 29-04-10, Permission M added for table 5050
    }
    END.
  }
}

