OBJECT Table 11229946 DPT Portal View Document Type
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ValidateFields;
             END;

    OnModify=BEGIN
               ValidateFields;
             END;

    OnDelete=BEGIN
               CALCFIELDS("Used in Project", "Used in Service Location");
               //IF ("Source Type" = "Source Type"::Project) AND ("Used in Project") THEN
               //  FIELDERROR("Used in Project");
               //IF ("Source Type" = "Source Type"::ServiceLocation) AND ("Used in Service Location") THEN
               //  FIELDERROR("Used in Service Location");
             END;

    CaptionML=ENU=DPT Portal View Document Type;
  }
  FIELDS
  {
    { 10  ;   ;Source Type         ;Option        ;CaptionML=[ENU=Source Type;
                                                              NOR=Kildetype;
                                                              SVE=Ursprungstyp];
                                                   OptionCaptionML=[ENU=" ,Project,Service Location";
                                                                    SVE=,Project,Serviceplats];
                                                   OptionString=[ ,Project,ServiceLocation];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 20  ;   ;Subsite Code        ;Code20        ;TableRelation="DPT Default Subsite".Code WHERE (Source Type=FIELD(Source Type));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Subsite Description", "Subsite Structure");
                                                              END;

                                                   CaptionML=ENU=Subsite Code;
                                                   NotBlank=Yes }
    { 21  ;   ;Subsite Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Default Subsite".Description WHERE (Source Type=FIELD(Source Type),
                                                                                                               Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Subsite Description;
                                                   Editable=No }
    { 22  ;   ;Subsite Structure   ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Default Subsite"."Subsite Structure" WHERE (Source Type=FIELD(Source Type),
                                                                                                                       Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Subsite Structure;
                                                   OptionCaptionML=ENU=1 Document Library,Document Library per Document Type,Permission per Document Type;
                                                   OptionString=OneDocLibrary,DocLibraryPerDocType,PermissionPerDocType;
                                                   Editable=No }
    { 23  ;   ;Used in Project     ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("DPT Subsite Project" WHERE (Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Used in Project;
                                                   Editable=No }
    { 24  ;   ;Used in Service Location;Boolean   ;FieldClass=FlowField;
                                                   CalcFormula=Exist("DPT Subsite ServLoc" WHERE (Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Used in Service;
                                                   Editable=No }
    { 30  ;   ;View Type           ;Option        ;OnValidate=BEGIN
                                                                ValidateFields;
                                                              END;

                                                   CaptionML=ENU=View Type;
                                                   OptionCaptionML=ENU=" ,Company,Document,Subsite";
                                                   OptionString=[ ,Company,Document,Subsite];
                                                   NotBlank=Yes }
    { 40  ;   ;Document Type       ;Code10        ;TableRelation="DPT Default Subsite Doc Type"."Document Type" WHERE (Source Type=FIELD(Source Type),
                                                                                                                       Subsite Code=FIELD(Subsite Code));
                                                   OnValidate=BEGIN
                                                                ValidateFields;
                                                                CALCFIELDS("Document Type Description");
                                                              END;

                                                   CaptionML=[ENU=Document Type;
                                                              NOR=Dokumenttype;
                                                              SVE=Dokumenttyp] }
    { 41  ;   ;Document Type Description;Text50   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Document Type".Description WHERE (Type=FIELD(Document Type)));
                                                   CaptionML=[ENU=Document Type Description;
                                                              NOR=Dokumenttype, Beskrivelse;
                                                              SVE=Dokumenttyp, beskrivning];
                                                   Editable=No }
    { 50  ;   ;Portal View Code    ;Code20        ;TableRelation="DPT Portal View".Code WHERE (Source Type=FIELD(Source Type));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Portal View Description");
                                                              END;

                                                   CaptionML=ENU=Portal View Code;
                                                   NotBlank=Yes }
    { 51  ;   ;Portal View Description;Text50     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Portal View".Description WHERE (Source Type=FIELD(Source Type),
                                                                                                           Code=FIELD(Portal View Code)));
                                                   CaptionML=ENU=Portal View Description;
                                                   Editable=No }
    { 100 ;   ;Default View        ;Boolean       ;OnValidate=BEGIN
                                                                CheckDefaultView;
                                                              END;

                                                   CaptionML=ENU=Default View }
  }
  KEYS
  {
    {    ;Source Type,Subsite Code,View Type,Document Type,Portal View Code;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE CreateAllPortalViews@1100525002();
    VAR
      DPTPortalView@1100525003 : Record 11229945;
      DPTDefaultSubsiteDocType@1100525000 : Record 11229942;
      DPTPortalViewDocumentType@1100525001 : Record 11229946;
      ViewType@1100525002 : Integer;
    BEGIN
      DPTPortalView.SETRANGE("Source Type", "Source Type");
      IF (NOT DPTPortalView.FINDSET) THEN
        EXIT;
      DPTDefaultSubsiteDocType.SETRANGE("Source Type", "Source Type");
      IF (NOT DPTDefaultSubsiteDocType.FINDSET) THEN
        EXIT;

      REPEAT
        DPTDefaultSubsiteDocType.FINDSET;
        REPEAT
          DPTDefaultSubsiteDocType.CALCFIELDS("Subsite Structure");
          FOR ViewType := 1 TO 3 DO BEGIN
            DPTPortalViewDocumentType.INIT;
            DPTPortalViewDocumentType."Source Type" := DPTDefaultSubsiteDocType."Source Type";
            DPTPortalViewDocumentType."Subsite Code" := DPTDefaultSubsiteDocType."Subsite Code";
            DPTPortalViewDocumentType."View Type" := ViewType;
            DPTPortalViewDocumentType."Portal View Code" := DPTPortalView.Code;
            CASE TRUE OF
              (ViewType = 1): BEGIN //Company
                DPTPortalViewDocumentType."Document Type" := '';
                IF (DPTPortalViewDocumentType.INSERT(TRUE)) THEN;
              END;
              (DPTDefaultSubsiteDocType."Subsite Structure" = DPTDefaultSubsiteDocType."Subsite Structure"::OneDocLibrary) AND
              (ViewType = 2): BEGIN //Document
                DPTPortalViewDocumentType."Document Type" := '';
                IF (DPTPortalViewDocumentType.INSERT(TRUE)) THEN;
              END;
              (DPTDefaultSubsiteDocType."Subsite Structure" <> DPTDefaultSubsiteDocType."Subsite Structure"::OneDocLibrary) AND
              (ViewType = 3): BEGIN //Subsite
                DPTPortalViewDocumentType."Document Type" := DPTDefaultSubsiteDocType."Document Type";
                IF (DPTPortalViewDocumentType.INSERT(TRUE)) THEN;
              END;
            END;
          END;
        UNTIL (DPTDefaultSubsiteDocType.NEXT = 0)
      UNTIL (DPTPortalView.NEXT = 0);
    END;

    LOCAL PROCEDURE "---Locals---"@1100525003();
    BEGIN
    END;

    LOCAL PROCEDURE CheckDefaultView@1100525004();
    VAR
      DPTPortalViewDocumentType@1100525000 : Record 11229946;
    BEGIN
      IF (NOT "Default View") THEN
        EXIT;
      DPTPortalViewDocumentType.SETRANGE("Source Type", "Source Type");
      DPTPortalViewDocumentType.SETRANGE("Subsite Code", "Subsite Code");
      DPTPortalViewDocumentType.SETRANGE("View Type", "View Type");
      DPTPortalViewDocumentType.SETRANGE("Document Type", "Document Type");
      DPTPortalViewDocumentType.SETRANGE("Default View", TRUE);
      IF (DPTPortalViewDocumentType.COUNT > 0) THEN
        FIELDERROR("Default View");
    END;

    LOCAL PROCEDURE ValidateFields@1100525001();
    BEGIN
      CALCFIELDS("Subsite Structure");
      IF ("Subsite Structure" = "Subsite Structure"::OneDocLibrary) OR

         ("View Type" = "View Type"::Company) OR
         ("View Type" = "View Type"::Document)
      THEN
        "Document Type" := '';
      IF (("Subsite Structure" <> "Subsite Structure"::OneDocLibrary) AND ("View Type" = "View Type"::Document)) OR
         (("Subsite Structure" = "Subsite Structure"::OneDocLibrary) AND ("View Type" = "View Type"::Subsite))
      THEN
        FIELDERROR("View Type");
    END;

    BEGIN
    END.
  }
}

