OBJECT Table 11012789 Sales Rental Removal Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               GetSalesHeader;
               "Currency Code" := SalesHeader."Currency Code";
             END;

    OnDelete=VAR
               SalesLine@1100525000 : Record 37;
             BEGIN
               TESTFIELD(Status, Status::Open);

               IF "Generated Sales Line No." <> 0 THEN BEGIN
                 SalesLine.SETRANGE("Document Type", SalesLine."Document Type"::Order);
                 SalesLine.SETRANGE("Document No.", "Document No.");
                 SalesLine.SETRANGE("Line No.", "Generated Sales Line No.");
                 IF SalesLine.FINDFIRST THEN
                   SalesLine.DELETE(TRUE);
               END;
             END;

    CaptionML=[ENU=Rental Removal Line;
               NOR=Fjerningsrad for Leie;
               SVE=Gratis hyra rad];
  }
  FIELDS
  {
    { 20  ;   ;Document No.        ;Code20        ;TableRelation="Sales Header".No. WHERE (Document Type=CONST(Order));
                                                   CaptionML=[ENU=Document No.;
                                                              NOR=Dokumentnr.;
                                                              SVE=Dokumentnr];
                                                   Editable=No }
    { 30  ;   ;Document Line No.   ;Integer       ;TableRelation="Sales Line"."Line No." WHERE (Document Type=CONST(Order),
                                                                                                Document No.=FIELD(Document No.),
                                                                                                Plant Type=FILTER(<>''));
                                                   CaptionML=[ENU=Order Line No.;
                                                              NOR=Ordrelinjenr.;
                                                              SVE=Orderradnr];
                                                   Editable=No }
    { 40  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr];
                                                   Editable=No }
    { 50  ;   ;Type                ;Option        ;InitValue=Removal;
                                                   OnValidate=BEGIN
                                                                IF Type = Type::Sale THEN BEGIN
                                                                  TESTFIELD("Plant Type"); //Items can not be sold here
                                                                  "Rental Ending Date" := 0D;
                                                                END ELSE BEGIN
                                                                  IF "Rental Ending Date" = 0D THEN
                                                                    "Rental Ending Date" := "Removal Date";
                                                                  VALIDATE("Unit Price", 0);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Typ];
                                                   OptionCaptionML=[ENU=Removal,Sales;
                                                                    NOR=Fjerning,Salg;
                                                                    SVE=Borttagning,FîrsÑljning];
                                                   OptionString=Removal,Sale }
    { 60  ;   ;Arrival Date        ;Date          ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Sales Line"."Arrival Date" WHERE (Document Type=CONST(Order),
                                                                                                         Document No.=FIELD(Document No.),
                                                                                                         Line No.=FIELD(Document Line No.)));
                                                   CaptionML=[ENU=Arrival Date;
                                                              NOR=Ankomstdato;
                                                              SVE=Ankomstdatum];
                                                   Editable=No }
    { 70  ;   ;Rental Starting Date;Date          ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Sales Line"."Rental Starting Date" WHERE (Document Type=CONST(Order),
                                                                                                                 Document No.=FIELD(Document No.),
                                                                                                                 Line No.=FIELD(Document Line No.)));
                                                   CaptionML=[ENU=Rental Starting Date;
                                                              NOR=Startdato for Leie;
                                                              SVE=Startdatum fîr uthyrning];
                                                   Editable=No }
    { 80  ;   ;Removal Date        ;Date          ;OnValidate=BEGIN
                                                                CALCFIELDS("Arrival Date", "Rental Starting Date");
                                                                IF ("Removal Date" > 0D) AND ("Removal Date" < "Arrival Date") THEN
                                                                  FIELDERROR("Removal Date", STRSUBSTNO(Text001,FIELDCAPTION("Arrival Date")));
                                                                IF ("Rental Ending Date" > "Removal Date") OR ("Rental Ending Date" = 0D) OR ("Rental Ending Date" = xRec."Removal Date") THEN
                                                                  VALIDATE("Rental Ending Date", "Removal Date");
                                                              END;

                                                   CaptionML=[ENU=Removal Date;
                                                              NOR=Fjerningsdato;
                                                              SVE=Borttagningsdatum] }
    { 90  ;   ;Rental Ending Date  ;Date          ;OnValidate=BEGIN
                                                                CALCFIELDS("Arrival Date", "Rental Starting Date");
                                                                IF Type = Type::Sale THEN
                                                                  "Rental Ending Date" := 0D
                                                                ELSE BEGIN
                                                                  IF "Rental Ending Date" > "Removal Date" THEN
                                                                    FIELDERROR("Rental Ending Date", STRSUBSTNO(Text002,FIELDCAPTION("Removal Date")));
                                                                  IF ("Rental Ending Date" > 0D) AND ("Rental Ending Date" < "Rental Starting Date") THEN
                                                                    FIELDERROR("Rental Ending Date", STRSUBSTNO(Text001,FIELDCAPTION("Rental Starting Date")));
                                                                  IF ("Rental Ending Date" > 0D) AND ("Rental Ending Date" < "Arrival Date") THEN
                                                                    FIELDERROR("Rental Ending Date", STRSUBSTNO(Text001,FIELDCAPTION("Arrival Date")));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Rental Ending Date;
                                                              NOR=Sluttdato for Leleie;
                                                              SVE=Slutdatum fîr uthyrning] }
    { 100 ;   ;Arrived Quantity    ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Sales Line".Quantity WHERE (Document Type=CONST(Order),
                                                                                                   Document No.=FIELD(Document No.),
                                                                                                   Line No.=FIELD(Document Line No.)));
                                                   CaptionML=[ENU=Arrived Quantity;
                                                              NOR=Antall Ankommet;
                                                              SVE=Antal anlÑnt];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 110 ;   ;Removed Quantity    ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Sales Rental Removal Line".Quantity WHERE (Document No.=FIELD(Document No.),
                                                                                                               Document Line No.=FIELD(Document Line No.)));
                                                   CaptionML=[ENU=Removed Quantity;
                                                              NOR=Fjernet antall;
                                                              SVE=Borrtaget antal];
                                                   Editable=No }
    { 120 ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD(Quantity);
                                                                TESTFIELD(Status, Status::Open);
                                                                IF CurrFieldNo = FIELDNO(Quantity) THEN BEGIN
                                                                  CALCFIELDS("Arrived Quantity","Removed Quantity");
                                                                  IF Quantity > "Arrived Quantity" - ("Removed Quantity" - xRec.Quantity) THEN  //current line not count
                                                                    ERROR(Text004);
                                                                END;
                                                                IF Type = Type::Sale THEN
                                                                  VALIDATE("Line Amount");
                                                              END;

                                                   CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   DecimalPlaces=0:2 }
    { 130 ;   ;Status              ;Option        ;InitValue=Open;
                                                   CaptionML=[ENU=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=[ENU=Open,Released,Processed;
                                                                    NOR=èpen,Frigitt,Behandlet;
                                                                    SVE=ôppen,SlÑppt,Behandlad];
                                                   OptionString=Open,Released,Processed;
                                                   Editable=No }
    { 135 ;   ;Removal Order Type  ;Option        ;CaptionML=[ENU=Removal Order Type;
                                                              NOR=Fjerningsordretype;
                                                              SVE=Borttagning av ordertyp];
                                                   OptionCaptionML=[ENU=,Plant Order,Exit-order;
                                                                    NOR=,Maskinordre,Sluttordre;
                                                                    SVE=,Maskinorder,Slutorder];
                                                   OptionString=,Plant Order,Exit-order;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 140 ;   ;Removal Order No.   ;Code20        ;TableRelation=IF (Removal Order Type=CONST(Plant Order)) "Plant Order"
                                                                 ELSE IF (Removal Order Type=CONST(Exit-order)) "Exit Order";
                                                   CaptionML=[ENU=Removal Order;
                                                              NOR=Fjerningsordre;
                                                              SVE=Uttagsorder];
                                                   Editable=No }
    { 145 ;   ;Removal Order Line No.;Integer     ;CaptionML=[ENU=Removal Order Line No.;
                                                              NOR=Fjerning Ordreradnr.;
                                                              SVE=Borttagning serviceradnr];
                                                   Editable=No }
    { 150 ;   ;Plant Type          ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Sales Line"."Plant Type" WHERE (Document Type=CONST(Order),
                                                                                                       Document No.=FIELD(Document No.),
                                                                                                       Line No.=FIELD(Document Line No.)));
                                                   CaptionML=[ENU=Plant Type;
                                                              NOR=Maskintype;
                                                              SVE=Maskintyp];
                                                   Editable=No }
    { 160 ;   ;Plant No.           ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Sales Line"."Plant No." WHERE (Document Type=CONST(Order),
                                                                                                      Document No.=FIELD(Document No.),
                                                                                                      Line No.=FIELD(Document Line No.)));
                                                   CaptionML=[ENU=Plant No.;
                                                              NOR=Maskinnr.;
                                                              SVE=Maskinnr];
                                                   Editable=No }
    { 170 ;   ;Description         ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Sales Line".Description WHERE (Document Type=CONST(Order),
                                                                                                      Document No.=FIELD(Document No.),
                                                                                                      Line No.=FIELD(Document Line No.)));
                                                   CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning];
                                                   Editable=No }
    { 180 ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=[ENU=Currency Code;
                                                              NOR=Valutakode;
                                                              SVE=Valutakod];
                                                   Editable=No }
    { 190 ;   ;Unit Price          ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Line Amount");
                                                              END;

                                                   CaptionML=[ENU=Unit Price;
                                                              NOR=Enhetspris;
                                                              SVE=A-pris];
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Currency Code";
                                                   CaptionClass=GetCaptionClass(FIELDNO("Unit Price")) }
    { 200 ;   ;Line Amount         ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD(Status, Status::Open);
                                                                IF "Unit Price" <> 0 THEN
                                                                  TESTFIELD(Type, Type::Sale);
                                                                TESTFIELD(Quantity);

                                                                GetSalesHeader;
                                                                "Line Amount" := ROUND(Quantity * "Unit Price" ,Currency."Amount Rounding Precision")
                                                              END;

                                                   CaptionML=[ENU=Line Amount;
                                                              NOR=Linjebelõp;
                                                              SVE=Radbelopp];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code";
                                                   CaptionClass=GetCaptionClass(FIELDNO("Line Amount")) }
    { 210 ;   ;Generated Sales Line No.;Integer   ;CaptionML=[ENU=Generated Sales Line No.;
                                                              NOR=Generert Salgsradnr.;
                                                              SVE=Genererad fîrsÑljningsradnr] }
    { 220 ;   ;Item No.            ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Sales Line"."Item No." WHERE (Document Type=CONST(Order),
                                                                                                     Document No.=FIELD(Document No.),
                                                                                                     Line No.=FIELD(Document Line No.)));
                                                   CaptionML=[ENU=Item No.;
                                                              NOR=Artikkelnr;
                                                              SVE=Artikelnr];
                                                   Description=Item No. Removal Cost;
                                                   Editable=No }
    { 230 ;   ;Removed Item Invoiced;Boolean      ;CaptionML=[ENU=Removed Item Invoiced;
                                                              NOR=Fjernet artikkel fakturert;
                                                              SVE=Borttagen artikel fakturerad] }
    { 240 ;   ;Plant Location Address Code;Code20 ;OnValidate=VAR
                                                                PlantLocationAddressRec@1100485000 : Record 11012590;
                                                              BEGIN
                                                                IF ("Plant Location Address Code" <> '') THEN BEGIN
                                                                  GetSalesHeader;

                                                                  PlantLocationAddressRec.CHANGECOMPANY(PlantMgtCompRec.GetPlantCompanySalesRental(TRUE));
                                                                  PlantLocationAddressRec.GET(SalesHeader."Plant Location Code", "Plant Location Address Code");
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              PlantLocationAddressRec@1100485000 : Record 11012590;
                                                            BEGIN
                                                              GetSalesHeader;

                                                              PlantLocationAddressRec.CHANGECOMPANY(PlantMgtCompRec.GetPlantCompanySalesRental(TRUE));
                                                              PlantLocationAddressRec.FILTERGROUP(10);
                                                              PlantLocationAddressRec.SETRANGE(Location, SalesHeader."Plant Location Code");
                                                              PlantLocationAddressRec.FILTERGROUP(0);
                                                              IF PAGE.RUNMODAL(PAGE::"Plant Location Addresses",PlantLocationAddressRec) = ACTION::LookupOK THEN
                                                                VALIDATE("Plant Location Address Code", PlantLocationAddressRec."Address Code");
                                                            END;

                                                   CaptionML=[ENU=Plant Location Address Code;
                                                              NOR=Adressekode, maskinplassering;
                                                              SVE=Adresskod, Maskinsplats] }
    { 250 ;   ;Unit of Measure Code;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Sales Line"."Unit of Measure Code" WHERE (Document Type=CONST(Order),
                                                                                                                 Document No.=FIELD(Document No.),
                                                                                                                 Line No.=FIELD(Document Line No.)));
                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              NOR=MÜleenhetskode;
                                                              SVE=Enhetskod];
                                                   Editable=No }
    { 260 ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   CaptionML=[ENU=Reason Code;
                                                              NOR=èrsaksspor;
                                                              SVE=Uppfîljningskod] }
  }
  KEYS
  {
    {    ;Document No.,Document Line No.,Line No. ;SumIndexFields=Quantity;
                                                   MaintainSIFTIndex=No;
                                                   Clustered=Yes }
    {    ;Document No.,Status                      }
    {    ;Document No.,Type,Removal Date,Plant Location Address Code }
    {    ;Document No.,Generated Sales Line No.    }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100485000 : TextConst 'ENU=%1 may not be before the %2;NOR=%1 kan ikke vëre fõr %2;SVE=%1 fÜr inte vara fîre %2';
      Text002@1100485001 : TextConst 'ENU=%1 may not be after the %2.;NOR=%1 kan ikke vëre etter %2.;SVE=%1 fÜr inte vara efter %2.';
      Text004@1100485003 : TextConst 'ENU=Removed quantity may not exceed arrived quantity.;NOR=Fjernet antall kan ikke overstige ankommet antall.;SVE=Borttaget antal fÜr inte îverstiga ankommet antal.';
      PlantMgtCompRec@1100485002 : Record 11020586;
      SalesHeader@1100485004 : Record 36;
      Currency@1100485005 : Record 4;
      Text005@1100525000 : TextConst 'ENU=Open removal lines of plant order ''%1''?;NOR=èpne fjerningsrader for maskinordre ''%1''?;SVE=ôppna borttagningsrader fîr maskinorder ''%1''?';
      Text006@1100525001 : TextConst 'ENU=Open removal lines ((%1 ''%2'')?;NOR=èpne fjerningsrader ((%1 %2)?;SVE=ôppna borttagningsrader ((%1 %2)?';

    PROCEDURE GenerateRemovalLines@1100485000(iSalesOrderHeadRec@1100485000 : Record 36;iQuantityLogisticsBased@1100485004 : Boolean);
    VAR
      lvSalesLine@1100485001 : Record 37;
      lvSalesLine2@1100485009 : Record 37;
      lvRemovalLine@1100485002 : Record 11012789;
      lvRemovalLine2Rec@1100485003 : Record 11012789;
      lvPlantTypeRec@1100485006 : Record 11012551;
      lvLogistQuant@1100485008 : Decimal;
      lvRoundPrecision@1100485005 : Decimal;
      lvTempQuantRec@1100485007 : TEMPORARY Record 11012552;
    BEGIN
      iSalesOrderHeadRec.TESTFIELD("Document Type", iSalesOrderHeadRec."Document Type"::Order);
      iSalesOrderHeadRec.TESTFIELD("No.");

      WITH lvRemovalLine DO BEGIN
        lvSalesLine.SETRANGE("Document Type", lvSalesLine."Document Type"::Order);
        lvSalesLine.SETRANGE("Document No.", iSalesOrderHeadRec."No.");
        IF lvSalesLine.FINDSET THEN
          REPEAT
            IF ((lvSalesLine."Plant Type" <> '') AND (lvSalesLine."Arrival Date" <> 0D)) OR
               lvSalesLine.ItemIsRemovalOnly THEN
            BEGIN
              INIT;
              "Document No." := lvSalesLine."Document No.";
              "Document Line No." := lvSalesLine."Line No.";
              IF (lvSalesLine."Plant Type" <> '') AND iQuantityLogisticsBased THEN BEGIN
                lvPlantTypeRec.CHANGECOMPANY(PlantMgtCompRec.GetPlantCompanySalesRental(TRUE));
                lvPlantTypeRec.GET(lvSalesLine."Plant Type");
                IF lvPlantTypeRec.Bulk THEN
                  lvRoundPrecision := 0.00001
                ELSE
                  lvRoundPrecision := 1;

                IF lvTempQuantRec.GET(lvSalesLine."Plant Type", lvSalesLine."Plant No.") THEN
                  lvLogistQuant := lvTempQuantRec.Quantity
                ELSE BEGIN
                  lvLogistQuant :=
                    ROUND(
                      SalesPlantQtyPerUnitOfMeasure(
                        lvSalesLine."Plant Type",
                        lvPlantTypeRec."Unit of Measure",
                        lvSalesLine."Unit of Measure Code") *
                      CountPlantInventory(iSalesOrderHeadRec."Plant Location Code", lvSalesLine."Plant Type", lvSalesLine."Plant No."),
                      lvRoundPrecision);
                END;

                lvSalesLine2.COPY(lvSalesLine);
                lvSalesLine2.SETFILTER("Line No.",'>%1', lvSalesLine."Line No.");
                lvSalesLine2.SETRANGE("Plant Type", lvSalesLine."Plant Type");
                lvSalesLine2.SETRANGE("Plant No.", lvSalesLine."Plant No.");
                lvSalesLine2.SETFILTER("Arrival Date", '>%1', 0D);
                IF lvSalesLine2.ISEMPTY THEN
                  Quantity := lvLogistQuant
                ELSE BEGIN
                  CALCFIELDS("Removed Quantity");
                  IF lvLogistQuant > lvSalesLine.Quantity - "Removed Quantity" THEN
                    Quantity := lvSalesLine.Quantity - "Removed Quantity"
                  ELSE
                    Quantity := lvLogistQuant;
                END;
                Quantity := ROUND(Quantity);
                lvLogistQuant := lvLogistQuant - Quantity;
                IF lvTempQuantRec.GET(lvSalesLine."Plant Type", lvSalesLine."Plant No.") THEN BEGIN
                  lvTempQuantRec.Quantity := lvLogistQuant;
                  lvTempQuantRec.MODIFY;
                END ELSE BEGIN
                  lvTempQuantRec."Plant Type" := lvSalesLine."Plant Type";
                  lvTempQuantRec."No." := lvSalesLine."Plant No.";
                  lvTempQuantRec.Quantity := lvLogistQuant;
                  lvTempQuantRec.INSERT;
                END;
              END ELSE BEGIN
                CALCFIELDS("Removed Quantity");
                Quantity := lvSalesLine.Quantity - "Removed Quantity";
              END;
              Quantity := ROUND(Quantity);
              IF Quantity > 0 THEN BEGIN
                lvRemovalLine2Rec.INIT;
                lvRemovalLine2Rec.SETRANGE("Document No.", lvSalesLine."Document No.");
                lvRemovalLine2Rec.SETRANGE("Document Line No.", lvSalesLine."Line No.");
                IF lvRemovalLine2Rec.FINDLAST THEN
                  "Line No." := lvRemovalLine2Rec."Line No." + 10000
                ELSE
                  "Line No." := 10000;
                VALIDATE("Removal Date", lvSalesLine."Removal Date");
                VALIDATE("Rental Ending Date", lvSalesLine."Rental Ending Date");
                "Plant Location Address Code" := lvSalesLine."Plant Location Address Code";
                INSERT;
              END;
            END;
          UNTIL lvSalesLine.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE CountPlantInventory@1100485003(iLocationCode@1100485000 : Code[20];iPlantType@1100485004 : Code[20];iPlantNo@1100485005 : Code[10]) ReturnQuan : Decimal;
    VAR
      lvPlantInvRec@1100485001 : Record 11012555;
    BEGIN
      ReturnQuan := 0;

      WITH lvPlantInvRec DO BEGIN
        RESET;
        CHANGECOMPANY(PlantMgtCompRec.GetPlantCompanySalesRental(TRUE));
        SETCURRENTKEY(Removed,Location,"Plant Type","Plant No.");
        SETRANGE(Removed,FALSE);
        SETRANGE(Location,iLocationCode);
        SETRANGE("Plant Type",iPlantType);
        IF iPlantNo = '' THEN
          iPlantNo := '0';
        SETRANGE("Plant No.",iPlantNo);
        SETRANGE(Sold, FALSE);
        SETFILTER(Quantity, '>0');
        IF FINDSET THEN
          REPEAT
            ReturnQuan := ReturnQuan + Quantity;
          UNTIL NEXT = 0;
      END;
    END;

    PROCEDURE ReleaseRemovalLines@1100485001(iSalesOrderHeadRec@1100485000 : Record 36);
    VAR
      lvRemovalLine@1100485001 : Record 11012789;
      lvRentalMgntCU@1100485002 : Codeunit 11012143;
      lvOriginalSalesLine@1100485003 : Record 37;
      lvSalesLine@1100485005 : Record 37;
      lvSalesLineNo@1100485006 : Integer;
      ModifyExistingLine@1100525000 : Boolean;
    BEGIN
      iSalesOrderHeadRec.TESTFIELD("Document Type", iSalesOrderHeadRec."Document Type"::Order);
      iSalesOrderHeadRec.TESTFIELD("No.");
      iSalesOrderHeadRec.TESTFIELD(Status, iSalesOrderHeadRec.Status::Released);

      WITH lvRemovalLine DO BEGIN
        //Go through all removal lines with status open and set to released if possible
        COPY(Rec);
        SETRANGE("Document No.", iSalesOrderHeadRec."No.");
        MARKEDONLY(TRUE);
        IF NOT FINDFIRST THEN
          MARKEDONLY(FALSE);
        SETRANGE(Status, Status::Open);
        SETCURRENTKEY("Document No.","Document Line No.","Line No.");
        IF FINDSET(TRUE,FALSE) THEN BEGIN
          REPEAT
            CALCFIELDS("Item No.");
            IF (Quantity <> 0) AND ("Removal Date" <> 0D) AND
               (((Type = Type::Removal) AND (("Rental Ending Date" <> 0D) OR ("Item No." <> ''))) OR (Type = Type::Sale))
            THEN BEGIN
              IF lvOriginalSalesLine.GET(lvOriginalSalesLine."Document Type"::Order, "Document No.", "Document Line No.") THEN BEGIN
                IF Type = Type::Sale THEN BEGIN
                  //Create Sales Line
                  CALCFIELDS("Plant Type", "Plant No.", Description);

                  ModifyExistingLine := FALSE;
                  lvSalesLine.SETRANGE("Document Type", iSalesOrderHeadRec."Document Type");
                  lvSalesLine.SETRANGE("Document No.", iSalesOrderHeadRec."No.");
                  IF "Generated Sales Line No." <> 0 THEN BEGIN
                    lvSalesLine.SETRANGE("Line No.", "Generated Sales Line No.");
                    ModifyExistingLine := lvSalesLine.FINDFIRST;
                    lvSalesLine.SETRANGE("Line No.");
                  END;
                  IF NOT ModifyExistingLine THEN
                    IF lvSalesLine.FINDLAST THEN
                      lvSalesLineNo := lvSalesLine."Line No." + 10000
                    ELSE
                      lvSalesLineNo := 10000;
                  lvSalesLine.SetHideValidationDialog(TRUE);
                  lvSalesLine.INIT;
                  lvSalesLine."Document Type" := iSalesOrderHeadRec."Document Type";
                  lvSalesLine."Document No." := iSalesOrderHeadRec."No.";
                  IF NOT ModifyExistingLine THEN
                    lvSalesLine."Line No." := lvSalesLineNo;
                  lvSalesLine.InitRecord;
                  IF NOT ModifyExistingLine THEN
                    lvSalesLine.INSERT;
                  lvSalesLine."System-Created Entry" := TRUE;
                  lvSalesLine.Type := lvSalesLine.Type::"G/L Account";
                  lvSalesLine.VALIDATE("Job No.", iSalesOrderHeadRec."Job No.");
                  lvSalesLine."Shortcut Dimension 1 Code" := lvOriginalSalesLine."Shortcut Dimension 1 Code";
                  lvSalesLine.VALIDATE("Plant Type", "Plant Type");
                  lvSalesLine.VALIDATE("Shortcut Dimension 2 Code", lvSalesLine."Shortcut Dimension 2 Code");
                  lvSalesLine."Plant No." := "Plant No.";
                  lvSalesLine.VALIDATE(Quantity, Quantity);
                  lvSalesLine.VALIDATE("Unit Price", "Unit Price");
                  lvSalesLine.Description := Description;
                  IF "Removal Order No." <> '' THEN
                    lvSalesLine."Removal Order Type" := "Removal Order Type"
                  ELSE
                    lvSalesLine."Removal Order Type" := lvSalesLine."Removal Order Type"::"Exit-order";
                  lvSalesLine."Removal Order" := "Removal Order No.";
                  lvSalesLine."Removal Date" := "Removal Date";
                  lvSalesLine."Rental Ending Date" := "Rental Ending Date";
                  lvSalesLine.VALIDATE("Arrival Date", 0D); //This also sets Shipment Date to 0D
                  lvSalesLine."Rental Starting Date" := 0D;
                  lvSalesLine."Relate to" := lvSalesLine."Relate to"::Sales;

                  lvSalesLine.MODIFY;
                  "Generated Sales Line No." := lvSalesLine."Line No.";

                  IF "Removal Order Type" = "Removal Order Type"::"Plant Order" THEN
                    Status := Status::Processed //Directly Processed. Removal Line is created by Logistics
                  ELSE
                    Status := Status::Released;
                END ELSE
                  Status := Status::Released;
                MODIFY;
              END;
            END;
          UNTIL NEXT = 0;
        END;

        //if there are removal lines, put ready for generating of removal/exit order
        MARKEDONLY(FALSE);
        SETCURRENTKEY("Document No.",Status);
        SETRANGE("Document No.", iSalesOrderHeadRec."No.");
        SETRANGE(Status, Status::Released);
        IF FINDFIRST THEN
          lvRentalMgntCU.AddForCreatePlantOrders(iSalesOrderHeadRec."No.", 2);
      END;
    END;

    PROCEDURE OpenRemovalLines@1100525000();
    VAR
      lvRemovalLine@1100525000 : Record 11012789;
      lvPlantOrder@1100525001 : Record 11012556;
    BEGIN
      //*13673.n
      IF Status = Status::Open THEN
        FIELDERROR(Status);

      lvRemovalLine.SETRANGE("Document No.", "Document No.");
      CASE Type OF
        Type::Removal:
          BEGIN
            lvRemovalLine.SETRANGE(Type, Type::Removal);
            IF ("Removal Order No." <> '') THEN BEGIN
              TESTFIELD("Removal Order Type", "Removal Order Type"::"Plant Order");
              lvPlantOrder.CHANGECOMPANY(PlantMgtCompRec.GetPlantCompanySalesRental(TRUE));
              IF lvPlantOrder.GET("Removal Order No.") THEN BEGIN
                lvPlantOrder.TESTFIELD(Posted, FALSE);
                lvPlantOrder.TESTFIELD(Status, lvPlantOrder.Status::Open);
              END;
              IF NOT CONFIRM(Text005, FALSE, "Removal Order No.") THEN
                EXIT;
              lvRemovalLine.SETRANGE("Removal Order No.", "Removal Order No.");  //* All lines with the same Removal Order
            END ELSE BEGIN
              IF NOT CONFIRM(Text006, FALSE, FIELDCAPTION(Type), Type) THEN
                EXIT;
              lvRemovalLine.SETRANGE("Removal Order No.", '');
            END;
          END;
        Type::Sale:
          BEGIN
            TESTFIELD(Status, Status::Released);
            TESTFIELD("Removal Order No.", '');
            IF NOT CONFIRM(Text006, FALSE, FIELDCAPTION(Type), Type) THEN
              EXIT;
            lvRemovalLine.SETRANGE(Type, Type::Sale);
            lvRemovalLine.SETRANGE("Removal Order No.", '');
          END;
      END;

      IF lvRemovalLine.FINDSET(TRUE,FALSE) THEN BEGIN
        REPEAT
          lvRemovalLine.Status := Status::Open;
          lvRemovalLine.MODIFY;
        UNTIL lvRemovalLine.NEXT =0;
      END;
    END;

    LOCAL PROCEDURE GetFieldCaption@31(FieldNumber@1000 : Integer) : Text[100];
    VAR
      Field@1001 : Record 2000000041;
    BEGIN
      Field.GET(DATABASE::"Sales Rental Removal Line",FieldNumber);
      EXIT(Field."Field Caption");
    END;

    LOCAL PROCEDURE GetCaptionClass@34(FieldNumber@1000 : Integer) : Text[80];
    VAR
      SalesPricesIncVar@1001 : Integer;
    BEGIN
      IF NOT SalesHeader.GET(SalesHeader."Document Type"::Order,"Document No.") THEN BEGIN
        SalesHeader."No." := '';
        SalesHeader.INIT;
      END;
      IF SalesHeader."Prices Including VAT" THEN
        SalesPricesIncVar := 1
      ELSE
        SalesPricesIncVar := 0;
      CLEAR(SalesHeader);
      EXIT('2,'+FORMAT(SalesPricesIncVar)+',' + GetFieldCaption(FieldNumber));
    END;

    LOCAL PROCEDURE GetSalesHeader@1();
    BEGIN
      TESTFIELD("Document No.");
      IF ("Document No." <> SalesHeader."No.") THEN BEGIN
        SalesHeader.GET(SalesHeader."Document Type"::Order,"Document No.");
        IF SalesHeader."Currency Code" = '' THEN
          Currency.InitRoundingPrecision
        ELSE BEGIN
          SalesHeader.TESTFIELD("Currency Factor");
          Currency.GET(SalesHeader."Currency Code");
          Currency.TESTFIELD("Amount Rounding Precision");
        END;
      END;
    END;

    PROCEDURE SalesPlantQtyPerUnitOfMeasure@1100485027(IPlantType@1100485001 : Code[20];ILogistUnit@1100485000 : Code[10];ISalesUnit@1100485006 : Code[10]) lvQtyPerUnit : Decimal;
    VAR
      lvPlantUnitRec@1100485002 : Record 11012599;
    BEGIN
      //This function returns the ratio of the unit code in logistics company and sales unit code in sales company
      lvQtyPerUnit := 1;
      IF (IPlantType = '') OR (ILogistUnit = '') OR (ISalesUnit = '') THEN
        EXIT;

      IF ILogistUnit = ISalesUnit THEN
        EXIT;

      lvPlantUnitRec.GET(IPlantType, ILogistUnit);
      lvPlantUnitRec.TESTFIELD("Qty. per Unit of Measure");
      lvQtyPerUnit := 1/lvPlantUnitRec."Qty. per Unit of Measure";
    END;

    BEGIN
    END.
  }
}

