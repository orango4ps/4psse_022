OBJECT Table 11229827 DPT Additional Data
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF (Type = 0) THEN
                 Type := Type::Text;
               IF (COPYSTR(Code, 1, 3) <> 'NAV') THEN
                 Code := 'NAV' + Code;
             END;

    OnModify=BEGIN
               TESTFIELD(Type);
               CASE Type OF
                 Type::Option: BEGIN
                   "Minimum Value" := 0;
                   "Maximum Value" := 0;
                   "DateTime Format" := 0;
                 END;
                 Type::Value: BEGIN
                   "DateTime Format" := 0;
                 END;
                 Type::DateTime: BEGIN
                   "Minimum Value" := 0;
                   "Maximum Value" := 0;
                 END;
               END;
             END;

    OnDelete=VAR
               DPTAdditionalDataOption@1100528300 : Record 11229828;
               DPTMetadata@1100528301 : Record 11229825;
             BEGIN
               DPTMetadata.SETRANGE("Source Type", "Source Type");
               DPTMetadata.SETRANGE(Code, Code);
               DPTMetadata.SETRANGE("Standard Data", FALSE);
               IF (DPTMetadata.FINDFIRST) THEN
                 ERROR(Text000, TABLECAPTION, Code, DPTMetadata.TABLECAPTION);

               DPTAdditionalDataOption.SETRANGE("Source Type", "Source Type");
               DPTAdditionalDataOption.SETRANGE("Additional Data Code", Code);
               DPTAdditionalDataOption.DELETEALL(TRUE);;
             END;

    CaptionML=ENU=DPT Additional Data;
  }
  FIELDS
  {
    { 10  ;   ;Source Type         ;Option        ;CaptionML=[ENU=Source Type;
                                                              NOR=Kildetype;
                                                              SVE=Ursprungstyp];
                                                   OptionCaptionML=[ENU=" ,Project,Service Location";
                                                                    SVE=,Project,Serviceplats];
                                                   OptionString=[ ,Project,ServiceLocation];
                                                   Editable=No }
    { 20  ;   ;Code                ;Text30        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 30  ;   ;Description         ;Text50        ;OnValidate=BEGIN
                                                                UpdateDefaultTranslationDescription;
                                                              END;

                                                   CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 40  ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Type;
                                                              SVE=Typ];
                                                   OptionCaptionML=ENU=" ,Text,Option,Value,DateTime";
                                                   OptionString=[ ,Text,Option,Value,DateTime] }
    { 50  ;   ;Minimum Value       ;Integer       ;OnValidate=BEGIN
                                                                IF ("Minimum Value" > "Maximum Value") AND ("Maximum Value" <> 0) THEN
                                                                  FIELDERROR("Minimum Value");
                                                              END;

                                                   CaptionML=[ENU=Minimum Value;
                                                              NOR=Minimumsverdi;
                                                              SVE=Minimiv„rde] }
    { 60  ;   ;Maximum Value       ;Integer       ;OnValidate=BEGIN
                                                                IF ("Maximum Value" < "Minimum Value") THEN
                                                                  FIELDERROR("Maximum Value");
                                                              END;

                                                   CaptionML=ENU=Maximum Value }
    { 70  ;   ;DateTime Format     ;Option        ;CaptionML=ENU=DateTime Format;
                                                   OptionCaptionML=ENU=" ,Date Only,Date And Time";
                                                   OptionString=[ ,DateOnly,DateAndTime] }
  }
  KEYS
  {
    {    ;Source Type,Code                        ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100525000 : TextConst 'ENU=%1 ''%2'' still used in %3.';

    LOCAL PROCEDURE UpdateDefaultTranslationDescription@1100525010();
    VAR
      DPTSetup@1100525000 : Record 11229823;
      DPTMetadataTranslation@1100528300 : Record 11229826;
    BEGIN
      DPTSetup.GET;
      IF (NOT DPTMetadataTranslation.GET("Source Type", 0, Code, DPTSetup."Language Code")) THEN BEGIN
        DPTMetadataTranslation."Source Type" := "Source Type";
        DPTMetadataTranslation.Type := DPTMetadataTranslation.Type::Description;
        DPTMetadataTranslation."Metadata Code" := Code;
        DPTMetadataTranslation."Language Code" := DPTSetup."Language Code";
        DPTMetadataTranslation.INSERT;
      END;
      DPTMetadataTranslation.Description := Description;
      DPTMetadataTranslation.MODIFY;
    END;

    BEGIN
    END.
  }
}

