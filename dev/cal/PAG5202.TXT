OBJECT Page 5202 Employee Picture
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03,4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Employee Picture;
               NOR=Bilde av ansatt;
               SVE=Personalbild];
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table5200;
    PageType=CardPart;
    OnOpenPage=BEGIN
                 //**4PS Generated Code. Do Not Change.sn01
                 DPA_IsHardFilter := DPA_SetFilters(CurrPage.OBJECTID(FALSE));
                 //**4PS Generated Code.en01
               END;

    OnNewRecord=BEGIN
                  //**4PS Generated Code. Do Not Change.sn02
                  DPA_SetDefaultDepartment;
                  //**4PS Generated Code.en02
                END;

    OnInsertRecord=VAR
                     xRecRef@1100000999 : RecordRef;
                   BEGIN
                     //**4PS Generated Code. Do Not Change.sn05
                     xRecRef.GETTABLE(xRec);
                     DPA_SetDefaultDpmtFromNoSeries(xRecRef);
                     //**4PS Generated Code.en05
                   END;

    OnAfterGetCurrRecord=BEGIN
                           SetEditableOnPictureActions;
                         END;

    ActionList=ACTIONS
    {
      { 2       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 3       ;1   ;Action    ;
                      Name=TakePicture;
                      CaptionML=[ENU=Take;
                                 NOR=Ta;
                                 SVE=Ta bild];
                      ToolTipML=[ENU=Activate the camera on the device.;
                                 NOR=Aktiver kameraet p† enheten.;
                                 SVE=Aktivera enhetens kamera.];
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      Visible=CameraAvailable;
                      PromotedIsBig=Yes;
                      Image=Camera;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 CameraOptions@1000 : DotNet "'Microsoft.Dynamics.Nav.ClientExtensions, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.Capabilities.CameraOptions";
                               BEGIN
                                 TESTFIELD("No.");
                                 TESTFIELD("First Name");
                                 TESTFIELD("Last Name");

                                 IF NOT CameraAvailable THEN
                                   EXIT;
                                 CameraOptions := CameraOptions.CameraOptions;
                                 CameraOptions.Quality := 100;
                                 CameraProvider.RequestPictureAsync(CameraOptions);
                               END;
                                }
      { 4       ;1   ;Action    ;
                      Name=ImportPicture;
                      CaptionML=[ENU=Import;
                                 NOR=Importer;
                                 SVE=Importera];
                      ToolTipML=[ENU=Import a picture file.;
                                 NOR=Importer en bildefil.;
                                 SVE=Importera en bildfil.];
                      ApplicationArea=#Basic,#Suite;
                      Image=Import;
                      OnAction=VAR
                                 FileManagement@1001 : Codeunit 419;
                                 FileName@1000 : Text;
                                 ClientFileName@1002 : Text;
                               BEGIN
                                 TESTFIELD("No.");
                                 TESTFIELD("First Name");
                                 TESTFIELD("Last Name");

                                 IF Image.HASVALUE THEN
                                   IF NOT CONFIRM(OverrideImageQst) THEN
                                     EXIT;

                                 FileName := FileManagement.UploadFile(SelectPictureTxt,ClientFileName);
                                 IF FileName = '' THEN
                                   EXIT;

                                 CLEAR(Image);
                                 Image.IMPORTFILE(FileName,ClientFileName);
                                 MODIFY(TRUE);

                                 IF FileManagement.DeleteServerFile(FileName) THEN;
                               END;
                                }
      { 6       ;1   ;Action    ;
                      Name=ExportFile;
                      CaptionML=[ENU=Export;
                                 NOR=Eksporter;
                                 SVE=Exportera];
                      ToolTipML=[ENU=Export the picture to a file.;
                                 NOR=Eksporter bildet til en fil.;
                                 SVE=Exportera bilden till en fil.];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=DeleteExportEnabled;
                      Image=Export;
                      OnAction=VAR
                                 DummyPictureEntity@1000 : Record 5468;
                                 FileManagement@1001 : Codeunit 419;
                                 ToFile@1003 : Text;
                                 ExportPath@1005 : Text;
                               BEGIN
                                 TESTFIELD("No.");
                                 TESTFIELD("First Name");
                                 TESTFIELD("Last Name");

                                 ToFile := DummyPictureEntity.GetDefaultMediaDescription(Rec);
                                 ExportPath := TEMPORARYPATH + "No." + FORMAT(Image.MEDIAID);
                                 Image.EXPORTFILE(ExportPath);

                                 FileManagement.ExportImage(ExportPath,ToFile);
                               END;
                                }
      { 7       ;1   ;Action    ;
                      Name=DeletePicture;
                      CaptionML=[ENU=Delete;
                                 NOR=Slett;
                                 SVE=Ta bort];
                      ToolTipML=[ENU=Delete the record.;
                                 NOR=Slett posten.;
                                 SVE=Ta bort posten.];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=DeleteExportEnabled;
                      Image=Delete;
                      OnAction=BEGIN
                                 TESTFIELD("No.");

                                 IF NOT CONFIRM(DeleteImageQst) THEN
                                   EXIT;

                                 CLEAR(Image);
                                 MODIFY(TRUE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Field     ;
                ToolTipML=[ENU=Specifies the picture of the employee.;
                           NOR=Angir bildet av den ansatte.;
                           SVE=Anger bilden av den anst„llde.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Image;
                ShowCaption=No }

  }
  CODE
  {
    VAR
      CameraProvider@1000 : DotNet "'Microsoft.Dynamics.Nav.ClientExtensions, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.Capabilities.CameraProvider" WITHEVENTS RUNONCLIENT;
      CameraAvailable@1002 : Boolean;
      OverrideImageQst@1007 : TextConst 'ENU=The existing picture will be replaced. Do you want to continue?;NOR=Det eksisterende bildet vil bli erstattet. Vil du fortsette?;SVE=Den befintliga bilden ers„tts. Vill du forts„tta?';
      DeleteImageQst@1003 : TextConst 'ENU=Are you sure you want to delete the picture?;NOR=Er du sikker p† at du vil slette bildet?;SVE=Vill du ta bort bilden?';
      SelectPictureTxt@1001 : TextConst 'ENU=Select a picture to upload;NOR=Velg et bilde for opplasting;SVE=V„lj en bild att ladda upp';
      DeleteExportEnabled@1005 : Boolean;
      DPA_IsHardFilter@1100000999 : Boolean;

    LOCAL PROCEDURE SetEditableOnPictureActions@1();
    BEGIN
      DeleteExportEnabled := Image.HASVALUE;
    END;

    EVENT CameraProvider@1000::PictureAvailable@10(PictureName@1001 : Text;PictureFilePath@1000 : Text);
    VAR
      File@1003 : File;
      Instream@1004 : InStream;
    BEGIN
      IF (PictureName = '') OR (PictureFilePath = '') THEN
        EXIT;

      IF Image.HASVALUE THEN
        IF NOT CONFIRM(OverrideImageQst) THEN BEGIN
          IF ERASE(PictureFilePath) THEN;
          EXIT;
        END;

      File.OPEN(PictureFilePath);
      File.CREATEINSTREAM(Instream);

      CLEAR(Image);
      Image.IMPORTSTREAM(Instream,PictureName);
      IF NOT MODIFY(TRUE) THEN
        INSERT(TRUE);

      File.CLOSE;
      IF ERASE(PictureFilePath) THEN;
    END;

    BEGIN
    END.
  }
}

