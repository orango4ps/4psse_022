OBJECT Codeunit 1344 Acct. WebServices Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [External]
    PROCEDURE SetCueStyle@12(TableID@1000 : Integer;FieldID@1001 : Integer;Amount@1010 : Decimal;VAR FinalStyle@1009 : 'None,,,,,,,Favorable,Unfavorable,Ambiguous,Subordinate');
    VAR
      CueSetup@1008 : Record 9701;
      LowRangeStyle@1007 : 'None,,,,,,,Favorable,Unfavorable,Ambiguous,Subordinate';
      Threshold1@1006 : Decimal;
      MiddleRangeStyle@1005 : 'None,,,,,,,Favorable,Unfavorable,Ambiguous,Subordinate';
      Threshold2@1004 : Decimal;
      HighRangeStyle@1003 : 'None,,,,,,,Favorable,Unfavorable,Ambiguous,Subordinate';
    BEGIN
      // First see if we have a record for the current user
      CueSetup.SETRANGE("User Name",USERID);
      CueSetup.SETRANGE("Table ID",TableID);
      CueSetup.SETRANGE("Field No.",FieldID);
      IF CueSetup.FINDFIRST THEN BEGIN
        LowRangeStyle := CueSetup."Low Range Style";
        Threshold1 := CueSetup."Threshold 1";
        MiddleRangeStyle := CueSetup."Middle Range Style";
        Threshold2 := CueSetup."Threshold 2";
        HighRangeStyle := CueSetup."High Range Style";
      END ELSE BEGIN
        CueSetup.RESET;
        CueSetup.SETRANGE("User Name",'');
        CueSetup.SETRANGE("Table ID",TableID);
        CueSetup.SETRANGE("Field No.",FieldID);
        IF CueSetup.FINDFIRST THEN BEGIN
          LowRangeStyle := CueSetup."Low Range Style";
          Threshold1 := CueSetup."Threshold 1";
          MiddleRangeStyle := CueSetup."Middle Range Style";
          Threshold2 := CueSetup."Threshold 2";
          HighRangeStyle := CueSetup."High Range Style";
        END ELSE BEGIN
          LowRangeStyle := CueSetup."Low Range Style"::None;
          Threshold1 := 0;
          MiddleRangeStyle := CueSetup."Middle Range Style"::None;
          Threshold2 := 0;
          HighRangeStyle := CueSetup."High Range Style"::None;
        END;
      END;

      IF Amount < Threshold1 THEN BEGIN
        FinalStyle := LowRangeStyle;
        EXIT;
      END;
      IF Amount > Threshold2 THEN BEGIN
        FinalStyle := HighRangeStyle;
        EXIT;
      END;
      FinalStyle := MiddleRangeStyle;
    END;

    [External]
    PROCEDURE FormatAmountString@3(Amount@1000 : Decimal) FormattedAmount : Text;
    VAR
      GeneralLedgerSetup@1001 : Record 98;
      FormatString@1002 : Text;
      AmountDecimalPlaces@1004 : Text[5];
      LocalCurrencySymbol@1003 : Text[10];
    BEGIN
      IF GeneralLedgerSetup.FINDFIRST THEN BEGIN
        AmountDecimalPlaces := GeneralLedgerSetup."Amount Decimal Places";
        LocalCurrencySymbol := GeneralLedgerSetup."Local Currency Symbol";
      END ELSE BEGIN
        AmountDecimalPlaces := '';
        LocalCurrencySymbol := '';
      END;

      IF AmountDecimalPlaces <> '' THEN
        FormatString := LocalCurrencySymbol + '<Precision,' + AmountDecimalPlaces + '><Standard Format,0>';

      FormattedAmount := FORMAT(Amount,0,FormatString);
    END;

    BEGIN
    {
      Contains helper functions when creating web services specific to the Accounting portal.
    }
    END.
  }
}

