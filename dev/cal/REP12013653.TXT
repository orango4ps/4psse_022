OBJECT Report 12013653 Assign Replacers
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF510000;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Assign Replacers;
               FIN=Aseta korvaaja;
               SVE=Utse ersÑttare];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   StartDate := TODAY;
                 END;

    UsageCategory=Tasks;
  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnQueryClosePage=BEGIN
                         IF CloseAction = ACTION::OK THEN
                           PostReport;
                       END;

    }
    CONTROLS
    {
      { 1100285000;;Container;
                  Name=General;
                  ContainerType=ContentArea }

      { 1100285007;1;Group  ;
                  CaptionML=[ENU=General;
                             SVE=Generellt];
                  GroupType=Group }

      { 1100285008;2;Field  ;
                  CaptionML=[ENU=Action;
                             SVE=ètgÑrd];
                  OptionCaptionML=[ENU=Add,Remove;
                                   SVE=LÑgg till,Ta bort];
                  SourceExpr=AddRemove;
                  OnValidate=BEGIN
                               IF AddRemove = AddRemove::Remove THEN BEGIN
                                 StartDate := 0D;
                                 EndDate := 0D;
                               END;
                             END;
                              }

      { 1100285005;2;Field  ;
                  CaptionML=[ENU=Approver;
                             SVE=AnvÑndare];
                  SourceExpr=DefaultApprover;
                  TableRelation="EX User Company"."User ID" }

      { 1100285002;2;Field  ;
                  CaptionML=[ENU=Replace with User;
                             SVE=ErsÑtt med anvÑndare];
                  SourceExpr=ReplaceWithUser;
                  TableRelation="EX User Company"."User ID" }

      { 1100285003;2;Field  ;
                  CaptionML=[ENU=Start Date:;
                             SVE=Startdatum];
                  SourceExpr=StartDate;
                  OnValidate=BEGIN
                               IF AddRemove = AddRemove::Add THEN BEGIN
                                 IF StartDate < TODAY THEN
                                   ERROR(Text007);
                                 IF (StartDate >= EndDate) AND NOT (EndDate = 0D) THEN
                                   ERROR(Text002, StartDate, EndDate);
                               END ELSE
                                 IF StartDate <> 0D THEN
                                   ERROR(Text008);
                             END;
                              }

      { 1100285004;2;Field  ;
                  CaptionML=[ENU=End Date;
                             SVE=Slutdatum];
                  SourceExpr=EndDate;
                  OnValidate=BEGIN
                               IF AddRemove = AddRemove::Add THEN BEGIN
                                 IF StartDate < TODAY THEN
                                   ERROR(Text007);
                                 IF (StartDate >= EndDate) THEN
                                   ERROR(Text002, StartDate, EndDate);
                               END ELSE
                                 IF EndDate <> 0D THEN
                                   ERROR(Text008);
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ReplaceWithUser@1100285000 : Code[50];
      DefaultApprover@1100285001 : Code[50];
      StartDate@1100285002 : Date;
      EndDate@1100285003 : Date;
      Text001@1100285005 : TextConst 'ENU=No Replacer User selected;SVE="Inga ersÑttare valda "';
      AddRemove@1100285004 : 'Add,Remove';
      Text002@1100285006 : TextConst 'ENU=Start Date (%1) can not be larger or equal to End Date (%2);SVE=Startdatum (%1) fÜr inte vara stîrre eller samma som slutdatum (%2)';
      Text003@1100285007 : TextConst 'ENU=Must fill in Date in both "Start Date" and "End Date" fields;SVE=MÜste fylla i datum i bÜde startdatum och slutdatum fÑlten';
      Text004@1100285008 : TextConst 'ENU=Replacer (%1) cannot be the same as Selected Approver (%2)';
      Text005@1100285009 : TextConst 'ENU=%1 replacer records where modified;SVE=%1 ersÑttarposter Ñndrades';
      Text006@1100285010 : TextConst 'ENU=Replacer removed;SVE=ErsÑttare borttagen';
      Text007@1100285011 : TextConst 'ENU=Date can not be earlier than today;SVE=Startdatum fÜr ej vare tidigare Ñn dagensdatum';
      Text008@1100285012 : TextConst 'ENU=Dates can not be chosen when removing approver.;SVE=Datum kan ej vÑjas nÑr man tar bor attestanter.';

    PROCEDURE SetDefaultApprover@1100285001(DefaultApp@1100285000 : Code[50]);
    BEGIN
      DefaultApprover := DefaultApp;
    END;

    LOCAL PROCEDURE PostReport@1100285000();
    VAR
      CountBefore@1100285000 : Integer;
      NumberOfChanged@1100285001 : Integer;
      ExUserGroupLine@1100285002 : Record 12013607;
      ReplaceUserMgt@1100285003 : Codeunit 12057141;
    BEGIN
      IF ReplaceWithUser = '' THEN
        ERROR(Text001);
      IF ReplaceWithUser = DefaultApprover THEN
        ERROR(Text004, ReplaceWithUser, DefaultApprover);

      IF AddRemove = AddRemove::Add THEN BEGIN
        IF StartDate >= EndDate THEN
          ERROR(Text002, StartDate, EndDate);
        IF (StartDate = 0D) OR (EndDate = 0D) THEN
          ERROR(Text003);
      END;

      CountBefore := ExUserGroupLine.COUNT;

      IF AddRemove = AddRemove::Remove THEN
        ReplaceUserMgt.RemoveReplaceUser(DefaultApprover, ReplaceWithUser)
      ELSE
        ReplaceUserMgt.ReplaceUser(COMPANYNAME, DefaultApprover, ReplaceWithUser, StartDate, EndDate);

      NumberOfChanged := ABS(CountBefore - ExUserGroupLine.COUNT);
      MESSAGE(Text005,NumberOfChanged);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

