OBJECT Codeunit 11012248 Assembly Project Management
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1100526100 : TextConst 'DEU=Projektstatus darf nicht %1 sein.;ENU=Project status may not be %1.;NLD=Projectstatus mag niet %1 zijn.;NOR=Prosjektstatus kan ikke v‘re %1.;SVE=Projektstatus f†r inte vara %1.';

    PROCEDURE ImportMaterialRequirementPEC@1100525000(Job@1100526101 : Record 11072003);
    VAR
      AssemblyYardNoObjectList@1100526100 : Page 11124928;
      AssemblyYardNumberObject@1100526102 : Record 11020596;
    BEGIN
      IF Job."Project Status" >= Job."Project Status"::"Technical Finished" THEN
        ERROR(Text000, Job."Project Status");

      AssemblyYardNumberObject.RESET;
      AssemblyYardNumberObject.SETCURRENTKEY("Project No.");
      AssemblyYardNumberObject.FILTERGROUP(9);
      AssemblyYardNumberObject.SETRANGE("Project No.", Job."No.");
      AssemblyYardNumberObject.FILTERGROUP(0);
      CLEAR(AssemblyYardNoObjectList);
      AssemblyYardNoObjectList.SETTABLEVIEW(AssemblyYardNumberObject);
      AssemblyYardNoObjectList.RUNMODAL;
    END;

    PROCEDURE DeleteCurrentMatReqLines@1100526110(ProjectNo@1100526101 : Code[20];YardNo@1100526102 : Code[20];EntityType@1100526103 : Option;SystemNo@1100526104 : Code[20];EntityNo@1100526105 : Code[20];ConstructionCompany@1100526106 : Text[30]);
    VAR
      ProjectMaterialReqLine@1100526100 : Record 11012526;
    BEGIN
      //kzwerver, 110110, c, #24085
      IF (ConstructionCompany <> COMPANYNAME) THEN ProjectMaterialReqLine.CHANGECOMPANY(ConstructionCompany);
      ProjectMaterialReqLine.SETRANGE("Project No.", ProjectNo);
      ProjectMaterialReqLine.SETRANGE("Yard No.", YardNo);
      CASE EntityType OF
        1://Cable:
          ProjectMaterialReqLine.SETRANGE("Entity Type", ProjectMaterialReqLine."Entity Type"::Cable);
        2://Device:
          ProjectMaterialReqLine.SETRANGE("Entity Type", ProjectMaterialReqLine."Entity Type"::Device);
        3://"Extra Need":
          ProjectMaterialReqLine.SETRANGE("Entity Type", ProjectMaterialReqLine."Entity Type"::ExtraNeed);
      END;
      ProjectMaterialReqLine.SETRANGE("System No.", SystemNo);
      ProjectMaterialReqLine.SETRANGE("Entity No.", EntityNo);
      ProjectMaterialReqLine.DELETEALL(TRUE);
    END;

    PROCEDURE ConProjMatReqLinsPresentInComp@1210190000(ProjectCompany@1100526100 : Text[30];ProjectNo@1100526101 : Code[20];YardNo@1100526103 : Code[20]) : Boolean;
    VAR
      ProjectMaterialReqLine@1100526102 : Record 11012526;
    BEGIN
      IF (ProjectCompany <> COMPANYNAME) THEN
        ProjectMaterialReqLine.CHANGECOMPANY(ProjectCompany);

      ProjectMaterialReqLine.SETRANGE("Project No.", ProjectNo);
      ProjectMaterialReqLine.SETRANGE("Yard No.", YardNo);
      EXIT (NOT ProjectMaterialReqLine.ISEMPTY);
    END;

    PROCEDURE ProjMatReqLinsPresentInComp@1100526112(ProjectCompany@1100526100 : Text[30];ProjectNo@1100526101 : Code[20];YardNo@1100526103 : Code[20];EntityType@1210190002 : Option;SystemNo@1210190001 : Code[20];EntityNo@1210190000 : Code[20]) : Boolean;
    VAR
      ProjectMaterialReqLine@1100526102 : Record 11012526;
    BEGIN
      IF (ProjectCompany <> COMPANYNAME) THEN
        ProjectMaterialReqLine.CHANGECOMPANY(ProjectCompany);

      ProjectMaterialReqLine.SETRANGE("Project No.", ProjectNo);
      ProjectMaterialReqLine.SETRANGE("Yard No.", YardNo);
      ProjectMaterialReqLine.SETRANGE("Entity Type", EntityType);
      ProjectMaterialReqLine.SETRANGE("System No.", SystemNo);
      ProjectMaterialReqLine.SETRANGE("Entity No.", EntityNo);
      EXIT (NOT ProjectMaterialReqLine.ISEMPTY);
    END;

    PROCEDURE ProjElementsPresentInComp@1100526120(ProjectCompany@1100526100 : Text[30];ProjectNo@1100526101 : Code[20]) : Boolean;
    VAR
      ProjectElement@1100526102 : Record 11012010;
    BEGIN
      IF (ProjectCompany <> COMPANYNAME) THEN
        ProjectElement.CHANGECOMPANY(ProjectCompany);

      ProjectElement.SETRANGE("Project No.", ProjectNo);
      EXIT (NOT ProjectElement.ISEMPTY);
    END;

    PROCEDURE ProjElementsMandatoryInComp@1100526113(ProjectCompany@1100526100 : Text[30];ProjectNo@1100526101 : Code[20]) : Boolean;
    VAR
      Job@1100526102 : Record 11072003;
    BEGIN
      IF (ProjectCompany <> COMPANYNAME) THEN
        Job.CHANGECOMPANY(ProjectCompany);

      IF (Job.GET(ProjectNo)) THEN
        EXIT(Job."Posting Element Mandatory");
      EXIT(FALSE);
    END;

    PROCEDURE SynchronizeMatReqLineDelDate@1100526116(ProjectNo@1100526100 : Code[20];YardNo@1100526101 : Code[20];EntityType@1100526104 : Option;SystemNo@1100526102 : Code[20];EntityNo@1100526103 : Code[20];DeliveryDate@1100526105 : Date);
    VAR
      ProjectMaterialReqLine@1100526106 : Record 11012526;
    BEGIN
      //kzwerver, 110105, n, #24495
      ProjectMaterialReqLine.SETRANGE("Project No.", ProjectNo);
      ProjectMaterialReqLine.SETRANGE("Yard No.", YardNo);
      ProjectMaterialReqLine.SETRANGE("Entity Type", EntityType);
      ProjectMaterialReqLine.SETRANGE("System No.", SystemNo);
      ProjectMaterialReqLine.SETRANGE("Entity No.", EntityNo);
      IF (ProjectMaterialReqLine.FINDSET) THEN BEGIN
        REPEAT
          IF (ProjectMaterialReqLine."Delivery Date" = 0D) THEN BEGIN
            ProjectMaterialReqLine."Delivery Date" := DeliveryDate;
            ProjectMaterialReqLine.MODIFY;
          END;
        UNTIL(ProjectMaterialReqLine.NEXT = 0);
      END;
    END;

    BEGIN
    END.
  }
}

