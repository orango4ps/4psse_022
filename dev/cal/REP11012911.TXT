OBJECT Report 11012911 Document Update Import (PPT)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Belegaktualisierung-Import (PPT);
               ENU=Document Update Import (PPT);
               NLD=Document update data importeren];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF (DocumentData) THEN
                    ImportDocuments;
                  IF (ResponsiblePersonData) THEN
                    ImportResponsiblePersons;
                  IF (AuthorityData) THEN
                    ImportAuthorities;
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525001;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100525002;2;Field  ;
                  CaptionML=[DEU=Verantwortliche;
                             ENU=Responsible Persons;
                             NLD=Verantwoordelijken;
                             NOR=Ansvarlige personer;
                             SVE=Ansvariga personer];
                  SourceExpr=ResponsiblePersonData }

      { 1100525003;2;Field  ;
                  CaptionML=[DEU=Instanzen;
                             ENU=Authorities;
                             NLD=Instanties;
                             NOR=Myndigheter;
                             SVE=Myndigheter];
                  SourceExpr=AuthorityData }

      { 1100525004;2;Field  ;
                  CaptionML=[DEU=Belege;
                             ENU=Documents;
                             NLD=Documenten;
                             NOR=Dokumenter;
                             SVE=Dokument];
                  SourceExpr=DocumentData }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      DocumentData@1100525000 : Boolean;
      ResponsiblePersonData@1100525001 : Boolean;
      AuthorityData@1100525002 : Boolean;
      ProjectNo@1100525003 : Code[20];
      SeqNo@1100525004 : Integer;

    LOCAL PROCEDURE ImportDocuments@1100525001();
    VAR
      Job@1100525000 : Record 11072003;
      DocumentProperties@1100409000 : Record 11012746;
      TempDocumentProperties@1100525001 : TEMPORARY Record 11012746;
      DocumentUpdateDocumentPPT@1100525002 : Record 11229802;
      DocumentVersionsPPT@1100525006 : Record 11229809;
      DocumentFilters@1100525003 : Codeunit 11012432;
      RecRef@1100525004 : RecordRef;
    BEGIN
      Job.GET(ProjectNo);
      RecRef.GETTABLE(Job);
      DocumentFilters.CreateTempFilterAndGetDocProps(RecRef, TempDocumentProperties);

      IF (NOT TempDocumentProperties.FINDSET) THEN
        EXIT;

      REPEAT
        IF (DocumentProperties.GET(TempDocumentProperties."No.")) THEN BEGIN
          DocumentVersionsPPT.SETRANGE("Project No.", ProjectNo);
          DocumentVersionsPPT.SETRANGE("Document No.", TempDocumentProperties."No.");
          IF (DocumentVersionsPPT.FINDLAST) THEN BEGIN
            DocumentUpdateDocumentPPT.INIT;
            DocumentUpdateDocumentPPT."Project No." := ProjectNo;
            DocumentUpdateDocumentPPT."Seq. No." := SeqNo;
            DocumentUpdateDocumentPPT."Document No." := DocumentVersionsPPT."Document No.";
            DocumentUpdateDocumentPPT.Version := DocumentVersionsPPT.Version;

            IF (DocumentUpdateDocumentPPT.INSERT) THEN;
          END;
        END;
      UNTIL (TempDocumentProperties.NEXT = 0);
    END;

    LOCAL PROCEDURE ImportResponsiblePersons@1100525002();
    VAR
      ProjectResponsiblePerson@1100525000 : Record 11012040;
    BEGIN
      ProjectResponsiblePerson.SETRANGE("Project No.", ProjectNo);
      IF (NOT ProjectResponsiblePerson.FINDSET) THEN
        EXIT;

      REPEAT
        InsertDocumentUpdateMember(1, ProjectResponsiblePerson."Employee No.", '', '');
      UNTIL (ProjectResponsiblePerson.NEXT = 0);
    END;

    LOCAL PROCEDURE ImportAuthorities@1100525003();
    VAR
      ProjectAuthority@1100525000 : Record 11020216;
    BEGIN
      ProjectAuthority.SETRANGE("Project No.", ProjectNo);
      IF (NOT ProjectAuthority.FINDSET) THEN
        EXIT;

      REPEAT
        InsertDocumentUpdateMember(2, '', ProjectAuthority."Contact No.", ProjectAuthority."Contact Person No.");
      UNTIL (ProjectAuthority.NEXT = 0);
    END;

    PROCEDURE InsertDocumentUpdateMember@1100525004(Type@1100525000 : Option;EmployeeNo@1100525001 : Code[20];ContactNo@1100525002 : Code[20];ContactPersonNo@1100525003 : Code[20]);
    VAR
      DocumentUpdateMemberPPT@1100525004 : Record 11229801;
    BEGIN
      DocumentUpdateMemberPPT.INIT;
      DocumentUpdateMemberPPT."Project No." := ProjectNo;
      DocumentUpdateMemberPPT."Seq. No." := SeqNo;
      DocumentUpdateMemberPPT.Type := Type;
      DocumentUpdateMemberPPT."Employee No." := EmployeeNo;
      DocumentUpdateMemberPPT."Contact No." := ContactNo;
      DocumentUpdateMemberPPT."Contact Person No." := ContactPersonNo;

      IF (DocumentUpdateMemberPPT.INSERT) THEN;
    END;

    PROCEDURE SetGlobalVars@1100525005(iProjectNo@1100525000 : Code[20];iSeqNo@1100525001 : Integer);
    BEGIN
      ProjectNo := iProjectNo;
      SeqNo := iSeqNo;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

