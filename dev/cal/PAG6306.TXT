OBJECT Page 6306 Power BI Report FactBox
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Power BI Report;
               NOR=Power BI-rapport;
               SVE=Power BI-rapport];
    PageType=CardPart;
    OnInit=BEGIN
             // Variables used by PingPong timer when deploying default PBI reports.
             TimerDelay := 30000; // 30 seconds
             MaxTimerCount := (60000 / TimerDelay) * 5; // 5 minutes
             IsVisible := FALSE;
           END;

    OnOpenPage=BEGIN
                 IF IsVisible THEN
                   LoadFactBox
               END;

    ActionList=ACTIONS
    {
      { 17      ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 16      ;1   ;Action    ;
                      Name=Select Report;
                      CaptionML=[ENU=Select Report;
                                 NOR=Velg rapport;
                                 SVE=VÑlj rapport];
                      ToolTipML=[ENU=Select the report.;
                                 NOR=Velg rapporten.;
                                 SVE=VÑlj rapporten.];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=NOT IsGettingStartedVisible AND NOT IsErrorMessageVisible;
                      Image=SelectChart;
                      OnAction=BEGIN
                                 SelectReports;
                               END;
                                }
      { 15      ;1   ;Action    ;
                      Name=Expand Report;
                      CaptionML=[ENU=Expand Report;
                                 NOR=Utvid rapport;
                                 SVE=Expandera rapport];
                      ToolTipML=[ENU=View all information in the report.;
                                 NOR=Vis all informasjon i rapporten.;
                                 SVE=Visa all information i rapporten.];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=HasReports AND NOT IsErrorMessageVisible;
                      Image=View;
                      OnAction=VAR
                                 PowerBiReportDialog@1000 : Page 6305;
                               BEGIN
                                 PowerBiReportDialog.SetUrl(GetEmbedUrlWithNavigationWithFilters,GetMessage);
                                 PowerBiReportDialog.CAPTION(TempPowerBiReportBuffer.ReportName);
                                 PowerBiReportDialog.SetFilter(messagefilter,reportfirstpage);
                                 PowerBiReportDialog.RUN;
                               END;
                                }
      { 14      ;1   ;Action    ;
                      Name=Previous Report;
                      CaptionML=[ENU=Previous Report;
                                 NOR=Forrige rapport;
                                 SVE=FîregÜende rapport];
                      ToolTipML=[ENU=Go to the previous report.;
                                 NOR=GÜ til forrige rapport.;
                                 SVE=GÜ till fîregÜende rapport.];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=HasReports AND NOT IsErrorMessageVisible;
                      Image=PreviousSet;
                      OnAction=BEGIN
                                 // need to reset filters or it would load the LastLoadedReport otherwise
                                 TempPowerBiReportBuffer.RESET;
                                 TempPowerBiReportBuffer.SETFILTER(Enabled,'%1',TRUE);
                                 IF TempPowerBiReportBuffer.NEXT(-1) = 0 THEN
                                   TempPowerBiReportBuffer.FINDLAST;

                                 IF AddInReady THEN
                                   CurrPage.WebReportViewer.Navigate(GetEmbedUrl);
                               END;
                                }
      { 13      ;1   ;Action    ;
                      Name=Next Report;
                      CaptionML=[ENU=Next Report;
                                 NOR=Neste rapport;
                                 SVE=NÑsta rapport];
                      ToolTipML=[ENU=Go to the next report.;
                                 NOR=GÜ til neste rapport.;
                                 SVE=GÜ till nÑsta rapport.];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=HasReports AND NOT IsErrorMessageVisible;
                      Image=NextSet;
                      OnAction=BEGIN
                                 // need to reset filters or it would load the LastLoadedReport otherwise
                                 TempPowerBiReportBuffer.RESET;
                                 TempPowerBiReportBuffer.SETFILTER(Enabled,'%1',TRUE);
                                 IF TempPowerBiReportBuffer.NEXT = 0 THEN
                                   TempPowerBiReportBuffer.FINDFIRST;

                                 IF AddInReady THEN
                                   CurrPage.WebReportViewer.Navigate(GetEmbedUrl);
                               END;
                                }
      { 18      ;1   ;Action    ;
                      Name=Refresh;
                      CaptionML=[ENU=Refresh Page;
                                 NOR=Oppdater side;
                                 SVE=Uppdatera sida];
                      ToolTipML=[ENU=Refresh the visible content.;
                                 NOR=Oppdater det synlige innholdet.;
                                 SVE=Uppdatera synligt innehÜll.];
                      ApplicationArea=#Basic,#Suite;
                      Enabled=NOT IsGettingStartedVisible;
                      Image=Refresh;
                      OnAction=BEGIN
                                 RefreshPart;
                               END;
                                }
      { 20      ;1   ;Action    ;
                      Name=Manage Report;
                      CaptionML=[ENU=Manage Report;
                                 NOR=Behandle rapport;
                                 SVE=Hantera rapport];
                      ToolTipML=[ENU=Opens current selected report for edits.;
                                 NOR=èpner gjeldende valgte rapport for redigering.;
                                 SVE=ôppnar aktuell vald rapport fîr redigering.];
                      ApplicationArea=#All;
                      Visible=IsSaaSUser;
                      Enabled=HasReports AND NOT IsErrorMessageVisible;
                      Image=PowerBI;
                      OnAction=VAR
                                 PowerBIManagement@1000 : Page 6319;
                               BEGIN
                                 PowerBIManagement.SetTargetReport(LastOpenedReportID,GetEmbedUrl);
                                 PowerBIManagement.LOOKUPMODE(TRUE);
                                 PowerBIManagement.RUN;
                                 RefreshPart;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                Name=Control1;
                ContainerType=ContentArea }

    { 3   ;1   ;Group     ;
                Visible=NOT IsGettingStartedVisible AND NOT IsErrorMessageVisible AND HasReports;
                GroupType=Group }

    { 4   ;2   ;Field     ;
                Name=WebReportViewer;
                ApplicationArea=#Basic,#Suite;
                ControlAddIn=[Microsoft.Dynamics.Nav.Client.WebPageViewer;PublicKeyToken=31bf3856ad364e35] }

    { 12  ;1   ;Group     ;
                GroupType=Group;
                Layout=Columns }

    { 11  ;2   ;Group     ;
                GroupType=Group }

    { 10  ;3   ;Group     ;
                Visible=IsGettingStartedVisible;
                GroupType=Group }

    { 9   ;4   ;Field     ;
                Name=GettingStarted;
                ToolTipML=[ENU=Specifies the process of connecting to Power BI.;
                           NOR=Angir prosessen med Ü koble til Power BI.;
                           SVE=Anger status fîr anslutningen till Power BI.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr='Get started with Power BI';
                Editable=FALSE;
                Style=StrongAccent;
                StyleExpr=TRUE;
                OnDrillDown=BEGIN
                              IF NOT TryAzureAdMgtGetAccessToken THEN
                                ShowErrorMessage(GETLASTERRORTEXT);

                              PowerBiServiceMgt.SelectDefaultReports;
                              LoadContent;
                            END;

                ShowCaption=No }

    { 8   ;3   ;Group     ;
                Visible=IsErrorMessageVisible;
                GroupType=Group }

    { 7   ;4   ;Field     ;
                ToolTipML=[ENU=Specifies the error message from Power BI.;
                           NOR=Angir feilmeldingen fr Power BI.;
                           SVE=Anger felmeddelandet frÜn Power BI.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ErrorMessageText;
                MultiLine=Yes;
                ShowCaption=No }

    { 19  ;4   ;Field     ;
                Name=ErrorUrlTextField;
                ExtendedDatatype=URL;
                ToolTipML=[ENU=Specifies the link that generated the error.;
                           NOR=Angir koblingen som genererte feilen.;
                           SVE=Anger lÑnken som skapade felet.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ErrorUrlText;
                Visible=IsUrlFieldVisible;
                ShowCaption=No }

    { 30  ;4   ;Group     ;
                Visible=IsGetReportsVisible;
                GroupType=Group }

    { 2   ;5   ;Field     ;
                Name=GetReportsLink;
                ToolTipML=[ENU=Specifies the reports.;
                           NOR=Angir rapportene.;
                           SVE=Anger rapporterna.];
                ApplicationArea=#All;
                SourceExpr='Get reports';
                Editable=FALSE;
                Style=StrongAccent;
                StyleExpr=TRUE;
                OnDrillDown=BEGIN
                              SelectReports;
                            END;

                ShowCaption=No }

    { 6   ;3   ;Group     ;
                Visible=NOT IsGettingStartedVisible AND NOT IsErrorMessageVisible AND NOT HasReports AND NOT IsDeployingReports AND NOT IsLicenseTimerActive AND NOT CheckingLicenseInBackground;
                GroupType=Group }

    { 5   ;4   ;Field     ;
                Name=EmptyMessage;
                CaptionML=[ENU=There are no enabled reports. Choose Select Report to see a list of reports that you can display.;
                           NOR=Det finnes ingen aktiverte rapporter. Klikk Velg rapport for Ü se en liste over rapporter du kan vise.;
                           SVE=Det finns inga aktiverade rapporter. Visa en lista med rapporter du kan visa med VÑlj rapport.];
                ToolTipML=[ENU=Specifies that the user needs to select Power BI reports.;
                           NOR=Angir at brukeren mÜ velge Power BI-rapporter.;
                           SVE=Anger att anvÑndaren mÜste vÑlja Power BI-rapporter.];
                ApplicationArea=#Basic,#Suite;
                Visible=NOT IsDeployingReports;
                Editable=FALSE;
                ShowCaption=No }

    { 22  ;3   ;Group     ;
                Visible=NOT IsDeploymentUnavailable AND IsDeployingReports AND NOT HasReports AND NOT CheckingLicenseInBackground;
                GroupType=Group }

    { 24  ;4   ;Field     ;
                Name=InProgressMessage;
                CaptionML=[ENU=Power BI report deployment is in progress.;
                           NOR=Distribusjon av Power BI-rapport pÜgÜr.;
                           SVE=Distribution av Power BI-rapport pÜgÜr.];
                ToolTipML=[ENU=Specifies that the page is deploying reports to Power BI.;
                           NOR=Angir at siden distribuerer rapporter til Power BI.;
                           SVE=Anger att sidan distribuerar rapporter till Power BI.];
                ApplicationArea=#All }

    { 28  ;3   ;Group     ;
                Visible=IsLicenseTimerActive OR CheckingLicenseInBackground AND NOT IsDeployingReports AND NOT IsErrorMessageVisible;
                GroupType=Group }

    { 27  ;4   ;Field     ;
                Name=CheckLicenseMessage;
                CaptionML=[ENU=Verifying your Power BI license.;
                           NOR=Sjekking av Power BI-lisensen.;
                           SVE=Verifierar din Power BI-licens.];
                ToolTipML=[ENU=Specifies that the page is checking for Power BI license.;
                           NOR=Angir at siden sjekker at man har en Power BI-lisens.;
                           SVE=Anger att sidan sîker efter en Power BI-licens.];
                ApplicationArea=#All;
                Editable=FALSE;
                ShowCaption=No }

    { 26  ;3   ;Group     ;
                Visible=IsDeploymentUnavailable AND NOT IsDeployingReports AND NOT HasReports;
                GroupType=Group }

    { 25  ;4   ;Field     ;
                Name=ServiceUnavailableMessage;
                CaptionML=[ENU=Power BI report deployment is currently unavailable.;
                           NOR=Distribusjon av Power BI-rapport er for õyeblikket ikke tilgjengelig.;
                           SVE=Distribution av Power BI-rapport kan fîr nÑrvarande inte anvÑndas.];
                ToolTipML=[ENU=Specifies that the page cannot currently deploy reports to Power BI.;
                           NOR=Angir at siden for õyeblikket ikke kan distribuere rapporter til Power BI.;
                           SVE=Anger att sidan fîr nÑrvarande inte kan distribuera rapporter till Power BI.];
                ApplicationArea=#All }

    { 21  ;3   ;Group      }

    { 23  ;4   ;Field     ;
                Name=DeployTimer;
                OptionCaptionML=[@@@={Locked};
                                 ENU="Height=1; Width=1;";
                                 NOR="Height=1; Width=1;";
                                 SVE="Height=1; Width=1;"];
                ApplicationArea=#All;
                ControlAddIn=[Microsoft.Dynamics.Nav.Client.PowerBIManagement;PublicKeyToken=31bf3856ad364e35] }

    { 29  ;4   ;Field     ;
                Name=LicenseTimer;
                OptionCaptionML=[@@@={Locked};
                                 ENU="Height=1; Width=1;";
                                 NOR="Height=1; Width=1;";
                                 SVE="Height=1; Width=1;"];
                ApplicationArea=#All;
                Visible=FALSE;
                Enabled=FALSE;
                ControlAddIn=[Microsoft.Dynamics.Nav.Client.PowerBIManagement;PublicKeyToken=31bf3856ad364e35] }

  }
  CODE
  {
    VAR
      NoReportsAvailableErr@1011 : TextConst 'ENU=There are no reports available from Power BI.;NOR=Det finnes ingen rapporter tilgjengelig fra Power BI.;SVE=Det finns inga rapporter att tillgÜ frÜn Power BI.';
      PowerBIUserConfiguration@1018 : Record 6304;
      TempPowerBiReportBuffer@1015 : TEMPORARY Record 6302;
      PowerBiServiceMgt@1013 : Codeunit 6301;
      AzureAdMgt@1012 : Codeunit 6300;
      SetPowerBIUserConfig@1017 : Codeunit 6305;
      ClientTypeManagement@1077 : Codeunit 4030;
      JObject@1019 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
      JObjecttemp@1020 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
      LastOpenedReportID@1034 : GUID;
      Context@1033 : Text[30];
      NameFilter@1032 : Text;
      IsGettingStartedVisible@1031 : Boolean;
      HasReports@1030 : Boolean;
      AddInReady@1029 : Boolean;
      IsErrorMessageVisible@1028 : Boolean;
      ErrorMessageText@1027 : Text;
      IsUrlFieldVisible@1026 : Boolean;
      IsGetReportsVisible@1025 : Boolean;
      ErrorUrlText@1024 : Text;
      CurrentListSelection@1023 : Text;
      reportLoadData@1022 : Text;
      IsValueInt@1021 : Boolean;
      ExceptionMessage@1016 : Text;
      ExceptionDetails@1010 : Text;
      messagefilter@1009 : Text;
      reportfirstpage@1008 : Text;
      TimerDelay@1007 : Integer;
      MaxTimerCount@1006 : Integer;
      CurrentTimerCount@1005 : Integer;
      IsDeployingReports@1004 : Boolean;
      IsDeploymentUnavailable@1003 : Boolean;
      IsTimerReady@1002 : Boolean;
      IsTimerActive@1001 : Boolean;
      IsSaaSUser@1000 : Boolean;
      IsLoaded@1035 : Boolean;
      IsVisible@1036 : Boolean;
      IsLicenseTimerActive@1037 : Boolean;
      CheckingLicenseInBackground@1014 : Boolean;

    PROCEDURE SetCurrentListSelection@1(CurrentSelection@1000 : Text;IsValueIntInput@1001 : Boolean;PowerBIVisible@1002 : Boolean);
    BEGIN
      IF NOT PowerBIVisible THEN
        EXIT;
      // get the name of the selected element from the corresponding list of the parent page and filter the report
      CurrentListSelection := PowerBiServiceMgt.FormatSpecialChars(CurrentSelection);
      IsValueInt := IsValueIntInput;
      GetAndSetReportFilter(reportLoadData);
    END;

    LOCAL PROCEDURE GetMessage@12() : Text;
    VAR
      HttpUtility@1000 : DotNet "'System.Web, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b03f5f7f11d50a3a'.System.Web.HttpUtility";
    BEGIN
      EXIT(
        '{"action":"loadReport","accessToken":"' +
        HttpUtility.JavaScriptStringEncode(AzureAdMgt.GetAccessToken(
            PowerBiServiceMgt.GetPowerBiResourceUrl,PowerBiServiceMgt.GetPowerBiResourceName,FALSE)) + '"}');
    END;

    LOCAL PROCEDURE GetEmbedUrl@2() : Text;
    BEGIN
      IF TempPowerBiReportBuffer.ISEMPTY THEN BEGIN
        // Clear out last opened report if there are no reports to display.
        CLEAR(LastOpenedReportID);
        SetLastOpenedReportID(LastOpenedReportID);
      END ELSE BEGIN
        // update last loaded report
        SetLastOpenedReportID(TempPowerBiReportBuffer.ReportID);
        // Hides both filters and tabs for embedding in small spaces where navigation is unnecessary.
        EXIT(TempPowerBiReportBuffer.EmbedUrl + '&filterPaneEnabled=false&navContentPaneEnabled=false');
      END;
    END;

    LOCAL PROCEDURE LoadContent@17();
    BEGIN
      // The end to end process for loading reports onscreen, or defaulting to an error state if that fails,
      // including deploying default reports in case they haven't been loaded yet. Called when first logging
      // into Power BI or any time the part has reloaded from scratch.
      IF NOT TryLoadPart THEN
        ShowErrorMessage(GETLASTERRORTEXT);

      // Always call this after TryLoadPart. Since we can't modify record from TryFunction.
      PowerBiServiceMgt.UpdateEmbedUrlCache(TempPowerBiReportBuffer,Context);

      // Always call this function after calling TryLoadPart to log exceptions to ActivityLog table
      PowerBiServiceMgt.LogException(ExceptionMessage,ExceptionDetails);

      IF NOT IsGettingStartedVisible THEN
        CheckPowerBILicense;

      CurrPage.UPDATE;

      DeployDefaultReports;
    END;

    LOCAL PROCEDURE LoadPart@5();
    BEGIN
      IsGettingStartedVisible := NOT PowerBiServiceMgt.IsUserReadyForPowerBI;

      TempPowerBiReportBuffer.RESET;
      TempPowerBiReportBuffer.DELETEALL;
      IF IsGettingStartedVisible THEN BEGIN
        IF AzureAdMgt.IsSaaS THEN
          ERROR(PowerBiServiceMgt.GetGenericError);

        TempPowerBiReportBuffer.INSERT // Hack to display Get Started link.
      END ELSE BEGIN
        PowerBiServiceMgt.GetReportsForUserContext(TempPowerBiReportBuffer,ExceptionMessage,ExceptionDetails,Context);
        RefreshAvailableReports;
      END;
    END;

    LOCAL PROCEDURE RefreshAvailableReports@7();
    BEGIN
      // Filters the report buffer to show the user's selected report onscreen if possible, otherwise defaulting
      // to other enabled reports.
      // (The updated selection will automatically get saved on render - can't save to database here without
      // triggering errors about calling MODIFY during a TryFunction.)

      TempPowerBiReportBuffer.RESET;
      TempPowerBiReportBuffer.SETFILTER(Enabled,'%1',TRUE);
      IF NOT ISNULLGUID(LastOpenedReportID) THEN BEGIN
        TempPowerBiReportBuffer.SETFILTER(ReportID,'%1',LastOpenedReportID);

        IF TempPowerBiReportBuffer.ISEMPTY THEN BEGIN
          // If last selection is invalid, clear it and default to showing the first enabled report.
          CLEAR(LastOpenedReportID);
          RefreshAvailableReports;
        END;
      END;

      HasReports := TempPowerBiReportBuffer.FINDFIRST;
    END;

    LOCAL PROCEDURE RefreshPart@22();
    BEGIN
      // Refreshes content by re-rendering the whole page part - removes any current error message text, and tries to
      // reload the user's list of reports, as if the page just loaded. Used by the Refresh button or when closing the
      // Select Reports page, to make sure we have the most up to date list of reports and aren't stuck in an error state.
      IsErrorMessageVisible := FALSE;
      IsUrlFieldVisible := FALSE;
      IsGetReportsVisible := FALSE;

      IsDeployingReports := PowerBiServiceMgt.GetIsDeployingReports;
      IsDeploymentUnavailable := NOT PowerBiServiceMgt.IsPBIServiceAvailable;

      PowerBiServiceMgt.SelectDefaultReports;

      SetPowerBIUserConfig.CreateOrReadUserConfigEntry(PowerBIUserConfiguration,LastOpenedReportID,Context);
      LoadContent;

      IF AddInReady THEN
        CurrPage.WebReportViewer.Navigate(GetEmbedUrl);
    END;

    PROCEDURE SetContext@6(ParentContext@1000 : Text[30]);
    BEGIN
      // Sets an ID that tracks which page to show reports for - called by the parent page hosting the part,
      // if possible (see UpdateContext).
      Context := ParentContext;
    END;

    LOCAL PROCEDURE UpdateContext@8();
    BEGIN
      // Automatically sets the parent page ID based on the user's selected role center (role centers can't
      // have codebehind, so they have no other way to set the context for their reports).
      IF Context = '' THEN
        SetContext(PowerBiServiceMgt.GetEnglishContext);
    END;

    PROCEDURE SetNameFilter@9(ParentFilter@1000 : Text);
    BEGIN
      // Sets a text value that tells the selection page how to filter the reports list. This should be called
      // by the parent page hosting this page part, if possible.
      NameFilter := ParentFilter;
    END;

    LOCAL PROCEDURE ShowErrorMessage@4(TextToShow@1000 : Text);
    BEGIN
      // this condition checks if we caught the authorization error that contains a link to Power BI
      // the function divide the error message into simple text and url part
      IF TextToShow = PowerBiServiceMgt.GetUnauthorizedErrorText THEN BEGIN
        IsUrlFieldVisible := TRUE;
        // this message is required to have ':' at the end, but it has '.' instead due to C/AL Localizability requirement
        TextToShow := DELSTR(PowerBiServiceMgt.GetUnauthorizedErrorText,STRLEN(PowerBiServiceMgt.GetUnauthorizedErrorText),1) + ':';
        ErrorUrlText := PowerBiServiceMgt.GetPowerBIUrl;
      END;

      IsGetReportsVisible := (TextToShow = NoReportsAvailableErr);

      IsErrorMessageVisible := TRUE;
      IsGettingStartedVisible := FALSE;
      TempPowerBiReportBuffer.DELETEALL; // Required to avoid one INSERT after another (that will lead to an error)
      IF TextToShow = '' THEN
        TextToShow := PowerBiServiceMgt.GetGenericError;
      ErrorMessageText := TextToShow;
      TempPowerBiReportBuffer.INSERT; // Hack to show the field with the text
      CurrPage.UPDATE;
    END;

    [TryFunction]
    LOCAL PROCEDURE TryLoadPart@11();
    BEGIN
      // Need the try function here to catch any possible internal errors
      LoadPart;
    END;

    [TryFunction]
    LOCAL PROCEDURE TryAzureAdMgtGetAccessToken@33();
    BEGIN
      AzureAdMgt.GetAccessToken(PowerBiServiceMgt.GetPowerBiResourceUrl,PowerBiServiceMgt.GetPowerBiResourceName,TRUE);
    END;

    LOCAL PROCEDURE SetReport@10();
    VAR
      JsonArray@1000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Array";
      DotNetString@1001 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.String";
    BEGIN
      IF (ClientTypeManagement.GetCurrentClientType <> CLIENTTYPE::Phone) AND
         (ClientTypeManagement.GetCurrentClientType <> CLIENTTYPE::Windows)
      THEN
        CurrPage.WebReportViewer.InitializeIFrame('4:3');
      // subscribe to events
      CurrPage.WebReportViewer.SubscribeToEvent('message',GetEmbedUrl);
      CurrPage.WebReportViewer.Navigate(GetEmbedUrl);
      JsonArray := JsonArray.CreateInstance(GETDOTNETTYPE(DotNetString),1);
      JsonArray.SetValue('{"statusCode":202,"headers":{}}',0);
      CurrPage.WebReportViewer.SetCallbacksFromSubscribedEventToIgnore('message',JsonArray);
    END;

    PROCEDURE SetLastOpenedReportID@14(LastOpenedReportIDInput@1000 : GUID);
    BEGIN
      // update the last loaded report field (the record at this point should already exist bacause it was created OnOpenPage)
      LastOpenedReportID := LastOpenedReportIDInput;
      PowerBIUserConfiguration.RESET;
      PowerBIUserConfiguration.SETFILTER("Page ID",'%1',Context);
      PowerBIUserConfiguration.SETFILTER("User Security ID",'%1',USERSECURITYID);
      PowerBIUserConfiguration.SETFILTER("Profile ID",'%1',PowerBiServiceMgt.GetEnglishContext);
      IF NOT PowerBIUserConfiguration.ISEMPTY THEN BEGIN
        PowerBIUserConfiguration."Selected Report ID" := LastOpenedReportID;
        PowerBIUserConfiguration.MODIFY;
        COMMIT;
      END;
    END;

    PROCEDURE SetFactBoxVisibility@13(VAR VisibilityInput@1000 : Boolean);
    BEGIN
      IF VisibilityInput THEN
        VisibilityInput := FALSE
      ELSE
        VisibilityInput := TRUE;

      PowerBIUserConfiguration.RESET;
      PowerBIUserConfiguration.SETFILTER("Page ID",'%1',Context);
      PowerBIUserConfiguration.SETFILTER("User Security ID",'%1',USERSECURITYID);
      PowerBIUserConfiguration.SETFILTER("Profile ID",'%1',PowerBiServiceMgt.GetEnglishContext);
      IF PowerBIUserConfiguration.FINDFIRST THEN BEGIN
        PowerBIUserConfiguration."Report Visibility" := VisibilityInput;
        PowerBIUserConfiguration.MODIFY;
      END;
      IF VisibilityInput AND NOT IsLoaded THEN
        LoadFactBox
    END;

    PROCEDURE GetAndSetReportFilter@64(data@1000 : Text);
    BEGIN
      IF NOT TryGetAndSetReportFilter(data) THEN BEGIN
        ExceptionMessage := GETLASTERRORTEXT;
        ExceptionDetails := data;
        PowerBiServiceMgt.LogException(ExceptionMessage,ExceptionDetails);
        EXIT;
      END
    END;

    LOCAL PROCEDURE GetEmbedUrlWithNavigationWithFilters@15() : Text;
    BEGIN
      // update last loaded report
      SetLastOpenedReportID(TempPowerBiReportBuffer.ReportID);
      // Shows filters and shows navigation tabs.
      EXIT(TempPowerBiReportBuffer.EmbedUrl);
    END;

    LOCAL PROCEDURE SelectReports@16();
    VAR
      PowerBIReportSelection@1000 : Page 6304;
      EmbedUrl@1001 : Text;
      PrevNameFilter@1002 : Text;
    BEGIN
      // Opens the report selection page, then updates the onscreen report depending on the user's
      // subsequent selection and enabled/disabled settings.
      PowerBIReportSelection.SetContext(Context);
      PrevNameFilter := NameFilter;
      IF Context <> '' THEN BEGIN
        NameFilter := PowerBiServiceMgt.GetFactboxFilterFromID(Context);
        IF NameFilter = '' THEN
          NameFilter := PrevNameFilter;
      END;
      PowerBIReportSelection.SetNameFilter(NameFilter);
      PowerBIReportSelection.LOOKUPMODE(TRUE);

      PowerBIReportSelection.RUNMODAL;
      IF PowerBIReportSelection.IsPageClosedOkay THEN BEGIN
        PowerBIReportSelection.GETRECORD(TempPowerBiReportBuffer);

        IF TempPowerBiReportBuffer.Enabled THEN BEGIN
          LastOpenedReportID := TempPowerBiReportBuffer.ReportID; // RefreshAvailableReports handles fallback logic on invalid selection.
          SetLastOpenedReportID(LastOpenedReportID); // Resolves bug to set last selected report
        END;

        RefreshPart;
        EmbedUrl := GetEmbedUrl;
        IF AddInReady AND (EmbedUrl <> '') THEN
          CurrPage.WebReportViewer.Navigate(GetEmbedUrl);
        // At this point, NAV will load the web page viewer since HasReports should be true. WebReportViewer::ControlAddInReady will then fire, calling Navigate()
      END;
    END;

    LOCAL PROCEDURE DeployDefaultReports@26();
    BEGIN
      IF NOT PowerBiServiceMgt.IsPowerBIDeploymentEnabled THEN
        EXIT;

      // Checks if there are any default reports the user needs to upload/select, and automatically begins
      // those processes. The page will refresh when the PingPong control runs later.
      DeleteMarkedReports;
      FinishPartialUploads;
      IF NOT CheckingLicenseInBackground AND NOT IsGettingStartedVisible AND NOT IsErrorMessageVisible AND AzureAdMgt.IsSaaS AND
         PowerBiServiceMgt.UserNeedsToDeployReports(Context) AND NOT PowerBiServiceMgt.IsUserDeployingReports
      THEN BEGIN
        IsDeployingReports := TRUE;
        PowerBiServiceMgt.UploadDefaultReportInBackground;
        StartDeploymentTimer;
      END;
    END;

    LOCAL PROCEDURE FinishPartialUploads@18();
    BEGIN
      // Checks if there are any default reports whose uploads only partially completed, and begins a
      // background process for those reports. The page will refresh when the PingPong control runs later.
      IF NOT CheckingLicenseInBackground AND NOT IsGettingStartedVisible AND NOT IsErrorMessageVisible AND AzureAdMgt.IsSaaS AND
         PowerBiServiceMgt.UserNeedsToRetryUploads AND NOT PowerBiServiceMgt.IsUserRetryingUploads
      THEN BEGIN
        IsDeployingReports := TRUE;
        PowerBiServiceMgt.RetryUnfinishedReportsInBackground;
        StartDeploymentTimer;
      END;
    END;

    LOCAL PROCEDURE DeleteMarkedReports@19();
    BEGIN
      // Checks if there are any default reports that have been marked to be deleted on page 6321, and begins
      // a background process for those reports. The page will refresh when the timer control runs later.
      IF NOT CheckingLicenseInBackground AND NOT IsGettingStartedVisible AND NOT IsErrorMessageVisible AND AzureAdMgt.IsSaaS AND
         PowerBiServiceMgt.UserNeedsToDeleteReports AND NOT PowerBiServiceMgt.IsUserDeletingReports
      THEN BEGIN
        IsDeployingReports := TRUE;
        PowerBiServiceMgt.DeleteDefaultReportsInBackground;
        StartDeploymentTimer;
      END;
    END;

    LOCAL PROCEDURE StartDeploymentTimer@25();
    BEGIN
      // Resets the timer for refreshing the page during OOB report deployment, if the add-in is
      // ready to go and the timer isn't already going. (This page doesn't deploy reports itself,
      // but it may be opened while another page is deploying reports that would show up here.)
      IF IsTimerReady AND NOT IsTimerActive AND NOT IsLicenseTimerActive THEN BEGIN
        CurrentTimerCount := 0;
        IsTimerActive := TRUE;
        CurrPage.DeployTimer.Ping(TimerDelay);
      END;
    END;

    PROCEDURE InitFactBox@3(PageId@1000 : Text[30];PageCaption@1001 : Text;VAR PowerBIVisible@1004 : Boolean);
    VAR
      PowerBIUserConfiguration@1003 : Record 6304;
      SetPowerBIUserConfig@1002 : Codeunit 6305;
    BEGIN
      SetNameFilter(PageCaption);
      SetContext(PageId);
      PowerBIVisible := SetPowerBIUserConfig.SetUserConfig(PowerBIUserConfiguration,PageId);
      IsVisible := PowerBIVisible;
    END;

    LOCAL PROCEDURE LoadFactBox@28();
    BEGIN
      UpdateContext;
      RefreshPart;
      IsSaaSUser := AzureAdMgt.IsSaaS;
      IsLoaded := TRUE;
    END;

    [TryFunction]
    LOCAL PROCEDURE TryGetAndSetReportFilter@21(data@1000 : Text);
    VAR
      firstpage@1002 : Text;
    BEGIN
      // get all pages of the report
      IF STRPOS(data,'reportPageLoaded') > 0 THEN BEGIN
        CurrPage.WebReportViewer.PostMessage('{"method":"GET","url":"/report/pages","headers":{"id":"getpagesfromreport"}}','*',TRUE);
        EXIT;
      END;

      // navigate to the first page of the report
      IF STRPOS(data,'getpagesfromreport') > 0 THEN BEGIN
        JObject := JObject.Parse(data);
        JObject := JObject.GetValue('body');
        JObject := JObject.First; // get the first (by index) page of the report
        firstpage := FORMAT(JObject.GetValue('name'));
        messagefilter := '{"method":"PUT","url":"/report/pages/active","headers":{"id":"setpage,' + firstpage +
          '"},"body": {"name":"' + firstpage + '","displayName": null}}';
        reportfirstpage := messagefilter;
        CurrPage.WebReportViewer.PostMessage(messagefilter,'*',TRUE);
        EXIT;
      END;

      // find all filters on this page of the report
      IF STRPOS(data,'setpage') > 0 THEN BEGIN
        JObject := JObject.Parse(data);
        JObject := JObject.GetValue('headers');
        firstpage := SELECTSTR(2,FORMAT(JObject.GetValue('id')));
        // messagefilter := '{"method":"GET","url":"/report/pages/' + firstpage + '/filters","headers": {"id":"getfilters,' + firstpage + '"}}'; // page filters
        messagefilter := '{"method":"GET","url":"/report/filters","headers": {"id":"getfilters,' + firstpage + '"}}'; // report filters
        CurrPage.WebReportViewer.PostMessage(messagefilter,'*',TRUE);
        EXIT;
      END;

      // change the filter value to the one received from the corresponding list (only for basic filters)
      IF (STRPOS(data,'getfilters') > 0) AND (STRPOS(data,'schema#basic') > 0) THEN BEGIN
        reportLoadData := data; // save data for filter update on change of selected list element

        JObject := JObject.Parse(data);
        JObjecttemp := JObject.GetValue('headers');
        firstpage := SELECTSTR(2,FORMAT(JObjecttemp.GetValue('id')));
        JObject := JObject.GetValue('body');
        // filter only if there is a filter in the report
        IF JObject.Count > 0 THEN BEGIN
          JObject := JObject.First;
          JObjecttemp := JObject.GetValue('target');

          messagefilter := '{"$schema":"' + FORMAT(JObject.GetValue('$schema')) +
            '","target":{"table":"' + FORMAT(JObjecttemp.GetValue('table')) + '","column":"' +
            FORMAT(JObjecttemp.GetValue('column')) + '"},';
          // filter parameter can be string, then value should be in ""; or it can be an integer, then no "" are required
          IF IsValueInt THEN
            messagefilter := messagefilter + '"operator":"In","values":[' + CurrentListSelection + ']}'
          ELSE
            messagefilter := messagefilter + '"operator":"In","values":["' + CurrentListSelection + '"]}';

          messagefilter := '{"method": "PUT", "url": "/report/filters", "headers": {}, "body": [' +
            messagefilter + '] }';
          CurrPage.WebReportViewer.PostMessage(messagefilter,'*',TRUE);
        END;

        EXIT;
      END;
    END;

    LOCAL PROCEDURE StartLicenseTimer@24();
    BEGIN
      IF CheckingLicenseInBackground AND IsTimerReady AND NOT IsLicenseTimerActive AND NOT IsTimerActive THEN BEGIN
        CurrentTimerCount := 0;
        IsLicenseTimerActive := TRUE;
        CurrPage.DeployTimer.Ping(TimerDelay);
      END;
    END;

    LOCAL PROCEDURE CheckPowerBILicense@53();
    BEGIN
      IF NOT PowerBiServiceMgt.CheckForPowerBILicense THEN BEGIN
        CheckingLicenseInBackground := TRUE;
        StartLicenseTimer;
      END;
    END;

    EVENT WebReportViewer@-4::ControlAddInReady@9(callbackUrl@1000 : Text);
    BEGIN
      AddInReady := TRUE;
      IF NOT TempPowerBiReportBuffer.ISEMPTY THEN
        SetReport;
    END;

    EVENT WebReportViewer@-4::DocumentReady@10();
    BEGIN
      IF NOT TempPowerBiReportBuffer.ISEMPTY THEN
        CurrPage.WebReportViewer.PostMessage(GetMessage,'*',FALSE);
    END;

    EVENT WebReportViewer@-4::Callback@11(data@1000 : Text);
    BEGIN
      GetAndSetReportFilter(data);
    END;

    EVENT WebReportViewer@-4::Refresh@12(callbackUrl@1000 : Text);
    BEGIN
      IF AddInReady AND NOT TempPowerBiReportBuffer.ISEMPTY THEN
        SetReport;
    END;

    EVENT DeployTimer@-23::AddInReady@5();
    BEGIN
      // Timer for refreshing the page during OOB report deployment started by another page.
      IsTimerReady := TRUE;
      StartLicenseTimer;
      IF NOT CheckingLicenseInBackground AND IsDeployingReports AND NOT IsErrorMessageVisible THEN
        StartDeploymentTimer;
    END;

    EVENT DeployTimer@-23::Pong@6();
    VAR
      HasPowerBILicense@1000 : Boolean;
    BEGIN
      HasPowerBILicense := PowerBiServiceMgt.CheckForPowerBILicense;
      CurrentTimerCount := 0;
      IsLicenseTimerActive := NOT HasPowerBILicense;
      CheckingLicenseInBackground := NOT HasPowerBILicense;

      IF HasPowerBILicense THEN BEGIN
        RefreshPart;

        IF IsTimerActive AND NOT IsLicenseTimerActive AND NOT CheckingLicenseInBackground
        THEN BEGIN
          PowerBiServiceMgt.SelectDefaultReports;
          DeployDefaultReports;

          IsDeployingReports := PowerBiServiceMgt.GetIsDeployingReports;

          CurrentTimerCount := CurrentTimerCount + 1;
          IsTimerActive := IsDeployingReports AND (CurrentTimerCount < MaxTimerCount) AND NOT IsErrorMessageVisible;
          IF IsTimerActive THEN
            CurrPage.DeployTimer.Ping(TimerDelay);
        END;
      END ELSE BEGIN
        ShowErrorMessage(PowerBiServiceMgt.GetUnauthorizedErrorText);
        CurrPage.DeployTimer.Stop;
      END;

      IF NOT IsDeployingReports AND HasReports THEN
        CurrPage.DeployTimer.Stop;
    END;

    EVENT LicenseTimer@-29::AddInReady@5();
    BEGIN
      CurrPage.LicenseTimer.Stop;
    END;

    EVENT LicenseTimer@-29::Pong@6();
    BEGIN
      CurrPage.LicenseTimer.Stop;
    END;

    BEGIN
    END.
  }
}

