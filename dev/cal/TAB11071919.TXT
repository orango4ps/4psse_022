OBJECT Table 11071919 Project-Building-Object Rel.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Project - Building - Object Relation;
               NOR=Prosjekt Í Objektrelasjon;
               SVE=Projekt-byggnad-obj.relation];
    LookupPageID=Page11126279;
    DrillDownPageID=Page11126279;
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr] }
    { 20  ;   ;Building Code       ;Code20        ;TableRelation=Building;
                                                   CaptionML=[ENU=Building;
                                                              NOR=Byggning;
                                                              SVE=Byggnad] }
    { 30  ;   ;Object No.          ;Code20        ;TableRelation=Service-Object;
                                                   OnValidate=VAR
                                                                BuildingObjectRelation@1100528600 : Record 11071820;
                                                              BEGIN
                                                                IF "Object No." <> '' THEN BEGIN
                                                                  BuildingObjectRelation.SETRANGE("Building Code", "Building Code");
                                                                  BuildingObjectRelation.SETRANGE("Object No.", "Object No.");
                                                                  BuildingObjectRelation.FINDFIRST;
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              ServiceObject@1100528600 : Record 11071691;
                                                              BuildingObjectRelation@1100528601 : Record 11071820;
                                                            BEGIN
                                                              BuildingObjectRelation.SETRANGE("Building Code", "Building Code");
                                                              IF BuildingObjectRelation.FINDSET THEN
                                                                REPEAT
                                                                  ServiceObject."No." := BuildingObjectRelation."Object No.";
                                                                  ServiceObject.MARK(TRUE);
                                                                UNTIL BuildingObjectRelation.NEXT = 0;
                                                              ServiceObject.MARKEDONLY(TRUE);
                                                              IF "Object No." <> '' THEN
                                                                IF ServiceObject.GET("Object No.") THEN;
                                                              IF PAGE.RUNMODAL(0, ServiceObject) = ACTION::LookupOK THEN
                                                                VALIDATE("Object No.", ServiceObject."No.");
                                                            END;

                                                   CaptionML=[ENU=Object No.;
                                                              NOR=Objektnr.;
                                                              SVE=Objektnr];
                                                   NotBlank=Yes }
    { 40  ;   ;Object Description  ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object.Description WHERE (No.=FIELD(Object No.)));
                                                   CaptionML=[ENU=Object Description;
                                                              NOR=Objektbeskrivelse;
                                                              SVE=Objektbeskrivning];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Project No.,Building Code,Object No.    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE SelectBuildingObjectRels@1100528615();
    VAR
      BuildingObjectRelation@1100528600 : Record 11071820;
      ProjectBuildingObjectRel@1100528602 : Record 11071919;
      BuildingObjectRelations@1100528601 : Page 11126264;
    BEGIN
      BuildingObjectRelation.FILTERGROUP(9);
      BuildingObjectRelation.SETRANGE("Building Code", "Building Code");
      BuildingObjectRelation.FILTERGROUP(0);
      IF "Object No." <> '' THEN
        IF BuildingObjectRelation.GET("Building Code", "Object No.") THEN;

      BuildingObjectRelations.LOOKUPMODE(TRUE);
      BuildingObjectRelations.SetSelection(DATABASE::"Project-Building-Object Rel.", "Project No.", "Building Code");
      BuildingObjectRelations.SETTABLEVIEW(BuildingObjectRelation);
      BuildingObjectRelations.SETRECORD(BuildingObjectRelation);
      IF BuildingObjectRelations.RUNMODAL = ACTION::LookupOK THEN BEGIN
        BuildingObjectRelations.GETRECORD(BuildingObjectRelation);
        ProjectBuildingObjectRel.INIT;
        ProjectBuildingObjectRel."Project No." := "Project No.";
        ProjectBuildingObjectRel."Building Code" := BuildingObjectRelation."Building Code";
        ProjectBuildingObjectRel.VALIDATE("Object No.", BuildingObjectRelation."Object No.");
        ProjectBuildingObjectRel.INSERT(TRUE);
      END;
    END;

    BEGIN
    END.
  }
}

