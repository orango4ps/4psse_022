OBJECT Table 11012912 Investment Proposal
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               lvProjDevelopRec@1210190000 : Record 11012915;
               lvInvPropRec@1210190001 : Record 11012912;
             BEGIN
               IF lvProjDevelopRec.GET("Project No.") THEN BEGIN
                 lvInvPropRec.SETRANGE("Project No.", "Project No.");
                 IF NOT lvInvPropRec.FIND('-') THEN BEGIN
                   lvProjDevelopRec."Investment Proposal" := Version;
                   lvProjDevelopRec."Actual Exploitation" := Version;
                   lvProjDevelopRec.MODIFY;
                 END;
               END;
             END;

    OnModify=BEGIN
               TESTFIELD(Fixed, FALSE);
             END;

    OnDelete=VAR
               lvProjDevelopRec@1210190002 : Record 11012915;
               lvInvPropRubricRec@1210190001 : Record 11012913;
               lvInvPropBudgetRec@1210190000 : Record 11012914;
               lvBudgetToDistrRec@1100485000 : Record 11012909;
             BEGIN
               TESTFIELD(Fixed, FALSE);

               IF lvProjDevelopRec.GET("Project No.") THEN BEGIN
                 IF (lvProjDevelopRec."Investment Proposal" = Version) OR
                    (lvProjDevelopRec."Definite Exploitation" = Version) OR
                    (lvProjDevelopRec."Actual Exploitation" = Version)
                 THEN BEGIN
                   IF (lvProjDevelopRec."Investment Proposal" = Version) THEN
                     lvProjDevelopRec."Investment Proposal" := '';
                   IF (lvProjDevelopRec."Definite Exploitation" = Version) THEN
                     lvProjDevelopRec."Definite Exploitation" := '';
                   IF (lvProjDevelopRec."Actual Exploitation" = Version) THEN
                     lvProjDevelopRec."Actual Exploitation" := '';
                   lvProjDevelopRec.MODIFY;
                 END;
               END;

               lvBudgetToDistrRec.SETRANGE("Project No.", "Project No.");
               lvBudgetToDistrRec.SETRANGE(Version, Version);
               lvBudgetToDistrRec.DELETEALL;

               lvInvPropBudgetRec.SETRANGE("Project No.", "Project No.");
               lvInvPropBudgetRec.SETRANGE(Version, Version);
               lvInvPropBudgetRec.DELETEALL;

               lvInvPropRubricRec.SETRANGE("Project No.", "Project No.");
               lvInvPropRubricRec.SETRANGE(Version, Version);
               lvInvPropRubricRec.DELETEALL;
             END;

    OnRename=VAR
               lvProjDevelopRec@1210190000 : Record 11012915;
             BEGIN
               TESTFIELD(Fixed, FALSE);

               IF lvProjDevelopRec.GET("Project No.") THEN BEGIN
                 IF (lvProjDevelopRec."Investment Proposal" = xRec.Version) OR
                    (lvProjDevelopRec."Definite Exploitation" = xRec.Version) OR
                    (lvProjDevelopRec."Actual Exploitation" = xRec.Version)
                 THEN BEGIN
                   IF (lvProjDevelopRec."Investment Proposal" = xRec.Version) THEN
                     lvProjDevelopRec."Investment Proposal" := Version;
                   IF (lvProjDevelopRec."Definite Exploitation" = xRec.Version) THEN
                     lvProjDevelopRec."Definite Exploitation" := Version;
                   IF (lvProjDevelopRec."Actual Exploitation" = xRec.Version) THEN
                     lvProjDevelopRec."Actual Exploitation" := Version;
                   lvProjDevelopRec.MODIFY;
                 END;
               END;
             END;

    CaptionML=[ENU=Investment Proposal;
               NOR=Investeringsforslag;
               SVE=Investeringsf”rslag];
    LookupPageID=Page11012661;
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation="Project Development"."Project No.";
                                                   OnValidate=BEGIN
                                                                IF ("Project No." <> xRec."Project No.") AND ("WACC Percentage" = 0) THEN BEGIN
                                                                  ProjDevelopRec.GET("Project No.");
                                                                  "WACC Percentage" := ProjDevelopRec."Average Capital Cost Perc.";
                                                                END;
                                                                CALCFIELDS("Project Description");
                                                              END;

                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr];
                                                   NotBlank=Yes }
    { 20  ;   ;Project Description ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Job.Description WHERE (No.=FIELD(Project No.)));
                                                   CaptionML=[ENU=Project Description;
                                                              NOR=Prosjektbeskrivelse;
                                                              SVE=Projektbeskrivning];
                                                   Editable=No }
    { 30  ;   ;Version             ;Code10        ;CaptionML=[ENU=Version;
                                                              NOR=Versjon;
                                                              SVE=Version];
                                                   NotBlank=Yes }
    { 40  ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 50  ;   ;Version Date        ;Date          ;CaptionML=[ENU=Version Date;
                                                              NOR=Versjonsdato;
                                                              SVE=Versionsdatum] }
    { 60  ;   ;WACC Percentage     ;Decimal       ;CaptionML=[ENU=Wacc Percentage;
                                                              NOR=Vektet kapitalkostnad, prosent;
                                                              SVE=Viktad kapitalkostnad, procent];
                                                   MinValue=0;
                                                   Description=Percentage: Weight Average Capital Costs (gewogen gemiddelde vermogenskosten) }
    { 70  ;   ;Phase               ;Code10        ;TableRelation="Project Development Phase".Phase WHERE (Project No.=FIELD(Project No.));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Phase Description", "Score Percentage");
                                                              END;

                                                   CaptionML=[ENU=Phase;
                                                              NOR=Fase;
                                                              SVE=Fas] }
    { 71  ;   ;Phase Description   ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Standard Development Phase".Description WHERE (Phase=FIELD(Phase)));
                                                   CaptionML=[ENU=Phase Description;
                                                              NOR=Fasebeskrivelse;
                                                              SVE=Fasbeskrivning];
                                                   Editable=No }
    { 72  ;   ;Score Percentage    ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Development Phase"."Score Percentage" WHERE (Project No.=FIELD(Project No.),
                                                                                                                            Phase=FIELD(Phase)));
                                                   CaptionML=[ENU=Score Percentage;
                                                              NOR=Poengprosent;
                                                              SVE=Po„ngprocent];
                                                   MinValue=0;
                                                   MaxValue=100;
                                                   Editable=No }
    { 80  ;   ;Fixed               ;Boolean       ;CaptionML=[ENU=Fixed;
                                                              NOR=Fast;
                                                              SVE=Fast] }
    { 100 ;   ;Is Actual Exploitation;Boolean     ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Project Development" WHERE (Project No.=FIELD(Project No.),
                                                                                                  Actual Exploitation=FIELD(Version)));
                                                   CaptionML=[ENU=Is Actual Exploitation;
                                                              NOR=Er faktisk utnyttelse;
                                                              SVE=Žr verklig exploatering];
                                                   Editable=No }
    { 110 ;   ;Reference Date      ;Date          ;OnValidate=VAR
                                                                lvInvPropBudgetRec@1100485000 : Record 11012914;
                                                              BEGIN
                                                                lvInvPropBudgetRec.SETCURRENTKEY("Project No.", Version, Date);
                                                                lvInvPropBudgetRec.SETRANGE("Project No.", "Project No.");
                                                                lvInvPropBudgetRec.SETRANGE(Version, Version);
                                                                IF lvInvPropBudgetRec.FIND('-') THEN BEGIN
                                                                  IF "Reference Date" > lvInvPropBudgetRec.Date THEN
                                                                    FIELDERROR("Reference Date", STRSUBSTNO(Text001,"Reference Date",lvInvPropBudgetRec.Date));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Reference Date;
                                                              NOR=Referansedato;
                                                              SVE=Referensdatum];
                                                   NotBlank=Yes }
    { 120 ;   ;Starting Date Sales ;Date          ;CaptionML=[ENU=Starting Date Sales;
                                                              NOR=Startdato salg;
                                                              SVE=Startdatum, f”rs„ljning] }
    { 130 ;   ;Starting Date Construction;Date    ;CaptionML=[ENU=Starting Date Construction;
                                                              NOR=Startdato bygging;
                                                              SVE=Startdatum, konstruktion] }
    { 140 ;   ;Delivery Date       ;Date          ;CaptionML=[ENU=Delivery Date;
                                                              NOR=Leveringsdato;
                                                              SVE=Leveransdatum] }
    { 150 ;   ;ROS % (Budget)      ;Decimal       ;CaptionML=[ENU=ROS % (Budget);
                                                              NOR=ROS % (Budsjett);
                                                              SVE=ROS % (budget)];
                                                   Editable=No }
    { 160 ;   ;EPV (Budget)        ;Decimal       ;CaptionML=[ENU=EPV (Budget);
                                                              NOR=EPV (Budsjett);
                                                              SVE=EPV (budget)];
                                                   Editable=No }
    { 170 ;   ;PRI % (Budget)      ;Decimal       ;CaptionML=[ENU=PRI % (Budget);
                                                              NOR=PRI % (Budsjett);
                                                              SVE=PRI % (budget)];
                                                   Editable=No }
    { 180 ;   ;ROS % (Total)       ;Decimal       ;CaptionML=[ENU=ROS % (Total);
                                                              NOR=ROS % (Budsjett);
                                                              SVE=ROS % (totalt)];
                                                   Editable=No }
    { 190 ;   ;EPV (Total)         ;Decimal       ;CaptionML=[ENU=EPV (Total);
                                                              NOR=EPV (Budsjett);
                                                              SVE=EPV (totalt)];
                                                   Editable=No }
    { 200 ;   ;PRI % (Total)       ;Decimal       ;CaptionML=[ENU=PRI % (Total);
                                                              NOR=PRI % (Budsjett);
                                                              SVE=PRI % (totalt)];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Project No.,Version                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ProjDevelopRec@1210190000 : Record 11012915;
      Text001@1100485000 : TextConst 'ENU=%1 not allowed, there is already budget present from %2;NOR=%1 ikke tillatt, det finnes allerede et budsjett fra %2;SVE=%1 „r inte till†tet, budget finns redan fr†n %2';

    PROCEDURE ResetFixed@1100485000();
    VAR
      lvInvPropRec@1100485000 : Record 11012912;
      lvText000@1100485001 : TextConst 'ENU=Reopen the Investment Proposal (resed ''%1''), are you sure?;NOR=Gjen†pne investeringsforslaget (nullstill ''%1''), er du sikker?;SVE=ter”ppna investeringsf”rslag (†terst„ll ''%1''), „r du s„ker?';
      lvText001@1100485002 : TextConst 'ENU=\ \Note: There is/are %2 following version(s) present (whereof %3 Fixed).;NOR=\ \Obs: Det er %2 finnes etterf›lgende versjon(er) (hvorav %3 fast(e)).;SVE=\ \Obs! F”ljande %2 version(er) finns (varav %3 „r fast).';
      lvFollowing@1100485004 : Integer;
      lvFixedFollow@1100485005 : Integer;
    BEGIN
      TESTFIELD(Fixed, TRUE);
      IF NOT lvInvPropRec.GET("Project No.", Version) THEN
        EXIT;

      lvInvPropRec.SETRANGE("Project No.", "Project No.");
      lvInvPropRec.SETFILTER(Version, '>%1', Version);
      lvFollowing := lvInvPropRec.COUNT;
      IF lvFollowing = 0 THEN BEGIN
        IF NOT CONFIRM(lvText000, FALSE, FIELDCAPTION(Fixed)) THEN
          EXIT;
      END ELSE BEGIN
        lvInvPropRec.SETRANGE(Fixed, TRUE);
        lvFixedFollow := lvInvPropRec.COUNT;
        IF NOT CONFIRM(lvText000 + lvText001, FALSE, FIELDCAPTION(Fixed), lvFollowing, lvFixedFollow) THEN
         EXIT;
      END;

      lvInvPropRec.GET("Project No.", Version);
      lvInvPropRec.Fixed := FALSE;
      lvInvPropRec.MODIFY;
    END;

    PROCEDURE UpdateROS_EPV_PRI@1100525000(PeriodType@1100525000 : Option;ReferenceDate@1100525004 : Date;EPVCalculationOn@1100525002 : Boolean;Budget@1100525003 : Boolean);
    VAR
      ProjectDevelopmentFunctionsCU@1100525001 : Codeunit 11012903;
    BEGIN
      //mg, 16-11-10: M19522
      IF Budget THEN BEGIN
        "ROS % (Budget)" := ROUND(ProjectDevelopmentFunctionsCU.CalculateTotalRos("Project No.", Version, ReferenceDate,
          TRUE));
        IF EPVCalculationOn THEN BEGIN
          "EPV (Budget)" := ROUND(ProjectDevelopmentFunctionsCU.CalculateTotalEPV("Project No.", Version, ReferenceDate, PeriodType,
            TRUE));
          "PRI % (Budget)" := ROUND(ProjectDevelopmentFunctionsCU.CalculateTotalPRI("Project No.", Version, ReferenceDate, PeriodType,
            TRUE));
        END;
      END ELSE BEGIN
        "ROS % (Total)" := ROUND(ProjectDevelopmentFunctionsCU.CalculateTotalRos("Project No.", Version, ReferenceDate,
          FALSE));
        IF EPVCalculationOn THEN BEGIN
          "EPV (Total)" := ROUND(ProjectDevelopmentFunctionsCU.CalculateTotalEPV("Project No.", Version, ReferenceDate, PeriodType,
            FALSE));
          "PRI % (Total)" := ROUND(ProjectDevelopmentFunctionsCU.CalculateTotalPRI("Project No.", Version, ReferenceDate, PeriodType,
            FALSE));
        END;
      END;
      MODIFY;
    END;

    BEGIN
    END.
  }
}

