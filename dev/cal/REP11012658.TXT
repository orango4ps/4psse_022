OBJECT Report 11012658 Process IC Revaluations
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=IC-Neubewertungen verarbeiten;
               ENU=Process IC Revaluations;
               NLD=IC herwaarderingen verwerken;
               NOR=Behandle IC-revalueringer;
               SVE=Behandla IC-omv„rderingar];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 1121;    ;DataItem;                    ;
               DataItemTable=Table11020696;
               DataItemTableView=SORTING(Post Date,Item No.);
               OnPreDataItem=VAR
                               ItemJournalLine@1100528300 : Record 83;
                             BEGIN
                               ItemJournalLine."Journal Template Name" := TemplateName;
                               ItemJournalLine."Journal Batch Name" := BatchName;
                               ItemJournalLine.TESTFIELD("Journal Template Name");
                               ItemJournalLine.TESTFIELD("Journal Batch Name");

                               SourceCodeSetup.GET;
                             END;

               OnAfterGetRecord=VAR
                                  ItemJournalLine@1100528300 : Record 83;
                                  Item@1100528301 : Record 27;
                                BEGIN
                                  IF (NOT Item.GET("Item No.")) THEN
                                    CurrReport.SKIP;

                                  Item.CALCFIELDS(Inventory);
                                  //IF (Item.Inventory = 0) THEN
                                  IF (Item.Inventory <= 0) THEN //#C009371
                                    CurrReport.SKIP;

                                  ItemJournalLine."Journal Template Name" := TemplateName;
                                  ItemJournalLine."Journal Batch Name" := BatchName;
                                  ItemJournalLine."Line No." := GetLastLineNo + 10000;
                                  ItemJournalLine.INSERT;

                                  ItemJournalLine."Entry Type" := ItemJournalLine."Entry Type"::"Positive Adjmt.";
                                  ItemJournalLine."Value Entry Type" := ItemJournalLine."Value Entry Type"::Revaluation;
                                  ItemJournalLine.VALIDATE("Item No.", "Item No.");

                                  ItemJournalLine."Document Date" := TODAY;
                                  ItemJournalLine."Document No." := GetDocumentNo;
                                  ItemJournalLine."Posting Date" := "Post Date";
                                  ItemJournalLine."Inventory Value Per" := ItemJournalLine."Inventory Value Per"::Item;
                                  ItemJournalLine."Partial Revaluation" := TRUE;
                                  ItemJournalLine."Source Code" := SourceCodeSetup."Revaluation Journal";

                                  ItemJournalLine.VALIDATE(Quantity, Item.Inventory);
                                  ItemJournalLine.VALIDATE("Unit Cost (Calculated)", Item."Standard Cost");
                                  ItemJournalLine.VALIDATE("Unit Cost (Revalued)", "Unit Cost (Revalued)");
                                  ItemJournalLine."Unit Cost (Revalued)" := "Unit Cost (Revalued)";
                                  ItemJournalLine.VALIDATE("Invoiced Quantity", ItemJournalLine.Quantity);
                                  ItemJournalLine.VALIDATE("Inventory Value (Calculated)", ItemJournalLine."Unit Cost (Calculated)" * ItemJournalLine.Quantity);
                                  ItemJournalLine.VALIDATE("Inventory Value (Revalued)", ItemJournalLine."Unit Cost (Revalued)" * ItemJournalLine.Quantity);
                                  ItemJournalLine."Update Standard Cost" := TRUE;
                                  ItemJournalLine.MODIFY;
                                END;

               ReqFilterFields=Post Date }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      SourceCodeSetup@1100528305 : Record 242;
      TemplateName@1100528301 : Code[20];
      BatchName@1100528300 : Code[20];

    LOCAL PROCEDURE GetLastLineNo@1100528300() : Integer;
    VAR
      ItemJournalLine@1100528300 : Record 83;
    BEGIN
      ItemJournalLine.SETRANGE("Journal Template Name", TemplateName);
      ItemJournalLine.SETRANGE("Journal Batch Name", BatchName);
      IF (ItemJournalLine.FINDLAST) THEN
        EXIT(ItemJournalLine."Line No.");
      EXIT(0);
    END;

    LOCAL PROCEDURE GetDocumentNo@1100528303() : Code[20];
    VAR
      ItemJournalBatch@1100528301 : Record 233;
      NoSeriesMgt@1100528300 : Codeunit 396;
    BEGIN
      ItemJournalBatch.GET(TemplateName, BatchName);
      IF ItemJournalBatch."No. Series" = '' THEN
        EXIT('');

      EXIT(NoSeriesMgt.GetNextNo(ItemJournalBatch."No. Series", "IC Revaluation"."Post Date", FALSE));
    END;

    PROCEDURE SetBatchName@1100528307(Name@1100528300 : Code[20]);
    BEGIN
      BatchName := Name;
    END;

    PROCEDURE SetTemplateName@1100528308(Name@1100528300 : Code[20]);
    BEGIN
      TemplateName := Name;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

