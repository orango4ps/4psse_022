OBJECT Codeunit 346 Purch. Line CaptionClass Mgmt
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04;
  }
  PROPERTIES
  {
    SingleInstance=Yes;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      GlobalPurchaseHeader@1000 : Record 38;
      GlobalField@1001 : Record 2000000041;

    [External]
    PROCEDURE GetPurchaseLineCaptionClass@12(VAR PurchaseLine@1000 : Record 39;FieldNumber@1001 : Integer) : Text;
    VAR
      Caption@1003 : Text;
      IsHandled@1002 : Boolean;
    BEGIN
      IF (GlobalPurchaseHeader."Document Type" <> PurchaseLine."Document Type") OR
         (GlobalPurchaseHeader."No." <> PurchaseLine."Document No.")
      THEN
        IF NOT GlobalPurchaseHeader.GET(PurchaseLine."Document Type",PurchaseLine."Document No.") THEN
          CLEAR(GlobalPurchaseHeader);

      OnGetPurchaseLineCaptionClass(GlobalPurchaseHeader,PurchaseLine,FieldNumber,IsHandled,Caption);
      IF IsHandled THEN
        EXIT(Caption);

      CASE FieldNumber OF
        PurchaseLine.FIELDNO("No."):
          EXIT(STRSUBSTNO('3,%1',GetFieldCaption(DATABASE::"Purchase Line",FieldNumber)));
        ELSE BEGIN
          IF GlobalPurchaseHeader."Prices Including VAT" THEN
            EXIT('2,1,' + GetFieldCaption(DATABASE::"Purchase Line",FieldNumber));
          EXIT('2,0,' + GetFieldCaption(DATABASE::"Purchase Line",FieldNumber));
        END;
      END;
    END;

    LOCAL PROCEDURE GetFieldCaption@31(TableNumber@1001 : Integer;FieldNumber@1000 : Integer) : Text;
    BEGIN
      IF (GlobalField.TableNo <> TableNumber) OR (GlobalField."No." <> FieldNumber) THEN
        GlobalField.GET(TableNumber,FieldNumber);
      EXIT(GlobalField."Field Caption");
    END;

    [Integration]
    LOCAL PROCEDURE OnGetPurchaseLineCaptionClass@2(PurchaseHeader@1002 : Record 38;VAR PurchaseLine@1001 : Record 39;FieldNumber@1000 : Integer;VAR IsHandled@1003 : Boolean;VAR Caption@1004 : Text);
    BEGIN
    END;

    [EventSubscriber(Table,38,OnAfterChangePricesIncludingVAT,"",Skip,Skip)]
    LOCAL PROCEDURE PurchaseHeaderChangedPricesIncludingVAT@11(VAR PurchaseHeader@1000 : Record 38);
    BEGIN
      GlobalPurchaseHeader := PurchaseHeader;
    END;

    [EventSubscriber(Table,38,OnValidatePurchaseHeaderPayToVendorNo,"",Skip,Skip)]
    LOCAL PROCEDURE UpdatePurchLineFieldsCaptionOnValidatePurchaseHeaderPayToVendorNo@1(VAR Sender@1000 : Record 38;Vendor@1001 : Record 23);
    BEGIN
      GlobalPurchaseHeader := Sender;
    END;

    BEGIN
    END.
  }
}

