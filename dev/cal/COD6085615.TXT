OBJECT Codeunit 6085615 DC Dimension Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW17.00.00.4.50;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      GLSetup@1160040001 : Record 98;
      GotGLSetup@1160040000 : Boolean;

    PROCEDURE UpdatePurchLineDim@1160040000(VAR PurchLine@1160040000 : Record 39;DimCode@1160040002 : Code[20];ValueCode@1160040003 : Code[20]);
    VAR
      DimSetEntry@1001 : TEMPORARY Record 480;
      DimMgt@1000 : Codeunit 408;
    BEGIN
      GetGLSetup;

      CASE DimCode OF
        GLSetup."Global Dimension 1 Code":
          BEGIN
            PurchLine.VALIDATE("Shortcut Dimension 1 Code",ValueCode);
            PurchLine.MODIFY(TRUE);
          END;

        GLSetup."Global Dimension 2 Code":
          BEGIN
            PurchLine.VALIDATE("Shortcut Dimension 2 Code",ValueCode);
            PurchLine.MODIFY(TRUE);
          END;

        ELSE BEGIN
          DimMgt.GetDimensionSet(DimSetEntry,PurchLine."Dimension Set ID");
          IF ValueCode = '' THEN BEGIN
            IF DimSetEntry.GET(PurchLine."Dimension Set ID",DimCode) THEN
              DimSetEntry.DELETE(TRUE);
          END ELSE BEGIN
            IF NOT DimSetEntry.GET(PurchLine."Dimension Set ID",DimCode) THEN BEGIN
              DimSetEntry.INIT;
              DimSetEntry."Dimension Set ID" := PurchLine."Dimension Set ID";
              DimSetEntry.VALIDATE("Dimension Code",DimCode);
              DimSetEntry.VALIDATE("Dimension Value Code",ValueCode);
              DimSetEntry.INSERT(TRUE);
            END ELSE BEGIN
              DimSetEntry.VALIDATE("Dimension Value Code",ValueCode);
              DimSetEntry.MODIFY(TRUE);
            END;
          END;

          PurchLine."Dimension Set ID" := DimMgt.GetDimensionSetID(DimSetEntry);
          PurchLine.MODIFY(TRUE);
        END;
      END;
    END;

    PROCEDURE UpdatePurchHeaderDim@1160040002(VAR PurchHeader@1160040000 : Record 38;DimCode@1160040002 : Code[20];ValueCode@1160040003 : Code[20]);
    VAR
      DimSetEntry@1001 : TEMPORARY Record 480;
      DimMgt@1000 : Codeunit 408;
    BEGIN
      GetGLSetup;

      CASE DimCode OF
        GLSetup."Global Dimension 1 Code":
          BEGIN
            PurchHeader.VALIDATE("Shortcut Dimension 1 Code",ValueCode);
            PurchHeader.MODIFY(TRUE);
          END;

        GLSetup."Global Dimension 2 Code":
          BEGIN
            PurchHeader.VALIDATE("Shortcut Dimension 2 Code",ValueCode);
            PurchHeader.MODIFY(TRUE);
          END;

        ELSE BEGIN
          DimMgt.GetDimensionSet(DimSetEntry,PurchHeader."Dimension Set ID");
          IF ValueCode = '' THEN BEGIN
            IF DimSetEntry.GET(PurchHeader."Dimension Set ID",DimCode) THEN
              DimSetEntry.DELETE(TRUE);
          END ELSE BEGIN
            IF NOT DimSetEntry.GET(PurchHeader."Dimension Set ID",DimCode) THEN BEGIN
              DimSetEntry.INIT;
              DimSetEntry."Dimension Set ID" := PurchHeader."Dimension Set ID";
              DimSetEntry.VALIDATE("Dimension Code",DimCode);
              DimSetEntry.VALIDATE("Dimension Value Code",ValueCode);
              DimSetEntry.INSERT(TRUE);
            END ELSE BEGIN
              DimSetEntry.VALIDATE("Dimension Value Code",ValueCode);
              DimSetEntry.MODIFY(TRUE);
            END;
          END;

          PurchHeader."Dimension Set ID" := DimMgt.GetDimensionSetID(DimSetEntry);
          PurchHeader.MODIFY(TRUE);
        END;
      END;
    END;

    PROCEDURE GetGLSetup@1160040001();
    BEGIN
      IF GotGLSetup THEN
        EXIT;

      GLSetup.GET;
      GotGLSetup := TRUE;
    END;

    BEGIN
    END.
  }
}

