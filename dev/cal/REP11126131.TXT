OBJECT Report 11126131 GS1 Import Invoice IDs
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=GS1-Rechnung IDen importieren;
               ENU=GS1 Import Invoice IDs;
               NLD=GS1 factuur IDs importeren;
               NOR=Faktura-IDer for GS1-import;
               SVE=Faktura-ID:n fîr GS1-import];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   RequestOptionsPageSeqIdEditable := TRUE;
                   RequestOptionsPageRefDateEditable := FALSE;
                 END;

    OnPreReport=BEGIN
                  SalesInConstructSetup.GET;
                END;

    OnPostReport=BEGIN
                   ImportIDs;
                 END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525001;1;Group  ;
                  Name=Options;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100525002;2;Field  ;
                  CaptionML=[DEU=Abrufmethode;
                             ENU=Request Method;
                             NLD=Opvraag methode;
                             NOR=Metode for forespõrsel;
                             SVE=Metod fîr fîrfrÜgan];
                  OptionCaptionML=[DEU=Folge-ID,Referenzdatum;
                                   ENU=Sequence Id,Reference Date;
                                   NLD=Volg Id,Referentie Datum;
                                   NOR=Sekvens-ID,Referansedato;
                                   SVE=Sekvens-ID,Referensdatum];
                  SourceExpr=RequestType;
                  OnValidate=BEGIN
                               SetFieldsEditable;
                             END;
                              }

      { 1100525003;2;Field  ;
                  CaptionML=[DEU=Folge-ID;
                             ENU=Sequence Id;
                             NLD=Volg Id;
                             NOR=Sekvens-ID;
                             SVE=Sekvens-ID];
                  SourceExpr=SequenceId;
                  Editable=RequestOptionsPageSeqIdEditable }

      { 1100525004;2;Field  ;
                  CaptionML=[DEU=Stichtag;
                             ENU="Reference Date ";
                             NLD=Referentiedatum;
                             NOR=Referansedato;
                             SVE=Referensdatum];
                  SourceExpr=ReferenceDate;
                  Editable=RequestOptionsPageRefDateEditable }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      SalesInConstructSetup@1100528310 : Record 11072650;
      WebServiceURL@1100528301 : Record 11020659;
      CompanyInformation@1100528316 : Record 79;
      WebServiceMgt@1100528302 : Codeunit 11012266;
      Namespaces@1100528304 : ARRAY [10,2] OF Text[250];
      RequestRootTag@1100528309 : Text[250];
      RequestRootXpath@1100528303 : Text[250];
      ResponseRootTag@1100528314 : Text[250];
      ResponseRootXpath@1100528305 : Text[250];
      SequenceId@1100528306 : Code[20];
      ReferenceDate@1100528307 : Date;
      RequestType@1100528308 : 'Sequence Id,Reference Date';
      Text002@1100528300 : TextConst 'DEU=%1 nicht gefunden.;ENU=No %1 found.;NLD=Geen %1 gevonden.;NOR=Fant ikke %1.;SVE=%1 hittades inte.';
      Text003@1100528315 : TextConst 'DEU=%1 %2 importiert.;ENU=%1 %2 imported.;NLD=%1 %2 geãmporteerd.;NOR=%1 %2 er importert;SVE=%1 %2 har importerats.';
      Window@1100528317 : Dialog;
      Text004@1100528318 : TextConst 'DEU=Web Service ausfÅhren;ENU=Processing Web Service;NLD=Webservice uitvoeren;NOR=Behandler Webtjeneste;SVE=Behandlar webbtjÑnst';
      RequestOptionsPageSeqIdEditable@1100525000 : Boolean INDATASET;
      RequestOptionsPageRefDateEditable@1100525001 : Boolean INDATASET;

    LOCAL PROCEDURE ImportIDs@1100528302();
    BEGIN
      SalesInConstructSetup.TESTFIELD(Username);
      SalesInConstructSetup.TESTFIELD(Password);

      Window.OPEN(Text004);

      GetAndCheckWebServiceURL;
      SetGlobalVars;
      CreateBasicRequest;
      SetRequestParameters;
      SendRequest;
      ProcessResponse;

      Window.CLOSE;
    END;

    LOCAL PROCEDURE GetAndCheckWebServiceURL@1210190008();
    VAR
      PurchasesPayablesSetup@1100528301 : Record 312;
    BEGIN
      PurchasesPayablesSetup.GET;
      PurchasesPayablesSetup.TESTFIELD("Web Service GS1");

      IF (WebServiceURL.GET(WebServiceURL.Type::GS1, PurchasesPayablesSetup."Web Service GS1", '')) THEN;
      WebServiceURL.TESTFIELD(URL);
      WebServiceURL.TESTFIELD("Target Namespace");
    END;

    LOCAL PROCEDURE SetGlobalVars@1100528300();
    BEGIN
      CompanyInformation.GET;
      CompanyInformation.TESTFIELD(GLN);
      WHILE (STRLEN(CompanyInformation.GLN) < 13) DO
        CompanyInformation.GLN := '0' + CompanyInformation.GLN;

      Namespaces[1,1] := 'ZSol';
      Namespaces[1,2] := WebServiceURL."Target Namespace";

      RequestRootTag := Namespaces[1,1] + ':documentIdsByCustomerListRequest';
      RequestRootXpath := './/' + RequestRootTag;
      ResponseRootTag := Namespaces[1,1] + ':documentIdsByCustomerListResponse';
      ResponseRootXpath := './/' + ResponseRootTag;
    END;

    LOCAL PROCEDURE CreateBasicRequest@1210190006();
    BEGIN
      WebServiceMgt.PrepareSOAPRequest(Namespaces[1,2], RequestRootTag);
      WebServiceMgt.AddRequestSelectionNamespaces(Namespaces);
    END;

    LOCAL PROCEDURE SetRequestParameters@1210190004();
    VAR
      Xpath@1100528300 : Text[250];
      tag@1100528301 : Text[100];
    BEGIN
      IF (RequestType = RequestType::"Sequence Id") THEN
        WebServiceMgt.AppendParameter(RequestRootXpath, Namespaces[1,2], Namespaces[1,1] + ':sequenceId',
          SequenceId);
      IF (RequestType = RequestType::"Reference Date") THEN
        WebServiceMgt.AppendParameter(RequestRootXpath, Namespaces[1,2], Namespaces[1,1] + ':referenceDate',
          FORMAT(ReferenceDate, 0, 9));

      tag := Namespaces[1,1] + ':recipient';
      Xpath := RequestRootXpath + '/' + tag;
      WebServiceMgt.AppendParameter(RequestRootXpath, Namespaces[1,2], tag, '');
      WebServiceMgt.AppendParameter(Xpath, Namespaces[1,2], Namespaces[1,1] + ':gln',
        CompanyInformation.GLN);
    END;

    LOCAL PROCEDURE SendRequest@1100528301();
    BEGIN
      WebServiceMgt.SendSOAPRequest2('sendDocumentIdsRequest', WebServiceURL.URL,
        SalesInConstructSetup.Username, SalesInConstructSetup.Password, TRUE);
      WebServiceMgt.AddResponseSelectionNamespaces(Namespaces);
    END;

    LOCAL PROCEDURE ProcessResponse@1100528305();
    VAR
      GS1InvoiceID@1100528301 : Record 11126131;
      Counter@1100528300 : Integer;
    BEGIN
      IF (WebServiceMgt.GetResponseValueText('.//' + Namespaces[1,1] + ':document[1]/' +
        Namespaces[1,1] + ':sequenceId', FALSE) = '')
      THEN
        ERROR(Text002, GS1InvoiceID.TABLECAPTION);

      REPEAT
        Counter += 1;
        GS1InvoiceID."Sequence ID" :=
          WebServiceMgt.GetResponseValueText('.//' + Namespaces[1,1] + ':document[' + FORMAT(Counter) + ']/' +
           Namespaces[1,1] + ':sequenceId', TRUE);
        GS1InvoiceID."Document ID" :=
          WebServiceMgt.GetResponseValueText('.//' + Namespaces[1,1] + ':document[' + FORMAT(Counter) + ']/' +
          Namespaces[1,1] + ':documentId', TRUE);
        GS1InvoiceID."Presentation Type" :=
          WebServiceMgt.GetResponseValueText('.//' + Namespaces[1,1] + ':document[' + FORMAT(Counter) + ']/' +
          Namespaces[1,1] + ':presentationType', FALSE);
        GS1InvoiceID."Z-Invoice ID" :=
          WebServiceMgt.GetResponseValueText('.//' + Namespaces[1,1] + ':document[' + FORMAT(Counter) + ']/' +
          Namespaces[1,1] + ':sender/' + Namespaces[1,1] + ':ZfactuurId', FALSE);
        GS1InvoiceID."Import Date" := CURRENTDATETIME;
        IF (GS1InvoiceID.INSERT(TRUE)) THEN;
      UNTIL (WebServiceMgt.GetResponseValueText('.//' + Namespaces[1,1] + ':document[' + FORMAT(Counter+1) + ']/' +
             Namespaces[1,1] + ':sequenceId', FALSE) = '');

      MESSAGE(Text003, Counter, GS1InvoiceID.TABLECAPTION);
    END;

    PROCEDURE SetReferenceDate@1100528311(RefDate@1100528300 : Date);
    BEGIN
      ReferenceDate := RefDate;
    END;

    PROCEDURE SetSequenceId@1100528312(SeqId@1100528300 : Code[20]);
    BEGIN
      SequenceId := SeqId;
    END;

    LOCAL PROCEDURE SetFieldsEditable@1100528307();
    BEGIN
      RequestOptionsPageSeqIdEditable := (RequestType = RequestType::"Sequence Id");
      RequestOptionsPageRefDateEditable := (RequestType = RequestType::"Reference Date");
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

