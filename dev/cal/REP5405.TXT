OBJECT Report 5405 Calc. Consumption
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Verbrauch berechnen;
               ENU=Calc. Consumption;
               NLD=Verbruik berekenen;
               NOR=Beregn forbruk];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 4824;    ;DataItem;                    ;
               DataItemTable=Table5405;
               DataItemTableView=SORTING(Status,No.)
                                 WHERE(Status=CONST(Released));
               OnPreDataItem=BEGIN
                               ItemJnlLine.SETRANGE("Journal Template Name",ToTemplateName);
                               ItemJnlLine.SETRANGE("Journal Batch Name",ToBatchName);
                               IF ItemJnlLine.FINDLAST THEN
                                 NextConsumpJnlLineNo := ItemJnlLine."Line No." + 10000
                               ELSE
                                 NextConsumpJnlLineNo := 10000;

                               Window.OPEN(
                                 Text000 +
                                 Text001 +
                                 Text002 +
                                 Text003);
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(1,"No.");
                                END;

               ReqFilterFields=No. }

    { 7771;1   ;DataItem;                    ;
               DataItemTable=Table5407;
               OnPreDataItem=BEGIN
                               SETFILTER("Flushing Method",'<>%1&<>%2',"Flushing Method"::Backward,"Flushing Method"::"Pick + Backward");
                             END;

               OnAfterGetRecord=VAR
                                  NeededQty@1000 : Decimal;
                                BEGIN
                                  Window.UPDATE(2,"Item No.");

                                  CLEAR(ItemJnlLine);
                                  Item.GET("Item No.");
                                  ProdOrderLine.GET(Status,"Prod. Order No.","Prod. Order Line No.");

                                  NeededQty := GetNeededQty(CalcBasedOn,TRUE);

                                  IF NeededQty <> 0 THEN BEGIN
                                    IF LocationCode <> '' THEN
                                      CreateConsumpJnlLine(LocationCode,'',NeededQty)
                                    ELSE
                                      CreateConsumpJnlLine("Location Code","Bin Code",NeededQty);
                                    LastItemJnlLine := ItemJnlLine;
                                  END;
                                END;

               ReqFilterFields=Item No.;
               DataItemLink=Status=FIELD(Status),
                            Prod. Order No.=FIELD(No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   InitializeRequest(WORKDATE,CalcBasedOn::"Expected Output");
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1   ;2   ;Field     ;
                  CaptionML=[DEU=Buchungsdatum;
                             ENU=Posting Date;
                             NLD=Boekingsdatum;
                             NOR=Bokf›ringsdato;
                             SVE=Bokf”ringsdatum];
                  ToolTipML=[DEU=Gibt das Buchungsdatum an, das das Programm im Fenster "Verbrauchs Buch.-Blatt" verwenden soll.;
                             ENU=Specifies the posting date that you want the program to use in the Consumption Journal window.;
                             NLD=Hiermee wordt de boekingsdatum opgegeven die moet worden gebruikt in het venster Verbruiksdagboek.;
                             NOR=Angir bokf›ringsdatoen du vil at programmet skal bruke i vinduet Forbrukskladd.];
                  ApplicationArea=#Manufacturing;
                  SourceExpr=PostingDate }

      { 4   ;2   ;Field     ;
                  CaptionML=[DEU=Berechnung basiert auf;
                             ENU=Calculation Based on;
                             NLD=Berekening op basis van;
                             NOR=Beregning basert p†];
                  ToolTipML=[DEU=Gibt an, ob die Berechnung der Verbrauchsmenge auf der Ist-Menge oder der Soll-Menge (der Menge von Artikeln, die Sie produzieren wollen) basiert.;
                             ENU=Specifies whether the calculation of the quantity to consume is based on the actual output or on the expected output (the quantity of finished goods that you expect to produce).;
                             NLD=Hiermee wordt opgegeven of de berekening van het te verbruiken aantal is gebaseerd op de werkelijke uitvoer of op de verwachte uitvoer (het aantal eindproducten dat u verwacht te produceren).;
                             NOR=Angir om beregningen av antallet som skal brukes, er basert p† den faktiske avgangen eller den forventede avgangen (antall ferdigvarer du forventer † produsere).];
                  OptionCaptionML=[DEU=Ist-Menge,Soll-Menge;
                                   ENU=Actual Output,Expected Output;
                                   NLD=Werkelijke output,Verwachte output;
                                   NOR=Faktisk avgang,Forventet avgang];
                  ApplicationArea=#Manufacturing;
                  SourceExpr=CalcBasedOn }

      { 6   ;2   ;Field     ;
                  CaptionML=[DEU=Kommissionierung von Lagerort;
                             ENU=Picking Location;
                             NLD=Pickvestiging;
                             NOR=Plukklokasjon];
                  ToolTipML=[DEU=Gibt den Lagerort an, ab dem die Artikel vom Programm gebucht werden sollen.;
                             ENU=Specifies the location from where you want the program to post the items.;
                             NLD=Hiermee wordt de locatie opgegeven waarvandaan de artikelen moeten worden geboekt.;
                             NOR=Angir lokasjonen du vil at programmet skal bokf›re varene fra.];
                  ApplicationArea=#Location;
                  SourceExpr=LocationCode;
                  OnLookup=VAR
                             Location@1002 : Record 14;
                           BEGIN
                             IF PAGE.RUNMODAL(0,Location) = ACTION::LookupOK THEN BEGIN
                               Text := Location.Code;
                               EXIT(TRUE);
                             END;
                             EXIT(FALSE);
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Verbrauch berechnen...\\;ENU=Calculating consumption...\\;NLD=Berekenen verbruik...\\;NOR=Beregner forbruk...\\';
      Text001@1001 : TextConst 'DEU=FA-Nr.            #1##########\;ENU=Prod. Order No.   #1##########\;NLD=Prod.-ordernr.    #1##########\;NOR=Prod.ordrenr.     #1##########\';
      Text002@1002 : TextConst 'DEU=Artikelnr.        #2##########\;ENU=Item No.          #2##########\;NLD=Artikelnr.        #2##########\;NOR=Varenr.           #2##########\';
      Text003@1003 : TextConst 'DEU=Menge             #3##########;ENU=Quantity          #3##########;NLD=Aantal            #3##########;NOR=Antall            #3##########';
      Item@1004 : Record 27;
      ProdOrderLine@1005 : Record 5406;
      ItemJnlLine@1006 : Record 83;
      LastItemJnlLine@1017 : Record 83;
      Window@1009 : Dialog;
      PostingDate@1010 : Date;
      CalcBasedOn@1011 : 'Actual Output,Expected Output';
      LocationCode@1013 : Code[10];
      ToTemplateName@1014 : Code[10];
      ToBatchName@1015 : Code[10];
      NextConsumpJnlLineNo@1016 : Integer;

    [External]
    PROCEDURE InitializeRequest@2(NewPostingDate@1001 : Date;NewCalcBasedOn@1000 : Option);
    BEGIN
      PostingDate := NewPostingDate;
      CalcBasedOn := NewCalcBasedOn;
    END;

    LOCAL PROCEDURE CreateConsumpJnlLine@4(LocationCode@1000 : Code[10];BinCode@1001 : Code[20];QtyToPost@1002 : Decimal);
    VAR
      Location@1004 : Record 14;
    BEGIN
      OnBeforeCreateConsumpJnlLine(LocationCode,BinCode,QtyToPost);

      Window.UPDATE(3,QtyToPost);

      IF Location.GET(LocationCode) AND Location."Require Pick" AND Location."Require Shipment" THEN
        "Prod. Order Component".AdjustQtyToQtyPicked(QtyToPost);

      IF (ItemJnlLine."Item No." = "Prod. Order Component"."Item No.") AND
         (LocationCode = ItemJnlLine."Location Code") AND
         (BinCode = ItemJnlLine."Bin Code")
      THEN BEGIN
        IF Item."Rounding Precision" > 0 THEN
          ItemJnlLine.VALIDATE(Quantity,ItemJnlLine.Quantity + ROUND(QtyToPost,Item."Rounding Precision",'>'))
        ELSE
          ItemJnlLine.VALIDATE(Quantity,ItemJnlLine.Quantity + ROUND(QtyToPost,0.00001));
        ItemJnlLine.MODIFY;
      END ELSE BEGIN
        ItemJnlLine.INIT;
        ItemJnlLine."Journal Template Name" := ToTemplateName;
        ItemJnlLine."Journal Batch Name" := ToBatchName;
        ItemJnlLine.SetUpNewLine(LastItemJnlLine);
        ItemJnlLine."Line No." := NextConsumpJnlLineNo;

        ItemJnlLine.VALIDATE("Entry Type",ItemJnlLine."Entry Type"::Consumption);
        ItemJnlLine.VALIDATE("Order Type",ItemJnlLine."Order Type"::Production);
        ItemJnlLine.VALIDATE("Order No.","Prod. Order Component"."Prod. Order No.");
        ItemJnlLine.VALIDATE("Source No.",ProdOrderLine."Item No.");
        ItemJnlLine.VALIDATE("Posting Date",PostingDate);
        ItemJnlLine.VALIDATE("Item No.","Prod. Order Component"."Item No.");
        ItemJnlLine.VALIDATE("Unit of Measure Code","Prod. Order Component"."Unit of Measure Code");
        ItemJnlLine.Description := "Prod. Order Component".Description;
        IF Item."Rounding Precision" > 0 THEN
          ItemJnlLine.VALIDATE(Quantity,ROUND(QtyToPost,Item."Rounding Precision",'>'))
        ELSE
          ItemJnlLine.VALIDATE(Quantity,ROUND(QtyToPost,0.00001));
        ItemJnlLine."Variant Code" := "Prod. Order Component"."Variant Code";
        ItemJnlLine.VALIDATE("Location Code",LocationCode);
        IF BinCode <> '' THEN
          ItemJnlLine."Bin Code" := BinCode;
        ItemJnlLine.VALIDATE("Order Line No.","Prod. Order Component"."Prod. Order Line No.");
        ItemJnlLine.VALIDATE("Prod. Order Comp. Line No.","Prod. Order Component"."Line No.");

        OnBeforeInsertItemJnlLine(ItemJnlLine,"Prod. Order Component");
        ItemJnlLine.INSERT;
        OnAfterInsertItemJnlLine(ItemJnlLine);

        IF Item."Item Tracking Code" <> '' THEN
          AssignItemTracking("Prod. Order Component",ItemJnlLine);
      END;

      NextConsumpJnlLineNo := NextConsumpJnlLineNo + 10000;

      OnAfterCreateConsumpJnlLine(LocationCode,BinCode,QtyToPost,ItemJnlLine);
    END;

    [External]
    PROCEDURE SetTemplateAndBatchName@1(TemplateName@1000 : Code[10];BatchName@1001 : Code[10]);
    BEGIN
      ToTemplateName := TemplateName;
      ToBatchName := BatchName;
    END;

    LOCAL PROCEDURE AssignItemTracking@18(ProdOrderComponent@1007 : Record 5407;ItemJournalLine@1000 : Record 83);
    VAR
      ItemLedgerEntry@1005 : Record 32;
      SourceTrackingSpecification@1004 : Record 336;
      TempTrackingSpecification@1003 : TEMPORARY Record 336;
      TempReservEntry@1002 : TEMPORARY Record 337;
      ItemTrackingMgt@1006 : Codeunit 6500;
      ItemTrackingLines@1001 : Page 6510;
    BEGIN
      IF ItemJournalLine.Quantity > 0 THEN
        ItemTrackingMgt.CopyItemTracking(ProdOrderComponent.RowID1,ItemJournalLine.RowID1,FALSE)
      ELSE BEGIN
        ItemLedgerEntry.SETRANGE("Entry Type",ItemLedgerEntry."Entry Type"::Consumption);
        ItemLedgerEntry.SETRANGE("Order Type",ItemLedgerEntry."Order Type"::Production);
        ItemLedgerEntry.SETRANGE("Order No.",ItemJournalLine."Order No.");
        ItemLedgerEntry.SETRANGE("Order Line No.",ItemJournalLine."Order Line No.");
        ItemLedgerEntry.SETRANGE("Prod. Order Comp. Line No.",ItemJournalLine."Prod. Order Comp. Line No.");
        ItemLedgerEntry.SETRANGE("Item No.",ItemJournalLine."Item No.");
        IF ItemLedgerEntry.ISEMPTY THEN
          EXIT;

        ItemLedgerEntry.FINDSET;
        REPEAT
          TempReservEntry.SetTrackingFilter(ItemLedgerEntry."Serial No.",ItemLedgerEntry."Lot No.");
          IF TempReservEntry.FINDFIRST THEN BEGIN
            TempReservEntry."Quantity (Base)" += ItemLedgerEntry.Quantity;
            TempReservEntry.MODIFY;
          END ELSE BEGIN
            TempReservEntry."Entry No." := ItemLedgerEntry."Entry No.";
            TempReservEntry.CopyTrackingFromItemLedgEntry(ItemLedgerEntry);
            TempReservEntry."Quantity (Base)" := ItemLedgerEntry.Quantity;
            TempReservEntry.INSERT;
          END;
        UNTIL ItemLedgerEntry.NEXT = 0;

        TempReservEntry.RESET;
        ItemTrackingMgt.SumUpItemTracking(TempReservEntry,TempTrackingSpecification,FALSE,TRUE);

        SourceTrackingSpecification.InitFromItemJnlLine(ItemJournalLine);
        ItemTrackingLines.RegisterItemTrackingLines(
          SourceTrackingSpecification,ItemJournalLine."Posting Date",TempTrackingSpecification);
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInsertItemJnlLine@1001(VAR ItemJournalLine@1000 : Record 83);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCreateConsumpJnlLine@3(LocationCode@1002 : Code[10];BinCode@1001 : Code[20];QtyToPost@1000 : Decimal);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeInsertItemJnlLine@6(VAR ItemJournalLine@1000 : Record 83;ProdOrderComponent@1001 : Record 5407);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCreateConsumpJnlLine@5(LocationCode@1002 : Code[10];BinCode@1001 : Code[20];QtyToPost@1000 : Decimal;VAR ItemJournalLine@1003 : Record 83);
    BEGIN
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

