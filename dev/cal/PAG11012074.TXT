OBJECT Page 11012074 Project Ledger (Compressed)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00,4PSSE;
  }
  PROPERTIES
  {
    Permissions=TableData 11072005=m;
    CaptionML=[ENU=Project Ledger (Compressed);
               NOR=Prosjektreskontro (komprimert);
               SVE=Projektreskontra (komprimerat)];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table11072005;
    DataCaptionFields=Job No.;
    PageType=List;
    OnInit=VAR
             ExFlowSetup@1101285000 : Record 12013601;
           BEGIN
             PurchaseActionVisible := TRUE;

             //>>180207 ITERO.SB REK056 Copied Exflow actions from page Job ledger entries
             ShowExFlowBtn := ExFlowSetup.GET;
             //<<
           END;

    OnOpenPage=BEGIN
                 //**4PS Generated Code. Do Not Change.sn01
                 DPA_IsHardFilter := DPA_SetFilters(CurrPage.OBJECTID(FALSE));
                 //**4PS Generated Code.en01
                 Compress(TRUE);
               END;

    OnNextRecord=VAR
                   Step@1100525003 : Integer;
                   CurrentSteps@1100525004 : Integer;
                   NumberOfSteps@1100525005 : Integer;
                   eof@1100525001 : Boolean;
                 BEGIN
                   IF NOT CompressedBln THEN
                     EXIT(NEXT(Steps));

                   ProjectLedgEntry.COPY(Rec);

                   IF Steps < 0 THEN
                     Step := -1
                   ELSE
                     Step := 1;

                   CurrentSteps := 0;
                   IF Steps <> 0 THEN
                     eof := ProjectLedgEntry.NEXT(Step) = 0;
                   IF NOT eof THEN
                     FOR NumberOfSteps := 1 TO ABS(Steps) DO BEGIN
                       WHILE SkipRec AND NOT eof DO
                         eof := ProjectLedgEntry.NEXT(Step) = 0;
                       IF NOT eof THEN BEGIN
                         Rec := ProjectLedgEntry;
                         CurrentSteps := CurrentSteps + Step;
                       END;
                     END;

                   EXIT(CurrentSteps);
                 END;

    OnAfterGetRecord=BEGIN
                       IF CompressedBln THEN BEGIN
                         IF CURRENTKEY <> AllowedKeyCompressed THEN BEGIN
                           MESSAGE(Text001, AllowedKeyCompressed);
                           SETCURRENTKEY("Main Project No.","Job No.", "Document No.", "Posting Date");
                         END;
                       END;

                       CountAmounts;
                     END;

    OnNewRecord=BEGIN
                  //**4PS Generated Code. Do Not Change.sn02
                  DPA_SetDefaultDepartment;
                  //**4PS Generated Code.en02
                END;

    OnInsertRecord=VAR
                     xRecRef@1100000999 : RecordRef;
                   BEGIN
                     //**4PS Generated Code. Do Not Change.sn05
                     xRecRef.GETTABLE(xRec);
                     DPA_SetDefaultDpmtFromNoSeries(xRecRef);
                     //**4PS Generated Code.en05
                   END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 24      ;1   ;Action    ;
                      CaptionML=[ENU=Compress;
                                 NOR=Komprimer;
                                 SVE=Komprimera];
                      Promoted=Yes;
                      Enabled=CompresBtnEnabled;
                      Image=Compress;
                      OnAction=BEGIN
                                 Compress(TRUE);
                               END;
                                }
      { 25      ;1   ;Action    ;
                      CaptionML=ENU=Undo Compress;
                      Promoted=Yes;
                      Enabled=UndoCompresBtnEnabled;
                      Image=EditFilter;
                      OnAction=BEGIN
                                 Compress(FALSE);
                               END;
                                }
      { 1100409000;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 23      ;1   ;Action    ;
                      CaptionML=[ENU=&Specification;
                                 NOR="&Spesifikasjon ";
                                 SVE=&Specifikation (vecka/person)];
                      Promoted=Yes;
                      Image=Track;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 ProjectLedgEntry.RESET;
                                 ProjectLedgEntry.COPY(Rec);
                                 ProjectLedgEntry.SETCURRENTKEY("Document No.", "Posting Date");
                                 ProjectLedgEntry.SETRANGE("Document No.", "Document No.");
                                 ProjectLedgEntry.SETRANGE(Compressed);
                                 PAGE.RUNMODAL(PAGE::"Job Ledger Entries", ProjectLedgEntry);
                               END;
                                }
      { 1100485000;1 ;Action    ;
                      CaptionML=[ENU=S&canned Document;
                                 NOR=S&kannet dokument;
                                 SVE=S&kannade dokument];
                      Promoted=Yes;
                      Image=Documents;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 lvPurchSetupRec@1100485000 : Record 312;
                               BEGIN
                                 lvPurchSetupRec.GET;
                                 lvPurchSetupRec.ShowScannedDocumentWithDocNoInSupplComp("Document No.", "Supplying Company");
                               END;
                                }
      { 1100285101;1 ;Action    ;
                      CaptionML=[ENU=Show &Documents;
                                 NOR=Vis &Dokumenter;
                                 SVE=Visa &dokument];
                      Promoted=Yes;
                      Image=Documents;
                      PromotedCategory=Report;
                      OnAction=VAR
                                 RecRef@1210190001 : RecordRef;
                                 DocumentFilters@1210190000 : Codeunit 11012432;
                                 SourceCodeSetup@1000000001 : Record 242;
                                 PurchInvHeader@1000000000 : Record 122;
                                 PurchCredHeader@1100285300 : Record 124;
                                 DocumentFilter@1000000003 : Record 11012073;
                                 GlobalTempDocumentProperties@1000000002 : TEMPORARY Record 11012746;
                                 SalesInvHeader@1101285001 : Record 112;
                                 SalesCredHeader@1101285000 : Record 114;
                               BEGIN
                                 //>> 180221 ITERO.SB REK-018 Show sales invoice document for source code "Sales"
                                 //>> 160922 ITERO.SB RFC-030 Show purchase invoice on compressed project ledger
                                 //RFC136
                                 //160324
                                 SourceCodeSetup.GET;
                                 CASE "Source Code" OF
                                   SourceCodeSetup.Purchases,'':
                                     BEGIN
                                       IF PurchInvHeader.GET("Document No.") THEN
                                         RecRef.GETTABLE(PurchInvHeader)
                                       ELSE
                                         IF PurchCredHeader.GET("Document No.") THEN
                                           RecRef.GETTABLE(PurchCredHeader)
                                         ELSE
                                         RecRef.GETTABLE(Rec);
                                 //    END;
                                 //  ELSE
                                 //    RecRef.GETTABLE(Rec);
                                 //END;

                                       DocumentFilters.CreateTemporaryFilter(RecRef, DocumentFilter);
                                       COMMIT;

                                       DocumentFilters.ApplyDocumentFilter(DocumentFilter, GlobalTempDocumentProperties);
                                       GlobalTempDocumentProperties.SETRANGE("From Filter Form", FALSE);
                                       CASE GlobalTempDocumentProperties.COUNT OF
                                         0:
                                           CASE RecRef.NUMBER OF
                                            122: BEGIN PurchInvHeader.SETRECFILTER; PurchInvHeader.PrintRecords(TRUE); END;
                                            124: BEGIN PurchCredHeader.SETRECFILTER; PurchCredHeader.PrintRecords(TRUE,FALSE); END;
                                           END;
                                         1:  GlobalTempDocumentProperties.OpenDocument;
                                         ELSE
                                         PAGE.RUNMODAL(0, GlobalTempDocumentProperties);
                                       END;

                                     END;

                                   SourceCodeSetup.Sales,'':
                                     BEGIN
                                       IF SalesInvHeader.GET("Document No.") THEN BEGIN
                                         SalesInvHeader.SETRECFILTER;
                                        SalesInvHeader.PrintRecords(TRUE,TRUE)
                                       END ELSE IF SalesCredHeader.GET("Document No.") THEN BEGIN
                                         SalesCredHeader.SETRECFILTER;
                                         SalesCredHeader.PrintRecords(TRUE);
                                       END;
                                     END;
                                 END;
                                 //<<
                               END;
                                }
      { 1101285001;1 ;Action    ;
                      ShortCutKey=Ctrl+I;
                      CaptionML=[ENU=&Show Document;
                                 NOR=V&is dokument;
                                 SVE=&Visa dokument];
                      Visible=ShowExFlowBtn;
                      Image=Document;
                      OnAction=BEGIN
                                 //>>180207 ITERO.SB REK056 Copied Exflow actions from page Job ledger entries
                                 // ExFlow
                                 IF PurchInvoiceRec.GET("Document No.") THEN
                                   ExFlow.ShowImagePostedDoc(2,"Document No.")
                                 ELSE IF PurchMemoRec.GET("Document No.") THEN
                                   ExFlow.ShowImagePostedDoc(3,"Document No.")
                                 ELSE
                                   MESSAGE(EXF001);
                                 // ExFlow
                                 //<<
                               END;
                                }
      { 1101285000;1 ;Action    ;
                      ShortCutKey=Ctrl+B;
                      CaptionML=[ENU=Attached Fil&es;
                                 NOR=Vedlegg;
                                 SVE=&Bilagor];
                      Visible=ShowExFlowBtn;
                      Image=Documents;
                      OnAction=BEGIN
                                 //>>180207 ITERO.SB REK056 Copied Exflow actions from page Job ledger entries
                                 // ExFlow
                                 IF PurchInvoiceRec.GET("Document No.") THEN
                                   ExFlow.ShowAttachmentPostedDoc(2, "Document No.")
                                 ELSE IF PurchMemoRec.GET("Document No.") THEN
                                   ExFlow.ShowAttachmentPostedDoc(3, "Document No.")
                                 ELSE
                                   MESSAGE(EXF001);
                                 // ExFlow
                                 //<<
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 1100525000;2;Field  ;
                SourceExpr="Document No.";
                Editable=FALSE }

    { 4   ;2   ;Field     ;
                SourceExpr="Posting Date";
                Editable=FALSE }

    { 6   ;2   ;Field     ;
                SourceExpr="Job No.";
                Editable=FALSE }

    { 8   ;2   ;Field     ;
                CaptionML=[ENU=Quantity;
                           NOR=Antall;
                           SVE=Antal];
                SourceExpr=TotalQuantity;
                Editable=FALSE }

    { 10  ;2   ;Field     ;
                CaptionML=[ENU=Total Cost;
                           NOR=Total kost;
                           SVE=Totalkostnad];
                SourceExpr=TotalCost;
                Editable=FALSE }

    { 14  ;2   ;Field     ;
                CaptionML=[ENU=Total Price;
                           NOR=Totalpris;
                           SVE=Totalpris];
                SourceExpr=TotalPrice;
                Editable=FALSE }

    { 1210190002;2;Field  ;
                CaptionML=[ENU=Actual Preclosure Result;
                           NOR=Reelle pr›veregnskapsresultater;
                           SVE=Periodiserat resultat];
                SourceExpr=TotalActualPreclosureResult;
                AutoFormatType=1;
                Editable=FALSE }

    { 1100525002;2;Field  ;
                CaptionML=[ENU=Actual Provision Result;
                           NOR=Reelt avsetningsresultat;
                           SVE=Verkligt avs„ttningsresultat];
                SourceExpr=TotalActualProvisionResult;
                AutoFormatType=1 }

    { 12  ;2   ;Field     ;
                CaptionML=[ENU=Closed Costs;
                           NOR=Opparbeidede koster;
                           SVE=Upparbetade kostnader];
                SourceExpr=TotalClosedCosts;
                Editable=FALSE }

    { 16  ;2   ;Field     ;
                CaptionML=[ENU=Closed Revenues;
                           NOR=Opparbeidede inntekter;
                           SVE=Upparbetade int„kter];
                SourceExpr=TotalClosedRevenues;
                Editable=FALSE }

    { 18  ;2   ;Field     ;
                CaptionML=[ENU=Remaining Amount;
                           NOR=Restbel›p;
                           SVE=terst†ende belopp];
                SourceExpr=TotalRemainingAmount;
                Editable=FALSE }

    { 1100485006;2;Field  ;
                SourceExpr="Vendor No." }

    { 1100485002;2;Field  ;
                SourceExpr="Vendor Name" }

    { 1100485008;2;Field  ;
                SourceExpr=Principal }

    { 1100485004;2;Field  ;
                SourceExpr="Principal Name" }

    { 1100525004;2;Field  ;
                SourceExpr="Purchase Action";
                Visible=PurchaseActionVisible }

    { 1210190000;2;Field  ;
                SourceExpr="Source Code" }

    { 1000000002;2;Field  ;
                CaptionML=ENU=Approver comment;
                SourceExpr=HasComment;
                Visible=ShowExFlowBtn }

    { 1000000006;0;Container;
                ContainerType=FactBoxArea }

    { 1000000003;1;Part   ;
                SubPageLink=Document No.=FIELD(Document No.);
                PagePartID=Page12057119;
                Visible=ShowExFlowBtn;
                PartType=Page }

  }
  CODE
  {
    VAR
      ProjectLedgEntry@11012000 : Record 11072005;
      CompressedBln@11012001 : Boolean;
      TotalCost@1210190000 : Decimal;
      TotalPrice@1210190001 : Decimal;
      TotalClosedCosts@1210190002 : Decimal;
      TotalClosedRevenues@1210190003 : Decimal;
      TotalRemainingAmount@1210190004 : Decimal;
      TotalQuantity@1210190005 : Decimal;
      TotalActualPreclosureResult@1210190006 : Decimal;
      TotalActualProvisionResult@1100525000 : Decimal;
      TotalInvoicedAmt@1100485000 : Decimal;
      TotalInvoicedQuan@1100485001 : Decimal;
      DPA_IsHardFilter@1100000999 : Boolean;
      AllowedKeyCompressed@1100525001 : Text[1024];
      Text001@1100525002 : TextConst 'ENU=For ''Compressed'' only sort allowed on ''%1''.;NOR=For ''Komprimert'' tillates kun sortering p† ''%1''.;SVE=F”r Komprimerad till†ts endast sortering p† ''%1''.';
      PurchaseActionVisible@19039042 : Boolean INDATASET;
      CompresBtnEnabled@1100409000 : Boolean INDATASET;
      UndoCompresBtnEnabled@1100409001 : Boolean INDATASET;
      "****ExFlow****"@1101285006 : Integer;
      ExFlow@1101285005 : Codeunit 12013601;
      PurchInvoiceRec@1101285004 : Record 122;
      PurchMemoRec@1101285003 : Record 124;
      ShowExFlowBtn@1101285002 : Boolean INDATASET;
      SelectedLineEnable@1101285001 : Boolean INDATASET;
      gvTmpJobEntry@1101285000 : TEMPORARY Record 11072005;
      EXF001@1101285007 : TextConst 'ENU=Posted ExFlow Document Entry does not exist.';

    PROCEDURE Compress@1(CompressMode@1100409000 : Boolean);
    BEGIN
      IF CompressMode THEN BEGIN
        SETCURRENTKEY("Main Project No.","Job No.", "Document No.", "Posting Date");
        AllowedKeyCompressed := CURRENTKEY;
      END;
      CompressedBln := CompressMode;
      PurchaseActionVisible := (NOT CompressedBln); //call C-019412
      CompresBtnEnabled := (NOT CompressedBln);
      UndoCompresBtnEnabled := CompressedBln;
    END;

    PROCEDURE SkipRec@1100525002() : Boolean;
    BEGIN
      IF (ProjectLedgEntry."Main Project No." <> Rec."Main Project No.") OR
         (ProjectLedgEntry."Job No." <>  Rec."Job No.") OR
         (ProjectLedgEntry."Document No." <> Rec."Document No.")
      THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    PROCEDURE CountAmounts@2();
    VAR
      VendorTemp@1100525000 : TEMPORARY Record 23;
      NoVendorName@1101285000 : Boolean;
    BEGIN
      TotalCost := "Total Cost (LCY)";
      TotalPrice := "Total Price (LCY)";
      TotalClosedCosts := "Closed Costs";
      TotalClosedRevenues := "Closed Revenues";
      TotalQuantity := Quantity;
      TotalActualPreclosureResult := "Actual Preclosure Result";
      TotalActualProvisionResult := "Actual Provision Result";
      CALCFIELDS("Amount Invoiced (LCY)", "Quantity Invoiced");
      TotalInvoicedQuan := "Quantity Invoiced";
      TotalInvoicedAmt := "Amount Invoiced (LCY)";

      IF NOT CompressedBln THEN
        EXIT;

      //>>170705 ITERO.FH PURE#270163
      //C008887
      // VendorTemp."No." := "Vendor No.";
      // VendorTemp.INSERT;
      //
      //<<170705 ITERO.FH PURE#270163

      ProjectLedgEntry.RESET;
      ProjectLedgEntry.COPY(Rec);
      ProjectLedgEntry.SETCURRENTKEY("Main Project No.","Job No.", "Document No.", "Posting Date");
      ProjectLedgEntry.SETRANGE("Main Project No.", "Main Project No.");
      ProjectLedgEntry.SETRANGE("Job No.", "Job No.");
      ProjectLedgEntry.SETRANGE("Document No.", "Document No.");
      IF ProjectLedgEntry.FINDSET THEN BEGIN
        REPEAT
          IF ProjectLedgEntry."Entry No." <> "Entry No." THEN BEGIN
            TotalCost := TotalCost + ProjectLedgEntry."Total Cost (LCY)";
            TotalPrice := TotalPrice + ProjectLedgEntry."Total Price (LCY)";
            TotalClosedCosts :=  TotalClosedCosts + ProjectLedgEntry. "Closed Costs";
            TotalClosedRevenues := TotalClosedRevenues + ProjectLedgEntry."Closed Revenues";
            TotalQuantity := TotalQuantity + ProjectLedgEntry.Quantity;
            TotalActualPreclosureResult := TotalActualPreclosureResult + ProjectLedgEntry."Actual Preclosure Result";
            TotalActualProvisionResult := TotalActualProvisionResult + ProjectLedgEntry."Actual Provision Result";
            ProjectLedgEntry.CALCFIELDS("Amount Invoiced (LCY)", "Quantity Invoiced");
            TotalInvoicedQuan := TotalInvoicedQuan + ProjectLedgEntry."Quantity Invoiced";
            TotalInvoicedAmt := TotalInvoicedAmt + ProjectLedgEntry."Amount Invoiced (LCY)";
            //>> 191203
            IF "Vendor No." <> ProjectLedgEntry."Vendor No." THEN
              NoVendorName := TRUE;
            //<<191203

      //>>170705 ITERO.FH PURE#270163
            //C008887
      //      IF ProjectLedgEntry."Vendor No." <> "Vendor No." THEN BEGIN
      //        VendorTemp."No." := ProjectLedgEntry."Vendor No.";
      //        IF VendorTemp.INSERT THEN;
      //      END;
            //
      //<<170705 ITERO.FH PURE#270163

          END;
        UNTIL ProjectLedgEntry.NEXT = 0;
      END;

      //>> 191203
      IF NoVendorName THEN BEGIN
        "Vendor No." := '';
        "Vendor Name" := '';
      END;
      //<< 191203

      "Purchase Action" := '';

      //>>170705 ITERO.FH PURE#270163
      // //C008887
      // IF VendorTemp.COUNT > 1 THEN BEGIN
      //  "Vendor No." := '[' + FORMAT(VendorTemp.COUNT) + ']';
      //  "Vendor Name" := '';
      // END;
      // //
      //<<170705 ITERO.FH PURE#270163
    END;

    LOCAL PROCEDURE HasComment@1000000002() : Boolean;
    VAR
      ExFlow4PS@1000000000 : Codeunit 12057160;
      EXPostedDocument@1000000001 : Record 12013626;
    BEGIN
      //>> 180220 ITERO.SB REK-014 Exflow, show approver comments
      IF ShowExFlowBtn THEN BEGIN
        EXPostedDocument.SETRANGE("Document No.","Document No.");
        IF EXPostedDocument.FINDFIRST THEN BEGIN
          EXPostedDocument.CALCFIELDS("Approver Comments");
          IF EXPostedDocument."Approver Comments".HASVALUE THEN
            EXIT(TRUE);
        END;
      END;
      //<<
    END;

    BEGIN
    {
      160922 ITERO.SB RFC-030 Show purchase invoice on compressed project ledger
      160909 ITERO.DL RFC136 view attached documents (from Symbrio) if no doc then print report
      170705 ITERO.FH PURE#270163 Removed interfering code from call C008887
      180207 ITERO.SB REK056 Copied Exflow actions from page Job ledger entries
      180220 ITERO.SB REK014 Exflow, show approver comments (added field HasComment + Posted Doc Comment Factbox)
      180221 ITERO.SB REK-018 Show sales invoice document for source code "Sales"
      191203 ORANGO.FH #26502 REKAB If Project Ledger Entrys have different "Vendor No." when compressed then "Vendor No." and "Vendor Name" is blank
    }
    END.
  }
}

