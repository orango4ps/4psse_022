OBJECT Table 1208 Direct Debit Collection Entry
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.01;
  }
  PROPERTIES
  {
    DataCaptionFields=Customer No.,Transaction ID;
    OnInsert=VAR
               DirectDebitCollectionEntry@1000 : Record 1208;
             BEGIN
               IF "Entry No." = 0 THEN BEGIN
                 DirectDebitCollectionEntry.SETRANGE("Direct Debit Collection No.","Direct Debit Collection No.");
                 LOCKTABLE;
                 IF DirectDebitCollectionEntry.FINDLAST THEN;
                 "Entry No." := DirectDebitCollectionEntry."Entry No." + 1;
               END;
             END;

    OnModify=BEGIN
               TESTFIELD(Status,Status::New);
               CALCFIELDS("Direct Debit Collection Status");
               TESTFIELD("Direct Debit Collection Status","Direct Debit Collection Status"::New);
             END;

    OnDelete=BEGIN
               DeletePaymentFileErrors;
             END;

    CaptionML=[ENU=Direct Debit Collection Entry;
               NOR=Post for Direct Debit-oppkreving;
               SVE=Autogirosamlingstransaktion];
    LookupPageID=Page1208;
    DrillDownPageID=Page1208;
  }
  FIELDS
  {
    { 1   ;   ;Direct Debit Collection No.;Integer;TableRelation="Direct Debit Collection";
                                                   CaptionML=[ENU=Direct Debit Collection No.;
                                                              NOR=Direct Debit-oppkrevingsnr.;
                                                              SVE=Autogirosamlingsnr.] }
    { 2   ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              NOR=Lõpenr.;
                                                              SVE=Lîpnr] }
    { 3   ;   ;Customer No.        ;Code20        ;TableRelation=Customer;
                                                   OnValidate=BEGIN
                                                                IF xRec."Customer No." <> '' THEN
                                                                  TESTFIELD(Status,Status::New);
                                                              END;

                                                   CaptionML=[ENU=Customer No.;
                                                              NOR=Kundenr.;
                                                              SVE=Kundnr] }
    { 4   ;   ;Applies-to Entry No.;Integer       ;TableRelation="Cust. Ledger Entry" WHERE (Customer No.=FIELD(Customer No.),
                                                                                             Document Type=FILTER(Invoice|Finance Charge Memo|Reminder),
                                                                                             Open=CONST(Yes));
                                                   OnValidate=VAR
                                                                CustLedgerEntry@1000 : Record 21;
                                                                DirectDebitCollection@1001 : Record 1207;
                                                                AllowedDocumentType@1002 : Boolean;
                                                              BEGIN
                                                                IF xRec."Applies-to Entry No." <> "Applies-to Entry No." THEN BEGIN
                                                                  TESTFIELD("Customer No.");
                                                                  CustLedgerEntry.GET("Applies-to Entry No.");
                                                                  CustLedgerEntry.TESTFIELD("Customer No.","Customer No.");
                                                                  CustLedgerEntry.TESTFIELD(Open);

                                                                  AllowedDocumentType :=
                                                                    (CustLedgerEntry."Document Type" IN
                                                                     [CustLedgerEntry."Document Type"::Invoice,
                                                                      CustLedgerEntry."Document Type"::"Finance Charge Memo",
                                                                      CustLedgerEntry."Document Type"::Reminder]);
                                                                  OnBeforeDocTypeErr(CustLedgerEntry,AllowedDocumentType);
                                                                  IF NOT AllowedDocumentType THEN
                                                                    ERROR(DocTypeErr);

                                                                  CustLedgerEntry.CALCFIELDS("Remaining Amount");
                                                                  IF CustLedgerEntry."Remaining Amount" <= 0 THEN
                                                                    ERROR(AmountMustBePositiveErr);

                                                                  "Transfer Date" := CustLedgerEntry."Due Date";
                                                                  "Currency Code" := CustLedgerEntry."Currency Code";
                                                                  "Transfer Amount" := CustLedgerEntry."Remaining Amount" - GetAmountInActiveCollections;
                                                                  VALIDATE("Mandate ID",CustLedgerEntry."Direct Debit Mandate ID");

                                                                  DirectDebitCollection.GET("Direct Debit Collection No.");
                                                                  "Transaction ID" := STRSUBSTNO('%1/%2',DirectDebitCollection.Identifier,"Entry No.");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Applies-to Entry No.;
                                                              NOR=Utligningspostnr.;
                                                              SVE=Kopplas till lîpnr] }
    { 5   ;   ;Transfer Date       ;Date          ;CaptionML=[ENU=Transfer Date;
                                                              NOR=Overfõringsdato;
                                                              SVE=ôverfîringsdatum] }
    { 6   ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=[ENU=Currency Code;
                                                              NOR=Valutakode;
                                                              SVE=Valutakod];
                                                   Editable=No }
    { 7   ;   ;Transfer Amount     ;Decimal       ;OnValidate=VAR
                                                                CustLedgEntry@1000 : Record 21;
                                                                IsHandled@1001 : Boolean;
                                                              BEGIN
                                                                IsHandled := FALSE;
                                                                OnBeforeValidateTransferAmount(Rec,IsHandled);
                                                                IF IsHandled THEN
                                                                  EXIT;

                                                                IF "Transfer Amount" <= 0 THEN
                                                                  ERROR(AmountMustBePositiveErr);
                                                                TESTFIELD("Applies-to Entry No.");
                                                                CustLedgEntry.GET("Applies-to Entry No.");
                                                                CustLedgEntry.CALCFIELDS("Remaining Amount");
                                                                IF "Transfer Amount" > CustLedgEntry."Remaining Amount" - GetAmountInActiveCollections THEN
                                                                  ERROR(LargerThanRemainingErr,CustLedgEntry."Remaining Amount",CustLedgEntry."Currency Code");
                                                              END;

                                                   CaptionML=[ENU=Transfer Amount;
                                                              NOR=Overfõringsbelõp;
                                                              SVE=ôverfîringsbelopp] }
    { 8   ;   ;Transaction ID      ;Text35        ;CaptionML=[ENU=Transaction ID;
                                                              NOR=Transaksjons-ID;
                                                              SVE=Transaktions-ID];
                                                   Editable=No }
    { 9   ;   ;Sequence Type       ;Option        ;CaptionML=[ENU=Sequence Type;
                                                              NOR=Type betaling;
                                                              SVE=Typ av betalning];
                                                   OptionCaptionML=[ENU=One Off,First,Recurring,Last;
                                                                    NOR=Engangsbetaling,Fõrste,Gjentatte betalinger,Siste;
                                                                    SVE=EngÜngsbetalning,fîrsta,Üterkommande betalning,sista];
                                                   OptionString=One Off,First,Recurring,Last;
                                                   Editable=No }
    { 10  ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=[ENU=New,File Created,Rejected,Posted;
                                                                    NOR=Ny,Fil opprettet,Avvist,Bokfõrt;
                                                                    SVE=Ny,Fil skapad,Avvisad,StÑngd];
                                                   OptionString=New,File Created,Rejected,Posted;
                                                   Editable=No }
    { 11  ;   ;Mandate ID          ;Code35        ;TableRelation="SEPA Direct Debit Mandate".ID WHERE (Customer No.=FIELD(Customer No.));
                                                   OnValidate=VAR
                                                                SEPADirectDebitMandate@1000 : Record 1230;
                                                              BEGIN
                                                                IF "Mandate ID" = '' THEN
                                                                  EXIT;
                                                                SEPADirectDebitMandate.GET("Mandate ID");
                                                                "Sequence Type" := SEPADirectDebitMandate.GetSequenceType;
                                                              END;

                                                   CaptionML=[ENU=Mandate ID;
                                                              NOR=Belastningsfullmakts-ID;
                                                              SVE=Medgivande-ID] }
    { 12  ;   ;Mandate Type of Payment;Option     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("SEPA Direct Debit Mandate"."Type of Payment" WHERE (ID=FIELD(Mandate ID)));
                                                   CaptionML=[ENU=Mandate Type of Payment;
                                                              NOR=Type betaling for belastningsfullmakt;
                                                              SVE=Medgivande fîr typ av betalning];
                                                   OptionCaptionML=[ENU=One Off,Recurrent;
                                                                    NOR=Engangsbetaling,Gjentatte betalinger;
                                                                    SVE=EngÜngsbetalning,Üterkommande betalning];
                                                   OptionString=One Off,Recurrent;
                                                   Editable=No }
    { 13  ;   ;Customer Name       ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Customer.Name WHERE (No.=FIELD(Customer No.)));
                                                   CaptionML=[ENU=Customer Name;
                                                              NOR=Betalers navn;
                                                              SVE=Betalarens namn];
                                                   Editable=No }
    { 14  ;   ;Applies-to Entry Document No.;Code20;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Cust. Ledger Entry"."Document No." WHERE (Entry No.=FIELD(Applies-to Entry No.)));
                                                   CaptionML=[ENU=Applies-to Entry Document No.;
                                                              NOR=Dokumentnummer for utligningspost;
                                                              SVE=Kopplas till lîpnr fîr dokument];
                                                   Editable=No }
    { 15  ;   ;Applies-to Entry Description;Text100;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Cust. Ledger Entry".Description WHERE (Entry No.=FIELD(Applies-to Entry No.)));
                                                   CaptionML=[ENU=Applies-to Entry Description;
                                                              NOR=Beskrivelse av utligningspost;
                                                              SVE=Kopplas till transaktionsbeskrivning];
                                                   Editable=No }
    { 16  ;   ;Applies-to Entry Posting Date;Date ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Cust. Ledger Entry"."Posting Date" WHERE (Entry No.=FIELD(Applies-to Entry No.)));
                                                   CaptionML=[ENU=Applies-to Entry Posting Date;
                                                              NOR=Bokfõringsdato for utligningspost;
                                                              SVE=Kopplas till transaktionens bokfîringsdatum];
                                                   Editable=No }
    { 17  ;   ;Applies-to Entry Currency Code;Code10;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Cust. Ledger Entry"."Currency Code" WHERE (Entry No.=FIELD(Applies-to Entry No.)));
                                                   TableRelation=Currency;
                                                   CaptionML=[ENU=Applies-to Entry Currency Code;
                                                              NOR=Valutakode for utligningspost;
                                                              SVE=Kopplas till transaktionens valutakod];
                                                   Editable=No }
    { 18  ;   ;Applies-to Entry Amount;Decimal    ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Detailed Cust. Ledg. Entry".Amount WHERE (Cust. Ledger Entry No.=FIELD(Applies-to Entry No.),
                                                                                                              Entry Type=FILTER(Initial Entry|Unrealized Loss|Unrealized Gain|Realized Loss|Realized Gain|Payment Discount|'Payment Discount (VAT Excl.)'|'Payment Discount (VAT Adjustment)'|Payment Tolerance|Payment Discount Tolerance|'Payment Tolerance (VAT Excl.)'|'Payment Tolerance (VAT Adjustment)'|'Payment Discount Tolerance (VAT Excl.)'|'Payment Discount Tolerance (VAT Adjustment)')));
                                                   CaptionML=[ENU=Applies-to Entry Amount;
                                                              NOR=Belõp i utligningspost;
                                                              SVE=Kopplas till transaktionsbelopp];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 19  ;   ;Applies-to Entry Rem. Amount;Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("Detailed Cust. Ledg. Entry".Amount WHERE (Cust. Ledger Entry No.=FIELD(Applies-to Entry No.)));
                                                   CaptionML=[ENU=Applies-to Entry Rem. Amount;
                                                              NOR=Restbelõp i utligningspost;
                                                              SVE=Kopplas till transaktionens Üterst. belopp];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 20  ;   ;Applies-to Entry Open;Boolean      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Cust. Ledger Entry".Open WHERE (Entry No.=FIELD(Applies-to Entry No.)));
                                                   CaptionML=[ENU=Applies-to Entry Open;
                                                              NOR=èpen utligningspost;
                                                              SVE=Kopplas îppen transaktion];
                                                   Editable=No }
    { 21  ;   ;Direct Debit Collection Status;Option;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Direct Debit Collection".Status WHERE (No.=FIELD(Direct Debit Collection No.)));
                                                   CaptionML=[ENU=Direct Debit Collection Status;
                                                              NOR=Status for Direct Debit-oppkreving;
                                                              SVE=Status fîr autogirosamling];
                                                   OptionCaptionML=[ENU=New,Canceled,File Created,Posted,Closed;
                                                                    NOR=Ny,Avbrutt,Fil opprettet,Bokfõrt,Lukket;
                                                                    SVE=Ny,Avbruten,Fil skapad,Bokfîrd,StÑngd];
                                                   OptionString=New,Canceled,File Created,Posted,Closed }
  }
  KEYS
  {
    {    ;Direct Debit Collection No.,Entry No.   ;Clustered=Yes }
    {    ;Applies-to Entry No.,Status              }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DocTypeErr@1000 : TextConst 'ENU=The customer ledger entry must be of type Invoice, Finance Charge Memo, or Reminder.;NOR=Kundeposten mÜ vëre av typen Faktura, Rentenota eller Purring.;SVE=Kundreskontratransaktionen mÜste vara av typen Faktura, RÑntefaktura eller BetalningspÜminnelse.';
      AmountMustBePositiveErr@1001 : TextConst 'ENU=The amount must be positive.;NOR=Belõpet mÜ vëre positivt.;SVE=Beloppet mÜste vara positivt.';
      LargerThanRemainingErr@1002 : TextConst '@@@="%1 = an amount. %2 = currency code, e.g. 123.45 EUR";ENU=You cannot collect an amount that is larger than the remaining amount for the invoice (%1 %2) that is not on other collection entries.;NOR=Du kan ikke kreve inn et belõp som er stõrre enn restbelõpet for fakturaen (%1 %2), og som ikke finnes i andre innkrevingsposter.;SVE=Du kan inte samla ett belopp som Ñr stîrre Ñn det ÜterstÜende beloppet fîr fakturan (%1 %2) som inte finns i andra samlingsposter.';
      RejectQst@1003 : TextConst 'ENU=Do you want to reject this collection entry?;NOR=Vil du avvise denne oppkrevingsposten?;SVE=Vill du avvisa den hÑr samlingstransaktionen?';

    [External]
    PROCEDURE CreateNew@3(DirectDebitCollectionNo@1000 : Integer;CustLedgerEntry@1001 : Record 21);
    VAR
      IsHandled@1002 : Boolean;
    BEGIN
      "Direct Debit Collection No." := DirectDebitCollectionNo;
      SETRANGE("Direct Debit Collection No.",DirectDebitCollectionNo);
      LOCKTABLE;
      IF FINDLAST THEN;
      "Entry No." += 1;
      INIT;
      VALIDATE("Customer No.",CustLedgerEntry."Customer No.");
      VALIDATE("Applies-to Entry No.",CustLedgerEntry."Entry No.");
      INSERT;

      IsHandled := FALSE;
      OnBeforeCheckSEPA(Rec,IsHandled);
      IF NOT IsHandled THEN
        CODEUNIT.RUN(CODEUNIT::"SEPA DD-Check Line",Rec);
    END;

    [External]
    PROCEDURE DeletePaymentFileErrors@61();
    VAR
      GenJnlLine@1001 : Record 81;
    BEGIN
      TransferPKToGenJnlLine(GenJnlLine);
      GenJnlLine.DeletePaymentFileErrors;
    END;

    [External]
    PROCEDURE HasPaymentFileErrors@5() : Boolean;
    VAR
      GenJnlLine@1001 : Record 81;
    BEGIN
      TransferPKToGenJnlLine(GenJnlLine);
      EXIT(GenJnlLine.HasPaymentFileErrors);
    END;

    [External]
    PROCEDURE ExportSEPA@2();
    VAR
      IsHandled@1000 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeExportSEPA(Rec,IsHandled);
      IF NOT IsHandled THEN
        CODEUNIT.RUN(CODEUNIT::"SEPA DD-Export File",Rec);
    END;

    [External]
    PROCEDURE InsertPaymentFileError@64(Text@1001 : Text);
    VAR
      GenJnlLine@1002 : Record 81;
    BEGIN
      TransferPKToGenJnlLine(GenJnlLine);
      GenJnlLine.InsertPaymentFileError(Text);
    END;

    [External]
    PROCEDURE Reject@1();
    VAR
      SEPADirectDebitMandate@1000 : Record 1230;
    BEGIN
      TESTFIELD(Status,Status::"File Created");
      IF NOT CONFIRM(RejectQst) THEN
        EXIT;
      Status := Status::Rejected;
      MODIFY;
      IF "Mandate ID" = '' THEN
        EXIT;
      SEPADirectDebitMandate.GET("Mandate ID");
      SEPADirectDebitMandate.RollBackSequenceType;
    END;

    LOCAL PROCEDURE TransferPKToGenJnlLine@4(VAR GenJnlLine@1000 : Record 81);
    BEGIN
      GenJnlLine."Document No." := COPYSTR(FORMAT("Direct Debit Collection No.",0,9),1,MAXSTRLEN(GenJnlLine."Document No."));
      GenJnlLine."Line No." := "Entry No.";
    END;

    LOCAL PROCEDURE GetAmountInActiveCollections@6() : Decimal;
    VAR
      DirectDebitCollectionEntry@1001 : Record 1208;
      AmountAlreadyInCollection@1000 : Decimal;
    BEGIN
      IF "Applies-to Entry No." = 0 THEN
        EXIT(0);

      DirectDebitCollectionEntry.SETRANGE("Applies-to Entry No.","Applies-to Entry No.");
      DirectDebitCollectionEntry.SETFILTER(Status,'%1|%2',Status::New,Status::"File Created");
      IF DirectDebitCollectionEntry.FINDSET THEN
        REPEAT
          IF (DirectDebitCollectionEntry."Direct Debit Collection No." <> "Direct Debit Collection No.") OR
             (DirectDebitCollectionEntry."Entry No." <> "Entry No.")
          THEN
            AmountAlreadyInCollection += DirectDebitCollectionEntry."Transfer Amount";
        UNTIL DirectDebitCollectionEntry.NEXT = 0;
      EXIT(AmountAlreadyInCollection);
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCheckSEPA@1009(VAR DirectDebitCollectionEntry@1000 : Record 1208;VAR IsHandled@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeDocTypeErr@1000(CustLedgerEntry@1001 : Record 21;VAR AlllowedDocumentType@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeExportSEPA@1011(VAR DirectDebitCollectionEntry@1000 : Record 1208;VAR IsHandled@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeValidateTransferAmount@1005(VAR DirectDebitCollectionEntry@1000 : Record 1208;VAR IsHandled@1001 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

