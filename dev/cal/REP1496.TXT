OBJECT Report 1496 Suggest Bank Acc. Recon. Lines
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Bankkontoabst.-Zeilen vorschl.;
               ENU=Suggest Bank Acc. Recon. Lines;
               NLD=Bankreconciliatie voorstellen;
               NOR=ForeslÜ bankkto.avstemm.linjer];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 4558;    ;DataItem;                    ;
               DataItemTable=Table270;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=BEGIN
                               OnPreDataItemBankAccount(ExcludeReversedEntries);

                               IF EndDate = 0D THEN
                                 ERROR(Text000);

                               BankAccReconLine.FilterBankRecLines(BankAccRecon);
                               IF NOT BankAccReconLine.FINDLAST THEN BEGIN
                                 BankAccReconLine."Statement Type" := BankAccRecon."Statement Type";
                                 BankAccReconLine."Bank Account No." := BankAccRecon."Bank Account No.";
                                 BankAccReconLine."Statement No." := BankAccRecon."Statement No.";
                                 BankAccReconLine."Statement Line No." := 0;
                               END;

                               SETRANGE("No.",BankAccRecon."Bank Account No.");
                             END;

               OnAfterGetRecord=BEGIN
                                  BankAccLedgEntry.RESET;
                                  BankAccLedgEntry.SETCURRENTKEY("Bank Account No.","Posting Date");
                                  BankAccLedgEntry.SETRANGE("Bank Account No.","No.");
                                  BankAccLedgEntry.SETRANGE("Posting Date",StartDate,EndDate);
                                  BankAccLedgEntry.SETRANGE(Open,TRUE);
                                  BankAccLedgEntry.SETRANGE("Statement Status",BankAccLedgEntry."Statement Status"::Open);
                                  IF ExcludeReversedEntries THEN
                                    BankAccLedgEntry.SETRANGE(Reversed,FALSE);
                                  EOFBankAccLedgEntries := NOT BankAccLedgEntry.FIND('-');

                                  IF IncludeChecks THEN BEGIN
                                    CheckLedgEntry.RESET;
                                    CheckLedgEntry.SETCURRENTKEY("Bank Account No.","Check Date");
                                    CheckLedgEntry.SETRANGE("Bank Account No.","No.");
                                    CheckLedgEntry.SETRANGE("Check Date",StartDate,EndDate);
                                    CheckLedgEntry.SETFILTER(
                                      "Entry Status",'%1|%2',CheckLedgEntry."Entry Status"::Posted,
                                      CheckLedgEntry."Entry Status"::"Financially Voided");
                                    CheckLedgEntry.SETRANGE(Open,TRUE);
                                    CheckLedgEntry.SETRANGE("Statement Status",BankAccLedgEntry."Statement Status"::Open);
                                    EOFCheckLedgEntries := NOT CheckLedgEntry.FIND('-');
                                  END;

                                  WHILE (NOT EOFBankAccLedgEntries) OR (IncludeChecks AND (NOT EOFCheckLedgEntries)) DO
                                    CASE TRUE OF
                                      NOT IncludeChecks:
                                        BEGIN
                                          EnterBankAccLine(BankAccLedgEntry);
                                          EOFBankAccLedgEntries := BankAccLedgEntry.NEXT = 0;
                                        END;
                                      (NOT EOFBankAccLedgEntries) AND (NOT EOFCheckLedgEntries) AND
                                      (BankAccLedgEntry."Posting Date" <= CheckLedgEntry."Check Date"):
                                        BEGIN
                                          CheckLedgEntry2.RESET;
                                          CheckLedgEntry2.SETCURRENTKEY("Bank Account Ledger Entry No.");
                                          CheckLedgEntry2.SETRANGE("Bank Account Ledger Entry No.",BankAccLedgEntry."Entry No.");
                                          CheckLedgEntry2.SETRANGE(Open,TRUE);
                                          IF NOT CheckLedgEntry2.FINDFIRST THEN
                                            EnterBankAccLine(BankAccLedgEntry);
                                          EOFBankAccLedgEntries := BankAccLedgEntry.NEXT = 0;
                                        END;
                                      (NOT EOFBankAccLedgEntries) AND (NOT EOFCheckLedgEntries) AND
                                      (BankAccLedgEntry."Posting Date" > CheckLedgEntry."Check Date"):
                                        BEGIN
                                          EnterCheckLine(CheckLedgEntry);
                                          EOFCheckLedgEntries := CheckLedgEntry.NEXT = 0;
                                        END;
                                      (NOT EOFBankAccLedgEntries) AND EOFCheckLedgEntries:
                                        BEGIN
                                          CheckLedgEntry2.RESET;
                                          CheckLedgEntry2.SETCURRENTKEY("Bank Account Ledger Entry No.");
                                          CheckLedgEntry2.SETRANGE("Bank Account Ledger Entry No.",BankAccLedgEntry."Entry No.");
                                          CheckLedgEntry2.SETRANGE(Open,TRUE);
                                          IF NOT CheckLedgEntry2.FINDFIRST THEN
                                            EnterBankAccLine(BankAccLedgEntry);
                                          EOFBankAccLedgEntries := BankAccLedgEntry.NEXT = 0;
                                        END;
                                      EOFBankAccLedgEntries AND (NOT EOFCheckLedgEntries):
                                        BEGIN
                                          EnterCheckLine(CheckLedgEntry);
                                          EOFCheckLedgEntries := CheckLedgEntry.NEXT = 0;
                                        END;
                                    END;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 5   ;2   ;Group     ;
                  CaptionML=[DEU=Auszug Periode;
                             ENU=Statement Period;
                             NLD=Dagafschriftperiode;
                             NOR=Utdragsperiode;
                             SVE=Rapportperiod] }

      { 1   ;3   ;Field     ;
                  Name=StartingDate;
                  CaptionML=[DEU=Startdatum;
                             ENU=Starting Date;
                             NLD=Begindatum;
                             NOR=Startdato;
                             SVE=Startdatum];
                  ToolTipML=[DEU=Gibt das Datum an, ab dem fÅr den Bericht oder die Stapelverarbeitung Informationen verarbeitet werden.;
                             ENU=Specifies the date from which the report or batch job processes information.;
                             NLD=Hiermee wordt de datum opgegeven vanaf wanneer het rapport of de batchverwerking informatie verwerkt.;
                             NOR=Angir datoen som rapporten eller kjõrselen behandler informasjon fra.;
                             SVE=Anger datumet frÜn vilket rapporten eller buntjobbet bearbetar information.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=StartDate }

      { 3   ;3   ;Field     ;
                  Name=EndingDate;
                  CaptionML=[DEU=Enddatum;
                             ENU=Ending Date;
                             NLD=Einddatum;
                             NOR=Sluttdato;
                             SVE=Slutdatum];
                  ToolTipML=[DEU=Gibt das Datum an, bis zu dem fÅr den Bericht oder die Stapelverarbeitung Informationen verarbeitet werden.;
                             ENU=Specifies the date to which the report or batch job processes information.;
                             NLD=Hiermee wordt de datum opgegeven tot wanneer het rapport of de batchverwerking informatie verwerkt.;
                             NOR=Angir datoen som rapporten eller kjõrselen behandler informasjon til.;
                             SVE=Anger datumet till vilket rapporten eller buntjobbet bearbetar information.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=EndDate }

      { 8   ;2   ;Field     ;
                  CaptionML=[DEU=Mit Schecks;
                             ENU=Include Checks;
                             NLD=Inclusief cheques;
                             NOR=Ta med sjekker];
                  ToolTipML=[DEU=Gibt an, ob der Bericht Scheckposten einbeziehen soll. Wenn Sie diese Option aktivieren, werden Scheckposten anstelle der entsprechenden Bankposten vorgeschlagen.;
                             ENU=Specifies if you want the report to include check ledger entries. If you choose this option, check ledger entries are suggested instead of the corresponding bank account ledger entries.;
                             NLD=Hiermee wordt opgegeven of u wilt dat in het rapport chequeposten worden opgenomen. Als u deze optie kiest, worden er chequeposten voorgesteld in plaats van de overeenkomstige bankposten.;
                             NOR=Angir om du vil at rapporten skal inkludere sjekkposter. Hvis du velger dette alternativet, foreslÜs sjekkposter i stedet for de tilsvarende bankkontopostene.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=IncludeChecks }

      { 2   ;2   ;Field     ;
                  Name=ExcludeReversedEntries;
                  CaptionML=[DEU=Stornierte Posten ausschlie·en;
                             ENU=Exclude Reversed Entries;
                             NLD=Tegengeboekte posten uitsluiten;
                             NOR=Utelat tilbakefõrte poster];
                  ToolTipML=[DEU=Gibt an, ob stornierte Posten aus dem Bericht ausgeschlossen werden sollen.;
                             ENU=Specifies if you want to exclude reversed entries from the report.;
                             NLD=Hiermee wordt opgegeven of u tegengeboekte posten uit het rapport wilt uitsluiten.;
                             NOR=Angir om du vil utelate tilbakefõrte poster fra rapporten.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=ExcludeReversedEntries }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Geben Sie das Enddatum ein.;ENU=Enter the Ending Date.;NLD=Vul de einddatum in.;NOR=Angi sluttdato.';
      BankAccLedgEntry@1001 : Record 271;
      CheckLedgEntry@1002 : Record 272;
      CheckLedgEntry2@1003 : Record 272;
      BankAccRecon@1004 : Record 273;
      BankAccReconLine@1005 : Record 274;
      BankAccSetStmtNo@1006 : Codeunit 375;
      CheckSetStmtNo@1007 : Codeunit 376;
      StartDate@1008 : Date;
      EndDate@1009 : Date;
      IncludeChecks@1010 : Boolean;
      EOFBankAccLedgEntries@1011 : Boolean;
      EOFCheckLedgEntries@1012 : Boolean;
      ExcludeReversedEntries@1013 : Boolean;

    [External]
    PROCEDURE SetStmt@1(VAR BankAccRecon2@1000 : Record 273);
    BEGIN
      BankAccRecon := BankAccRecon2;
      EndDate := BankAccRecon."Statement Date";
    END;

    LOCAL PROCEDURE EnterBankAccLine@2(VAR BankAccLedgEntry2@1000 : Record 271);
    BEGIN
      BankAccReconLine.INIT;
      BankAccReconLine."Statement Line No." := BankAccReconLine."Statement Line No." + 10000;
      BankAccReconLine."Transaction Date" := BankAccLedgEntry2."Posting Date";
      BankAccReconLine.Description := BankAccLedgEntry2.Description;
      BankAccReconLine."Document No." := BankAccLedgEntry2."Document No.";
      BankAccReconLine."Statement Amount" := BankAccLedgEntry2."Remaining Amount";
      BankAccReconLine."Applied Amount" := BankAccReconLine."Statement Amount";
      BankAccReconLine.Type := BankAccReconLine.Type::"Bank Account Ledger Entry";
      BankAccReconLine."Applied Entries" := 1;
      BankAccSetStmtNo.SetReconNo(BankAccLedgEntry2,BankAccReconLine);
      BankAccReconLine.INSERT;
    END;

    LOCAL PROCEDURE EnterCheckLine@3(VAR CheckLedgEntry3@1000 : Record 272);
    BEGIN
      BankAccReconLine.INIT;
      BankAccReconLine."Statement Line No." := BankAccReconLine."Statement Line No." + 10000;
      BankAccReconLine."Transaction Date" := CheckLedgEntry3."Check Date";
      BankAccReconLine.Description := CheckLedgEntry3.Description;
      BankAccReconLine."Statement Amount" := -CheckLedgEntry3.Amount;
      BankAccReconLine."Applied Amount" := BankAccReconLine."Statement Amount";
      BankAccReconLine.Type := BankAccReconLine.Type::"Check Ledger Entry";
      BankAccReconLine."Check No." := CheckLedgEntry3."Check No.";
      BankAccReconLine."Applied Entries" := 1;
      CheckSetStmtNo.SetReconNo(CheckLedgEntry3,BankAccReconLine);
      BankAccReconLine.INSERT;
    END;

    [External]
    PROCEDURE InitializeRequest@4(NewStartDate@1000 : Date;NewEndDate@1001 : Date;NewIncludeChecks@1002 : Boolean);
    BEGIN
      StartDate := NewStartDate;
      EndDate := NewEndDate;
      IncludeChecks := NewIncludeChecks;
      ExcludeReversedEntries := FALSE;
    END;

    [Integration]
    PROCEDURE OnPreDataItemBankAccount@5(VAR ExcludeReversedEntries@1000 : Boolean);
    BEGIN
      // ExcludeReversedEntries = FALSE by default
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

