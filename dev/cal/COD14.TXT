OBJECT Codeunit 14 Gen. Jnl.-Show Entries
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    TableNo=81;
    OnRun=BEGIN
            CASE "Account Type" OF
              "Account Type"::"G/L Account":
                BEGIN
                  GLEntry.SETCURRENTKEY("G/L Account No.","Posting Date");
                  GLEntry.SETRANGE("G/L Account No.","Account No.");
                  IF GLEntry.FINDLAST THEN;
                  PAGE.RUN(PAGE::"General Ledger Entries",GLEntry);
                END;
              "Account Type"::Customer:
                BEGIN
                  CustLedgEntry.SETCURRENTKEY("Customer No.","Posting Date");
                  CustLedgEntry.SETRANGE("Customer No.","Account No.");
                  IF CustLedgEntry.FINDLAST THEN;
                  PAGE.RUN(PAGE::"Customer Ledger Entries",CustLedgEntry);
                END;
              "Account Type"::Vendor:
                BEGIN
                  VendLedgEntry.SETCURRENTKEY("Vendor No.","Posting Date");
                  VendLedgEntry.SETRANGE("Vendor No.","Account No.");
                  IF VendLedgEntry.FINDLAST THEN;
                  PAGE.RUN(PAGE::"Vendor Ledger Entries",VendLedgEntry);
                END;
              "Account Type"::Employee:
                BEGIN
                  EmplLedgEntry.SETCURRENTKEY("Employee No.","Posting Date");
                  EmplLedgEntry.SETRANGE("Employee No.","Account No.");
                  IF EmplLedgEntry.FINDLAST THEN;
                  PAGE.RUN(PAGE::"Employee Ledger Entries",EmplLedgEntry);
                END;
              "Account Type"::"Bank Account":
                BEGIN
                  BankAccLedgEntry.SETCURRENTKEY("Bank Account No.","Posting Date");
                  BankAccLedgEntry.SETRANGE("Bank Account No.","Account No.");
                  IF BankAccLedgEntry.FINDLAST THEN;
                  PAGE.RUN(PAGE::"Bank Account Ledger Entries",BankAccLedgEntry);
                END;
              "Account Type"::"Fixed Asset":
                IF "FA Posting Type" <> "FA Posting Type"::Maintenance THEN BEGIN
                  FALedgEntry.SETCURRENTKEY("FA No.","Depreciation Book Code","FA Posting Date");
                  FALedgEntry.SETRANGE("FA No.","Account No.");
                  IF "Depreciation Book Code" <> '' THEN
                    FALedgEntry.SETRANGE("Depreciation Book Code","Depreciation Book Code");
                  IF FALedgEntry.FINDLAST THEN;
                  PAGE.RUN(PAGE::"FA Ledger Entries",FALedgEntry);
                END ELSE BEGIN
                  MaintenanceLedgEntry.SETCURRENTKEY("FA No.","Depreciation Book Code","FA Posting Date");
                  MaintenanceLedgEntry.SETRANGE("FA No.","Account No.");
                  IF "Depreciation Book Code" <> '' THEN
                    MaintenanceLedgEntry.SETRANGE("Depreciation Book Code","Depreciation Book Code");
                  IF MaintenanceLedgEntry.FINDLAST THEN;
                  PAGE.RUN(PAGE::"Maintenance Ledger Entries",MaintenanceLedgEntry);
                END;
              "Account Type"::"IC Partner":
                ERROR(Text001);
            END;

            OnAfterRun(Rec);
          END;

  }
  CODE
  {
    VAR
      GLEntry@1000 : Record 17;
      EmplLedgEntry@1007 : Record 5222;
      CustLedgEntry@1001 : Record 21;
      VendLedgEntry@1002 : Record 25;
      BankAccLedgEntry@1003 : Record 271;
      FALedgEntry@1004 : Record 5601;
      MaintenanceLedgEntry@1005 : Record 11072085;
      Text001@1006 : TextConst 'ENU=Intercompany partners do not have ledger entries.;NOR=Det finnes ikke poster for konserninterne partnere.;SVE=Koncerninterna partner har inte reskontraposter.';

    [Integration]
    LOCAL PROCEDURE OnAfterRun@1(GenJournalLine@1000 : Record 81);
    BEGIN
    END;

    BEGIN
    END.
  }
}

