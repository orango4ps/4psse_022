OBJECT Report 11012143 Create Leave Lines
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Urlaubszeilen erstellen;
               ENU=Create Leave Lines;
               NLD=Verlofregels aanmaken;
               NOR=Opprette frav‘rsrader;
               SVE=Skapa fr†nvarorader];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 7528;    ;DataItem;                    ;
               DataItemTable=Table5200;
               OnPreDataItem=BEGIN
                               IF PostingDate = 0D THEN
                                 ERROR(Text001);

                               IF EntryType = EntryType::"General Ledger" THEN BEGIN
                                 IF DivideYearRightBy = 0 THEN
                                   ERROR(Text002);
                                 IF StartDate = 0D THEN
                                   ERROR(Text003);
                                 IF EndDate = 0D THEN
                                   ERROR(Text004);
                               END;

                               IF GenJnlBatch."No. Series" <> '' THEN BEGIN
                                 CLEAR(NoSeriesMgt);
                                 DocumentNo := NoSeriesMgt.TryGetNextNo(GenJnlBatch."No. Series",PostingDate);
                               END;

                               LeaveJournalLine.SETRANGE("Journal Template Name", SelectedTemplate);
                               LeaveJournalLine.SETRANGE("Journal Batch Name", SelectedBatch);
                               IF LeaveJournalLine.FIND('+') THEN
                                 LineNo := LeaveJournalLine."Line No." + 10000
                               ELSE
                                 LineNo := 10000;
                             END;

               OnAfterGetRecord=VAR
                                  TotHours@1100485000 : Decimal;
                                BEGIN
                                  LeaveJournalLine.INIT;
                                  LeaveJournalLine."Journal Template Name" := SelectedTemplate;
                                  LeaveJournalLine."Journal Batch Name" := SelectedBatch;
                                  LeaveJournalLine."Posting Date" := PostingDate;
                                  LeaveJournalLine."Document Date" := PostingDate;
                                  LeaveJournalLine."Document No." := DocumentNo;
                                  LeaveJournalLine."Employee No." := "No.";
                                  LeaveJournalLine.Description := DescTxt;
                                  LeaveJournalLine."Entry Type" := EntryType;
                                  LeaveJournalLine."Source Code" := GenJnlTemplate."Source Code";
                                  LeaveJournalLine."Reason Code" := GenJnlBatch."Reason Code";
                                  LeaveJournalLine."Posting No. Series" := GenJnlBatch."Posting No. Series";

                                  LeaveRightRec.SETCURRENTKEY("Employee No.", "Start Date");
                                  LeaveRightRec.SETRANGE("Employee No.", "No.");
                                  LeaveRightRec.SETRANGE("Start Date", 0D, PostingDate);
                                  IF LeaveRightRec.FIND('+') THEN BEGIN
                                    LeaveRightRec.SETRANGE("Start Date", LeaveRightRec."Start Date");
                                    IF LeaveRightRec.FIND('-') THEN BEGIN
                                      TotHours := 0;
                                      REPEAT
                                        IF EntryType = EntryType::Hours THEN BEGIN
                                          LeaveJournalLine."Line No." := LineNo;
                                          LineNo := LineNo + 10000;
                                          LeaveJournalLine."Leave Type" := LeaveRightRec."Leave Type";
                                          LeaveJournalLine."No. of Hours" := LeaveRightRec."Hours Per Year";
                                          LeaveJournalLine.INSERT;
                                        END ELSE
                                          TotHours := TotHours + LeaveRightRec."Hours Per Year";
                                      UNTIL LeaveRightRec.NEXT = 0;
                                      IF (EntryType = EntryType::"General Ledger") AND (TotHours > 0) THEN BEGIN
                                        LeaveJournalLine."Line No." := LineNo;
                                        LineNo := LineNo + 10000;
                                        LeaveJournalLine.Rate := LeaveJournalLine.GetRate;
                                        LeaveJournalLine.VALIDATE("No. of Hours", TotHours/DivideYearRightBy);
                                        LeaveJournalLine.INSERT;
                                      END;
                                    END;
                                  END;

                                  IF (EntryType = EntryType::"General Ledger") THEN BEGIN
                                    TotHours := 0;
                                    LeaveLedgerEntry.SETCURRENTKEY("Employee No.");
                                    LeaveLedgerEntry.SETRANGE("Employee No.", "No.");
                                    LeaveLedgerEntry.SETRANGE("Posting Date", StartDate, EndDate);
                                    LeaveLedgerEntry.SETRANGE("Entry Type", LeaveLedgerEntry."Entry Type"::Hours);
                                    LeaveLedgerEntry.SETFILTER("Journal Batch Name", '%1', '');
                                    IF LeaveLedgerEntry.FIND('-') THEN
                                      REPEAT
                                        TotHours := TotHours + LeaveLedgerEntry."No. of Hours";
                                      UNTIL LeaveLedgerEntry.NEXT = 0;
                                    IF TotHours <> 0 THEN BEGIN
                                      LeaveJournalLine."Line No." := LineNo;
                                      LineNo := LineNo + 10000;
                                      LeaveJournalLine.Rate := LeaveJournalLine.GetRate;
                                      LeaveJournalLine.VALIDATE("No. of Hours", TotHours);
                                      LeaveJournalLine.INSERT;
                                    END;
                                  END;
                                END;

               ReqFilterFields=Status,Employment Date,Inactive Date,Termination Date }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnInit=BEGIN
               DivideByEditable := TRUE;
               EndDateEditable := TRUE;
               StartDateEditable := TRUE;
               DocumentNoEditable := TRUE;
             END;

      OnOpenPage=BEGIN
                   SetEditable;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1100485002;2;Field  ;
                  Name=Posting Date;
                  CaptionML=[DEU=Buchungsdatum;
                             ENU=Posting Date;
                             NLD=Boekingsdatum;
                             NOR=Bokf›ringsdato;
                             SVE=Bokf”ringsdatum];
                  SourceExpr=PostingDate }

      { 1100485000;2;Field  ;
                  Name=DocumentNo;
                  CaptionML=[DEU=Belegnr.;
                             ENU=Document No.;
                             NLD=Documentnr.;
                             NOR=Bilagsnr.;
                             SVE=Verifikationsnr];
                  SourceExpr=DocumentNo;
                  Editable=DocumentNoEditable }

      { 1100485004;2;Field  ;
                  Name=Entry Type;
                  CaptionML=[DEU=Postenart;
                             ENU=Entry Type;
                             NLD=Boekingssoort;
                             NOR=Posttype;
                             SVE=Transaktionstyp];
                  OptionCaptionML=[DEU=Stunden,Sachkonto;
                                   ENU=Hours,General Ledger;
                                   NLD=Uren,Grootboek;
                                   NOR=Timer,hovedbok;
                                   SVE=Timmar,redovisning];
                  SourceExpr=EntryType;
                  OnValidate=BEGIN
                               SetEditable;
                             END;
                              }

      { 1100485008;2;Field  ;
                  Name=Description;
                  CaptionML=[DEU=Beschreibung;
                             ENU=Description;
                             NLD=Omschrijving;
                             NOR=Beskrivelse;
                             SVE=Beskrivning];
                  SourceExpr=DescTxt }

      { 1100485011;2;Field  ;
                  Name=StartDate;
                  CaptionML=[DEU=Startdatum;
                             ENU=Start Date;
                             NLD=Startdatum;
                             NOR=Startdato;
                             SVE=Startdatum];
                  SourceExpr=StartDate;
                  Editable=StartDateEditable }

      { 1100485013;2;Field  ;
                  Name=EndDate;
                  CaptionML=[DEU=Enddatum;
                             ENU=End Date;
                             NLD=Einddatum;
                             NOR=Sluttdato;
                             SVE=Slutdatum];
                  SourceExpr=EndDate;
                  Editable=EndDateEditable }

      { 1100485006;2;Field  ;
                  Name=DivideBy;
                  CaptionML=[DEU=Teilen j„hrliches Urlaubsrecht durch;
                             ENU=Divide Year Right By;
                             NLD=Deel jaarlijks verlofrecht door;
                             NOR=Del †r korrekt etter;
                             SVE=Dela †r korrekt efter];
                  BlankZero=Yes;
                  SourceExpr=DivideYearRightBy;
                  Editable=DivideByEditable }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      GenJnlTemplate@1100485015 : Record 80;
      GenJnlBatch@1100485008 : Record 232;
      LeaveJournalLine@1100485011 : Record 11020234;
      LeaveRightRec@1100485007 : Record 11020261;
      LeaveLedgerEntry@1100485019 : Record 11020235;
      NoSeriesMgt@1100485016 : Codeunit 396;
      DocumentNo@1100485002 : Code[20];
      SelectedTemplate@1100485013 : Code[10];
      SelectedBatch@1100485012 : Code[10];
      PostingDate@1100485001 : Date;
      StartDate@1100485009 : Date;
      EndDate@1100485010 : Date;
      EntryType@1100485003 : 'Hours,General Ledger';
      DescTxt@1100485004 : Text[30];
      DivideYearRightBy@1100485005 : Decimal;
      Text001@1100485000 : TextConst 'DEU=Das Buchungsdatum muss eingegeben werden.;ENU=Posting Date may not be empty;NLD=De boekingsdatum moet gevuld worden.;NOR=Bokf›ringsdato kan ikke v‘re tom.;SVE=Bokf”ringsdatum f†r inte vara tomt.';
      LineNo@1100485006 : Integer;
      Text002@1100485014 : TextConst 'DEU=''Teilen j„hrliches Urlaubsrecht durch'' muss eingegeben werden.;ENU=''Divide Year Right By'' may not be empty;NLD=''Deel jaarlijks verlofrecht door'' moet gevuld worden.;NOR=''Del †r korrekt etter'' kan ikke v‘re tom;SVE=''Dela †r korrekt efter'' kan inte vara tomt';
      Text003@1100485017 : TextConst 'DEU=Das Startdatum muss eingegeben werden.;ENU=Start Date may not be empty;NLD=De startdatum moet gevuld worden.;NOR=Startdato kan ikke v‘re tom;SVE=Startdatumet f†r inte vara tomt';
      Text004@1100485018 : TextConst 'DEU=Das Enddatum muss eingegeben werden.;ENU=End Date may not be empty;NLD=De einddatum moet gevuld worden.;NOR=Sluttdato kan ikke v‘re tom;SVE=Slutdatum f†r inte vara tomt';
      DocumentNoEditable@19065439 : Boolean INDATASET;
      StartDateEditable@19045313 : Boolean INDATASET;
      EndDateEditable@19074532 : Boolean INDATASET;
      DivideByEditable@19074990 : Boolean INDATASET;

    PROCEDURE SetBatch@1100485000(lTemplate@1100485000 : Code[10];lBatch@1100485001 : Code[10]);
    BEGIN
      SelectedTemplate := lTemplate;
      SelectedBatch := lBatch;

      GenJnlTemplate.GET(SelectedTemplate);
      GenJnlBatch.GET(SelectedTemplate, SelectedBatch);
    END;

    PROCEDURE SetEditable@1100485001();
    BEGIN
      DocumentNoEditable := GenJnlBatch."No. Series" = '';
      StartDateEditable := EntryType = EntryType::"General Ledger";
      EndDateEditable := EntryType = EntryType::"General Ledger";
      DivideByEditable := EntryType = EntryType::"General Ledger";
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

