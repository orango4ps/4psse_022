OBJECT Page 6500 Item Tracking Summary
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Item Tracking Summary;
               NOR=Varesporingssummering;
               SVE=Artikelsp†rning sammandrag];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table338;
    PageType=Worksheet;
    SourceTableTemporary=Yes;
    OnInit=BEGIN
             Undefined1Visible := TRUE;
             Selected1Visible := TRUE;
             MaxQuantity1Visible := TRUE;
             BinContentVisible := TRUE;
           END;

    OnOpenPage=BEGIN
                 UpdateSelectedQuantity;

                 BinContentVisible := CurrBinCode <> '';
               END;

    OnAfterGetCurrRecord=BEGIN
                           UpdateIfFiltersHaveChanged;
                         END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 18  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the lot number for which availability is presented in the Item Tracking Summary window.;
                           NOR=Angir partinummeret for tilgjengeligheten som presenteres i vinduet Varesporingssummering.;
                           SVE=Anger det partinummer som disponibiliteten visas f”r i f”nstret Artikelsp†rning sammandrag.];
                ApplicationArea=#ItemTracking;
                SourceExpr="Lot No.";
                Editable=FALSE }

    { 16  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the serial number for which availability is presented in the Item Tracking Summary window.;
                           NOR=Angir serienummeret for tilgjengeligheten som presenteres i vinduet Varesporingssummering.;
                           SVE=Anger det serienummer som disponibiliteten visas f”r i f”nstret Artikelsp†rning sammandrag.];
                ApplicationArea=#ItemTracking;
                SourceExpr="Serial No.";
                Editable=FALSE }

    { 20  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the warranty expiration date, if any, of the item carrying the item tracking number.;
                           NOR=Angir utl›psdatoen for garantien hvis det finnes, for varen som inneholder varesporingsnummeret.;
                           SVE=Anger det eventuella utg†ngsdatumet f”r garantin p† artikeln med artikelsp†rningsnumret.];
                ApplicationArea=#ItemTracking;
                SourceExpr="Warranty Date";
                Visible=FALSE;
                Editable=FALSE }

    { 22  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the expiration date, if any, of the item carrying the item tracking number.;
                           NOR=Angir utl›psdatoen hvis det finnes, for varen som inneholder varesporingsnummeret.;
                           SVE=Anger det eventuella utg†ngsdatumet p† artikeln med artikelsp†rningsnumret.];
                ApplicationArea=#ItemTracking;
                SourceExpr="Expiration Date";
                Visible=FALSE;
                Editable=FALSE }

    { 8   ;2   ;Field     ;
                DrillDown=Yes;
                ToolTipML=[ENU=Specifies the total quantity of the item in inventory.;
                           NOR=Angir det samlede antallet for varen p† lager.;
                           SVE=Anger det totala antalet av artikeln som finns i lager.];
                ApplicationArea=#ItemTracking;
                SourceExpr="Total Quantity";
                Editable=FALSE;
                OnDrillDown=BEGIN
                              DrillDownEntries(FIELDNO("Total Quantity"));
                            END;
                             }

    { 24  ;2   ;Field     ;
                DrillDown=Yes;
                ToolTipML=[ENU=Specifies the total quantity of the lot or serial number that is requested in all documents.;
                           NOR=Angir det totale antallet for parti- eller serienummeret som ›nskes i alle dokumenter.;
                           SVE=Anger det totala antalet av det parti- eller serienummer som har best„llts i alla dokument.];
                ApplicationArea=#ItemTracking;
                SourceExpr="Total Requested Quantity";
                Editable=FALSE;
                OnDrillDown=BEGIN
                              DrillDownEntries(FIELDNO("Total Requested Quantity"));
                            END;
                             }

    { 31  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the quantity from the item tracking line that is selected on the document but not yet committed to the database.;
                           NOR=Angir antallet fra varesporingslinjen som er valgt i dokumentet, men som enn† ikke er sendt til databasen.;
                           SVE=Anger antalet fr†n den artikelsp†rningsrad som har markerats i dokumentet, men som „nnu inte har skickats till databasen.];
                ApplicationArea=#ItemTracking;
                SourceExpr="Current Pending Quantity";
                Editable=FALSE }

    { 12  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the quantity available for the user to request, in entries of the type on the line.;
                           NOR=Angir antallet som brukeren kan reservere i poster av typen p† linjen.;
                           SVE=Anger det antal som „r tillg„ngligt f”r anv„ndaren att best„lla i transaktioner f”r typen p† raden.];
                ApplicationArea=#ItemTracking;
                SourceExpr="Total Available Quantity";
                Editable=FALSE }

    { 14  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the quantity of items in the entry that are reserved for the line that the Reservation window is opened from.;
                           NOR=Angir antall varer i posten som er reservert for linjen som vinduet Reservasjon er †pnet fra.;
                           SVE=Anger hur m†nga artiklar i transaktionen som „r reserverade f”r raden som f”nstret Reservation ”ppnas fr†n.];
                ApplicationArea=#Reservation;
                SourceExpr="Current Reserved Quantity";
                Visible=FALSE;
                Editable=FALSE }

    { 10  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the total quantity of the relevant item that is reserved on documents or entries of the type on the line.;
                           NOR=Angir det totale antallet for den relevante varen som er reservert i dokumenter eller poster av typen p† linjen.;
                           SVE=Anger det totala antalet av artikeln som har reserverats i dokument eller transaktioner f”r typen p† raden.];
                ApplicationArea=#Reservation;
                SourceExpr="Total Reserved Quantity";
                Visible=FALSE;
                Editable=FALSE }

    { 34  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the quantity of the item in the bin specified in the document line.;
                           NOR=Angir antallet for varen p† hyllen som er angitt i dokumentlinjen.;
                           SVE=Anger antalet av artikeln p† lagerplatsen som anges p† dokumentraden.];
                ApplicationArea=#Warehouse;
                SourceExpr="Bin Content";
                Visible=BinContentVisible;
                OnDrillDown=BEGIN
                              DrillDownBinContent(FIELDNO("Bin Content"));
                            END;
                             }

    { 2   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the quantity of each lot or serial number that you want to use to fulfill the demand for the transaction.;
                           NOR=Angir antallet for hvert parti- eller serienummer du vil bruke til † oppfylle behovet for transaksjonen.;
                           SVE=Anger antalet f”r varje parti- eller serienummer som du vill anv„nda f”r att uppfylla behovet f”r transaktionen.];
                ApplicationArea=#ItemTracking;
                SourceExpr="Selected Quantity";
                Visible=SelectedQuantityVisible;
                Editable=SelectedQuantityEditable;
                Style=Strong;
                StyleExpr=TRUE;
                OnValidate=BEGIN
                             SelectedQuantityOnAfterValidat;
                           END;
                            }

    { 50  ;1   ;Group      }

    { 1901775901;2;Group  ;
                GroupType=FixedLayout }

    { 1900545401;3;Group  ;
                CaptionML=[ENU=Selectable;
                           NOR=Kan velges;
                           SVE=Valbar];
                Visible=MaxQuantity1Visible;
                GroupType=Group }

    { 27  ;4   ;Field     ;
                Name=MaxQuantity1;
                CaptionML=[ENU=Selectable;
                           NOR=Kan velges;
                           SVE=Valbar];
                ToolTipML=[ENU=Specifies the value from the Undefined field on the Item Tracking Lines window. This value indicates how much can be selected.;
                           NOR=Angir verdien fra Udefinert-feltet i vinduet Varesporingslinjer. Denne verdien viser hvor mye som kan velges.;
                           SVE=Anger v„rdet fr†n f„ltet Odefinierad i f”nstret Artikelsp†rningsrader. V„rdet anger hur mycket som kan v„ljas.];
                ApplicationArea=#ItemTracking;
                DecimalPlaces=0:5;
                SourceExpr=MaxQuantity;
                Editable=FALSE }

    { 1900724401;3;Group  ;
                CaptionML=[ENU=Selected;
                           NOR=Valgt;
                           SVE=Vald];
                Visible=Selected1Visible;
                GroupType=Group }

    { 5   ;4   ;Field     ;
                Name=Selected1;
                CaptionML=[ENU=Selected;
                           NOR=Valgt;
                           SVE=Vald];
                ToolTipML=[ENU=Specifies the sum of the quantity that you have selected. It Specifies a total of the quantities in the Selected Quantity fields.;
                           NOR=Angir summen av antallet du har valgt. Det angir en total for antallene i Valgt antall-feltene.;
                           SVE=Anger summan av det antal som du har valt. Den anger det totala antalet i f„lten Valt antal.];
                ApplicationArea=#ItemTracking;
                DecimalPlaces=0:5;
                SourceExpr=SelectedQuantity;
                Editable=FALSE }

    { 1900724101;3;Group  ;
                CaptionML=[ENU=Undefined;
                           NOR=Udefinert;
                           SVE=Odefinierad];
                Visible=Undefined1Visible;
                GroupType=Group }

    { 6   ;4   ;Field     ;
                Name=Undefined1;
                CaptionML=[ENU=Undefined;
                           NOR=Udefinert;
                           SVE=Odefinierad];
                ToolTipML=[ENU=Specifies the difference between the quantity that can be selected for the document line, and the quantity selected in this window.;
                           NOR=Angir forskjellen mellom antallet som kan velges for dokumentlinjen, og antallet som er valgt i dette vinduet.;
                           SVE=Anger skillnaden mellan det antal som kan v„ljas f”r dokumentraden och det antal som du har valt i det h„r f”nstret.];
                ApplicationArea=#ItemTracking;
                DecimalPlaces=2:5;
                BlankZero=Yes;
                SourceExpr=MaxQuantity - SelectedQuantity;
                Editable=FALSE }

  }
  CODE
  {
    VAR
      CurrItemTrackingCode@1001 : Record 6502;
      TempReservEntry@1000 : TEMPORARY Record 337;
      xFilterRec@1006 : Record 338;
      ItemTrackingDataCollection@1004 : Codeunit 6501;
      MaxQuantity@1002 : Decimal;
      SelectedQuantity@1003 : Decimal;
      CurrBinCode@1005 : Code[20];
      SelectedQuantityVisible@9622 : Boolean INDATASET;
      BinContentVisible@9450 : Boolean INDATASET;
      MaxQuantity1Visible@8075 : Boolean INDATASET;
      Selected1Visible@5215 : Boolean INDATASET;
      Undefined1Visible@1540 : Boolean INDATASET;
      SelectedQuantityEditable@5082 : Boolean INDATASET;

    [External]
    PROCEDURE SetSources@1(VAR ReservEntry@1000 : Record 337;VAR EntrySummary@1001 : Record 338);
    VAR
      xEntrySummary@1002 : Record 338;
    BEGIN
      TempReservEntry.RESET;
      TempReservEntry.DELETEALL;
      IF ReservEntry.FIND('-') THEN
        REPEAT
          TempReservEntry := ReservEntry;
          TempReservEntry.INSERT;
        UNTIL ReservEntry.NEXT = 0;

      xEntrySummary.SETVIEW(GETVIEW);
      RESET;
      DELETEALL;
      IF EntrySummary.FINDSET THEN
        REPEAT
          IF EntrySummary.HasQuantity THEN BEGIN
            Rec := EntrySummary;
            INSERT;
          END;
        UNTIL EntrySummary.NEXT = 0;
      SETVIEW(xEntrySummary.GETVIEW);
      UpdateSelectedQuantity;
    END;

    [External]
    PROCEDURE SetSelectionMode@2(SelectionMode@1000 : Boolean);
    BEGIN
      SelectedQuantityVisible := SelectionMode;
      SelectedQuantityEditable := SelectionMode;
      MaxQuantity1Visible := SelectionMode;
      Selected1Visible := SelectionMode;
      Undefined1Visible := SelectionMode;
    END;

    [External]
    PROCEDURE SetMaxQuantity@3(MaxQty@1000 : Decimal);
    BEGIN
      MaxQuantity := MaxQty;
    END;

    [External]
    PROCEDURE SetCurrentBinAndItemTrkgCode@8(BinCode@1000 : Code[20];ItemTrackingCode@1001 : Record 6502);
    BEGIN
      ItemTrackingDataCollection.SetCurrentBinAndItemTrkgCode(BinCode,ItemTrackingCode);
      BinContentVisible := BinCode <> '';
      CurrBinCode := BinCode;
      CurrItemTrackingCode := ItemTrackingCode;
      OnAfterSetCurrentBinAndItemTrkgCode(CurrBinCode,CurrItemTrackingCode,BinContentVisible,Rec,TempReservEntry);
    END;

    [External]
    PROCEDURE AutoSelectTrackingNo@56();
    VAR
      AvailableQty@1002 : Decimal;
      SelectedQty@1000 : Decimal;
      IsHandled@1001 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeAutoSelectTrackingNo(Rec,MaxQuantity,IsHandled);
      IF IsHandled THEN
        EXIT;

      IF MaxQuantity = 0 THEN
        EXIT;

      SelectedQty := MaxQuantity;
      IF FINDSET THEN
        REPEAT
          AvailableQty := "Total Available Quantity";
          IF "Bin Active" THEN
            AvailableQty := MinValueAbs("Bin Content","Total Available Quantity");

          IF AvailableQty > 0 THEN BEGIN
            "Selected Quantity" := MinValueAbs(AvailableQty,SelectedQty);
            SelectedQty -= "Selected Quantity";
            MODIFY;
          END;
        UNTIL (NEXT = 0) OR (SelectedQty <= 0);
    END;

    LOCAL PROCEDURE MinValueAbs@20(Value1@1000 : Decimal;Value2@1001 : Decimal) : Decimal;
    BEGIN
      IF ABS(Value1) < ABS(Value2) THEN
        EXIT(Value1);

      EXIT(Value2);
    END;

    LOCAL PROCEDURE UpdateSelectedQuantity@4();
    VAR
      xEntrySummary@1000 : Record 338;
    BEGIN
      IF NOT SelectedQuantityVisible THEN
        EXIT;
      IF MODIFY THEN; // Ensure that changes to current rec are included in CALCSUMS
      xEntrySummary := Rec;
      CALCSUMS("Selected Quantity");
      SelectedQuantity := "Selected Quantity";
      Rec := xEntrySummary;
    END;

    [External]
    PROCEDURE GetSelected@5(VAR EntrySummary@1000 : Record 338);
    BEGIN
      EntrySummary.RESET;
      EntrySummary.DELETEALL;
      SETFILTER("Selected Quantity",'<>%1',0);
      IF FINDSET THEN
        REPEAT
          EntrySummary := Rec;
          EntrySummary.INSERT;
        UNTIL NEXT = 0;
    END;

    LOCAL PROCEDURE DrillDownEntries@7(FieldNumber@1001 : Integer);
    VAR
      TempReservEntry2@1000 : TEMPORARY Record 337;
    BEGIN
      TempReservEntry.RESET;
      TempReservEntry.SETCURRENTKEY(
        "Item No.","Source Type","Source Subtype","Reservation Status",
        "Location Code","Variant Code","Shipment Date","Expected Receipt Date","Serial No.","Lot No.");

      TempReservEntry.SETRANGE("Lot No.","Lot No.");
      IF "Serial No." <> '' THEN
        TempReservEntry.SETRANGE("Serial No.","Serial No.");

      CASE FieldNumber OF
        FIELDNO("Total Quantity"):
          BEGIN
            // An Item Ledger Entry will in itself be represented with a surplus TempReservEntry. Order tracking
            // and reservations against Item Ledger Entries are therefore kept out, as these quantities would
            // otherwise be represented twice in the drill down.

            TempReservEntry.SETRANGE(Positive,TRUE);
            TempReservEntry2.COPY(TempReservEntry);  // Copy key
            IF TempReservEntry.FINDSET THEN
              REPEAT
                TempReservEntry2 := TempReservEntry;
                IF TempReservEntry."Source Type" = DATABASE::"Item Ledger Entry" THEN BEGIN
                  IF TempReservEntry."Reservation Status" = TempReservEntry."Reservation Status"::Surplus THEN
                    TempReservEntry2.INSERT;
                END ELSE
                  TempReservEntry2.INSERT;
              UNTIL TempReservEntry.NEXT = 0;
            TempReservEntry2.ASCENDING(FALSE);
            PAGE.RUNMODAL(PAGE::"Avail. - Item Tracking Lines",TempReservEntry2);
          END;
        FIELDNO("Total Requested Quantity"):
          BEGIN
            TempReservEntry.SETRANGE(Positive,FALSE);
            TempReservEntry.ASCENDING(FALSE);
            PAGE.RUNMODAL(PAGE::"Avail. - Item Tracking Lines",TempReservEntry);
          END;
      END;
    END;

    LOCAL PROCEDURE DrillDownBinContent@11(FieldNumber@1000 : Integer);
    VAR
      BinContent@1001 : Record 7302;
    BEGIN
      IF CurrBinCode = '' THEN
        EXIT;
      TempReservEntry.RESET;
      IF NOT TempReservEntry.FINDFIRST THEN
        EXIT;

      CurrItemTrackingCode.TESTFIELD(Code);

      BinContent.SETRANGE("Location Code",TempReservEntry."Location Code");
      BinContent.SETRANGE("Item No.",TempReservEntry."Item No.");
      BinContent.SETRANGE("Variant Code",TempReservEntry."Variant Code");
      IF CurrItemTrackingCode."Lot Warehouse Tracking" THEN
        IF "Lot No." <> '' THEN
          BinContent.SETRANGE("Lot No. Filter","Lot No.");
      IF CurrItemTrackingCode."SN Warehouse Tracking" THEN
        IF "Serial No." <> '' THEN
          BinContent.SETRANGE("Serial No. Filter","Serial No.");

      IF FieldNumber = FIELDNO("Bin Content") THEN
        BinContent.SETRANGE("Bin Code",CurrBinCode);

      PAGE.RUNMODAL(PAGE::"Bin Content",BinContent);
    END;

    LOCAL PROCEDURE UpdateIfFiltersHaveChanged@9();
    BEGIN
      // In order to update Selected Quantity when filters have been changed on the form.
      IF GETFILTERS = xFilterRec.GETFILTERS THEN
        EXIT;

      UpdateSelectedQuantity;
      xFilterRec.COPY(Rec);
    END;

    LOCAL PROCEDURE SelectedQuantityOnAfterValidat@3707();
    BEGIN
      UpdateSelectedQuantity;
      CurrPage.UPDATE;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSetCurrentBinAndItemTrkgCode@6(VAR CurrBinCode@1000 : Code[20];VAR CurrItemTrackingCode@1001 : Record 6502;VAR BinContentVisible@1002 : Boolean;VAR EntrySummary@1003 : Record 338;VAR ReservationEntry@1004 : Record 337);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeAutoSelectTrackingNo@13(VAR EntrySummary@1000 : Record 338;VAR MaxQuantity@1001 : Decimal;VAR IsHandled@1002 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

