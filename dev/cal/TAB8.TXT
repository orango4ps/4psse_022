OBJECT Table 8 Language
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,NAVDK;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Language;
               NOR=Spr†k;
               SVE=Spr†k];
    LookupPageID=Page9;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Text50        ;CaptionML=[ENU=Name;
                                                              NOR=Navn;
                                                              SVE=Namn] }
    { 6   ;   ;Windows Language ID ;Integer       ;TableRelation="Windows Language";
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Windows Language Name");
                                                              END;

                                                   CaptionML=[ENU=Windows Language ID;
                                                              NOR=ID for Windows-spr†k;
                                                              SVE=Windows spr†k-ID];
                                                   BlankZero=Yes }
    { 7   ;   ;Windows Language Name;Text80       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Windows Language".Name WHERE (Language ID=FIELD(Windows Language ID)));
                                                   CaptionML=[ENU=Windows Language Name;
                                                              NOR=Navn p† Windows-spr†k;
                                                              SVE=Windows spr†kbeteckning];
                                                   Editable=No }
    { 13600;  ;OIOUBL Country/Region Code;Code10  ;CaptionML=ENU=OIOUBL Country/Region Code;
                                                   Description=NAVDK }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;Brick               ;Name                                     }
  }
  CODE
  {

    [External]
    PROCEDURE GetUserLanguage@1() : Code[10];
    VAR
      UserLanguageId@1000 : Integer;
      Handled@1001 : Boolean;
    BEGIN
      OnGetUserLanguageId(UserLanguageId,Handled);

      IF NOT Handled THEN
        UserLanguageId := GLOBALLANGUAGE;

      EXIT(GetLanguageCode(UserLanguageId));
    END;

    [External]
    PROCEDURE GetLanguageID@2(LanguageCode@1000 : Code[10]) : Integer;
    VAR
      IsHandled@1001 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnGetLanguageID(LanguageCode,"Windows Language ID",IsHandled);
      IF IsHandled THEN BEGIN
        TESTFIELD("Windows Language ID");
        EXIT("Windows Language ID");
      END;

      CLEAR(Rec);
      IF LanguageCode <> '' THEN
        IF GET(LanguageCode) THEN
          EXIT("Windows Language ID");

      "Windows Language ID" := GLOBALLANGUAGE;
      EXIT("Windows Language ID");
    END;

    [External]
    PROCEDURE GetUserSelectedLanguageId@4() : Integer;
    VAR
      UserPersonalization@1000 : Record 2000000073;
      BlankGuid@1001 : GUID;
      LanguageId@1002 : Integer;
    BEGIN
      UserPersonalization.SETRANGE("User ID",USERID);
      IF NOT UserPersonalization.FINDFIRST THEN BEGIN
        UserPersonalization.SETRANGE("User ID",BlankGuid);
        IF NOT UserPersonalization.FINDFIRST THEN;
      END;

      LanguageId := UserPersonalization."Language ID";
      IF LanguageId = 0 THEN
        LanguageId := GLOBALLANGUAGE;

      EXIT(LanguageId);
    END;

    LOCAL PROCEDURE GetLanguageCode@7(LanguageId@1000 : Integer) : Code[10];
    BEGIN
      CLEAR(Rec);
      SETRANGE("Windows Language ID",LanguageId);
      IF FINDFIRST THEN;
      SETRANGE("Windows Language ID");
      EXIT(Code);
    END;

    [Integration]
    LOCAL PROCEDURE OnGetLanguageID@5(VAR LanguageCode@1000 : Code[10];VAR LanguageID@1001 : Integer;VAR IsHandled@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnGetUserLanguageId@3(VAR UserLanguageId@1000 : Integer;VAR Handled@1001 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

