OBJECT Table 11012968 KOMO Installation by Plot
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               UpdatePlotMeasureEquip1();
             END;

    OnModify=BEGIN
               UpdatePlotMeasureEquip1();
             END;

    OnDelete=VAR
               lvPlotDeviationRec@1210190000 : Record 11012969;
               lvPlotMeasureEquipRec@1210190001 : Record 11012970;
               lvPlotDevActionRec@1210190002 : Record 11012973;
             BEGIN
               lvPlotDevActionRec.SETRANGE("Project No.", "Project No.");
               lvPlotDevActionRec.SETRANGE("Plot No.", "Plot No.");
               lvPlotDevActionRec.SETRANGE("Line No.", "Line No.");
               lvPlotDevActionRec.DELETEALL;

               lvPlotDeviationRec.SETRANGE("Project No.", "Project No.");
               lvPlotDeviationRec.SETRANGE("Plot No.", "Plot No.");
               lvPlotDeviationRec.SETRANGE("Line No.", "Line No.");
               lvPlotDeviationRec.DELETEALL;

               lvPlotMeasureEquipRec.SETRANGE("Project No.", "Project No.");
               lvPlotMeasureEquipRec.SETRANGE("Plot No.", "Plot No.");
               lvPlotMeasureEquipRec.SETRANGE("Line No.", "Line No.");
               lvPlotMeasureEquipRec.DELETEALL;
             END;

    CaptionML=[ENU=KOMO Installation by Plot;
               NOR=KOMO-installasjon etter tegning;
               SVE=KOMO installation per ritning];
    LookupPageID=Page11012970;
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr];
                                                   NotBlank=Yes }
    { 20  ;   ;Plot No.            ;Code10        ;TableRelation=Plot."Plot No." WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=[ENU=Plot No.;
                                                              NOR=Tegningnr;
                                                              SVE=Ritningnr];
                                                   NotBlank=Yes }
    { 30  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 40  ;   ;Discipline          ;Code10        ;TableRelation="KOMO Discipline";
                                                   OnValidate=BEGIN
                                                                IF Installation <> '' THEN BEGIN
                                                                  VALIDATE(Installation);
                                                                  VALIDATE(Inspection);
                                                                END;

                                                                CALCFIELDS("Discipline Description");
                                                              END;

                                                   CaptionML=[ENU=Discipline;
                                                              NOR=Disiplin;
                                                              SVE=Disciplin] }
    { 41  ;   ;Discipline Description;Text50      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("KOMO Discipline".Description WHERE (Code=FIELD(Discipline)));
                                                   CaptionML=[ENU=Discipline Description;
                                                              NOR=Disiplinbeskrivelse;
                                                              SVE=Disciplinbeskrivning];
                                                   Editable=No }
    { 50  ;   ;Installation        ;Code10        ;TableRelation="KOMO Installation".Code WHERE (Discipline=FIELD(Discipline));
                                                   OnValidate=BEGIN
                                                                VALIDATE(Inspection);
                                                                IF Inspector <> '' THEN
                                                                  VALIDATE(Inspector);

                                                                CALCFIELDS("Installation Description");
                                                              END;

                                                   CaptionML=[ENU=Installation;
                                                              NOR=Installasjon;
                                                              SVE=Installation] }
    { 51  ;   ;Installation Description;Text50    ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("KOMO Installation".Description WHERE (Code=FIELD(Installation)));
                                                   CaptionML=[ENU=Installation Description;
                                                              NOR=Installasjonsbeskrivelse;
                                                              SVE=Installationsbeskrivning];
                                                   Editable=No }
    { 60  ;   ;Inspection          ;Code10        ;TableRelation="KOMO Inspection by Instal.".Inspection WHERE (Installation=FIELD(Installation));
                                                   OnValidate=BEGIN
                                                                IF Inspection <> '' THEN BEGIN
                                                                  IF TblCombinationAlreadyPresent(Rec) THEN
                                                                    ERROR(Text001, FIELDCAPTION(Discipline), FIELDCAPTION(Installation), FIELDCAPTION(Inspection));
                                                                END;

                                                                CALCFIELDS("Inspection Description","Final Inspection");
                                                                IF (NOT "Final Inspection") AND ("Date Released" <> 0D) THEN
                                                                  "Date Released" := 0D;
                                                              END;

                                                   CaptionML=[ENU=Inspection;
                                                              NOR=Inspeksjon;
                                                              SVE=Inspektion] }
    { 61  ;   ;Inspection Description;Text50      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("KOMO Inspection".Description WHERE (Code=FIELD(Inspection)));
                                                   CaptionML=[ENU=Inspection Description;
                                                              NOR=Inspeksjonsbeskrivelse;
                                                              SVE=Beskrivning Inspektion];
                                                   Editable=No }
    { 62  ;   ;Final Inspection    ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("KOMO Inspection"."Final Inspection" WHERE (Code=FIELD(Inspection)));
                                                   CaptionML=[ENU=Final Inspection;
                                                              NOR=Sluttinspeksjon;
                                                              SVE=Slutinspektion];
                                                   Editable=No }
    { 70  ;   ;Inspector           ;Code20        ;TableRelation="Authorized Inspector/ Instal.".Inspector WHERE (Installation=FIELD(Installation));
                                                   OnValidate=BEGIN
                                                                CheckEmployed();
                                                                CALCFIELDS("Inspector Name");
                                                              END;

                                                   CaptionML=[ENU=Inspector;
                                                              NOR=Inspekt›r;
                                                              SVE=Inspekt”r] }
    { 71  ;Yes;Inspector Name      ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Full Name" WHERE (No.=FIELD(Inspector)));
                                                   CaptionML=[ENU=Inspector Name;
                                                              NOR=Inspekt›rens navn;
                                                              SVE=Inspekt”rens namn];
                                                   Editable=No }
    { 80  ;   ;Inspection Date     ;Date          ;OnValidate=BEGIN
                                                                CheckEmployed();

                                                                IF ("Inspection Date" = 0D) AND ("Date Released" <> 0D) THEN
                                                                  "Date Released" := 0D;
                                                              END;

                                                   CaptionML=[ENU=Inspection Date;
                                                              NOR=Inspeksjonsdato;
                                                              SVE=Inspektionsdatum] }
    { 90  ;   ;Date Released       ;Date          ;OnValidate=BEGIN
                                                                TESTFIELD("Inspection Date");
                                                                CALCFIELDS("Final Inspection");
                                                                TESTFIELD("Final Inspection");
                                                              END;

                                                   CaptionML=[ENU=Date Released;
                                                              NOR=Dato frigitt;
                                                              SVE=Datum sl„ppt] }
    { 100 ;   ;Deviations          ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Deviation by Plot/Installation" WHERE (Project No.=FIELD(Project No.),
                                                                                                             Plot No.=FIELD(Plot No.),
                                                                                                             Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=Deviations;
                                                              NOR=Avvik;
                                                              SVE=Avvikelser];
                                                   Editable=No }
    { 101 ;   ;Accepted Deviations ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Deviation by Plot/Installation" WHERE (Project No.=FIELD(Project No.),
                                                                                                             Plot No.=FIELD(Plot No.),
                                                                                                             Line No.=FIELD(Line No.),
                                                                                                             Date Accepted=FILTER(1000-01-01..)));
                                                   CaptionML=[ENU=Accepted Deviations;
                                                              NOR=Aksepterte avvik;
                                                              SVE=Godk„nda avvikelser];
                                                   Editable=No }
    { 110 ;   ;Measure Equipment   ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Measuring Equip. by Plot/Inst." WHERE (Project No.=FIELD(Project No.),
                                                                                                             Plot No.=FIELD(Plot No.),
                                                                                                             Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=Measure Equipment;
                                                              NOR=M†leredskap;
                                                              SVE=M„tutrustning];
                                                   Editable=No }
    { 111 ;   ;No. of Meas. Equip. ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Measuring Equip. by Plot/Inst." WHERE (Project No.=FIELD(Project No.),
                                                                                                             Plot No.=FIELD(Plot No.),
                                                                                                             Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=No. of Meas. Equip.;
                                                              NOR=Antall m†leredskaper;
                                                              SVE=Antal m„tutrustningar];
                                                   Editable=No }
    { 120 ;   ;Measuring Equip. 1  ;Code10        ;TableRelation="Measuring Eqp. by Installation"."Measuring Equip." WHERE (Installation=FIELD(Installation));
                                                   CaptionML=[ENU=Measuring Equipment 1;
                                                              NOR=M†leredskap 1;
                                                              SVE=M„tutrustning 1] }
  }
  KEYS
  {
    {    ;Project No.,Plot No.,Line No.           ;Clustered=Yes }
    {    ;Project No.,Plot No.,Discipline,Installation,Inspection }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1210190000 : TextConst 'ENU=Combination ''%1-%2-%3'' is already present for this Plot.;NOR=KOmbinasjonen ''%1-%2-%3'' finnes allerede for denne tegningen.;SVE=Kombination ''%1-%2-%3'' finns redan f”r denna tomt.';
      Text002@1210190002 : TextConst 'ENU=Employee inactive since %1;NOR=Ansatt inaktiv siden %1;SVE=Anst„lld har varit inaktiv sedan %1';
      Text003@1210190001 : TextConst 'ENU=Employee not employed since %1;NOR=Ansatt ikke ansatt siden %1;SVE=Anst„lld har inte varit anst„lld sedan %1';

    PROCEDURE CheckEmployed@1210190000();
    VAR
      lvEmplRec@1210190000 : Record 5200;
    BEGIN
      IF (Inspector = '') OR ("Inspection Date" = 0D) THEN
        EXIT;
      IF NOT lvEmplRec.GET(Inspector) THEN
        EXIT;

      IF (lvEmplRec."Termination Date" < "Inspection Date") AND (lvEmplRec."Termination Date" <> 0D) THEN
        ERROR(Text003, lvEmplRec."Termination Date")
      ELSE BEGIN
        IF (((Inspector <> xRec.Inspector) AND (CurrFieldNo = FIELDNO(Inspector))) OR
            (("Inspection Date" <> xRec."Inspection Date") AND (CurrFieldNo = FIELDNO("Inspection Date")))) AND
           (lvEmplRec."Inactive Date" < "Inspection Date") AND (lvEmplRec."Inactive Date" <> 0D)
        THEN
          MESSAGE(Text002, lvEmplRec."Inactive Date");
      END;
    END;

    PROCEDURE TblCombinationAlreadyPresent@1210190017(IPlotInstallationRec@1210190001 : Record 11012968) : Boolean;
    VAR
      lvPlotInstallationRec@1210190000 : Record 11012968;
    BEGIN
      lvPlotInstallationRec.SETCURRENTKEY("Project No.","Plot No.",Discipline,Installation,Inspection);
      lvPlotInstallationRec.SETRANGE("Project No.", IPlotInstallationRec."Project No.");
      lvPlotInstallationRec.SETRANGE("Plot No.", IPlotInstallationRec."Plot No.");
      lvPlotInstallationRec.SETRANGE(Discipline, IPlotInstallationRec.Discipline);
      lvPlotInstallationRec.SETRANGE(Installation, IPlotInstallationRec.Installation);
      lvPlotInstallationRec.SETRANGE(Inspection, IPlotInstallationRec.Inspection);
      lvPlotInstallationRec.SETFILTER("Line No.", '<>%1', IPlotInstallationRec."Line No.");
      IF lvPlotInstallationRec.FIND('-') THEN
        EXIT(TRUE);

      EXIT(FALSE);
    END;

    PROCEDURE UpdatePlotMeasureEquip1@1210190003();
    VAR
      lvPlotMeasureEquipRec@1210190000 : Record 11012970;
    BEGIN
      CALCFIELDS("No. of Meas. Equip.", "Measure Equipment");
      IF ("No. of Meas. Equip." > 1) OR (("No. of Meas. Equip." = 0) AND ("Measuring Equip. 1" = '')) THEN
        EXIT;

      IF ("No. of Meas. Equip." = 1) THEN BEGIN
        lvPlotMeasureEquipRec.SETRANGE("Project No.", "Project No.");
        lvPlotMeasureEquipRec.SETRANGE("Plot No.", "Plot No.");
        lvPlotMeasureEquipRec.SETRANGE("Line No.", "Line No.");
        IF lvPlotMeasureEquipRec.FIND('-') THEN
          IF (lvPlotMeasureEquipRec."Measuring Equip." = "Measuring Equip. 1") THEN
            EXIT
          ELSE
            lvPlotMeasureEquipRec.DELETE;
      END;

      IF ("Measuring Equip. 1" = '') THEN
        EXIT;

      lvPlotMeasureEquipRec.INIT;
      lvPlotMeasureEquipRec."Project No." := "Project No.";
      lvPlotMeasureEquipRec."Plot No." := "Plot No.";
      lvPlotMeasureEquipRec."Line No." := "Line No.";
      lvPlotMeasureEquipRec."Measuring Equip." := "Measuring Equip. 1";
      lvPlotMeasureEquipRec.Installation := Installation;
      lvPlotMeasureEquipRec.INSERT;
    END;

    PROCEDURE EditDocuments@1100528702(DocumentTemplateTypeFilter@1100528704 : Text[250];ContactNo@1210190003 : Code[20];ContactPersonNo@1210190004 : Code[20];DocumentAction@1100528701 : Code[20];SendMail@1210190002 : Boolean;Recipient@1100528700 : Text[120]);
    VAR
      Job@1100528702 : Record 11072003;
      Plot@1100528703 : Record 11012500;
      DocumentMailing@1210190000 : Codeunit 11012278;
      RecRef@1100525000 : RecordRef;
      DocumentLinkRIDs@1100525001 : ARRAY [5] OF RecordID;
    BEGIN
      RecRef.GETTABLE(Rec);
      DocumentLinkRIDs[1] := RecRef.RECORDID;

      Job.GET("Project No.");
      RecRef.GETTABLE(Job);
      DocumentLinkRIDs[2] := RecRef.RECORDID;

      IF NOT Plot.GET("Project No.", "Plot No.") THEN
        EXIT;
      RecRef.GETTABLE(Plot);
      DocumentLinkRIDs[3] := RecRef.RECORDID;

      DocumentMailing.EditDocuments2(
        DocumentTemplateTypeFilter, DocumentLinkRIDs, '', ContactNo, ContactPersonNo, DocumentAction, SendMail, Recipient, FALSE);
    END;

    BEGIN
    END.
  }
}

