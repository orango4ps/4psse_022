OBJECT Table 11126687 Empoly Warehouse Order
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               EmpolySetup@1100525000 : Record 11126692;
               EmpolyWarehouseOrderArch@1100529900 : Record 11126689;
               NoSeriesMgt@1100525001 : Codeunit 396;
             BEGIN
               IF CURRENTCOMPANY <> COMPANYNAME THEN BEGIN
                 EmpolySetup.CHANGECOMPANY(CURRENTCOMPANY);
                 EmpolyWarehouseOrderArch.CHANGECOMPANY(CURRENTCOMPANY);
                 NoSeriesMgt.SetFromOtherCompany(CURRENTCOMPANY);
               END;

               EmpolySetup.GET;
               IF "No." = '' THEN BEGIN
                 EmpolySetup.TESTFIELD("Order No. Serie");
                 NoSeriesMgt.InitSeries(EmpolySetup."Order No. Serie",xRec."No. Series",0D,"No.","No. Series");
               END;
               TESTFIELD("No.");

               IF EmpolyWarehouseOrderArch.GET("No.") THEN
                 ERROR(Text000, "No.", EmpolyWarehouseOrderArch.TABLECAPTION);

               "Order Date" := WORKDATE;
               "Order Time" := TIME;
             END;

    OnModify=BEGIN
               TESTFIELD("Processed On", 0DT);
               IF "Scanner ID" = '' THEN
                 "User ID" := USERID;
             END;

    OnDelete=VAR
               OrderLine@1100525000 : Record 11126688;
               CommentLine@1100525001 : Record 97;
             BEGIN
               OrderLine.SETRANGE("Order No.", "No.");
               OrderLine.DELETEALL;

               CommentLine.SETRANGE("Table Name", CommentLine."Table Name"::EmpolyOrder);
               CommentLine.SETRANGE("No.", "No.");
               CommentLine.DELETEALL;
             END;

    CaptionML=ENU=4PS Warehouse App Order;
    LookupPageID=Page11126734;
    DrillDownPageID=Page11126734;
  }
  FIELDS
  {
    { 10  ;   ;No.                 ;Code20        ;CaptionML=[ENU=No.;
                                                              NOR=Nr;
                                                              SVE=Nr] }
    { 11  ;   ;No. Of Order Lines  ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Empoly Warehouse OrderLine" WHERE (Order No.=FIELD(No.)));
                                                   CaptionML=ENU=No. of Order Lines;
                                                   Editable=No }
    { 20  ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=User ID;
                                                              NOR=Bruker-ID;
                                                              SVE=Anv„ndar-ID] }
    { 30  ;   ;Scanner ID          ;Code20        ;CaptionML=[ENU=Scanner ID;
                                                              NOR=Skanner-ID;
                                                              SVE=Skanner-ID] }
    { 40  ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=ENU=Open,Finished;
                                                   OptionString=Open,Finished }
    { 50  ;   ;Order Date          ;Date          ;CaptionML=[ENU=Order Date;
                                                              NOR=Bestillingsdato;
                                                              SVE=Orderdatum] }
    { 60  ;   ;Order Time          ;Time          ;CaptionML=[ENU=Order Time;
                                                              NOR=Ordretidspunkt;
                                                              SVE=Ordertid] }
    { 70  ;   ;Process             ;Option        ;CaptionML=[ENU=Process;
                                                              NOR=Behandle;
                                                              SVE=Process];
                                                   OptionCaptionML=ENU=Project Delivery,Service Delivery,Phys. Inventory,Stock Transfer,Purch. Requisition,Project Return,Service Return,,,,Sales,Sales Return;
                                                   OptionString=Project Delivery,Service Delivery,Phys. Inventory,Stock Transfer,Purch. Requisition,Project Retour,Service Retour,,,,Sales,Sales Return }
    { 80  ;   ;Receiving Company   ;Text30        ;TableRelation=Company.Name;
                                                   CaptionML=[ENU=Receiving Company;
                                                              NOR=Mottakselskap;
                                                              SVE=Mottagningsf”retag] }
    { 90  ;   ;Project No.         ;Code20        ;TableRelation=IF (Receiving Company=FILTER('')) Job.No.;
                                                   OnValidate=BEGIN
                                                                CheckCompanyIC;
                                                                IF "Project No." <> '' THEN BEGIN
                                                                  TESTFIELD("Service Order No.", '');
                                                                  ProjRec.GET("Project No.");
                                                                END;
                                                                CALCFIELDS("Description Project");
                                                              END;

                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr] }
    { 91  ;   ;Description Project ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Job.Description WHERE (No.=FIELD(Project No.)));
                                                   CaptionML=[ENU=Description Project;
                                                              NOR=Prosjektbeskrivelse;
                                                              SVE=Projektbeskrivning];
                                                   Editable=No }
    { 100 ;   ;Service Order No.   ;Code20        ;TableRelation=IF (Receiving Company=FILTER('')) "Service Order".No.;
                                                   OnValidate=BEGIN
                                                                CheckCompanyIC;
                                                                IF "Service Order No." <> '' THEN BEGIN
                                                                  TESTFIELD("Project No.", '');
                                                                  ServOrderRec.GET("Service Order No.");
                                                                END;
                                                                CALCFIELDS("Description Service Order");
                                                              END;

                                                   CaptionML=[ENU=Service Order No.;
                                                              NOR=Serviceordrenr.;
                                                              SVE=Serviceordernr] }
    { 101 ;   ;Description Service Order;Text50   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Order".Description WHERE (No.=FIELD(Service Order No.)));
                                                   CaptionML=[ENU=Description Service Order;
                                                              NOR=Serviceordrebeskrivelse;
                                                              SVE=Serviceorderbeskrivning];
                                                   Editable=No }
    { 110 ;   ;Element             ;Code20        ;TableRelation=IF (Receiving Company=FILTER('')) "Project Element".Element WHERE (Project No.=FIELD(Project No.),
                                                                                                                                    Publish in Empoly App=CONST(Yes));
                                                   OnValidate=BEGIN
                                                                CheckCompanyIC;
                                                                IF Element <> '' THEN BEGIN
                                                                  TESTFIELD("Project No.");
                                                                  ProjElemRec.GET("Project No.", Element);
                                                                  ProjElemRec.TESTFIELD("Publish in Empoly App", TRUE);
                                                                END;
                                                                CALCFIELDS("Description Element");
                                                              END;

                                                   CaptionML=[ENU=Element;
                                                              NOR=Element;
                                                              SVE=Element] }
    { 111 ;   ;Description Element ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Element".Description WHERE (Project No.=FIELD(Project No.),
                                                                                                           Element=FIELD(Element)));
                                                   CaptionML=[ENU=Description Element;
                                                              NOR=Beskrivelse, element;
                                                              SVE=Beskrivning, element];
                                                   Editable=No }
    { 120 ;   ;Location Code       ;Code10        ;TableRelation=IF (Receiving Company=FILTER('')) Location.Code;
                                                   OnValidate=BEGIN
                                                                CheckCompanyIC;
                                                                IF "Location Code" <> '' THEN
                                                                  LocRec.GET("Location Code");
                                                              END;

                                                   CaptionML=[ENU=Location Code;
                                                              NOR=Plasseringkode;
                                                              SVE=Lagerst„llekod] }
    { 121 ;   ;Description Location;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Location.Name WHERE (Code=FIELD(Location Code)));
                                                   CaptionML=[ENU=Description Location;
                                                              NOR=Beskrivelse, lokasjon;
                                                              SVE=Beskrivning, lagerst„lle];
                                                   Editable=No }
    { 130 ;   ;To Location Code    ;Code10        ;TableRelation=Location.Code;
                                                   OnValidate=BEGIN
                                                                IF "To Location Code" <> '' THEN
                                                                   TESTFIELD(Process, Process::"Stock Transfer");
                                                              END;

                                                   CaptionML=[ENU=To Location Code;
                                                              NOR=Til lokasjonskode;
                                                              SVE=Till lagerst„lleskod] }
    { 131 ;   ;Description Location To;Text100    ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Location.Name WHERE (Code=FIELD(To Location Code)));
                                                   CaptionML=ENU=Description Location To;
                                                   Editable=No }
    { 140 ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              NOR=Nr.serie;
                                                              SVE=Nr-serie];
                                                   Editable=No }
    { 150 ;   ;Customer No.        ;Code20        ;TableRelation=Customer;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Customer Name");
                                                              END;

                                                   CaptionML=[ENU=Customer No.;
                                                              NOR=Kundenr;
                                                              SVE=Kundnr] }
    { 151 ;   ;Customer Name       ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Customer.Name WHERE (No.=FIELD(Customer No.)));
                                                   CaptionML=[ENU=Customer Name;
                                                              NOR=Kundnavn;
                                                              SVE=Betalarens namn];
                                                   Editable=No }
    { 160 ;   ;Comment Lines       ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Comment Line" WHERE (Table Name=CONST(EmpolyOrder),
                                                                                           No.=FIELD(No.)));
                                                   CaptionML=ENU=Comment Lines;
                                                   Editable=No }
    { 170 ;   ;Processed On        ;DateTime      ;CaptionML=[ENU=Processed On;
                                                              SVE=Behandlat den];
                                                   Editable=No }
    { 180 ;   ;Picker No.          ;Code20        ;TableRelation="Resource (WOP)".No. WHERE (Type=CONST(Employee));
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Order Picker }
    { 190 ;   ;Extension Contract  ;Code20        ;TableRelation=IF (Receiving Company=FILTER('')) "Extension Contract"."Contract No." WHERE (Project No.=FIELD(Project No.));
                                                   OnValidate=BEGIN
                                                                CheckCompanyIC;
                                                                IF "Extension Contract" <> '' THEN BEGIN
                                                                  TESTFIELD("Project No.");
                                                                  ExtensionContract.GET("Project No.", "Extension Contract");
                                                                END;
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Extension Contract;
                                                              SVE=ŽTA] }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      LocRec@1100525006 : Record 14;
      ProjRec@1100525005 : Record 11072003;
      ProjElemRec@1100525004 : Record 11012010;
      ServOrderRec@1100525000 : Record 11012823;
      Text000@1100529900 : TextConst 'ENU=Order %1 already exists in %2.';
      ExtensionContract@1100529600 : Record 11012004;

    PROCEDURE AutoProcessOrder@1100528300();
    BEGIN
      TESTFIELD(Status, Status::Finished);
      CreateProcessQueueEntries;
    END;

    PROCEDURE CheckCompanyIC@1210190032();
    VAR
      lvCompName@1210190000 : Text[50];
    BEGIN
      IF "Receiving Company" <> '' THEN
        lvCompName := "Receiving Company"
      ELSE
        lvCompName := CURRENTCOMPANY;

      ProjRec.CHANGECOMPANY(lvCompName);
      ProjElemRec.CHANGECOMPANY(lvCompName);
      ExtensionContract.CHANGECOMPANY(lvCompName);
      LocRec.CHANGECOMPANY(lvCompName);
      ServOrderRec.CHANGECOMPANY(lvCompName);
    END;

    PROCEDURE CreateProcessQueueEntries@1100528301();
    VAR
      GenericProcessQueue@1100528302 : Record 11229430;
      EmpolyProcessManagement@1100528301 : Codeunit 11126683;
    BEGIN
      TESTFIELD(Status, Status::Finished);
      IF (NOT GenericProcessQueue.CheckQueueExists(COMPANYNAME, RECORDID, FALSE)) THEN
        EmpolyProcessManagement.InitGenericProcessQueue(Rec);
    END;

    PROCEDURE LookupProject@1100525001();
    VAR
      EmpolyWarehouseMgt@1100528501 : Codeunit 11126681;
      FilterString@1100528500 : Text;
    BEGIN
      CheckCompanyIC;
      ProjRec."No." := "Project No.";
      FilterString := EmpolyWarehouseMgt.SetProjectFilter;
      IF (FilterString <> '') THEN
        ProjRec.SETFILTER("Project Status", FilterString);
      IF PAGE.RUNMODAL(0, ProjRec) = ACTION::LookupOK THEN
        VALIDATE("Project No.", ProjRec."No.");
    END;

    PROCEDURE LookupElement@1100525002();
    BEGIN
      CheckCompanyIC;
      ProjElemRec."Project No." := "Project No.";
      ProjElemRec.Element := Element;
      ProjElemRec.SETRANGE("Project No.", "Project No.");
      ProjElemRec.SETRANGE("Publish in Empoly App", TRUE);
      IF PAGE.RUNMODAL(0, ProjElemRec) = ACTION::LookupOK THEN
        VALIDATE(Element, ProjElemRec.Element);
    END;

    PROCEDURE LookupServiceOrder@1100525000();
    VAR
      EmpolyWarehouseMgt@1100528501 : Codeunit 11126681;
      FilterString@1100528500 : Text;
    BEGIN
      CheckCompanyIC;
      ServOrderRec."No." := "Service Order No.";
      FilterString := EmpolyWarehouseMgt.SetSOFilter;
      IF (FilterString <> '') THEN
        ServOrderRec.SETFILTER(Status, FilterString);
      IF PAGE.RUNMODAL(0, ServOrderRec) = ACTION::LookupOK THEN
        VALIDATE("Service Order No.", ServOrderRec."No.");
    END;

    BEGIN
    END.
  }
}

