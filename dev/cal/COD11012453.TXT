OBJECT Codeunit 11012453 DPT Event Log NAS JQ
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    TableNo=472;
    OnRun=BEGIN
            TESTFIELD("Parameter String");
            DPTSetup.GET;
            SetStartedFrom(3); //JobQueue
            ProcessEvents("Parameter String");
          END;

  }
  CODE
  {
    VAR
      NOTIMPLEMENTED@1100528306 : TextConst 'DEU=Diese Handlung ist nicht implementiert. Aktueller Wert: %1);ENU=This Action is not implemented. Current Value: %1);NLD=Deze actie is niet ge‹mplementeerd. (Huidige waarde: %1)';
      DPTSetup@1100525001 : Record 11229823;
      StartedFrom@1100528307 : Option;
      ImpersonationUserSID@1100528300 : Text;
      EventLogSeqNo@1100525000 : Integer;

    PROCEDURE ProcessEvents@1100528304(ParameterString@1100528302 : Text);
    VAR
      DPTEventLog@1100528300 : Record 11229822;
      Parameter@1100528304 : Text;
      Pos@1100528305 : Integer;
    BEGIN
      DPTEventLog.SETCURRENTKEY("Processed On", Action, "Rec ID");
      DPTEventLog.SETRANGE(Processing, FALSE);
      IF (DPTSetup."Max No. of Event Log Attempts" > 0) THEN
        DPTEventLog.SETFILTER("Number of Attempts", '<%1', DPTSetup."Max No. of Event Log Attempts");

      REPEAT
        Pos := STRPOS(ParameterString, ',');
        IF (Pos > 0) THEN
          Parameter := UPPERCASE(COPYSTR(ParameterString, 1, Pos - 1))
        ELSE
          Parameter := UPPERCASE(ParameterString);

        CASE Parameter OF
          UPPERCASE(DPTEventLog.GetCleanEventLogText):
            DPTEventLog.CleanupEventLog;
          ELSE BEGIN
            DPTEventLog.SETRANGE("Processed On", 0DT);
            DPTEventLog.SETFILTER(Action, '%1', '@'+Parameter);
            IF (DPTEventLog.FINDSET) THEN BEGIN
              REPEAT
                SetEventLogSeqNo(DPTEventLog."Seq No");
                SetImpersonationUser(DPTEventLog."Created By SID");
                ProcessEvent(DPTEventLog."Seq No");
              UNTIL (DPTEventLog.NEXT = 0);
            END;
          END;
        END;
        ParameterString := COPYSTR(ParameterString, Pos + 1);
      UNTIL (Pos = 0);
    END;

    PROCEDURE ProcessEvent@1100528300(SeqNo@1100528300 : Integer);
    VAR
      DPTEventLog@1100528305 : Record 11229822;
    BEGIN
      IF (NOT DPTEventLog.GET(SeqNo)) THEN
        EXIT;

      SetDPTEventLogProcessing(EventLogSeqNo, ImpersonationUserSID);
      CASE DPTEventLog.Action OF
        DPTEventLog.GetSyncAuthorityText:
          ProcessSyncAuthority(DPTEventLog);
        DPTEventLog.GetDeleteDocumentText:
          ProcessDeleteDocument(DPTEventLog);
        DPTEventLog.GetSyncPortalDataText:
          ProcessSyncPortalData(DPTEventLog);
        DPTEventLog.GetSyncPortalMetaDataText:
          ProcessSyncPortalMetaData(DPTEventLog);
        DPTEventLog.GetSyncPortalSearchDataText:
          ProcessSyncPortalSearchData(DPTEventLog);
        DPTEventLog.GetSyncPortalSiteText:
          ProcessSyncPortalSite(DPTEventLog);
        DPTEventLog.GetSyncDocumentText:
          ProcessSyncDocument(DPTEventLog);
        DPTEventLog.GetSyncResponsiblePersonText:
          ProcessSyncResponsiblePerson(DPTEventLog);
      END;
      DPTEventLog.GET(SeqNo);
      IF (DPTEventLog."Message Text" = '') THEN
        SetDPTEventLogProcessed(EventLogSeqNo, ImpersonationUserSID);
    END;

    PROCEDURE SetEventLogSeqNo@1100525003(EventLogSeqNoIn@1100528300 : Integer);
    BEGIN
      EventLogSeqNo := EventLogSeqNoIn;
    END;

    PROCEDURE SetImpersonationUser@1100528305(ImpersonationUserSIDIn@1100528300 : Text);
    BEGIN
      ImpersonationUserSID := ImpersonationUserSIDIn;
    END;

    PROCEDURE SetStartedFrom@1100528301(StartedFromIn@1100528300 : Option);
    BEGIN
      StartedFrom := StartedFromIn;
    END;

    LOCAL PROCEDURE "---Locals---"@1100528302();
    BEGIN
    END;

    LOCAL PROCEDURE ProcessDeleteDocument@1100528307(DPTEventLog@1100528300 : Record 11229822);
    VAR
      DocumentProperties@1100528305 : Record 11012746;
      Job@1100528304 : Record 11072003;
      ServiceLocation@1100528307 : Record 11012801;
      DPTEventLogParameter@1100528306 : Record 11229830;
      DPTInterfaceProject@1100528303 : Codeunit 11012956;
      DPTInterfaceServLoc@1100528308 : Codeunit 11012949;
      RecId@1100528301 : RecordID;
    BEGIN
      RecId := DPTEventLog."Rec ID";

      CASE DPTEventLog."Source Type" OF
        DPTEventLog."Source Type"::Project: BEGIN
          Job.GET(DPTEventLog."Source No");
          IF (NOT DocumentProperties.GET(RecId)) THEN
            DocumentProperties.INIT;
          DocumentProperties."Storage Type" := DocumentProperties."Storage Type"::DPTDUAL;
          DocumentProperties."Web link" := DPTEventLogParameter.GetEventLogParameter(DPTEventLog."Seq No",
            DATABASE::"Document Properties", DocumentProperties.FIELDNO("Web link"));
          DocumentProperties."Document Portal Subsite" := DPTEventLogParameter.GetEventLogParameter(DPTEventLog."Seq No",
            DATABASE::"Document Properties", DocumentProperties.FIELDNO("Document Portal Subsite"));
          SetInterfaceProjectVars(DPTInterfaceProject);
          DPTInterfaceProject.DeleteDocument(Job, DocumentProperties);
        END;
        DPTEventLog."Source Type"::ServiceLocation: BEGIN
          ServiceLocation.GET(DPTEventLog."Source No");
          IF (NOT DocumentProperties.GET(RecId)) THEN
            DocumentProperties.INIT;
          DocumentProperties."Storage Type" := DocumentProperties."Storage Type"::DPTDUAL;
          DocumentProperties."Web link" := DPTEventLogParameter.GetEventLogParameter(DPTEventLog."Seq No",
            DATABASE::"Document Properties", DocumentProperties.FIELDNO("Web link"));
          DocumentProperties."Document Portal Subsite" := DPTEventLogParameter.GetEventLogParameter(DPTEventLog."Seq No",
            DATABASE::"Document Properties", DocumentProperties.FIELDNO("Document Portal Subsite"));
          SetInterfaceServiceVars(DPTInterfaceServLoc);
          DPTInterfaceServLoc.DeleteDocument(ServiceLocation, DocumentProperties);
        END;
        ELSE
          ERROR(NOTIMPLEMENTED, DPTEventLog.Action);
      END;
    END;

    LOCAL PROCEDURE ProcessSyncAuthority@1100528303(DPTEventLog@1100528300 : Record 11229822);
    VAR
      Job@1100528304 : Record 11072003;
      ServiceLocation@1100528307 : Record 11012801;
      DPTEventLogParameter@1100528301 : Record 11229830;
      ProjectAuthority@1100528306 : Record 11020216;
      DPTInterfaceProject@1100528303 : Codeunit 11012956;
      DPTInterfaceServLoc@1100528308 : Codeunit 11012949;
    BEGIN
      CASE DPTEventLog."Source Type" OF
        DPTEventLog."Source Type"::Project: BEGIN
          Job.GET(DPTEventLog."Source No");
          SetInterfaceProjectVars(DPTInterfaceProject);
          DPTEventLogParameter.SETRANGE("Seq No", DPTEventLog."Seq No");
          IF (DPTEventLogParameter.FINDFIRST) THEN BEGIN
            DPTInterfaceProject.SyncAuthority(Job,
              DPTEventLogParameter.GetEventLogParameter(DPTEventLog."Seq No", DATABASE::"Project Authority",
                ProjectAuthority.FIELDNO("Contact No.")),
              DPTEventLogParameter.GetEventLogParameter(DPTEventLog."Seq No", DATABASE::"Project Authority",
                ProjectAuthority.FIELDNO("Contact Person No.")));
          END ELSE
            DPTInterfaceProject.SyncAuthorities(Job);
        END;
        DPTEventLog."Source Type"::ServiceLocation: BEGIN
          ServiceLocation.GET(DPTEventLog."Source No");
          SetInterfaceServiceVars(DPTInterfaceServLoc);
          DPTEventLogParameter.SETRANGE("Seq No", DPTEventLog."Seq No");
          IF (DPTEventLogParameter.FINDFIRST) THEN BEGIN
            DPTInterfaceServLoc.SyncAuthority(ServiceLocation,
              DPTEventLogParameter.GetEventLogParameter(DPTEventLog."Seq No", DATABASE::"Service Authority DPT",
                ProjectAuthority.FIELDNO("Contact No.")),
              DPTEventLogParameter.GetEventLogParameter(DPTEventLog."Seq No", DATABASE::"Service Authority DPT",
                ProjectAuthority.FIELDNO("Contact Person No.")));
          END ELSE
            DPTInterfaceServLoc.SyncAuthorities(ServiceLocation);
        END;
        ELSE
          ERROR(NOTIMPLEMENTED, DPTEventLog.Action);
      END;
    END;

    LOCAL PROCEDURE ProcessSyncDocument@1100528311(DPTEventLog@1100528300 : Record 11229822);
    VAR
      DocumentProperties@1100528305 : Record 11012746;
      Job@1100528304 : Record 11072003;
      ServiceLocation@1100528302 : Record 11012801;
      DPTInterfaceProject@1100528303 : Codeunit 11012956;
      DPTInterfaceServLoc@1100528306 : Codeunit 11012949;
      RecId@1100528301 : RecordID;
    BEGIN
      RecId := DPTEventLog."Rec ID";

      CASE DPTEventLog."Source Type" OF
        DPTEventLog."Source Type"::Project: BEGIN
          Job.GET(DPTEventLog."Source No");
          IF (NOT DocumentProperties.GET(RecId)) THEN
            EXIT;
          SetInterfaceProjectVars(DPTInterfaceProject);
          DPTInterfaceProject.SyncDocument(Job, DocumentProperties);
        END;
        DPTEventLog."Source Type"::ServiceLocation: BEGIN
          ServiceLocation.GET(DPTEventLog."Source No");
          IF (NOT DocumentProperties.GET(RecId)) THEN
            EXIT;
          SetInterfaceServiceVars(DPTInterfaceServLoc);
          DPTInterfaceServLoc.SyncDocument(ServiceLocation, DocumentProperties);
        END;
        ELSE
          ERROR(NOTIMPLEMENTED, DPTEventLog.Action);
      END;
    END;

    LOCAL PROCEDURE ProcessSyncPortalData@1100528312(DPTEventLog@1100528300 : Record 11229822);
    VAR
      Job@1100528304 : Record 11072003;
      ServiceLocation@1100528301 : Record 11012801;
      DPTInterfaceProject@1100528303 : Codeunit 11012956;
      DPTInterfaceServLoc@1100528302 : Codeunit 11012949;
    BEGIN
      CASE DPTEventLog."Source Type" OF
        DPTEventLog."Source Type"::Project: BEGIN
          Job.GET(DPTEventLog."Source No");
          SetInterfaceProjectVars(DPTInterfaceProject);
          DPTInterfaceProject.SyncPortalData(Job);
        END;
        DPTEventLog."Source Type"::ServiceLocation: BEGIN
          ServiceLocation.GET(DPTEventLog."Source No");
          SetInterfaceServiceVars(DPTInterfaceServLoc);
          DPTInterfaceServLoc.SyncPortalData(ServiceLocation);
        END;
        ELSE
          ERROR(NOTIMPLEMENTED, DPTEventLog.Action);
      END;
    END;

    LOCAL PROCEDURE ProcessSyncPortalMetaData@1100528313(DPTEventLog@1100528300 : Record 11229822);
    VAR
      Job@1100528304 : Record 11072003;
      ServiceLocation@1100528301 : Record 11012801;
      DPTInterfaceProject@1100528303 : Codeunit 11012956;
      DPTInterfaceServLoc@1100528302 : Codeunit 11012949;
    BEGIN
      CASE DPTEventLog."Source Type" OF
        DPTEventLog."Source Type"::Project: BEGIN
          Job.GET(DPTEventLog."Source No");
          SetInterfaceProjectVars(DPTInterfaceProject);
          DPTInterfaceProject.SyncPortalMetaData(Job);
        END;
        DPTEventLog."Source Type"::ServiceLocation: BEGIN
          ServiceLocation.GET(DPTEventLog."Source No");
          SetInterfaceServiceVars(DPTInterfaceServLoc);
          DPTInterfaceServLoc.SyncPortalMetaData(ServiceLocation);
        END;
        ELSE
          ERROR(NOTIMPLEMENTED, DPTEventLog.Action);
      END;
    END;

    LOCAL PROCEDURE ProcessSyncPortalSearchData@1100528308(DPTEventLog@1100528300 : Record 11229822);
    VAR
      Job@1100528304 : Record 11072003;
      ServiceLocation@1100528301 : Record 11012801;
      DPTInterfaceProject@1100528303 : Codeunit 11012956;
      DPTInterfaceServLoc@1100528302 : Codeunit 11012949;
    BEGIN
      CASE DPTEventLog."Source Type" OF
        DPTEventLog."Source Type"::Project: BEGIN
          Job.GET(DPTEventLog."Source No");
          SetInterfaceProjectVars(DPTInterfaceProject);
          DPTInterfaceProject.SyncPortalSearchData(Job);
        END;
        DPTEventLog."Source Type"::ServiceLocation: BEGIN
          ServiceLocation.GET(DPTEventLog."Source No");
          SetInterfaceServiceVars(DPTInterfaceServLoc);
          DPTInterfaceServLoc.SyncPortalSearchData(ServiceLocation);
        END;
        ELSE
          ERROR(NOTIMPLEMENTED, DPTEventLog.Action);
      END;
    END;

    LOCAL PROCEDURE ProcessSyncPortalSite@1100528306(DPTEventLog@1100528300 : Record 11229822);
    VAR
      Job@1100528304 : Record 11072003;
      ServiceLocation@1100528301 : Record 11012801;
      DPTInterfaceProject@1100528303 : Codeunit 11012956;
      DPTInterfaceServLoc@1100528302 : Codeunit 11012949;
    BEGIN
      CASE DPTEventLog."Source Type" OF
        DPTEventLog."Source Type"::Project: BEGIN
          Job.GET(DPTEventLog."Source No");
          SetInterfaceProjectVars(DPTInterfaceProject);
          DPTInterfaceProject.SyncPortalSite(Job);
        END;
        DPTEventLog."Source Type"::ServiceLocation: BEGIN
          ServiceLocation.GET(DPTEventLog."Source No");
          SetInterfaceServiceVars(DPTInterfaceServLoc);
          DPTInterfaceServLoc.SyncPortalSite(ServiceLocation);
        END;
        ELSE
          ERROR(NOTIMPLEMENTED, DPTEventLog.Action);
      END;
    END;

    LOCAL PROCEDURE ProcessSyncResponsiblePerson@1100528309(DPTEventLog@1100528300 : Record 11229822);
    VAR
      Job@1100528304 : Record 11072003;
      ServiceLocation@1100528301 : Record 11012801;
      DPTInterfaceProject@1100528303 : Codeunit 11012956;
      DPTInterfaceServLoc@1100528302 : Codeunit 11012949;
    BEGIN
      CASE DPTEventLog."Source Type" OF
        DPTEventLog."Source Type"::Project: BEGIN
          Job.GET(DPTEventLog."Source No");
          SetInterfaceProjectVars(DPTInterfaceProject);
          DPTInterfaceProject.SyncResponsiblePersons(Job);
        END;
        DPTEventLog."Source Type"::ServiceLocation: BEGIN
          ServiceLocation.GET(DPTEventLog."Source No");
          SetInterfaceServiceVars(DPTInterfaceServLoc);
          DPTInterfaceServLoc.SyncResponsiblePersons(ServiceLocation);
        END;
        ELSE
          ERROR(NOTIMPLEMENTED, DPTEventLog.Action);
      END;
    END;

    LOCAL PROCEDURE SetDPTEventLogProcessing@1100525007(SeqNoIn@1100525001 : Integer;UserSIDIn@1100525000 : GUID);
    VAR
      DPTEventLog@1100528301 : Record 11229822;
    BEGIN
      DPTEventLog.GET(SeqNoIn);
      DPTEventLog."Message Text" := '';
      DPTEventLog.Processing := TRUE;
      DPTEventLog."Modified By SID" := UserSIDIn;
      DPTEventLog."Modified On" := CURRENTDATETIME;
      CLEAR(DPTEventLog."Processed By SID");
      DPTEventLog."Processed On" := 0DT;
      DPTEventLog."Number of Attempts" += 1;
      DPTEventLog.MODIFY(TRUE);
      COMMIT;
    END;

    LOCAL PROCEDURE SetDPTEventLogProcessed@1100525006(SeqNoIn@1100525001 : Integer;UserSIDIn@1100525000 : GUID);
    VAR
      DPTEventLog@1100528301 : Record 11229822;
    BEGIN
      DPTEventLog.GET(SeqNoIn);
      DPTEventLog."Message Text" := '';
      DPTEventLog.Processing := FALSE;
      DPTEventLog."Processed By SID" := UserSIDIn;
      DPTEventLog."Processed On" := CURRENTDATETIME;
      DPTEventLog.MODIFY(TRUE);
      COMMIT;
    END;

    LOCAL PROCEDURE SetInterfaceProjectVars@1100528310(VAR DPTInterfaceProject@1100528300 : Codeunit 11012956);
    BEGIN
      DPTInterfaceProject.SetImpersonationUser(ImpersonationUserSID);
      DPTInterfaceProject.SetStartedFrom(StartedFrom);
      DPTInterfaceProject.SetEventLogSeqNo(EventLogSeqNo);
    END;

    LOCAL PROCEDURE SetInterfaceServiceVars@1100528315(VAR DPTInterfaceServLoc@1100528300 : Codeunit 11012949);
    BEGIN
      DPTInterfaceServLoc.SetImpersonationUser(ImpersonationUserSID);
      DPTInterfaceServLoc.SetStartedFrom(StartedFrom);
      DPTInterfaceServLoc.SetEventLogSeqNo(EventLogSeqNo);
    END;

    BEGIN
    END.
  }
}

