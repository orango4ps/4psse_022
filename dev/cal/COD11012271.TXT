OBJECT Codeunit 11012271 Option Field Tools
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE ReadableOptionToOptionValue@1100528700(OptionString@1100528701 : Text;ReadableOption@1100528703 : Text;VAR OptionValue@1100528702 : Integer) : Boolean;
    VAR
      I@1100528704 : Integer;
      P@1100528700 : Integer;
    BEGIN
      OptionValue := -1;

      I := -1;
      P := STRPOS(OptionString, ',');
      WHILE P > 0 DO BEGIN
        I += 1;
        IF UPPERCASE(ReadableOption) = UPPERCASE(COPYSTR(OptionString, 1, P - 1)) THEN BEGIN
          OptionValue := I;
          EXIT(TRUE);
        END;
        OptionString := COPYSTR(OptionString, P + 1);
        P := STRPOS(OptionString, ',');
      END;
      IF (OptionString <> '') AND (UPPERCASE(OptionString) = UPPERCASE(ReadableOption)) THEN BEGIN
        I += 1;
        OptionValue := I;
        EXIT(TRUE);
      END;
    END;

    PROCEDURE ReadableOptionToOptionValue2@1100528703(FldRef@1100528701 : FieldRef;ReadableOption@1100528703 : Text[250];VAR OptionValue@1100528700 : Integer) : Boolean;
    VAR
      FieldRec@1100528707 : Record 2000000041;
      RecRef@1100528702 : RecordRef;
    BEGIN
      RecRef := FldRef.RECORD;
      FieldRec.GET(RecRef.NUMBER, FldRef.NUMBER);
      IF FieldRec.Type = FieldRec.Type::Option THEN
        EXIT(ReadableOptionToOptionValue(FldRef.OPTIONCAPTION, ReadableOption, OptionValue));
    END;

    PROCEDURE OptionValueToReadableOption@1100528701(OptionString@1100528701 : Text;OptionValue@1100528702 : Integer;VAR ReadableOption@1100528700 : Text) : Boolean;
    VAR
      StringFunctions@1100528300 : Codeunit 11012273;
      ListArray@1100528301 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.ArrayList";
    BEGIN
      ReadableOption := '';

      StringFunctions.SplitString2(OptionString, ',', ListArray);
      ReadableOption := ListArray.Item(OptionValue);
      IF (ReadableOption <> '') THEN
        EXIT(TRUE);
    END;

    PROCEDURE OptionValueToReadableOption2@1100528702(FldRef@1100528701 : FieldRef;VAR ReadableOption@1100528700 : Text[250]) : Boolean;
    VAR
      FieldRec@1100528707 : Record 2000000041;
      RecRef@1100528702 : RecordRef;
      OptionValue@1100528705 : Integer;
    BEGIN
      RecRef := FldRef.RECORD;
      FieldRec.GET(RecRef.NUMBER, FldRef.NUMBER);
      IF FieldRec.Type = FieldRec.Type::Option THEN BEGIN
        OptionValue := FldRef.VALUE;
        EXIT(OptionValueToReadableOption(FldRef.OPTIONCAPTION, OptionValue, ReadableOption));
      END;
    END;

    PROCEDURE OptionValueToReadableOption3@1100528333(TableNo@1100528302 : Integer;FieldNo@1100528300 : Integer;OptionValue@1100528303 : Integer;VAR ReadableOption@1100528306 : Text) : Boolean;
    VAR
      Field@1100528301 : Record 2000000041;
    BEGIN
      ReadableOption := '';
      IF (NOT Field.GET(TableNo, FieldNo)) THEN
        EXIT(FALSE);
      IF (Field.Type <> Field.Type::Option) THEN
        EXIT(FALSE);

      EXIT(OptionValueToReadableOption(Field.OptionString, OptionValue, ReadableOption));
    END;

    BEGIN
    END.
  }
}

