OBJECT Table 484 Change Global Dim. Header
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Change Global Dim. Header;
               NOR=Topptekst for endring for global dimensjon];
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Code10        ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Primary Key;
                                                              NOR=Prim‘rn›kkel;
                                                              SVE=Prim„rnyckel] }
    { 2   ;   ;Old Global Dimension 1 Code;Code20 ;TableRelation=Dimension;
                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Old Global Dimension 1 Code;
                                                              NOR=Gammel kode for global dimensjon 1] }
    { 3   ;   ;Old Global Dimension 2 Code;Code20 ;TableRelation=Dimension;
                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Old Global Dimension 2 Code;
                                                              NOR=Gammel kode for global dimensjon 2] }
    { 4   ;   ;Global Dimension 1 Code;Code20     ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                ValidateDimCode("Global Dimension 1 Code");
                                                                IF "Global Dimension 1 Code" = "Global Dimension 2 Code" THEN BEGIN
                                                                  "Global Dimension 2 Code" := '';
                                                                  "Change Type 2" := "Change Type 2"::Blank;
                                                                END;
                                                                CalcChangeType("Change Type 1","Global Dimension 1 Code","Old Global Dimension 1 Code","Old Global Dimension 2 Code");
                                                              END;

                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Global Dimension 1 Code;
                                                              NOR=Kode for global dimensjon 1;
                                                              SVE=Global dimension 1 kod] }
    { 5   ;   ;Global Dimension 2 Code;Code20     ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                ValidateDimCode("Global Dimension 2 Code");
                                                                IF "Global Dimension 2 Code" = "Global Dimension 1 Code" THEN BEGIN
                                                                  "Global Dimension 1 Code" := '';
                                                                  "Change Type 1" := "Change Type 1"::Blank;
                                                                END;
                                                                CalcChangeType("Change Type 2","Global Dimension 2 Code","Old Global Dimension 2 Code","Old Global Dimension 1 Code");
                                                              END;

                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Global Dimension 2 Code;
                                                              NOR=Kode for global dimensjon 2;
                                                              SVE=Global dimension 2 kod] }
    { 6   ;   ;Parallel Processing ;Boolean       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Parallel Processing;
                                                              NOR=Parallell behandling] }
    { 7   ;   ;Change Type 1       ;Option        ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Change Type 1;
                                                              NOR=Endringstype 1];
                                                   OptionCaptionML=[ENU=None,Blank,Replace,New;
                                                                    NOR=Ingen,Tom,Erstatt,Ny];
                                                   OptionString=None,Blank,Replace,New }
    { 8   ;   ;Change Type 2       ;Option        ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Change Type 2;
                                                              NOR=Endringstype 2];
                                                   OptionCaptionML=[ENU=None,Blank,Replace,New;
                                                                    NOR=Ingen,Tom,Erstatt,Ny];
                                                   OptionString=None,Blank,Replace,New }
  }
  KEYS
  {
    {    ;Primary Key                             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DimIsUsedInGLSetupErr@1000 : TextConst '@@@=%1 - a dimension code, like PROJECT;ENU=The dimension %1 is used in General Ledger Setup window as a shortcut dimension.;NOR=Dimensjonen %1 er brukt i vinduet Finansoppsett som en snarveisdimensjon.';
      GeneralLedgerSetup@1001 : Record 98;

    LOCAL PROCEDURE CalcChangeType@13(VAR ChangeType@1003 : 'None,Blank,Replace,New';Code@1002 : Code[20];OldCode@1000 : Code[20];OtherOldCode@1001 : Code[20]);
    BEGIN
      IF Code = OtherOldCode THEN
        ChangeType := ChangeType::Replace
      ELSE
        IF Code = OldCode THEN
          ChangeType := ChangeType::None
        ELSE
          IF Code = '' THEN
            ChangeType := ChangeType::Blank
          ELSE
            ChangeType := ChangeType::New
    END;

    [External]
    PROCEDURE Refresh@1();
    BEGIN
      RefreshCurrentDimCodes;
      "Global Dimension 1 Code" := GeneralLedgerSetup."Global Dimension 1 Code";
      "Global Dimension 2 Code" := GeneralLedgerSetup."Global Dimension 2 Code";
      "Change Type 1" := "Change Type 1"::None;
      "Change Type 2" := "Change Type 2"::None;
    END;

    [External]
    PROCEDURE RefreshCurrentDimCodes@2();
    BEGIN
      GeneralLedgerSetup.GET;
      "Old Global Dimension 1 Code" := GeneralLedgerSetup."Global Dimension 1 Code";
      "Old Global Dimension 2 Code" := GeneralLedgerSetup."Global Dimension 2 Code";
    END;

    LOCAL PROCEDURE IsUsedInShortcurDims@17(DimensionCode@1000 : Code[20]) : Boolean;
    BEGIN
      GeneralLedgerSetup.GET;
      EXIT(
        DimensionCode IN
        [GeneralLedgerSetup."Shortcut Dimension 3 Code",
         GeneralLedgerSetup."Shortcut Dimension 4 Code",
         GeneralLedgerSetup."Shortcut Dimension 5 Code",
         GeneralLedgerSetup."Shortcut Dimension 6 Code",
         GeneralLedgerSetup."Shortcut Dimension 7 Code",
         GeneralLedgerSetup."Shortcut Dimension 8 Code"]);
    END;

    LOCAL PROCEDURE ValidateDimCode@9(NewCode@1000 : Code[20]);
    VAR
      Dimension@1002 : Record 348;
    BEGIN
      IF NewCode <> '' THEN BEGIN
        Dimension.GET(NewCode);
        IF IsUsedInShortcurDims(NewCode) THEN
          ERROR(DimIsUsedInGLSetupErr,NewCode);
      END;
    END;

    BEGIN
    END.
  }
}

