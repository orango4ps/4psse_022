OBJECT Codeunit 6085580 Doc. - Field Validation
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.4.07;
  }
  PROPERTIES
  {
    TableNo=6085590;
    OnRun=VAR
            DocumentLine@1000000000 : TEMPORARY Record 6085596;
            Field@1000000004 : Record 6085580;
            DocumentComment@1000000008 : Record 6085594;
            TempDocLine@1000000002 : TEMPORARY Record 6085596;
            CaptureMgnt@1000000009 : Codeunit 6085576;
            IsValid@1000000001 : Boolean;
          BEGIN
            DeleteComments(DocumentComment.Area::Validation);
            IsValid := TRUE;

            Field.SETRANGE("Template No.",Rec."Template No.");
            Field.SETRANGE(Type,Field.Type::Header);
            IF Field.FINDSET THEN
              REPEAT
                CheckFieldSourceTable(Rec,Field,0,IsValid);
                IF NOT CaptureMgnt.IsValidValue(Field,Rec."No.",0) THEN BEGIN
                  DocumentComment.Add(Rec,Field,0,DocumentComment.Area::Validation,DocumentComment."Comment Type"::Error,
                    STRSUBSTNO(Text001,Field."Field Name"));
                  IsValid := FALSE;
                END;
              UNTIL Field.NEXT = 0;

            Rec.BuildTempLinesTable(TempDocLine);
            IF TempDocLine.FINDSET THEN BEGIN
              Field.SETRANGE("Template No.",Rec."Template No.");
              Field.SETRANGE(Type,Field.Type::Line);

              REPEAT
                IF Field.FINDSET THEN
                  REPEAT
                    CheckFieldSourceTable(Rec,Field,TempDocLine."Line No.",IsValid);
                    IF NOT CaptureMgnt.IsValidValue(Field,Rec."No.",TempDocLine."Line No.") THEN BEGIN
                      DocumentComment.Add(Rec,Field,TempDocLine."Line No.",DocumentComment.Area::Validation,
                        DocumentComment."Comment Type"::Error,STRSUBSTNO(Text002,Field."Field Name",TempDocLine."Line No."));
                      IsValid := FALSE;
                    END;
                  UNTIL Field.NEXT = 0;
              UNTIL TempDocLine.NEXT = 0;
            END;

            Rec.OK := IsValid;
            Rec.MODIFY;
          END;

  }
  CODE
  {
    VAR
      Text001@1000000003 : TextConst 'DEU=%1 ist nicht korrekt.;ENU=%1 is not correct.;NLD=%1 is niet correct;NOR=%1 er ikke riktig.;SVE=%1 „r felaktigt.';
      Text002@1000000000 : TextConst 'DEU=%1 ist nicht korrekt in Zeile %2.;ENU=%1 is not correct on line %2.;NLD=%1 is niet correct op regel %2';

    PROCEDURE CheckFieldSourceTable@1160040000(Document@1160040001 : Record 6085590;Field@1160040003 : Record 6085580;LineNo@1160040004 : Integer;VAR IsValid@1160040002 : Boolean);
    VAR
      Value@1160040000 : Record 6085593;
      Comment@1160040006 : Record 6085594;
      DCMgt@1160040005 : Codeunit 6085603;
    BEGIN
      IF Field."Source Table No." = 0 THEN
        EXIT;

      IF NOT Value.GET(Document."No.",TRUE,Field.Code,LineNo) THEN
        EXIT;

      IF Value."Value (Record ID Tree ID)" = 0 THEN
        EXIT;

      DCMgt.IsLogActive2(Field."Source Table No.",TRUE);
    END;

    BEGIN
    END.
  }
}

