OBJECT Table 11126688 Empoly Warehouse OrderLine
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               EmpolyWarehouseOrder@1100529400 : Record 11126687;
             BEGIN
               IF CURRENTCOMPANY <> COMPANYNAME THEN
                 EmpolyWarehouseOrder.CHANGECOMPANY(CURRENTCOMPANY);
               EmpolyWarehouseOrder.GET("Order No.");
               Element := EmpolyWarehouseOrder.Element;
               EmpolyWarehouseOrder.TESTFIELD("Processed On", 0DT);
             END;

    OnModify=VAR
               EmpolyWarehouseOrder@1100529900 : Record 11126687;
             BEGIN
               TESTFIELD("Item No.");
               TESTFIELD("Processed On", 0DT);
               EmpolyWarehouseOrder.GET("Order No.");
               EmpolyWarehouseOrder.TESTFIELD("Processed On", 0DT);
             END;

    CaptionML=ENU=4PS Warehouse App Order Line;
  }
  FIELDS
  {
    { 10  ;   ;Order No.           ;Code20        ;TableRelation="Empoly Warehouse Order".No.;
                                                   CaptionML=[ENU=Order No.;
                                                              NOR=Ordrenr.;
                                                              SVE=Ordernr] }
    { 20  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 80  ;   ;Item No.            ;Code20        ;TableRelation=Item.No.;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Description Item", "Base Unit of Measure");
                                                              END;

                                                   CaptionML=[ENU=Item No.;
                                                              NOR=Varenr.;
                                                              SVE=Artikelnr] }
    { 81  ;   ;Description Item    ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Item.Description WHERE (No.=FIELD(Item No.)));
                                                   CaptionML=ENU=Description Item;
                                                   Editable=No }
    { 90  ;   ;Quantity            ;Decimal       ;CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   DecimalPlaces=0:5 }
    { 91  ;   ;Base Unit of Measure;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Item."Base Unit of Measure" WHERE (No.=FIELD(Item No.)));
                                                   CaptionML=[ENU=Base Unit of Measure;
                                                              NOR=Lagerenhet;
                                                              SVE=Basenhet];
                                                   Editable=No }
    { 100 ;   ;Requested Quantity  ;Decimal       ;CaptionML=ENU=Requested Quantity;
                                                   DecimalPlaces=0:5;
                                                   MaxValue=99ÿ999 }
    { 110 ;   ;Reason Code         ;Code10        ;OnValidate=BEGIN
                                                                CheckCompanyIC;
                                                                IF ("Reason Code" <> '') THEN BEGIN
                                                                  IF (EmpolyWarehouseOrder.Process = EmpolyWarehouseOrder.Process::"Sales Return") THEN BEGIN
                                                                    ReturnReason.GET("Reason Code");
                                                                  END ELSE BEGIN
                                                                    ReasonCode.GET("Reason Code");
                                                                  END;
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              CheckCompanyIC;
                                                              IF (EmpolyWarehouseOrder.Process = EmpolyWarehouseOrder.Process::"Sales Return") THEN BEGIN
                                                                IF (ReturnReason.GET("Reason Code")) THEN;
                                                                IF (PAGE.RUNMODAL(0, ReturnReason) = ACTION::LookupOK) THEN
                                                                  VALIDATE("Reason Code", ReturnReason.Code);
                                                              END ELSE BEGIN
                                                                IF (ReasonCode.GET("Reason Code")) THEN;
                                                                IF (PAGE.RUNMODAL(0, ReasonCode) = ACTION::LookupOK) THEN
                                                                  VALIDATE("Reason Code", ReasonCode.Code);
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Reason Code;
                                                              NOR=Overv†kingskode;
                                                              SVE=Uppf”ljningskod] }
    { 120 ;   ;Element             ;Code20        ;OnValidate=BEGIN
                                                                CheckCompanyIC;
                                                                IF Element <> '' THEN BEGIN
                                                                  EmpolyWarehouseOrder.TESTFIELD("Project No.");
                                                                  ProjElemRec.GET(EmpolyWarehouseOrder."Project No.", Element);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Element;
                                                              NOR=Element;
                                                              SVE=Element] }
    { 130 ;   ;Processed On        ;DateTime      ;CaptionML=[ENU=Processed On;
                                                              SVE=Behandlat den];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Order No.,Line No.                      ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      EmpolyWarehouseOrder@1100529400 : Record 11126687;
      LocRec@1100529404 : Record 14;
      ProjRec@1100529403 : Record 11072003;
      ProjElemRec@1100529402 : Record 11012010;
      ServOrderRec@1100529401 : Record 11012823;
      ReturnReason@1100528300 : Record 6635;
      ReasonCode@1100528301 : Record 231;

    PROCEDURE CheckCompanyIC@1210190032();
    VAR
      lvCompName@1210190000 : Text[50];
    BEGIN
      EmpolyWarehouseOrder.GET("Order No.");
      IF EmpolyWarehouseOrder."Receiving Company" <> '' THEN
        lvCompName := EmpolyWarehouseOrder."Receiving Company"
      ELSE
        lvCompName := CURRENTCOMPANY;

      ProjRec.CHANGECOMPANY(lvCompName);
      ProjElemRec.CHANGECOMPANY(lvCompName);
      LocRec.CHANGECOMPANY(lvCompName);
      ServOrderRec.CHANGECOMPANY(lvCompName);
      ReasonCode.CHANGECOMPANY(lvCompName);
      ReturnReason.CHANGECOMPANY(lvCompName);
    END;

    PROCEDURE LookupElement@1100525002();
    BEGIN
      CheckCompanyIC;
      ProjElemRec."Project No." := EmpolyWarehouseOrder."Project No.";
      ProjElemRec.Element := Element;
      ProjElemRec.SETRANGE("Project No.", EmpolyWarehouseOrder."Project No.");
      ProjElemRec.SETRANGE("Publish in Empoly App", TRUE);
      IF PAGE.RUNMODAL(0, ProjElemRec) = ACTION::LookupOK THEN
        VALIDATE(Element, ProjElemRec.Element);
    END;

    PROCEDURE GetReasonDescription@1100528301() : Text;
    BEGIN
      IF ("Reason Code" = '') THEN
        EXIT('');
      CheckCompanyIC;

      EmpolyWarehouseOrder.GET("Order No.");
      IF (EmpolyWarehouseOrder.Process = EmpolyWarehouseOrder.Process::"Sales Return") THEN BEGIN
        IF (NOT ReturnReason.GET("Reason Code")) THEN
          ReturnReason.INIT;
        EXIT(ReturnReason.Description);
      END ELSE BEGIN
        IF (NOT ReasonCode.GET("Reason Code")) THEN
          ReasonCode.INIT;
        EXIT(ReasonCode.Description);
      END;
      EXIT('');
    END;

    BEGIN
    END.
  }
}

