OBJECT Report 6034 Remove Lines from Contract
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW113.00,4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Servicevertragszeilen lîschen;
               ENU=Remove Service Contract Lines;
               NLD=Servicecontractregels verwijderen;
               NOR=Fjern servicekontraktlinjer;
               SVE=Ta bort servicekontraktsrader];
    ApplicationArea=#Service;
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   DelToDate := WORKDATE;
                   ServMgtSetup.GET;
                 END;

    OnPreReport=BEGIN
                  CreateCreditfromContractLines.InitVariables;
                END;

    OnPostReport=BEGIN
                   IF DeleteLines = DeleteLines::"Delete Lines" THEN
                     IF LinesRemoved > 1 THEN
                       MESSAGE(Text000,LinesRemoved)
                     ELSE
                       MESSAGE(Text001,LinesRemoved);

                   IF CreditMemoCreated = 1 THEN
                     MESSAGE(Text006);

                   IF CreditMemoCreated > 1 THEN
                     MESSAGE(Text007);
                   CreateCreditfromContractLines.InitVariables;
                 END;

  }
  DATASET
  {
    { 6062;    ;DataItem;                    ;
               DataItemTable=Table5964;
               DataItemTableView=SORTING(Contract Type,Contract No.,Credited,New Line)
                                 WHERE(Contract Type=CONST(Contract),
                                       Contract Status=CONST(Signed),
                                       New Line=CONST(No));
               OnPreDataItem=BEGIN
                               IF DeleteLines = DeleteLines::"Print Only" THEN BEGIN
                                 CLEAR(ExpiredContractLinesTest);
                                 ExpiredContractLinesTest.InitVariables(DelToDate,ReasonCode);
                                 ExpiredContractLinesTest.SETTABLEVIEW("Service Contract Line");
                                 ExpiredContractLinesTest.RUNMODAL;
                                 CurrReport.BREAK;
                               END;

                               IF DelToDate = 0D THEN
                                 ERROR(Text002);
                               ServMgtSetup.GET;
                               IF ServMgtSetup."Use Contract Cancel Reason" THEN
                                 IF ReasonCode = '' THEN
                                   ERROR(Text003);
                               SETFILTER("Contract Expiration Date",'<>%1&<=%2',0D,DelToDate);

                               Window.OPEN(
                                 Text004 +
                                 '@1@@@@@@@@@@@@@@@@@@@@@@@@@@@');
                               i := COUNT;
                               j := 0;
                               LinesRemoved := 0;
                               LastContractNo := '';
                               CreditMemoCreated := 0;
                             END;

               OnAfterGetRecord=BEGIN
                                  j := j + 1;
                                  Window.UPDATE(1,ROUND(j / i * 10000,1));

                                  IF LastContractNo <> "Contract No." THEN BEGIN
                                    LastContractNo := "Contract No.";
                                    ServContract.GET("Contract Type","Contract No.");
                                    FiledServContract.FileContract(ServContract);
                                    IF ServContract."Automatic Credit Memos" AND
                                       ("Credit Memo Date" > 0D) AND
                                       CreditMemoBaseExists
                                    THEN
                                      CreditMemoCreated := CreditMemoCreated + 1;
                                  END;
                                  SuspendStatusCheck(TRUE);
                                  DELETE(TRUE);

                                  LinesRemoved := LinesRemoved + 1;
                                END;

               ReqFilterFields=Contract No.,Service Item No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 5   ;2   ;Field     ;
                  CaptionML=[DEU=Zeilen lîschen bis;
                             ENU=Remove Lines to;
                             NLD=Regels verwijderen tot;
                             NOR=Fjern linjer t.o.m.];
                  ToolTipML=[DEU=Gibt das Datum an, bis zu welchem Vertragszeilen entfernt werden sollen. Die Stapelverarbeitung umfasst Vertragszeilen mit Vertragsablaufdaten an oder vor diesem Datum.;
                             ENU=Specifies the date up to which you want to remove contract lines. The batch job includes contract lines with contract expiration dates on or before this date.;
                             NLD=Hiermee wordt de datum opgegeven tot wanneer contractregels moeten worden verwijderd. Contractregels met een contractvervaldatum op of v¢¢r deze datum worden opgenomen in de batchtaak.;
                             NOR=Angir datoen du vil fjerne kontraktlinjer frem til. Kjõrselen inkluderer kontraktlinjer med kontraktenes utlõpsdatoer pÜ eller fõr denne datoen.];
                  ApplicationArea=#Service;
                  SourceExpr=DelToDate }

      { 1   ;2   ;Field     ;
                  CaptionML=[DEU=Ursachencode;
                             ENU=Reason Code;
                             NLD=Redencode;
                             NOR=èrsaksspor;
                             SVE=Uppfîljningskod];
                  ToolTipML=[DEU=Gibt den Ursachencode fÅr das Entfernen von Zeilen aus dem Vertrag an. Um die bestehenden Ursachencodes anzuzeigen, wÑhlen Sie das Feld "Filter" aus.;
                             ENU=Specifies the reason code for the removal of lines from the contract. To see the existing reason codes, choose the Filter field.;
                             NLD=Hiermee wordt de redencode opgegeven voor het verwijderen van regels uit het contract. Als u de bestaande redencodes wilt bekijken, kiest u het filterveld.;
                             NOR=Angir Ürsakskoden for fjerning av linjer fra kontrakten. Hvis du vil se eksisterende Ürsakskoder, velger du feltet Filter.];
                  ApplicationArea=#Service;
                  SourceExpr=ReasonCode;
                  OnValidate=BEGIN
                               ReasonCode2.GET(ReasonCode);
                             END;

                  OnLookup=BEGIN
                             ReasonCode2.RESET;
                             ReasonCode2.Code := ReasonCode;
                             IF PAGE.RUNMODAL(0,ReasonCode2) = ACTION::LookupOK THEN BEGIN
                               ReasonCode2.GET(ReasonCode2.Code);
                               ReasonCode := ReasonCode2.Code;
                             END;
                           END;
                            }

      { 7   ;2   ;Field     ;
                  Name=Reason Code;
                  CaptionML=[DEU=Ursachencodebeschreibung;
                             ENU=Reason Code Description;
                             NLD=Redencodeomschrijving;
                             NOR=Beskr. av Ürsaksspor];
                  ToolTipML=[DEU=Gibt eine Beschreibung des Ursachencodes an.;
                             ENU=Specifies a description of the Reason Code.;
                             NLD=Hiermee wordt een omschrijving van de redencode opgegeven.;
                             NOR=Angir en beskrivelse av Ürsakskoden.];
                  ApplicationArea=#Service;
                  SourceExpr=ReasonCode2.Description;
                  Editable=FALSE }

      { 10  ;2   ;Field     ;
                  CaptionML=[DEU=Aktion;
                             ENU=Action;
                             NLD=Actie;
                             NOR=Handling;
                             SVE=ètgÑrd];
                  ToolTipML=[DEU=Gibt die gewÅnschte Aktion zur Entfernung von abgelaufenen Vertragsposten aus ServicevertrÑgen an.;
                             ENU=Specifies the desired action relating to removing expired contract lines from service contracts.;
                             NLD=Hiermee wordt de gewenste actie opgegeven die gerelateerd is aan het verwijderen van vervallen contractregels uit servicecontracten.;
                             NOR=Angir den õnskede handlingen nÜr det gjelder Ü fjerne utlõpte kontraktlinjer fra servicekontrakter.];
                  OptionCaptionML=[DEU=Zeilen lîschen,Nur Drucken;
                                   ENU=Delete Lines,Print Only;
                                   NLD=Regels verwijderen,Alleen afdrukken;
                                   NOR=Slett linjer,Kun utskrift];
                  ApplicationArea=#Service;
                  SourceExpr=DeleteLines }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=%1 Vertragszeilen wurden gelîscht.;ENU=%1 contract lines were removed.;NLD=%1 contractregels werden verwijderd.;NOR=%1 kontraktlinjer ble fjernet.';
      Text001@1001 : TextConst 'DEU=%1 Vertragszeile wurde gelîscht.;ENU=%1 contract line was removed.;NLD=%1 contractregel werd verwijderd.;NOR=%1 kontraktlinje ble fjernet.';
      Text002@1002 : TextConst 'DEU=Sie mÅssen das Feld ''Zeilen lîschen bis'' ausfÅllen.;ENU=You must fill in the Remove Lines to field.;NLD=U moet het veld Regels verwijderen t/m invullen.;NOR=Du mÜ fylle ut feltet Fjern linjer t.o.m.';
      Text003@1003 : TextConst 'DEU=Sie mÅssen das Feld Ursachencode ausfÅllen.;ENU=You must fill in the Reason Code field.;NLD=U moet het veld Redencode invullen.;NOR=Du mÜ fylle ut feltet èrsaksspor.';
      Text004@1004 : TextConst 'DEU=Vertragszeilen werden gelîscht ... \\;ENU=Removing contract lines... \\;NLD=Verwijderen contractregels... \\;NOR=Fjerner kontraktlinjer... \\';
      ServMgtSetup@1006 : Record 5911;
      ServContract@1007 : Record 5965;
      FiledServContract@1008 : Record 5970;
      ReasonCode2@1009 : Record 231;
      ExpiredContractLinesTest@1022 : Report 5987;
      CreateCreditfromContractLines@1010 : Codeunit 5945;
      Window@1012 : Dialog;
      i@1013 : Integer;
      j@1014 : Integer;
      LinesRemoved@1015 : Integer;
      DelToDate@1017 : Date;
      DeleteLines@1018 : 'Delete Lines,Print Only';
      ReasonCode@1019 : Code[10];
      LastContractNo@1020 : Code[20];
      Text006@1023 : TextConst 'DEU=Eine Gutschrift wurde erstellt/aktualisiert.;ENU=A credit memo was created/updated.;NLD=Creditnota is gemaakt/bijgewerkt.;NOR=Det er opprettet/oppdatert en kreditnota.';
      CreditMemoCreated@1011 : Integer;
      Text007@1016 : TextConst 'DEU=Gutschriften wurden erstellt/aktualisiert.;ENU=Credit memos were created/updated.;NLD=Creditnota''s zijn gemaakt/bijgewerkt.;NOR=Det er opprettet/oppdatert kreditnotaer.';

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

