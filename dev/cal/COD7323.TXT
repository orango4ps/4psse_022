OBJECT Codeunit 7323 Whse.-Act.-Post (Yes/No)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    TableNo=5767;
    OnRun=BEGIN
            WhseActivLine.COPY(Rec);
            Code;
            COPY(WhseActivLine);
          END;

  }
  CODE
  {
    VAR
      Text000@1002 : TextConst 'ENU=&Receive,Receive &and Invoice;NOR=&Motta,Motta &og fakturer;SVE=&Inleverera,Inleverera &och fakturera';
      WhseActivLine@1007 : Record 5767;
      WhseActivityPost@1008 : Codeunit 7324;
      Selection@1003 : Integer;
      Text001@1000 : TextConst 'ENU=&Ship,Ship &and Invoice;NOR=&Lever,Lever &og fakturer;SVE=&Leverera,Leverera &och fakturera';
      Text002@1001 : TextConst 'ENU=Do you want to post the %1 and %2?;NOR=Vil du bokf›re %1 og %2?;SVE=Vill du bokf”ra %1 och %2?';
      PrintDoc@1004 : Boolean;
      DefaultOption@1005 : Integer;

    LOCAL PROCEDURE Code@1();
    VAR
      HideDialog@1000 : Boolean;
      IsHandled@1001 : Boolean;
    BEGIN
      IsHandled := FALSE;
      HideDialog := FALSE;
      DefaultOption := 2;
      OnBeforeConfirmPost(WhseActivLine,HideDialog,Selection,DefaultOption,IsHandled);
      IF IsHandled THEN
        EXIT;

      IF (DefaultOption < 1) OR (DefaultOption > 2) THEN
        DefaultOption := 2;

      WITH WhseActivLine DO BEGIN
        IF NOT HideDialog THEN
          CASE "Activity Type" OF
            "Activity Type"::"Invt. Put-away":
              IF NOT SelectForPutAway THEN
                EXIT;
            ELSE
              IF NOT SelectForOtherTypes THEN
                EXIT;
          END;

        WhseActivityPost.SetInvoiceSourceDoc(Selection = 2);
        WhseActivityPost.PrintDocument(PrintDoc);
        WhseActivityPost.RUN(WhseActivLine);
        CLEAR(WhseActivityPost);
      END;
    END;

    [External]
    PROCEDURE PrintDocument@2(SetPrint@1000 : Boolean);
    BEGIN
      PrintDoc := SetPrint;
    END;

    LOCAL PROCEDURE SelectForPutAway@3() : Boolean;
    BEGIN
      WITH WhseActivLine DO
        IF ("Source Document" = "Source Document"::"Prod. Output") OR
           ("Source Document" = "Source Document"::"Inbound Transfer") OR
           ("Source Document" = "Source Document"::"Prod. Consumption")
        THEN BEGIN
          IF NOT CONFIRM(Text002,FALSE,"Activity Type","Source Document") THEN
            EXIT(FALSE);
        END ELSE BEGIN
          Selection := STRMENU(Text000,DefaultOption);
          IF Selection = 0 THEN
            EXIT(FALSE);
        END;

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE SelectForOtherTypes@4() : Boolean;
    BEGIN
      WITH WhseActivLine DO
        IF ("Source Document" = "Source Document"::"Prod. Consumption") OR
           ("Source Document" = "Source Document"::"Outbound Transfer")
        THEN BEGIN
          IF NOT CONFIRM(Text002,FALSE,"Activity Type","Source Document") THEN
            EXIT(FALSE);
        END ELSE BEGIN
          Selection := STRMENU(Text001,DefaultOption);
          IF Selection = 0 THEN
            EXIT(FALSE);
        END;

      EXIT(TRUE);
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeConfirmPost@7(VAR WhseActivLine@1000 : Record 5767;VAR HideDialog@1001 : Boolean;VAR Selection@1002 : Integer;VAR DefaultOption@1003 : Integer;VAR IsHandled@1004 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

