OBJECT Table 11071727 Service Order Extension
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               JobRelatedServiceOrder@1100528200 : Record 11229339;
               Job@1100528201 : Record 11072003;
               ServiceProcessManagement@1100528600 : Codeunit 11012883;
             BEGIN
               IF "Master Project" <> '' THEN BEGIN
                 JobRelatedServiceOrder.CHANGECOMPANY("Master Company");
                 Job.CHANGECOMPANY("Master Company");
                 IF NOT JobRelatedServiceOrder.GET("Master Project", COMPANYNAME, "Service Order No.") THEN BEGIN
                   IF Job.GET("Master Project") THEN BEGIN
                     JobRelatedServiceOrder."Job No." := "Master Project";
                     JobRelatedServiceOrder."Company Service Order" := COMPANYNAME;
                     JobRelatedServiceOrder."Service Order No." := "Service Order No.";
                     JobRelatedServiceOrder.INSERT;
                   END;
                 END;
               END;
               ServiceProcessManagement.CheckAllowedValues(Rec, xRec, TRUE);
             END;

    OnModify=VAR
               JobRelatedServiceOrder@1100528201 : Record 11229339;
               Job@1100528200 : Record 11072003;
               ServiceProcessManagement@1100528600 : Codeunit 11012883;
             BEGIN
               IF (xRec."Master Company" <> "Master Company") OR
                  (xRec."Master Project" <> "Master Project") THEN BEGIN

                 JobRelatedServiceOrder.CHANGECOMPANY(xRec."Master Company");
                 IF JobRelatedServiceOrder.GET(xRec."Master Project", COMPANYNAME, "Service Order No.") THEN
                   JobRelatedServiceOrder.DELETE;

                 JobRelatedServiceOrder.CHANGECOMPANY("Master Company");
                 Job.CHANGECOMPANY("Master Company");
                 IF NOT JobRelatedServiceOrder.GET("Master Project", COMPANYNAME, "Service Order No.") THEN BEGIN
                   IF Job.GET("Master Project") THEN BEGIN
                     JobRelatedServiceOrder."Job No." := "Master Project";
                     JobRelatedServiceOrder."Company Service Order" := COMPANYNAME;
                     JobRelatedServiceOrder."Service Order No." := "Service Order No.";
                     JobRelatedServiceOrder.INSERT;
                   END;
                 END;
               END;
               ServiceProcessManagement.CheckAllowedValues(Rec, xRec, FALSE);
             END;

    OnDelete=VAR
               JobRelatedServiceOrder@1100528201 : Record 11229339;
             BEGIN
               IF "Master Project" <> '' THEN BEGIN
                 JobRelatedServiceOrder.CHANGECOMPANY("Master Company");
                 IF JobRelatedServiceOrder.GET("Master Project", COMPANYNAME, "Service Order No.") THEN
                   JobRelatedServiceOrder.DELETE;
               END;
             END;

    CaptionML=[ENU=Service Order Extension;
               NOR=Serviceordre-ETA;
               SVE=Serviceorder-éTA];
  }
  FIELDS
  {
    { 10  ;   ;Service Order No.   ;Code20        ;TableRelation="Service Order".No. WHERE (No.=FIELD(Service Order No.));
                                                   CaptionML=[ENU=Service Order No.;
                                                              NOR=Serviceordrenr.;
                                                              SVE=Serviceordernr];
                                                   NotBlank=Yes }
    { 11  ;   ;Service Order Description;Text50   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Order".Description WHERE (No.=FIELD(Service Order No.)));
                                                   CaptionML=[ENU=Service Order Description;
                                                              NOR=Serviceordre Beskrivelse;
                                                              SVE=Serviceorder beskrivning];
                                                   Editable=No }
    { 14  ;   ;Master Company      ;Text30        ;TableRelation=Company;
                                                   OnValidate=VAR
                                                                CheckLicenceIC@1210190000 : Codeunit 11012780;
                                                              BEGIN
                                                                IF "Master Company" <> '' THEN
                                                                  CheckLicenceIC.CheckICEntry("Master Company");
                                                                IF "Master Company" <> xRec."Master Company" THEN BEGIN
                                                                  CheckCompanyIC;
                                                                  "Master Project" := '';
                                                                  "Master Element" := '';
                                                                  "Master Extension Contract" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Master Company;
                                                              SVE=Huvudfîretag] }
    { 20  ;   ;Master Project      ;Code20        ;TableRelation=IF (Master Company=FILTER('')) Job.No. WHERE (Internal Project=CONST(No));
                                                   OnValidate=BEGIN
                                                                IF "Master Project" <> '' THEN BEGIN
                                                                  CheckCompanyIC;
                                                                  ServOrderRec.GET("Service Order No.");
                                                                  ProjRec.GET("Master Project");
                                                                  ProjectSetup.GET;
                                                                  IF ProjectSetup."Provisions at Closure" THEN
                                                                    ProjRec.CheckProjectStatus(-1)
                                                                  ELSE
                                                                    ProjRec.CheckProjectStatus(0);
                                                                END;

                                                                IF NOT ProjElemRec.GET("Master Project", "Master Element") THEN
                                                                  "Master Element" := '';
                                                                IF ("Master Element" <> '') AND ProjElemRec.Blocked THEN
                                                                  "Master Element" := '';

                                                                IF NOT ExtensionContract.GET("Master Project", "Master Extension Contract") THEN
                                                                  "Master Extension Contract" := '';
                                                              END;

                                                   OnLookup=BEGIN
                                                              OnLookupJobNo(TRUE);
                                                            END;

                                                   CaptionML=[ENU=Master Project;
                                                              NOR=Hovedprosjekt;
                                                              SVE=Debitera till projekt] }
    { 30  ;   ;Master Element      ;Code20        ;TableRelation=IF (Master Company=FILTER('')) "Project Element".Element WHERE (Project No.=FIELD(Master Project));
                                                   OnValidate=BEGIN
                                                                IF "Master Element" <> '' THEN BEGIN
                                                                  CheckCompanyIC;
                                                                  FormatMgt.FormatElem("Master Element",2,'');
                                                                  ProjElemRec.GET("Master Project", "Master Element");
                                                                  ProjElemRec.TESTFIELD(Blocked, FALSE);
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              CheckCompanyIC;
                                                              ProjElemRec."Project No." := "Master Project";
                                                              ProjElemRec.Element := "Master Element";
                                                              ProjElemRec.SETRANGE("Project No.", "Master Project");
                                                              IF PAGE.RUNMODAL(0, ProjElemRec) = ACTION::LookupOK THEN
                                                                VALIDATE("Master Element", ProjElemRec.Element);
                                                            END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Master Element;
                                                              NOR=Hovedelement;
                                                              SVE=Debitera till elementet] }
    { 35  ;   ;Master Extension Contract;Code10   ;TableRelation="Extension Contract"."Contract No." WHERE (Project No.=FIELD(Master Project));
                                                   OnValidate=BEGIN
                                                                IF "Master Extension Contract" <> '' THEN BEGIN
                                                                  CheckCompanyIC;
                                                                  ExtensionContract.GET("Master Project", "Master Extension Contract");
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              CheckCompanyIC;
                                                              ExtensionContract."Project No." := "Master Project";
                                                              ExtensionContract."Contract No." := "Master Extension Contract";
                                                              ExtensionContract.SETRANGE("Project No.", "Master Project");
                                                              IF PAGE.RUNMODAL(0, ExtensionContract) = ACTION::LookupOK THEN
                                                                VALIDATE("Master Extension Contract", ExtensionContract."Contract No.");
                                                            END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Master Extension Contract }
    { 40  ;   ;Warranty found at   ;Text50        ;CaptionML=[ENU=Warranty found at;
                                                              NOR=Garanti funnet i;
                                                              SVE=Garantin finns i];
                                                   Editable=No }
    { 50  ;   ;Executing Company Name;Text30      ;TableRelation=Company;
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Executing Company Name;
                                                              NOR=Navn pÜ utfõrende selskap;
                                                              SVE=Kîr fîretagsnamn];
                                                   Editable=No }
    { 60  ;   ;Order No. Executing Company;Code20 ;CaptionML=[ENU=Order No. Executing Company;
                                                              NOR=Ordrenr, utfõrende selskap;
                                                              SVE=Ordernr, utfîrande fîretag];
                                                   Editable=No }
    { 70  ;   ;Principal Company Name;Text30      ;TableRelation=Company;
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Principal Company Name;
                                                              NOR=Navn pÜ oppdragsgiver;
                                                              SVE=Uppdragsgivarens fîretagsnamn] }
    { 80  ;   ;Serv. Contr. No. Princ. Comp.;Code20;
                                                   CaptionML=[ENU=Serv. Contr. No. Princ. Comp.;
                                                              NOR=Servicekontraktnummer for oppdragsgiver;
                                                              SVE=Serv.kontr.nr fîr huvudfîretag] }
    { 90  ;   ;Act. Ref. Point Exec. Company;Integer;
                                                   CaptionML=[ENU=Act. Ref. Point Exec. Company;
                                                              NOR=Faktisk referansepunkt for utfõrende selskap;
                                                              SVE=ètg. ref.punktutf. Fîretag];
                                                   Editable=No }
    { 91  ;   ;Act. Ref. Point Ex. Comp. Dsc.;Text50;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Reference Point".Description WHERE (No.=FIELD(Act. Ref. Point Exec. Company)));
                                                   CaptionML=[ENU=Act. Ref. Point Ex. Comp. Dsc.;
                                                              NOR=Beskr. for Fakt. Ref. Pkt. Ekst. Selsk.;
                                                              SVE=Beskr Ütg.ref.punkt fîr verkst.fîretag];
                                                   Editable=No }
    { 100 ;   ;Vessel Name         ;Text30        ;CaptionML=[ENU=Vessel Name;
                                                              NOR=Navn pÜ fartõy;
                                                              SVE=Fartygsnamn];
                                                   Description=Vessels }
    { 110 ;   ;Agent No.           ;Code20        ;TableRelation=Agent;
                                                   CaptionML=[ENU=Agent No.;
                                                              NOR=Agentnr.;
                                                              SVE=Ombudsnr];
                                                   Description=Vessels }
    { 120 ;   ;Port                ;Code10        ;CaptionML=[ENU=Port;
                                                              NOR=Havn;
                                                              SVE=Port];
                                                   Description=Vessels }
    { 130 ;   ;Quay Berth          ;Text30        ;CaptionML=[ENU=Quay Berth;
                                                              NOR=Kai;
                                                              SVE=Kajplats];
                                                   Description=Vessels }
    { 140 ;   ;Price Book Code     ;Code20        ;OnValidate=VAR
                                                                ServiceUnitPriceManagement@1100528600 : Codeunit 11012846;
                                                              BEGIN
                                                                ServiceUnitPriceManagement.ValidateSOUnitPriceChange("Service Order No.", FIELDCAPTION("Price Book Code"));

                                                                IF "Price Book Code" <> xRec."Price Book Code" THEN
                                                                  VALIDATE("Unit Price Set", '');
                                                              END;

                                                   OnLookup=VAR
                                                              ServiceUnitPrice@1100528600 : Record 11071768;
                                                              ServiceUnitPriceList@1100528601 : Page 11125269;
                                                            BEGIN
                                                              ServOrderRec.GET("Service Order No.");

                                                              ServiceUnitPrice.SETRANGE(Type, ServOrderRec."Price Book Type");
                                                              ServiceUnitPriceList.LOOKUPMODE(TRUE);
                                                              ServiceUnitPriceList.SETTABLEVIEW(ServiceUnitPrice);
                                                              IF (ServiceUnitPriceList.RUNMODAL = ACTION::LookupOK) THEN BEGIN
                                                                ServiceUnitPriceList.GETRECORD(ServiceUnitPrice);
                                                                VALIDATE("Price Book Code", ServiceUnitPrice.Code);
                                                                VALIDATE("Price Book Index Date", ServiceUnitPrice."Index Date");
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Price Book Code;
                                                              SVE=Enhetspriskod] }
    { 150 ;   ;Price Book Index Date;Date         ;OnValidate=VAR
                                                                ServiceUnitPriceManagement@1100528600 : Codeunit 11012846;
                                                              BEGIN
                                                                ServiceUnitPriceManagement.ValidateSOUnitPriceChange("Service Order No.", FIELDCAPTION("Price Book Index Date"));
                                                              END;

                                                   CaptionML=[ENU=Price Book Index Date;
                                                              SVE=Indexdatum fîr enhetspris] }
    { 160 ;   ;Surcharge % Labor   ;Decimal       ;CaptionML=[ENU=Surcharge % Labor;
                                                              NOR=Tilleggsgebyrsprosent, arbeidskraft;
                                                              SVE=TillÑggsavgiftsprocent, arbetskraft] }
    { 170 ;   ;Surcharge % Material;Decimal       ;CaptionML=[ENU=Surcharge % Material;
                                                              NOR=Tilleggsgebyrsprosent, Materiale;
                                                              SVE=TillÑggsavgiftsprocent, material] }
    { 180 ;   ;Surcharge % Subcontr.;Decimal      ;CaptionML=[ENU=Surcharge % Subcontracting;
                                                              NOR=Tilleggsgebyrsprosent, underentrepenõr;
                                                              SVE=TillÑggsavgiftsprocent, underentreprenad] }
    { 190 ;   ;Surcharge % Plant   ;Decimal       ;CaptionML=[ENU=Surcharge % Plant;
                                                              NOR=Tilleggsgebyrsprosent, maskin;
                                                              SVE=TillÑggsavgiftsprocent, maskin] }
    { 200 ;   ;Surcharge % Sundry  ;Decimal       ;CaptionML=[ENU=Surcharge % Sundry;
                                                              NOR=PÜslagssatser, diverse;
                                                              SVE=TillÑggsavgiftsprocent, diverse] }
    { 210 ;   ;Item Price Cost Plus Entry;Option  ;CaptionML=[ENU=Item Price Cost Plus Entry;
                                                              NOR=Selvkosttransaksjon for artikkelpris;
                                                              SVE=Redovisad sjÑlvkostnad fîr artikelpris];
                                                   OptionCaptionML=[ENU=CostPrice,Gross Price - Sales Discount;
                                                                    NOR=Selvkostpris,Bruttopris Õ Salgsrabatt;
                                                                    SVE=SjÑlvkostnadspris,Bruttopris Õ FîrsÑljningsrabatt];
                                                   OptionString=CostPrice,GrossMin }
    { 220 ;   ;Appointment Letter Code;Code10     ;TableRelation="SO Appointment Letter";
                                                   OnValidate=VAR
                                                                ServiceOrder@1100528600 : Record 11012823;
                                                                SCCustSourceTypeSetup@1100528601 : Record 11071800;
                                                              BEGIN
                                                                IF "Appointment Letter Code" <> '' THEN
                                                                  IF ServiceOrder.GET("Service Order No.") THEN BEGIN
                                                                    SCCustSourceTypeSetup.GetAppointmentInfoByServiceOrder(ServiceOrder, SCCustSourceTypeSetup);
                                                                    SCCustSourceTypeSetup.TESTFIELD("Appointments by");
                                                                  END;
                                                              END;

                                                   CaptionML=[ENU=Appointment Letter;
                                                              NOR=Mõtebrev;
                                                              SVE=Mîtesbrev] }
    { 230 ;   ;Date Appointm. Confirm. Sent;Date  ;CaptionML=ENU=Date Appointment Confirmation Sent;
                                                   Editable=No }
    { 235 ;   ;Appointment Confirm. Sent by;Option;CaptionML=[ENU=Appointment Confirmation Sent by;
                                                              SVE=MîtesbekrÑftelse skickad av];
                                                   OptionCaptionML=[ENU=" ,Letter,E-Mail";
                                                                    SVE=" ,Brev,E-Post"];
                                                   OptionString=[ ,Letter,E-Mail];
                                                   Editable=No }
    { 240 ;   ;Expected Starting Date;Date        ;OnValidate=BEGIN
                                                                TestExpectedDateTimes;
                                                                VALIDATE("Expected Dates Modified on", CREATEDATETIME(TODAY, TIME));
                                                              END;

                                                   CaptionML=[ENU=Expected Starting Date;
                                                              NOR=Forventet startdato;
                                                              SVE=FîrvÑntat startdatum] }
    { 245 ;   ;Expected Starting Time;Time        ;OnValidate=BEGIN
                                                                TestExpectedDateTimes;
                                                                VALIDATE("Expected Dates Modified on", CREATEDATETIME(TODAY, TIME));
                                                              END;

                                                   CaptionML=ENU=Expected Starting Time }
    { 250 ;   ;Expected Ending Date;Date          ;OnValidate=BEGIN
                                                                TestExpectedDateTimes;
                                                                VALIDATE("Expected Dates Modified on", CREATEDATETIME(TODAY, TIME));
                                                              END;

                                                   CaptionML=[ENU=Expected Ending Date;
                                                              NOR=Forventet Sluttdato;
                                                              SVE=FîrvÑntat slutdatum] }
    { 255 ;   ;Expected Ending Time;Time          ;OnValidate=BEGIN
                                                                TestExpectedDateTimes;
                                                                VALIDATE("Expected Dates Modified on", CREATEDATETIME(TODAY, TIME));
                                                              END;

                                                   CaptionML=[ENU=Expected Ending Time;
                                                              SVE=FîrvÑntat sluttid] }
    { 260 ;   ;Expected Dates Modified on;DateTime;OnValidate=VAR
                                                                ServiceOrderStatusMail@1100409000 : Codeunit 11012816;
                                                              BEGIN
                                                                IF "Expected Dates Modified on" <> xRec."Expected Dates Modified on" THEN BEGIN
                                                                  MODIFY;
                                                                  ServiceOrderStatusMail.SetMailAction(3);
                                                                  ServiceOrderStatusMail.ServiceOrderMail("Service Order No.");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Expected Dates Modified on;
                                                              NOR=Forventede datoer modifisert den;
                                                              SVE=FîrvÑntade datum Ñndrade den];
                                                   Editable=No }
    { 270 ;   ;Resend Appointm. Confirmation;Boolean;
                                                   OnValidate=BEGIN
                                                                IF "Resend Appointm. Confirmation" THEN BEGIN
                                                                  CheckCustomerAppointmentSettings;
                                                                  TESTFIELD("Date Appointm. Confirm. Sent");
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Resend Appointment Confirmation }
    { 280 ;   ;Unit Price Set      ;Code20        ;OnValidate=VAR
                                                                ServiceUnitPriceSet@1100528600 : Record 11072284;
                                                              BEGIN
                                                                IF "Unit Price Set" <> '' THEN BEGIN
                                                                  ServOrderRec.GET("Service Order No.");
                                                                  ServiceUnitPriceSet.GET("Price Book Code", "Price Book Index Date", "Unit Price Set");
                                                                  IF ServOrderRec."Unit Price Status" > ServOrderRec."Unit Price Status"::Recording THEN
                                                                    ServOrderRec.FIELDERROR("Unit Price Status");
                                                                  IF ServOrderRec."Unit Price Status" = ServOrderRec."Unit Price Status"::" " THEN BEGIN
                                                                    ServOrderRec.VALIDATE("Unit Price Status", ServOrderRec."Unit Price Status"::"Pre-Recording");
                                                                    ServOrderRec.MODIFY(TRUE);
                                                                  END;
                                                                  CopyUnitPriceSetLines;
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              ServiceUnitPriceSet@1100528600 : Record 11072284;
                                                            BEGIN
                                                              ServiceUnitPriceSet.SETRANGE("Price Book Code", "Price Book Code");
                                                              ServiceUnitPriceSet.SETRANGE("Price Book Index Date", "Price Book Index Date");
                                                              IF PAGE.RUNMODAL(0, ServiceUnitPriceSet) = ACTION::LookupOK THEN
                                                                VALIDATE("Unit Price Set", ServiceUnitPriceSet.Code);
                                                            END;

                                                   CaptionML=[ENU=Unit Price Set;
                                                              SVE=A-pris grupp] }
    { 290 ;   ;Total Recording Amount;Decimal     ;FieldClass=FlowField;
                                                   CalcFormula=Sum("SUP Recording Line"."Recording Amount" WHERE (Service Order No.=FIELD(Service Order No.)));
                                                   CaptionML=ENU=Total Recording Amount;
                                                   Editable=No }
    { 300 ;   ;Service Planning Set;Code10        ;TableRelation="Service Planning Set";
                                                   OnValidate=VAR
                                                                ServiceOrder@1100528602 : Record 11012823;
                                                                ServicePlanningSet@1100528601 : Record 11072306;
                                                                SUPRecordingLine@1100528603 : Record 11071772;
                                                                ServiceUnitPriceManagement@1100528600 : Codeunit 11012846;
                                                              BEGIN
                                                                IF "Service Planning Set" <> '' THEN BEGIN
                                                                  ServiceOrder.DeleteRelatedWorkOrders("Service Order No.");
                                                                  SUPRecordingLine.SETRANGE("Service Order No.", "Service Order No.");
                                                                  IF NOT SUPRecordingLine.ISEMPTY THEN
                                                                    ERROR(Text005, FIELDCAPTION("Service Order No."), "Service Order No.");
                                                                  ServicePlanningSet.GET("Service Planning Set");
                                                                  ServiceOrder.GET("Service Order No.");
                                                                  IF ServiceOrder."Price Book Type" <> ServiceOrder."Price Book Type"::None THEN
                                                                    IF (ServicePlanningSet."Price Book Code" <> "Price Book Code") OR (ServicePlanningSet."Price Book Index Date" <> "Price Book Index Date") THEN
                                                                      ERROR(Text006, ServicePlanningSet.TABLECAPTION, ServiceOrder.TABLECAPTION);
                                                                  UpdateServOrderExtension("Service Order No.");
                                                                  ServiceUnitPriceManagement.ApplyPlanningSetToServiceOrder("Service Order No.", "Service Planning Set");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Service Planning Set;
                                                              SVE=Serviceplaneringsmallar] }
    { 310 ;   ;Service Location Struct. No.;Integer;
                                                   TableRelation="Service Location Structure".No. WHERE (Use for Service Order/Call=CONST(Yes));
                                                   OnValidate=VAR
                                                                ServiceOrder@1100528600 : Record 11012823;
                                                                ServiceLocationStructure@1100528601 : Record 11071719;
                                                              BEGIN
                                                                IF "Service Location Struct. No." <> 0 THEN
                                                                  IF ServiceOrder.GET("Service Order No.") THEN BEGIN
                                                                    ServiceLocationStructure.SETRANGE("Service Location No.", ServiceOrder."Service Location No.");
                                                                    ServiceLocationStructure.SETRANGE("No.", "Service Location Struct. No.");
                                                                    ServiceLocationStructure.SETRANGE("Use for Service Order/Call", TRUE);
                                                                    ServiceLocationStructure.FINDFIRST;
                                                                  END;
                                                                VALIDATE("Service Loc. Struct. Line No.", '');
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupServiceLocationStructNo;
                                                            END;

                                                   CaptionML=ENU=Service Location Struct. No.;
                                                   BlankZero=Yes }
    { 320 ;   ;Service Loc. Struct. Line No.;Code20;
                                                   TableRelation="Service Loc. Structure Line".No. WHERE (Structure No.=FIELD(Service Location Struct. No.),
                                                                                                          Object Line=CONST(No));
                                                   OnValidate=VAR
                                                                ServiceOrder@1100528600 : Record 11012823;
                                                                ServiceLocStructureLine@1100528601 : Record 11071720;
                                                              BEGIN
                                                                IF "Service Loc. Struct. Line No." <> '' THEN
                                                                  IF ServiceOrder.GET("Service Order No.") THEN
                                                                    ServiceLocStructureLine.GET(ServiceOrder."Service Location No.", "Service Location Struct. No.", "Service Loc. Struct. Line No.", '');
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupServiceLocStructLineNo;
                                                            END;

                                                   CaptionML=ENU=Service Location Structure Line No. }
    { 330 ;   ;Latitude            ;Code20        ;CaptionML=[ENU=Latitude;
                                                              NOR=Breddegrad;
                                                              SVE=Latitude] }
    { 340 ;   ;Longitude           ;Code20        ;CaptionML=[ENU=Longitude;
                                                              NOR=Lengdegrad;
                                                              SVE=Longitude] }
  }
  KEYS
  {
    {    ;Service Order No.                       ;Clustered=Yes }
    {    ;Price Book Code,Price Book Index Date    }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ServOrderRec@1210190000 : Record 11012823;
      ProjRec@1210190002 : Record 11072003;
      ProjElemRec@1210190003 : Record 11012010;
      Text000@1100528600 : TextConst 'ENU=%1 is not a valid %2.;NOR=%1 er ikke en gyldig %2.;SVE=%1 kan inte anvÑndas som %2.';
      ProjectSetup@1100529000 : Record 315;
      FormatMgt@1210190004 : Codeunit 11012022;
      Text002@1100525003 : TextConst 'ENU=%1 must be within %2 and %3';
      Text003@1100525002 : TextConst 'ENU=%1 cannot precede %2';
      Text004@1100525001 : TextConst 'ENU=%1 must be before %2';
      Text005@1100528601 : TextConst 'ENU=Pre-Recording Lines exist for %1 %2';
      Text006@1100528602 : TextConst 'ENU=Price Book of %1 doesn''t match Price Book of %2';
      ExtensionContract@1100527300 : Record 11012004;

    PROCEDURE GetServOrderExtension@1100525001(ServOrder@1100525001 : Code[20]);
    BEGIN
      IF NOT GET(ServOrder) THEN BEGIN
        INIT;
        "Service Order No."  := ServOrder;
      END;
      CALCFIELDS("Act. Ref. Point Ex. Comp. Dsc.");
    END;

    PROCEDURE UpdateServOrderExtension@1100525000(ServOrder@1100525000 : Code[20]);
    VAR
      ServOrderExtRec@1100525001 : Record 11071727;
      ServOrderExtInitRec@1100525003 : Record 11071727;
      DeleteExt@1100525004 : Boolean;
      JobRelatedServiceOrder@1100528200 : Record 11229339;
      Job@1100528201 : Record 11072003;
    BEGIN
      ServOrderExtInitRec.INIT;
      ServOrderExtInitRec."Service Order No."  := ServOrder;
      IF FORMAT(Rec) = FORMAT(ServOrderExtInitRec) THEN
        DeleteExt := TRUE
      ELSE
        DeleteExt := FALSE;

      IF NOT ServOrderExtRec.GET(ServOrder) THEN BEGIN
        IF DeleteExt THEN
          EXIT;
        ServOrderExtRec.INIT;
        ServOrderExtRec."Service Order No."  := ServOrder;
        ServOrderExtRec.INSERT(TRUE);
      END ELSE BEGIN
        IF DeleteExt THEN BEGIN
          ServOrderExtRec.DELETE(TRUE);
          EXIT;
        END;
      END;

      //In the following ServOrderExtRec.MODIFY the xREc is not available, therefore this check is required
      IF (ServOrderExtRec."Master Company" <> "Master Company") OR
         (ServOrderExtRec."Master Project" <> "Master Project") THEN BEGIN

        JobRelatedServiceOrder.CHANGECOMPANY(ServOrderExtRec."Master Company");
        IF JobRelatedServiceOrder.GET(ServOrderExtRec."Master Project", COMPANYNAME, "Service Order No.") THEN
          JobRelatedServiceOrder.DELETE;

        IF (ServOrderExtRec."Master Company" <> "Master Company") AND
           (ServOrderExtRec."Master Project" = "Master Project") THEN BEGIN
          "Master Project" := '';
          "Master Element" := '';
          "Master Extension Contract" := '';
        END;

        JobRelatedServiceOrder.CHANGECOMPANY("Master Company");
        Job.CHANGECOMPANY("Master Company");
        IF NOT JobRelatedServiceOrder.GET("Master Project", COMPANYNAME, "Service Order No.") THEN BEGIN
          IF Job.GET("Master Project") THEN BEGIN
            JobRelatedServiceOrder."Job No." := "Master Project";
            JobRelatedServiceOrder."Company Service Order" := COMPANYNAME;
            JobRelatedServiceOrder."Service Order No." := "Service Order No.";
            JobRelatedServiceOrder.INSERT;
          END;
        END;
      END;

      ServOrderExtRec.TRANSFERFIELDS(Rec, FALSE);
      ServOrderExtRec.MODIFY(TRUE);
    END;

    PROCEDURE CopyServOrderExtension@1100525002(FromServOrder@1100525000 : Code[20];ToServOrder@1100525002 : Code[20]);
    VAR
      FromServOrderExtRec@1100525004 : Record 11071727;
      ToServOrderExtRec@1100525005 : Record 11071727;
    BEGIN
      IF ToServOrderExtRec.GET(ToServOrder) THEN
        ToServOrderExtRec.DELETE(TRUE);
      IF FromServOrderExtRec.GET(FromServOrder) THEN BEGIN
        ToServOrderExtRec."Service Order No."  := ToServOrder;
        ToServOrderExtRec.TRANSFERFIELDS(FromServOrderExtRec, FALSE);
        //kkleermaker.sn, 110913
        ToServOrderExtRec."Executing Company Name" := '';
        ToServOrderExtRec."Order No. Executing Company" := '';
        ToServOrderExtRec."Principal Company Name" := '';
        ToServOrderExtRec."Serv. Contr. No. Princ. Comp." := '';
        ToServOrderExtRec."Act. Ref. Point Exec. Company" := 0;
        //kkleermaker.en, 110913
        ToServOrderExtRec.INSERT(TRUE);
      END;
    END;

    PROCEDURE ValidateUnitPriceFields@1100528600(VAR ServiceOrder@1100528600 : Record 11012823);
    VAR
      ServicePriceBook@1100528601 : Record 11071768;
    BEGIN
      IF ("Price Book Code" = '') THEN
        EXIT;

      ServicePriceBook.SETRANGE(Type, ServiceOrder."Price Book Type");
      ServicePriceBook.SETRANGE(Code, "Price Book Code");
      IF (NOT ServicePriceBook.FINDFIRST) THEN
        ERROR(Text000, "Price Book Code", FIELDCAPTION("Price Book Code"));

      IF "Price Book Code" = '' THEN
        IF ServiceOrder."Settlement Method" <> ServiceOrder."Settlement Method"::"Cost Plus" THEN
          ServiceOrder.VALIDATE("Settlement Method", ServiceOrder."Settlement Method"::"Cost Plus");
    END;

    PROCEDURE ValidateUnitPriceFieldsIncDate@1210190000(VAR ServiceOrder@1100528600 : Record 11012823);
    VAR
      ServicePriceBook@1100528601 : Record 11071768;
    BEGIN
      IF ("Price Book Code" = '') THEN
        EXIT;

      ServicePriceBook.SETRANGE(Type, ServiceOrder."Price Book Type");
      ServicePriceBook.SETRANGE(Code, "Price Book Code");
      ServicePriceBook.SETRANGE("Index Date", "Price Book Index Date");
      IF NOT ServicePriceBook.FINDFIRST THEN
        ERROR(Text000, "Price Book Index Date", FIELDCAPTION("Price Book Index Date"));

      IF  ServicePriceBook."Settlement Method" <> ServicePriceBook."Settlement Method"::" " THEN
        IF ServiceOrder."Settlement Method" <> ServicePriceBook."Settlement Method" -1 THEN
          ServiceOrder.VALIDATE("Settlement Method", ServicePriceBook."Settlement Method" -1);
    END;

    PROCEDURE ValidateMasterProject@1100528601(VAR OServiceOrder@1100528600 : Record 11012823);
    VAR
      ServiceSourceTypeSetup@1100528602 : Record 11071726;
    BEGIN
      IF "Master Project" <> '' THEN BEGIN
        CheckCompanyIC;

        IF CheckCustomerICDataAllowed THEN
          IF OServiceOrder."Customer No." = '' THEN BEGIN
            IF NOT ProjRec.GET("Master Project") THEN
              ProjRec.INIT;
            OServiceOrder.VALIDATE("Customer No.", ProjRec."Bill-to Customer No.");
          END;

        ServiceSourceTypeSetup.GET(OServiceOrder."Source Type");
        ServiceSourceTypeSetup.TESTFIELD("Service Type (Int. Charge)");
        ServiceSourceTypeSetup.TESTFIELD("Service Type (Other) (Int.Ch.)");

        OServiceOrder.VALIDATE("Service Type", ServiceSourceTypeSetup."Service Type (Int. Charge)");
        OServiceOrder.VALIDATE("Service Type (Other)", ServiceSourceTypeSetup."Service Type (Other) (Int.Ch.)");
      END;
    END;

    PROCEDURE TestExpectedDateTimes@1100525003();
    VAR
      ServiceOrder@1100525000 : Record 11012823;
    BEGIN
      ServiceOrder.GET("Service Order No.");
      IF (ServiceOrder."First Possible Starting Date" = 0D) AND (ServiceOrder."Last Possible Ending Date" = 0D) THEN
        EXIT;
      IF ("Expected Starting Date" = 0D) AND ("Expected Ending Date" = 0D) THEN
        EXIT;

      IF ("Expected Starting Date" <> 0D) AND
         (("Expected Starting Date" < ServiceOrder."First Possible Starting Date") OR
          ("Expected Starting Date" > ServiceOrder."Last Possible Ending Date"))
      THEN
        ERROR(Text002, FIELDCAPTION("Expected Starting Date"),
          ServiceOrder.FIELDCAPTION("First Possible Starting Date"), ServiceOrder.FIELDCAPTION("Last Possible Ending Date"));

      IF ("Expected Ending Date" <> 0D) AND
         (("Expected Ending Date" < ServiceOrder."First Possible Starting Date") OR
          ("Expected Ending Date" > ServiceOrder."Last Possible Ending Date"))
      THEN
        ERROR(Text002, FIELDCAPTION("Expected Ending Date"),
          ServiceOrder.FIELDCAPTION("First Possible Starting Date"), ServiceOrder.FIELDCAPTION("Last Possible Ending Date"));

      IF ("Expected Starting Date" <> 0D) AND
         ("Expected Starting Time" <> 0T) AND
         (ServiceOrder."First Possible Starting Time" <> 0T) AND
         ("Expected Starting Date" = ServiceOrder."First Possible Starting Date") AND
         ("Expected Starting Time" < ServiceOrder."First Possible Starting Time")
      THEN
        ERROR(Text003, FIELDCAPTION("Expected Starting Time"), ServiceOrder.FIELDCAPTION("First Possible Starting Time"));

      IF ("Expected Ending Date" <> 0D) AND
         ("Expected Ending Time" <> 0T) AND
         (ServiceOrder."Last Possible Ending Time" <> 0T) AND
         ("Expected Ending Date" = ServiceOrder."Last Possible Ending Date") AND
         ("Expected Ending Time" > ServiceOrder."Last Possible Ending Time")
      THEN
        ERROR(Text004, FIELDCAPTION("Expected Ending Time"), ServiceOrder.FIELDCAPTION("Last Possible Ending Time"));
    END;

    PROCEDURE LookupUnitPriceIndexDate@1100528602();
    VAR
      ServiceOrder@1100528601 : Record 11012823;
      ServiceUnitPrice@1100528600 : Record 11071768;
    BEGIN
      ServiceOrder.GET("Service Order No.");
      ServiceUnitPrice.SETRANGE(Type, ServiceOrder."Price Book Type");
      ServiceUnitPrice.SETRANGE(Code, "Price Book Code");
      IF PAGE.RUNMODAL(0, ServiceUnitPrice) = ACTION::LookupOK THEN
        VALIDATE("Price Book Index Date", ServiceUnitPrice."Index Date");
    END;

    LOCAL PROCEDURE CopyUnitPriceSetLines@1100528604();
    VAR
      ServiceUnitPriceSetLine@1100528600 : Record 11072285;
      SUPRecordingLine@1100528601 : Record 11071772;
      LineNo@1100528602 : Integer;
    BEGIN
      ServOrderRec.GET("Service Order No.");

      SUPRecordingLine.SETRANGE("Service Order No.", "Service Order No.");
      IF NOT SUPRecordingLine.ISEMPTY THEN
        ERROR(Text005, FIELDCAPTION("Service Order No."), "Service Order No.");
      ServiceUnitPriceSetLine.SETRANGE("Price Book Code", "Price Book Code");
      ServiceUnitPriceSetLine.SETRANGE("Price Book Index Date", "Price Book Index Date");
      ServiceUnitPriceSetLine.SETRANGE("Set Code", "Unit Price Set");
      IF ServiceUnitPriceSetLine.FINDSET THEN
        REPEAT
          LineNo += 10000;
          SUPRecordingLine.INIT;
          SUPRecordingLine."Service Order No." := "Service Order No.";
          SUPRecordingLine."Line No." := LineNo;
          SUPRecordingLine."Price Book Type" := ServOrderRec."Price Book Type";
          SUPRecordingLine."Price Book Code" := "Price Book Code";
          SUPRecordingLine."Index Date" := "Price Book Index Date";
          SUPRecordingLine."Unit Price Code" := '';  //db, 31-01-19: skip message-check (Text009) on validate if more then 1 record should be processed
          SUPRecordingLine.VALIDATE("Unit Price Code", ServiceUnitPriceSetLine."Unit Price Code");
          SUPRecordingLine.VALIDATE("Pre-Recording Quantity", ServiceUnitPriceSetLine.Quantity);
          SUPRecordingLine."Base Service Order No." := ServOrderRec."Base Service Order No.";
          SUPRecordingLine.INSERT(TRUE);
        UNTIL ServiceUnitPriceSetLine.NEXT = 0;
    END;

    LOCAL PROCEDURE CheckCustomerAppointmentSettings@1100528603();
    VAR
      ServiceOrder@1100528601 : Record 11012823;
      SCCustSourceTypeSetup@1100528600 : Record 11071800;
    BEGIN
      IF NOT ServiceOrder.GET("Service Order No.") THEN
        EXIT;
      IF NOT SCCustSourceTypeSetup.GET(SCCustSourceTypeSetup.Type::Customer, ServiceOrder."Customer No.", ServiceOrder."Source Type") THEN
        SCCustSourceTypeSetup.INIT;
      IF SCCustSourceTypeSetup."Appointments by" = SCCustSourceTypeSetup."Appointments by"::" " THEN
        SCCustSourceTypeSetup.FIELDERROR("Appointments by");
    END;

    PROCEDURE CheckCompanyIC@1210190001();
    VAR
      lvCompName@1210190027 : Text[50];
    BEGIN
      IF "Master Company" <> '' THEN
        lvCompName := "Master Company"
      ELSE
        lvCompName := COMPANYNAME;

      ProjectSetup.CHANGECOMPANY(lvCompName);
      ProjRec.CHANGECOMPANY(lvCompName);
      ProjElemRec.CHANGECOMPANY(lvCompName);
      ExtensionContract.CHANGECOMPANY(lvCompName);
    END;

    PROCEDURE OnLookupJobNo@1100525012(EditableMode@1100525000 : Boolean);
    VAR
      PageNo@1100525001 : Integer;
    BEGIN
      CheckCompanyIC;
      ProjRec."No." := "Master Project";
      IF RefersToOtherCompany THEN
        PageNo := PAGE::"Job List IC";
      IF NOT EditableMode THEN
        PAGE.RUNMODAL(PageNo, ProjRec)
      ELSE BEGIN
        IF PAGE.RUNMODAL(PageNo, ProjRec) = ACTION::LookupOK THEN
          VALIDATE("Master Project", ProjRec."No.");
      END;
    END;

    LOCAL PROCEDURE CheckCustomerICDataAllowed@1100529410() : Boolean;
    VAR
      CentralDataManagement@1100529400 : Codeunit 11012010;
      MarketingSetup@1100529401 : Record 5079;
    BEGIN
      IF NOT RefersToOtherCompany THEN
        EXIT(TRUE);
      IF NOT CentralDataManagement.CentralActive() THEN
        EXIT(FALSE);
      MarketingSetup.GET;
      IF MarketingSetup.Customers THEN
        EXIT(TRUE);
    END;

    PROCEDURE RefersToOtherCompany@1100525008() : Boolean;
    BEGIN
      EXIT(("Master Company" <> '') AND ("Master Company" <> COMPANYNAME));
    END;

    PROCEDURE LookupServicePlanningSet@1100528605();
    VAR
      ServiceOrder@1100528601 : Record 11012823;
      ServicePlanningSet@1100528600 : Record 11072306;
    BEGIN
      ServiceOrder.GET("Service Order No.");
      IF ServiceOrder."Price Book Type" <> ServiceOrder."Price Book Type"::None THEN BEGIN
        ServicePlanningSet.SETRANGE("Price Book Code", "Price Book Code");
        ServicePlanningSet.SETRANGE("Price Book Index Date", "Price Book Index Date");
      END;
      IF PAGE.RUNMODAL(0, ServicePlanningSet) = ACTION::LookupOK THEN
        VALIDATE("Service Planning Set", ServicePlanningSet.Code);
    END;

    PROCEDURE LookupServiceLocationStructNo@1100528606();
    VAR
      ServiceOrder@1100528600 : Record 11012823;
      ServiceLocationStructure@1100528601 : Record 11071719;
    BEGIN
      IF ServiceOrder.GET("Service Order No.") THEN BEGIN
        ServiceLocationStructure.SETRANGE("Service Location No.", ServiceOrder."Service Location No.");
        ServiceLocationStructure.SETRANGE("Use for Service Order/Call", TRUE);
        IF "Service Location Struct. No." <> 0 THEN
          IF ServiceLocationStructure.GET(ServiceOrder."Service Location No.", "Service Location Struct. No.") THEN;
        IF PAGE.RUNMODAL(0, ServiceLocationStructure) = ACTION::LookupOK THEN
          VALIDATE("Service Location Struct. No.", ServiceLocationStructure."No.");
      END;
    END;

    PROCEDURE LookupServiceLocStructLineNo@1100528607();
    VAR
      ServiceOrder@1100528601 : Record 11012823;
      ServiceLocStructureLine@1100528600 : Record 11071720;
    BEGIN
      IF ServiceOrder.GET("Service Order No.") THEN BEGIN
        ServiceLocStructureLine.SETRANGE("Service Location No.", ServiceOrder."Service Location No.");
        ServiceLocStructureLine.SETRANGE("Structure No.", "Service Location Struct. No.");
        ServiceLocStructureLine.SETRANGE("Object Line", FALSE);
        IF "Service Loc. Struct. Line No." <> '' THEN
          IF ServiceLocStructureLine.GET(ServiceOrder."Service Location No.", "Service Location Struct. No.", "Service Loc. Struct. Line No.", '') THEN;
        IF PAGE.RUNMODAL(0, ServiceLocStructureLine) = ACTION::LookupOK THEN
          VALIDATE("Service Loc. Struct. Line No.", ServiceLocStructureLine."No.");
      END;
    END;

    PROCEDURE UpdateLongitudeLatitude@1100525052() : Boolean;
    VAR
      BingMapsHandler@1100525000 : Codeunit 11012485;
      tempLatitude@1100525001 : Text[20];
      tempLongitude@1100525002 : Text[20];
      InstallationManagement@1100525005 : Codeunit 11012268;
      ServiceOrder@1100525003 : Record 11012823;
      ServiceLocation@1100525004 : Record 11012801;
    BEGIN
      IF NOT InstallationManagement.LicensePermissionForTableData(DATABASE::"Bing Maps Setup") THEN
        EXIT;

      IF NOT BingMapsHandler.SetupIsDoneAndEnabled THEN
        EXIT;

      IF ServiceOrder.GET("Service Order No.") THEN
        IF ServiceOrder."Service Location No." <> '' THEN BEGIN
          ServiceLocation.GET(ServiceOrder."Service Location No.");
          Latitude := ServiceLocation.Latitude;
          Longitude := ServiceLocation.Longitude;
          MODIFY;
          EXIT(TRUE);
        END ELSE
          IF BingMapsHandler.GetLatLngFromAddress(tempLatitude, tempLongitude, ServiceOrder.Address, ServiceOrder.City, ServiceOrder."Post Code") THEN BEGIN
            Latitude := tempLatitude;
            Longitude := tempLongitude;
            MODIFY;
            EXIT(TRUE);
          END;
        // ELSE BingMapsHandler.LocationError(ServiceOrder.TABLECAPTION, ServiceOrder."No."); C047603.o
    END;

    BEGIN
    END.
  }
}

