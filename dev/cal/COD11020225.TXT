OBJECT Codeunit 11020225 Build Purchase Control
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=VAR
            Project@1100525000 : Record 11072003;
          BEGIN
            BuildPurchaseControl(Project);
          END;

  }
  CODE
  {
    VAR
      RunInCompanyName@1100529600 : Text[30];

    PROCEDURE BuildPurchaseControl@1100525001(VAR Project@1100525000 : Record 11072003);
    VAR
      Project2@1100525001 : Record 11072003;
    BEGIN
      Project2.COPY(Project);
      IF Project2.FINDSET THEN
        REPEAT
          BuildProjPurchControl(Project2);
        UNTIL Project2.NEXT = 0;
    END;

    PROCEDURE BuildProjPurchControl@1100525000(Project@1100525000 : Record 11072003);
    VAR
      OrderControlLineToBuild@1100525003 : Record 11020592;
      PurchLine@1100525002 : Record 39;
      PurchOrderControlLine@1100525001 : Record 11020221;
    BEGIN
      IF RunInCompanyName <> '' THEN BEGIN
        OrderControlLineToBuild.CHANGECOMPANY(RunInCompanyName);
        PurchLine.CHANGECOMPANY(RunInCompanyName);
        PurchOrderControlLine.CHANGECOMPANY(RunInCompanyName);
      END;

      SetPurchProjControlFilters(Project, OrderControlLineToBuild);
      IF OrderControlLineToBuild.FINDSET THEN BEGIN
        REPEAT
          IF PurchLine.GET(
               PurchLine."Document Type"::Order, OrderControlLineToBuild."Order No.", OrderControlLineToBuild."Order Line No.") THEN
            PurchOrderControlLine.CreatePurchOrderControlLines(PurchLine,FALSE,TRUE)
          ELSE BEGIN
            PurchLine.INIT;
            PurchLine."Document Type" := PurchLine."Document Type"::Order;
            PurchLine."Document No." := OrderControlLineToBuild."Order No.";
            PurchLine."Line No." := OrderControlLineToBuild."Order Line No.";
            PurchOrderControlLine.CreatePurchOrderControlLines(PurchLine,TRUE,TRUE); //DeleteOnly
          END;
        UNTIL OrderControlLineToBuild.NEXT = 0;
        OrderControlLineToBuild.DELETEALL;
      END;
    END;

    PROCEDURE ProjPurchControlNeedsBuild@1210190000(Project@1210190000 : Record 11072003) : Boolean;
    VAR
      OrderControlLineToBuild@1210190001 : Record 11020592;
    BEGIN
      SetPurchProjControlFilters(Project, OrderControlLineToBuild);
      EXIT(NOT OrderControlLineToBuild.ISEMPTY);
    END;

    LOCAL PROCEDURE SetPurchProjControlFilters@1210190001(Project@1210190001 : Record 11072003;VAR OrderControlLineToBuild@1210190000 : Record 11020592);
    BEGIN
      IF Project."Single/Main/Sub Project" = Project."Single/Main/Sub Project"::"Main Project" THEN BEGIN
        OrderControlLineToBuild.SETCURRENTKEY("Main Project No.");
        OrderControlLineToBuild.SETRANGE("Main Project No.", Project."Main Project");
      END ELSE BEGIN
        OrderControlLineToBuild.SETCURRENTKEY("Project No.");
        OrderControlLineToBuild.SETRANGE("Project No.", Project."No.");
      END;
    END;

    PROCEDURE SetRunInCompanyName@1100529600(RunInCompanyNameNew@1100529600 : Text[30]);
    BEGIN
      RunInCompanyName := RunInCompanyNameNew;
    END;

    BEGIN
    END.
  }
}

