OBJECT Codeunit 11012281 DropZone Management
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      RecRefNotValidTxt@1100528300 : TextConst 'DEU=%1 kann nicht gefunden werden.\M”glicherweise mssen Sie den Datensatz zuerst speichern.;ENU=%1 can not be found.\Possibly, you have to write the record first.;NLD=%1 kan niet gevonden worden.\Mogelijk moet het record eerst weggeschreven worden.';
      Text000@1100525000 : TextConst 'DEU=Beleg wurde nicht korrekt registriert.;ENU=Document is not registered correctly.;NLD=Documenten is niet goed geregistreerd.';
      Text001@1100525001 : TextConst 'DEU=In dieser Version k”nnen aktuell nicht mehr als 5 Belege hochgeladen werden.;ENU=It is currently not possible in this release, to upload more than 5 documents.;NLD=Het is in deze release niet mogelijk om meer dan 5 documenten te laden.';
      Text002@1100528701 : TextConst 'DEU=%1 externe Belege sind registriert worden.;ENU=%1 external documents are registered.;NLD=%1 externe documenten zijn geregistreerd.;NOR=%1 eksterne dokumenter er registrert.;SVE=%1 externa dokument har registrerats.';
      Text003@1100528700 : TextConst 'DEU=%2 externe Belege sind nicht richtig registriert worden.;ENU=%2 external documents are not registered correctly.;NLD=%2 externe documenten zijn niet goed geregistreerd.;NOR=%2 eksterne dokumenter er ikke korrekt registrert.;SVE=%2 externa dokument „r inte korrekt registrerade.';
      RecReference@1100528705 : RecordRef;
      DepartmentCode@1100528704 : Code[20];
      CanCopy@1100528703 : Boolean;
      ByCompany@1100528702 : Boolean;
      DocLinkRIDArray@1100528504 : ARRAY [5] OF RecordID;
      Job@1100528503 : Record 11072003;
      HasDPTProject@1100528502 : Boolean;
      HasPPTProject@1100528501 : Boolean;
      ServiceLocation@1100528500 : Record 11012801;
      RecReferenceDPT@1100528505 : RecordRef;

    PROCEDURE SetRecordReference@1100528105(RecRef@1100528100 : RecordRef);
    BEGIN
      RecReference := RecRef;
    END;

    PROCEDURE SetDepartmentCode@1100525000(NewValue@1100525000 : Code[20]);
    BEGIN
      DepartmentCode := NewValue;
    END;

    PROCEDURE SetCanCopy@1100525001(NewValue@1100525000 : Boolean);
    BEGIN
      CanCopy := NewValue;
    END;

    PROCEDURE SetByCompany@1100525002(NewValue@1100525000 : Boolean);
    BEGIN
      ByCompany := NewValue;
    END;

    PROCEDURE "-"@1100528701();
    BEGIN
    END;

    PROCEDURE InitQueue@1100528700();
    VAR
      QueuedExternalDocument@1100528701 : Record 11012148;
      QueuedDocumentLink@1100528702 : Record 11125736;
    BEGIN
      QueuedExternalDocument.SETRANGE(User, USERID);
      QueuedExternalDocument.DELETEALL(TRUE);
      QueuedDocumentLink.SETRANGE(User, USERID);
      QueuedDocumentLink.DELETEALL(TRUE);
      COMMIT;
      CreateAdditionalDocumentLinks;
      SetGlobalDocumentPortalVariables;
    END;

    PROCEDURE RegisterQueuedDocuments@1100528101();
    VAR
      InitialDocumentProperties@1100528106 : Record 11012746;
      DocumentProperties@1100528107 : Record 11012746;
      QueuedExternalDocument@1100528108 : Record 11012148;
      ErrorRec@1100528112 : Record 11012051;
      TempBlob@1100525002 : TEMPORARY Record 99008535;
      FileManagement@1100529900 : Codeunit 419;
      ErrorMessage@1100528109 : Text[250];
      RegisterAsEngDoc@1100528103 : Boolean;
      Relate@1100528102 : Boolean;
      RelateToType@1100528101 : 'Document,Purchase Quote,Purchase Order,Try-out Quote,Blanket Order,,,,Service Order,Sales Invoice,Sales Cr.Memo,Posted Sales Invoice,Posted Sales Cr.Memo';
      RelateToCode@1100528100 : Code[20];
      NoOfSucc@1100528111 : Integer;
      NoOfFailed@1100528110 : Integer;
      NoOfDocuments@1100528113 : Integer;
      ExternalDocument@1100528700 : Page 11012417;
      DocumentLinkManagement@1100528500 : Codeunit 11012401;
    BEGIN
      CheckQueuedDocumentLinks;
      InitialDocumentProperties.INIT;
      InitialDocumentProperties."External Document" := TRUE;
      InitialDocumentProperties."Internal Company" := COMPANYNAME;
      InitialDocumentProperties.SetDPTFields(RecReference.RECORDID);
      RegisterAsEngDoc := FALSE;
      Relate := FALSE;
      RelateToType := 0;
      RelateToCode := '';
      DocumentProperties.INIT;

      QueuedExternalDocument.SETRANGE(User, USERID);
      NoOfDocuments := QueuedExternalDocument.COUNT;
      IF QueuedExternalDocument.FINDSET THEN BEGIN
        IF (HasPPTProject OR HasDPTProject) THEN BEGIN //#C031444.c
          CheckAndFillDocumentPropertiesDPT(InitialDocumentProperties);
          IF (HasPPTProject) AND (NoOfDocuments > 5) THEN //#C031444.c
            ERROR(Text001);
        END ELSE BEGIN //#C031444.c
          IF (NoOfDocuments > 1) AND (FileManagement.IsWindowsClient) THEN BEGIN
            GetCommonDocumentPropertiesFromQueue(InitialDocumentProperties);
            CheckAndFillCommonDocumentProperties(InitialDocumentProperties);
          END;
        END;
        REPEAT
          IF (InitialDocumentProperties."Project Portal Subsite" <> '') THEN BEGIN
            QueuedExternalDocument."Contact No." := '';
            QueuedExternalDocument."Contact Person No." := '';
          END;

          IF NOT QueuedExternalDocument.RegisterDocument(InitialDocumentProperties,
              Relate, RelateToType, RelateToCode, ErrorMessage, DocumentProperties) THEN BEGIN
            StoreErrorMessage(ErrorMessage, DocumentProperties);
            NoOfFailed += 1;
          END ELSE BEGIN
            DocumentProperties.CreateDocumentRelationForLinkedDocuments;
            IF (NOT HasPPTProject) AND (NOT HasDPTProject) AND
               (NoOfDocuments = 1) AND (FileManagement.IsWindowsClient)
            THEN BEGIN //#C031444.c
              COMMIT;
              ExternalDocument.SETRECORD(DocumentProperties);
              ExternalDocument.RUNMODAL;
              CLEAR(ExternalDocument);
            END;
          END;
          NoOfSucc += 1;
          QueuedExternalDocument.DELETE(TRUE);
          COMMIT;
        UNTIL QueuedExternalDocument.NEXT = 0;
      END;
      QueuedExternalDocument.DELETEALL(TRUE);

      IF NOT ((NoOfFailed = 0) AND (NoOfSucc = 1)) THEN BEGIN
        MESSAGE(Text002 + '\' + Text003, NoOfSucc, NoOfFailed);
        ErrorRec.SETRANGE("User ID", USERID);
        ErrorRec.SETRANGE("Source Type", ErrorRec."Source Type"::"Read Documents");
        IF ErrorRec.FINDFIRST THEN BEGIN
          COMMIT;
          PAGE.RUN(PAGE::Errors, ErrorRec);
        END;
      END;
    END;

    PROCEDURE CreateDocumentLinkInQueue@1100528102(RecRef@1100528100 : RecordRef);
    VAR
      QueuedDocumentLink@1100528102 : Record 11125736;
      RID@1100528101 : RecordID;
      SeqNo@1100528103 : Integer;
      DocumentLinkMgt@1100528104 : Codeunit 11012401;
    BEGIN
      RID := RecRef.RECORDID;

      QueuedDocumentLink.SETRANGE(User, USERID);
      IF QueuedDocumentLink.FINDLAST THEN
        SeqNo := QueuedDocumentLink."Seq. No." + 10000
      ELSE
        SeqNo := 10000;

      QueuedDocumentLink.INIT;
      QueuedDocumentLink.User := USERID;
      QueuedDocumentLink."Seq. No." := SeqNo;
      QueuedDocumentLink."Table No." := RID.TABLENO;
      QueuedDocumentLink."Filter Expression" := DocumentLinkMgt.GetFilterExpression(RID);
      QueuedDocumentLink."Record ID" := RID;
      QueuedDocumentLink.Source := FALSE;
      QueuedDocumentLink.INSERT;
    END;

    PROCEDURE CreateDocumentInQueue@1100528103(FileNames@1100528100 : Text[1024];ContactNo@1100409001 : Code[20];Description@1100409000 : Text[50];AddressedTo@1100528701 : Code[20];DocumentDate@1100528700 : Date);
    VAR
      QueuedExternalDocument@1100528102 : Record 11012148;
      Contact@1100409002 : Record 5050;
    BEGIN
      SplitFileName(FileNames, QueuedExternalDocument.Folder, QueuedExternalDocument.Description);

      QueuedExternalDocument.User := USERID;
      // IF COPYSTR(QueuedExternalDocument.Folder, STRLEN(QueuedExternalDocument.Folder), 1) <> '\' THEN
      //  QueuedExternalDocument.Folder := QueuedExternalDocument.Folder + '\';
      QueuedExternalDocument.Source := QueuedExternalDocument.Source::System;
      IF ContactNo = '' THEN
        ContactNo := GetContactFromQueuedDocument;
      IF Contact.GET(ContactNo) THEN BEGIN
        IF NOT Contact.IsContactPerson THEN
          QueuedExternalDocument.VALIDATE("Contact No.", Contact."No.")
        ELSE BEGIN
          QueuedExternalDocument.VALIDATE("Contact No.", Contact."Company No.");
          QueuedExternalDocument.VALIDATE("Contact Person No.", Contact."No.");
        END;
      END;
      QueuedExternalDocument.Description := Description;
      QueuedExternalDocument."Addressed to" := AddressedTo;
      QueuedExternalDocument."Document Date" := DocumentDate;
      QueuedExternalDocument.INSERT(TRUE);
    END;

    PROCEDURE CreateContentInQueue@1100525004(Base64EncodedContent@1100525000 : Text;FileName@1100525001 : Text;ContactNo@1100527803 : Code[20];AddressedTo@1100527802 : Code[20];DocumentDate@1100525002 : Date);
    VAR
      DocumentMgtSetup@1100525009 : Record 11071831;
      QueuedExternalDocument@1100525003 : Record 11012148;
      OStream@1100525004 : OutStream;
      Contact@1100525011 : Record 5050;
      DocumentManagement@1100527800 : Codeunit 11012406;
      Base64Big@1100527801 : BigText;
    BEGIN
      QueuedExternalDocument.User := USERID;
      QueuedExternalDocument.Content.CREATEOUTSTREAM(OStream);
      Base64Big.ADDTEXT(Base64EncodedContent);
      Base64Big.WRITE(OStream);

      QueuedExternalDocument.Source := QueuedExternalDocument.Source::System;
      QueuedExternalDocument.Description := COPYSTR(FileName, 1, MAXSTRLEN(QueuedExternalDocument.Description));
      QueuedExternalDocument."File Name" := FileName;

      IF ContactNo = '' THEN
        ContactNo := GetContactFromQueuedDocument;
      IF Contact.GET(ContactNo) THEN BEGIN
        IF NOT Contact.IsContactPerson THEN
          QueuedExternalDocument.VALIDATE("Contact No.", Contact."No.")
        ELSE BEGIN
          QueuedExternalDocument.VALIDATE("Contact No.", Contact."Company No.");
          QueuedExternalDocument.VALIDATE("Contact Person No.", Contact."No.");
        END;
      END;

      QueuedExternalDocument."Addressed to" := AddressedTo;
      QueuedExternalDocument."Document Date" := DocumentDate;
      QueuedExternalDocument.INSERT(TRUE);
    END;

    PROCEDURE StoreErrorMessage@5(ErrorMessage@11012001 : Text[250];DocumentProperties@1100525000 : Record 11012746);
    VAR
      Error@1210190000 : Record 11012051;
      LineNo@11012002 : Integer;
    BEGIN
      Error.SETRANGE("User ID", USERID);
      Error.SETRANGE("Source Type", Error."Source Type"::"Read Documents");
      IF Error.FINDLAST THEN
        LineNo := Error."Line No." + 1
      ELSE
        LineNo := 1;

      Error.INIT;
      Error."User ID" := USERID;
      Error."Source Type" := Error."Source Type"::"Read Documents";
      Error."Line No." := LineNo;
      Error.Description := DocumentProperties.Description;
      Error."Error message" := ErrorMessage;
      Error."Document No." := DocumentProperties."No.";
      Error.INSERT;
    END;

    PROCEDURE SplitFileName@1100525008(FullPath@1100525000 : Text[1024];VAR DirectoryName@1100525001 : Text[1024];VAR FileName@1100525002 : Text[1024]);
    VAR
      FileManagement@1100525004 : Codeunit 419;
    BEGIN
      DirectoryName := FileManagement.GetDirectoryNameWithBackSlash(FullPath);
      FileName := FileManagement.GetFileName(FullPath);
    END;

    PROCEDURE GetContactFromEMail@1100528704(EMail@1100528700 : Text[1024];VAR ContactNo@1100528701 : Code[20]) : Boolean;
    VAR
      Contact@1100528707 : Record 5050;
      StringFunctions@1100528705 : Codeunit 11012273;
      EMailArr@1100528704 : ARRAY [10] OF Text[250];
      I@1100528703 : Integer;
    BEGIN
      StringFunctions.SplitString(EMail, ';', EMailArr);
      I := 1;
      REPEAT
        IF EMailArr[I] = '' THEN
          EXIT(FALSE)
        ELSE BEGIN
          Contact.SETCURRENTKEY("Search E-Mail");
          Contact.SETRANGE("Search E-Mail", UPPERCASE(EMailArr[I]));
          IF Contact.FINDFIRST THEN BEGIN
            ContactNo := Contact."No.";
            EXIT(TRUE);
          END;
        END;
        I += 1;
      UNTIL I > ARRAYLEN(EMailArr);
    END;

    PROCEDURE GetContactFromQueuedDocument@1100409000() : Code[20];
    VAR
      QueuedDocumentLink@1100409003 : Record 11125736;
      RID@1100409000 : RecordID;
      RecRef@1100409001 : RecordRef;
      Contact@1100409002 : Record 5050;
      Vendor@1100409004 : Record 23;
      Customer@1100409005 : Record 18;
      ServiceCall@1100529900 : Record 11012822;
      ServiceOrder@1100529901 : Record 11012823;
    BEGIN
      QueuedDocumentLink.SETRANGE(User, USERID);
      IF QueuedDocumentLink.FINDFIRST THEN BEGIN
        CASE QueuedDocumentLink."Table No." OF
          DATABASE::Contact:
            BEGIN
              RID := QueuedDocumentLink."Record ID";
              RecRef := RID.GETRECORD;
              IF RecRef.FIND THEN BEGIN //C039264.n
                RecRef.SETTABLE(Contact);
                EXIT(Contact."No.");
              END; //C039264.n
            END;
          DATABASE::Vendor:
            BEGIN
              RID := QueuedDocumentLink."Record ID";
              RecRef := RID.GETRECORD;
              IF RecRef.FIND THEN BEGIN //C039264.n
                RecRef.SETTABLE(Vendor);
                IF Contact.GetContactByVendor(Vendor."No.", Contact, FALSE) THEN
                  EXIT(Contact."No.");
              END; //C039264.n
            END;
          DATABASE::Customer:
            BEGIN
              RID := QueuedDocumentLink."Record ID";
              RecRef := RID.GETRECORD;
              IF RecRef.FIND THEN BEGIN //C039264.n
                RecRef.SETTABLE(Customer);
                IF Contact.GetContactByCustomer(Customer."No.", Contact, FALSE) THEN
                  EXIT(Contact."No.");
              END; //C039264.n
            END;
      //C039264.sn
          DATABASE::"Service Call":
            BEGIN
              RID := QueuedDocumentLink."Record ID";
              RecRef := RID.GETRECORD;
              IF RecRef.FIND THEN BEGIN //C039264.n
                RecRef.SETTABLE(ServiceCall);
                IF Contact.GetContactByCustomer(ServiceCall."Customer No.", Contact, FALSE) THEN
                  EXIT(Contact."No.");
              END; //C039264.n
           END;
          DATABASE::"Service Order":
            BEGIN
              RID := QueuedDocumentLink."Record ID";
              RecRef := RID.GETRECORD;
              IF RecRef.FIND THEN BEGIN
                RecRef.SETTABLE(ServiceOrder);
                IF Contact.GetContactByCustomer(ServiceOrder."Customer No.", Contact, FALSE) THEN
                  EXIT(Contact."No.");
              END;
            END;
      //C039264.en
        END;
      END;
    END;

    PROCEDURE "--"@1100528702();
    BEGIN
    END;

    PROCEDURE ShowDocuments@1100528703();
    VAR
      DocumentFilters@1100528700 : Codeunit 11012432;
    BEGIN
      DocumentFilters.CreateTempFilterAndExecute2(
        RecReference, DepartmentCode, CanCopy, ByCompany, FORMAT(TRUE));
    END;

    LOCAL PROCEDURE CheckAndFillDocumentPropertiesDPT@1100525003(VAR DocumentProperties@1100525000 : Record 11012746) : Boolean;
    VAR
      Job@1100525003 : Record 11072003;
      FillDocumentProperties@1100528300 : Page 11072416;
      DPTFillDropZoneFields@1100528301 : Page 11229622;
      SharePointSetup4PS@1100528500 : Record 11012730;
      DPTSetup@1100528501 : Record 11229823;
    BEGIN
      DocumentProperties.SetDPTFields(RecReferenceDPT.RECORDID);
      IF (NOT HasPPTProject AND NOT HasDPTProject) THEN
        EXIT(FALSE);

      IF (HasPPTProject) THEN BEGIN
        IF (SharePointSetup4PS.GET) THEN
          DocumentProperties.DPTPublish := (SharePointSetup4PS."Document Version" = SharePointSetup4PS."Document Version"::"Major Version");

        FillDocumentProperties.SetVars(Job."No.", 0, DocumentProperties."Project Portal Subsite",
          DocumentProperties."Document Type", DocumentProperties."Contact No.", DocumentProperties."Contact Person No.",
          DocumentProperties."Private Company Document", DocumentProperties."Project Element",DocumentProperties.DPTPublish);
        IF (FillDocumentProperties.RUNMODAL = ACTION::OK) THEN BEGIN
          FillDocumentProperties.GetVars(DocumentProperties."Project Portal Subsite", DocumentProperties."Document Type",
            DocumentProperties."Contact No.", DocumentProperties."Contact Person No.",
            DocumentProperties."Private Company Document", DocumentProperties."Project Element",DocumentProperties.DPTPublish);
        END;
        IF (DocumentProperties."Project Portal Subsite" = '') OR (DocumentProperties."Document Type" = '') THEN
          ERROR(Text000);
      END ELSE BEGIN
        IF (DPTSetup.GET) THEN
          DocumentProperties.DPTPublish := (DPTSetup."Document Version" = DPTSetup."Document Version"::"Major Version");

        DPTFillDropZoneFields.SetVars(DocumentProperties."Document Portal Source Type",
          DocumentProperties."Document Portal Source No.", DocumentProperties."Document Portal Subsite",
          DocumentProperties."Document Type", DocumentProperties."Contact No.", DocumentProperties."Contact Person No.",
          DocumentProperties."Document Portal Private Doc", DocumentProperties."Project Element",DocumentProperties.DPTPublish);
        IF (DPTFillDropZoneFields.RUNMODAL = ACTION::OK) THEN BEGIN
          DPTFillDropZoneFields.GetVars(DocumentProperties."Document Portal Subsite", DocumentProperties."Document Type",
            DocumentProperties."Contact No.", DocumentProperties."Contact Person No.",
            DocumentProperties."Document Portal Private Doc", DocumentProperties."Project Element",DocumentProperties.DPTPublish);
        END;
        IF (DocumentProperties."Document Portal Subsite" = '') OR (DocumentProperties."Document Type" = '') THEN
          ERROR(Text000);
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE GetCommonDocumentPropertiesFromQueue@1100528708(VAR DocumentProperties@1100528700 : Record 11012746);
    VAR
      QueuedExternalDocument@1100528701 : Record 11012148;
      QueuedExternalDocument2@1100528702 : Record 11012148;
    BEGIN
      QueuedExternalDocument.SETRANGE(User, USERID);
      QueuedExternalDocument.SETRANGE(Source, QueuedExternalDocument.Source::System);
      QueuedExternalDocument.FINDFIRST;

      QueuedExternalDocument2.SETRANGE(User, QueuedExternalDocument2.User);
      QueuedExternalDocument2.SETRANGE(Source, QueuedExternalDocument2.Source);

      QueuedExternalDocument2.SETFILTER("Contact No.", '<>%1', QueuedExternalDocument."Contact No.");
      IF NOT QueuedExternalDocument2.FINDFIRST THEN
        DocumentProperties."Contact No." := QueuedExternalDocument."Contact No.";
      QueuedExternalDocument2.SETRANGE("Contact No.");

      QueuedExternalDocument2.SETFILTER("Contact Person No.", '<>%1', QueuedExternalDocument."Contact Person No.");
      IF NOT QueuedExternalDocument2.FINDFIRST THEN
        DocumentProperties."Contact Person No." := QueuedExternalDocument."Contact Person No.";
      QueuedExternalDocument2.SETRANGE("Contact Person No.");

      QueuedExternalDocument2.SETFILTER("Addressed to", '<>%1', QueuedExternalDocument."Addressed to");
      IF NOT QueuedExternalDocument2.FINDFIRST THEN
        DocumentProperties."Addressed To" := QueuedExternalDocument."Addressed to";
      QueuedExternalDocument2.SETRANGE("Addressed to");

      QueuedExternalDocument2.SETFILTER("Document Date", '<>%1', QueuedExternalDocument."Document Date");
      IF NOT QueuedExternalDocument2.FINDFIRST THEN
        DocumentProperties."Document Date" := QueuedExternalDocument."Document Date";
      QueuedExternalDocument2.SETRANGE("Document Date");

      DocumentProperties.SetDefaultValues;
    END;

    LOCAL PROCEDURE CheckAndFillCommonDocumentProperties@1100528706(VAR DocumentProperties@1100528300 : Record 11012746);
    VAR
      CommonDocumentProperties@1100528701 : Page 11126563;
    BEGIN
      CommonDocumentProperties.SetRecId(RecReference.RECORDID, DocumentProperties);

      CommonDocumentProperties.SetDocumentProperties(DocumentProperties);
      IF CommonDocumentProperties.RUNMODAL = ACTION::OK THEN
        CommonDocumentProperties.GetDocumentProperties(DocumentProperties)
      ELSE
       ERROR(Text000);
    END;

    LOCAL PROCEDURE AreDPTDocuments@1100525005(DocumentProperties@1100525000 : Record 11012746) : Boolean;
    VAR
      Job@1100525003 : Record 11072003;
    BEGIN
      //#C031444.c
      CASE RecReference.NUMBER OF
        DATABASE::Job: BEGIN
          RecReference.SETTABLE(Job);
          EXIT(Job.IsJobInProjectPortal);
        END;
      END;
      EXIT(FALSE);
    END;

    LOCAL PROCEDURE AreDPTDualDocuments@1100525006(DocumentProperties@1100525000 : Record 11012746) : Boolean;
    VAR
      Job@1100525003 : Record 11072003;
      ServiceLocation@1100525001 : Record 11012801;
    BEGIN
      //#C031444.n
      CASE RecReference.NUMBER OF
        DATABASE::Job: BEGIN
          RecReference.SETTABLE(Job);
          EXIT(Job.IsDocumentPortalDualProject);
        END;
        DATABASE::"Service Location": BEGIN
          RecReference.SETTABLE(ServiceLocation);
          EXIT(ServiceLocation.IsDocumentPortalServiceLocation);
        END;
      END;
      EXIT(FALSE);
    END;

    LOCAL PROCEDURE CreateAdditionalDocumentLinks@1100527805();
    VAR
      DocumentLinkManagement@1100527800 : Codeunit 11012401;
      i@1100527802 : Integer;
      DocLinkRID@1100527801 : RecordID;
      LinkRecReference@1100527803 : RecordRef;
    BEGIN
      CLEAR(DocLinkRIDArray);
      DocumentLinkManagement.GetAdditionalDocumentLinksFromRecordID(RecReference.RECORDID, DocLinkRIDArray);
      FOR i := 1 TO ARRAYLEN(DocLinkRIDArray) DO BEGIN
        IF FORMAT(DocLinkRIDArray[i]) <> '' THEN BEGIN
          DocLinkRID := DocLinkRIDArray[i];
          LinkRecReference.GET(DocLinkRID);
          CreateDocumentLinkInQueue(LinkRecReference);
         END;
      END;
    END;

    LOCAL PROCEDURE SetGlobalDocumentPortalVariables@1100527809();
    VAR
      DocumentLinkManagement@1100527800 : Codeunit 11012401;
    BEGIN
      HasPPTProject := DocumentLinkManagement.HasPPTProject(DocLinkRIDArray,Job);
      IF (HasPPTProject) THEN
        BEGIN
          RecReferenceDPT.GET(Job.RECORDID);
          EXIT;
        END;

      HasDPTProject := DocumentLinkManagement.HasDPTDualProject(DocLinkRIDArray,Job,ServiceLocation);
      IF (HasDPTProject) THEN
      BEGIN
        IF (Job."No." <> '') THEN
          RecReferenceDPT.GET(Job.RECORDID);

        IF (ServiceLocation."No." <> '' ) THEN
          RecReferenceDPT.GET(ServiceLocation.RECORDID);

        EXIT;
      END;
    END;

    LOCAL PROCEDURE CheckQueuedDocumentLinks@1100528300();
    VAR
      QueuedDocumentLink@1100528300 : Record 11125736;
      RecRef@1100528301 : RecordRef;
    BEGIN
      //For pages with delayed insert = true
      QueuedDocumentLink.SETRANGE(User, USERID);
      IF (QueuedDocumentLink.FINDSET) THEN
        REPEAT
          IF (NOT RecRef.GET(QueuedDocumentLink."Record ID")) THEN
            ERROR(RecRefNotValidTxt, QueuedDocumentLink."Record ID");
        UNTIL (QueuedDocumentLink.NEXT = 0);
    END;

    BEGIN
    {
      Continued by DJN
    }
    END.
  }
}

