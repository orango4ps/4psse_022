OBJECT Page 11125194 Service Loc.Structure SF
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Servicestandortstruktur;
               ENU=Service Location Structure;
               NLD=Servicelocatie structuur;
               NOR=Servicelokasjonstruktur;
               SVE=Serviceplatsstruktur];
    SourceTable=Table11071720;
    DelayedInsert=Yes;
    PageType=ListPart;
    OnOpenPage=BEGIN
                 UpdateEditable;
               END;

    OnAfterGetRecord=VAR
                       ServiceObjectInfoMgt@1100525000 : Codeunit 11012840;
                     BEGIN
                       IF NOT "Object Line" THEN BEGIN
                         LineDescription := Description;
                         LineDescription2 := '';
                         MeasurementDate := 0D;
                         ConditionScore := 0;
                       END ELSE BEGIN
                         CALCFIELDS("Object Description", "Object Description 2", "Brand Code", "Model Code", "Type Code");
                         LineDescription := "Object Description";
                         LineDescription2 := "Object Description 2";
                         MeasurementDate := TODAY;
                         ConditionScore := NEN2767Management.GetObjectConditionScore("Service Object No.", MeasurementDate);
                       END;

                       ActualContractNo := MaintenanceManagementCU.GetActualContractsForObject("Service Object No.",TODAY);
                       ServiceObjectInfoMgt.GetStdSpecValues("Service Object No.", SerialNo, YearOfManufacture);
                       LineDescriptionOnFormat;
                       UpdateEditable;
                     END;

    OnNewRecord=BEGIN
                  LineDescription := '';
                  UpdateEditable;
                END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100525001;1 ;ActionGroup;
                      CaptionML=[DEU=Objekt;
                                 ENU=Object;
                                 NLD=Object;
                                 NOR=Objekt;
                                 SVE=Objekt] }
      { 1100525002;2 ;Action    ;
                      CaptionML=[DEU=Alle zugeh”rigen Objekte;
                                 ENU=All Related Objects;
                                 NLD=Alle gerelateerde objecten;
                                 NOR=Alle relaterte objekter;
                                 SVE=Alla relaterade objekt];
                      Image=SuggestCapacity;
                      OnAction=VAR
                                 ServiceObjectManagement@1100525000 : Codeunit 11012808;
                               BEGIN
                                 ServiceObjectManagement.ShowRelatedObjects("Service Object No.");
                               END;
                                }
      { 1906587504;1 ;ActionGroup;
                      CaptionML=[DEU=F&unktion;
                                 ENU=F&unctions;
                                 NLD=F&uncties;
                                 NOR=Fu&nksjon;
                                 SVE=Fu&nktion] }
      { 1210190000;2 ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Ausw„hlen Standardobjekte;
                                 ENU=Select Standard Objects;
                                 NLD=Selecteer standaard objecten;
                                 NOR=Velg standardobjekter;
                                 SVE=V„lj standardobjekt];
                      Image=ServiceItem;
                      OnAction=BEGIN
                                 SelectStandardObjects;
                               END;
                                }
      { 1902236704;2 ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Ausw„hlen Objekte;
                                 ENU=Select Objects;
                                 NLD=Selecteer objecten;
                                 NOR=Velg objekter;
                                 SVE=V„lj objekt];
                      Image=ServiceItem;
                      OnAction=BEGIN
                                 SelectObjects;
                               END;
                                }
      { 1902575904;2 ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Verbinden mit Servicevertrag;
                                 ENU=Attach to Service Contract;
                                 NLD=Koppel aan servicecontract;
                                 NOR=Legg ved servicekontrakt;
                                 SVE=Bifoga till servicekontrakt];
                      Image=CopyBudget;
                      OnAction=VAR
                                 ServiceLocStructureLine@1100525001 : Record 11071720;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(ServiceLocStructureLine);
                                 CreateContractForSelObjects(ServiceLocStructureLine);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100526200;1;Group  ;
                IndentationColumnName=DescriptionIndentationValue;
                IndentationControls=Description;
                GroupType=Repeater }

    { 1100526201;2;Field  ;
                SourceExpr="No.";
                Editable=NoEditable;
                OnValidate=BEGIN
                             LineDescriptionOnFormat;
                           END;

                OnAssistEdit=BEGIN
                               SelectObjects;
                             END;
                              }

    { 1100526203;2;Field  ;
                Name=Description;
                CaptionML=[DEU=Beschreibung;
                           ENU=Description;
                           NLD=Omschrijving;
                           NOR=Beskrivelse;
                           SVE=Beskrivning];
                SourceExpr=LineDescription;
                Editable=DescriptionEditable;
                Style=Strong;
                StyleExpr=DescriptionEmphasize;
                OnValidate=BEGIN
                             Description := LineDescription;
                           END;
                            }

    { 1100528604;2;Field  ;
                Name=Description2;
                CaptionML=[DEU=Beschreibung 2;
                           ENU=Description 2;
                           NLD=Omschrijving 2;
                           NOR=Beskrivelse 2;
                           SVE=Beskrivning 2];
                SourceExpr=LineDescription2;
                Editable=DescriptionEditable;
                Style=Strong;
                StyleExpr=DescriptionEmphasize;
                OnValidate=BEGIN
                             "Description 2" := LineDescription2;
                           END;
                            }

    { 1100526205;2;Field  ;
                Lookup=No;
                SourceExpr="Service Object No.";
                Editable=FALSE;
                OnAssistEdit=VAR
                               ServiceObject@1100528600 : Record 11071691;
                             BEGIN
                               IF "Service Object No." = '' THEN EXIT;
                               ServiceObject.GET("Service Object No.");
                               PAGE.RUNMODAL(PAGE::"Service Object", ServiceObject);
                             END;
                              }

    { 1100528609;2;Field  ;
                SourceExpr="Brand Code" }

    { 1100528612;2;Field  ;
                SourceExpr="Brand Description";
                Visible=FALSE }

    { 1100528611;2;Field  ;
                SourceExpr="Model Code" }

    { 1100528614;2;Field  ;
                SourceExpr="Model Description";
                Visible=FALSE }

    { 1100528610;2;Field  ;
                SourceExpr="Type Code" }

    { 1100528613;2;Field  ;
                SourceExpr="Type Description";
                Visible=FALSE }

    { 1100528600;2;Field  ;
                CaptionML=[DEU=Aktuelle Vertragsnr.;
                           ENU=Actual Contract No.;
                           NLD=Actueel contractnr.;
                           NOR=Faktisk kontraktsnr;
                           SVE=Verkligt kontraktnr];
                SourceExpr=ActualContractNo;
                TableRelation=Service-Contract;
                Editable=FALSE;
                OnLookup=BEGIN
                           MaintenanceManagementCU.ShowActualContractsForObject("Service Object No.", '', TODAY, TRUE, '');
                           CurrPage.UPDATE;
                         END;
                          }

    { 1100526207;2;Field  ;
                SourceExpr="Standard Object No." }

    { 1100525000;2;Field  ;
                CaptionML=[DEU=Seriennr.;
                           ENU=Serial No.;
                           NLD=Serienr.;
                           NOR=Serienr.;
                           SVE=Tillverkningsnr];
                SourceExpr=SerialNo;
                Editable=false }

    { 1100525003;2;Field  ;
                CaptionML=[DEU=Baujahr;
                           ENU=Year of Manufacture;
                           NLD=Bouwjaar;
                           NOR=Produksjons†r;
                           SVE=Tillverknings†r];
                SourceExpr=YearOfManufacture;
                Editable=false }

    { 1100528601;2;Field  ;
                SourceExpr=Blocked }

    { 1100528702;2;Field  ;
                SourceExpr="Customer Feature" }

    { 1100528703;2;Field  ;
                SourceExpr="Object Customer" }

    { 1100528602;2;Field  ;
                CaptionML=[DEU=Messdatum;
                           ENU=Measurement Date;
                           NLD=Meetdatum;
                           NOR=M†lingsdato];
                SourceExpr=MeasurementDate;
                Visible=false;
                Editable=false }

    { 1100528603;2;Field  ;
                CaptionML=[DEU=Bedingung Endstand;
                           ENU=Condition Score;
                           NLD=Conditiescore;
                           NOR=Poengsum for tilstand];
                BlankZero=Yes;
                SourceExpr=ConditionScore;
                Visible=false;
                Editable=false }

  }
  CODE
  {
    VAR
      MaintenanceManagementCU@1100528601 : Codeunit 11012829;
      NEN2767Management@1100528605 : Codeunit 11012863;
      LineDescription@1100526200 : Text[100];
      Text000@1100528600 : TextConst 'DEU=Zuerst eine Strukturzeile ausw„hlen;ENU=First Select a Stucture Line;NLD=Selecteer eerst een structuurregel;NOR=Velg f›rst en strukturrad;SVE=V„lj f”rst en strukturrad';
      LineDescription2@1100528610 : Text[50];
      ActualContractNo@1100528602 : Code[20];
      DescriptionEmphasize@19010547 : Boolean INDATASET;
      DescriptionIndentationValue@19057867 : Integer INDATASET;
      NoEditable@1210190000 : Boolean INDATASET;
      DescriptionEditable@1210190001 : Boolean INDATASET;
      SerialNo@1100525000 : Text[80];
      YearOfManufacture@1100525001 : Text[80];
      ConditionScore@1100528604 : ',1,2,3,4,5,6';
      MeasurementDate@1100528603 : Date;

    PROCEDURE SelectObjects@1100528600();
    VAR
      lvElementObjectFrm@1100528601 : Page 11125195;
      lvElementObjectRec@1100528600 : Record 11071720;
    BEGIN
      IF NOT "Object Line" THEN BEGIN
        lvElementObjectRec.SETRANGE("Service Location No.","Service Location No.");
        lvElementObjectRec.SETRANGE("Structure No.","Structure No.");
        lvElementObjectRec.SETRANGE("No.","No.");
        lvElementObjectRec.SETRANGE("Object Line",TRUE);
        lvElementObjectFrm.LOOKUPMODE(TRUE);
        lvElementObjectFrm.SETTABLEVIEW(lvElementObjectRec);
        lvElementObjectFrm.SetDefaults("Service Location No.","No.","Structure No.");
        IF lvElementObjectFrm.RUNMODAL = ACTION::LookupOK THEN
          CurrPage.UPDATE;
      END ELSE
        ERROR(Text000);
    END;

    PROCEDURE CreateObject@1100528601();
    VAR
      ServiceLocStructureLine@1100528600 : Record 11071720;
      ServiceObject@1100528601 : Record 11071691;
    BEGIN
      IF NOT "Object Line" THEN BEGIN
        ServiceObject.INIT;
        ServiceObject.INSERT(TRUE);
        ServiceObject.VALIDATE("Service Location No.", "Service Location No.");
        ServiceObject.MODIFY(TRUE);

        ServiceLocStructureLine.INIT;
        ServiceLocStructureLine.VALIDATE("Service Location No.", "Service Location No.");
        ServiceLocStructureLine.VALIDATE("Structure No.", "Structure No.");
        ServiceLocStructureLine.VALIDATE("No.", "No.");
        ServiceLocStructureLine.VALIDATE("Object Line", TRUE);
        ServiceLocStructureLine.VALIDATE("Service Object No.", ServiceObject."No.");
        ServiceLocStructureLine.INSERT(TRUE);
        CurrPage.UPDATE;
      END ELSE
        ERROR(Text000);
    END;

    PROCEDURE SelectStandardObjects@1100528603();
    VAR
      TmpContractConsObjWizardLine@1100528600 : TEMPORARY Record 11071698;
    BEGIN
      IF NOT "Object Line" THEN BEGIN
        IF PAGE.RUNMODAL(PAGE::"Select Standard Objects", TmpContractConsObjWizardLine) = ACTION::LookupOK THEN
          CreateObjectsWithinStructure(TmpContractConsObjWizardLine);
      END ELSE
        ERROR(Text000);
    END;

    PROCEDURE CreateContractForSelObjects@1100528602();
    VAR
      ServiceLocStructureLine@1100525000 : Record 11071720;
    BEGIN
      CurrPage.SETSELECTIONFILTER(ServiceLocStructureLine);
      Rec.CreateContractForSelObjects(ServiceLocStructureLine);
    END;

    LOCAL PROCEDURE LineDescriptionOnFormat@19076134();
    BEGIN
      DescriptionEmphasize := FALSE;
      IF "Object Line" THEN
        DescriptionIndentationValue := Level + 1
      ELSE BEGIN
        DescriptionIndentationValue := Level;
        DescriptionEmphasize := TRUE;
      END;
    END;

    PROCEDURE UpdateEditable@1210190000();
    BEGIN
      NoEditable := NOT "Object Line";
      DescriptionEditable := NOT "Object Line";
    END;

    PROCEDURE ShowObject@1100528607();
    VAR
      ServiceObject@1100528600 : Record 11071691;
    BEGIN
      TESTFIELD("Object Line");
      ServiceObject.GET("Service Object No.");
      PAGE.RUN(PAGE::"Service Object", ServiceObject);
    END;

    BEGIN
    END.
  }
}

