OBJECT Report 5753 Get Source Documents
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.05;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Herkunftsbelege holen;
               ENU=Get Source Documents;
               NLD=Brondocumenten ophalen;
               NOR=Hent kildedokumenter;
               SVE=H„mta k„lldokument];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  ActivitiesCreated := 0;
                  LineCreated := FALSE;
                END;

    OnPostReport=BEGIN
                   IF NOT HideDialog THEN
                     CASE RequestType OF
                       RequestType::Receive:
                         ShowReceiptDialog;
                       RequestType::Ship:
                         ShowShipmentDialog;
                     END;
                   IF SkippedSourceDoc > 0 THEN
                     MESSAGE(CustomerIsBlockedMsg,SkippedSourceDoc);
                   Completed := TRUE;
                 END;

  }
  DATASET
  {
    { 9356;    ;DataItem;                    ;
               DataItemTable=Table5765;
               DataItemTableView=WHERE(Document Status=CONST(Released),
                                       Completely Handled=FILTER(No));
               OnPreDataItem=BEGIN
                               IF OneHeaderCreated THEN BEGIN
                                 CASE RequestType OF
                                   RequestType::Receive:
                                     Type := Type::Inbound;
                                   RequestType::Ship:
                                     Type := Type::Outbound;
                                 END;
                                 SETRANGE(Type,Type);
                               END;

                               OnAfterWarehouseRequestOnPreDataItem("Warehouse Request");
                             END;

               OnAfterGetRecord=VAR
                                  WhseSetup@1000 : Record 5769;
                                  SkipRecord@1003 : Boolean;
                                  BreakReport@1001 : Boolean;
                                BEGIN
                                  WhseHeaderCreated := FALSE;
                                  OnBeforeWarehouseRequestOnAfterGetRecord(
                                    "Warehouse Request",WhseHeaderCreated,SkipRecord,BreakReport);
                                  IF BreakReport THEN
                                    CurrReport.BREAK;
                                  IF SkipRecord THEN
                                    CurrReport.SKIP;

                                  CASE Type OF
                                    Type::Inbound:
                                      BEGIN
                                        IF NOT Location.RequireReceive("Location Code") THEN BEGIN
                                          IF "Location Code" = '' THEN
                                            WhseSetup.TESTFIELD("Require Receive");
                                          Location.GET("Location Code");
                                          Location.TESTFIELD("Require Receive");
                                        END;
                                        IF NOT OneHeaderCreated THEN
                                          RequestType := RequestType::Receive;
                                      END;
                                    Type::Outbound:
                                      BEGIN
                                        IF NOT Location.RequireShipment("Location Code") THEN BEGIN
                                          IF "Location Code" = '' THEN
                                            WhseSetup.TESTFIELD("Require Shipment");
                                          Location.GET("Location Code");
                                          Location.TESTFIELD("Require Shipment");
                                        END;
                                        IF NOT OneHeaderCreated THEN
                                          RequestType := RequestType::Ship;
                                      END;
                                  END;
                                END;

               OnPostDataItem=VAR
                                IsHandled@1000 : Boolean;
                              BEGIN
                                IsHandled := NOT (WhseHeaderCreated OR OneHeaderCreated);
                                OnBeforeCreateWhseDocuments(WhseReceiptHeader,WhseShptHeader,IsHandled,"Warehouse Request");
                                IF NOT IsHandled THEN BEGIN
                                  OnAfterCreateWhseDocuments(WhseReceiptHeader,WhseShptHeader,WhseHeaderCreated,"Warehouse Request");
                                  WhseShptHeader.SortWhseDoc;
                                  WhseReceiptHeader.SortWhseDoc;
                                END;
                              END;

               ReqFilterFields=Source Document,Source No. }

    { 6640;1   ;DataItem;                    ;
               DataItemTable=Table36;
               DataItemTableView=SORTING(Document Type,No.);
               OnPreDataItem=BEGIN
                               IF "Warehouse Request"."Source Type" <> DATABASE::"Sales Line" THEN
                                 CurrReport.BREAK;

                               OnAfterSalesHeaderOnPreDataItem("Sales Header");
                             END;

               OnAfterGetRecord=VAR
                                  SkipRecord@1000 : Boolean;
                                  BreakReport@1001 : Boolean;
                                BEGIN
                                  TESTFIELD("Sell-to Customer No.");
                                  Cust.GET("Sell-to Customer No.");
                                  IF NOT SkipBlockedCustomer THEN
                                    Cust.CheckBlockedCustOnDocs(Cust,"Document Type",FALSE,FALSE);
                                  SalesHeaderCounted := FALSE;

                                  BreakReport := FALSE;
                                  SkipRecord := FALSE;
                                  OnAfterSalesHeaderOnAfterGetRecord("Sales Header",SkipRecord,BreakReport,"Warehouse Request");
                                  IF BreakReport THEN
                                    CurrReport.BREAK;
                                  IF SkipRecord THEN
                                    CurrReport.SKIP;
                                END;

               DataItemLink=Document Type=FIELD(Source Subtype),
                            No.=FIELD(Source No.) }

    { 2844;2   ;DataItem;                    ;
               DataItemTable=Table37;
               DataItemTableView=SORTING(Document Type,Document No.,Line No.);
               OnPreDataItem=BEGIN
                               SETRANGE(Type,Type::Item);
                               IF (("Warehouse Request".Type = "Warehouse Request".Type::Outbound) AND
                                   ("Warehouse Request"."Source Document" = "Warehouse Request"."Source Document"::"Sales Order")) OR
                                  (("Warehouse Request".Type = "Warehouse Request".Type::Inbound) AND
                                   ("Warehouse Request"."Source Document" = "Warehouse Request"."Source Document"::"Sales Return Order"))
                               THEN
                                 SETFILTER("Outstanding Quantity",'>0')
                               ELSE
                                 SETFILTER("Outstanding Quantity",'<0');
                               SETRANGE("Drop Shipment",FALSE);
                               SETRANGE("Job No.",'');

                               OnAfterSalesLineOnPreDataItem("Sales Line",OneHeaderCreated,WhseShptHeader,WhseReceiptHeader);
                             END;

               OnAfterGetRecord=BEGIN
                                  VerifyItemNotBlocked("No.");
                                  IF "Location Code" = "Warehouse Request"."Location Code" THEN
                                    CASE RequestType OF
                                      RequestType::Receive:
                                        IF WhseActivityCreate.CheckIfSalesLine2ReceiptLine("Sales Line") THEN BEGIN
                                          OnSalesLineOnAfterGetRecordOnBeforeCreateRcptHeader(
                                            "Sales Line","Warehouse Request",WhseReceiptHeader,WhseHeaderCreated,OneHeaderCreated);
                                          IF NOT OneHeaderCreated AND NOT WhseHeaderCreated THEN
                                            CreateReceiptHeader;
                                          IF NOT WhseActivityCreate.SalesLine2ReceiptLine(WhseReceiptHeader,"Sales Line") THEN
                                            ErrorOccured := TRUE;
                                          LineCreated := TRUE;
                                        END;
                                      RequestType::Ship:
                                        IF WhseActivityCreate.CheckIfFromSalesLine2ShptLine("Sales Line") THEN BEGIN
                                          IF Cust.Blocked <> Cust.Blocked::" " THEN BEGIN
                                            IF NOT SalesHeaderCounted THEN BEGIN
                                              SkippedSourceDoc += 1;
                                              SalesHeaderCounted := TRUE;
                                            END;
                                            CurrReport.SKIP;
                                          END;
                                          OnSalesLineOnAfterGetRecordOnBeforeCreateShptHeader(
                                            "Sales Line","Warehouse Request",WhseShptHeader,WhseHeaderCreated,OneHeaderCreated);
                                          IF NOT OneHeaderCreated AND NOT WhseHeaderCreated THEN
                                            CreateShptHeader;
                                          IF NOT WhseActivityCreate.FromSalesLine2ShptLine(WhseShptHeader,"Sales Line") THEN
                                            ErrorOccured := TRUE;
                                          LineCreated := TRUE;
                                        END;
                                    END;
                                END;

               OnPostDataItem=BEGIN
                                IF OneHeaderCreated OR WhseHeaderCreated THEN BEGIN
                                  UpdateReceiptHeaderStatus;
                                  CheckFillQtyToHandle;
                                END;

                                OnAfterProcessDocumentLine(WhseShptHeader,"Warehouse Request",LineCreated);
                              END;

               DataItemLink=Document Type=FIELD(Document Type),
                            Document No.=FIELD(No.) }

    { 4458;1   ;DataItem;                    ;
               DataItemTable=Table38;
               DataItemTableView=SORTING(Document Type,No.);
               OnPreDataItem=BEGIN
                               IF "Warehouse Request"."Source Type" <> DATABASE::"Purchase Line" THEN
                                 CurrReport.BREAK;
                             END;

               DataItemLink=Document Type=FIELD(Source Subtype),
                            No.=FIELD(Source No.) }

    { 6547;2   ;DataItem;                    ;
               DataItemTable=Table39;
               DataItemTableView=SORTING(Document Type,Document No.,Line No.);
               OnPreDataItem=BEGIN
                               SETRANGE(Type,Type::Item);
                               IF (("Warehouse Request".Type = "Warehouse Request".Type::Inbound) AND
                                   ("Warehouse Request"."Source Document" = "Warehouse Request"."Source Document"::"Purchase Order")) OR
                                  (("Warehouse Request".Type = "Warehouse Request".Type::Outbound) AND
                                   ("Warehouse Request"."Source Document" = "Warehouse Request"."Source Document"::"Purchase Return Order"))
                               THEN
                                 SETFILTER("Outstanding Quantity",'>0')
                               ELSE
                                 SETFILTER("Outstanding Quantity",'<0');
                               SETRANGE("Drop Shipment",FALSE);
                               SETRANGE("Job No.",'');

                               OnAfterPurchaseLineOnPreDataItem("Purchase Line",OneHeaderCreated,WhseShptHeader,WhseReceiptHeader);
                             END;

               OnAfterGetRecord=BEGIN
                                  VerifyItemNotBlocked("No.");
                                  IF "Location Code" = "Warehouse Request"."Location Code" THEN
                                    CASE RequestType OF
                                      RequestType::Receive:
                                        IF WhseActivityCreate.CheckIfPurchLine2ReceiptLine("Purchase Line") THEN BEGIN
                                          IF NOT OneHeaderCreated AND NOT WhseHeaderCreated THEN
                                            CreateReceiptHeader;
                                          IF NOT WhseActivityCreate.PurchLine2ReceiptLine(WhseReceiptHeader,"Purchase Line") THEN
                                            ErrorOccured := TRUE;
                                          LineCreated := TRUE;
                                        END;
                                      RequestType::Ship:
                                        IF WhseActivityCreate.CheckIfFromPurchLine2ShptLine("Purchase Line") THEN BEGIN
                                          IF NOT OneHeaderCreated AND NOT WhseHeaderCreated THEN
                                            CreateShptHeader;
                                          IF NOT WhseActivityCreate.FromPurchLine2ShptLine(WhseShptHeader,"Purchase Line") THEN
                                            ErrorOccured := TRUE;
                                          LineCreated := TRUE;
                                        END;
                                    END;
                                END;

               OnPostDataItem=BEGIN
                                IF OneHeaderCreated OR WhseHeaderCreated THEN BEGIN
                                  UpdateReceiptHeaderStatus;
                                  CheckFillQtyToHandle;
                                END;

                                OnAfterProcessDocumentLine(WhseShptHeader,"Warehouse Request",LineCreated);
                              END;

               DataItemLink=Document Type=FIELD(Document Type),
                            Document No.=FIELD(No.) }

    { 2957;1   ;DataItem;                    ;
               DataItemTable=Table5740;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=BEGIN
                               IF "Warehouse Request"."Source Type" <> DATABASE::"Transfer Line" THEN
                                 CurrReport.BREAK;
                             END;

               OnAfterGetRecord=VAR
                                  BreakReport@1000 : Boolean;
                                  SkipRecord@1001 : Boolean;
                                BEGIN
                                  BreakReport := FALSE;
                                  SkipRecord := FALSE;
                                  OnAfterTransHeaderOnAfterGetRecord("Transfer Header",SkipRecord,BreakReport,"Warehouse Request");
                                  IF BreakReport THEN
                                    CurrReport.BREAK;
                                  IF SkipRecord THEN
                                    CurrReport.SKIP;
                                END;

               DataItemLink=No.=FIELD(Source No.) }

    { 9370;2   ;DataItem;                    ;
               DataItemTable=Table5741;
               DataItemTableView=SORTING(Document No.,Line No.);
               OnPreDataItem=BEGIN
                               CASE "Warehouse Request"."Source Subtype" OF
                                 0:
                                   SETFILTER("Outstanding Quantity",'>0');
                                 1:
                                   SETFILTER("Qty. in Transit",'>0');
                               END;

                               OnAfterTransferLineOnPreDataItem("Transfer Line",OneHeaderCreated,WhseShptHeader,WhseReceiptHeader);
                             END;

               OnAfterGetRecord=BEGIN
                                  CASE RequestType OF
                                    RequestType::Receive:
                                      IF WhseActivityCreate.CheckIfTransLine2ReceiptLine("Transfer Line") THEN BEGIN
                                        OnTransferLineOnAfterGetRecordOnBeforeCreateRcptHeader(
                                          "Transfer Line","Warehouse Request",WhseReceiptHeader,WhseHeaderCreated,OneHeaderCreated);
                                        IF NOT OneHeaderCreated AND NOT WhseHeaderCreated THEN
                                          CreateReceiptHeader;
                                        IF NOT WhseActivityCreate.TransLine2ReceiptLine(WhseReceiptHeader,"Transfer Line") THEN
                                          ErrorOccured := TRUE;
                                        LineCreated := TRUE;
                                      END;
                                    RequestType::Ship:
                                      IF WhseActivityCreate.CheckIfFromTransLine2ShptLine("Transfer Line") THEN BEGIN
                                        OnTransferLineOnAfterGetRecordOnBeforeCreateShptHeader(
                                          "Transfer Line","Warehouse Request",WhseShptHeader,WhseHeaderCreated,OneHeaderCreated);
                                        IF NOT OneHeaderCreated AND NOT WhseHeaderCreated THEN
                                          CreateShptHeader;
                                        IF NOT WhseActivityCreate.FromTransLine2ShptLine(WhseShptHeader,"Transfer Line") THEN
                                          ErrorOccured := TRUE;
                                        LineCreated := TRUE;
                                      END;
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                IF OneHeaderCreated OR WhseHeaderCreated THEN BEGIN
                                  UpdateReceiptHeaderStatus;
                                  CheckFillQtyToHandle;
                                END;

                                OnAfterProcessDocumentLine(WhseShptHeader,"Warehouse Request",LineCreated);
                              END;

               DataItemLink=Document No.=FIELD(No.) }

    { 1634;1   ;DataItem;                    ;
               DataItemTable=Table5900;
               DataItemTableView=SORTING(Document Type,No.);
               OnPreDataItem=BEGIN
                               IF "Warehouse Request"."Source Type" <> DATABASE::"Service Line" THEN
                                 CurrReport.BREAK;
                             END;

               OnAfterGetRecord=BEGIN
                                  TESTFIELD("Bill-to Customer No.");
                                  Cust.GET("Bill-to Customer No.");
                                  IF NOT SkipBlockedCustomer THEN
                                    Cust.CheckBlockedCustOnDocs(Cust,"Document Type",FALSE,FALSE)
                                  ELSE
                                    IF Cust.Blocked <> Cust.Blocked::" " THEN
                                      CurrReport.SKIP;
                                END;

               DataItemLink=Document Type=FIELD(Source Subtype),
                            No.=FIELD(Source No.) }

    { 6560;2   ;DataItem;                    ;
               DataItemTable=Table5902;
               DataItemTableView=SORTING(Document Type,Document No.,Line No.);
               OnPreDataItem=BEGIN
                               SETRANGE(Type,Type::Item);
                               IF (("Warehouse Request".Type = "Warehouse Request".Type::Outbound) AND
                                   ("Warehouse Request"."Source Document" = "Warehouse Request"."Source Document"::"Service Order"))
                               THEN
                                 SETFILTER("Outstanding Quantity",'>0')
                               ELSE
                                 SETFILTER("Outstanding Quantity",'<0');
                               SETRANGE("Job No.",'');

                               OnAfterServiceLineOnPreDataItem("Service Line",OneHeaderCreated,WhseShptHeader,WhseReceiptHeader);
                             END;

               OnAfterGetRecord=BEGIN
                                  IF "Location Code" = "Warehouse Request"."Location Code" THEN
                                    CASE RequestType OF
                                      RequestType::Ship:
                                        IF WhseActivityCreate.CheckIfFromServiceLine2ShptLin("Service Line") THEN BEGIN
                                          IF NOT OneHeaderCreated AND NOT WhseHeaderCreated THEN
                                            CreateShptHeader;
                                          IF NOT WhseActivityCreate.FromServiceLine2ShptLine(WhseShptHeader,"Service Line") THEN
                                            ErrorOccured := TRUE;
                                          LineCreated := TRUE;
                                        END;
                                    END;
                                END;

               DataItemLink=Document Type=FIELD(Document Type),
                            Document No.=FIELD(No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1   ;2   ;Field     ;
                  CaptionML=[DEU=Bewegungsmenge nicht ausfllen;
                             ENU=Do Not Fill Qty. to Handle;
                             NLD=Te verwerken aantal niet opvullen;
                             NOR=Ikke fyll ut antall som skal h†ndteres];
                  ToolTipML=[DEU=Gibt an, ob das Feld "Zu bearbeitende Menge" des Logistikbelegs entsprechend den Mengen im Herkunftsbeleg vorausgefllt ist.;
                             ENU=Specifies if the Quantity to Handle field in the warehouse document is prefilled according to the source document quantities.;
                             NLD=Hiermee wordt opgegeven of het veld Werk. verwerkt aantal in het magazijndocument vooraf wordt gevuld op basis van de aantallen in het brondocument.;
                             NOR=Angir om feltet Antall som skal h†ndteres i lagerdokumentet, er forh†ndsutfylt i henhold til antallene i kildedokumentet.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=DoNotFillQtytoHandle }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Es wurden keine Wareneingangszeilen erstellt.;ENU=There are no Warehouse Receipt Lines created.;NLD=Er zijn geen magazijnontvangstregels gemaakt.;NOR=Det er ikke opprettet noen lagermottakslinjer.';
      Text001@1001 : TextConst 'DEU=%1 %2 wurde erstellt.;ENU=%1 %2 has been created.;NLD=%1 %2 is gemaakt.;NOR=%1 %2 er opprettet.;SVE=%1 %2 har skapats.';
      WhseReceiptHeader@1015 : Record 7316;
      WhseReceiptLine@1014 : Record 7317;
      WhseShptHeader@1018 : Record 7320;
      WhseShptLine@1017 : Record 7321;
      Location@1002 : Record 14;
      Cust@1005 : Record 18;
      WhseActivityCreate@1006 : Codeunit 5750;
      ActivitiesCreated@1007 : Integer;
      OneHeaderCreated@1008 : Boolean;
      Completed@1010 : Boolean;
      LineCreated@1012 : Boolean;
      WhseHeaderCreated@1009 : Boolean;
      DoNotFillQtytoHandle@1004 : Boolean;
      HideDialog@1013 : Boolean;
      SkipBlockedCustomer@1020 : Boolean;
      SkipBlockedItem@1026 : Boolean;
      RequestType@1016 : 'Receive,Ship';
      SalesHeaderCounted@1023 : Boolean;
      SkippedSourceDoc@1024 : Integer;
      Text002@1019 : TextConst 'DEU=%1 Wareneing„nge wurden erstellt.;ENU=%1 Warehouse Receipts have been created.;NLD=Er zijn %1 magazijnontvangstregels gemaakt.;NOR=%1 lagermottak er opprettet.';
      Text003@1003 : TextConst 'DEU=Es wurden keine Warenausgangszeilen erstellt.;ENU=There are no Warehouse Shipment Lines created.;NLD=Er zijn geen magazijnverzendingsregels gemaakt.;NOR=Ingen lagerf›lgeseddellinjer er opprettet.';
      Text004@1011 : TextConst 'DEU=%1 Warenausg„nge wurden erstellt.;ENU=%1 Warehouse Shipments have been created.;NLD=%1 magazijnverzendingsregels zijn gemaakt.;NOR=%1 lagerleveringer er opprettet.';
      ErrorOccured@1021 : Boolean;
      Text005@1022 : TextConst 'DEU=Mindestens eine der Zeilen im aktuellen Element vom Typ ''%1'' erfordert einen speziellen Lagerdurchlauf. Fr die entsprechenden Zeilen wurde Folgendes als leer festgelegt: %2.;ENU=One or more of the lines on this %1 require special warehouse handling. The %2 for such lines has been set to blank.;NLD=Voor een of meer regels op deze %1 is speciale magazijnverwerking vereist. De %2 voor dergelijke regels is leeg.;NOR=n eller flere linjer p† denne %1 krever spesiallagerh†ndtering. %2 for slike linjer er satt til tom.';
      CustomerIsBlockedMsg@1025 : TextConst 'DEU=%1 Quelldokumente wurden nicht bercksichtigt, da der Debitor gesperrt ist.;ENU=%1 source documents were not included because the customer is blocked.;NLD=%1 brondocumenten zijn niet opgenomen omdat de klant geblokkeerd is.;NOR=%1 kildedokumenter ble ikke inkludert fordi kunden er sperret.';

    [External]
    PROCEDURE SetHideDialog@6(NewHideDialog@1000 : Boolean);
    BEGIN
      HideDialog := NewHideDialog;
    END;

    [External]
    PROCEDURE SetOneCreatedShptHeader@1(WhseShptHeader2@1000 : Record 7320);
    BEGIN
      RequestType := RequestType::Ship;
      WhseShptHeader := WhseShptHeader2;
      IF WhseShptHeader.FIND THEN
        OneHeaderCreated := TRUE;
    END;

    [External]
    PROCEDURE SetOneCreatedReceiptHeader@7(WhseReceiptHeader2@1000 : Record 7316);
    BEGIN
      RequestType := RequestType::Receive;
      WhseReceiptHeader := WhseReceiptHeader2;
      IF WhseReceiptHeader.FIND THEN
        OneHeaderCreated := TRUE;
    END;

    [External]
    PROCEDURE SetDoNotFillQtytoHandle@3(DoNotFillQtytoHandle2@1000 : Boolean);
    BEGIN
      DoNotFillQtytoHandle := DoNotFillQtytoHandle2;
    END;

    [External]
    PROCEDURE GetLastShptHeader@5(VAR WhseShptHeader2@1000 : Record 7320);
    BEGIN
      RequestType := RequestType::Ship;
      WhseShptHeader2 := WhseShptHeader;
    END;

    [External]
    PROCEDURE GetLastReceiptHeader@8(VAR WhseReceiptHeader2@1000 : Record 7316);
    BEGIN
      RequestType := RequestType::Receive;
      WhseReceiptHeader2 := WhseReceiptHeader;
    END;

    [External]
    PROCEDURE NotCancelled@4() : Boolean;
    BEGIN
      EXIT(Completed);
    END;

    LOCAL PROCEDURE CreateShptHeader@10();
    VAR
      IsHandled@1000 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeCreateShptHeader(WhseShptHeader,"Warehouse Request","Sales Line",IsHandled);
      IF IsHandled THEN
        EXIT;

      WhseShptHeader.INIT;
      WhseShptHeader."No." := '';
      WhseShptHeader."Location Code" := "Warehouse Request"."Location Code";
      IF Location.Code = WhseShptHeader."Location Code" THEN
        WhseShptHeader."Bin Code" := Location."Shipment Bin Code";
      WhseShptHeader."External Document No." := "Warehouse Request"."External Document No.";
      WhseShptLine.LOCKTABLE;
      OnBeforeWhseShptHeaderInsert(WhseShptHeader,"Warehouse Request");
      WhseShptHeader.INSERT(TRUE);
      ActivitiesCreated := ActivitiesCreated + 1;
      WhseHeaderCreated := TRUE;

      OnAfterCreateShptHeader(WhseShptHeader,"Warehouse Request","Sales Line");
    END;

    LOCAL PROCEDURE CreateReceiptHeader@9();
    VAR
      IsHandled@1000 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeCreateRcptHeader(WhseReceiptHeader,"Warehouse Request","Purchase Line",IsHandled);
      IF IsHandled THEN
        EXIT;

      WhseReceiptHeader.INIT;
      WhseReceiptHeader."No." := '';
      WhseReceiptHeader."Location Code" := "Warehouse Request"."Location Code";
      IF Location.Code = WhseReceiptHeader."Location Code" THEN
        WhseReceiptHeader."Bin Code" := Location."Receipt Bin Code";
      WhseReceiptHeader."Vendor Shipment No." := "Warehouse Request"."External Document No.";
      WhseReceiptLine.LOCKTABLE;
      OnBeforeWhseReceiptHeaderInsert(WhseReceiptHeader,"Warehouse Request");
      WhseReceiptHeader.INSERT(TRUE);
      ActivitiesCreated := ActivitiesCreated + 1;
      WhseHeaderCreated := TRUE;
      COMMIT;

      OnAfterCreateRcptHeader(WhseReceiptHeader,"Warehouse Request","Purchase Line");
    END;

    LOCAL PROCEDURE UpdateReceiptHeaderStatus@19();
    BEGIN
      WITH WhseReceiptHeader DO BEGIN
        IF "No." = '' THEN
          EXIT;
        VALIDATE("Document Status",GetHeaderStatus(0));
        MODIFY(TRUE);
      END;
    END;

    [External]
    PROCEDURE SetSkipBlocked@2(Skip@1000 : Boolean);
    BEGIN
      SkipBlockedCustomer := Skip;
    END;

    [External]
    PROCEDURE SetSkipBlockedItem@11(Skip@1000 : Boolean);
    BEGIN
      SkipBlockedItem := Skip;
    END;

    LOCAL PROCEDURE VerifyItemNotBlocked@12(ItemNo@1000 : Code[20]);
    VAR
      Item@1001 : Record 27;
    BEGIN
      Item.GET(ItemNo);
      IF SkipBlockedItem AND Item.Blocked THEN
        CurrReport.SKIP;

      Item.TESTFIELD(Blocked,FALSE);
    END;

    [External]
    PROCEDURE ShowReceiptDialog@13();
    VAR
      SpecialHandlingMessage@1000 : Text[1024];
    BEGIN
      IF NOT LineCreated THEN
        ERROR(Text000);

      IF ErrorOccured THEN
        SpecialHandlingMessage :=
          ' ' + STRSUBSTNO(Text005,WhseReceiptHeader.TABLECAPTION,WhseReceiptLine.FIELDCAPTION("Bin Code"));
      IF (ActivitiesCreated = 0) AND LineCreated AND ErrorOccured THEN
        MESSAGE(SpecialHandlingMessage);
      IF ActivitiesCreated = 1 THEN
        MESSAGE(STRSUBSTNO(Text001,ActivitiesCreated,WhseReceiptHeader.TABLECAPTION) + SpecialHandlingMessage);
      IF ActivitiesCreated > 1 THEN
        MESSAGE(STRSUBSTNO(Text002,ActivitiesCreated) + SpecialHandlingMessage);
    END;

    [External]
    PROCEDURE ShowShipmentDialog@14();
    VAR
      SpecialHandlingMessage@1000 : Text[1024];
    BEGIN
      IF NOT LineCreated THEN
        ERROR(Text003);

      IF ErrorOccured THEN
        SpecialHandlingMessage :=
          ' ' + STRSUBSTNO(Text005,WhseShptHeader.TABLECAPTION,WhseShptLine.FIELDCAPTION("Bin Code"));
      IF (ActivitiesCreated = 0) AND LineCreated AND ErrorOccured THEN
        MESSAGE(SpecialHandlingMessage);
      IF ActivitiesCreated = 1 THEN
        MESSAGE(STRSUBSTNO(Text001,ActivitiesCreated,WhseShptHeader.TABLECAPTION) + SpecialHandlingMessage);
      IF ActivitiesCreated > 1 THEN
        MESSAGE(STRSUBSTNO(Text004,ActivitiesCreated) + SpecialHandlingMessage);
    END;

    LOCAL PROCEDURE CheckFillQtyToHandle@15();
    BEGIN
      IF DoNotFillQtytoHandle AND (RequestType = RequestType::Receive) THEN BEGIN
        WhseReceiptLine.RESET;
        WhseReceiptLine.SETRANGE("No.",WhseReceiptHeader."No.");
        WhseReceiptLine.DeleteQtyToReceive(WhseReceiptLine);
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCreateRcptHeader@22(VAR WarehouseReceiptHeader@1000 : Record 7316;WarehouseRequest@1001 : Record 5765;PurchaseLine@1002 : Record 39);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCreateShptHeader@20(VAR WarehouseShipmentHeader@1000 : Record 7320;WarehouseRequest@1001 : Record 5765;SalesLine@1002 : Record 37);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCreateWhseDocuments@16(VAR WhseReceiptHeader@1000 : Record 7316;VAR WhseShipmentHeader@1001 : Record 7320;WhseHeaderCreated@1002 : Boolean;VAR WarehouseRequest@1003 : Record 5765);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterProcessDocumentLine@17(VAR WarehouseShipmentHeader@1000 : Record 7320;VAR WarehouseRequest@1001 : Record 5765;VAR LineCreated@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSalesHeaderOnAfterGetRecord@23(SalesHeader@1000 : Record 36;VAR SkipRecord@1001 : Boolean;VAR BreakReport@1003 : Boolean;VAR WarehouseRequest@1002 : Record 5765);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterTransHeaderOnAfterGetRecord@41(TransferHeader@1002 : Record 5740;VAR SkipRecord@1001 : Boolean;VAR BreakReport@1000 : Boolean;VAR WarehouseRequest@1003 : Record 5765);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSalesHeaderOnPreDataItem@42(VAR SalesHeader@1000 : Record 36);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSalesLineOnPreDataItem@25(VAR SalesLine@1000 : Record 37;OneHeaderCreated@1001 : Boolean;WhseShptHeader@1002 : Record 7320;WhseReceiptHeader@1003 : Record 7316);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterServiceLineOnPreDataItem@32(VAR ServiceLine@1000 : Record 5902;OneHeaderCreated@1001 : Boolean;WhseShptHeader@1002 : Record 7320;WhseReceiptHeader@1003 : Record 7316);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPurchaseLineOnPreDataItem@28(VAR PurchaseLine@1000 : Record 39;OneHeaderCreated@1001 : Boolean;WhseShptHeader@1002 : Record 7320;WhseReceiptHeader@1003 : Record 7316);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterTransferLineOnPreDataItem@30(VAR TransferLine@1000 : Record 5741;OneHeaderCreated@1001 : Boolean;WhseShptHeader@1002 : Record 7320;WhseReceiptHeader@1003 : Record 7316);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterWarehouseRequestOnPreDataItem@43(VAR WarehouseRequest@1000 : Record 5765);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCreateRcptHeader@24(VAR WarehouseReceiptHeader@1000 : Record 7316;VAR WarehouseRequest@1001 : Record 5765;PurchaseLine@1002 : Record 39;VAR IsHandled@1003 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCreateShptHeader@21(VAR WarehouseShipmentHeader@1000 : Record 7320;VAR WarehouseRequest@1002 : Record 5765;SalesLine@1001 : Record 37;VAR IsHandled@1003 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCreateWhseDocuments@18(VAR WarehouseReceiptHeader@1000 : Record 7316;VAR WarehouseShipmentHeader@1001 : Record 7320;VAR IsHandled@1002 : Boolean;VAR WarehouseRequest@1003 : Record 5765);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeWarehouseRequestOnAfterGetRecord@44(VAR WarehouseRequest@1000 : Record 5765;VAR WhseHeaderCreated@1001 : Boolean;VAR SkipRecord@1003 : Boolean;VAR BreakReport@1004 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeWhseReceiptHeaderInsert@26(VAR WarehouseReceiptHeader@1000 : Record 7316;WarehouseRequest@1001 : Record 5765);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeWhseShptHeaderInsert@45(VAR WarehouseShipmentHeader@1000 : Record 7320;VAR WarehouseRequest@1001 : Record 5765);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnSalesLineOnAfterGetRecordOnBeforeCreateRcptHeader@46(SalesLine@1000 : Record 37;VAR WarehouseRequest@1001 : Record 5765;VAR WarehouseReceiptHeader@1002 : Record 7316;VAR WhseHeaderCreated@1003 : Boolean;VAR OneHeaderCreated@1004 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnSalesLineOnAfterGetRecordOnBeforeCreateShptHeader@47(SalesLine@1000 : Record 37;VAR WarehouseRequest@1001 : Record 5765;VAR WarehouseShipmentHeader@1002 : Record 7320;VAR WhseHeaderCreated@1003 : Boolean;VAR OneHeaderCreated@1004 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnTransferLineOnAfterGetRecordOnBeforeCreateRcptHeader@48(TransferLine@1000 : Record 5741;VAR WarehouseRequest@1001 : Record 5765;VAR WarehouseReceiptHeader@1002 : Record 7316;VAR WhseHeaderCreated@1003 : Boolean;VAR OneHeaderCreated@1004 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnTransferLineOnAfterGetRecordOnBeforeCreateShptHeader@49(TransferLine@1000 : Record 5741;VAR WarehouseRequest@1001 : Record 5765;VAR WarehouseShipmentHeader@1002 : Record 7320;VAR WhseHeaderCreated@1003 : Boolean;VAR OneHeaderCreated@1004 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

