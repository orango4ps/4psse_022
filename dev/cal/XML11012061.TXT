OBJECT XMLport 11012061 Import GL Entries Paymaster 6
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Import GL Entries Paymaster 6;
               SVE=Importera redovisningstransaktioner Paymaster 6];
    Direction=Import;
    DefaultFieldsValidation=No;
    TextEncoding=WINDOWS;
    OnInitXMLport=BEGIN
                    GLSetup.GET;
                    HumanResourcesSetup.GET;
                    HumanResourcesSetup.TESTFIELD("Salary Application",HumanResourcesSetup."Salary Application"::Paymaster);
                    IF HumanResourcesSetup."Length Company Paymaster" = 0 THEN
                      HumanResourcesSetup."Length Company Paymaster" := 4;

                    currXMLport.FILENAME := HumanResourcesSetup."Pathname Files from Salary";
                  END;

    OnPostXMLport=BEGIN
                    WITH "Gen. Journal Line" DO BEGIN //With statement added because this code has been copied from dataport.

                    WHILE BufferRec.FIND('-') DO BEGIN
                      BufferRec.SETRANGE(Company, BufferRec.Company);
                      BufferRec.SETRANGE("Journal Template Name", BufferRec."Journal Template Name");
                      BufferRec.SETRANGE("Journal Batch Name", BufferRec."Journal Batch Name");
                      BufferRec.SETRANGE(Post, BufferRec.Post);
                      BufferRec.SETRANGE("Account No.", BufferRec."Account No.");
                      BufferRec.SETRANGE("Balance Account No.", BufferRec."Balance Account No.");
                      BufferRec.SETRANGE(Project, BufferRec.Project);
                      BufferRec.SETRANGE("Department Code", BufferRec."Department Code");
                      BufferRec.SETRANGE("Cost Object", BufferRec."Cost Object");

                      CumAmount := 0;
                      REPEAT
                        CumAmount := CumAmount + BufferRec.Amount;
                      UNTIL BufferRec.NEXT = 0;

                      "Gen. Journal Line".CHANGECOMPANY(BufferRec.Company);  //diekus, 14-06-06

                      PostRec.CHANGECOMPANY(BufferRec.Company);
                      PostRec.LOCKTABLE;
                      PostRec.SETRANGE("Journal Template Name", BufferRec."Journal Template Name");
                      PostRec.SETRANGE("Journal Batch Name", BufferRec."Journal Batch Name");
                      IF NOT PostRec.FIND('+') THEN PostRec.INIT;

                      INIT;
                      "Journal Template Name" := BufferRec."Journal Template Name";
                      "Journal Batch Name" := BufferRec."Journal Batch Name";
                      "Line No." := PostRec."Line No." + 10000;
                      "Posting Date" := BufferRec."Posting Date";
                      "Document Date" := BufferRec."Posting Date";
                      "Document No." := BufferRec."Document No.";
                      "Source Code" := BufferRec."Source Code";
                      "Reason Code" := BufferRec."Reason Code";
                      "Wage Component" := BufferRec."Wage Component";
                      Amount := CumAmount;
                      "Job No." := BufferRec.Project;
                      Description := BufferRec.Description;
                      "Shortcut Dimension 2 Code" := BufferRec."Cost Object";
                      "Shortcut Dimension 1 Code" := BufferRec."Department Code";
                      IF "Shortcut Dimension 1 Code" <> '' THEN BEGIN                    //DP00387 sn
                        DimMgt.SetCompany("Receiving Company");
                        DimMgt.ValidateShortcutDimValues(1,"Shortcut Dimension 1 Code","Dimension Set ID");
                        DimMgt.SetCompany(COMPANYNAME);
                      END;
                      IF "Shortcut Dimension 2 Code" <> '' THEN BEGIN
                        DimMgt.SetCompany("Receiving Company");
                        DimMgt.ValidateShortcutDimValues(2,"Shortcut Dimension 2 Code","Dimension Set ID");
                        DimMgt.SetCompany(COMPANYNAME);
                      END;                                                               //DP00387 en
                      //FIXED Fill Dimension Set ID
                      IF BufferRec.Post = '' THEN
                         "Account No." := BufferRec."Account No."
                      ELSE
                         "Account No." := BufferRec.Post;
                      "Bal. Account No." := BufferRec."Balance Account No.";
                      "Account Type" := "Account Type"::"G/L Account";
                      "Bal. Account Type" := "Bal. Account Type"::"G/L Account";
                      "System-Created Entry" := TRUE;
                      "Amount (LCY)" := Amount;
                      IF Amount > 0 THEN
                        "Debit Amount" := Amount
                      ELSE
                        "Credit Amount" := -Amount;
                      CASE TRUE OF
                        ("Account No." <> '') AND ("Bal. Account No." <> ''):
                          "Balance (LCY)" := 0;
                        "Bal. Account No." <> '':
                          "Balance (LCY)" := -"Amount (LCY)";
                        ELSE
                          "Balance (LCY)" := "Amount (LCY)";
                      END;

                      //**Check VAT-postinginfo (start)
                      IF NOT GlAccRec.GET("Account No.") THEN GlAccRec.INIT;
                      IF GlAccRec."Import Posting Information" = TRUE THEN BEGIN
                        //**validate-trigger T81 (account no.)
                        "Gen. Posting Type" := GlAccRec."Gen. Posting Type";
                        "Gen. Bus. Posting Group" := GlAccRec."Gen. Bus. Posting Group";
                        "Gen. Prod. Posting Group" := GlAccRec."Gen. Prod. Posting Group";
                        "VAT Bus. Posting Group" := GlAccRec."VAT Bus. Posting Group";
                        "VAT Prod. Posting Group" := GlAccRec."VAT Prod. Posting Group";
                        "Tax Area Code" := GlAccRec."Tax Area Code";
                        "Tax Liable" := GlAccRec."Tax Liable";
                        "Tax Group Code" := GlAccRec."Tax Group Code";
                      END;

                      IF NOT GlAccRec.GET("Bal. Account No.") THEN GlAccRec.INIT;
                      IF GlAccRec."Import Posting Information" = TRUE THEN BEGIN
                        //**validate-trigger T81 (balance account no.)
                        "Bal. Gen. Posting Type" := GlAccRec."Gen. Posting Type";
                        "Bal. Gen. Bus. Posting Group" := GlAccRec."Gen. Bus. Posting Group";
                        "Bal. Gen. Prod. Posting Group" := GlAccRec."Gen. Prod. Posting Group";
                        "Bal. VAT Bus. Posting Group" := GlAccRec."VAT Bus. Posting Group";
                        "Bal. VAT Prod. Posting Group" := GlAccRec."VAT Prod. Posting Group";
                        "Bal. Tax Area Code" := GlAccRec."Tax Area Code";
                        "Bal. Tax Liable" := GlAccRec."Tax Liable";
                        "Bal. Tax Group Code" := GlAccRec."Tax Group Code";
                      END;

                      IF VATPostSetup.GET("VAT Bus. Posting Group", "VAT Prod. Posting Group") THEN BEGIN
                        //**validate-trigger T81 (vat %)
                        "VAT Calculation Type" := VATPostSetup."VAT Calculation Type";
                        IF "VAT Calculation Type" = "VAT Calculation Type"::"Normal VAT" THEN
                          "VAT %" := VATPostSetup."VAT %";
                        CASE "VAT Calculation Type" OF
                          "VAT Calculation Type"::"Normal VAT",
                          "VAT Calculation Type"::"Reverse Charge VAT":
                            "VAT Amount" := Amount - ROUND(Amount / (1 + "VAT %" / 100), 0.01);
                          "VAT Calculation Type"::"Full VAT":
                            "VAT Amount" := Amount;
                        END;
                      END;
                      "Amount (LCY)" := Amount;
                      "VAT Base Amount" := Amount - "VAT Amount";
                      "VAT Amount (LCY)" := "VAT Amount";
                      "VAT Base Amount (LCY)" := "Amount (LCY)" - "VAT Amount (LCY)";

                      IF VATPostSetup.GET("Bal. VAT Bus. Posting Group", "Bal. VAT Prod. Posting Group") THEN BEGIN
                        "Bal. VAT Calculation Type" := VATPostSetup."VAT Calculation Type";
                        IF "Bal. VAT Calculation Type" = "Bal. VAT Calculation Type"::"Normal VAT" THEN
                          "Bal. VAT %" := VATPostSetup."VAT %";
                        CASE "Bal. VAT Calculation Type" OF
                          "Bal. VAT Calculation Type"::"Normal VAT",
                          "Bal. VAT Calculation Type"::"Reverse Charge VAT":
                            "Bal. VAT Amount" := -(Amount - ROUND(Amount / (1 + "Bal. VAT %" / 100), 0.01));
                          "Bal. VAT Calculation Type"::"Full VAT":
                            "Bal. VAT Amount" := -Amount;
                        END;
                      END;
                      "Bal. VAT Base Amount" := -(Amount + "Bal. VAT Amount");
                      "Bal. VAT Amount (LCY)" := "Bal. VAT Amount";
                      "Bal. VAT Base Amount (LCY)" := -("Amount (LCY)" + "Bal. VAT Amount (LCY)");
                    //**validate-trigger T81 (vat % balance account)
                      "Origin Salary Application" := TRUE;

                      INSERT;

                      BufferRec.DELETEALL;
                      BufferRec.RESET;
                    END;

                    END; //Belongs to With statement.

                    NewFile := HumanResourcesSetup.DetermineBackupFile(currXMLport.FILENAME);
                    IF NewFile <> currXMLport.FILENAME THEN
                      FileMgt.RenameClientFile(currXMLport.FILENAME, NewFile, TRUE);
                  END;

    Format=Fixed Text;
  }
  ELEMENTS
  {
    { [{C2A664D9-787F-4630-AC87-E054BC8183AA}];  ;Root                ;Element ;Text     }

    { [{2E7DE16E-6874-40E8-8AC5-42B66F431A3C}];1 ;GenJournalLine      ;Element ;Table   ;
                                                  SourceTable=Table81;
                                                  SourceTableView=SORTING(Field1,Field51,Field2);
                                                  Import::OnAfterInitRecord=BEGIN
                                                                              "Gen. Journal Line".INIT;
                                                                              CLEAR(JournalTemplate);
                                                                              CLEAR(DocumentNo);
                                                                              CLEAR(Year);
                                                                              CLEAR(Period);
                                                                              CLEAR(Date);
                                                                              CLEAR(AccountCode);
                                                                              CLEAR(BalanceAccountCode);
                                                                              CLEAR(Department);
                                                                              CLEAR(WageCode);
                                                                              CLEAR(Desc);
                                                                              CLEAR(RunNo);
                                                                              CLEAR(HoursTxt);
                                                                              CLEAR(HourSign);
                                                                              CLEAR(ValueTxt);
                                                                              CLEAR(AmountSign);
                                                                              CLEAR(CompanyAndAdministration);
                                                                            END;

                                                  Import::OnBeforeInsertRecord=BEGIN
                                                                                 WITH "Gen. Journal Line" DO BEGIN //With statement added because this code has been copied from dataport.

                                                                                 Company := COPYSTR(CompanyAndAdministration, 1, HumanResourcesSetup."Length Company Paymaster");
                                                                                 Administration := COPYSTR(CompanyAndAdministration, HumanResourcesSetup."Length Company Paymaster" + 1);
                                                                                 DetermineCompany;

                                                                                 //switch per table to company depending on data from dataportrecord;
                                                                                 //be aware: validate only works on table of own company !
                                                                                 HumanResSetup.CHANGECOMPANY(CompRec."Navision Company");
                                                                                 JobsSetup.CHANGECOMPANY(CompRec."Navision Company");
                                                                                 JnlTemplRec.CHANGECOMPANY(CompRec."Navision Company");
                                                                                 JnlTemplRec.CHANGECOMPANY(CompRec."Navision Company");
                                                                                 JnlPaymRec.CHANGECOMPANY(CompRec."Navision Company");
                                                                                 WageRec.CHANGECOMPANY(CompRec."Navision Company");
                                                                                 ProjRec.CHANGECOMPANY(CompRec."Navision Company");
                                                                                 PtypRec.CHANGECOMPANY(CompRec."Navision Company");
                                                                                 DimValRec.CHANGECOMPANY(CompRec."Navision Company");
                                                                                 PostRec.CHANGECOMPANY(CompRec."Navision Company");
                                                                                 GlAccRec.CHANGECOMPANY(CompRec."Navision Company");
                                                                                 VATPostSetup.CHANGECOMPANY(CompRec."Navision Company");
                                                                                 "Gen. Journal Line".CHANGECOMPANY(CompRec."Navision Company");

                                                                                 HumanResSetup.GET;
                                                                                 HumanResSetup.TESTFIELD("Salary Application",HumanResSetup."Salary Application"::Paymaster);
                                                                                 JobsSetup.GET;

                                                                                 IF NOT JnlPaymRec.GET(JournalTemplate) THEN
                                                                                   ERROR(Text005,CompRec."Navision Company");

                                                                                 IF NOT JnlTemplRec.GET(JnlPaymRec."GL Entry Journal Templ. (NAV)") THEN
                                                                                   JnlTemplRec.INIT;
                                                                                 IF NOT JnlBatchRec.GET(JnlPaymRec."GL Entry Journal Templ. (NAV)", JnlPaymRec."GL Entry Journal Batch (NAV)") THEN
                                                                                   JnlBatchRec.INIT;

                                                                                 IF DocNo = '' THEN BEGIN
                                                                                   NoSeriesMgt.SetFromOtherCompany(CompRec."Navision Company");
                                                                                   IF JnlBatchRec."No. Series" <> '' THEN
                                                                                     DocNo := NoSeriesMgt.GetNextNo(JnlBatchRec."No. Series",0D,FALSE)
                                                                                   ELSE
                                                                                     IF JnlTemplRec."No. Series" <> '' THEN
                                                                                       DocNo := NoSeriesMgt.GetNextNo(JnlTemplRec."No. Series",0D,FALSE);
                                                                                 END;

                                                                                 IF NOT WageRec.GET(WageCode) THEN WageRec.INIT;

                                                                                 IF WageRec.Project <> '' THEN BEGIN
                                                                                   //get account no. via Project Type/Cost Type
                                                                                   DimValRec.GET(GLSetup."Shortcut Dimension 2 Code", WageRec."Cost Object");
                                                                                   ProjRec.GET(WageRec.Project);
                                                                                   PtypRec.GET(ProjRec."Project Type");

                                                                                   IF (JobsSetup."Provisions at Closure") AND
                                                                                      (ProjRec."Project Status" >= ProjRec."Project Status"::Finished) THEN
                                                                                   BEGIN
                                                                                     CASE DimValRec."Cost Type" OF
                                                                                       DimValRec."Cost Type"::Labor :
                                                                                         WageRec."Account No." := PtypRec."Provision Account Labor";
                                                                                       DimValRec."Cost Type"::Material :
                                                                                         WageRec."Account No." := PtypRec."Provision Account Material";
                                                                                       DimValRec."Cost Type"::Subcontracting :
                                                                                         WageRec."Account No." := PtypRec."Provision Account Subcontr.";
                                                                                       DimValRec."Cost Type"::Plant :
                                                                                         WageRec."Account No." := PtypRec."Provision Account Plant";
                                                                                       DimValRec."Cost Type"::Sundry :
                                                                                         WageRec."Account No." := PtypRec."Provision Account Sundry";
                                                                                       DimValRec."Cost Type"::Revenue :
                                                                                         WageRec."Account No." := PtypRec."Provision Account Revenue";
                                                                                     END;

                                                                                   END ELSE BEGIN

                                                                                     CASE DimValRec."Cost Type" OF
                                                                                       DimValRec."Cost Type"::Labor :
                                                                                         WageRec."Account No." := PtypRec."WIP Account Labor";
                                                                                       DimValRec."Cost Type"::Material :
                                                                                         WageRec."Account No." := PtypRec."WIP Account Material";
                                                                                       DimValRec."Cost Type"::Subcontracting :
                                                                                         WageRec."Account No." := PtypRec."WIP Account Subcontracting";
                                                                                       DimValRec."Cost Type"::Plant :
                                                                                         WageRec."Account No." := PtypRec."WIP Account Plant";
                                                                                       DimValRec."Cost Type"::Sundry :
                                                                                         WageRec."Account No." := PtypRec."WIP Account Sundry";
                                                                                       DimValRec."Cost Type"::Revenue :
                                                                                         WageRec."Account No." := PtypRec."WIP Account Revenue";
                                                                                     END;

                                                                                   END;
                                                                                 END;

                                                                                 //if (balance)account not to be found via wage code  (or empty),
                                                                                 //take default values from Paymaster-file (fjrlbest.txt)
                                                                                 IF WageRec."Account No." = '' THEN BEGIN
                                                                                   StripZero(AccountCode, MaxLenAcc);
                                                                                   WageRec."Account No." := AccountCode;
                                                                                 END;
                                                                                 IF WageRec."Balance Account No." = '' THEN BEGIN
                                                                                   StripZero(BalanceAccountCode, MaxLenAcc);
                                                                                   WageRec."Balance Account No." := BalanceAccountCode;
                                                                                 END;
                                                                                 IF WageRec."Department Code" = '' THEN BEGIN
                                                                                   IF HumanResourcesSetup."Split Department (Paymaster)" THEN BEGIN
                                                                                     WageRec."Cost Object" := COPYSTR(Department,1,HumanResourcesSetup."Split Department from Position"-1);
                                                                                     Department := COPYSTR(
                                                                                       Department,HumanResourcesSetup."Split Department from Position",
                                                                                       8-(HumanResourcesSetup."Split Department from Position"-1));
                                                                                   END;
                                                                                   StripZero(Department, MaxLenDep);
                                                                                   WageRec."Department Code" := Department;
                                                                                 END;
                                                                                 IF WageRec.Description = '' THEN
                                                                                   WageRec.Description := Desc;

                                                                                 IF HumanResSetup."Compress Journal Entries" = TRUE THEN BEGIN
                                                                                   BufferRec.LOCKTABLE;
                                                                                   LineCounter := LineCounter + 10000;
                                                                                 END ELSE BEGIN
                                                                                   PostRec.LOCKTABLE;
                                                                                   PostRec.SETRANGE("Journal Template Name", JnlPaymRec."GL Entry Journal Templ. (NAV)");
                                                                                   PostRec.SETRANGE("Journal Batch Name", JnlPaymRec."GL Entry Journal Batch (NAV)");
                                                                                   IF NOT PostRec.FIND('+') THEN PostRec.INIT;
                                                                                   LineCounter := PostRec."Line No." + 10000;
                                                                                 END;

                                                                                 //provides a problem on G/L account as 0123 ...; test on length of first record ?
                                                                                 filler := Date;
                                                                                 StripZero(filler, 0);
                                                                                 IF filler <> '' THEN BEGIN
                                                                                   EVALUATE(BookYear,COPYSTR(Date,5,4));
                                                                                   EVALUATE(BookMonth,COPYSTR(Date,3,2));
                                                                                   EVALUATE(BookDay,COPYSTR(Date,1,2));
                                                                                   PostingDate := DMY2DATE(BookDay,BookMonth,BookYear);
                                                                                 END ELSE BEGIN
                                                                                   PostingDate := 0D;
                                                                                 END;

                                                                                 //date supplied in format ddmmyy
                                                                                 Value := Value / 100.0;
                                                                                 IF AmountSign = '-' THEN Value := (Value * -1.0);

                                                                                 ShowStatus(
                                                                                   JnlPaymRec."GL Entry Journal Templ. (NAV)",
                                                                                   JnlPaymRec."GL Entry Journal Batch (NAV)",
                                                                                   LineCounter);

                                                                                 IF HumanResSetup."Compress Journal Entries" = TRUE THEN BEGIN
                                                                                   WITH BufferRec DO BEGIN
                                                                                     INIT;
                                                                                     Company := CompRec."Navision Company";
                                                                                     "Journal Template Name" := JnlPaymRec."GL Entry Journal Templ. (NAV)";
                                                                                     "Journal Batch Name" := JnlPaymRec."GL Entry Journal Batch (NAV)";
                                                                                     "Entry No." := LineCounter;
                                                                                     "Posting Date" := PostingDate;
                                                                                     IF DocNo <> '' THEN
                                                                                       "Document No." := DocNo  //based on no.series of template/batch
                                                                                     ELSE
                                                                                       "Document No." := DocumentNo;  //based on importfile
                                                                                     "Source Code" := JnlTemplRec."Source Code";
                                                                                     IF JnlBatchRec."Reason Code" <> '' THEN
                                                                                       "Reason Code" := JnlBatchRec."Reason Code"
                                                                                     ELSE
                                                                                       "Reason Code" := JnlTemplRec."Reason Code";
                                                                                     "Wage Component" := WageCode;
                                                                                     Amount := Value;
                                                                                     Project := WageRec.Project;
                                                                                     IF EntryDesc <> '' THEN
                                                                                       Description := EntryDesc
                                                                                     ELSE
                                                                                       Description := WageRec.Description;
                                                                                     "Cost Object" := WageRec."Cost Object";
                                                                                     "Department Code" := WageRec."Department Code";
                                                                                     "Account No." := WageRec."Account No.";
                                                                                     "Balance Account No." := WageRec."Balance Account No.";
                                                                                     IF ("Account No." <> '') AND ("Balance Account No." = '') THEN BEGIN
                                                                                       Post := "Account No.";
                                                                                       "Account No." := '';
                                                                                     END;
                                                                                     IF ("Account No." = '') AND ("Balance Account No." <> '') THEN BEGIN
                                                                                       Post := "Balance Account No.";
                                                                                       "Balance Account No." := '';
                                                                                       //TEST Amount := -Amount;
                                                                                     END;

                                                                                     INSERT;
                                                                                     Post := '';  //amount in format 9V2 (without delimiter); seperate +/- sign in file
                                                                                   END;
                                                                                   currXMLport.SKIP;
                                                                                 END ELSE BEGIN
                                                                                   INIT;
                                                                                   "Journal Template Name" := JnlPaymRec."GL Entry Journal Templ. (NAV)";
                                                                                   "Journal Batch Name" := JnlPaymRec."GL Entry Journal Batch (NAV)";
                                                                                   "Line No." := LineCounter;
                                                                                   "Posting Date" := PostingDate;
                                                                                   "Document Date" := PostingDate;
                                                                                   IF DocNo <> '' THEN
                                                                                     "Document No." := DocNo  //based on no.series of template/batch
                                                                                   ELSE
                                                                                     "Document No." := DocumentNo;  //based on importfile
                                                                                   "Source Code" := JnlTemplRec."Source Code";
                                                                                   IF JnlBatchRec."Reason Code" <> '' THEN
                                                                                     "Reason Code" := JnlBatchRec."Reason Code"
                                                                                   ELSE
                                                                                     "Reason Code" := JnlTemplRec."Reason Code";
                                                                                   "Wage Component" := WageCode;
                                                                                   "Job No." := WageRec.Project;
                                                                                   IF EntryDesc <> '' THEN
                                                                                     Description := EntryDesc
                                                                                   ELSE
                                                                                     Description := WageRec.Description;
                                                                                   "Shortcut Dimension 2 Code" := WageRec."Cost Object";
                                                                                   "Shortcut Dimension 1 Code" := WageRec."Department Code";
                                                                                   "Account No." := WageRec."Account No.";
                                                                                   "Bal. Account No." := WageRec."Balance Account No.";
                                                                                   "Account Type" := "Account Type"::"G/L Account";
                                                                                   "Bal. Account Type" := "Bal. Account Type"::"G/L Account";
                                                                                   "System-Created Entry" := TRUE;
                                                                                   Amount := Value;
                                                                                   IF ("Account No." = '') AND ("Bal. Account No." <> '') THEN //27-04-11
                                                                                     Amount := -Amount; //27-04-11
                                                                                   "Amount (LCY)" := Amount;
                                                                                   IF Amount > 0 THEN
                                                                                     "Debit Amount" := Amount
                                                                                   ELSE
                                                                                     "Credit Amount" := -Amount;
                                                                                   CASE TRUE OF
                                                                                     ("Account No." <> '') AND ("Bal. Account No." <> ''):
                                                                                       "Balance (LCY)" := 0;
                                                                                     "Bal. Account No." <> '':
                                                                                       "Balance (LCY)" := -"Amount (LCY)";
                                                                                     ELSE
                                                                                       "Balance (LCY)" := "Amount (LCY)";
                                                                                   END;

                                                                                   IF NOT GlAccRec.GET("Account No.") THEN GlAccRec.INIT;
                                                                                   IF GlAccRec."Import Posting Information" = TRUE THEN BEGIN
                                                                                     //**Check VAT-postinginfo (start)
                                                                                     "Gen. Posting Type" := GlAccRec."Gen. Posting Type";
                                                                                     "Gen. Bus. Posting Group" := GlAccRec."Gen. Bus. Posting Group";
                                                                                     "Gen. Prod. Posting Group" := GlAccRec."Gen. Prod. Posting Group";
                                                                                     "VAT Bus. Posting Group" := GlAccRec."VAT Bus. Posting Group";
                                                                                     "VAT Prod. Posting Group" := GlAccRec."VAT Prod. Posting Group";
                                                                                     "Tax Area Code" := GlAccRec."Tax Area Code";
                                                                                     "Tax Liable" := GlAccRec."Tax Liable";
                                                                                     "Tax Group Code" := GlAccRec."Tax Group Code";
                                                                                   END;

                                                                                   IF NOT GlAccRec.GET("Bal. Account No.") THEN GlAccRec.INIT;
                                                                                   IF GlAccRec."Import Posting Information" = TRUE THEN BEGIN
                                                                                     //**validate-trigger T81 (account no.)
                                                                                     "Bal. Gen. Posting Type" := GlAccRec."Gen. Posting Type";
                                                                                     "Bal. Gen. Bus. Posting Group" := GlAccRec."Gen. Bus. Posting Group";
                                                                                     "Bal. Gen. Prod. Posting Group" := GlAccRec."Gen. Prod. Posting Group";
                                                                                     "Bal. VAT Bus. Posting Group" := GlAccRec."VAT Bus. Posting Group";
                                                                                     "Bal. VAT Prod. Posting Group" := GlAccRec."VAT Prod. Posting Group";
                                                                                     "Bal. Tax Area Code" := GlAccRec."Tax Area Code";
                                                                                     "Bal. Tax Liable" := GlAccRec."Tax Liable";
                                                                                     "Bal. Tax Group Code" := GlAccRec."Tax Group Code";
                                                                                   END;

                                                                                   IF VATPostSetup.GET("VAT Bus. Posting Group", "VAT Prod. Posting Group") THEN BEGIN
                                                                                     //**validate-trigger T81 (balance account no.)
                                                                                     "VAT Calculation Type" := VATPostSetup."VAT Calculation Type";
                                                                                     IF "VAT Calculation Type" = "VAT Calculation Type"::"Normal VAT" THEN
                                                                                       "VAT %" := VATPostSetup."VAT %";
                                                                                     CASE "VAT Calculation Type" OF
                                                                                       "VAT Calculation Type"::"Normal VAT",
                                                                                       "VAT Calculation Type"::"Reverse Charge VAT":
                                                                                          "VAT Amount" := Amount - ROUND(Amount / (1 + "VAT %" / 100), 0.01);
                                                                                       "VAT Calculation Type"::"Full VAT":
                                                                                         "VAT Amount" := Amount;
                                                                                     END;
                                                                                   END;
                                                                                   "Amount (LCY)" := Amount;
                                                                                   "VAT Base Amount" := Amount - "VAT Amount";
                                                                                   "VAT Amount (LCY)" := "VAT Amount";
                                                                                   "VAT Base Amount (LCY)" := "Amount (LCY)" - "VAT Amount (LCY)";

                                                                                   IF VATPostSetup.GET("Bal. VAT Bus. Posting Group", "Bal. VAT Prod. Posting Group") THEN BEGIN
                                                                                     //**validate-trigger T81 (vat %)
                                                                                     "Bal. VAT Calculation Type" := VATPostSetup."VAT Calculation Type";
                                                                                     IF "Bal. VAT Calculation Type" = "Bal. VAT Calculation Type"::"Normal VAT" THEN
                                                                                       "Bal. VAT %" := VATPostSetup."VAT %";
                                                                                     CASE "Bal. VAT Calculation Type" OF
                                                                                       "Bal. VAT Calculation Type"::"Normal VAT",
                                                                                       "Bal. VAT Calculation Type"::"Reverse Charge VAT":
                                                                                         "Bal. VAT Amount" := -(Amount - ROUND(Amount / (1 + "Bal. VAT %" / 100), 0.01));
                                                                                       "Bal. VAT Calculation Type"::"Full VAT":
                                                                                         "Bal. VAT Amount" := -Amount;
                                                                                     END;
                                                                                   END;
                                                                                   "Bal. VAT Base Amount" := -(Amount + "Bal. VAT Amount");
                                                                                   "Bal. VAT Amount (LCY)" := "Bal. VAT Amount";
                                                                                   "Bal. VAT Base Amount (LCY)" := -("Amount (LCY)" + "Bal. VAT Amount (LCY)");
                                                                                   "Origin Salary Application" := TRUE;
                                                                                   //FIXED Fill Dimension Set ID
                                                                                   IF "Shortcut Dimension 1 Code" <> '' THEN BEGIN                    //DP00387 sn
                                                                                     DimMgt.SetCompany("Receiving Company");
                                                                                     DimMgt.ValidateShortcutDimValues(1,"Shortcut Dimension 1 Code","Dimension Set ID");
                                                                                     DimMgt.SetCompany(COMPANYNAME);
                                                                                   END;
                                                                                   IF "Shortcut Dimension 2 Code" <> '' THEN BEGIN
                                                                                     DimMgt.SetCompany("Receiving Company");
                                                                                     DimMgt.ValidateShortcutDimValues(2,"Shortcut Dimension 2 Code","Dimension Set ID");
                                                                                     DimMgt.SetCompany(COMPANYNAME);
                                                                                   END;                                                               //DP00387 en

                                                                                   INSERT;
                                                                                 END;

                                                                                 currXMLport.SKIP;

                                                                                 END; //Belongs to With statement.
                                                                               END;
                                                                                }

    { [{9CC237A7-EBEB-4C16-A229-65392CC50DBE}];2 ;JournalTemplate     ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=2 }

    { [{8BE236EA-0EDB-47D9-B2DD-07C491BCF5B3}];2 ;DocumentNo          ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=7 }

    { [{58544B08-E8FA-4F38-A316-228B57F7B91A}];2 ;Year                ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=4 }

    { [{2E8D237C-0BF0-40A2-A71D-EBC1498E7D39}];2 ;Period              ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=2 }

    { [{B66EFA61-0924-44B7-A555-E69E1F489566}];2 ;Date                ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=8 }

    { [{0AA1DABB-5720-43B5-8142-0AFF99B91F0A}];2 ;AccountCode         ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=9 }

    { [{82550078-E0EC-401C-8CDD-91C0167D12B3}];2 ;BalanceAccountCode  ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=9 }

    { [{D73C02F9-FAAF-4876-9AC0-914C7C25BC4C}];2 ;Department          ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=10 }

    { [{5A21F9F7-2A3D-4674-A5DA-F6BC32C1AFF6}];2 ;WageCode            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=4 }

    { [{0250ADED-6753-40D6-ADF6-1307E0781B58}];2 ;Desc                ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=20 }

    { [{DCE6C1EE-7EB2-4490-AB3E-33A4C29C794E}];2 ;RunNo               ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=7 }

    { [{4E8A3BC1-F3E9-4F27-AE70-9AD0C80F9DBE}];2 ;HoursTxt            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Export::OnBeforePassVariable=BEGIN
                                                                                 FillEmptyTextWithZero(HoursTxt);
                                                                                 EVALUATE(Hours, HoursTxt);
                                                                               END;

                                                  Width=11 }

    { [{9DE29069-BDAB-46EF-92E8-6EE08568AD34}];2 ;HourSign            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=1 }

    { [{7686A59E-99BF-408D-8C1B-130FC86F4C5E}];2 ;ValueTxt            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  FillEmptyTextWithZero(ValueTxt);
                                                                                  EVALUATE(Value, ValueTxt);
                                                                                END;

                                                  Width=11 }

    { [{65A872A6-128B-4C22-A31B-4DBDDF0FC49B}];2 ;AmountSign          ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=1 }

    { [{4536BF19-9654-49F8-9EED-39B2025928C5}];2 ;CompanyAndAdministration;Element;Text ;
                                                  MinOccurs=Zero;
                                                  Width=7 }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1100409001;;Container;
                  ContainerType=ContentArea }

      { 1100409000;1;Group  ;
                  CaptionML=[ENU=Options;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100409002;2;Field  ;
                  Name=EntryDesc;
                  CaptionML=[ENU=Entry Description;
                             SVE=Transaktionsbeskrivning];
                  SourceExpr=EntryDesc }

      { 1100409003;2;Field  ;
                  Name=RemZero;
                  CaptionML=[ENU=Remove Zero Prefix;
                             SVE=Ta bort nollprefix];
                  OptionCaptionML=[ENU=All,Upto Maximum Used Field Length;
                                   SVE=Alla,Upp till maximalt anvnd fltlngd];
                  SourceExpr=RemZero;
                  OnValidate=BEGIN
                               IF RemZero = RemZero::All THEN BEGIN
                                 MaxLenAcc := 0;
                                 MaxLenDep := 0;
                               END ELSE BEGIN
                                 MaxLenAcc := MAXSTRLEN(GlAccRec."No.");
                                 MaxLenDep := MAXSTRLEN(DimValRec.Code);
                               END;
                             END;
                              }

      { 1100409004;2;Field  ;
                  Name=MaxLenAcc;
                  CaptionML=[ENU=Account No.;
                             SVE=Nr];
                  SourceExpr=MaxLenAcc;
                  OnValidate=BEGIN
                               IF RemZero = RemZero::All THEN
                                 MaxLenAcc := 0;
                             END;
                              }

      { 1100409005;2;Field  ;
                  Name=MaxLenDep;
                  CaptionML=[ENU=Department Code;
                             SVE=Mottagande avdelningskod];
                  SourceExpr=MaxLenDep;
                  OnValidate=BEGIN
                               IF RemZero = RemZero::All THEN
                                 MaxLenDep := 0;
                             END;
                              }

    }
  }
  CODE
  {
    VAR
      GLSetup@1100409048 : Record 98;
      HumanResourcesSetup@1100409047 : Record 5218;
      VATPostSetup@1100409046 : Record 325;
      JnlPaymRec@1100409045 : Record 11012376;
      JnlTemplRec@1100409044 : Record 80;
      JnlBatchRec@1100409043 : Record 232;
      CompRec@1100409042 : Record 11012375;
      WageRec@1100409041 : Record 11012377;
      HumanResSetup@1100409040 : Record 5218;
      JobsSetup@1100525001 : Record 315;
      PostRec@1100409039 : Record 81;
      ProjRec@1100409038 : Record 11072003;
      PtypRec@1100409037 : Record 11012009;
      GlAccRec@1100409036 : Record 15;
      BufferRec@1100409035 : Record 11012378;
      DimValRec@1100409033 : Record 349;
      FileMgt@1100409006 : Codeunit 419;
      XMLportStatusDialogMgt@1100409016 : Codeunit 11130006;
      DimMgt@1100525000 : Codeunit 408;
      LineCounter@1100409032 : Integer;
      BookYear@1100409031 : Integer;
      BookMonth@1100409030 : Integer;
      BookDay@1100409029 : Integer;
      PostingDate@1100409028 : Date;
      Company@1100409025 : Text[30];
      Administration@1100409024 : Text[30];
      DocNo@1100409020 : Code[20];
      Hours@1100409011 : Decimal;
      Value@1100409009 : Decimal;
      CumAmount@1100409008 : Decimal;
      EntryDesc@1100409007 : Text[30];
      NewFile@1100409005 : Text[250];
      filler@1100409004 : Text[30];
      NoSeriesMgt@1100409003 : Codeunit 396;
      RemZero@1100409002 : 'All,Field Length';
      MaxLenAcc@1100409001 : Integer;
      MaxLenDep@1100409000 : Integer;
      Text001@1100409014 : TextConst 'ENU=Navision Company can not be determined via Company and Administration from Paymaster;SVE=Navision-fretaget kan inte faststllas via fretag och administration frn Paymaster';
      Text005@1100409010 : TextConst 'ENU=Journal Paymaster not attached to Navision GL Entry Journal Template for Company %1;SVE=Paymaster-journal r inte bifogad till journalmall fr Navision GL-transaktion fr fretag %1';

    PROCEDURE StripZero@1(VAR convtext@11012000 : Text[30];LenFld@1210190000 : Integer);
    VAR
      Position@11012001 : Integer;
    BEGIN
      Position := STRPOS(convtext,'0');
      WHILE Position = 1 DO BEGIN
        IF LenFld <> 0 THEN
          IF STRLEN(convtext) = LenFld THEN
            EXIT;
        convtext := DELSTR(convtext,Position,1);
        Position := STRPOS(convtext,'0');
      END;
    END;

    PROCEDURE DetermineCompany@1210190001();
    BEGIN
      CompRec.SETRANGE("Company Salary Application", Company);
      CompRec.SETRANGE(Administration, Administration);
      IF NOT CompRec.FIND('-') THEN
        ERROR(Text001);
    END;

    PROCEDURE FillEmptyTextWithZero@1100525013(VAR Text@1100525000 : Text[1024]);
    BEGIN
      IF DELCHR(Text,'<>=',' ') = '' THEN
        Text := '0';
    END;

    LOCAL PROCEDURE ShowStatus@1100409000(JournalTemplateName@1100409000 : Code[10];JournalBatchName@1100409001 : Code[10];LineNo@1100409002 : Integer);
    VAR
      GenJournalLine@1100409004 : Record 81;
      RecRef@1100409003 : RecordRef;
    BEGIN
      GenJournalLine."Journal Template Name" := JournalTemplateName;
      GenJournalLine."Journal Batch Name" := JournalBatchName;
      GenJournalLine."Line No." := LineNo;
      RecRef.GETTABLE(GenJournalLine);
      XMLportStatusDialogMgt.ShowImportStatus(RecRef);
    END;

    PROCEDURE InitXMLPort@1100528500(iEntryDesc@1100528503 : Text[30];iRemZero@1100528502 : 'All,Field Length';iMaxLenAcc@1100528501 : Integer;iMaxLenDep@1100528500 : Integer);
    BEGIN
      EntryDesc := iEntryDesc;
      RemZero := iRemZero;
      MaxLenAcc := iMaxLenAcc;
      MaxLenDep := iMaxLenDep;
    END;

    BEGIN
    {
      CC: Dataport 11012353
    }
    END.
  }
}

