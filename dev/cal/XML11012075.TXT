OBJECT XMLport 11012075 Import GL Entries UNIT4
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Import GL Entries UNIT4;
               SVE=Importera redovisningstransaktioner UNIT4];
    Direction=Import;
    DefaultFieldsValidation=No;
    TextEncoding=WINDOWS;
    OnInitXMLport=BEGIN
                    HumanResourcesSetup.GET;
                    HumanResourcesSetup.TESTFIELD("Pathname Files from Salary");
                    HumanResourcesSetup.TESTFIELD("GL Entry Journal Template");
                    HumanResourcesSetup.TESTFIELD("GL Entry Journal Batch");

                    currXMLport.FILENAME(HumanResourcesSetup."Pathname Files from Salary");
                  END;

    OnPreXMLport=BEGIN
                   GenJnlLine.LOCKTABLE;
                   GenJnlLine.SETRANGE("Journal Template Name",HumanResourcesSetup."GL Entry Journal Template");
                   GenJnlLine.SETRANGE("Journal Batch Name",HumanResourcesSetup."GL Entry Journal Batch");
                   IF GenJnlLine.FIND('+') THEN
                     LineCounter := GenJnlLine."Line No."
                   ELSE
                     LineCounter := 0;

                   JnlTemplRec.GET(HumanResourcesSetup."GL Entry Journal Template");
                   JnlBatchRec.GET(HumanResourcesSetup."GL Entry Journal Template", HumanResourcesSetup."GL Entry Journal Batch");

                   IF JnlBatchRec."No. Series" <> '' THEN
                     DocNo := NoSeriesMgt.GetNextNo(JnlBatchRec."No. Series",0D,FALSE)
                   ELSE
                     IF JnlTemplRec."No. Series" <> '' THEN
                       DocNo := NoSeriesMgt.GetNextNo(JnlTemplRec."No. Series",0D,FALSE);
                 END;

    OnPostXMLport=BEGIN
                    OldFile := currXMLport.FILENAME;
                    NewFile := HumanResourcesSetup.DetermineBackupFile(OldFile);
                    IF NewFile <> OldFile THEN
                      FileMgt.RenameClientFile(OldFile, NewFile, TRUE);
                  END;

    Format=Fixed Text;
    UseRequestPage=No;
  }
  ELEMENTS
  {
    { [{5CA0C9E0-7B29-44BA-ACE2-F569DDE033EF}];  ;Root                ;Element ;Text     }

    { [{C6A46525-F3F9-45B4-9D95-155B0A0C73D2}];1 ;GenJournalLine      ;Element ;Table   ;
                                                  SourceTable=Table81;
                                                  SourceTableView=SORTING(Field1,Field51,Field2);
                                                  Import::OnAfterInitRecord=BEGIN
                                                                              "Gen. Journal Line".INIT;
                                                                              CLEAR(DescTxt);
                                                                              CLEAR(DebitDecTxt);
                                                                              CLEAR(CreditDecTxt);
                                                                              CLEAR(DummyTxt);
                                                                            END;

                                                  Import::OnBeforeInsertRecord=VAR
                                                                                 RecRef@1100528400 : RecordRef;
                                                                               BEGIN
                                                                                 WITH "Gen. Journal Line" DO BEGIN //With statement added because this code has been copied from dataport.

                                                                                 "Journal Template Name" := HumanResourcesSetup."GL Entry Journal Template";
                                                                                 "Journal Batch Name" := HumanResourcesSetup."GL Entry Journal Batch";
                                                                                 "Line No." := LineCounter + 10000;
                                                                                 LineCounter := "Line No.";
                                                                                 "Account Type" := "Account Type"::"G/L Account";
                                                                                 "Document No." := DocNo;
                                                                                 "Document Date" := "Posting Date";
                                                                                 "Source Code"   := JnlTemplRec."Source Code";
                                                                                 IF JnlBatchRec."Reason Code" <> '' THEN
                                                                                   "Reason Code" := JnlBatchRec."Reason Code"
                                                                                 ELSE
                                                                                   "Reason Code" := JnlTemplRec."Reason Code";

                                                                                 StripZero("Account No.");

                                                                                 //Strip Department Code trailing zero's one by one until department code exists
                                                                                 DimMgt.GetDimValueRec(1, "Shortcut Dimension 1 Code", DimValRec, FALSE,'');
                                                                                 WHILE StripDepartmentFurther DO BEGIN
                                                                                   "Shortcut Dimension 1 Code" := DELSTR("Shortcut Dimension 1 Code", STRLEN("Shortcut Dimension 1 Code"), 1);
                                                                                   DimMgt.GetDimValueRec(1, "Shortcut Dimension 1 Code", DimValRec, FALSE,'');
                                                                                 END;

                                                                                 IF DebitDec <> 0 THEN
                                                                                   VALIDATE(Amount, DebitDec/100)
                                                                                 ELSE
                                                                                   VALIDATE(Amount, -CreditDec/100);

                                                                                 VALIDATE("Account No.");
                                                                                 VALIDATE("Shortcut Dimension 1 Code");

                                                                                 IF NOT GlAccRec.GET("Account No.") THEN
                                                                                   GlAccRec.INIT;
                                                                                 IF GlAccRec."Import Posting Information" = FALSE THEN
                                                                                   GlAccRec.INIT;
                                                                                 "Gen. Posting Type" := GlAccRec."Gen. Posting Type";
                                                                                 "Gen. Bus. Posting Group" := GlAccRec."Gen. Bus. Posting Group";
                                                                                 "Gen. Prod. Posting Group" := GlAccRec."Gen. Prod. Posting Group";
                                                                                 "VAT Bus. Posting Group" := GlAccRec."VAT Bus. Posting Group";
                                                                                 "VAT Prod. Posting Group" := GlAccRec."VAT Prod. Posting Group";
                                                                                 VALIDATE("VAT Prod. Posting Group");

                                                                                 Description := DescTxt;
                                                                                 "Origin Salary Application" := TRUE;

                                                                                 RecRef.GETTABLE("Gen. Journal Line");
                                                                                 XMLportStatusDialogMgt.ShowImportStatus(RecRef);

                                                                                 INSERT;

                                                                                 currXMLport.SKIP;

                                                                                 END; //Belongs to With statement.
                                                                               END;
                                                                                }

    { [{B75BCE7C-CB27-427A-970D-4CE4028402A4}];2 ;Filler1             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=4 }

    { [{4EC5792F-C5E4-4B6A-9832-A7349C205917}];2 ;DescTxt             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=6 }

    { [{B6BB4034-FD8A-4339-B57E-3D5970E679BA}];2 ;Filler2             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=2 }

    { [{E8DEF588-FD51-42E2-942B-A3EBE26E3BE2}];2 ;PostingDate         ;Element ;Field   ;
                                                  DataType=Date;
                                                  SourceField=Gen. Journal Line::Posting Date;
                                                  MinOccurs=Zero;
                                                  Width=6 }

    { [{0E492378-4829-4BC0-B616-2154EB17B8BC}];2 ;Filler3             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=6 }

    { [{6E57C69C-4EFB-4C64-BFE3-5E7F4BB8D0B7}];2 ;AccountNo           ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=Gen. Journal Line::Account No.;
                                                  MinOccurs=Zero;
                                                  Width=6 }

    { [{7ABDC28A-7A25-4003-88BB-9033EDD4EFAC}];2 ;Filler4             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=20 }

    { [{83DF2C18-28A2-43C5-A459-76EE922130F1}];2 ;DebitDecTxt         ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  FillEmptyTextWithZero(DebitDecTxt);
                                                                                  EVALUATE(DebitDec, DebitDecTxt);
                                                                                END;

                                                  Width=9 }

    { [{C7EA63C7-F807-4CE2-9635-BE8F56C1AD7E}];2 ;CreditDecTxt        ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  FillEmptyTextWithZero(CreditDecTxt);
                                                                                  EVALUATE(CreditDec, CreditDecTxt);
                                                                                END;

                                                  Width=9 }

    { [{9C3D512D-1260-442F-8082-526CE5B13CB0}];2 ;Filler5             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=7 }

    { [{FCED2188-0FEB-4FDF-A662-A619CFBF350C}];2 ;ShortcutDimension1Code;Element;Field  ;
                                                  DataType=Code;
                                                  SourceField=Gen. Journal Line::Shortcut Dimension 1 Code;
                                                  MinOccurs=Zero;
                                                  Width=6 }

    { [{3D10B181-59E2-4D33-B808-72824DBC0551}];2 ;DummyTxt            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=29 }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      HumanResourcesSetup@1100528415 : Record 5218;
      GenJnlLine@1100528414 : Record 81;
      JnlTemplRec@1100528413 : Record 80;
      JnlBatchRec@1100528412 : Record 232;
      GlAccRec@1100528411 : Record 15;
      DimValRec@1100528410 : Record 349;
      NoSeriesMgt@1100528409 : Codeunit 396;
      DimMgt@1100528408 : Codeunit 408;
      FileMgt@1100528400 : Codeunit 419;
      XMLportStatusDialogMgt@1100528403 : Codeunit 11130006;
      LineCounter@1100528407 : Integer;
      CreditDec@1100528406 : Decimal;
      DebitDec@1100528405 : Decimal;
      DocNo@1100528404 : Code[20];
      OldFile@1100528402 : Text[250];
      NewFile@1100528401 : Text[250];

    PROCEDURE StripZero@1(VAR convtext@11012000 : Code[30]);
    VAR
      Position@11012001 : Integer;
    BEGIN
      Position := STRPOS(convtext,'0');
      WHILE Position = 1 DO BEGIN
        convtext := DELSTR(convtext,Position,1);
        Position  := STRPOS(convtext,'0');
      END;
    END;

    PROCEDURE StripDepartmentFurther@1100485001() lStripFurther : Boolean;
    BEGIN
      lStripFurther := TRUE;

      WITH "Gen. Journal Line" DO BEGIN
        IF DimValRec.Code <> '' THEN
          lStripFurther := FALSE
        ELSE
          IF STRLEN("Shortcut Dimension 1 Code") = 0 THEN
            lStripFurther := FALSE
          ELSE
            IF (COPYSTR("Shortcut Dimension 1 Code",STRLEN("Shortcut Dimension 1 Code"), 1) <> '0') THEN
              lStripFurther := FALSE;
      END;
    END;

    PROCEDURE FillEmptyTextWithZero@1100525013(VAR Text@1100525000 : Text[1024]);
    BEGIN
      IF DELCHR(Text,'<>=',' ') = '' THEN
        Text := '0';
    END;

    BEGIN
    {
      CC: Dataport 11012345
    }
    END.
  }
}

