OBJECT Table 6086001 Continia User
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.5.00.03;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    DataCaptionFields=User ID,Name;
    OnInsert=BEGIN
               "Creation Date" := TODAY;
             END;

    OnDelete=VAR
               Company@1160040000 : Record 2000000006;
               ContiniaUserSetup@1160040001 : Record 6086002;
               NoOfContiniaUserSetups@1160040002 : Integer;
             BEGIN
               IF NOT ContiniaUserSetupDeleteSuspend THEN BEGIN
                 NoOfContiniaUserSetups := GetNoOfContiniaUserSetups;
                 IF NoOfContiniaUserSetups > 1 THEN
                   IF NOT CONFIRM(Text003,TRUE,"User ID") THEN
                     ERROR('');

                 Company.FINDSET;
                 REPEAT
                   ContiniaUserSetup.CHANGECOMPANY(Company.Name);
                   IF ContiniaUserSetup.GET("User ID") THEN BEGIN
                     ContiniaUserSetup.SetCurrCompanyName(Company.Name);
                     ContiniaUserSetup.SetHideUserDeleteConfirmation(TRUE);
                     ContiniaUserSetup.SuspendContiniaUserDelete(TRUE);
                     ContiniaUserSetup.DELETE(TRUE);
                   END;
                 UNTIL Company.NEXT = 0;
               END;
             END;

    OnRename=VAR
               ContiniaUserMgt@6085573 : Codeunit 6085781;
             BEGIN
               ContiniaUserMgt.RenameContiniaUser(xRec."User ID","User ID");
             END;

    CaptionML=[DEU=Continia-Benutzer;
               ENU=Continia User;
               NLD=Continia-gebruiker];
    LookupPageID=Page6086052;
    DrillDownPageID=Page6086052;
  }
  FIELDS
  {
    { 1   ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   OnValidate=VAR
                                                                ContiniaUserMgt@1160040000 : Codeunit 6085781;
                                                                UserSetup@6085573 : Record 91;
                                                              BEGIN
                                                                TESTFIELD("User ID");

                                                                "NAV Login Type" := ContiniaUserMgt.GetNAVUserLoginType("User ID");
                                                                IF "NAV Login Type" = "NAV Login Type"::"No NAV Login" THEN
                                                                  IF NOT CONFIRM(Text000,TRUE,"User ID") THEN
                                                                    ERROR('');

                                                                "User ID" := ContiniaUserMgt.GetNAVUserID("User ID");

                                                                IF UserSetup.GET("User ID") THEN
                                                                  "E-Mail" := UserSetup."E-Mail";

                                                                Name := GetName;
                                                              END;

                                                   OnLookup=VAR
                                                              LoginMgt@1160040000 : Codeunit 418;
                                                              ContiniaUserMgt@1160040001 : Codeunit 6085781;
                                                            BEGIN
                                                              LoginMgt.LookupUserID("User ID");
                                                              IF xRec."User ID" <> "User ID" THEN
                                                                VALIDATE("User ID");
                                                            END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[DEU=Benutzer-ID;
                                                              ENU=User ID;
                                                              NLD=Gebruikers-id;
                                                              NOR=Bruker-ID;
                                                              SVE=AnvÑndar-ID];
                                                   NotBlank=Yes }
    { 3   ;   ;Name                ;Text50        ;OnValidate=VAR
                                                                UserSetup@1160040001 : Record 91;
                                                                SalespersonPurchaser@1160040000 : Record 13;
                                                              BEGIN
                                                                IF UserSetup.GET("User ID") THEN
                                                                  IF SalespersonPurchaser.GET(UserSetup."Salespers./Purch. Code") THEN BEGIN
                                                                    SalespersonPurchaser.Name := Name;
                                                                    SalespersonPurchaser.MODIFY;
                                                                  END;
                                                              END;

                                                   CaptionML=[DEU=Name;
                                                              ENU=Name;
                                                              NLD=Naam;
                                                              NOR=Navn;
                                                              SVE=Namn] }
    { 4   ;   ;E-Mail              ;Text250       ;OnValidate=VAR
                                                                Company@1160040000 : Record 2000000006;
                                                                ContiniaUser@1160040004 : Record 6086001;
                                                                ContiniaUserSetup@1160040001 : Record 6086002;
                                                                UserSetup@1160040002 : Record 91;
                                                                SmtpMailMgt@1160040003 : Codeunit 6085598;
                                                              BEGIN
                                                                IF "E-Mail" <> '' THEN
                                                                  IF NOT SmtpMailMgt.IsValidEmail("E-Mail") THEN
                                                                    ERROR(Text005,"E-Mail");

                                                                IF "E-Mail" <> '' THEN BEGIN
                                                                  ContiniaUser.SETFILTER("User ID",'<>%1',"User ID");
                                                                  ContiniaUser.SETRANGE("E-Mail","E-Mail");
                                                                  IF ContiniaUser.FINDFIRST THEN
                                                                    ERROR(Text006,FIELDCAPTION("E-Mail"),"E-Mail",ContiniaUser."User ID");
                                                                END;

                                                                "E-Mail" := LOWERCASE("E-Mail");

                                                                IF Company.FINDSET THEN
                                                                  REPEAT
                                                                    ContiniaUserSetup.CHANGECOMPANY(Company.Name);
                                                                    IF ContiniaUserSetup.GET("User ID") THEN BEGIN
                                                                      ValidateApproverClient(ContiniaUserSetup);

                                                                      UserSetup.CHANGECOMPANY(Company.Name);
                                                                      IF UserSetup.GET("User ID") THEN BEGIN
                                                                        UserSetup."E-Mail" := "E-Mail";
                                                                        UserSetup.MODIFY;
                                                                      END;

                                                                      IF ContiniaUserSetup."Approval Client" = ContiniaUserSetup."Approval Client"::"Continia Web Portal" THEN
                                                                        IF "E-Mail" <> xRec."E-Mail" THEN
                                                                          "Send Welcome E-mail" := TRUE;

                                                                      IF ContiniaUserSetup."Expense Management User" THEN
                                                                        IF "E-Mail" <> xRec."E-Mail" THEN
                                                                          "Send Welcome E-mail (EM)" := TRUE;
                                                                    END;
                                                                  UNTIL Company.NEXT = 0;
                                                              END;

                                                   CaptionML=[DEU=E-Mail;
                                                              ENU=E-Mail;
                                                              NLD=E-mail;
                                                              NOR=E-Mail;
                                                              SVE=E-post] }
    { 6   ;   ;NAV Login Type      ;Option        ;CaptionML=[DEU=NAV-Anmeldungsart;
                                                              ENU=NAV Login Type;
                                                              NLD=NAV Login Type];
                                                   OptionCaptionML=[DEU=Keine NAV-Anmeldung,Windows,Datenbank;
                                                                    ENU=No NAV Login,Windows,Database;
                                                                    NLD=Geen NAV Login,Windows,Database];
                                                   OptionString=No NAV Login,Windows,Database;
                                                   Editable=No }
    { 9   ;   ;Creation Date       ;Date          ;CaptionML=[DEU=Errichtungsdatum;
                                                              ENU=Creation Date;
                                                              NLD=Aanmaakdatum;
                                                              NOR=Opprettet den;
                                                              SVE=UpplÑggningsdatum] }
    { 12  ;   ;Send Welcome E-mail ;Boolean       ;CaptionML=[DEU=BegrÅ·ungs-E-Mail senden;
                                                              ENU=Send Welcome E-mail;
                                                              NLD=Verstuur welkomst E-mails] }
    { 13  ;   ;Send Welcome E-mail (EM);Boolean   ;CaptionML=[DEU=BegrÅ·ungs-E-Mail (EM) senden;
                                                              ENU=Send Welcome E-mail (EM);
                                                              NLD=Verstuur welkomst-email (EM)] }
    { 31  ;   ;Activation Link     ;Text250       ;CaptionML=[DEU=Aktivierungslink;
                                                              ENU=Activation Link;
                                                              NLD=Activatielink] }
    { 32  ;   ;Activation Link (EM);Text250       ;CaptionML=[DEU=Aktvierungslink (EM);
                                                              ENU=Activation Link (EM);
                                                              NLD=Activatielink (EM)] }
  }
  KEYS
  {
    {    ;User ID                                 ;Clustered=Yes }
    {    ;Send Welcome E-mail                      }
    {    ;Send Welcome E-mail (EM)                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@116004000 : TextConst 'DEU=%1 ist nicht als NAV-Benutzer vorhanden. Mîchten Sie den Benutzer dennoch hinzufÅgen?;ENU=%1 does not exist as a NAV-user. Do you want to add the user anyway?;NLD=%1 bestaat niet als NAV-gebruiker. Wilt u deze gebruiker toch toevoegen?';
      Text001@161024012 : TextConst 'DEU=%1 Listencode;ENU=%1 List Code;NLD=%1 lijstcode';
      Text002@161024013 : TextConst 'DEU=Aktualisieren von %1.;ENU=Updating %1.;NLD=%1 updaten.';
      ContiniaUserSetupDeleteSuspend@1160040002 : Boolean;
      UpdateSendWelcomeEmail@1160040003 : Boolean;
      Text003@1160040004 : TextConst 'DEU=%1 hat Benutzereinrichtingen in einem oder mehreren Unternehmen.\\Wenn Sie den Benutzer lîschen, kann dieser den Unternehmen nicht mehr nÑhern.\\Mîchten Sie mit dem Lîschen von %1 fortfahren?;ENU=%1 has a user setup in one or more companies.\\If you delete the user it will no longer be able to access these companies.\\Do you want to continue deleting %1?;NLD=%1 heeft gebruikersinstellingen in ÇÇn of meer bedrijven.\\Als u de gebruiker verwijdert kan deze de bedrijven niet meer benaderen.\\Wilt u doorgaan met het verwijderen van %1?';
      Text005@1160040001 : TextConst 'DEU=Dieser Benutzer wurde in %1 in Unternehmen %2 eingerichtet und kann daher nicht gelîscht werden.;ENU=''%1'' is not a valid e-mail address.;NLD=%1'' is geen geldig e-mailadres.';
      Text006@1160040005 : TextConst 'DEU=%1 "%2" wurde bereits dem Benutzer %3 zugewiesen.\\Bitte, spezifizieren Sie eine eindeuitge E-Mail-Adresse.;ENU=%1 ''%2'' is already assigned to user %3.\\Please specify an unique e-mail address.;NLD=%1 "%2" is reeds toegewezen aan gebruiker %3.\\Specificeer a. u. b. een uniek e-mailadres.';

    PROCEDURE GetWebDimCaption@161024012(WebDimNo@161024012 : Integer) : Text[50];
    VAR
      DCSetup@161024013 : Record 6085573;
      Dimension@161024015 : Record 348;
      DimCode@161024014 : Code[20];
    BEGIN
      IF NOT DCSetup.GET THEN
        EXIT;

      CASE WebDimNo OF
        1:
          DimCode := DCSetup."Web: Dimension 1 Code (Lines)";
        2:
          DimCode := DCSetup."Web: Dimension 2 Code (Lines)";
        3:
          DimCode := DCSetup."Web: Dimension 3 Code (Lines)";
        4:
          DimCode := DCSetup."Web: Dimension 4 Code (Lines)";
        5:
          DimCode := DCSetup."Web: Dimension 5 Code (Lines)";
        6:
          DimCode := DCSetup."Web: Dimension 6 Code (Lines)";
        7:
          DimCode := DCSetup."Web: Dimension 7 Code (Lines)";
        8:
          DimCode := DCSetup."Web: Dimension 8 Code (Lines)";
      END;

      IF Dimension.GET(DimCode) THEN
        EXIT(STRSUBSTNO(Text001,Dimension.Name));
    END;

    LOCAL PROCEDURE GetName@1160040018() : Text[50];
    VAR
      UserSetup@1160040002 : Record 91;
      SalespersonPurchaser@1160040003 : Record 13;
      DCAppvlMgt@1160040000 : Codeunit 6085722;
      ContiniaUserMgt@6085573 : Codeunit 6085781;
      Name2@6085574 : Text[50];
    BEGIN
      IF ContiniaUserMgt.GetName("User ID",Name2) THEN
        EXIT(Name2);

      IF UserSetup.GET("User ID") THEN
        IF UserSetup."Salespers./Purch. Code" <> '' THEN
          IF SalespersonPurchaser.GET(UserSetup."Salespers./Purch. Code") THEN
            IF SalespersonPurchaser.Name <> '' THEN
              EXIT(SalespersonPurchaser.Name);

      IF SalespersonPurchaser.GET(COPYSTR(DCAppvlMgt.RemoveDomainName("User ID"),1,MAXSTRLEN(SalespersonPurchaser.Code))) THEN
        IF SalespersonPurchaser.Name <> '' THEN
          EXIT(SalespersonPurchaser.Name);

      EXIT("User ID");
    END;

    PROCEDURE SuspendContiniaUserSetupDelete@1160040010(Suspend@1160040000 : Boolean);
    BEGIN
      ContiniaUserSetupDeleteSuspend := Suspend;
    END;

    PROCEDURE ResendWelcomeEmail@1160040013();
    VAR
      ExportContiniaUsers@1160040001 : Codeunit 6085619;
      CEM@1160040000 : Codeunit 6085782;
    BEGIN
      IF IsWebApprovalUser(TRUE) THEN
        "Send Welcome E-mail" := TRUE;
      IF IsEMUser(TRUE) THEN
        "Send Welcome E-mail (EM)" := TRUE;
      MODIFY(TRUE);

      ExportContiniaUsers.SendWelcomeEmail;

      CEM.SendWelcomeEmail;
    END;

    PROCEDURE IsWebApprovalUser@1160040000(CheckThisCompany@1160040000 : Boolean) : Boolean;
    VAR
      Company@1160040001 : Record 2000000006;
      ContiniaUserSetup@1160040002 : Record 6086002;
    BEGIN
      IF Company.FINDSET THEN
        REPEAT
          IF CheckThisCompany OR (COMPANYNAME <> Company.Name) THEN BEGIN
            ContiniaUserSetup.CHANGECOMPANY(Company.Name);
            ContiniaUserSetup.SETRANGE("Continia User ID","User ID");
            ContiniaUserSetup.SETRANGE("Approval Client",ContiniaUserSetup."Approval Client"::"Continia Web Portal");

            IF NOT ContiniaUserSetup.ISEMPTY THEN
              EXIT(TRUE);
          END;
        UNTIL Company.NEXT = 0;
    END;

    PROCEDURE IsEMUser@1160040001(CheckThisCompany@1160040000 : Boolean) : Boolean;
    VAR
      Company@1160040001 : Record 2000000006;
      ContiniaUserSetup@1160040002 : Record 6086002;
    BEGIN
      IF Company.FINDSET THEN
        REPEAT
          IF CheckThisCompany OR (COMPANYNAME <> Company.Name) THEN BEGIN
            ContiniaUserSetup.CHANGECOMPANY(Company.Name);
            ContiniaUserSetup.SETRANGE("Continia User ID","User ID");
            ContiniaUserSetup.SETRANGE("Expense Management User",TRUE);
            IF NOT ContiniaUserSetup.ISEMPTY THEN
              EXIT(TRUE);
          END;
        UNTIL Company.NEXT = 0;
    END;

    PROCEDURE InsertContiniaUserSetupInComp@1160040002(CompName@1160040001 : Text[250]);
    VAR
      ContiniaUserSetup@1160040002 : Record 6086002;
    BEGIN
      IF CompName <> COMPANYNAME THEN BEGIN
        IF ContiniaUserSetup.GET("User ID") THEN
          ContiniaUserSetup.CopyUserSetup(CompName,"User ID",FALSE)
        ELSE BEGIN
          ContiniaUserSetup.CHANGECOMPANY(CompName);
          ContiniaUserSetup.SetCurrCompanyName(CompName);
          ContiniaUserSetup.INIT;
          ContiniaUserSetup."Continia User ID" := "User ID";
          ContiniaUserSetup.INSERT(TRUE);
        END;
      END ELSE BEGIN
        ContiniaUserSetup.INIT;
        ContiniaUserSetup."Continia User ID" := "User ID";
        ContiniaUserSetup.INSERT(TRUE);
      END;
    END;

    PROCEDURE DeleteContiniaUserSetupInComp@1160040003(CompName@1160040001 : Text[250]);
    VAR
      ContiniaUserSetup@1160040002 : Record 6086002;
    BEGIN
      ContiniaUserSetup.CHANGECOMPANY(CompName);
      ContiniaUserSetup.GET("User ID");
      ContiniaUserSetup.SetCurrCompanyName(CompName);
      ContiniaUserSetup.SetHideUserDeleteConfirmation(TRUE);
      ContiniaUserSetup.SuspendContiniaUserDelete(TRUE);
      ContiniaUserSetup.DELETE(TRUE);
    END;

    PROCEDURE GetCompaniesWithContUserSetup@1160040005(VAR TempCompany@1160040000 : TEMPORARY Record 2000000006);
    VAR
      Company2@1160040002 : Record 2000000006;
      ContiniaUserSetup@1160040001 : Record 6086002;
    BEGIN
      Company2.FINDSET;
      REPEAT
        ContiniaUserSetup.CHANGECOMPANY(Company2.Name);
        IF ContiniaUserSetup.GET("User ID") THEN BEGIN
          TempCompany := Company2;
          TempCompany.INSERT;
        END;
      UNTIL Company2.NEXT = 0;
    END;

    PROCEDURE GetNoOfContiniaUserSetups@1160040020() NoOfContiniaUserSetups : Integer;
    VAR
      TempCompany@1160040002 : TEMPORARY Record 2000000006;
    BEGIN
      GetCompaniesWithContUserSetup(TempCompany);
      EXIT(TempCompany.COUNT);
    END;

    PROCEDURE ShowSetupByCompany@1160040004();
    VAR
      SetupByCompany@1160040000 : Page 6086061;
    BEGIN
      SetupByCompany.SetUSerID("User ID");
      SetupByCompany.RUN;
    END;

    PROCEDURE ValidateApproverClient@6085576(ContiniaUserSetup@6085573 : Record 6086002);
    BEGIN
      IF (ContiniaUserSetup."Approval Client" <> ContiniaUserSetup."Approval Client"::" ") AND ("E-Mail" = '') THEN
        TESTFIELD("E-Mail");
    END;

    BEGIN
    END.
  }
}

