OBJECT Page 11127017 Service Loc. Struct. Ln FSA WS
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=;
    SourceTable=Table11071720;
    PageType=List;
    SourceTableTemporary=Yes;
  }
  CONTROLS
  {
    { 1100526000;0;Container;
                ContainerType=ContentArea }

    { 1100526007;1;Field  ;
                Name=LocationNo;
                SourceExpr=ServiceLocationNo;
                OnValidate=BEGIN
                             FillSourceTable;
                           END;
                            }

    { 1100526001;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1100526008;2;Field  ;
                Name=CompanyName;
                SourceExpr=COMPANYNAME }

    { 1100526003;2;Field  ;
                Name=ServiceLocationNo;
                SourceExpr="Service Location No." }

    { 1100526005;2;Field  ;
                Name=ParentNo;
                SourceExpr="Element Level 1" }

    { 1100526004;2;Field  ;
                Name=No;
                SourceExpr="No." }

    { 1100526002;2;Field  ;
                Name=ObjectNo;
                SourceExpr="Service Object No." }

    { 1100526006;2;Field  ;
                Name=Description;
                SourceExpr=Description }

  }
  CODE
  {
    VAR
      ServiceLocationNo@1100526000 : Code[20];

    LOCAL PROCEDURE FillSourceTable@1100526000();
    VAR
      ServiceLocationStructure@1100526000 : Record 11071719;
      ServiceLocationStructureLine@1100526001 : Record 11071720;
    BEGIN
      Rec.RESET;
      Rec.DELETEALL;

      ServiceLocationStructure.SETRANGE("Service Location No.", ServiceLocationNo);
      ServiceLocationStructure.SETRANGE("Available for Field Service", TRUE);
      IF NOT ServiceLocationStructure.FINDFIRST THEN
        EXIT;

      ServiceLocationStructureLine.SETRANGE("Service Location No.", ServiceLocationStructure."Service Location No.");
      ServiceLocationStructureLine.SETRANGE("Structure No.", ServiceLocationStructure."No.");
      IF ServiceLocationStructureLine.FINDSET THEN
        REPEAT
          Rec.INIT;
          Rec."Service Location No." := ServiceLocationStructureLine."Service Location No.";
          Rec."Structure No." := ServiceLocationStructureLine."Structure No.";
          Rec."No." := ServiceLocationStructureLine."No.";
          Rec."Element Level 1" := GetParentNo(ServiceLocationStructureLine);
          Rec."Service Object No." := ServiceLocationStructureLine."Service Object No.";
          IF ServiceLocationStructureLine."Service Object No." <> '' THEN BEGIN
            ServiceLocationStructureLine.CALCFIELDS("Object Description");
            Rec.Description := ServiceLocationStructureLine."Object Description";
          END ELSE
            Rec.Description := ServiceLocationStructureLine.Description;
          IF Rec.INSERT THEN;
        UNTIL ServiceLocationStructureLine.NEXT = 0;
    END;

    LOCAL PROCEDURE GetParentNo@1100526003(ServiceLocationStructureLine@1100526000 : Record 11071720) : Code[20];
    VAR
      ServiceLocationStructureLine2@1100526001 : Record 11071720;
    BEGIN
      IF ServiceLocationStructureLine.Level = 0 THEN
        EXIT;

      IF ServiceLocationStructureLine."Service Object No." <> '' THEN BEGIN
        IF ServiceLocationStructureLine.Level = 1 THEN
          EXIT(ServiceLocationStructureLine."Element Level 1");
        IF ServiceLocationStructureLine.Level = 2 THEN
          EXIT(ServiceLocationStructureLine."Element Level 2");
        IF ServiceLocationStructureLine.Level = 3 THEN
          EXIT(ServiceLocationStructureLine."No.");
      END ELSE BEGIN
        IF ServiceLocationStructureLine.Level = 1 THEN BEGIN
          ServiceLocationStructureLine2.SETRANGE("Service Location No.", ServiceLocationStructureLine."Service Location No.");
          ServiceLocationStructureLine2.SETRANGE("Structure No.", ServiceLocationStructureLine."Structure No.");
          ServiceLocationStructureLine2.SETFILTER("Service Object No.", '%1', '');
          ServiceLocationStructureLine2.SETRANGE(Level, ServiceLocationStructureLine.Level - 1);
          IF ServiceLocationStructureLine2.FINDFIRST THEN
              EXIT(ServiceLocationStructureLine2."No.");
        END;
        IF ServiceLocationStructureLine.Level = 2 THEN
          EXIT(ServiceLocationStructureLine."Element Level 1");
        IF ServiceLocationStructureLine.Level = 3 THEN
          EXIT(ServiceLocationStructureLine."Element Level 2");
      END;
    END;

    BEGIN
    END.
  }
}

