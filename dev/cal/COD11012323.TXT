OBJECT Codeunit 11012323 Exp. Plant Req. (Plant Mgt.)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=VAR
            PlantRequest@1100525001 : Record 11020520;
            PlantRequests@1100525000 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlElement";
            XMLDOMManagement4PS@1100525002 : Codeunit 11020220;
          BEGIN
            IF GUIALLOWED THEN
              Window.OPEN(Text000);

            UtilityFunctions.CreateOutFolder;

            DOMDocument := DOMDocument.XmlDocument;

            AppendElement(DOMDocument, 'plantrequests', '', PlantRequests);
            PlantRequests.SetAttribute('version', '1.0');
            PlantRequests.SetAttribute('exportdate', UtilityFunctions.FormatDateTime(CURRENTDATETIME));

            PlantRequest.SETFILTER("Created on", '%1..|%2', CREATEDATETIME(CALCDATE('<-3M>', WORKDATE), 000000T), 0DT);

            IF PlantRequest.FINDSET THEN
              REPEAT
                IF GUIALLOWED THEN
                  Window.UPDATE;
                AppendPlantRequest(PlantRequests, PlantRequest);
              UNTIL PlantRequest.NEXT = 0;

            IF GUIALLOWED THEN
              XMLDOMManagement4PS.SaveXMLToClientFile(DOMDocument, FileName)
            ELSE
              DOMDocument.Save(FileName);

            IF GUIALLOWED THEN
              Window.CLOSE;
          END;

  }
  CODE
  {
    VAR
      UtilityFunctions@1100525003 : Codeunit 11012318;
      DOMDocument@1100525002 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      Window@1100525000 : Dialog;
      Text000@1100525001 : TextConst 'DEU=Werkzeuganfragen exportieren...;ENU=Exporting Plant Requests...;NLD=Materieelaanvragen exporteren...;SVE=Exporterar Maskinsbeg„randen ...';

    PROCEDURE AppendElement@1100525006(ParentNode@1100525000 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";Name@1100525001 : Text[80];Value@1100525002 : Text[1024];VAR NewElement@1100525003 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlElement");
    VAR
      TextNode@1100525004 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlText";
    BEGIN
      NewElement := DOMDocument.CreateElement(Name);

      IF Value <> '' THEN BEGIN
        TextNode := DOMDocument.CreateTextNode(Value);
        NewElement.AppendChild(TextNode);
      END;

      ParentNode.AppendChild(NewElement);
    END;

    PROCEDURE AppendPlantRequest@1100525003(ParentNode@1100525000 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";PlantRequest@1100525001 : Record 11020520);
    VAR
      PlantCommentLine@1100525004 : Record 11072666;
      PlantRequestLine@1100525006 : Record 11020521;
      PlantRequest2@1100525002 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlElement";
      Dummy@1100525003 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlElement";
      CommentLine2@1100525008 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlElement";
    BEGIN
      AppendElement(ParentNode, 'plantrequest', '', PlantRequest2);
      AppendElement(PlantRequest2, 'no', PlantRequest."No.", Dummy);
      AppendElement(PlantRequest2, 'type', UtilityFunctions.FormatOption(PlantRequest.Type, 'Arrival,Removal,Other Transfers'), Dummy);
      AppendElement(PlantRequest2, 'transferdate', UtilityFunctions.FormatDate(PlantRequest."Transfer Date"), Dummy);
      AppendElement(PlantRequest2, 'postingdate', UtilityFunctions.FormatDateTime(PlantRequest."Posted on"), Dummy);
      AppendElement(PlantRequest2, 'fromlocation', PlantRequest."From Location", Dummy);
      AppendElement(PlantRequest2, 'tolocation', PlantRequest."To Location", Dummy);
      AppendElement(PlantRequest2, 'status',
        UtilityFunctions.FormatOption(PlantRequest.Status, 'Open,,,,Requested,,,,Approved,,,Disapproved,Cancelled'), Dummy);
      AppendElement(PlantRequest2, 'createdby', PlantRequest."Created by", Dummy);
      AppendElement(PlantRequest2, 'shippingcharges', UtilityFunctions.FormatOption(PlantRequest."Transport Cost for (Advice)",
        ' ,Internal,Project,Location,From Location,To Location'), Dummy);
      AppendElement(PlantRequest2, 'comment', '', Dummy);

      //DP00183.sc
      PlantCommentLine.SETRANGE("Table No.", DATABASE::"Plant Request");  //C005461.c
      PlantCommentLine.SETRANGE("No.", PlantRequest."No.");               //C005461.c
      IF PlantCommentLine.FINDSET THEN
        REPEAT
          AppendElement(Dummy, 'commentline', PlantCommentLine.Comment, CommentLine2);
          CommentLine2.SetAttribute('date', UtilityFunctions.FormatDate(PlantCommentLine.Date));
          CommentLine2.SetAttribute('createdby', PlantCommentLine."Created by");
        UNTIL PlantCommentLine.NEXT = 0;
      //DP00183.ec

      AppendElement(PlantRequest2, 'internetorder', UtilityFunctions.FormatBoolean(PlantRequest."Internet Order"), Dummy);
      AppendElement(PlantRequest2, 'login', PlantRequest."CP Login ID", Dummy);
      AppendElement(PlantRequest2, 'websitecode', PlantRequest."Web Site Code", Dummy);
      AppendElement(PlantRequest2, 'yourreference', PlantRequest."Your Reference", Dummy);
      AppendElement(PlantRequest2, 'plantrequestlines', '', Dummy);

      PlantRequestLine.SETRANGE("Plant Request No.", PlantRequest."No.");

      IF PlantRequestLine.FINDSET THEN
        REPEAT
          AppendPlantRequestLine(Dummy, PlantRequestLine);
        UNTIL PlantRequestLine.NEXT = 0 ;
    END;

    PROCEDURE AppendPlantRequestLine@1100525012(ParentNode@1100525001 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";PlantRequestLine@1100525000 : Record 11020521);
    VAR
      PlantRequestLine2@1100525002 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlElement";
      Dummy@1100525003 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlElement";
    BEGIN
      AppendElement(ParentNode, 'plantrequestline', '', PlantRequestLine2);
      AppendElement(PlantRequestLine2, 'type', UtilityFunctions.FormatOption(PlantRequestLine.Type, 'Plant,Item,Text'), Dummy);
      AppendElement(PlantRequestLine2, 'description', PlantRequestLine.Description, Dummy);
      AppendElement(PlantRequestLine2, 'comment', PlantRequestLine."Comment CP Order", Dummy);
      AppendElement(PlantRequestLine2, 'planttype', PlantRequestLine."Plant Type", Dummy);
      AppendElement(PlantRequestLine2, 'expectedreturn', UtilityFunctions.FormatDate(PlantRequestLine."Expected return on"), Dummy);
      AppendElement(PlantRequestLine2, 'quantity', UtilityFunctions.FormatDecimal(PlantRequestLine.Quantity), Dummy);
    END;

    PROCEDURE FileName@1100525000() : Text[260];
    VAR
      PlantMgtFileExchangeSetup@1100525000 : Record 11125655;
    BEGIN
      PlantMgtFileExchangeSetup.GET;
      PlantMgtFileExchangeSetup.TESTFIELD("Export Path");

      EXIT(
        STRSUBSTNO('%1\plantrequests%2.xml', DELCHR(PlantMgtFileExchangeSetup."Export Path", '>', '\'),
        UtilityFunctions.FileNameDateTime));
    END;

    BEGIN
    END.
  }
}

