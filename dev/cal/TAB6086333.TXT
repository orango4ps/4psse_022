OBJECT Table 6086333 Exp. Posting Desc. Field
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.3.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Field Type Code,Field Description;
    CaptionML=[DEU=Buchungsbeschr.-Feld;
               ENU=Posting Desc. Field;
               NLD=Boeking omschrijvingveld];
  }
  FIELDS
  {
    { 1   ;   ;Parameter No.       ;Integer       ;CaptionML=[DEU=Parameternr.;
                                                              ENU=Parameter No.;
                                                              NLD=Parameternr.] }
    { 10  ;   ;Field Type Code     ;Code20        ;TableRelation="Field Type";
                                                   CaptionML=[DEU=Feldartcode;
                                                              ENU=Field Type Code;
                                                              NLD=Veldtypecode] }
    { 11  ;   ;Field Description   ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Field Type".Description WHERE (Code=FIELD(Field Type Code)));
                                                   CaptionML=[DEU=Feldbeschreibung;
                                                              ENU=Field Description;
                                                              NLD=Veldomschrijving;
                                                              NOR=Feltbeskrivelse;
                                                              SVE=F„ltbeskrivning];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Parameter No.                           ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE GetExpPostingDesc@1160040002(Expense@1160040000 : Record 6086320) : Text[1024];
    VAR
      EMSetup@1160040001 : Record 6086300;
    BEGIN
      EMSetup.GET;
      EXIT(ReplacePlaceholdersWithValues(DATABASE::Expense,0,'',Expense."Entry No.",EMSetup."Posting Description"));
    END;

    PROCEDURE GetExpInvPostingDesc@1160040023(Expense@1160040000 : Record 6086320) : Text[1024];
    VAR
      EMSetup@1160040001 : Record 6086300;
    BEGIN
      EMSetup.GET;
      EXIT(ReplacePlaceholdersWithValues(DATABASE::Expense,0,'',Expense."Entry No.",EMSetup."Invoice Posting Description"));
    END;

    LOCAL PROCEDURE ReplacePlaceholdersWithValues@1160040024(TableID@1160040006 : Integer;DocumentType@1160040005 : Integer;DocumentNo@1160040004 : Code[20];DocRefNo@1160040003 : Integer;InputDescription@1160040002 : Text[1024]) PostingDesc : Text[1024];
    VAR
      ExpPostingDescFields@1160040000 : Record 6086333;
    BEGIN
      PostingDesc := InputDescription;

      ExpPostingDescFields.ASCENDING(FALSE);
      IF ExpPostingDescFields.FINDFIRST THEN
        REPEAT
          PostingDesc :=
            ReplaceText(PostingDesc,'%' + FORMAT(ExpPostingDescFields."Parameter No."),
              GetFieldValue(TableID,DocumentType,DocumentNo,DocRefNo,ExpPostingDescFields."Field Type Code"),FALSE);
        UNTIL ExpPostingDescFields.NEXT = 0;
    END;

    PROCEDURE GetMilPostingDesc@1160040016(Mileage@1160040000 : Record 6086338) PostingDesc : Text[1024];
    VAR
      EMSetup@1160040001 : Record 6086300;
    BEGIN
      EMSetup.GET;
      EXIT(ReplacePlaceholdersWithValues(DATABASE::Mileage,0,'',Mileage."Entry No.",EMSetup."Mileage Posting Description"));
    END;

    PROCEDURE GetMilInvPostingDesc@1160040015(MIleage@1160040000 : Record 6086338) : Text[1024];
    VAR
      EMSetup@1160040001 : Record 6086300;
    BEGIN
      EMSetup.GET;
      EXIT(ReplacePlaceholdersWithValues(DATABASE::Mileage,0,'',MIleage."Entry No.",EMSetup."Invoice Posting Description"));
    END;

    PROCEDURE GetSettlementPostingDesc@1160040018(ExpHeader@1160040000 : Record 6086339) : Text[1024];
    VAR
      EMSetup@1160040001 : Record 6086300;
    BEGIN
      EMSetup.GET;
      EXIT(ReplacePlaceholdersWithValues(
        DATABASE::"Expense Header",ExpHeader."Document Type",ExpHeader."No.",0,EMSetup."Settlement Posting Description"));
    END;

    LOCAL PROCEDURE ReplaceText@1000000003(Text@1000000000 : Text[1024];ReplaceTxt@1000000001 : Text[1024];ReplaceWithTxt@1000000002 : Text[1024];CaseSensitive@1160040000 : Boolean) : Text[1024];
    VAR
      i@1000000005 : Integer;
      ReplaceTxtLen@1000000003 : Integer;
      ReplaceWithTxtLen@1000000004 : Integer;
    BEGIN
      ReplaceTxtLen := STRLEN(ReplaceTxt);
      ReplaceWithTxtLen := STRLEN(ReplaceWithTxt);

      IF CaseSensitive THEN
        i := STRPOS(Text,ReplaceTxt)
      ELSE
        i := STRPOS(UPPERCASE(Text),UPPERCASE(ReplaceTxt));

      IF i <> 0 THEN BEGIN
        Text := COPYSTR(Text,1,i-1) + ReplaceWithTxt + COPYSTR(Text,i + ReplaceTxtLen);
        Text :=
          COPYSTR(Text,1,i-1 + ReplaceWithTxtLen) +
            ReplaceText(COPYSTR(Text,i + ReplaceWithTxtLen),ReplaceTxt,ReplaceWithTxt,CaseSensitive);
      END;

      EXIT(Text);
    END;

    LOCAL PROCEDURE GetFieldValue@1160040008(TableID@1160040011 : Integer;DocumentType@1160040010 : Integer;DocumentNo@1160040009 : Code[20];DocRefNo@1160040006 : Integer;FieldTypeCode@1160040001 : Code[20]) : Text[1024];
    VAR
      Expense@1160040005 : Record 6086320;
      Mileage@1160040012 : Record 6086338;
      ExpHeader@1160040013 : Record 6086339;
      FieldType@1160040008 : Record 6086345;
      EMDim@1160040007 : Record 6086360;
      RecRef@1160040003 : RecordRef;
      FldRef@1160040002 : FieldRef;
      DimCode@1160040004 : Code[20];
      i@1160040000 : Integer;
    BEGIN
      IF FieldType.GET(FieldTypeCode) THEN
        IF FieldType.GetSystemFieldMilTableNo(FieldType.Code) > 0 THEN BEGIN
          CASE TableID OF
            DATABASE::Expense:
              BEGIN
                Expense.GET(DocRefNo);
                RecRef.GETTABLE(Expense);
              END;

            DATABASE::Mileage:
              BEGIN
                Mileage.GET(DocRefNo);
                RecRef.GETTABLE(Mileage);
              END;

            DATABASE::"Expense Header":
              BEGIN
                ExpHeader.GET(DocumentType,DocumentNo);
                RecRef.GETTABLE(ExpHeader);
              END;
          END;

          FOR i := 1 TO RecRef.FIELDCOUNT DO BEGIN
            FldRef := RecRef.FIELDINDEX(i);
            CASE TableID OF
              DATABASE::Mileage:
                IF FldRef.NUMBER = FieldType.GetSystemFieldMilTableNo(FieldType.Code) THEN
                  EXIT(FORMAT(FldRef.VALUE));
              DATABASE::Expense:
                IF FldRef.NUMBER = FieldType.GetSystemFieldExpTableNo(FieldType.Code) THEN
                  EXIT(FORMAT(FldRef.VALUE));
              DATABASE::"Expense Header":
                IF FldRef.NUMBER = FieldType.GetSystemFieldSettlemenTableNo(FieldType.Code) THEN
                  EXIT(FORMAT(FldRef.VALUE));
            END;

          END;
        END ELSE BEGIN
          DimCode := FieldType.GetDimCode;

          EMDim.SETCURRENTKEY("Table ID","Document Type","Document No.","Doc. Ref. No.");
          EMDim.SETRANGE("Table ID",TableID);
          EMDim.SETRANGE("Document Type",DocumentType);
          EMDim.SETRANGE("Document No.",DocumentNo);
          EMDim.SETRANGE("Doc. Ref. No.",DocRefNo);

          IF DimCode <> '' THEN BEGIN
            EMDim.SETRANGE("Dimension Code",DimCode);
            IF EMDim.FINDFIRST THEN
              EXIT(EMDim."Dimension Value Code");
          END ELSE BEGIN
            EMDim.SETRANGE("Field Code",FieldTypeCode);
            IF EMDim.FINDFIRST THEN
              EXIT(EMDim."Field Value");
          END;
        END;
    END;

    BEGIN
    END.
  }
}

