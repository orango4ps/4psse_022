OBJECT Report 11012740 Close SC Control Period
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Servicevertrag-šberwachungsperiode schlieáen;
               ENU=Close Service Contract Control Period;
               NLD=Servicecontract bewakingsperiode afsluiten;
               NOR=Lukk kontrollperiode for servicekontrakt;
               SVE=St„ng kntr.per. servicekntr.];
    ProcessingOnly=Yes;
    OnPostReport=BEGIN
                   DisplayError;
                 END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 4178;    ;DataItem;                    ;
               DataItemTable=Table11012812;
               DataItemTableView=SORTING(No.);
               ReqFilterFields=No.,Contract Group,Contract Manager,Contract Form }

    { 9302;1   ;DataItem;                    ;
               DataItemTable=Table11071746;
               DataItemTableView=SORTING(Service Contract No.,Code)
                                 WHERE(Date Finished=CONST());
               OnPreDataItem=BEGIN
                               SETFILTER("Ending Date", '..%1', PeriodEndingDate);
                             END;

               OnAfterGetRecord=VAR
                                  ServiceOrder@1100528601 : Record 11012823;
                                  DeferredServiceOrder@1100527901 : Record 11012823;
                                  NextPeriodStart@1100527900 : Date;
                                BEGIN
                                  TmpServiceOrder.RESET;
                                  TmpServiceOrder.DELETEALL;

                                  // Service Orders (Calls and Direct)
                                  ServiceOrder.SETCURRENTKEY("Service Contract No.");
                                  ServiceOrder.SETRANGE("Service Contract No.", "Service Contract No.");
                                  ServiceOrder.SETRANGE("Service Control Period Date", "Starting Date", "Ending Date");
                                  ServiceOrder.SETFILTER("Source Type", '%1|%2', ServiceOrder."Source Type"::Call, ServiceOrder."Source Type"::Direct);
                                  ServiceOrder.SETFILTER(Status, '<%1', ServiceOrder.Status::Cancelled);
                                  IF ServiceOrder.FINDSET THEN
                                    REPEAT
                                      InsertSOtoTmp(ServiceOrder, '', "Service Contract No.", Code);
                                    UNTIL ServiceOrder.NEXT = 0;

                                  // Service Orders (Contract) without Contract Arrangement link (due to conversion)
                                  ServiceOrder.SETCURRENTKEY("Service Contract No.");
                                  ServiceOrder.SETRANGE("Service Contract No.", "Service Contract No.");
                                  ServiceOrder.SETRANGE("Service Control Period Date", "Starting Date", "Ending Date");
                                  ServiceOrder.SETRANGE("Source Type", ServiceOrder."Source Type"::Contract);
                                  ServiceOrder.SETFILTER(Status, '<%1', ServiceOrder.Status::Cancelled);
                                  IF ServiceOrder.FINDSET THEN
                                    REPEAT
                                      IF NOT ErrorFoundOnContractObjectMaintenance(ServiceOrder, Code) THEN
                                        InsertSOtoTmp(ServiceOrder, '', "Service Contract No.", Code);
                                    UNTIL ServiceOrder.NEXT = 0;

                                  CheckTmpServiceOrders("Service-Contract Ctrl. Period");
                                  CheckServiceContractPeriod("Service-Contract Ctrl. Period");

                                  IF Finalize THEN BEGIN
                                    IF Defer THEN
                                      IF TmpDeferedServiceOrder.FINDSET THEN
                                        REPEAT
                                          CLEAR(NextPeriodStart);
                                          DeferredServiceOrder.GET(TmpDeferedServiceOrder."No.");
                                          NextPeriodStart := CALCDATE("Service-Contract"."Control Period Interval",ServiceOrder."Service Control Period Date");
                                          IF NextPeriodStart <= "Service-Contract"."Ending Date" THEN BEGIN
                                            DeferredServiceOrder.VALIDATE("Service Control Period Date",NextPeriodStart);
                                            DeferredServiceOrder.MODIFY(TRUE);
                                            AddError(STRSUBSTNO(Text006,DeferredServiceOrder."No."),'',"Service Contract No.",Code);  // Not an errorr, just message in the same window; ServOrder."No."=''
                                          END ELSE
                                            AddError(STRSUBSTNO(Text007, DeferredServiceOrder."No."), DeferredServiceOrder."No.", "Service-Contract"."No.", Code);
                                        UNTIL TmpDeferedServiceOrder.NEXT = 0;

                                    IF NOT ErrorsExist("Service Contract No.", Code) THEN BEGIN
                                      FinalizeTmpServiceOrders("Service-Contract Ctrl. Period");
                                      FinalizeServiceContractPeriod("Service-Contract Ctrl. Period");
                                      COMMIT;
                                    END;
                                  END;
                                END;

               DataItemLink=Service Contract No.=FIELD(No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525001;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100525002;2;Field  ;
                  Name=Period Ending Date;
                  CaptionML=[DEU=Periodenenddatum;
                             ENU=Period Ending Date;
                             NLD=Einddatum periode;
                             NOR=Periodens sluttdato;
                             SVE=Periodens slutdatum];
                  SourceExpr=PeriodEndingDate }

      { 1100525003;2;Field  ;
                  Name=Posting Date;
                  CaptionML=[DEU=Buchungsdatum;
                             ENU=Posting Date;
                             NLD=Boekingsdatum;
                             NOR=Bokf›ringsdato;
                             SVE=BokfÂ”ringsdatum];
                  SourceExpr=PostingDate }

      { 1100525004;2;Field  ;
                  Name=Ingnore Sales Control;
                  CaptionML=[DEU=Ignorieren Verkaufskontrolle;
                             ENU=Ingnore Sales Control;
                             NLD=Negeer verkoopcontrole;
                             NOR=Ignorer salgskontroll;
                             SVE=Ignorera f”rs„ljningskontroll];
                  SourceExpr=IgnoreSales;
                  OnValidate=BEGIN
                               IF IgnoreSales THEN
                                 MESSAGE(Text000 + Text001);
                             END;
                              }

      { 1100525005;2;Field  ;
                  Name=Ignore Purchase Control;
                  CaptionML=[DEU=Ignorieren Einkaufskontrolle;
                             ENU=Ignore Purchase Control;
                             NLD=Negeer inkoopcontrole;
                             NOR=Ignorer innkj›pskontroll;
                             SVE=Ignorera ink”pskontroll];
                  SourceExpr=IgnorePurchase;
                  OnValidate=BEGIN
                               IF IgnorePurchase THEN
                                 MESSAGE(Text002 + Text003);
                             END;
                              }

      { 1100525006;2;Field  ;
                  Name=Ignore Future Posting Control;
                  CaptionML=[DEU=Ignorieren Buchungen nach Buch.-Datumberpr.;
                             ENU=Ignore Future Posting Control;
                             NLD=Negeer boekingen na boekingsdatum controle;
                             NOR=Ignorer kontroll av framtidig bokf›ring;
                             SVE=Ignorera kontroll av framtida bokf”ring];
                  SourceExpr=IgnoreFuturePostings }

      { 1100528600;2;Field  ;
                  CaptionML=[DEU=Arbeitsauftr„ge ignorieren;
                             ENU=Ignore Work Orders;
                             NLD=Negeer werkorders];
                  SourceExpr=IgnoreWorkOrders }

      { 1100528201;2;Field  ;
                  CaptionML=[DEU=Ignoriere Artikeljournalkontrolle;
                             ENU=Ignore Item Journal Check;
                             NLD=Negeer artikeljournaalcontrole];
                  SourceExpr=IgnoreItemJournal }

      { 1100528200;2;Field  ;
                  CaptionML=[DEU=Ignoriere andere Betriebe;
                             ENU=Ignore Other Companies;
                             NLD=Negeer andere bedrijven];
                  SourceExpr=IgnoreInterCompany }

      { 1100527900;2;Field  ;
                  CaptionML=[DEU=Offene Serviceauftr„ge in n„chster Periode;
                             ENU=Defer open service orders;
                             NLD=Open serviceorders naar volgende periode];
                  SourceExpr=Defer }

      { 1100525007;2;Field  ;
                  Name=Post;
                  CaptionML=[DEU=Buchen;
                             ENU=Post;
                             NLD=Boeken;
                             NOR=Bokf›r;
                             SVE=Bokf”r];
                  SourceExpr=Finalize }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TmpServiceOrder@1100528602 : TEMPORARY Record 11012823;
      TmpDeferedServiceOrder@1100527902 : TEMPORARY Record 11012823;
      PeriodEndingDate@1100528601 : Date;
      PostingDate@1100528604 : Date;
      IgnorePurchase@1100528605 : Boolean;
      IgnoreSales@1100528606 : Boolean;
      IgnoreFuturePostings@1100528607 : Boolean;
      IgnoreWorkOrders@1100528600 : Boolean;
      Finalize@1100528603 : Boolean;
      Text000@1100528609 : TextConst 'DEU=Das Ignorieren der Einkaufskontrolle kann zur Folge haben, dass Serviceavertr„ge zu frh fertiggemeldet werden.\;ENU=Ignoring the Sales Check could result in evaluating Service Contracts too early.\;NLD=Het negeren van de verkoopcontrole kan tot gevolg hebben dat servicecontracten te vroeg worden gereedgemeld.\;NOR=Ignorering av salgskontroll kan resultere i for tidlig evaluering av servicekontrakt.\;SVE=Om f”rs„ljningskontrollen ignoreras kan servicekontrakt avslutas f”r tidigt.\';
      Text001@1100528608 : TextConst 'DEU=Es k”nnen doch noch nicht fakturierte Regieeintrag-Posten oder Raten vorhanden sein.;ENU=There could be not Invoiced Cost Plus Entries or Installemtens present.;NLD=Er kunnen immers nog ongefactureerde regiemutaties of termijnen aanwezig zijn.;NOR=Det kan v‘re ikke-fakturerte selvkosttransaksjoner eller betalingsplaner tilstede.;SVE=Det kan finnas redovisade sj„lvkostnader eller avbetalningar.';
      Text002@1100528611 : TextConst 'DEU=Das Ignorieren der Einkaufskontrolle kann zur Folge haben, dass Serviceauftr„ge zu frh fertiggemeldet werden.\;ENU=Ignoring the Purchase Check could result in closing Service Orders too early.\;NLD=Het negeren van de inkoopcontrole kan tot gevolg hebben dat serviceorders te vroeg worden gereedgemeld.\;NOR=Ignorering av innkj›pskontroll kan resultere i for tidlig lukking av serviceordre.\;SVE=Om k”pkontrollen ignoreras kan serviceorder avslutas f”r tidigt.\';
      Text003@1100528610 : TextConst 'DEU=Es k”nnen doch noch Einkaufsbestellungen vorhanden sein.;ENU=There could still be Purchase Orders present.;NLD=Er kunnen immers nog inkooporders aanwezig zijn.;NOR=Det kan fortsatt finnes innkj›psordre.;SVE=Det kan fortfarande finnas ink”psorder.';
      Text004@1100528612 : TextConst 'DEU=Serviceauftrag nicht administrativ abgeschlossen;ENU=Service Order not administrative finished;NLD=Serviceorder heeft niet de status administratief gereed;NOR=Serviceordre er ikke administrativt ferdig;SVE=Serviceorder inte administrativt klar';
      Text005@1100528500 : TextConst 'DEU=Keine "%1" fr Objekt %2 angegeben, Wartungskonzept %3;ENU=No "%1" specified for object %2, maintenance scheme %3;NLD=Geen "%1" gespecificeerd voor object %2, onderhoudsschema %3';
      Defer@1100527900 : Boolean;
      Text006@1100527901 : TextConst 'DEU=Serviceauftrag %1 ist zur folgenden Periode verschoben worden;ENU=Service Order %1 is defered to the next period;NLD=Serviceorder %1 is verplaatst naar de volgende periode';
      Text007@1100529601 : TextConst 'DEU=Serviceauftrag %1 kann nicht zu einer Periode nach dem Enddatum des Vertrags verschoben werden.;ENU=Service Order %1 cannot be moved to the period after the end date of the contract.;NLD=Serviceorder %1 kan niet verschoven worden naar een periode na de contract einddatum.';
      IgnoreItemJournal@1100528200 : Boolean;
      IgnoreInterCompany@1100528201 : Boolean;

    PROCEDURE CheckTmpServiceOrders@1100528603(IServiceContractCtrlPeriod@1100525000 : Record 11071746);
    VAR
      CloseProjectServiceCU@1100528600 : Codeunit 11012046;
      Type@1100528602 : 'Project,Service Contract,Service Order';
    BEGIN
      IF NOT TmpServiceOrder.FINDSET THEN
        EXIT;
      REPEAT
        CLEAR(CloseProjectServiceCU);
        CloseProjectServiceCU.SetSelection(
          Type::"Service Order", '', TmpServiceOrder."Service Contract No.", TmpServiceOrder."No.",
          PostingDate, IgnorePurchase, IgnoreSales, IgnoreFuturePostings, FALSE, IgnoreWorkOrders, '', FALSE,
          IgnoreItemJournal, IgnoreInterCompany);
        CloseProjectServiceCU.SetSelectionCloseService(0, IServiceContractCtrlPeriod.Code);
        CloseProjectServiceCU.SetServOrderExtraTest(FALSE, TRUE);
        CloseProjectServiceCU.Check;
      UNTIL TmpServiceOrder.NEXT = 0;
    END;

    PROCEDURE FinalizeTmpServiceOrders@1100528604(IServiceContractCtrlPeriod@1100525000 : Record 11071746);
    VAR
      CloseProjectServiceCU@1100528601 : Codeunit 11012046;
      Type@1100528600 : 'Project,Service Contract,Service Order';
    BEGIN
      IF NOT TmpServiceOrder.FINDSET THEN
        EXIT;
      REPEAT
        CLEAR(CloseProjectServiceCU);
        CloseProjectServiceCU.SetSelection(
          Type::"Service Order", '', TmpServiceOrder."Service Contract No.", TmpServiceOrder."No.",
          PostingDate, IgnorePurchase, IgnoreSales, IgnoreFuturePostings, FALSE, IgnoreWorkOrders, '', FALSE,
          IgnoreItemJournal, IgnoreInterCompany);
        CloseProjectServiceCU.SetSelectionCloseService(0, IServiceContractCtrlPeriod.Code);
        CloseProjectServiceCU.SetServOrderExtraTest(FALSE, TRUE);
        CloseProjectServiceCU.Check;
        CloseProjectServiceCU.Post;
      UNTIL TmpServiceOrder.NEXT = 0;
    END;

    PROCEDURE CheckServiceContractPeriod@1100528600(IServiceContractCtrlPeriod@1100528601 : Record 11071746);
    VAR
      CloseProjectServiceCU@1100528602 : Codeunit 11012046;
      Type@1100528600 : 'Project,Service Contract,Service Order';
    BEGIN
      CloseProjectServiceCU.SetSelection(
        Type::"Service Contract", '', IServiceContractCtrlPeriod."Service Contract No.", '',
        PostingDate, IgnorePurchase, IgnoreSales, IgnoreFuturePostings, FALSE, FALSE, '', FALSE,
        IgnoreItemJournal, IgnoreInterCompany);
      CloseProjectServiceCU.SetSelectionCloseService(0, IServiceContractCtrlPeriod.Code);
      CloseProjectServiceCU.Check;
    END;

    PROCEDURE FinalizeServiceContractPeriod@1100528605(IServiceContractCtrlPeriod@1100528602 : Record 11071746);
    VAR
      CloseProjectServiceCU@1100528601 : Codeunit 11012046;
      Type@1100528600 : 'Project,Service Contract,Service Order';
    BEGIN
      CloseProjectServiceCU.SetSelection(
        Type::"Service Contract", '', IServiceContractCtrlPeriod."Service Contract No.", '',
        PostingDate, IgnorePurchase, IgnoreSales, IgnoreFuturePostings, FALSE, FALSE, '', FALSE,
        IgnoreItemJournal, IgnoreInterCompany);
      CloseProjectServiceCU.SetSelectionCloseService(0, IServiceContractCtrlPeriod.Code);
      CloseProjectServiceCU.Check;
      CloseProjectServiceCU.Post;
    END;

    PROCEDURE InsertSOtoTmp@1100528607(IServiceOrder@1100528600 : Record 11012823;IBaseServiceOrderNo@1100528601 : Code[20];IServiceContractNo@1100525001 : Code[20];IServContrControlPeriodCode@1100525000 : Code[10]);
    BEGIN
      IF IServiceOrder.Status < IServiceOrder.Status::"Administrative Finished" THEN
        IF Defer THEN BEGIN
          TmpDeferedServiceOrder := IServiceOrder;
          IF TmpDeferedServiceOrder.INSERT THEN;
        END ELSE
          AddError(Text004, IServiceOrder."No.", IServiceContractNo, IServContrControlPeriodCode)
      ELSE BEGIN
        TmpServiceOrder := IServiceOrder;
        IF TmpServiceOrder.INSERT THEN;
      END;
    END;

    PROCEDURE AddError@7(IMessTxt@11012000 : Text[250];IServiceOrder@1100525001 : Code[20];IServiceContractNo@1100525002 : Code[20];IServContrControlPeriodCode@1100525003 : Code[10]);
    VAR
      ErrorRec@1100525000 : Record 11012051;
      NewLine@1100485000 : Integer;
    BEGIN
      WITH ErrorRec DO BEGIN
        SETRANGE("User ID", USERID);
        SETRANGE("Source Type", "Source Type"::"Finish Service-Project");
        IF FINDLAST THEN
          NewLine := "Line No." + 1
        ELSE
          NewLine := 1;

        INIT;
        "User ID" := USERID;
        "Source Type" := "Source Type"::"Finish Service-Project";
        "Line No." := NewLine;
        "Error message" := IMessTxt;
        "Service Order No." := IServiceOrder;
        "Service Contract No." := IServiceContractNo;
        "Serv. Contr. Ctrl. Period Code" := IServContrControlPeriodCode;
        INSERT;
      END;
    END;

    PROCEDURE DisplayError@1210190000();
    VAR
      ErrorRec@1100525000 : Record 11012051;
    BEGIN
      ErrorRec.SETRANGE("User ID", USERID);
      ErrorRec.SETRANGE("Source Type", ErrorRec."Source Type"::"Finish Service-Project");
      IF ErrorRec.FINDSET THEN BEGIN
        COMMIT;
        PAGE.RUNMODAL(PAGE::Errors, ErrorRec);
      END;
    END;

    PROCEDURE ErrorsExist@1100528606(IServiceContractNo@1100525000 : Code[20];IServContrControlPeriodCode@1100525001 : Code[10]) : Boolean;
    VAR
      ErrorRec@1100528600 : Record 11012051;
    BEGIN
      ErrorRec.SETRANGE("User ID", USERID);
      ErrorRec.SETRANGE("Source Type", ErrorRec."Source Type"::"Finish Service-Project");
      ErrorRec.SETRANGE("Service Contract No.", IServiceContractNo);
      ErrorRec.SETRANGE("Serv. Contr. Ctrl. Period Code", IServContrControlPeriodCode);
      EXIT(NOT ErrorRec.ISEMPTY)
    END;

    PROCEDURE ErrorFoundOnContractObjectMaintenance@1100525014(ServiceOrder@1100528500 : Record 11012823;ServiceContractControlPeriodCode@1100528501 : Code[10]) ErrorFound : Boolean;
    VAR
      ServiceOrderObject@1100525000 : Record 11071724;
      ContractObjectMaintenance@1100525001 : Record 11071692;
      ErrorMessage@1100528502 : Text;
    BEGIN
      ErrorFound := FALSE;
      ServiceOrderObject.SETRANGE("Service Order No.", ServiceOrder."No.");
      ServiceOrderObject.SETFILTER("Maintenance Scheme", '<>%1', '');
      IF ServiceOrderObject.FINDSET THEN
        REPEAT
          IF ContractObjectMaintenance.GET(
            ServiceOrderObject."Service Contract No.", ServiceOrderObject."Object No.", ServiceOrderObject."Maintenance Scheme")
          THEN BEGIN
            IF (FORMAT(ContractObjectMaintenance."Maintenance Interval") = '') AND (ContractObjectMaintenance."Maintenance Interval (Counter)" = 0) THEN BEGIN
              ErrorMessage := STRSUBSTNO(Text005,ContractObjectMaintenance.FIELDCAPTION("Maintenance Interval"), ContractObjectMaintenance."Object No.", ContractObjectMaintenance."Maintenance Scheme");
              ErrorFound := TRUE;
              AddError(ErrorMessage, ServiceOrder."No.", ServiceOrder."Service Contract No.", ServiceContractControlPeriodCode);
            END;
            IF (ContractObjectMaintenance."Maintenance Interval (Counter)" <> 0) AND (ContractObjectMaintenance."Consumption Component" = '') THEN BEGIN
              ErrorMessage := STRSUBSTNO(Text005,ContractObjectMaintenance.FIELDCAPTION("Consumption Component"), ContractObjectMaintenance."Object No.", ContractObjectMaintenance."Maintenance Scheme");
              ErrorFound := TRUE;
              AddError(ErrorMessage, ServiceOrder."No.", ServiceOrder."Service Contract No.", ServiceContractControlPeriodCode);
            END;
            IF (ContractObjectMaintenance."Consumption Component" <> '') AND (ContractObjectMaintenance."Maintenance Interval (Counter)" = 0) THEN BEGIN
              ErrorMessage := STRSUBSTNO(Text005, ContractObjectMaintenance.FIELDCAPTION("Maintenance Interval (Counter)"), ContractObjectMaintenance."Object No.", ContractObjectMaintenance."Maintenance Scheme");
              ErrorFound := TRUE;
              AddError(ErrorMessage, ServiceOrder."No.", ServiceOrder."Service Contract No.", ServiceContractControlPeriodCode);
            END;
            IF (ContractObjectMaintenance."Consumption Component" = '') AND (FORMAT(ContractObjectMaintenance."Maintenance Interval") = '') THEN BEGIN
              ErrorMessage := STRSUBSTNO(Text005, ContractObjectMaintenance.FIELDCAPTION("Maintenance Interval"), ContractObjectMaintenance."Object No.", ContractObjectMaintenance."Maintenance Scheme");
              ErrorFound := TRUE;
              AddError(ErrorMessage, ServiceOrder."No.", ServiceOrder."Service Contract No.", ServiceContractControlPeriodCode);
            END;
          END;
        UNTIL ServiceOrderObject.NEXT = 0;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

