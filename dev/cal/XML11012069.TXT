OBJECT XMLport 11012069 Import GL Entries LOSA (KRAAN)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Import GL Entries LOSA (KRAAN);
               SVE=Importera redovisningstransaktioner LOSA (KRAAN)];
    Direction=Import;
    DefaultFieldsValidation=No;
    TextEncoding=WINDOWS;
    OnInitXMLport=BEGIN
                    HumanResourcesSetup.GET;
                    GLSetup.GET;

                    HumanResourcesSetup.TESTFIELD("GL Entry Journal Template");
                    HumanResourcesSetup.TESTFIELD("GL Entry Journal Batch");
                    HumanResourcesSetup.TESTFIELD("Salary Application",HumanResourcesSetup."Salary Application"::"LOSA-Kraan");

                    currXMLport.FILENAME(HumanResourcesSetup."Pathname Files from Salary");
                  END;

    OnPreXMLport=BEGIN
                   GenJnlLine.LOCKTABLE;
                   GenJnlLine.SETRANGE("Journal Template Name",HumanResourcesSetup."GL Entry Journal Template");
                   GenJnlLine.SETRANGE("Journal Batch Name",HumanResourcesSetup."GL Entry Journal Batch");
                   IF GenJnlLine.FIND('+') THEN
                     LineCounter := GenJnlLine."Line No."
                   ELSE
                     LineCounter := 0;

                   JnlTemplRec.GET(HumanResourcesSetup."GL Entry Journal Template");
                   JnlBatchRec.GET(HumanResourcesSetup."GL Entry Journal Template", HumanResourcesSetup."GL Entry Journal Batch");

                   IF JnlBatchRec."No. Series" <> '' THEN
                     DocNo := NoSeriesMgt.GetNextNo(JnlBatchRec."No. Series",0D,FALSE)
                   ELSE
                     IF JnlTemplRec."No. Series" <> '' THEN
                       DocNo := NoSeriesMgt.GetNextNo(JnlTemplRec."No. Series",0D,FALSE);
                 END;

    OnPostXMLport=BEGIN
                    OldFile := currXMLport.FILENAME;
                    NewFile := HumanResourcesSetup.DetermineBackupFile(OldFile);
                    IF NewFile <> OldFile THEN
                      FileMgt.RenameClientFile(OldFile, NewFile, TRUE);
                  END;

    Format=Fixed Text;
    FileName=*.ASC;
  }
  ELEMENTS
  {
    { [{6539BAE4-C54F-4342-A94E-B49C081EEE48}];  ;Root                ;Element ;Text     }

    { [{3F0C1642-834A-4FDF-8D7C-EDFEE19D2B98}];1 ;GenJournalLine      ;Element ;Table   ;
                                                  SourceTable=Table81;
                                                  SourceTableView=SORTING(Field1,Field51,Field2);
                                                  Import::OnAfterInitRecord=BEGIN
                                                                              "Gen. Journal Line".INIT;
                                                                              CLEAR(SupplyingComp);
                                                                              CLEAR(ReceivingComp);
                                                                              CLEAR(DebitDecTxt);
                                                                              CLEAR(CreditDecTxt);
                                                                              CLEAR(DescTxt);
                                                                              CLEAR(HoursDecTxt);
                                                                              CLEAR(UnitCode);
                                                                            END;

                                                  Import::OnBeforeInsertRecord=VAR
                                                                                 RecRef@1100528400 : RecordRef;
                                                                               BEGIN
                                                                                 WITH "Gen. Journal Line" DO BEGIN //With statement added because this code has been copied from dataport.

                                                                                 "Journal Template Name" := HumanResourcesSetup."GL Entry Journal Template";
                                                                                 "Journal Batch Name" := HumanResourcesSetup."GL Entry Journal Batch";
                                                                                 "Line No." := LineCounter + 10000;
                                                                                 LineCounter := "Line No.";
                                                                                 "Account Type" := "Account Type"::"G/L Account";
                                                                                 "Document No." := DocNo;
                                                                                 "Posting Date" := TODAY;
                                                                                 "Document Date" := "Posting Date";
                                                                                 "Source Code"   := JnlTemplRec."Source Code";
                                                                                 IF JnlBatchRec."Reason Code" <> '' THEN
                                                                                   "Reason Code" := JnlBatchRec."Reason Code"
                                                                                 ELSE
                                                                                   "Reason Code" := JnlTemplRec."Reason Code";

                                                                                 StripZero("Account No.", MaxLenAccount);
                                                                                 StripZero("Job No.", MaxLenProj);
                                                                                 StripZero("Shortcut Dimension 2 Code", MaxLenCostObject);
                                                                                 StripZero("Employee No.", MaxLenEmpl);

                                                                                 "Receiving Company" := CheckCompany;

                                                                                 IF DebitDec <> 0 THEN
                                                                                   VALIDATE(Amount, DebitDec)
                                                                                 ELSE
                                                                                   VALIDATE(Amount, -CreditDec);

                                                                                 Quantity := HoursDec;
                                                                                 "Unit of Measure Code" := UnitCode;

                                                                                 IF "Shortcut Dimension 2 Code" <> '' THEN BEGIN
                                                                                   IF NOT DimValRec.GET(GLSetup."Shortcut Dimension 2 Code", "Shortcut Dimension 2 Code") THEN
                                                                                     HandleUnknownData("Shortcut Dimension 2 Code", 1)
                                                                                   ELSE
                                                                                     IF "Job No." = '' THEN
                                                                                       VALIDATE("Shortcut Dimension 2 Code");
                                                                                 END;
                                                                                 IF "Job No." <> '' THEN BEGIN
                                                                                   IF NOT ProjRec.GET("Job No.") THEN
                                                                                     HandleUnknownData("Job No.", 2)
                                                                                   ELSE
                                                                                     IF DimValRec.GET(GLSetup."Shortcut Dimension 2 Code", "Shortcut Dimension 2 Code") THEN
                                                                                       VALIDATE("Job No.");  //incl. validate Cost Object
                                                                                 END;
                                                                                 IF "Employee No." <> '' THEN BEGIN
                                                                                   IF NOT EmplRec.GET("Employee No.") THEN
                                                                                     HandleUnknownData("Employee No.", 3)
                                                                                   ELSE
                                                                                     VALIDATE("Employee No.");
                                                                                 END;
                                                                                 IF "Account No." <> '' THEN BEGIN
                                                                                   IF NOT GlAccRec.GET("Account No.") THEN
                                                                                     HandleUnknownData("Account No.", 4)
                                                                                   ELSE
                                                                                     VALIDATE("Account No.");
                                                                                 END;

                                                                                 IF NOT GlAccRec.GET("Account No.") THEN GlAccRec.INIT;
                                                                                 IF GlAccRec."Import Posting Information" = FALSE THEN GlAccRec.INIT;
                                                                                 "Gen. Posting Type" := GlAccRec."Gen. Posting Type";
                                                                                 "Gen. Bus. Posting Group" := GlAccRec."Gen. Bus. Posting Group";
                                                                                 "Gen. Prod. Posting Group" := GlAccRec."Gen. Prod. Posting Group";
                                                                                 "VAT Bus. Posting Group" := GlAccRec."VAT Bus. Posting Group";
                                                                                 "VAT Prod. Posting Group" := GlAccRec."VAT Prod. Posting Group";
                                                                                 VALIDATE("VAT Prod. Posting Group");

                                                                                 Description := DescTxt;
                                                                                 "Origin Salary Application" := TRUE;

                                                                                 FillSystemCreatedEntry; //C032670

                                                                                 RecRef.GETTABLE("Gen. Journal Line");
                                                                                 XMLportStatusDialogMgt.ShowImportStatus(RecRef);

                                                                                 INSERT;

                                                                                 currXMLport.SKIP;

                                                                                 END; //Belongs to With statement.
                                                                               END;
                                                                                }

    { [{67661104-3159-45E5-801A-D3DC48E20755}];2 ;Filler1             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=6 }

    { [{D4B92773-3A3B-411F-8E05-6D41A22513D5}];2 ;SupplyingComp       ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=5 }

    { [{5D55E5FA-C8FA-4DA4-9ADC-C2E06EF0D0DD}];2 ;ReceivingComp       ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=5 }

    { [{1D5B97D7-306B-4254-9566-BA41ABDB291F}];2 ;Filler2             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=3 }

    { [{CF022B7F-F6F2-4D9B-A583-15D40E4D7DB4}];2 ;Filler3             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=6 }

    { [{FCDCC404-96F8-4628-AA78-D29B8466FCB5}];2 ;Filler4             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=8 }

    { [{366F15C1-359B-4E1F-8933-F686D72D1A0C}];2 ;AccountNo           ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=Gen. Journal Line::Account No.;
                                                  MinOccurs=Zero;
                                                  Width=14 }

    { [{D23B9255-E7C9-4BEC-8D5A-541BF454DC3B}];2 ;Filler5             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=34 }

    { [{FEDEBCF1-12C5-4B8A-B06C-650977974D92}];2 ;JobNo               ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=Gen. Journal Line::Job No.;
                                                  MinOccurs=Zero;
                                                  Width=10 }

    { [{AC8929BE-1B2D-4405-9FD6-8EB686B1CA6F}];2 ;ShortcutDimension2Code;Element;Field  ;
                                                  DataType=Code;
                                                  SourceField=Gen. Journal Line::Shortcut Dimension 2 Code;
                                                  MinOccurs=Zero;
                                                  Width=10 }

    { [{BE28A6C2-A31E-417D-9CC0-1A5A02FB6BB7}];2 ;Filler6             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=15 }

    { [{10B1AD7B-B426-4FE1-A296-0F503429AC0D}];2 ;Filler7             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=1 }

    { [{1EA7F453-34E7-4B6D-BD28-01F3835E7131}];2 ;Filler8             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=8 }

    { [{8A98AF10-307F-433E-BE33-FE0130BDC7EC}];2 ;EmployeeNo          ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=Gen. Journal Line::Employee No.;
                                                  MinOccurs=Zero;
                                                  Width=6 }

    { [{30E22A27-9DF1-4AA6-8202-28EA52602EAC}];2 ;Filler9             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=1 }

    { [{66568CA5-B01D-408D-A185-9F50693613E1}];2 ;DebitDecTxt         ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  FillEmptyTextWithZero(DebitDecTxt);
                                                                                  ReplaceDotWithDecimalSeparator(DebitDecTxt);
                                                                                  EVALUATE(DebitDec, DebitDecTxt);
                                                                                END;

                                                  Width=12 }

    { [{7D530E26-39A1-4770-9315-899427ED636E}];2 ;Filler10            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=1 }

    { [{AA9706E1-4C49-4C19-89BA-E3D4DB695DD2}];2 ;CreditDecTxt        ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  FillEmptyTextWithZero(CreditDecTxt);
                                                                                  ReplaceDotWithDecimalSeparator(CreditDecTxt);
                                                                                  EVALUATE(CreditDec, CreditDecTxt);
                                                                                END;

                                                  Width=12 }

    { [{F3274D47-7C8A-49BC-ACCF-F269737DCE9B}];2 ;Filler11            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=3 }

    { [{3CF64C76-4057-4CEA-BB12-F54CBD538026}];2 ;Filler12            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=12 }

    { [{EAB275C9-0D43-4986-AABC-086878CB85E3}];2 ;Filler13            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=1 }

    { [{AE967A9A-BE06-497F-958B-3A02EFF9F356}];2 ;Filler14            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=12 }

    { [{D0BB04AC-FE47-4ECE-A6E3-5478AA8BACCD}];2 ;Filler15            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=1 }

    { [{9BE3920A-7E6E-4AC0-AD2E-4C7FB9FEF2D3}];2 ;Filler16            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=12 }

    { [{7DCD564E-AA42-442A-9292-AB4105B6FC96}];2 ;Filler17            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=4 }

    { [{035D4CC1-1176-4B49-BA50-FA7E87F4F71C}];2 ;Filler18            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=12 }

    { [{271D37F5-7D08-4B22-85F1-680FA1841C3B}];2 ;Filler19            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=1 }

    { [{9EF956FB-4309-464E-B217-F2E237E34EA9}];2 ;Filler20            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=12 }

    { [{51231F86-FDC9-4EAE-A072-61E53B29DEC6}];2 ;Filler21            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=8 }

    { [{EAC849D9-EEE6-43E2-8E06-0664DC298DE6}];2 ;DescTxt             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=46 }

    { [{BCEE60F7-8E61-4027-8A16-573D31104364}];2 ;Filler22            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=8 }

    { [{896CE053-6281-4F19-90A8-B5C67A91DA10}];2 ;Filler23            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=31 }

    { [{23BE4FAE-5793-4339-86ED-AECA851F4FCB}];2 ;HoursDecTxt         ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  FillEmptyTextWithZero(HoursDecTxt);
                                                                                  ReplaceDotWithDecimalSeparator(HoursDecTxt);
                                                                                  EVALUATE(HoursDec, HoursDecTxt);
                                                                                END;

                                                  Width=8 }

    { [{FE760E82-EDA7-4054-BD10-BBA4334CFF27}];2 ;Filler24            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=1 }

    { [{4A8B1D14-9B85-46E0-B5FB-8E97BB208CEB}];2 ;Filler25            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=8 }

    { [{CB082A9A-4BCE-4150-AE47-5F6F7DCC150E}];2 ;UnitCode            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=3 }

    { [{CA207BDF-60E8-40C6-ACFD-BBB8C2453979}];2 ;Filler26            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=128 }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1100528408;;Container;
                  ContainerType=ContentArea }

      { 1100528407;1;Group  ;
                  CaptionML=[ENU=Options;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100528406;2;Field  ;
                  Name=RemZero;
                  CaptionML=[ENU=Remove Zero Prefix;
                             SVE=Ta bort nollprefix];
                  OptionCaptionML=[ENU=All,Upto Maximum Used Field Length;
                                   SVE=Alla,Upp till maximalt anv„nd f„ltl„ngd];
                  SourceExpr=RemZero;
                  OnValidate=BEGIN
                               IF RemZero = RemZero::All THEN BEGIN
                                 MaxLenProj := 0;
                                 MaxLenCostObject := 0;
                                 MaxLenEmpl := 0;
                                 MaxLenAccount := 0;
                               END ELSE BEGIN
                                 MaxLenProj := MAXSTRLEN(ProjRec."No.");
                                 MaxLenCostObject := MAXSTRLEN(DimValRec.Code);
                                 MaxLenEmpl := MAXSTRLEN(EmplRec."No.");
                                 MaxLenAccount := MAXSTRLEN(GlAccRec."No.");
                               END;
                             END;
                              }

      { 1100528405;2;Field  ;
                  Name=MaxLenProj;
                  CaptionML=[ENU=Project;
                             SVE=Projekt];
                  SourceExpr=MaxLenProj;
                  OnValidate=BEGIN
                               IF MaxLenProj > MAXSTRLEN(ProjRec."No.") THEN
                                 MaxLenProj := MAXSTRLEN(ProjRec."No.");
                               IF RemZero = RemZero::All THEN
                                 MaxLenProj := 0;
                             END;
                              }

      { 1100528401;2;Field  ;
                  Name=MaxLenAccount;
                  CaptionML=[ENU=G/L Account;
                             SVE=Redovisningskonto];
                  SourceExpr=MaxLenAccount;
                  OnValidate=BEGIN
                               IF MaxLenAccount > MAXSTRLEN(GlAccRec."No.") THEN
                                 MaxLenAccount := MAXSTRLEN(GlAccRec."No.");
                               IF RemZero = RemZero::All THEN
                                 MaxLenAccount := 0;
                             END;
                              }

      { 1100528403;2;Field  ;
                  Name=MaxLenCostObject;
                  CaptionML=[ENU=Cost Object;
                             SVE=Kostnadsobjekt];
                  SourceExpr=MaxLenCostObject;
                  OnValidate=BEGIN
                               IF MaxLenCostObject > MAXSTRLEN(DimValRec.Code) THEN
                                 MaxLenCostObject := MAXSTRLEN(DimValRec.Code);
                               IF RemZero = RemZero::All THEN
                                 MaxLenCostObject := 0;
                             END;
                              }

      { 1100528402;2;Field  ;
                  Name=MaxLenEmpl;
                  CaptionML=[ENU=Employee;
                             SVE=Personal];
                  SourceExpr=MaxLenEmpl;
                  OnValidate=BEGIN
                               IF MaxLenEmpl > MAXSTRLEN(EmplRec."No.") THEN
                                 MaxLenEmpl := MAXSTRLEN(EmplRec."No.");
                               IF RemZero = RemZero::All THEN
                                 MaxLenEmpl := 0;
                             END;
                              }

    }
  }
  CODE
  {
    VAR
      GLSetup@1100528427 : Record 98;
      HumanResourcesSetup@1100528426 : Record 5218;
      ProjRec@1100528425 : Record 11072003;
      EmplRec@1100528424 : Record 5200;
      DimValRec@1100528423 : Record 349;
      GenJnlLine@1100528422 : Record 81;
      JnlTemplRec@1100528421 : Record 80;
      JnlBatchRec@1100528420 : Record 232;
      GlAccRec@1100528419 : Record 15;
      NoSeriesMgt@1100528418 : Codeunit 396;
      FileMgt@1100528414 : Codeunit 419;
      XMLportStatusDialogMgt@1100528428 : Codeunit 11130006;
      LineCounter@1100528417 : Integer;
      OldFile@1100528416 : Text[250];
      NewFile@1100528415 : Text[250];
      DocNo@1100528413 : Text[30];
      DebitDec@1100528409 : Decimal;
      CreditDec@1100528408 : Decimal;
      HoursDec@1100528407 : Decimal;
      Continue@1100528405 : ARRAY [10] OF Boolean;
      RemZero@1100528404 : 'All,Field Length';
      MaxLenProj@1100528403 : Integer;
      MaxLenAccount@1100528402 : Integer;
      MaxLenCostObject@1100528401 : Integer;
      MaxLenEmpl@1100528400 : Integer;
      Text010@1100528412 : TextConst 'ENU=Import Unknown Cost Object;SVE=Importera ok„nt kostnadsobjekt';
      Text011@1100528411 : TextConst 'ENU=Import Unknown Project;SVE=Importera ok„nt projekt.';
      Text012@1100528410 : TextConst 'ENU=Import Unknown Employee;SVE=Importera ok„nd anst„lld';
      Text013@1100528406 : TextConst 'ENU=Import Unknown G/L Account;SVE=Importera ok„nt redovisningskonto';

    PROCEDURE StripZero@1100485001(VAR convtext@11012000 : Code[20];LenFld@1210190000 : Integer);
    VAR
      Position@11012001 : Integer;
    BEGIN
      Position := STRPOS(convtext,'0');
      WHILE Position = 1 DO BEGIN
        IF RemZero = RemZero::"Field Length" THEN BEGIN
          IF LenFld = 0 THEN
            EXIT
          ELSE
            IF STRLEN(convtext) = LenFld THEN EXIT;
        END;
        convtext := DELSTR(convtext,Position,1);
        Position := STRPOS(convtext,'0');
      END;
    END;

    PROCEDURE HandleUnknownData@1100485000(importdata@11012000 : Text[30];ImportNumber@11012001 : Integer);
    BEGIN
      //HandleUnknownData
      //array set to 10
      IF Continue[ImportNumber] = FALSE THEN BEGIN
        CASE ImportNumber OF
          1: Continue[ImportNumber] := CONFIRM(Text010);  //dimension
          2: Continue[ImportNumber] := CONFIRM(Text011);  //project
          3: Continue[ImportNumber] := CONFIRM(Text012);  //employee
          4: Continue[ImportNumber] := CONFIRM(Text013);  //account
        END;
        IF Continue[ImportNumber] = FALSE THEN ERROR(importdata);
      END;
    END;

    PROCEDURE CheckCompany@2() : Text[30];
    VAR
      IcRelRec@11012000 : Record 11012057;
      CompFound@11012002 : Boolean;
      lvText000@1210190000 : TextConst 'ENU=Company %1 not determined by %2 %3.;SVE=F”retag %1 avg”rs inte av %2 %3.';
    BEGIN
      HumanResourcesSetup.TESTFIELD("Delivering Company (Navision)", SupplyingComp);
      IF ReceivingComp = '' THEN
        CompFound := TRUE
      ELSE BEGIN
        CompFound := FALSE;
        IF HumanResourcesSetup."Delivering Company (Navision)" <> ReceivingComp THEN BEGIN
          IF IcRelRec.FIND('-') THEN BEGIN
            REPEAT
              HumanResourcesSetup.CHANGECOMPANY(IcRelRec."Receiving Company");
              HumanResourcesSetup.GET;
              IF HumanResourcesSetup."Delivering Company (Navision)" = ReceivingComp THEN BEGIN
                CompFound := TRUE;
                IF COMPANYNAME = IcRelRec."Receiving Company" THEN
                  ReceivingComp := ''
                ELSE
                  ReceivingComp := IcRelRec."Receiving Company";
              END;
            UNTIL (IcRelRec.NEXT = 0) OR (CompFound = TRUE);
          END;
          HumanResourcesSetup.CHANGECOMPANY(COMPANYNAME);
          HumanResourcesSetup.GET;
        END;
      END;

      IF CompFound = FALSE THEN
        ERROR(lvText000, ReceivingComp,
          HumanResourcesSetup.TABLECAPTION, HumanResourcesSetup.FIELDCAPTION("Delivering Company (Navision)"));

      IF ReceivingComp <> '' THEN BEGIN
        ProjRec.CHANGECOMPANY(ReceivingComp);
        EmplRec.CHANGECOMPANY(ReceivingComp);
        DimValRec.CHANGECOMPANY(ReceivingComp);
        GlAccRec.CHANGECOMPANY(ReceivingComp);
      END ELSE BEGIN
        ProjRec.CHANGECOMPANY(COMPANYNAME);
        EmplRec.CHANGECOMPANY(COMPANYNAME);
        DimValRec.CHANGECOMPANY(COMPANYNAME);
        GlAccRec.CHANGECOMPANY(COMPANYNAME);
      END;

      EXIT(ReceivingComp);
    END;

    PROCEDURE FillEmptyTextWithZero@1100525013(VAR Text@1100525000 : Text[1024]);
    BEGIN
      IF DELCHR(Text,'<>=',' ') = '' THEN
        Text := '0';
    END;

    PROCEDURE ReplaceDotWithDecimalSeparator@1100409000(VAR Text@1100409001 : Text[1024]);
    VAR
      DecimalSeparator@1100409000 : Text[10];
    BEGIN
      DecimalSeparator := DELCHR(FORMAT(1.1), '=', '1');
      Text := CONVERTSTR(Text, '.', DecimalSeparator);
    END;

    PROCEDURE InitXMLPort@1100528500(iRemZero@1100529500 : 'All,Field Length';iMaxLenAccount@1100529501 : Integer;iMaxLenProj@1100529502 : Integer;iMaxLenCostObject@1100529503 : Integer;iMaxLenEmpl@1100529504 : Integer);
    BEGIN
      RemZero := iRemZero;
      MaxLenAccount := iMaxLenAccount;
      MaxLenProj := iMaxLenProj;
      MaxLenCostObject := iMaxLenCostObject;
      MaxLenEmpl := iMaxLenEmpl;
    END;

    BEGIN
    {
      CC: Dataport 11012334
      4PS, 26-08-16, C032670, FillSystemCreatedEntry added
    }
    END.
  }
}

