OBJECT Table 11229822 DPT Event Log
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               "Created On" := CURRENTDATETIME;
               IF (FORMAT("Created By SID") = '') THEN
                 "Created By SID" := USERSECURITYID;
             END;

    OnModify=BEGIN
               IF ("Processed On" = 0DT) THEN BEGIN
                 "Modified On" := CURRENTDATETIME;
                 IF (FORMAT("Modified By SID") = '') THEN
                   "Modified By SID" := USERSECURITYID;
               END;
               IF ("Number of Attempts" < 0) THEN
                 "Number of Attempts" := 0;
               AddMessageTextToProcessMessages;
             END;

    OnDelete=BEGIN
               TESTFIELD("Processed On");
             END;

    OnRename=BEGIN
               ERROR('');
             END;

    CaptionML=ENU=DPT Event Log;
  }
  FIELDS
  {
    { 10  ;   ;Seq No              ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=ENU=Seq No;
                                                   Editable=No }
    { 20  ;   ;Action              ;Text50        ;CaptionML=[ENU=Action;
                                                              NOR=Handling;
                                                              SVE=tg„rd];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 30  ;   ;Source Type         ;Option        ;CaptionML=[ENU=Source Type;
                                                              NOR=Kildetype;
                                                              SVE=Ursprungstyp];
                                                   OptionCaptionML=[ENU=" ,Project,Service Location";
                                                                    SVE=,Project,Serviceplats];
                                                   OptionString=[ ,Project,ServiceLocation];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 40  ;   ;Source No           ;Code20        ;TableRelation=IF (Source Type=CONST(Project)) Job
                                                                 ELSE IF (Source Type=CONST(ServiceLocation)) "Service Location";
                                                   CaptionML=ENU=Source No;
                                                   Editable=No }
    { 50  ;   ;Rec ID              ;RecordID      ;CaptionML=[ENU=Record ID;
                                                              NOR=Post-ID;
                                                              SVE=Post-ID];
                                                   Editable=No }
    { 60  ;   ;xRec ID             ;RecordID      ;CaptionML=ENU=xRecord ID;
                                                   Editable=No }
    { 70  ;   ;Trigger OLD         ;Boolean       ;ObsoleteState=Pending;
                                                   ObsoleteReason=Not used anymore;
                                                   CaptionML=ENU=Trigger OLD;
                                                   Editable=No }
    { 100 ;   ;Created On          ;DateTime      ;CaptionML=[ENU=Created On;
                                                              NOR=Created On;
                                                              SVE=Created On];
                                                   Editable=No }
    { 110 ;   ;Created By SID      ;GUID          ;CaptionML=ENU=Created By SID;
                                                   Editable=No }
    { 150 ;   ;Modified On         ;DateTime      ;CaptionML=[ENU=Modified On;
                                                              NOR=Modified On;
                                                              SVE=Modified On];
                                                   Editable=No }
    { 155 ;   ;Processing          ;Boolean       ;OnValidate=BEGIN
                                                                IF (Processing) THEN
                                                                  TESTFIELD(Processing, FALSE);
                                                              END;

                                                   CaptionML=[ENU=Processing;
                                                              NOR=Behandler;
                                                              SVE=Behandlar];
                                                   Editable=No }
    { 160 ;   ;Modified By SID     ;GUID          ;CaptionML=ENU=Modified By SID;
                                                   Editable=No }
    { 170 ;   ;Processed On        ;DateTime      ;CaptionML=[ENU=Processed On;
                                                              SVE=Behandlat den];
                                                   Editable=No }
    { 180 ;   ;Processed By SID    ;GUID          ;CaptionML=ENU=Processed By SID;
                                                   Editable=No }
    { 190 ;   ;Number of Attempts  ;Integer       ;CaptionML=ENU=Number of Attempts;
                                                   BlankZero=Yes }
    { 200 ;   ;Message Text        ;Text250       ;CaptionML=[ENU=Message;
                                                              NOR=Melding;
                                                              SVE=Meddelande];
                                                   Editable=No }
    { 210 ;   ;Parameters Present  ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("DPT Event Log Parameter" WHERE (Seq No=FIELD(Seq No)));
                                                   CaptionML=ENU=Parameters Present;
                                                   Editable=No }
    { 220 ;   ;Process Messages    ;BLOB          ;CaptionML=ENU=Process Messages }
  }
  KEYS
  {
    {    ;Seq No                                  ;Clustered=Yes }
    {    ;Processed On,Action,Rec ID               }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      CLEANEVENTLOGTEXT@1100528306 : TextConst 'ENU=CleanEventLog';
      DELETEDOCUMENTTEXT@1100528305 : TextConst 'ENU=DeleteDocument';
      DOWNLOADDOCUMENTTEXT@1100528311 : TextConst 'ENU=Download Document';
      ENSUREPORTALSITECOLLECTION@1100528313 : TextConst 'ENU=EnsurePortalSiteCollection';
      NAS@1100528300 : TextConst 'ENU=NAS';
      SYNCAUTHORITYTEXT@1100528304 : TextConst 'ENU=SyncAuthorities';
      SYNCDOCUMENTTEXT@1100528302 : TextConst 'ENU=SyncDocument';
      SYNCPORTALDATATEXT@1100528308 : TextConst 'ENU=SyncPortalData';
      SYNCPORTALMETADATATEXT@1100528309 : TextConst 'ENU=SyncPortalMetaData';
      SYNCPORTALSEARCHDATATEXT@1100528303 : TextConst 'ENU=SyncPortalSearchData';
      SYNCPORTALSITETEXT@1100528310 : TextConst 'ENU=SyncPortalSite';
      SYNCRESPONSIBLEPERSONTEXT@1100528301 : TextConst 'ENU=SyncResponsiblePersons';
      Text000@1100528307 : TextConst 'ENU=Forced Set to Processed.';
      Text001@1100529400 : TextConst 'ENU=Reset Field Processing.';
      Text002@1100529900 : TextConst 'ENU=Record processed succesfully.';
      Text003@1100529901 : TextConst 'ENU=Record inserted succesfully.';

    PROCEDURE AddEventLog@1100528317(ActionIn@1100528300 : Text;SourceTypeIn@1100528301 : Option;SourceNoIn@1100528302 : Code[20];RecIdIn@1100528306 : RecordID;xRecIdIn@1100528307 : RecordID) : Integer;
    BEGIN
      EXIT(AddEventLogLocal(ActionIn, SourceTypeIn, SourceNoIn, RecIdIn, xRecIdIn, USERSECURITYID));
    END;

    PROCEDURE AddEventLog2@1100529900(ActionIn@1100528300 : Text;SourceTypeIn@1100528301 : Option;SourceNoIn@1100528302 : Code[20];RecIdIn@1100528306 : RecordID;xRecIdIn@1100528307 : RecordID;UserSID@1100529900 : GUID) : Integer;
    BEGIN
      EXIT(AddEventLogLocal(ActionIn, SourceTypeIn, SourceNoIn, RecIdIn, xRecIdIn, UserSID));
    END;

    PROCEDURE AddEventLogParameter@1100528308(SeqNo@1100528300 : Integer;TableNo@1100528301 : Integer;FieldNo@1100528302 : Integer;FieldValue@1100528303 : Text);
    VAR
      DPTEventLogParameter@1100528304 : Record 11229830;
    BEGIN
      DPTEventLogParameter.AddEventLogParameter(SeqNo, TableNo, FieldNo, FieldValue);
    END;

    PROCEDURE AddMessageTextToProcessMessages@1100529902();
    VAR
      IStream@1100529906 : InStream;
      OStream@1100529905 : OutStream;
      CRLF@1100529907 : Text[2];
      ProcessMessages@1100529908 : Text;
    BEGIN
      CRLF[1] := 13;
      CRLF[2] := 10;
      CALCFIELDS("Process Messages");
      "Process Messages".CREATEINSTREAM(IStream);
      IStream.READ(ProcessMessages);
      "Process Messages".CREATEOUTSTREAM(OStream);

      CASE TRUE OF
        ("Processed On" = 0DT) AND ("Modified On" = 0DT): BEGIN
          ProcessMessages += (STRSUBSTNO('%1 (%2): %3', FORMAT("Created On"), GetCreatedBy, Text003) + CRLF);
          OStream.WRITE(ProcessMessages);
        END;
        ("Processed On" = 0DT) AND ("Modified On" <> 0DT) AND ("Message Text" <> ''): BEGIN
          ProcessMessages += (STRSUBSTNO('%1 (%2): %3', FORMAT("Modified On"), GetModifiedBy, "Message Text") + CRLF);
          OStream.WRITE(ProcessMessages);
        END;
        ("Processed On" <> 0DT) AND ("Modified On" <> 0DT): BEGIN
          ProcessMessages += (STRSUBSTNO('%1 (%2): %3', FORMAT("Processed On"), GetProcessedBy, Text002) + CRLF);
          OStream.WRITE(ProcessMessages);
        END;
      END;
    END;

    PROCEDURE GetEventLogParameter@1100528310(SeqNo@1100528300 : Integer;TableNo@1100528301 : Integer;FieldNo@1100528302 : Integer) : Text;
    VAR
      DPTEventLogParameter@1100528303 : Record 11229830;
    BEGIN
      EXIT(DPTEventLogParameter.GetEventLogParameter(SeqNo, TableNo, FieldNo));
    END;

    PROCEDURE ProcessEvent@1100528300(RunAsCurrentUser@1100528300 : Boolean);
    VAR
      DPTEventLogNASJQ@1100528301 : Codeunit 11012453;
      MessageText@1100528302 : Text;
    BEGIN
      GET("Seq No"); //Get latest record;
      TESTFIELD(Processing, FALSE);
      MessageText := '';
      DPTEventLogNASJQ.SetStartedFrom(1); //EventLog
      DPTEventLogNASJQ.SetEventLogSeqNo("Seq No");
      IF (RunAsCurrentUser) THEN
        DPTEventLogNASJQ.SetImpersonationUser(USERSECURITYID)
      ELSE
        DPTEventLogNASJQ.SetImpersonationUser("Created By SID");
      DPTEventLogNASJQ.ProcessEvent("Seq No");
    END;

    PROCEDURE CleanupEventLog@1100528301();
    VAR
      DPTSetup@1100528300 : Record 11229823;
      DPTEventLog@1100528301 : Record 11229822;
      CleanupDate@1100528302 : Date;
    BEGIN
      DPTSetup.GET;
      IF (FORMAT(DPTSetup."Event Log Time Window") = '') THEN
        EVALUATE(DPTSetup."Event Log Time Window", '-1M');
      CleanupDate := CALCDATE(DPTSetup."Event Log Time Window", TODAY);

      DPTEventLog.SETCURRENTKEY("Processed On");
      DPTEventLog.SETFILTER("Processed On", '>%1&<=%2', 0DT, CREATEDATETIME(CleanupDate, 0T));
      DPTEventLog.DELETEALL(TRUE);
    END;

    PROCEDURE ForceSetToProcessed@1100528304(VAR DPTEventLog@1100528302 : Record 11229822);
    VAR
      DPTChangeLogEntry@1100528300 : Record 11012207;
    BEGIN
      IF (NOT DPTEventLog.FINDSET(TRUE)) THEN
        EXIT;

      REPEAT
        DPTChangeLogEntry.AddLogEntry(DPTEventLog."Rec ID", STRSUBSTNO('%1 - %2', Text000, DPTEventLog.Action), TRUE);
        DPTEventLog."Modified By SID" := USERSECURITYID;
        DPTEventLog."Modified On" := CURRENTDATETIME;
        DPTEventLog.Processing := FALSE;
        DPTEventLog."Processed By SID" := USERSECURITYID;
        DPTEventLog."Processed On" := CURRENTDATETIME;
        DPTEventLog."Message Text" := STRSUBSTNO(Text000);
        DPTEventLog.MODIFY(TRUE);
        DPTChangeLogEntry.AddLogEntry(DPTEventLog."Rec ID", STRSUBSTNO('%1 - %2', Text000, DPTEventLog.Action), FALSE);
      UNTIL (DPTEventLog.NEXT = 0);
    END;

    PROCEDURE GetCreatedBy@1100528302() : Text;
    BEGIN
      IF ("Created On" = 0DT) THEN
        EXIT('');
      EXIT(GetUserName("Created By SID"));
    END;

    PROCEDURE GetModifiedBy@1100528305() : Text;
    BEGIN
      IF ("Modified On" = 0DT) THEN
        EXIT('');
      EXIT(GetUserName("Modified By SID"));
    END;

    PROCEDURE GetProcessedBy@1100528306() : Text;
    BEGIN
      IF ("Processed On" = 0DT) THEN
        EXIT('');
      EXIT(GetUserName("Processed By SID"));
    END;

    PROCEDURE GetCleanEventLogText@1100528303() : Text;
    BEGIN
      EXIT(CLEANEVENTLOGTEXT);
    END;

    PROCEDURE GetDeleteDocumentText@1100528364() : Text;
    BEGIN
      EXIT(DELETEDOCUMENTTEXT);
    END;

    PROCEDURE GetDownloadDocumentText@1100528316() : Text;
    BEGIN
      EXIT(DOWNLOADDOCUMENTTEXT);
    END;

    PROCEDURE GetEnsurePortalSiteCollectionText@1100528307() : Text;
    BEGIN
      EXIT(ENSUREPORTALSITECOLLECTION);
    END;

    PROCEDURE GetSyncAuthorityText@1100528315() : Text;
    BEGIN
      EXIT(SYNCAUTHORITYTEXT);
    END;

    PROCEDURE GetSyncDocumentText@1100528362() : Text;
    BEGIN
      EXIT(SYNCDOCUMENTTEXT);
    END;

    PROCEDURE GetSyncPortalDataText@1100528312() : Text;
    BEGIN
      EXIT(SYNCPORTALDATATEXT);
    END;

    PROCEDURE GetSyncPortalMetaDataText@1100528309() : Text;
    BEGIN
      EXIT(SYNCPORTALMETADATATEXT);
    END;

    PROCEDURE GetSyncPortalSearchDataText@1100528311() : Text;
    BEGIN
      EXIT(SYNCPORTALSEARCHDATATEXT);
    END;

    PROCEDURE GetSyncPortalSiteText@1100528313() : Text;
    BEGIN
      EXIT(SYNCPORTALSITETEXT);
    END;

    PROCEDURE GetSyncResponsiblePersonText@1100528357() : Text;
    BEGIN
      EXIT(SYNCRESPONSIBLEPERSONTEXT);
    END;

    PROCEDURE ResetProcessing@1100529400(VAR DPTEventLog@1100528302 : Record 11229822);
    VAR
      DPTChangeLogEntry@1100528300 : Record 11012207;
    BEGIN
      DPTEventLog.SETRANGE(Processing, TRUE);
      IF (NOT DPTEventLog.FINDSET(TRUE)) THEN
        EXIT;

      REPEAT
        DPTChangeLogEntry.AddLogEntry(DPTEventLog."Rec ID", STRSUBSTNO('%1 - %2', Text001, DPTEventLog.Action), TRUE);
        DPTEventLog.Processing := FALSE;
        DPTEventLog."Message Text" := '';
        DPTEventLog."Modified On" := CURRENTDATETIME;
        CLEAR(DPTEventLog."Processed By SID");
        DPTEventLog."Processed On" := 0DT;
        DPTEventLog."Number of Attempts" -= 1;
        DPTEventLog.MODIFY;
        DPTChangeLogEntry.AddLogEntry(DPTEventLog."Rec ID", STRSUBSTNO('%1 - %2', Text001, DPTEventLog.Action), FALSE);
      UNTIL (DPTEventLog.NEXT = 0);
    END;

    PROCEDURE ShowProcessMessages@1100529901();
    VAR
      TempBlob@1100529901 : TEMPORARY Record 99008535;
      FileManagement@1100529900 : Codeunit 419;
      IStream@1100529902 : InStream;
      OStream@1100529903 : OutStream;
      ClientTempFileName@1100529904 : Text;
    BEGIN
      CALCFIELDS("Process Messages");
      IF (NOT "Process Messages".HASVALUE) THEN
        EXIT;

      "Process Messages".CREATEINSTREAM(IStream);
      TempBlob.Blob.CREATEOUTSTREAM(OStream);
      COPYSTREAM(OStream, IStream);
      ClientTempFileName := FileManagement.ClientTempFileName('txt');
      FileManagement.BLOBExport4PS(TempBlob, ClientTempFileName, FALSE, FALSE);
      HYPERLINK(ClientTempFileName);
    END;

    LOCAL PROCEDURE "---Locals---"@1100528318();
    BEGIN
    END;

    PROCEDURE AddEventLogLocal@1100528319(ActionIn@1100528300 : Text;SourceTypeIn@1100528301 : Option;SourceNoIn@1100528302 : Code[20];RecIdIn@1100528306 : RecordID;xRecIdIn@1100528307 : RecordID;UserSID@1100529900 : GUID) : Integer;
    VAR
      DPTEventLog@1100528303 : Record 11229822;
      DPTUserTokenCache@1100528305 : Codeunit 11012455;
    BEGIN
      DPTEventLog.SETCURRENTKEY("Processed On", Action, "Rec ID");
      DPTEventLog.SETRANGE("Processed On", 0DT);
      DPTEventLog.SETRANGE(Action, ActionIn);
      DPTEventLog.SETRANGE("Rec ID", RecIdIn);
      DPTEventLog.SETRANGE("Source Type", SourceTypeIn);
      DPTEventLog.SETRANGE("Source No", SourceNoIn);
      DPTEventLog.SETRANGE(Processing, FALSE);
      IF (DPTEventLog.FINDFIRST) THEN BEGIN
        DPTEventLog."Number of Attempts" := 0;
        DPTEventLog."Message Text" := '';
        DPTEventLog."Modified By SID" := UserSID;
        DPTEventLog.MODIFY(TRUE);
        EXIT(DPTEventLog."Seq No");
      END;
      DPTEventLog.RESET;

      IF (GUIALLOWED) AND (CURRENTCLIENTTYPE = CLIENTTYPE::Windows) THEN
        DPTUserTokenCache.ValidateUserTokenCache(USERSECURITYID);

      DPTEventLog.INIT;
      DPTEventLog.Action := COPYSTR(ActionIn, 1, MAXSTRLEN(DPTEventLog.Action));
      DPTEventLog."Source Type" := SourceTypeIn;
      DPTEventLog."Source No" := SourceNoIn;
      DPTEventLog."Rec ID" := RecIdIn;
      DPTEventLog."xRec ID" := xRecIdIn;
      DPTEventLog."Created By SID" := UserSID;
      DPTEventLog.INSERT(TRUE);
      EXIT(DPTEventLog."Seq No");
    END;

    LOCAL PROCEDURE GetUserName@1100528320(SID@1100528300 : GUID) : Text;
    VAR
      User@1100528301 : Record 2000000120;
    BEGIN
      IF (User.GET(SID)) THEN
        EXIT(User."User Name");
      User.INIT;
      IF (SID = User."User Security ID") THEN
        EXIT(NAS);
      EXIT('');
    END;

    BEGIN
    END.
  }
}

