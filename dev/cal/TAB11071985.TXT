OBJECT Table 11071985 Service Category
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ServiceContract@1100528600 : Record 11012812;
             BEGIN
               IF GetServiceContract(ServiceContract, FALSE) THEN
                 IF NOT (ServiceContract."Budget Based on" IN [ServiceContract."Budget Based on"::Category, ServiceContract."Budget Based on"::"Category + Maintenance Scheme"]) THEN
                   ServiceContract.FIELDERROR("Budget Based on", STRSUBSTNO(Text002, ServiceContract."Budget Based on"::Category, ServiceContract."Budget Based on"::"Category + Maintenance Scheme"));
             END;

    OnDelete=VAR
               ServiceContract@1100528603 : Record 11012812;
               SCRevenueDivisionLine@1100528602 : Record 11071987;
               ContractObjectMaintenance@1100528600 : Record 11071692;
               SCCategoryControl@1100528604 : Record 11071988;
               SControlCategory@1100528605 : Record 11072294;
               SControlCategoryCostType@1100528606 : Record 11072296;
               ServCategoryBudgetbyPer@1100528601 : Record 11072314;
               ServicePackage@1100529600 : Record 11012806;
               ContractInstallment@1100529601 : Record 11071707;
               ContractObject@1100529602 : Record 11071702;
             BEGIN
               IF "Service Contract No." = '' THEN BEGIN
                 ServicePackage.SETRANGE("Service Category", Code);
                 IF NOT ServicePackage.ISEMPTY THEN
                   ERROR(Text000, TABLECAPTION, Code, ServicePackage.TABLECAPTION);
               END ELSE BEGIN
                 IF ServiceContract.GET("Service Contract No.") THEN
                   IF (ServiceContract."Service Category (Call)" = Code) OR (ServiceContract."Service Category (Direct)" = Code) THEN
                     ERROR(Text001, TABLECAPTION, Code, ServiceContract."No.");

                 SCRevenueDivisionLine.SETRANGE("Service Contract No.", "Service Contract No.");
                 SCRevenueDivisionLine.SETRANGE("Service Category", Code);
                 IF NOT SCRevenueDivisionLine.ISEMPTY THEN
                   ERROR(Text000, TABLECAPTION, Code, SCRevenueDivisionLine.TABLECAPTION);

                 ContractObject.SETRANGE("Service Contract No.", "Service Contract No.");
                 ContractObject.SETRANGE("Service Category (Contract)", Code);
                 IF NOT ContractObject.ISEMPTY THEN
                   ERROR(Text000, TABLECAPTION, Code, ContractObject.TABLECAPTION);

                 ContractObjectMaintenance.SETRANGE("Service Contract No.", "Service Contract No.");
                 ContractObjectMaintenance.SETRANGE("Service Category", Code);
                 IF NOT ContractObjectMaintenance.ISEMPTY THEN
                   ERROR(Text000, TABLECAPTION, Code, ContractObjectMaintenance.TABLECAPTION);

                 SCCategoryControl.SETRANGE("Service Contract No.", "Service Contract No.");
                 SCCategoryControl.SETRANGE("Service Category", Code);
                 IF NOT SCCategoryControl.ISEMPTY THEN
                   ERROR(Text000, TABLECAPTION, Code, SCCategoryControl.TABLECAPTION);

                 ContractInstallment.SETRANGE("Service Contract No.", "Service Contract No.");
                 ContractInstallment.SETRANGE("Service Category", Code);
                 IF NOT ContractInstallment.ISEMPTY THEN
                   ERROR(Text000, TABLECAPTION, Code, ContractInstallment.TABLECAPTION);

                 SControlCategory.SETRANGE("Service Contract No.", "Service Contract No.");
                 SControlCategory.SETRANGE("Service Category", Code);
                 SControlCategory.DELETEALL(TRUE);

                 SControlCategoryCostType.SETRANGE("Service Contract No.", "Service Contract No.");
                 SControlCategoryCostType.SETRANGE("Service Category", Code);
                 SControlCategoryCostType.DELETEALL(TRUE);

                 ServCategoryBudgetbyPer.SETRANGE("Service Contract No.", "Service Contract No.");
                 ServCategoryBudgetbyPer.SETRANGE("Service Category", Code);
                 ServCategoryBudgetbyPer.DELETEALL(TRUE);
               END;
             END;

    CaptionML=[ENU=Service Category;
               SVE=Servicekategori];
    LookupPageID=Page11229331;
    DrillDownPageID=Page11229331;
  }
  FIELDS
  {
    { 10  ;   ;Service Contract No.;Code20        ;TableRelation=Service-Contract;
                                                   OnValidate=VAR
                                                                ServiceContract@1100528600 : Record 11012812;
                                                              BEGIN
                                                                IF "Service Contract No." <> '' THEN
                                                                  IF ServiceContract.GET("Service Contract No.") THEN BEGIN
                                                                    "Calc. Progress Perc. Progn/Res" := ServiceContract."Calc. Progress Perc. Progn/Res";
                                                                  END;
                                                              END;

                                                   CaptionML=[ENU=Service Contract No.;
                                                              NOR=Servicekontraktnr;
                                                              SVE=Servicekontraktnr];
                                                   Editable=No }
    { 20  ;   ;Code                ;Code10        ;TableRelation=IF (Service Contract No.=FILTER(<>'')) "Service Category".Code WHERE (Service Contract No.=CONST());
                                                   OnValidate=VAR
                                                                ServiceCategory@1100528600 : Record 11071985;
                                                              BEGIN
                                                                IF "Service Contract No." <> '' THEN
                                                                  IF ServiceCategory.GET('', Code) THEN BEGIN
                                                                    Description := ServiceCategory.Description;
                                                                    "Service Type" := ServiceCategory."Service Type";
                                                                    "Service Type (Other)" := ServiceCategory."Service Type (Other)";
                                                                    "Exclude SOs From Est. Result" := ServiceCategory."Exclude SOs From Est. Result";
                                                                  END;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 30  ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 40  ;   ;Calc. Progress Perc. Progn/Res;Option;
                                                   OnValidate=VAR
                                                                ServiceContract@1100528601 : Record 11012812;
                                                                ServicePrognosisManagement@1100528600 : Codeunit 11012884;
                                                              BEGIN
                                                                IF GetServiceContract(ServiceContract, FALSE) THEN  BEGIN
                                                                  IF NOT ("Calc. Progress Perc. Progn/Res" IN ["Calc. Progress Perc. Progn/Res"::"Actual Cost", "Calc. Progress Perc. Progn/Res"::"Elapsed Time"]) THEN
                                                                    ServiceContract.TESTFIELD("Result Calc. on Total Contract", FALSE);
                                                                  ServicePrognosisManagement.TestProgressMethodvsProgressMeasurement(ServiceContract."No.", "Calc. Progress Perc. Progn/Res", ServiceContract."Level Progress Measurement");
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Calculation Progress Perc. Prognosis/Preclosure Result;
                                                   OptionCaptionML=ENU=Allowed Hours,,,Actual Cost,Elapsed Time,,Available Hours,,Allowed Cost,,Available Cost,,None;
                                                   OptionString=Allowed Hours,,,Actual Cost,Elapsed Time,,Available Hours,,Allowed Cost,,Available Cost,,None }
    { 50  ;   ;Unit Price Status   ;Option        ;OnValidate=VAR
                                                                ServiceContract@1100528600 : Record 11012812;
                                                              BEGIN
                                                                IF "Unit Price Status" <> "Unit Price Status"::" " THEN BEGIN
                                                                  GetServiceContract(ServiceContract, TRUE);
                                                                  IF ServiceContract."Price Book Type" = ServiceContract."Price Book Type"::None THEN
                                                                    ServiceContract.FIELDERROR("Price Book Type");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Unit Price Status;
                                                              NOR=Enhetspris, status;
                                                              SVE=Enhetsprisstatus];
                                                   OptionCaptionML=ENU=" ,Pre-Recording,Recording,Production Recording";
                                                   OptionString=[ ,Pre-Recording,Recording,Production Recording] }
    { 60  ;   ;Service Type        ;Code20        ;TableRelation="Service Type";
                                                   OnValidate=BEGIN
                                                                CheckServiceAuthorization(FIELDCAPTION("Service Type"));

                                                                ServiceSetup.GET;
                                                                IF ServiceSetup."Copy Additional Cost Setup" THEN
                                                                  VALIDATE("Service Type (Other)", "Service Type");
                                                              END;

                                                   CaptionML=[ENU=Service Type;
                                                              SVE=Servicetyp] }
    { 70  ;   ;Service Type (Other);Code20        ;TableRelation="Service Type";
                                                   OnValidate=BEGIN
                                                                CheckServiceAuthorization(FIELDCAPTION("Service Type (Other)"));

                                                                ServiceSetup.GET;
                                                                IF ServiceSetup."Copy Additional Cost Setup" THEN
                                                                  TESTFIELD("Service Type (Other)", "Service Type");
                                                              END;

                                                   CaptionML=[ENU=Service Type (Additional Cost);
                                                              SVE=Servicetyp (extra kostnad)] }
    { 80  ;   ;Rest of Contract    ;Boolean       ;OnValidate=VAR
                                                                ServiceContract@1100529620 : Record 11012812;
                                                              BEGIN
                                                                IF GetServiceContract(ServiceContract, FALSE) THEN
                                                                  IF "Rest of Contract" THEN BEGIN
                                                                    IF (FORMAT(ServiceContract."Contract Duration") = '') AND (ServiceContract."Ending Date" = 0D) THEN
                                                                      ERROR(Text003, ServiceContract.FIELDCAPTION("Contract Duration"), ServiceContract.FIELDCAPTION("Ending Date"), "Service Contract No.");
                                                                  END ELSE
                                                                    ServiceContract.TESTFIELD("Result Calc. on Total Contract", FALSE);
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Rest of Contract }
    { 90  ;   ;Calc. Interim Results by Order;Boolean;
                                                   OnValidate=VAR
                                                                ServiceContract@1100529660 : Record 11012812;
                                                              BEGIN
                                                                IF "Calc. Interim Results by Order" THEN BEGIN
                                                                  GetServiceContract(ServiceContract, TRUE);
                                                                  ServiceContract.TESTFIELD("Level Progress Measurement", ServiceContract."Level Progress Measurement"::"Service Category");
                                                                END;
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Calculate Interim Results by Order }
    { 100 ;   ;Actual Costs        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Service-Ledger Entry"."Total Cost (LCY)" WHERE (Service Contract No.=FIELD(Service Contract No.),
                                                                                                                    Service Category=FIELD(Code),
                                                                                                                    Cost Present=CONST(Yes)));
                                                   CaptionML=[ENU=Actual Costs;
                                                              SVE=Verkliga kostnader];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 110 ;   ;Revenues            ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Service-Ledger Entry"."Total Revenue (LCY)" WHERE (Service Contract No.=FIELD(Service Contract No.),
                                                                                                                       Service Category=FIELD(Code),
                                                                                                                       Revenue Present=CONST(Yes)));
                                                   CaptionML=[ENU=Revenues;
                                                              SVE=Int„kter];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 120 ;   ;Exclude SOs From Est. Result;Boolean;
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Exclude Service Orders From Estimated Result }
  }
  KEYS
  {
    {    ;Service Contract No.,Code               ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528600 : TextConst 'ENU=%1 %2 cannot be deleted, still being used in table %3';
      Text001@1100528601 : TextConst 'ENU=%1 %2 cannot be deleted, still being used in Service Contract %3';
      Text002@1100528602 : TextConst 'ENU=Must be %1 or %2';
      Text003@1100529620 : TextConst 'ENU=%1 or %2 must be filled in Service Contract %3.';
      Text004@1100529600 : TextConst 'ENU=%1 can not be changed by user %2, because cost and/or revenues are registrated.;SVE=Kontraktsbelopp finns.';
      ServiceSetup@1100529601 : Record 11012800;
      NewServiceContract@1100529602 : Record 11012812;

    PROCEDURE ShowSCCategoryControl@1100528600();
    VAR
      SCCategoryControl@1100528600 : Record 11071988;
    BEGIN
      SCCategoryControl.SETRANGE("Service Contract No.", "Service Contract No.");
      SCCategoryControl.SETRANGE("Service Category", Code);
      IF SCCategoryControl.FINDLAST THEN
        SCCategoryControl.SETRANGE("Reporting Date", SCCategoryControl."Reporting Date");
      IF SCCategoryControl.FINDFIRST THEN;
      PAGE.RUN(PAGE::"SC Category Control List", SCCategoryControl);
    END;

    PROCEDURE GetServiceType@1100529600(ServiceContractNo@1100529602 : Code[20];ServiceCategoryCode@1100529601 : Code[10];ServiceTypeOther@1100529603 : Boolean;VAR ServiceType@1100529600 : Record 11012814) : Boolean;
    BEGIN
      IF (ServiceContractNo = '') OR (ServiceCategoryCode = '') THEN
        EXIT(FALSE);
      IF NOT GET(ServiceContractNo, ServiceCategoryCode) THEN
        EXIT(FALSE);
      IF ServiceTypeOther THEN
        "Service Type" := "Service Type (Other)";
      IF "Service Type" = '' THEN
        EXIT(FALSE);
      EXIT(ServiceType.GET("Service Type"));
    END;

    PROCEDURE CheckServiceAuthorization@1100485028(FieldCaption@1100485000 : Text[100]);
    VAR
      UserSetup@1100528601 : Record 91;
    BEGIN
      IF "Service Contract No." = '' THEN
        EXIT;
      IF CurrFieldNo = 0 THEN
        EXIT;

      ServiceSetup.GET;
      IF NOT ServiceSetup."Service Authorization" THEN
        EXIT;

      IF CheckCostsAndRevenuesZero THEN
        EXIT;

      IF UserSetup.GET(USERID) THEN
        IF UserSetup."Service Authorization Active" THEN
          EXIT;

      ERROR(Text004, FieldCaption, USERID);
    END;

    PROCEDURE CheckCostsAndRevenuesZero@1100485031() : Boolean;
    BEGIN
      CALCFIELDS(Revenues);
      IF Revenues <> 0 THEN
        EXIT(FALSE);
      CALCFIELDS("Actual Costs");
      EXIT("Actual Costs" = 0);
    END;

    PROCEDURE SetNewServiceContract@1100529601(LocServiceContract@1100529600 : Record 11012812);
    BEGIN
      NewServiceContract := LocServiceContract;
    END;

    LOCAL PROCEDURE GetServiceContract@1100529602(VAR LocServiceContract@1100529600 : Record 11012812;CheckServiceContract@1100529601 : Boolean) : Boolean;
    BEGIN
      IF (NewServiceContract."No." <> '') AND (NewServiceContract."No." = "Service Contract No.") THEN BEGIN
        LocServiceContract := NewServiceContract;
        EXIT(TRUE);
      END;

      IF CheckServiceContract THEN BEGIN
        TESTFIELD("Service Contract No.");
        LocServiceContract.GET("Service Contract No.");
        EXIT(TRUE);
      END;

      IF "Service Contract No." = '' THEN
        EXIT(FALSE);
      EXIT(LocServiceContract.GET("Service Contract No."));
    END;

    BEGIN
    END.
  }
}

