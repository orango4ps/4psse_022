OBJECT Table 320 Tax Jurisdiction
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Code,Description;
    OnInsert=BEGIN
               SetDefaults;
               InsertDetailLines;
             END;

    OnDelete=BEGIN
               DeleteDetailLines;
             END;

    CaptionML=[ENU=Tax Jurisdiction;
               NOR=Mva-jurisdiksjon;
               SVE=Skattemyndighet];
    LookupPageID=Page466;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 3   ;   ;Tax Account (Sales) ;Code20        ;TableRelation="G/L Account";
                                                   CaptionML=[ENU=Tax Account (Sales);
                                                              NOR=Mva-konto (salg);
                                                              SVE=Skatt (f”rs.)] }
    { 4   ;   ;Tax Account (Purchases);Code20     ;TableRelation="G/L Account";
                                                   CaptionML=[ENU=Tax Account (Purchases);
                                                              NOR=Mva-konto (kj›p);
                                                              SVE=Skatt (ink”p)] }
    { 5   ;   ;Report-to Jurisdiction;Code10      ;TableRelation="Tax Jurisdiction";
                                                   CaptionML=[ENU=Report-to Jurisdiction;
                                                              NOR=Rapport til-jurisdiksjon;
                                                              SVE=Rapportering skattemyndighet] }
    { 6   ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Date Filter;
                                                              NOR=Datofilter;
                                                              SVE=Datumfilter] }
    { 7   ;   ;Tax Group Filter    ;Code20        ;FieldClass=FlowFilter;
                                                   TableRelation="Tax Group";
                                                   CaptionML=[ENU=Tax Group Filter;
                                                              NOR=Mva-gruppefilter;
                                                              SVE=Skattegruppsfilter] }
    { 8   ;   ;Unreal. Tax Acc. (Sales);Code20    ;TableRelation="G/L Account";
                                                   CaptionML=[ENU=Unreal. Tax Acc. (Sales);
                                                              NOR=Konto for real. mva (salg);
                                                              SVE=Oreal. skatt (f”rs.)] }
    { 9   ;   ;Unreal. Tax Acc. (Purchases);Code20;TableRelation="G/L Account";
                                                   CaptionML=[ENU=Unreal. Tax Acc. (Purchases);
                                                              NOR=Konto for ureal. mva (kj›p);
                                                              SVE=Oreal. skatt (ink”p)] }
    { 10  ;   ;Reverse Charge (Purchases);Code20  ;TableRelation="G/L Account";
                                                   CaptionML=[ENU=Reverse Charge (Purchases);
                                                              NOR=Snudd avregning (kj›p);
                                                              SVE=Omv„nd oms„ttningsskatt ink”p] }
    { 11  ;   ;Unreal. Rev. Charge (Purch.);Code20;TableRelation="G/L Account";
                                                   CaptionML=[ENU=Unreal. Rev. Charge (Purch.);
                                                              NOR=Ureal. snudd avregning (kj›p);
                                                              SVE=Oreal. omv„nd avgift (ink”p)] }
    { 12  ;   ;Unrealized VAT Type ;Option        ;OnValidate=BEGIN
                                                                IF "Unrealized VAT Type" > 0 THEN BEGIN
                                                                  GLSetup.GET;
                                                                  GLSetup.TESTFIELD("Unrealized VAT",TRUE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Unrealized VAT Type;
                                                              NOR=Urealisert mva-type;
                                                              SVE=Hantering oreal. moms];
                                                   OptionCaptionML=[ENU=" ,Percentage,First,Last,First (Fully Paid),Last (Fully Paid)";
                                                                    NOR=" ,Prosent,F›rst,Sist,F›rst (Fullt betalt),Sist (Fullt betalt)";
                                                                    SVE=" ,Procentuell f”rdelning,F”rst moms,Sist moms,F”rst moms (hela bel.),Sist moms (hela bel.)"];
                                                   OptionString=[ ,Percentage,First,Last,First (Fully Paid),Last (Fully Paid)] }
    { 13  ;   ;Calculate Tax on Tax;Boolean       ;OnValidate=BEGIN
                                                                TaxDetail.SETRANGE("Tax Jurisdiction Code",Code);
                                                                TaxDetail.MODIFYALL("Calculate Tax on Tax","Calculate Tax on Tax");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Calculate Tax on Tax;
                                                              NOR=Beregn mva. p† mva.;
                                                              SVE=Ber„kna skatt p† skatt] }
    { 14  ;   ;Adjust for Payment Discount;Boolean;OnValidate=BEGIN
                                                                IF "Adjust for Payment Discount" THEN BEGIN
                                                                  GLSetup.GET;
                                                                  GLSetup.TESTFIELD("Adjust for Payment Disc.",TRUE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Adjust for Payment Discount;
                                                              NOR=Juster for kontantrabatt;
                                                              SVE=Justera f”r kassarabatt] }
    { 15  ;   ;Name                ;Text30        ;CaptionML=[ENU=Name;
                                                              NOR=Navn;
                                                              SVE=Namn] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
    {    ;Report-to Jurisdiction                   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      GLSetup@1000 : Record 98;
      TaxDetail@1001 : Record 322;

    [External]
    PROCEDURE GetSalesAccount@4(Unrealized@1000 : Boolean) : Code[20];
    BEGIN
      IF Unrealized THEN BEGIN
        TESTFIELD("Unreal. Tax Acc. (Sales)");
        EXIT("Unreal. Tax Acc. (Sales)");
      END;
      TESTFIELD("Tax Account (Sales)");
      EXIT("Tax Account (Sales)");
    END;

    [External]
    PROCEDURE GetPurchAccount@5(Unrealized@1000 : Boolean) : Code[20];
    BEGIN
      IF Unrealized THEN BEGIN
        TESTFIELD("Unreal. Tax Acc. (Purchases)");
        EXIT("Unreal. Tax Acc. (Purchases)");
      END;
      TESTFIELD("Tax Account (Purchases)");
      EXIT("Tax Account (Purchases)");
    END;

    [External]
    PROCEDURE GetRevChargeAccount@6(Unrealized@1000 : Boolean) : Code[20];
    BEGIN
      IF Unrealized THEN BEGIN
        TESTFIELD("Unreal. Rev. Charge (Purch.)");
        EXIT("Unreal. Rev. Charge (Purch.)");
      END;
      TESTFIELD("Reverse Charge (Purchases)");
      EXIT("Reverse Charge (Purchases)");
    END;

    [External]
    PROCEDURE CreateTaxJurisdiction@7(NewJurisdictionCode@1000 : Code[10]);
    BEGIN
      INIT;
      Code := NewJurisdictionCode;
      Description := NewJurisdictionCode;
      SetDefaults;
      IF INSERT(TRUE) THEN;
    END;

    LOCAL PROCEDURE SetDefaults@3();
    VAR
      TaxSetup@1000 : Record 326;
    BEGIN
      TaxSetup.GET;
      "Tax Account (Sales)" := TaxSetup."Tax Account (Sales)";
      "Tax Account (Purchases)" := TaxSetup."Tax Account (Purchases)";
      "Unreal. Tax Acc. (Sales)" := TaxSetup."Unreal. Tax Acc. (Sales)";
      "Unreal. Tax Acc. (Purchases)" := TaxSetup."Unreal. Tax Acc. (Purchases)";
      "Reverse Charge (Purchases)" := TaxSetup."Reverse Charge (Purchases)";
      "Unreal. Rev. Charge (Purch.)" := TaxSetup."Unreal. Rev. Charge (Purch.)";
    END;

    LOCAL PROCEDURE InsertDetailLines@1();
    VAR
      TaxDetail@1000 : Record 322;
      TaxSetup@1001 : Record 326;
    BEGIN
      TaxSetup.GET;
      IF NOT TaxSetup."Auto. Create Tax Details" THEN
        EXIT;

      TaxDetail.SETRANGE("Tax Jurisdiction Code",Code);
      IF NOT TaxDetail.ISEMPTY THEN
        EXIT;

      TaxDetail.INIT;
      TaxDetail."Tax Jurisdiction Code" := Code;
      TaxDetail."Tax Group Code" := '';
      TaxDetail."Tax Type" := TaxDetail."Tax Type"::"Sales Tax";
      TaxDetail."Effective Date" := WORKDATE;
      TaxDetail.INSERT;

      IF TaxSetup."Non-Taxable Tax Group Code" <> '' THEN BEGIN
        TaxDetail.INIT;
        TaxDetail."Tax Jurisdiction Code" := Code;
        TaxDetail."Tax Group Code" := TaxSetup."Non-Taxable Tax Group Code";
        TaxDetail."Tax Type" := TaxDetail."Tax Type"::"Sales Tax";
        TaxDetail."Effective Date" := WORKDATE;
        TaxDetail.INSERT;
      END;
    END;

    LOCAL PROCEDURE DeleteDetailLines@2();
    VAR
      TaxAreaLine@1000 : Record 319;
      TaxDetail@1001 : Record 322;
    BEGIN
      TaxAreaLine.SETRANGE("Tax Jurisdiction Code",Code);
      TaxAreaLine.DELETEALL;

      TaxDetail.SETRANGE("Tax Jurisdiction Code",Code);
      TaxDetail.DELETEALL;
    END;

    [External]
    PROCEDURE GetDescriptionInCurrentLanguage@9() : Text[50];
    VAR
      TaxJurisdictionTranslation@1001 : Record 327;
      Language@1002 : Record 8;
    BEGIN
      IF TaxJurisdictionTranslation.GET(Code,Language.GetUserLanguage) THEN
        EXIT(TaxJurisdictionTranslation.Description);
      EXIT(Description);
    END;

    [External]
    PROCEDURE GetName@8() : Text[30];
    BEGIN
      IF Name = '' THEN
        Name := Code;

      EXIT(Name);
    END;

    BEGIN
    END.
  }
}

