OBJECT Table 11020426 Unit Price Production Mot. L.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               lvProdLineRec@1100485000 : Record 11020426;
               lvProdMotRec@1100485001 : Record 11020425;
             BEGIN
               TestProjectStatus;
               IF "Line No." = 0 THEN BEGIN
                 lvProdLineRec.SETRANGE("Project No.", "Project No.");
                 lvProdLineRec.SETRANGE("Project Principal", "Project Principal");
                 lvProdLineRec.SETRANGE("Production Date", "Production Date");
                 lvProdLineRec.SETCURRENTKEY("Project No.", "Extension Contract No.", "Production Date", Code, "Line No.");
                 IF lvProdLineRec.FINDLAST THEN
                   "Line No." := lvProdLineRec."Line No." + 10000
                 ELSE
                   "Line No." := 10000;
               END;

               TESTFIELD("Production Date");
               lvProdMotRec.GET("Project No.", "Project Principal", "Production Date", Code);
               "Order No." := lvProdMotRec."Order No.";
               "Prev. Production Filter" := '<' + FORMAT("Order No.");
               "Curr. Production Filter" := '<=' + FORMAT("Order No.");
               CALCFIELDS("Customer No.");
               UpdateStatus; //C013394
             END;

    OnModify=BEGIN
               TestProjectStatus;
               CheckChangingAllowed; //C011505
               UpdateStatus; //C013394
             END;

    OnDelete=BEGIN
               TestProjectStatus;
               CheckChangingAllowed; //C011505
               UpdateStatus; //C013394
             END;

    OnRename=BEGIN
               TestProjectStatus;
             END;

    CaptionML=[ENU=Unit Price Production Motivation Line;
               NOR=Enhetspris for Produksjonsmotivasjonsrad;
               SVE=Enhetspris f”r Produktionsorsaksrad];
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation=Job.No.;
                                                   CaptionML=[ENU=Job No.;
                                                              NOR=Prosjektnr.;
                                                              SVE=Projektnr] }
    { 15  ;   ;Project Principal   ;Code20        ;TableRelation="Unit Price Production Mot."."Project Principal" WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=[ENU=Project Principal;
                                                              NOR=Prosjektoppdragsgiver;
                                                              SVE=Projektuppdragsgivare];
                                                   Editable=No }
    { 20  ;   ;Production Date     ;Date          ;TableRelation="Unit Price Production Mot."."Production Date" WHERE (Project No.=FIELD(Project No.),
                                                                                                                       Project Principal=FIELD(Project Principal),
                                                                                                                       Code=FIELD(Code));
                                                   CaptionML=[ENU=Production Date;
                                                              NOR=Produksjonsdato;
                                                              SVE=Produktionsdatum];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 25  ;   ;Code                ;Code10        ;TableRelation="Unit Price Production Mot.".Code WHERE (Project No.=FIELD(Project No.),
                                                                                                          Project Principal=FIELD(Project Principal),
                                                                                                          Production Date=FIELD(Production Date));
                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod] }
    { 30  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr];
                                                   Editable=No }
    { 40  ;   ;Customer No.        ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Unit Price Production Mot."."Customer No." WHERE (Project No.=FIELD(Project No.),
                                                                                                                         Project Principal=FIELD(Project Principal),
                                                                                                                         Production Date=FIELD(Production Date),
                                                                                                                         Code=FIELD(Code)));
                                                   TableRelation=Customer;
                                                   CaptionML=[ENU=Principal Unit Price;
                                                              NOR=Enhetspris for Oppdragsgiver;
                                                              SVE=Huvudkund];
                                                   Editable=No }
    { 50  ;   ;Price List          ;Code10        ;TableRelation="Price List".Code WHERE (Customer No.=FIELD(Customer No.),
                                                                                          Status=CONST(Open));
                                                   OnValidate=BEGIN
                                                                IF "Unit Price" <> '' THEN
                                                                  VALIDATE("Unit Price"); //current uit price must exist in price list
                                                              END;

                                                   CaptionML=[ENU=Price List;
                                                              NOR=Prisliste;
                                                              SVE=Prislista] }
    { 60  ;   ;Unit Price          ;Code20        ;TableRelation="Unit Price".Code WHERE (Customer No.=FIELD(Customer No.),
                                                                                          Price List=FIELD(Price List));
                                                   OnValidate=VAR
                                                                lvUnitPriceRec@1100485002 : Record 11020420;
                                                                lvProjUnitPriceRec@1100525000 : Record 11020427;
                                                              BEGIN
                                                                DoCalcFields;
                                                                "Unit of Measure" := "Unit Price Unit of Measure";
                                                                "Sales Price" := "Unit Price Sales Price";

                                                                IF "Unit Price" <> '' THEN BEGIN
                                                                  IF lvUnitPriceRec.GET("Customer No.", "Price List", "Unit Price", "Reference Date") THEN BEGIN
                                                                    Description := lvUnitPriceRec.Description;
                                                                    "Description 2" := lvUnitPriceRec."Description 2";
                                                                    VALIDATE(Element, lvUnitPriceRec.Element);
                                                                  END;
                                                                  //C012539
                                                                  //IF lvProjUnitPriceRec.GET("Project No.","Customer No.","Extension Contract No.", "Price List","Unit Price") THEN BEGIN
                                                                  //  VALIDATE(Element, lvProjUnitPriceRec.Element);// C011958 - 07-01-14
                                                                  //END;
                                                                  IF ("Project Unit Price Line No." <> 0) AND (lvProjUnitPriceRec.GET("Project No.", "Project Unit Price Line No.")) THEN BEGIN
                                                                    VALIDATE(Element, lvProjUnitPriceRec.Element);
                                                                    "Description 3" := lvProjUnitPriceRec.Description;
                                                                  END ELSE BEGIN
                                                                    //Support the old situation
                                                                    lvProjUnitPriceRec.SETRANGE("Project No.","Project No.");
                                                                    lvProjUnitPriceRec.SETRANGE("Customer No.","Customer No.");
                                                                    lvProjUnitPriceRec.SETRANGE("Extension Contract No.","Extension Contract No.");
                                                                    lvProjUnitPriceRec.SETRANGE("Price List","Price List");
                                                                    lvProjUnitPriceRec.SETRANGE("Unit Price","Unit Price");
                                                                    IF lvProjUnitPriceRec.FINDFIRST THEN BEGIN
                                                                      VALIDATE(Element, lvProjUnitPriceRec.Element);
                                                                      "Description 3" := lvProjUnitPriceRec.Description;
                                                                    END;
                                                                  END;
                                                                  //
                                                                END ELSE BEGIN
                                                                  Element := '';
                                                                  "Unit of Measure" := '';
                                                                  "Sales Price" := 0;
                                                                END;
                                                                "Production Cumulative" := "Previous Production Cumulative";
                                                                Production := 0;
                                                              END;

                                                   CaptionML=[ENU=Unit Price;
                                                              NOR=Salgspris;
                                                              SVE=A-pris] }
    { 70  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 75  ;   ;Description 2       ;Text50        ;CaptionML=[ENU=Description 2;
                                                              NOR=Beskrivelse 2;
                                                              SVE=Beskrivning 2] }
    { 76  ;   ;Description 3       ;Text50        ;CaptionML=[ENU=Description 3;
                                                              NOR=Beskrivelse 3;
                                                              SVE=Beskrivning 3] }
    { 80  ;   ;Quantity            ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Unit Price".Quantity WHERE (Project No.=FIELD(Project No.),
                                                                                                           Line No.=FIELD(Project Unit Price Line No.)));
                                                   CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   Editable=No }
    { 90  ;   ;Reference Date Principal;Date      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Principal"."Ref. Date Unit Price" WHERE (Project No.=FIELD(Project No.),
                                                                                                                        Principal=FIELD(Customer No.)));
                                                   CaptionML=[ENU=Reference Date Principal;
                                                              SVE=Referensdataprojekt];
                                                   Editable=No }
    { 95  ;   ;Element             ;Code20        ;TableRelation="Project Element".Element WHERE (Project No.=FIELD(Project No.));
                                                   OnValidate=VAR
                                                                ElementFormat@1100525000 : Codeunit 11012022;
                                                              BEGIN
                                                                ElementFormat.FormatElem(Element,2,'');
                                                              END;

                                                   CaptionML=[ENU=Element;
                                                              NOR=Element;
                                                              SVE=Element] }
    { 100 ;   ;Unit Price Unit of Measure;Code10  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Unit Price"."Unit of Measure" WHERE (Customer No.=FIELD(Customer No.),
                                                                                                            Price List=FIELD(Price List),
                                                                                                            Code=FIELD(Unit Price),
                                                                                                            Start Date=FIELD(Reference Date)));
                                                   CaptionML=[ENU=Unit Price Unit of Measure;
                                                              NOR=Enhetspris for M†leenhet;
                                                              SVE=Enhetspris f”r enhet];
                                                   Editable=No }
    { 105 ;   ;Unit of Measure     ;Code10        ;TableRelation="Unit of Measure";
                                                   CaptionML=[ENU=Unit of Measure;
                                                              NOR=Enhet;
                                                              SVE=Enhet] }
    { 110 ;   ;RefDateFilter       ;Text30        ;CaptionML=[ENU=RefDateFilter;
                                                              NOR=RefDateFilter;
                                                              SVE=RefDateFilter] }
    { 120 ;   ;Reference Date      ;Date          ;FieldClass=FlowField;
                                                   CalcFormula=Max("Unit Price"."Start Date" WHERE (Customer No.=FIELD(Customer No.),
                                                                                                    Price List=FIELD(Price List),
                                                                                                    Code=FIELD(Unit Price),
                                                                                                    Start Date=FIELD(UPPERLIMIT(FILTER(RefDateFilter)))));
                                                   CaptionML=[ENU=Reference Date;
                                                              NOR=Referansedato;
                                                              SVE=Referensdatum];
                                                   Editable=No }
    { 130 ;   ;Unit Price Sales Price;Decimal     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Unit Price".Price WHERE (Customer No.=FIELD(Customer No.),
                                                                                                Price List=FIELD(Price List),
                                                                                                Code=FIELD(Unit Price),
                                                                                                Start Date=FIELD(Reference Date)));
                                                   CaptionML=[ENU=Unit Price Sales Price;
                                                              NOR=Enhetspris salgspris;
                                                              SVE=Enhetspris F”rs„ljningspris];
                                                   Editable=No }
    { 135 ;   ;Sales Price         ;Decimal       ;OnValidate=BEGIN
                                                                CalculateProgress;
                                                              END;

                                                   CaptionML=[ENU=Sales Price;
                                                              NOR=Salgspris;
                                                              SVE=F”rs„ljningspris] }
    { 137 ;   ;Sales Discount %    ;Decimal       ;OnValidate=BEGIN
                                                                //DP00659
                                                                TestProjectStatus;
                                                                CheckChangingAllowed;
                                                                CalculateProgress;
                                                              END;

                                                   CaptionML=[ENU=Sales Discount %;
                                                              NOR=Salgsrabattprosent;
                                                              SVE=F”rs„ljningsrabattprocent];
                                                   DecimalPlaces=0:5;
                                                   BlankZero=Yes }
    { 138 ;   ;Sales Surcharge %   ;Decimal       ;OnValidate=BEGIN
                                                                //DP00659
                                                                TestProjectStatus;
                                                                CheckChangingAllowed;
                                                                CalculateProgress;
                                                              END;

                                                   CaptionML=[ENU=Sales Surcharge %;
                                                              NOR=Salgstilleggsgebyr %;
                                                              SVE=F”rs„ljningstill„ggsavgiftsprocent];
                                                   DecimalPlaces=0:5;
                                                   BlankZero=Yes }
    { 140 ;   ;Production          ;Decimal       ;OnValidate=BEGIN
                                                                TestProjectStatus; //C011505
                                                                CheckChangingAllowed; //C011505

                                                                CALCFIELDS("Previous Production Cumulative");
                                                                CALCFIELDS("Production Cumulative");

                                                                CheckProgress;
                                                                CalculateProgress;
                                                              END;

                                                   CaptionML=[ENU=Production in Period;
                                                              NOR=Produksjon i periode;
                                                              SVE=Produktion i period] }
    { 151 ;   ;Production Cumulative;Decimal      ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Unit Price Production Mot. L.".Production WHERE (Project No.=FIELD(Project No.),
                                                                                                                     Project Principal=FIELD(Project Principal),
                                                                                                                     Extension Contract No.=FIELD(Extension Contract No.),
                                                                                                                     Unit Price=FIELD(Unit Price),
                                                                                                                     Line No.=FIELD(Line No.),
                                                                                                                     Status=FILTER(<>Expired),
                                                                                                                     Order No.=FIELD(FILTER(Curr. Production Filter))));
                                                   CaptionML=[ENU=Production Cumulative;
                                                              NOR=Akkumulert Produksjon;
                                                              SVE=ProductionCumulative];
                                                   Editable=No }
    { 160 ;   ;Production Value    ;Decimal       ;CaptionML=[ENU=Production Value;
                                                              NOR=Produksjonsverdi;
                                                              SVE=Produktionsv„rde];
                                                   Editable=No }
    { 171 ;   ;Production Value Cumulative;Decimal;FieldClass=FlowField;
                                                   CalcFormula=Sum("Unit Price Production Mot. L."."Production Value" WHERE (Project No.=FIELD(Project No.),
                                                                                                                             Project Principal=FIELD(Project Principal),
                                                                                                                             Extension Contract No.=FIELD(Extension Contract No.),
                                                                                                                             Unit Price=FIELD(Unit Price),
                                                                                                                             Line No.=FIELD(Line No.),
                                                                                                                             Status=FILTER(<>Expired),
                                                                                                                             Order No.=FIELD(FILTER(Curr. Production Filter))));
                                                   CaptionML=[ENU=Production Value Cumulative;
                                                              NOR=Akkumulert produksjonsverdi;
                                                              SVE=Ackumulerat produktionsv„rde];
                                                   Editable=No }
    { 180 ;   ;Total Production Value;Decimal     ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Unit Price Production Mot. L."."Production Value" WHERE (Project No.=FIELD(Project No.),
                                                                                                                             Project Principal=FIELD(Project Principal),
                                                                                                                             Production Date=FIELD(Production Date),
                                                                                                                             Code=FIELD(Code)));
                                                   CaptionML=[ENU=Total Production Value;
                                                              NOR=Total Produksjonsverdi;
                                                              SVE=Totalt produktionsv„rde];
                                                   Editable=No }
    { 190 ;   ;Prev. Production Filter;Text30     ;CaptionML=[ENU=Previous Product Filter;
                                                              NOR=Forrige Produktfilter;
                                                              SVE=F”reg†ende produktfilter] }
    { 200 ;   ;Curr. Production Filter;Text30     ;CaptionML=[ENU=Current Product Filter;
                                                              NOR=Aktuelt Produktfilter;
                                                              SVE=Aktuellt produktfilter] }
    { 210 ;   ;Previous Production Cumulative;Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("Unit Price Production Mot. L.".Production WHERE (Project No.=FIELD(Project No.),
                                                                                                                     Project Principal=FIELD(Project Principal),
                                                                                                                     Extension Contract No.=FIELD(Extension Contract No.),
                                                                                                                     Unit Price=FIELD(Unit Price),
                                                                                                                     Line No.=FIELD(Line No.),
                                                                                                                     Status=FILTER(<>Expired),
                                                                                                                     Order No.=FIELD(FILTER(Prev. Production Filter))));
                                                   CaptionML=[ENU=Previous Production Cumulative;
                                                              NOR=Forrige Akkumulerte Produksjon;
                                                              SVE=Ackumulerad f”reg†ende produktion];
                                                   Editable=No }
    { 220 ;   ;Prev. Production Value;Decimal     ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Unit Price Production Mot. L."."Production Value" WHERE (Project No.=FIELD(Project No.),
                                                                                                                             Project Principal=FIELD(Project Principal),
                                                                                                                             Extension Contract No.=FIELD(Extension Contract No.),
                                                                                                                             Unit Price=FIELD(Unit Price),
                                                                                                                             Line No.=FIELD(Line No.),
                                                                                                                             Status=FILTER(<>Expired),
                                                                                                                             Order No.=FIELD(FILTER(Prev. Production Filter))));
                                                   CaptionML=[ENU=Previous Production Value Cumulative;
                                                              NOR=Forrige Akkumulerte Produksjonsverdi;
                                                              SVE=Ackumulerat f”reg†ende produktionsv„rde];
                                                   Editable=No }
    { 230 ;   ;Total Production Cumulative;Decimal;FieldClass=FlowField;
                                                   CalcFormula=Sum("Unit Price Production Mot. L.".Production WHERE (Project No.=FIELD(Project No.),
                                                                                                                     Project Principal=FIELD(Project Principal),
                                                                                                                     Extension Contract No.=FIELD(Extension Contract No.),
                                                                                                                     Unit Price=FIELD(Unit Price),
                                                                                                                     Status=FILTER(<>Expired)));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Previous Production Cumulative");
                                                                VALIDATE(Production,("Production Cumulative" - "Previous Production Cumulative"));
                                                              END;

                                                   CaptionML=[ENU=Total Production Cumulative;
                                                              NOR=Total Akkumulert Produksjon;
                                                              SVE=Totalt ackumulerad produktion];
                                                   Description=Total Production for this unit price;
                                                   Editable=No }
    { 240 ;   ;Order No.           ;Integer       ;CaptionML=[ENU=Order No.;
                                                              NOR=Ordrenr;
                                                              SVE=Ordernr];
                                                   Editable=No }
    { 250 ;   ;Extension Contract No.;Code10      ;TableRelation="Extension Contract"."Contract No." WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=[ENU=Extension Contract No.;
                                                              NOR=ETA-nr;
                                                              SVE=ŽTA-nr] }
    { 260 ;   ;Project Unit Price Line No.;Integer;TableRelation="Project Unit Price"."Line No." WHERE (Project No.=FIELD(Project No.));
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=ENU=Project Unit Price Line No. }
    { 300 ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=[ENU=Proposal,Offered,Approved,Expired;
                                                                    NOR=Forslag,Tilbudt,Godkjent,Forfalt;
                                                                    SVE=F”rslag,Erbjuden,Godk„nd,F”rfallen];
                                                   OptionString=Proposal,Offered,Approved,Expired }
  }
  KEYS
  {
    {    ;Project No.,Project Principal,Extension Contract No.,Production Date,Code,Line No.;
                                                   Clustered=Yes }
    {    ;Project No.,Project Principal,Production Date,Code,Unit Price }
    {    ;Project No.,Project Principal,Order No.,Unit Price;
                                                   SumIndexFields=Production,Production Value }
    {    ;Project No.,Project Principal,Production Date,Code,Line No. }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100485000 : TextConst 'ENU=Production (%1) is greater than entered in Project Unit Prices (%2). Do you want to continue?;SVE=Produktion (%1) „r st”rre „n angivet v„rde i projektenhetspriser. Vill du forts„tta?';
      Text002@1100485001 : TextConst 'ENU=It is not allowed to change the production motivation anymore because the %1 is %2.;SVE=Det „r inte till†tet att „ndra Produktionsorsaken l„ngre eftersom den „r godk„nd.';
      Text003@1100525000 : TextConst 'ENU=%1 %2 has %3 %4.;NOR=%1 %2 har %3 %4.;SVE=%1 %2 har %3 %4.';

    PROCEDURE DoCalcFields@1100485002();
    VAR
      lvProjectPrincipal@1100525000 : Record 11012005;
    BEGIN
      IF "Project No." = '' THEN
        EXIT; //This function is called when maximising the prod mot form, when the page is still empty you
              //don't want to execute the code below
      CALCFIELDS("Customer No.","Reference Date Principal");
      IF "Reference Date Principal" = 0D THEN BEGIN
        lvProjectPrincipal.GET("Project No.", "Customer No.");
        lvProjectPrincipal.TESTFIELD("Ref. Date Unit Price");
      END;
      RefDateFilter := STRSUBSTNO('..%1', "Reference Date Principal");
      CALCFIELDS("Reference Date");
      CALCFIELDS("Unit Price Sales Price");
      IF "Unit Price" <> '' THEN
        "Sales Price" := "Unit Price Sales Price";
      //we have two fields with Sales Price, one is the flowfield, one can be filled by user

      CALCFIELDS("Unit Price Unit of Measure", "Total Production Value", Quantity);
      CALCFIELDS("Previous Production Cumulative", "Prev. Production Value");
    END;

    PROCEDURE CheckProgress@1100485000();
    VAR
      lvProjectUnitPriceRec@1100485000 : Record 11020427;
      Job@1210190000 : Record 11072003;
    BEGIN
      IF (Production = 0) THEN
        EXIT;

      CALCFIELDS("Production Cumulative", Quantity);
      IF ("Production Cumulative") > Quantity THEN BEGIN
        IF Quantity = 0 THEN BEGIN
          lvProjectUnitPriceRec.SETRANGE("Project No.", "Project No.");
          IF NOT lvProjectUnitPriceRec.FINDFIRST THEN //if no "project unit prices", don't check
            EXIT;
        END;
        CALCFIELDS("Customer No.");
        Job.GET("Project No.");
      //IF ("Project Principal" = Job."Bill-to Customer No.") THEN //28-02-2014.o
      //  IF NOT CONFIRM(Text001, FALSE, "Total Production Cumulative", Quantity) THEN //27-02-2014.o
          IF NOT CONFIRM(Text001, FALSE, "Production Cumulative", Quantity) THEN //27-02-2014.n
            ERROR('');
      END;
    END;

    PROCEDURE CalculateProgress@1100485001();
    BEGIN
      DoCalcFields;
      //"Production Value" := ROUND(Production * "Sales Price"); //DP00659.o
      "Production Value" := ROUND(Production * CalculatedSalesPrice); //DP00659.n
      CALCFIELDS("Production Value Cumulative");
      CALCFIELDS("Total Production Value");
    END;

    PROCEDURE CorrectNextMotivations@1100485004();
    VAR
      UnitPriceProductionMotLine@1100485000 : Record 11020426;
    BEGIN
      //C011505
      UnitPriceProductionMotLine.SETRANGE("Project No.", "Project No.");
      UnitPriceProductionMotLine.SETRANGE("Project Principal", "Project Principal");
      UnitPriceProductionMotLine.SETRANGE("Extension Contract No.","Extension Contract No.");
      UnitPriceProductionMotLine.SETRANGE("Unit Price","Unit Price");
      UnitPriceProductionMotLine.SETFILTER("Order No.",'>%1',"Order No.");
      IF UnitPriceProductionMotLine.FINDSET(TRUE,FALSE) THEN
        REPEAT
          UnitPriceProductionMotLine.VALIDATE(Production, UnitPriceProductionMotLine.Production + Production);
          UnitPriceProductionMotLine.MODIFY(TRUE);
        UNTIL UnitPriceProductionMotLine.NEXT = 0;
    END;

    PROCEDURE TestProjectStatus@1100525000();
    VAR
      JobRec@1100525000 : Record 11072003;
    BEGIN
      JobRec.GET("Project No.");
      IF (JobRec."Project Status" > JobRec."Project Status"::"Administrative Finished") THEN
        ERROR(Text003,JobRec.TABLECAPTION,"Project No.",JobRec.FIELDCAPTION("Project Status"),JobRec."Project Status");
    END;

    PROCEDURE CheckChangingAllowed@1100525001();
    VAR
      UnitPriceProductionMot@1100525000 : Record 11020425;
    BEGIN
      //C011505
      UnitPriceProductionMot.GET("Project No.", "Project Principal", "Production Date", Code);
      UnitPriceProductionMot.TESTFIELD(Invoiced, FALSE);
      UnitPriceProductionMot.TESTFIELD("Item Usage Posted", FALSE);
      IF (UnitPriceProductionMot.Status >= UnitPriceProductionMot.Status::Approved) THEN
        ERROR(Text002,UnitPriceProductionMot.FIELDCAPTION(Status),UnitPriceProductionMot.Status);
    END;

    PROCEDURE UpdateStatus@1100528500();
    VAR
      UnitPriceProductionMot@1100528500 : Record 11020425;
    BEGIN
      //C013394
      UnitPriceProductionMot.GET("Project No.", "Project Principal", "Production Date", Code);
      Status := UnitPriceProductionMot.Status;
    END;

    PROCEDURE CopyToProjectUnitPrices@1100528501(VAR UnitPriceProductionMotLine@1100528500 : Record 11020426);
    VAR
      ProjectUnitPrice@1100485000 : Record 11020427;
      UnitPriceProductionMotLine2@1100485001 : Record 11020426;
      NextLineNo@1100485002 : Integer;
    BEGIN
      //DP00659
      UnitPriceProductionMotLine2.COPY(UnitPriceProductionMotLine);
      UnitPriceProductionMotLine2.SETRANGE("Project Unit Price Line No.",0);
      IF UnitPriceProductionMotLine2.FINDSET(TRUE,FALSE) THEN BEGIN
        ProjectUnitPrice.SETRANGE("Project No.",UnitPriceProductionMotLine2."Project No.");
        IF ProjectUnitPrice.FINDLAST THEN
          NextLineNo := ProjectUnitPrice."Line No." + 10000
        ELSE
          NextLineNo := 10000;
        REPEAT
          UnitPriceProductionMotLine2.CALCFIELDS("Customer No.");

          ProjectUnitPrice.INIT;
          ProjectUnitPrice."Project No." := UnitPriceProductionMotLine2."Project No.";
          ProjectUnitPrice."Line No." := NextLineNo;
          ProjectUnitPrice."Customer No." := UnitPriceProductionMotLine2."Customer No.";
          ProjectUnitPrice."Unit Price" := UnitPriceProductionMotLine2."Unit Price";
          ProjectUnitPrice."Price List" := UnitPriceProductionMotLine2."Price List";
          ProjectUnitPrice.Description := UnitPriceProductionMotLine2."Description 3";
          ProjectUnitPrice.Quantity := UnitPriceProductionMotLine2.Production;
          ProjectUnitPrice.Element := UnitPriceProductionMotLine2.Element;
          ProjectUnitPrice."Extension Contract No." := UnitPriceProductionMotLine2."Extension Contract No.";
          ProjectUnitPrice."Sales Discount %" := UnitPriceProductionMotLine2."Sales Discount %";
          ProjectUnitPrice."Sales Surcharge %" := UnitPriceProductionMotLine2."Sales Surcharge %";
          ProjectUnitPrice.INSERT;
          ProjectUnitPrice.DoCalcFields; //This updates RefDateFilter
          ProjectUnitPrice.MODIFY(TRUE);

          UnitPriceProductionMotLine2."Project Unit Price Line No." := ProjectUnitPrice."Line No.";
          UnitPriceProductionMotLine2.MODIFY(TRUE);

          NextLineNo := NextLineNo + 10000;
        UNTIL UnitPriceProductionMotLine2.NEXT = 0;
      END;
    END;

    PROCEDURE CalculatedSalesPrice@1100485008() : Decimal;
    VAR
      Discount@1100528500 : Decimal;
      Surcharge@1100528501 : Decimal;
    BEGIN
      //DP00659
      Discount := "Sales Discount %"/100 * "Sales Price";
      Surcharge := "Sales Surcharge %"/100 * "Sales Price";
      EXIT("Sales Price" - Discount + Surcharge);
    END;

    BEGIN
    {
      4PS01 JD 13 oct 2008, Call 12962 Table ref. of field Code not correct.
      4PS02 JD 16 feb 2009, Call 13700 One unit price several times on the same motivation (also some fixing in the calculation of the
                                        cumulative values)
      4PS, 24-12-2013, C011505, Production values are set to zero when status becomes expired.
      4PS, 27-02-2014, FlowField quantity and function CheckProgess changed, Total Production Value corrected
      4PS, 28-02-2014, ProgressCheck for all Principals.
    }
    END.
  }
}

