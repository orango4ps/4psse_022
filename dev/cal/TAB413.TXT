OBJECT Table 413 IC Partner
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    Permissions=TableData 17=rm;
    OnDelete=VAR
               GLEntry@1007 : Record 17;
               GLSetup@1006 : Record 98;
               AccountingPeriod@1004 : Record 50;
               Cust@1003 : Record 18;
               Vend@1002 : Record 23;
               ICInbox@1001 : Record 418;
               ICOutbox@1000 : Record 414;
             BEGIN
               GLEntry.SETRANGE("IC Partner Code",Code);
               AccountingPeriod.SETRANGE(Closed,FALSE);
               IF AccountingPeriod.FINDFIRST THEN
                 GLEntry.SETFILTER("Posting Date",'>=%1',AccountingPeriod."Starting Date");
               IF NOT GLEntry.ISEMPTY THEN
                 ERROR(Text000,xRec.Code);

               GLSetup.GET;
               IF GLSetup."Allow G/L Acc. Deletion Before" <> 0D THEN BEGIN
                 GLEntry.SETFILTER("Posting Date",'>=%1',GLSetup."Allow G/L Acc. Deletion Before");
                 IF NOT GLEntry.ISEMPTY THEN
                   ERROR(Text001,Code,GLSetup."Allow G/L Acc. Deletion Before");
               END;

               IF "Customer No." <> '' THEN
                 IF Cust.GET("Customer No.") THEN
                   ERROR(Text002,Code,Cust.TABLECAPTION,Cust."No.");

               IF "Vendor No." <> '' THEN
                 IF Vend.GET("Vendor No.") THEN
                   ERROR(Text002,Code,Vend.TABLECAPTION,Vend."No.");

               ICInbox.SETRANGE("IC Partner Code",Code);
               IF NOT ICInbox.ISEMPTY THEN
                 ERROR(Text003,Code,ICInbox.TABLECAPTION);

               ICOutbox.SETRANGE("IC Partner Code",Code);
               IF NOT ICOutbox.ISEMPTY THEN
                 ERROR(Text003,Code,ICOutbox.TABLECAPTION);

               GLEntry.RESET;
               GLEntry.SETCURRENTKEY("IC Partner Code");
               GLEntry.SETRANGE("IC Partner Code",Code);
               GLEntry.MODIFYALL("IC Partner Code",'');

               CommentLine.SETRANGE("Table Name",CommentLine."Table Name"::"IC Partner");
               CommentLine.SETRANGE("No.",Code);
               CommentLine.DELETEALL;

               DimMgt.DeleteDefaultDim(DATABASE::"IC Partner",Code);
             END;

    OnRename=BEGIN
               DimMgt.RenameDefaultDim(DATABASE::"IC Partner",xRec.Code,Code);
             END;

    CaptionML=[ENU=IC Partner;
               NOR=KI-partner;
               SVE=Konc.int. partner];
    LookupPageID=Page608;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Text100       ;CaptionML=[ENU=Name;
                                                              NOR=Navn;
                                                              SVE=Namn] }
    { 3   ;   ;Currency Code       ;Code10        ;TableRelation=Currency.Code;
                                                   CaptionML=[ENU=Currency Code;
                                                              NOR=Valutakode;
                                                              SVE=Valutakod] }
    { 4   ;   ;Inbox Type          ;Option        ;InitValue=Database;
                                                   OnValidate=VAR
                                                                Cust@1000 : Record 18;
                                                                Vend@1001 : Record 23;
                                                              BEGIN
                                                                IF "Inbox Type" <> xRec."Inbox Type" THEN
                                                                  "Inbox Details" := '';
                                                                IF "Inbox Type" = "Inbox Type"::Email THEN BEGIN
                                                                  IF "Customer No." <> '' THEN BEGIN
                                                                    IF Cust.GET("Customer No.") THEN
                                                                      "Inbox Details" := Cust."E-Mail";
                                                                  END ELSE
                                                                    IF "Vendor No." <> '' THEN
                                                                      IF Vend.GET("Vendor No.") THEN
                                                                        "Inbox Details" := Vend."E-Mail";
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Inbox Type;
                                                              NOR=Innbokstype;
                                                              SVE=Inkorgstyp];
                                                   OptionCaptionML=[ENU=File Location,Database,Email,No IC Transfer;
                                                                    NOR=Fillokasjon,Database,E-post,Ingen KI-overf›ring;
                                                                    SVE=Filplats,Databas,E-post,Ingen konc.int. ”verf”ring];
                                                   OptionString=File Location,Database,Email,No IC Transfer }
    { 5   ;   ;Inbox Details       ;Text250       ;TableRelation=IF (Inbox Type=CONST(Database)) Company.Name;
                                                   OnLookup=VAR
                                                              Company@1000 : Record 2000000006;
                                                              FileMgt@1006 : Codeunit 419;
                                                              Companies@1001 : Page 357;
                                                              FileName@1004 : Text;
                                                              FileName2@1005 : Text;
                                                              Path@1003 : Text;
                                                            BEGIN
                                                              CASE "Inbox Type" OF
                                                                "Inbox Type"::Database:
                                                                  BEGIN
                                                                    Company.SETFILTER(Name,'<>%1',COMPANYNAME);
                                                                    Companies.SETTABLEVIEW(Company);
                                                                    Companies.LOOKUPMODE := TRUE;
                                                                    IF Companies.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                      Companies.GETRECORD(Company);
                                                                      "Inbox Details" := Company.Name;
                                                                    END;
                                                                  END;
                                                                "Inbox Type"::"File Location":
                                                                  BEGIN
                                                                    IF "Inbox Details" = '' THEN
                                                                      FileName := STRSUBSTNO('%1.xml',Code)
                                                                    ELSE
                                                                      FileName := "Inbox Details" + STRSUBSTNO('\%1.xml',Code);

                                                                    FileName2 := FileMgt.SaveFileDialog(Text005,FileName,'');
                                                                    IF FileName <> FileName2 THEN BEGIN
                                                                      Path := FileMgt.GetDirectoryName(FileName2);
                                                                      IF Path <> '' THEN
                                                                        "Inbox Details" := COPYSTR(Path,1,250);
                                                                    END;
                                                                  END;
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Inbox Details;
                                                              NOR=Innboksdetaljer;
                                                              SVE=Inkorgsinformation] }
    { 6   ;   ;Receivables Account ;Code20        ;TableRelation="G/L Account".No.;
                                                   CaptionML=[ENU=Receivables Account;
                                                              NOR=Samlekonto (kunde);
                                                              SVE=Kundfordringar] }
    { 7   ;   ;Payables Account    ;Code20        ;TableRelation="G/L Account".No.;
                                                   CaptionML=[ENU=Payables Account;
                                                              NOR=Samlekonto (leverand›r);
                                                              SVE=Leverant”rsskulder] }
    { 10  ;   ;Blocked             ;Boolean       ;CaptionML=[ENU=Blocked;
                                                              NOR=Sperret;
                                                              SVE=Sp„rrad] }
    { 11  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Comment Line" WHERE (Table Name=CONST(IC Partner),
                                                                                           No.=FIELD(Code)));
                                                   CaptionML=[ENU=Comment;
                                                              NOR=Merknad;
                                                              SVE=Kommentar];
                                                   Editable=No }
    { 12  ;   ;Customer No.        ;Code20        ;TableRelation=Customer;
                                                   CaptionML=[ENU=Customer No.;
                                                              NOR=Kundenr.;
                                                              SVE=Kundnr] }
    { 13  ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor;
                                                   CaptionML=[ENU=Vendor No.;
                                                              NOR=Leverand›rnr.;
                                                              SVE=Leverant”rsnr] }
    { 14  ;   ;Outbound Sales Item No. Type;Option;CaptionML=[ENU=Outbound Sales Item No. Type;
                                                              NOR=Utg†ende salgsvarenummertype;
                                                              SVE=Avg†ende f”rs.artikelnrtyp];
                                                   OptionCaptionML=[ENU=Internal No.,Common Item No.,Cross Reference;
                                                                    NOR=Internt nr.,Felles varenr.,Kryssreferanse;
                                                                    SVE=Internt nr,Gemensamt artikelnr,Korsreferens];
                                                   OptionString=Internal No.,Common Item No.,Cross Reference }
    { 15  ;   ;Outbound Purch. Item No. Type;Option;
                                                   CaptionML=[ENU=Outbound Purch. Item No. Type;
                                                              NOR=Utg†ende kj›psvarenummertype;
                                                              SVE=Avg†ende ink”psartikelnrtyp];
                                                   OptionCaptionML=[ENU=Internal No.,Common Item No.,Cross Reference,Vendor Item No.;
                                                                    NOR=Internt nr.,Felles varenr.,Kryssreferanse,Leverand›rs varenr.;
                                                                    SVE=Internt nr,Gemensamt artikelnr,Korsreferens,Leverant”rsartikelnr];
                                                   OptionString=Internal No.,Common Item No.,Cross Reference,Vendor Item No. }
    { 16  ;   ;Cost Distribution in LCY;Boolean   ;CaptionML=[ENU=Cost Distribution in LCY;
                                                              NOR=Kostfordeling i NOK;
                                                              SVE=Kostnadsf”rdelning i BVA] }
    { 17  ;   ;Auto. Accept Transactions;Boolean  ;CaptionML=[ENU=Auto. Accept Transactions;
                                                              NOR=Godta transaksjoner automatisk;
                                                              SVE=Automatiskt. Acceptera transaktioner] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      CommentLine@1001 : Record 97;
      Text000@1005 : TextConst 'ENU=You cannot delete IC Partner %1 because it has ledger entries in a fiscal year that has not been closed yet.;NOR=Du kan ikke slette KI-partneren %1 fordi den har poster i et regnskaps†r som ikke er avsluttet enn†.;SVE=Du kan inte ta bort konc.int. partner %1 eftersom det finns transaktioner i ett r„kenskaps†r som inte har st„ngts „nnu.';
      Text001@1004 : TextConst 'ENU=You cannot delete IC Partner %1 because it has ledger entries after %2.;NOR=Du kan ikke slette KI-partneren %1 fordi den har poster etter %2.;SVE=Du kan inte ta bort konc.int. partner %1 eftersom det finns transaktioner efter %2.';
      Text002@1003 : TextConst 'ENU=You cannot delete IC Partner %1 because it is used for %2 %3;NOR=Du kan ikke slette KI-partneren %1 fordi den brukes i %2 %3;SVE=Du kan inte ta bort konc.int. partner %1 eftersom den anv„nds f”r %2 %3';
      Text003@1002 : TextConst 'ENU=You cannot delete IC Partner %1 because it is used in %2;NOR=Du kan ikke slette KI-partneren %1 fordi den brukes i %2;SVE=Du kan inte ta bort konc.int. partner %1 eftersom den anv„nds i %2';
      Text004@1006 : TextConst 'ENU=%1 %2 is linked to a blocked IC Partner.;NOR=%1 %2 er koblet til en sperret KI-partner.;SVE=%1 %2 „r kopplad till en sp„rrad konc.int. partner.';
      Text005@1000 : TextConst 'ENU=File Location for IC files;NOR=Fillokasjon for KI-filer;SVE=Filplats f”r konc.int. filer';
      DimMgt@1007 : Codeunit 408;

    [External]
    PROCEDURE CheckICPartner@1();
    BEGIN
      TESTFIELD(Blocked,FALSE);
    END;

    [External]
    PROCEDURE CheckICPartnerIndirect@2(AccountType@1000 : Text[250];AccountNo@1001 : Code[20]);
    BEGIN
      IF Blocked THEN
        ERROR(Text004,AccountType,AccountNo);
    END;

    [External]
    PROCEDURE PropagateCustomerICPartner@25(PreviousCustomerNo@1000 : Code[20];NewCustomerNo@1001 : Code[20];ICPartnerCode@1002 : Code[20]);
    VAR
      Customer@1003 : Record 18;
    BEGIN
      IF (PreviousCustomerNo <> NewCustomerNo) AND Customer.GET(PreviousCustomerNo) THEN BEGIN
        Customer.VALIDATE("IC Partner Code",'');
        Customer.MODIFY;
      END;

      IF Customer.GET(NewCustomerNo) THEN BEGIN
        Customer.VALIDATE("IC Partner Code",ICPartnerCode);
        Customer.MODIFY;
      END;
    END;

    [External]
    PROCEDURE PropagateVendorICPartner@26(PreviousVendorNo@1002 : Code[20];NewVendorNo@1001 : Code[20];ICPartnerCode@1000 : Code[20]);
    VAR
      Vendor@1003 : Record 23;
    BEGIN
      IF (PreviousVendorNo <> NewVendorNo) AND Vendor.GET(PreviousVendorNo) THEN BEGIN
        Vendor.VALIDATE("IC Partner Code",'');
        Vendor.MODIFY;
      END;

      IF Vendor.GET(NewVendorNo) THEN BEGIN
        Vendor.VALIDATE("IC Partner Code",ICPartnerCode);
        Vendor.MODIFY;
      END;
    END;

    BEGIN
    END.
  }
}

