OBJECT Table 11071699 SC Budget (Calls) Release
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ServiceContract@1100528600 : Record 11012812;
             BEGIN
               TESTFIELD(Code);

               IF Type = Type::Contract THEN
                 IF ServiceContract.GET(Code) THEN
                   ServiceContract.TESTFIELD("Budget Based on", ServiceContract."Budget Based on"::"Maintenance Scheme");
             END;

    CaptionML=[ENU=Service Contract Budget (Calls) Release;
               NOR=Servicekontraktbudsjett (samtaler), frigi;
               SVE=Sl„pp servicekontraktsbudget (samtal)];
  }
  FIELDS
  {
    { 5   ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Type;
                                                              SVE=Typ];
                                                   OptionCaptionML=[ENU=Contract Group,Contract;
                                                                    NOR=Kontraktgruppe,Kontrakt;
                                                                    SVE=Kontraktsgrupp,kontrakt];
                                                   OptionString=Contract Group,Contract;
                                                   Editable=No }
    { 10  ;   ;Code                ;Code20        ;TableRelation=IF (Type=CONST(Contract Group)) "Service Contract Group".Code
                                                                 ELSE IF (Type=CONST(Contract)) Service-Contract.No.;
                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   Editable=No }
    { 30  ;   ;Month               ;Option        ;CaptionML=[ENU=Month;
                                                              NOR=M†ned;
                                                              SVE=M†nad];
                                                   OptionCaptionML=[ENU=January,Februari,March,April,May,June,July,August,September,October,November,December;
                                                                    NOR=Januar,Februar,Mars,April,Mai,Juni,Juli,August,September,Oktober,November,Desember;
                                                                    SVE=Januari,Februari,Mars,April,Maj,Juni,Juli,Augusti,September,Oktober,November,December];
                                                   OptionString=January,Februari,March,April,May,June,July,August,September,October,November,December }
    { 140 ;   ;Release in %        ;Decimal       ;CaptionML=[ENU=Release in %;
                                                              NOR=Frigi i prosent;
                                                              SVE=Sl„pp i procent] }
    { 150 ;   ;Total Release in %  ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("SC Budget (Calls) Release"."Release in %" WHERE (Type=FIELD(Type),
                                                                                                                     Code=FIELD(Code)));
                                                   CaptionML=[ENU=Total Release in %;
                                                              NOR=Total frigivelse i %;
                                                              SVE=Total utgivning i procent];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Type,Code,Month                         ;SumIndexFields=Release in %;
                                                   MaintainSIFTIndex=No;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528600 : TextConst 'ENU=Total of %1 is not 100%;NOR=Totalen til %1 er ikke 100%;SVE=Summan av %1 „r inte 100 %';

    PROCEDURE CalcTotalReleasePerc@1100526202(IContractNo@1100528604 : Code[20];IFromDate@1100528601 : Date;IToDate@1100528602 : Date;IToDatePeriod@1100528611 : Date) ReleasePerc : Decimal;
    VAR
      DateRec@1100528600 : Record 2000000007;
      SCBudgetCallsRelease@1100528603 : Record 11071699;
      ServiceContract@1100528609 : Record 11012812;
      GetMaintenanceSalesAndCost@1100528610 : Codeunit 11012827;
      NoOfDaysMonth@1100528608 : Integer;
      Day@1100528605 : Integer;
      Month@1100528606 : Integer;
    BEGIN
      IF IToDate > IToDatePeriod THEN
        IToDate := IToDatePeriod;
      IF IToDate < IFromDate THEN
        EXIT;

      SCBudgetCallsRelease.SETRANGE(Type, SCBudgetCallsRelease.Type::Contract);
      SCBudgetCallsRelease.SETRANGE(Code, IContractNo);
      IF SCBudgetCallsRelease.ISEMPTY THEN BEGIN
        IF NOT ServiceContract.GET(IContractNo) THEN
          EXIT;
        SCBudgetCallsRelease.SETRANGE(Type, SCBudgetCallsRelease.Type::"Contract Group");
        SCBudgetCallsRelease.SETRANGE(Code, ServiceContract."Contract Group");
      END;

      DateRec.SETRANGE("Period Type", DateRec."Period Type"::Month);
      Day := DATE2DMY(IFromDate, 1);
      IF Day > 1 THEN BEGIN
        DateRec.SETRANGE("Period Start", 0D, IFromDate);
        IF DateRec.FINDLAST THEN BEGIN
          NoOfDaysMonth := DATE2DMY(NORMALDATE(DateRec."Period End"), 1);
          SCBudgetCallsRelease.SETRANGE(Month, DateRec."Period No." -1);
          IF SCBudgetCallsRelease.FINDFIRST THEN
            ReleasePerc := SCBudgetCallsRelease."Release in %" * (NoOfDaysMonth - Day +1) / NoOfDaysMonth;
        END;
      END;

      DateRec.SETRANGE("Period Start", IFromDate, IToDate);
      IF DateRec.FINDSET THEN
        REPEAT
          NoOfDaysMonth := DATE2DMY(DateRec."Period End", 1);
          IF (DATE2DMY(IToDate, 2) = DateRec."Period No.") AND
            (DATE2DMY(IToDate, 3) = DATE2DMY(DateRec."Period Start", 3)) THEN
            Day := DATE2DMY(IToDate, 1)
          ELSE
            Day := NoOfDaysMonth;
          SCBudgetCallsRelease.SETRANGE(Month, DateRec."Period No." -1);
          IF SCBudgetCallsRelease.FINDFIRST THEN
            ReleasePerc += SCBudgetCallsRelease."Release in %" * Day / NoOfDaysMonth;
        UNTIL DateRec.NEXT = 0;

      IF ServiceContract.GET(IContractNo) THEN;
      ReleasePerc *= GetMaintenanceSalesAndCost.NoOfDaysPerYear(IFromDate) / (GetMaintenanceSalesAndCost.NoOfDaysInterval(ServiceContract, IFromDate, IToDatePeriod));
    END;

    PROCEDURE Check100Percent@1100528600(VAR ISCBudgetCallsRelease@1100528600 : Record 11071699);
    VAR
      SCBudgetCallsRelease@1100528601 : Record 11071699;
    BEGIN
      SCBudgetCallsRelease.COPY(ISCBudgetCallsRelease);
      SCBudgetCallsRelease.CALCSUMS("Release in %");
      IF SCBudgetCallsRelease."Release in %" <> 100 THEN
        MESSAGE(Text000, FIELDCAPTION("Release in %"));
    END;

    BEGIN
    END.
  }
}

