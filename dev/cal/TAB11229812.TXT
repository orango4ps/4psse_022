OBJECT Table 11229812 Resp Pers Perm (PPT)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnModify=BEGIN
               IF (NOT "High Security Subsite") AND (Type = Type::Access) THEN
                 "Document Type" := '';
               CheckPermissions;
             END;

    CaptionML=ENU=Project Portal Resp Pers Perm;
  }
  FIELDS
  {
    { 3   ;   ;Subsite Type        ;Option        ;CaptionML=ENU=Subsite Type;
                                                   OptionCaptionML=ENU=Project,Service Location;
                                                   OptionString=Project,ServLoc;
                                                   Editable=No }
    { 5   ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=ENU=,Access,Filter;
                                                   OptionString=,Access,Field Filter;
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 10  ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projekt- nr];
                                                   NotBlank=Yes }
    { 20  ;   ;Line No.            ;Integer       ;TableRelation="Project Responsible Person"."Line No." WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr];
                                                   NotBlank=Yes }
    { 21  ;   ;Responsibility      ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Responsible Person".Responsibility WHERE (Project No.=FIELD(Project No.),
                                                                                                                         Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=Responsibility;
                                                              NOR=Ansvar;
                                                              SVE=Ansvar];
                                                   Editable=No }
    { 22  ;   ;Responsibility Description;Text100 ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Responsibility.Description WHERE (Code=FIELD(Responsibility)));
                                                   CaptionML=[ENU=Responsibility Description;
                                                              NOR=Ansvar, Beskrivelse;
                                                              SVE=Ansvar, beskrivning];
                                                   Editable=No }
    { 23  ;   ;Employee No.        ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Responsible Person"."Employee No." WHERE (Project No.=FIELD(Project No.),
                                                                                                                         Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=Employee No.;
                                                              NOR=Ansattnr;
                                                              SVE=Anst„llningsnr];
                                                   Editable=No }
    { 24  ;   ;Employee Name       ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Full Name" WHERE (No.=FIELD(Employee No.)));
                                                   CaptionML=[ENU=Employee Name;
                                                              NOR=Navn p† ansatt;
                                                              SVE=Namn p† anst„lld];
                                                   Editable=No }
    { 30  ;   ;Project Portal Subsite;Code20      ;TableRelation="Project Portal Subsite".Code WHERE (Quote Request Subsite=CONST(No),
                                                                                                      Type=FIELD(Subsite Type));
                                                   CaptionML=ENU=Document Portal Subsite }
    { 31  ;   ;Project Portal Subsite Descr;Text30;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Portal Subsite".Description WHERE (Code=FIELD(Project Portal Subsite)));
                                                   CaptionML=ENU=Document Portal Subsite Description;
                                                   Editable=No }
    { 32  ;   ;High Security Subsite;Boolean      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Portal Subsite"."High Security Subsite" WHERE (Code=FIELD(Project Portal Subsite)));
                                                   CaptionML=ENU=High Security Subsite;
                                                   Editable=No }
    { 33  ;   ;Quote Request Subsite;Boolean      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Portal Subsite"."Quote Request Subsite" WHERE (Code=FIELD(Project Portal Subsite)));
                                                   CaptionML=ENU=Quote Request Subsite;
                                                   Editable=No }
    { 40  ;   ;Document Type       ;Code20        ;TableRelation=IF (Type=CONST(Access),
                                                                     High Security Subsite=CONST(Yes)) "Subsite-Doc Type (PPT)"."Document Type" WHERE (Project Portal Subsite=FIELD(Project Portal Subsite))
                                                                     ELSE IF (Type=CONST(Field Filter)) "Subsite-Doc Type (PPT)"."Document Type" WHERE (Project Portal Subsite=FIELD(Project Portal Subsite));
                                                   CaptionML=[ENU=Document Type;
                                                              NOR=Dokumenttype;
                                                              SVE=Dokumenttyp] }
    { 41  ;   ;Document Type Description;Text30   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Document Type".Description WHERE (Type=FIELD(Document Type)));
                                                   CaptionML=[ENU=Document Type Description;
                                                              NOR=Dokumenttype, Beskrivelse;
                                                              SVE=Dokumenttyp, beskrivning];
                                                   Editable=No }
    { 50  ;   ;Project Element     ;Code20        ;TableRelation="Project Element".Element WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=[ENU=Project Element;
                                                              NOR=Prosjektelement;
                                                              SVE=Projektelement] }
    { 51  ;   ;Project Element Description;Text50 ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Project Element".Description WHERE (Project No.=FIELD(Project No.),
                                                                                                           Element=FIELD(Project Element)));
                                                   CaptionML=ENU=Project Element Description;
                                                   Editable=No }
    { 60  ;   ;Permission Level    ;Option        ;OnValidate=BEGIN
                                                                CheckPermissions;
                                                                IF ("Permission Level" < "Private Permission Level") THEN
                                                                  "Private Permission Level" := "Permission Level";
                                                                IF ("Permission Level" = "Permission Level"::Owner) AND ("Project Portal Subsite" <> '') AND
                                                                   ("Document Type" = '')
                                                                THEN
                                                                  "Private Permission Level" := "Permission Level";
                                                              END;

                                                   CaptionML=ENU=Permission Level;
                                                   OptionCaptionML=ENU=None,Visitor,Member,Owner;
                                                   OptionString=None,Visitor,Member,Owner }
    { 70  ;   ;Show in View        ;Boolean       ;CaptionML=ENU=Show in View }
    { 80  ;   ;Private Permission Level;Option    ;OnValidate=BEGIN
                                                                TESTFIELD("Project Portal Subsite");
                                                                TESTFIELD("Document Type", '');
                                                                IF ("Permission Level" = "Permission Level"::Owner) THEN
                                                                  "Private Permission Level" := "Permission Level";
                                                                IF ("Permission Level" < "Private Permission Level") THEN
                                                                  FIELDERROR("Private Permission Level");
                                                              END;

                                                   CaptionML=ENU=Private Permission Level;
                                                   OptionCaptionML=ENU=None,Visitor,Member,Owner;
                                                   OptionString=None,Visitor,Member,Owner }
  }
  KEYS
  {
    {    ;Subsite Type,Type,Project No.,Line No.,Project Portal Subsite,Document Type,Project Element;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100409000 : TextConst 'ENU=You have no permission to change Portal Permission Records.';

    PROCEDURE AddProjectPortalPermissions@1100528301();
    VAR
      ProjectResponsiblePerson@1100528300 : Record 11012040;
    BEGIN
      ProjectResponsiblePerson."Project No." := "Project No.";
      ProjectResponsiblePerson.AddProjectPortalPermissions;
    END;

    PROCEDURE UpdateProjectPortal@1100528302();
    VAR
      ProjectResponsiblePerson@1100528300 : Record 11012040;
    BEGIN
      ProjectResponsiblePerson."Project No." := "Project No.";
      ProjectResponsiblePerson.UpdateProjectPortal;
    END;

    PROCEDURE CheckPermissions@1100409007();
    BEGIN
      CheckModifyPermission;
      CheckQuoteSitePermissionLevel;
      CheckPermissionLevel;
    END;

    LOCAL PROCEDURE CheckPermissionLevel@1100528303();
    VAR
      RespPersPermPPT@1100528300 : Record 11229812;
    BEGIN
      IF (Type <> Type::Access) THEN
        EXIT;
      CALCFIELDS("High Security Subsite");
      IF (NOT "High Security Subsite") THEN
        EXIT;

      IF ("Document Type" = '') OR ("Permission Level" = "Permission Level"::Owner) THEN BEGIN
        RespPersPermPPT.SETRANGE("Subsite Type", "Subsite Type");
        RespPersPermPPT.SETRANGE(Type, Type);
        RespPersPermPPT.SETRANGE("Project No.", "Project No.");
        RespPersPermPPT.SETRANGE("Line No.", "Line No.");
        RespPersPermPPT.SETRANGE("Project Portal Subsite", "Project Portal Subsite");
        RespPersPermPPT.SETFILTER("Document Type", '<>%1', "Document Type");
        RespPersPermPPT.MODIFYALL("Permission Level", "Permission Level");
      END ELSE BEGIN
        RespPersPermPPT.GET("Subsite Type", Type, "Project No.", "Line No.", "Project Portal Subsite", '', '');
        IF (RespPersPermPPT."Permission Level" = RespPersPermPPT."Permission Level"::Owner) THEN BEGIN
          "Permission Level" := "Permission Level"::Owner;
        END;
        IF (RespPersPermPPT."Permission Level" < "Permission Level") THEN BEGIN
          RespPersPermPPT."Permission Level" := "Permission Level";
          RespPersPermPPT.MODIFY;
        END;
      END;
    END;

    PROCEDURE CheckModifyPermission@1100409000();
    VAR
      UserSetup@1100409000 : Record 91;
      ProjectResponsiblePerson@1100409001 : Record 11012040;
      RespPersPermPPT@1100409002 : Record 11229812;
    BEGIN
      IF (NOT UserSetup.GET(USERID)) THEN
        ERROR(Text000);
      UserSetup.TESTFIELD("Employee No.");
      ProjectResponsiblePerson.SETRANGE("Project No.", "Project No.");
      ProjectResponsiblePerson.SETRANGE("Employee No.", UserSetup."Employee No.");
      IF (NOT ProjectResponsiblePerson.FINDSET) THEN
        ERROR(Text000);
      REPEAT
        RespPersPermPPT.SETRANGE("Subsite Type", 0); //Project
        RespPersPermPPT.SETRANGE(Type, RespPersPermPPT.Type::Access);
        RespPersPermPPT.SETRANGE("Project No.", ProjectResponsiblePerson."Project No.");
        RespPersPermPPT.SETRANGE("Line No.", ProjectResponsiblePerson."Line No.");
        RespPersPermPPT.SETFILTER("Employee No.", '<>%1', ProjectResponsiblePerson."Employee No.");
        RespPersPermPPT.SETFILTER("Permission Level", '<>%1', RespPersPermPPT."Permission Level"::Owner);
        IF (RespPersPermPPT.FINDFIRST) THEN
          ERROR(Text000);
      UNTIL (ProjectResponsiblePerson.NEXT = 0);
    END;

    LOCAL PROCEDURE CheckQuoteSitePermissionLevel@1100409001();
    BEGIN
      CALCFIELDS("Quote Request Subsite");
      IF (NOT "Quote Request Subsite") THEN
        EXIT;
      IF ("Permission Level" = "Permission Level"::Member) THEN
        FIELDERROR("Permission Level");
    END;

    PROCEDURE MakePrivatePermEqualToPerm@1100529900();
    VAR
      RespPersPermPPT@1100529900 : Record 11229812;
    BEGIN
      RespPersPermPPT.SETRANGE("Subsite Type", "Subsite Type"::Project);
      RespPersPermPPT.SETRANGE(Type, Type::Access);
      RespPersPermPPT.SETRANGE("Project No.", "Project No.");
      RespPersPermPPT.SETFILTER("Project Portal Subsite", '%1', '');
      RespPersPermPPT.MODIFYALL("Private Permission Level", "Private Permission Level"::None, FALSE);

      RespPersPermPPT.SETFILTER("Project Portal Subsite", '<>%1', '');
      RespPersPermPPT.SETFILTER("Document Type", '<>%1', '');
      RespPersPermPPT.MODIFYALL("Private Permission Level", "Private Permission Level"::None, FALSE);

      RespPersPermPPT.SETFILTER("Document Type", '%1', '');
      IF (NOT RespPersPermPPT.FINDSET) THEN
        EXIT;
      REPEAT
        RespPersPermPPT."Private Permission Level" := RespPersPermPPT."Permission Level";
        RespPersPermPPT.MODIFY;
      UNTIL (RespPersPermPPT.NEXT = 0);
    END;

    BEGIN
    END.
  }
}

