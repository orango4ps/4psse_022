OBJECT Table 11012028 Payment Agreement
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF GETFILTER("Sub Contractor") <> '' THEN
                 IF GETRANGEMIN("Sub Contractor") = GETRANGEMAX("Sub Contractor") THEN BEGIN
                   SubContr := GETRANGEMIN("Sub Contractor");
                   IF VendorRec.GET(SubContr) THEN BEGIN
                     GiveMessage := TRUE;  // 31123.n
                     VALIDATE("Sub Contractor", SubContr); //When called from Vendor Card Validation must be forced.
                   END;
                 END;

               InsertCentralManagement();
             END;

    OnModify=BEGIN
               CentrDataMgt.EditPaymentAgreement(Rec,2,Rec);
             END;

    OnDelete=BEGIN
               CentrDataMgt.EditPaymentAgreement(Rec,4,Rec);
             END;

    CaptionML=[ENU=Payment Agreement;
               NOR=Betalingsvilk†r;
               SVE=Betalningsvillkor];
    LookupPageID=Page11012059;
  }
  FIELDS
  {
    { 10  ;   ;Code                ;Code20        ;OnValidate=BEGIN
                                                                OldPaymentAgreement := xRec;
                                                                IF OldPaymentAgreement.FIND THEN
                                                                  CentrDataMgt.EditPaymentAgreement(Rec,3,xRec);
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 11  ;   ;Date                ;Date          ;CaptionML=[ENU=Date;
                                                              NOR=Dato;
                                                              SVE=Datum] }
    { 20  ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 30  ;   ;Sub Contractor      ;Code20        ;TableRelation=Vendor.No. WHERE (Subcontractor Type=FILTER(Subcontractor|Self Employed|Employment Agency));
                                                   OnValidate=BEGIN
                                                                IF VendorRec.GET("Sub Contractor") THEN BEGIN
                                                                  VendorRec.CheckBlockedVendOnDocs(VendorRec,FALSE);
                                                                  IF VendorRec."Security Fund" THEN
                                                                    VALIDATE("Contribution Percentage Labor",0);

                                                                  IF GiveMessage THEN BEGIN // 31123.n
                                                                    WKACheck.RESET;
                                                                    WKACheck.SETRANGE(Mandatory, TRUE);
                                                                    WKACheck.SetWKAVendorFilters(VendorRec);
                                                                    IF WKACheck.FINDSET(FALSE, FALSE) THEN
                                                                      REPEAT
                                                                        IF NOT VendorWKACheck.GET(VendorRec."No.", WKACheck.Code) THEN
                                                                          MESSAGE(Text000, WKACheck.Description, VendorRec."No.")
                                                                        ELSE
                                                                          IF NOT VendorWKACheck.DataIsValid(TODAY) THEN
                                                                            MESSAGE(Text000, WKACheck.Description, VendorRec."No.")
                                                                      UNTIL WKACheck.NEXT = 0;

                                                                    VendorWKACheck.RESET;
                                                                    VendorWKACheck.SETRANGE("Vendor No.", VendorRec."No.");
                                                                    VendorWKACheck.SETRANGE(Mandatory, FALSE);
                                                                    VendorWKACheck.SETRANGE("Extra Obligation Vendor", TRUE);
                                                                    IF VendorWKACheck.FINDSET(FALSE, FALSE) THEN
                                                                      REPEAT
                                                                        IF NOT VendorWKACheck.DataIsValid(TODAY) THEN BEGIN
                                                                          VendorWKACheck.CALCFIELDS(Description);
                                                                          MESSAGE(Text000, VendorWKACheck.Description, VendorRec."No.");
                                                                        END;
                                                                      UNTIL VendorWKACheck.NEXT = 0;
                                                                  END;  // 31123.n
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Sub Contractor;
                                                              NOR=Underentrepren›r;
                                                              SVE=Underentrepren”r] }
    { 40  ;   ;Expected Effective Date;Date       ;OnValidate=BEGIN
                                                                IF ("Expected Effective Date" > "Expected Enddate") AND ("Expected Enddate" <> 0D) THEN
                                                                  FIELDERROR("Expected Effective Date",STRSUBSTNO(Text007,FIELDCAPTION("Expected Enddate")));
                                                              END;

                                                   CaptionML=[ENU=Expected Effective Date;
                                                              NOR=Forventet ikrafttredelsesdato;
                                                              SVE=F”rv„ntat ikrafttr„dandedatum] }
    { 50  ;   ;Expected Enddate    ;Date          ;OnValidate=BEGIN
                                                                IF ("Expected Effective Date" > "Expected Enddate") AND ("Expected Enddate" <> 0D) THEN
                                                                  FIELDERROR("Expected Enddate",STRSUBSTNO(Text008,FIELDCAPTION("Expected Effective Date")));
                                                              END;

                                                   CaptionML=[ENU=Expected Enddate;
                                                              NOR=Forventet sluttdato;
                                                              SVE=F”rv„ntat slutdatum] }
    { 60  ;   ;Number of Hours     ;Decimal       ;OnValidate=BEGIN
                                                                WKACalculationCu.CalculateWageCharge("Number of Hours","Avg. Wage Rate",'',"Labor Amount");
                                                                VALIDATE("Pay To Tax Authority");
                                                              END;

                                                   CaptionML=[ENU=Number of Hours;
                                                              NOR=Antall timer;
                                                              SVE=Antal timmar];
                                                   MinValue=0 }
    { 70  ;   ;Avg. Wage Rate      ;Decimal       ;OnValidate=BEGIN
                                                                WKACalculationCu.CalculateWageCharge("Number of Hours","Avg. Wage Rate",'',"Labor Amount");
                                                                VALIDATE("Pay To Tax Authority");
                                                              END;

                                                   CaptionML=[ENU=Avg. Wage Rate;
                                                              NOR=Gjennomsnitlig l›nnssats;
                                                              SVE=Gen. L”neavgift];
                                                   MinValue=0;
                                                   AutoFormatType=2 }
    { 80  ;   ;Labor Amount        ;Decimal       ;OnValidate=BEGIN
                                                                WKACalculationCu.CalculateRate("Number of Hours","Labor Amount","Avg. Wage Rate");
                                                                VALIDATE("Pay To Tax Authority");
                                                              END;

                                                   CaptionML=[ENU=Labor Amount;
                                                              NOR=Arbeidskraftsbel›p;
                                                              SVE=Arbetskraftsbelopp];
                                                   MinValue=0;
                                                   AutoFormatType=1 }
    { 90  ;   ;Contribution Percentage Labor;Decimal;
                                                   OnValidate=BEGIN
                                                                IF (xRec."Contribution Percentage Labor" = 0) AND ("Contribution Percentage Labor" <> 0) THEN BEGIN
                                                                  "Contribution Percentage Total" := 0;
                                                                  "Fixed B Amount per Hour" := 0;
                                                                END;
                                                                VALIDATE("Pay To Tax Authority");
                                                              END;

                                                   OnLookup=VAR
                                                              ContBusRel@11012002 : Record 5054;
                                                              ContIndustGroup@11012000 : Record 5058;
                                                            BEGIN
                                                              ContBusRel.SETCURRENTKEY("Link to Table","No.");
                                                              ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Vendor);
                                                              ContBusRel.SETRANGE("No.", "Sub Contractor");
                                                              IF NOT ContBusRel.FIND('-') THEN
                                                                ERROR(Text009,ContBusRel.TABLECAPTION,FIELDCAPTION("Sub Contractor"),"Sub Contractor");

                                                              ContIndustGroup.SETRANGE("Contact No.",ContBusRel."Contact No.");
                                                              IF PAGE.RUNMODAL(PAGE::"Contact Industry Groups",ContIndustGroup) = ACTION::LookupOK THEN BEGIN
                                                                "Contribution Percentage Labor" := ContIndustGroup."Contribution Percentage Labor";
                                                                "Contribution Percentage Total" := ContIndustGroup."Contribution Percentage Total";
                                                                "Fixed B Amount per Hour" := ContIndustGroup."Fixed B Amount per Hour";
                                                                 VALIDATE("Contribution Percentage Labor");
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Contribution Percentage Labor;
                                                              NOR=Dekningsbidrag, arbeidskraft;
                                                              SVE=T„ckningsbidrag, arbetskraft];
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 91  ;   ;Contribution Percentage Total;Decimal;
                                                   OnValidate=BEGIN
                                                                IF (xRec."Contribution Percentage Total" = 0) AND ("Contribution Percentage Total" <> 0) THEN BEGIN
                                                                  "Contribution Percentage Labor" := 0;
                                                                  "Fixed B Amount per Hour" := 0;
                                                                END;
                                                                VALIDATE("Pay To Tax Authority");
                                                              END;

                                                   OnLookup=VAR
                                                              ContBusRel@11012002 : Record 5054;
                                                              ContIndustGroup@11012000 : Record 5058;
                                                            BEGIN
                                                              ContBusRel.SETCURRENTKEY("Link to Table","No.");
                                                              ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Vendor);
                                                              ContBusRel.SETRANGE("No.", "Sub Contractor");
                                                              IF NOT ContBusRel.FIND('-') THEN
                                                                ERROR(Text009,ContBusRel.TABLECAPTION,FIELDCAPTION("Sub Contractor"),"Sub Contractor");

                                                              ContIndustGroup.SETRANGE("Contact No.",ContBusRel."Contact No.");
                                                              IF PAGE.RUNMODAL(PAGE::"Contact Industry Groups",ContIndustGroup) = ACTION::LookupOK THEN BEGIN
                                                                "Contribution Percentage Total" := ContIndustGroup."Contribution Percentage Total";
                                                                "Contribution Percentage Labor" := ContIndustGroup."Contribution Percentage Labor";
                                                                "Fixed B Amount per Hour" := ContIndustGroup."Fixed B Amount per Hour";
                                                                 VALIDATE("Contribution Percentage Total");
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Contribution Percentage Total Invoice Amount;
                                                              NOR=Dekningsbidrag, totalt fakturabel›p;
                                                              SVE=T„ckningsbidrag, totalt fakturabelopp];
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 93  ;   ;Fixed B Amount per Hour;Decimal    ;OnValidate=BEGIN
                                                                IF (xRec."Fixed B Amount per Hour" = 0) AND ("Fixed B Amount per Hour" <> 0) THEN BEGIN
                                                                  "Contribution Percentage Labor" := 0;
                                                                  "Contribution Percentage Total" := 0;
                                                                END;
                                                                VALIDATE("Pay To Tax Authority");
                                                              END;

                                                   OnLookup=VAR
                                                              ContBusRel@1100528201 : Record 5054;
                                                              ContIndustGroup@1100528200 : Record 5058;
                                                            BEGIN
                                                              ContBusRel.SETCURRENTKEY("Link to Table","No.");
                                                              ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Vendor);
                                                              ContBusRel.SETRANGE("No.", "Sub Contractor");
                                                              IF NOT ContBusRel.FIND('-') THEN
                                                                ERROR(Text009,ContBusRel.TABLECAPTION,FIELDCAPTION("Sub Contractor"),"Sub Contractor");

                                                              ContIndustGroup.SETRANGE("Contact No.",ContBusRel."Contact No.");
                                                              IF PAGE.RUNMODAL(PAGE::"Contact Industry Groups",ContIndustGroup) = ACTION::LookupOK THEN BEGIN
                                                                "Contribution Percentage Total" := ContIndustGroup."Contribution Percentage Total";
                                                                "Contribution Percentage Labor" := ContIndustGroup."Contribution Percentage Labor";
                                                                "Fixed B Amount per Hour" := ContIndustGroup."Fixed B Amount per Hour";
                                                                 VALIDATE("Fixed B Amount per Hour");
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Fixed B Amount per Hour;
                                                              SVE=Fast B-belopp per timme] }
    { 95  ;   ;Pay To Tax Authority;Boolean       ;OnValidate=BEGIN
                                                                TESTFIELD("Pay To Tax Authority", FALSE);
                                                                WKACalculationCu.CalculateWageChart
                                                                  ("Sub Contractor","Labor Amount","Contribution Percentage Labor","Pay To Tax Authority",'',"Wage Charge (to B Account)",
                                                                  "Wage Charge (to Tax Authority)","Contribution Percentage Total",
                                                                  "Fixed B Amount per Hour","Number of Hours", 0);
                                                              END;

                                                   CaptionML=[ENU=Pay To Tax Authority;
                                                              NOR=Betal til skattemyndighet;
                                                              SVE=Betala till skattemyndighet] }
    { 100 ;   ;Wage Charge (to B Account);Decimal ;OnValidate=BEGIN
                                                                WKACalculationCu.ValidateWageCharge(
                                                                  "Labor Amount",'',"Wage Charge (to B Account)",
                                                                  "Contribution Percentage Labor","Contribution Percentage Total",
                                                                  "Fixed B Amount per Hour",0);
                                                              END;

                                                   CaptionML=[ENU=Wage Charge (to B Account);
                                                              NOR=L›nnsgebyr (til B-konto);
                                                              SVE=L”neavgift (till B-konto)];
                                                   MinValue=0;
                                                   AutoFormatType=1 }
    { 110 ;   ;Wage Charge (to Tax Authority);Decimal;
                                                   OnValidate=BEGIN
                                                                TESTFIELD("Wage Charge (to Tax Authority)", 0);
                                                                WKACalculationCu.ValidateWageTax(
                                                                  "Sub Contractor","Labor Amount",'',"Contribution Percentage Labor",
                                                                  "Wage Charge (to Tax Authority)", "Contribution Percentage Total",
                                                                  "Fixed B Amount per Hour",0);
                                                              END;

                                                   CaptionML=[ENU=Wage Charge (to Tax Authority);
                                                              NOR=L›nnsgebyr (til skattemyndighet);
                                                              SVE=L”neavgift (till skattemyndigheten)];
                                                   MinValue=0;
                                                   AutoFormatType=1 }
    { 120 ;   ;Nat.Ins.Contr.      ;Decimal       ;CaptionML=[ENU=Nat.Ins.Contr.;
                                                              NOR=Nat.Ins.Contr.;
                                                              SVE=Nat.Ins.Contr.];
                                                   MinValue=0;
                                                   Description=This field expires in update 005;
                                                   AutoFormatType=1 }
    { 130 ;   ;Safe Guarding       ;Option        ;CaptionML=[ENU=Safe Guarding;
                                                              NOR=Ivaretakelse;
                                                              SVE=Skyddar];
                                                   OptionCaptionML=[ENU=Not Received,Applied,Received;
                                                                    NOR=Ikke mottatt,Gjelder,Mottatt;
                                                                    SVE=Inte mottagen,Till„mpad,Mottagen];
                                                   OptionString=Not Received,Applied,Received }
    { 140 ;   ;Chain Condition     ;Boolean       ;CaptionML=[ENU=Chain Condition;
                                                              NOR=Kjedebetingelse;
                                                              SVE=Kedjetillst†nd] }
    { 150 ;   ;Memo                ;Text250       ;CaptionML=[ENU=Memo;
                                                              NOR=Merknad;
                                                              SVE=PM] }
    { 160 ;   ;Project No.         ;Code20        ;TableRelation=Job.No.;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr] }
    { 170 ;   ;Signed Return Date  ;Date          ;CaptionML=[ENU=Signed Return Date;
                                                              NOR=Undertegnet Returdato;
                                                              SVE=Undertecknat returdatum] }
    { 180 ;   ;Vendor Name         ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor.Name WHERE (No.=FIELD(Sub Contractor)));
                                                   CaptionML=[ENU=Vendor Name;
                                                              NOR=Leverand›rsnavn;
                                                              SVE=Leverant”rsnamn];
                                                   Editable=No }
    { 190 ;   ;Pay VAT Amt. on B-Account Vend;Boolean;
                                                   CaptionML=ENU=Pay VAT Amount on B-Account Vendor }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
    {    ;Sub Contractor,Code                      }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@11012003 : TextConst 'ENU=Fill out %1 for Vendor %2;NOR=Fyll ut %1 for leverand›r %2;SVE=Fyll i %1 f”r leverant”r %2';
      Text007@11012010 : TextConst 'ENU=must occur before %1;NOR=m† inntreffe f›r %1;SVE=m†ste intr„ffa f”re %1';
      Text008@11012011 : TextConst 'ENU=Must occur after  %1;NOR=M† inntreffe etter %1;SVE=M†ste intr„ffa efter %1';
      Text009@11012012 : TextConst 'ENU=No %1 known for %2 %3;NOR=Ingen %1 kjent for %2 %3;SVE=Ingen %1 k„nns till f”r %2 %3';
      VendorRec@11012000 : Record 23;
      WKACheck@1100485001 : Record 11020275;
      VendorWKACheck@1100485000 : Record 11020276;
      WKACalculationCu@11012001 : Codeunit 11012020;
      SubContr@11012002 : Code[20];
      CentrDataMgt@1100485002 : Codeunit 11012010;
      OldPaymentAgreement@1100485003 : Record 11012028;
      GiveMessage@1210190000 : Boolean;

    PROCEDURE InsertCentralManagement@30();
    BEGIN
      CentrDataMgt.EditPaymentAgreement(Rec,1,xRec);
    END;

    BEGIN
    END.
  }
}

