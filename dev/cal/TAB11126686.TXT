OBJECT Table 11126686 Plot-Rental Unit Relation Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Company (Rental Unit)", COMPANYNAME);
             END;

    OnModify=BEGIN
               TESTFIELD("Company (Rental Unit)", COMPANYNAME);
             END;

    OnDelete=VAR
               RentalUnit@1100528800 : Record 11012940;
             BEGIN
               TESTFIELD("Company (Rental Unit)", COMPANYNAME);
               IF "Rental Unit" <> '' THEN BEGIN
                 IF RentalUnit.GET("Project No.", "Rental Unit") THEN
                   TESTFIELD("Rental Unit", '');
               END;
             END;

    OnRename=BEGIN
               TESTFIELD("Company (Rental Unit)", COMPANYNAME);
               TESTFIELD("Rental Unit", '');
             END;

    CaptionML=ENU=Plot-Rental Unit Relation Line;
  }
  FIELDS
  {
    { 10  ;   ;Company (Rental Unit);Text30       ;TableRelation=Company;
                                                   CaptionML=ENU=Company (Rental Unit);
                                                   NotBlank=Yes;
                                                   Description=Rental unit company;
                                                   Editable=No }
    { 20  ;   ;Company (Plot)      ;Text30        ;TableRelation=Company;
                                                   CaptionML=ENU=Company (Plot);
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 30  ;   ;Project No. (Plot)  ;Code20        ;CaptionML=ENU=Project No. (Plot);
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 40  ;   ;Plot No.            ;Code10        ;OnValidate=VAR
                                                                Plot@1100528800 : Record 11012500;
                                                              BEGIN
                                                                IF "Plot No." <> '' THEN BEGIN
                                                                  Plot.CHANGECOMPANY("Company (Plot)");
                                                                  Plot.GET("Project No. (Plot)", "Plot No.");
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              Plot@1100528800 : Record 11012500;
                                                            BEGIN
                                                              Plot.CHANGECOMPANY("Company (Plot)");
                                                              Plot.SETRANGE("Project No.", "Project No. (Plot)");
                                                              Plot."Plot No." := "Plot No.";
                                                              IF PAGE.RUNMODAL(0, Plot) = ACTION::LookupOK THEN
                                                                VALIDATE("Plot No.", Plot."Plot No.");
                                                            END;

                                                   CaptionML=[ENU=Plot No.;
                                                              NOR=Tegningnr;
                                                              SVE=Ritningnr];
                                                   NotBlank=Yes }
    { 50  ;   ;Project No.         ;Code20        ;TableRelation=Job.No.;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr];
                                                   Editable=No }
    { 51  ;   ;Project Description ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Job.Description WHERE (No.=FIELD(Project No.)));
                                                   CaptionML=[ENU=Project Description;
                                                              NOR=Prosjektbeskrivelse;
                                                              SVE=Projektbeskrivning];
                                                   Editable=No }
    { 60  ;   ;Rental Unit         ;Code20        ;TableRelation="Rental Unit"."Rental Unit" WHERE (Project No.=FIELD(Project No.));
                                                   CaptionML=[ENU=Rental Unit;
                                                              NOR=Leieenhet;
                                                              SVE=Uthyrningsenhet];
                                                   Editable=No }
    { 61  ;   ;Rental Unit Description;Text50     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Rental Unit".Description WHERE (Project No.=FIELD(Project No.),
                                                                                                       Rental Unit=FIELD(Rental Unit)));
                                                   CaptionML=ENU=Rental Unit Description;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Company (Rental Unit),Company (Plot),Project No. (Plot),Plot No.;
                                                   Clustered=Yes }
    {    ;Project No.,Rental Unit                  }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE GetAddressField@1100529000(FieldName@1100529000 : Text) : Text;
    VAR
      Plot@1100529001 : Record 11012500;
      RentalUnit@1100529002 : Record 11012940;
    BEGIN
      IF "Rental Unit" <> '' THEN BEGIN
        IF "Company (Rental Unit)" <> COMPANYNAME THEN
          RentalUnit.CHANGECOMPANY("Company (Rental Unit)");
        IF RentalUnit.GET("Project No.", "Rental Unit") THEN BEGIN
          CASE LOWERCASE(FieldName) OF
            'address':
              EXIT(RentalUnit.Address);
            'postcode':
              EXIT(RentalUnit."Post Code");
            'city':
              EXIT(RentalUnit.City);
          END;
        END;
      END ELSE BEGIN
        IF "Company (Plot)" <> COMPANYNAME THEN
          Plot.CHANGECOMPANY("Company (Plot)");
        IF Plot.GET("Project No. (Plot)", "Plot No.") THEN BEGIN
          CASE LOWERCASE(FieldName) OF
            'address':
              EXIT(Plot.Address);
            'postcode':
              EXIT(Plot."Post Code");
            'city':
              EXIT(Plot.City);
          END;
        END;
      END;
      EXIT('');
    END;

    PROCEDURE CreateRentalUnits@1100528800(VAR TmpRentalUnit@1100528805 : TEMPORARY Record 11012940);
    VAR
      Plot@1100528800 : Record 11012500;
      PlotRentalUnitRelationLine@1100528801 : Record 11126686;
      RentalUnit@1100528803 : Record 11012940;
      RecordRef@1100528804 : RecordRef;
    BEGIN
      PlotRentalUnitRelationLine.COPY(Rec);  // Copy filters (selected lines)
      PlotRentalUnitRelationLine.FILTERGROUP(9);
      PlotRentalUnitRelationLine.SETRANGE("Company (Rental Unit)", "Company (Rental Unit)");
      PlotRentalUnitRelationLine.SETRANGE("Company (Plot)", "Company (Plot)");
      PlotRentalUnitRelationLine.SETRANGE("Project No. (Plot)", "Project No. (Plot)");
      PlotRentalUnitRelationLine.SETFILTER("Plot No.", '<>%1', '');
      PlotRentalUnitRelationLine.SETRANGE("Rental Unit", '');
      PlotRentalUnitRelationLine.FILTERGROUP(0);
      IF PlotRentalUnitRelationLine.FINDSET(TRUE,FALSE) THEN BEGIN
        Plot.CHANGECOMPANY("Company (Plot)");
        REPEAT
          Plot.GET(PlotRentalUnitRelationLine."Project No. (Plot)", PlotRentalUnitRelationLine."Plot No.");
          RentalUnit.INIT;
          RentalUnit.VALIDATE("Project No.", TmpRentalUnit."Project No.");
          RentalUnit.VALIDATE("Rental Unit", Plot."Plot No.");
          RentalUnit.INSERT(TRUE);
          RentalUnit.TRANSFERFIELDS(TmpRentalUnit, FALSE);
          RentalUnit.Address := Plot.Address;
          RentalUnit."Post Code" := Plot."Post Code";
          RentalUnit.City := Plot.City;
          RentalUnit."Service Location No." := '';  // 1:1 relation
          RentalUnit.MODIFY(TRUE);
          PlotRentalUnitRelationLine."Project No." := RentalUnit."Project No.";
          PlotRentalUnitRelationLine."Rental Unit" := RentalUnit."Rental Unit";
          PlotRentalUnitRelationLine.MODIFY;
          RecordRef.GETTABLE(Plot);
          CreateDocumentLink(RentalUnit, RecordRef, "Company (Plot)");
        UNTIL PlotRentalUnitRelationLine.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE CreateDocumentLink@1100525005(RentalUnit@1100525007 : Record 11012940;RecRef@1100525006 : RecordRef;CompName@1100525008 : Text[50]);
    VAR
      DocumentProperties@1100525005 : Record 11012746;
      DocumentLink@1100525004 : Record 11012747;
      DocumentLinkMgt@1100525002 : Codeunit 11012401;
      DocLinkRID@1100525000 : ARRAY [5] OF RecordID;
    BEGIN
      DocumentLink.RESET;
      DocumentLink.SETCURRENTKEY("Record ID");
      DocumentLink.SETRANGE("Table No.", RecRef.NUMBER);
      DocumentLink.SETFILTER("Record ID", STRSUBSTNO('''%1''', RecRef.RECORDID()));
      DocumentLink.SETRANGE("Internal Company", CompName);
      IF DocumentLink.FINDSET THEN BEGIN
        REPEAT
          DocumentProperties.SETRANGE("No.", DocumentLink."Document No.");
          IF DocumentProperties.FINDFIRST THEN BEGIN
            RecRef.GETTABLE(RentalUnit);
            DocLinkRID[2] := RecRef.RECORDID();
            DocumentLinkMgt.CreateDocumentLink(DocumentProperties, DocLinkRID);
            DocumentProperties.MODIFY(TRUE);
          END;
        UNTIL DocumentLink.NEXT = 0;
      END;
    END;

    BEGIN
    {
      Copied from T11072219, Now for plot and rental unit, split in header and line table
    }
    END.
  }
}

