OBJECT Codeunit 1206 Pmt Export Mgt Gen. Jnl Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,NAVDK,4PSSE;
  }
  PROPERTIES
  {
    TableNo=81;
    Permissions=TableData 25=rm,
                TableData 81=rm,
                TableData 1226=rimd;
    OnRun=BEGIN
            ExportJournalPaymentFile(Rec);
          END;

  }
  CODE
  {
    VAR
      ExportAgainQst@1002 : TextConst 'ENU=One or more of the selected lines has already been exported. Do you want to export it again?;NOR=ên eller flere av de valgte linjene er allerede eksportert. Vil du eksportere den pÜ nytt?;SVE=Minst en av de valda raderna har redan exporterats. Vill du exportera den igen?';
      ProgressMsg@1001 : TextConst 'ENU=Processing line no. #1######.;NOR=Behandler linje nr. #1######.;SVE=Behandlar rad nr #1######.';
      HasErrorsErr@1008 : TextConst 'ENU=The file export has one or more errors.\\For each line to be exported, resolve the errors displayed to the right and then try to export again.;NOR=Fileksporten har Çn eller flere feil.\\Lõs alle feil som vises til hõyre, for hver linje som skal eksporteres, og prõv deretter Ü eksportere pÜ nytt.;SVE=Det finns ett eller flera fel i filexporten.\\ètgÑrda felen som visas till hîger fîr varje rad som ska exporteras, och fîrsîk sedan att exportera pÜ nytt.';
      PaymentExportMgt@1003 : Codeunit 1210;
      GLSetup@1100285500 : Record 98;
      gotGLSetup@1100285501 : Boolean;

    [Internal]
    PROCEDURE ExportJournalPaymentFileYN@2(VAR GenJnlLine@1001 : Record 81);
    BEGIN
      GenJnlLine.SETRANGE("Journal Template Name",GenJnlLine."Journal Template Name");
      GenJnlLine.SETRANGE("Journal Batch Name",GenJnlLine."Journal Batch Name");
      IF GenJnlLine.IsExportedToPaymentFile THEN
        IF NOT CONFIRM(ExportAgainQst) THEN
          EXIT;
      ExportJournalPaymentFile(GenJnlLine);
    END;

    [Internal]
    PROCEDURE ExportJournalPaymentFile@118(VAR GenJnlLine@1001 : Record 81);
    VAR
      BankAccount@1000 : Record 270;
      CreditTransferRegister@1004 : Record 1205;
      DataExchDef@1002 : Record 1222;
    BEGIN
      BankAccount.GET(GenJnlLine."Bal. Account No.");
      BankAccount.GetDataExchDefPaymentExport(DataExchDef);
      CreditTransferRegister.CreateNew(DataExchDef.Code,GenJnlLine."Bal. Account No.");
      COMMIT;

      CheckGenJnlLine(GenJnlLine);
      ExportGenJnlLine(GenJnlLine,CreditTransferRegister);
    END;

    LOCAL PROCEDURE CheckGenJnlLine@9(VAR GenJnlLine@1000 : Record 81);
    VAR
      GenJnlLine2@1001 : Record 81;
      PaymentExportGenJnlCheck@1060000 : Codeunit 1211;
    BEGIN
      GenJnlLine.DeletePaymentFileBatchErrors;
      GenJnlLine2.COPYFILTERS(GenJnlLine);
      GetGLSetup;  //4PSSE.DK
      IF GenJnlLine2.FINDSET THEN
        REPEAT
          CODEUNIT.RUN(CODEUNIT::"Payment Export Gen. Jnl Check",GenJnlLine2);
          //NAVDK
          IF GLSetup."Danish Localization Active" THEN
            PaymentExportGenJnlCheck.CheckFormatSpecificPaymentRules(GenJnlLine2);
          OnCheckGenJnlLine(GenJnlLine2);
        UNTIL GenJnlLine2.NEXT = 0;

      IF GenJnlLine2.HasPaymentFileErrorsInBatch THEN BEGIN
        COMMIT;
        ERROR(HasErrorsErr);
      END;
    END;

    [Internal]
    PROCEDURE ExportGenJnlLine@15(VAR GenJnlLine@1000 : Record 81;VAR CreditTransferRegister@1007 : Record 1205);
    VAR
      GenJnlLine2@1004 : Record 81;
      DataExch@1001 : Record 1220;
      CreditTransferEntry@1006 : Record 1206;
      Window@1002 : Dialog;
      LineNo@1003 : Integer;
      LineAmount@1216 : Decimal;
      TransferDate@1217 : Date;
      TotalAmount@1218 : Decimal;
      HandledGenJnlDataExchLine@1213 : Boolean;
      HandledPaymentExport@1214 : Boolean;
    BEGIN
      GenJnlLine2.COPYFILTERS(GenJnlLine);
      GenJnlLine2.FINDSET;

      PaymentExportMgt.CreateDataExch(DataExch,GenJnlLine2."Bal. Account No.");
      GenJnlLine2.MODIFYALL("Data Exch. Entry No.",DataExch."Entry No.");

      Window.OPEN(ProgressMsg);
      REPEAT
        LineNo += 1;
        Window.UPDATE(1,LineNo);

        OnBeforeCreateGenJnlDataExchLine(DataExch,GenJnlLine2,LineNo,LineAmount,TotalAmount,TransferDate,HandledGenJnlDataExchLine);
        IF NOT HandledGenJnlDataExchLine THEN
        CreateGenJnlDataExchLine(DataExch."Entry No.",GenJnlLine2,LineNo,LineAmount,TransferDate);
        TotalAmount += LineAmount;

        CreditTransferEntry.CreateNew(CreditTransferRegister."No.",LineNo,
          GenJnlLine2."Account Type",GenJnlLine2."Account No.",GenJnlLine2.GetAppliesToDocEntryNo,
          GenJnlLine2."Posting Date",GenJnlLine2."Currency Code",GenJnlLine2.Amount,'',
          GenJnlLine2."Recipient Bank Account",GenJnlLine2."Message to Recipient");
      UNTIL GenJnlLine2.NEXT = 0;
      Window.CLOSE;

      GetGLSetup;  //4PSSE.DK
      IF GLSetup."Danish Localization Active" THEN
        ExportDataFromBuffer(GenJnlLine."Bal. Account No.",DataExch."Entry No.",LineNo,TotalAmount,TransferDate)  //NAVDK
      ELSE
      OnBeforePaymentExport(GenJnlLine."Bal. Account No.",DataExch."Entry No.",LineNo,TotalAmount,TransferDate,HandledPaymentExport);
      IF NOT HandledPaymentExport THEN
        PaymentExportMgt.ExportToFile(DataExch."Entry No.");

      CODEUNIT.RUN(CODEUNIT::"Exp. User Feedback Gen. Jnl.",DataExch);
    END;

    LOCAL PROCEDURE CreateGenJnlDataExchLine@16(DataExchEntryNo@1000 : Integer;GenJnlLine@1001 : Record 81;LineNo@1002 : Integer;VAR PmtExportLineAmount@1060001 : Decimal;VAR PmtExportTransferDate@1060002 : Date);
    VAR
      TempPaymentExportData@1004 : TEMPORARY Record 1226;
    BEGIN
      WITH GenJnlLine DO BEGIN
        PreparePaymentExportDataJnl(TempPaymentExportData,GenJnlLine,DataExchEntryNo,LineNo);
        PmtExportLineAmount := TempPaymentExportData.Amount;  //NAVDK
        PmtExportTransferDate := TempPaymentExportData."Transfer Date";  //NAVDK
        PaymentExportMgt.CreatePaymentLines(TempPaymentExportData);
      END;
    END;

    [External]
    PROCEDURE PreparePaymentExportDataJnl@12(VAR TempPaymentExportData@1000 : TEMPORARY Record 1226;GenJnlLine@1001 : Record 81;DataExchEntryNo@1003 : Integer;LineNo@1007 : Integer);
    VAR
      GeneralLedgerSetup@1009 : Record 98;
      Vendor@1006 : Record 23;
      VendorBankAccount@1008 : Record 288;
      PaymentMethod@1010 : Record 289;
      BankAccount@1002 : Record 270;
      BankExportImportSetup@1004 : Record 1200;
    BEGIN
      GeneralLedgerSetup.GET;
      Vendor.GET(GenJnlLine."Account No.");

      WITH TempPaymentExportData DO BEGIN
        BankAccount.GET(GenJnlLine."Bal. Account No.");
        BankAccount.GetBankExportImportSetup(BankExportImportSetup);
        SetPreserveNonLatinCharacters(BankExportImportSetup."Preserve Non-Latin Characters");

        INIT;
        "Data Exch Entry No." := DataExchEntryNo;
        "Sender Bank Account Code" := GenJnlLine."Bal. Account No.";

        IF VendorBankAccount.GET(GenJnlLine."Account No.",GenJnlLine."Recipient Bank Account") THEN BEGIN
          IF BankAccount."Country/Region Code" = VendorBankAccount."Country/Region Code" THEN BEGIN
            Amount := GenJnlLine."Amount (LCY)";
            "Currency Code" := GeneralLedgerSetup."LCY Code";
          END ELSE BEGIN
            Amount := GenJnlLine.Amount;
            "Currency Code" := GeneralLedgerSetup.GetCurrencyCode(GenJnlLine."Currency Code");
          END;

          "Recipient Bank Acc. No." :=
            COPYSTR(VendorBankAccount.GetBankAccountNo,1,MAXSTRLEN("Recipient Bank Acc. No."));
          "Recipient Reg. No." := VendorBankAccount."Bank Branch No.";
          "Recipient Acc. No." := VendorBankAccount."Bank Account No.";
          "Recipient Bank Country/Region" := VendorBankAccount."Country/Region Code";
          "Recipient Bank Name" := COPYSTR(VendorBankAccount.Name,1,35);
          "Recipient Bank Address" := COPYSTR(VendorBankAccount.Address,1,35);
          "Recipient Bank City" := COPYSTR(VendorBankAccount."Post Code" + VendorBankAccount.City,1,35);
          "Recipient Bank BIC" := VendorBankAccount."SWIFT Code";
        END ELSE
          IF (GenJnlLine."Creditor No." <> '') OR (GenJnlLine."Giro Acc. No." <> '') THEN BEGIN
            Amount := GenJnlLine."Amount (LCY)";
            "Currency Code" := GeneralLedgerSetup."LCY Code";
          END;

        "Recipient Name" := COPYSTR(Vendor.Name,1,35);
        "Recipient Address" := COPYSTR(Vendor.Address,1,35);
        "Recipient City" := COPYSTR(Vendor."Post Code" + ' ' + Vendor.City,1,35);
        "Transfer Date" := GenJnlLine."Posting Date";
        "Message to Recipient 1" := COPYSTR(GenJnlLine."Message to Recipient",1,35);
        "Message to Recipient 2" := COPYSTR(GenJnlLine."Message to Recipient",36,70);
        "Document No." := GenJnlLine."Document No.";
        "Applies-to Ext. Doc. No." := GenJnlLine."Applies-to Ext. Doc. No.";
        "Short Advice" := GenJnlLine."Applies-to Ext. Doc. No.";
        "Line No." := LineNo;
        "Payment Reference" := GenJnlLine."Payment Reference";
        IF PaymentMethod.GET(GenJnlLine."Payment Method Code") THEN
          "Data Exch. Line Def Code" := PaymentMethod."Pmt. Export Line Definition";
        "Recipient Creditor No." := GenJnlLine."Creditor No.";
        "Recipient Giro Acc. No." := GenJnlLine."Giro Acc. No.";
        OnBeforeInsertPmtExportDataJnlFromGenJnlLine(TempPaymentExportData,GenJnlLine,GeneralLedgerSetup);
        INSERT(TRUE);
      END;
    END;

    LOCAL PROCEDURE ExportDataFromBuffer@1060002(BalAccountNo@1060001 : Code[20];DataExchEntryNo@1060004 : Integer;LineCount@1060006 : Integer;TotalAmount@1060007 : Decimal;TransferDate@1060008 : Date);
    VAR
      BankAccount@1060000 : Record 270;
      BankExportImportSetup@1060002 : Record 1200;
      DataExchDef@1060003 : Record 1222;
      TempPaymentExportData@1060005 : TEMPORARY Record 1226;
      DataExch@1060009 : Record 1220;
    BEGIN
      GetGLSetup;  //NAVDK
      IF NOT GLSetup."Danish Localization Active" THEN EXIT;
      //NAVDK
      BankAccount.GET(BalAccountNo);
      BankExportImportSetup.GET(BankAccount."Payment Export Format");
      DataExchDef.GET(BankExportImportSetup."Data Exch. Def. Code");
      IF DataExchDef."Reading/Writing Codeunit" = CODEUNIT::"Export BankData Fixed Width" THEN BEGIN
        TempPaymentExportData.INIT;
        TempPaymentExportData."Data Exch Entry No." := DataExchEntryNo;
        TempPaymentExportData."Line No." := LineCount;
        TempPaymentExportData.Amount := TotalAmount;
        TempPaymentExportData."Transfer Date" := TransferDate;
        TempPaymentExportData.INSERT;
        CODEUNIT.RUN(DataExchDef."Reading/Writing Codeunit",TempPaymentExportData);
        DataExch.GET(DataExchEntryNo);
        CODEUNIT.RUN(DataExchDef."Ext. Data Handling Codeunit",DataExch);
      END ELSE
        PaymentExportMgt.ExportToFile(DataExchEntryNo);
    END;

    [External]
    PROCEDURE EnableExportToServerTempFile@27(SilentServerMode@1000 : Boolean;ServerFileExtension@1001 : Text[3]);
    BEGIN
      PaymentExportMgt.EnableExportToServerTempFile(SilentServerMode,ServerFileExtension);
    END;

    [External]
    PROCEDURE GetServerTempFileName@29() : Text[1024];
    BEGIN
      EXIT(PaymentExportMgt.GetServerTempFileName);
    END;

    [Integration]
    PROCEDURE OnBeforeInsertPmtExportDataJnlFromGenJnlLine@67(VAR PaymentExportData@1000 : Record 1226;GenJournalLine@1001 : Record 81;GeneralLedgerSetup@1002 : Record 98);
    BEGIN
    END;

    [Integration]
    PROCEDURE OnBeforeCreateGenJnlDataExchLine@213(DataExch@1219 : Record 1220;GenJournalLine@1218 : Record 81;LineNo@1217 : Integer;VAR LineAmount@1216 : Decimal;VAR TotalAmount@1215 : Decimal;VAR TransferDate@1214 : Date;VAR Handled@1213 : Boolean);
    BEGIN
    END;

    [Integration]
    PROCEDURE OnBeforePaymentExport@214(BalAccountNo@1213 : Code[20];DataExchEntryNo@1214 : Integer;LineCount@1215 : Integer;TotalAmount@1216 : Decimal;TransferDate@1217 : Date;VAR Handled@1218 : Boolean);
    BEGIN
    END;

    [Integration]
    PROCEDURE OnCheckGenJnlLine@215(GenJournalLine@1213 : Record 81);
    BEGIN
    END;

    LOCAL PROCEDURE GetGLSetup@1100285500();
    BEGIN
      //4PSSE.DK
      IF NOT gotGLSetup THEN
        GLSetup.GET;
      gotGLSetup := TRUE;
    END;

    BEGIN
    END.
  }
}

