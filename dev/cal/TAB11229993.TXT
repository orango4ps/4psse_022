OBJECT Table 11229993 Website Text Line Translation
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Source Type,Source No.;
    OnInsert=BEGIN
               CheckKeyFields;
             END;

    OnRename=BEGIN
               CheckKeyFields;
             END;

    CaptionML=ENU=Website Text Line Translation;
  }
  FIELDS
  {
    { 10  ;   ;Source Type         ;Option        ;CaptionML=[ENU=Source Type;
                                                              SVE=Ursprungstyp];
                                                   OptionCaptionML=ENU=Plant Type,Item;
                                                   OptionString=Plant Type,Item }
    { 20  ;   ;Source No.          ;Code20        ;TableRelation=IF (Source Type=CONST(Plant Type)) "Plant Type".Code
                                                                 ELSE IF (Source Type=CONST(Item)) Item.No.;
                                                   CaptionML=[ENU=Source No.;
                                                              SVE=Ursprungsnr] }
    { 25  ;   ;Line No.            ;Integer       ;DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Line No.;
                                                              SVE=Radnr] }
    { 30  ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Language Code;
                                                              SVE=Spr†kkod];
                                                   NotBlank=Yes }
    { 40  ;   ;Translation         ;Text50        ;DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Translation;
                                                              SVE=™vers„ttning] }
  }
  KEYS
  {
    {    ;Source Type,Source No.,Line No.,Language Code;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100527400 : TextConst 'ENU=No language selected';

    [External]
    PROCEDURE CheckKeyFields@1100529001();
    BEGIN
      CASE "Source Type" OF
        "Source Type"::"Plant Type":
          BEGIN
            TESTFIELD("Source No.");
          END;
        "Source Type"::Item:
          BEGIN
            TESTFIELD("Source No.");
          END;
      END;
    END;

    [External]
    PROCEDURE GetDescription@1100529000() : Text[100];
    VAR
      PlantType@1100529000 : Record 11012551;
      Item@1100529001 : Record 27;
    BEGIN
      CASE "Source Type" OF
        "Source Type"::"Plant Type":
          BEGIN
            IF PlantType.GET("Source No.") THEN
              EXIT(PlantType.Description);
          END;
        "Source Type"::Item:
          BEGIN
            IF Item.GET("Source No.") THEN
              EXIT(Item.Description);
          END;
      END;
      EXIT('');
    END;

    [External]
    PROCEDURE SelectLanguageTmpTable@1100529003(CurrLanguage@1100529000 : Code[10]) : Code[10];
    VAR
      Language@1100529001 : Record 8;
    BEGIN
      Language.Code := CurrLanguage;
      IF PAGE.RUNMODAL(0, Language) = ACTION::LookupOK THEN BEGIN
        IF (Language.Code <> '') AND (Language.Code <> CurrLanguage) THEN
          CurrLanguage := Language.Code;
      END;
      IF CurrLanguage = '' THEN
        ERROR(Text001);
      EXIT(CurrLanguage);
    END;

    [External]
    PROCEDURE BuildTmpTable@1100529002(VAR TmpRec@1100529007 : TEMPORARY Record 11229993;CurrType@1100529008 : Option;CurrLanguage@1100529009 : Code[10];CurrLineNo@1100527400 : Integer);
    VAR
      PlantType@1100529000 : Record 11012551;
      Item@1100529001 : Record 27;
      WebsiteTextLineTranslation@1100529003 : Record 11229993;
      CurrWebsiteTextLineTranslation@1100529004 : Record 11229993;
      Window@1100529005 : Dialog;
      Counter@1100529006 : Integer;
    BEGIN
      IF CurrLanguage = '' THEN
        EXIT;
      CurrWebsiteTextLineTranslation := TmpRec;

      WITH TmpRec DO BEGIN
        RESET;
        DELETEALL;
        CASE CurrType OF
          "Source Type"::"Plant Type":
            BEGIN
              IF GUIALLOWED THEN BEGIN
                Window.OPEN(PlantType.TABLECAPTION + ' #1############################');
                IF PlantType.FINDSET THEN BEGIN
                  REPEAT
                    Counter := Counter + 1;
                    IF (Counter MOD 100) = 1 THEN
                      Window.UPDATE(1, PlantType.Code);
                    INIT;
                    "Source Type" := CurrType;
                    "Source No." := PlantType.Code;
                    "Language Code" := CurrLanguage;
                    "Line No." := CurrLineNo;
                    IF WebsiteTextLineTranslation.GET("Source Type", "Source No.", "Line No.", "Language Code") THEN
                      Translation := WebsiteTextLineTranslation.Translation;
                    INSERT;
                  UNTIL PlantType.NEXT = 0;
                END;
                Window.CLOSE;
              END;
            END;
          "Source Type"::Item:
            BEGIN
              IF GUIALLOWED THEN BEGIN
                Window.OPEN(PlantType.TABLECAPTION + ' #1############################');
                IF Item.FINDSET THEN BEGIN
                  REPEAT
                    Counter := Counter + 1;
                    IF (Counter MOD 100) = 1 THEN
                      Window.UPDATE(1, Item."No.");
                    INIT;
                    "Source Type" := CurrType;
                    "Source No." := Item."No.";
                    "Line No." := CurrLineNo;
                    "Language Code" := CurrLanguage;
                    IF WebsiteTextLineTranslation.GET("Source Type", "Source No.", "Line No.", "Language Code") THEN
                      Translation := WebsiteTextLineTranslation.Translation;
                    INSERT;
                  UNTIL Item.NEXT = 0;
                END;
                Window.CLOSE;
              END;
            END;
        END;
        IF NOT GET(CurrType, CurrWebsiteTextLineTranslation."Source No.", CurrLineNo, CurrLanguage) THEN
          IF FINDFIRST THEN;
      END;
    END;

    [External]
    PROCEDURE UpdateTranslationFromTmpRecord@1100529016();
    VAR
      WebsiteTextLineTranslation@1100529000 : Record 11229993;
    BEGIN
      IF Translation = '' THEN
        EXIT;
      IF WebsiteTextLineTranslation.GET("Source Type", "Source No.", "Line No.", "Language Code") THEN BEGIN
        IF Translation = '' THEN
          WebsiteTextLineTranslation.DELETE
        ELSE BEGIN
          WebsiteTextLineTranslation.Translation := Translation;
          WebsiteTextLineTranslation.MODIFY;
        END;
      END ELSE BEGIN
        IF Translation <> '' THEN BEGIN
          WebsiteTextLineTranslation.INIT;
          WebsiteTextLineTranslation."Source Type" := "Source Type";
          WebsiteTextLineTranslation."Source No." := "Source No.";
          WebsiteTextLineTranslation."Line No." := "Line No.";
          WebsiteTextLineTranslation."Language Code" := "Language Code";
          WebsiteTextLineTranslation.Translation := Translation;
          WebsiteTextLineTranslation.INSERT;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

