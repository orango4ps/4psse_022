OBJECT Codeunit 11124922 PLA Helper Functions
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE GetPlantTypeThumbnail@1100529045(VAR PlantType@1100529002 : Record 11012551) ThumbnailBase64 : Text;
    VAR
      Base64EncodedContents@1100528300 : BigText;
      MemoryStream@1100528001 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.MemoryStream";
    BEGIN
      IF PlantType.Image.HASVALUE THEN BEGIN
        MemoryStream := MemoryStream.MemoryStream;
        PlantType.Image.EXPORTSTREAM(MemoryStream);
        MemoryStream.Position := 0;

        CreateBase64ContentFromStream(MemoryStream, Base64EncodedContents);
        Base64EncodedContents.GETSUBTEXT(ThumbnailBase64, 1);
      END;
    END;

    PROCEDURE GetPlantTypeCodeThumbnail@1100528000(PlantTypeCode@1100529002 : Code[20]) ThumbnailBase64 : Text;
    VAR
      PlantType@1100528000 : Record 11012551;
    BEGIN
      IF PlantType.GET(PlantTypeCode) THEN
        EXIT(GetPlantTypeThumbnail(PlantType));

      EXIT('');
    END;

    PROCEDURE GetItemThumbnail@1100528002(Item@1100529002 : Record 27) ThumbnailBase64 : Text;
    VAR
      Base64EncodedContents@1100528300 : BigText;
      MemoryStream@1100525000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.MemoryStream";
      TenantMediaSet@1100528001 : Record 2000000183;
    BEGIN
      IF (Item.Picture.COUNT <> 0) THEN BEGIN
        TenantMediaSet.SETRANGE(ID, Item.Picture.MEDIAID);
        TenantMediaSet.SETRANGE("Company Name", COMPANYNAME);

        IF TenantMediaSet.COUNT <> 1 THEN
          EXIT('');

        TenantMediaSet.FINDFIRST;

        MemoryStream := MemoryStream.MemoryStream;
        TenantMediaSet."Media ID".EXPORTSTREAM(MemoryStream);

        MemoryStream.Position := 0;
        IF MemoryStream.Length = 0 THEN
          EXIT('');
        CreateBase64ContentFromStream(MemoryStream, Base64EncodedContents);
        Base64EncodedContents.GETSUBTEXT(ThumbnailBase64, 1);
      END;
    END;

    PROCEDURE GetItemNoThumbnail@1100528001(ItemNo@1100529002 : Code[20]) ThumbnailBase64 : Text;
    VAR
      Item@1100528000 : Record 27;
    BEGIN
      IF Item.GET(ItemNo) THEN
        EXIT(GetItemThumbnail(Item));

      EXIT('');
    END;

    LOCAL PROCEDURE CreateBase64ContentFromStream@1100525000(OriginalStream@1100529006 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.Stream";VAR Base64EncodedContents@1100529007 : BigText);
    VAR
      ResizeImage@1100529000 : Codeunit 11125359;
      Convert@1100529009 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Convert";
      ResizeStream@1100525000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.MemoryStream";
      Bytes@1100528000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Array";
    BEGIN
      ResizeStream := ResizeStream.MemoryStream;
      ResizeImage.CompressPictureStream(OriginalStream, ResizeStream, 200);
      CLEAR(Base64EncodedContents);
      Bytes := ResizeStream.ToArray();
      Base64EncodedContents.ADDTEXT(Convert.ToBase64String(Bytes));
    END;

    PROCEDURE IsValidPickerNo@1100528006(PickerNo@1100528000 : Code[20]) : Boolean;
    VAR
      Resource@1100528001 : Record 11229278;
      AppUserSetupGAP@1100526000 : Record 11124976;
      PLAUserSetupGAP@1100526001 : Record 11124977;
      PlantAppSetup@1100526002 : Record 11124924;
    BEGIN
      PlantAppSetup.GET;

      IF PlantAppSetup."Use Generic User Management" THEN BEGIN
        AppUserSetupGAP.SETRANGE("Employee No.", PickerNo);
        IF AppUserSetupGAP.FINDSET THEN
          REPEAT
            PLAUserSetupGAP.SETRANGE(Username, AppUserSetupGAP.Username);
            PLAUserSetupGAP.SETRANGE(Active, PLAUserSetupGAP.Active::Yes);
            IF NOT PLAUserSetupGAP.ISEMPTY THEN
              EXIT(TRUE);
          UNTIL AppUserSetupGAP.NEXT = 0;

        EXIT(FALSE);
      END ELSE BEGIN
        Resource.SETRANGE(Company, COMPANYNAME);
        Resource.SETRANGE(Type, Resource.Type::Employee);
        Resource.SETRANGE("Source No.", PickerNo);
        Resource.SETRANGE("Active in PLA", TRUE);
        EXIT(NOT Resource.ISEMPTY);
      END;
    END;

    PROCEDURE LookupPicker@1100528005(CurrentPickerNo@1100528002 : Code[20]) : Code[20];
    VAR
      Employee@1100528001 : Record 5200;
      EmployeeList@1100528000 : Page 5201;
    BEGIN
      IF Employee.FINDSET THEN
        REPEAT
          IF IsValidPickerNo(Employee."No.") THEN
            Employee.MARK(TRUE);
        UNTIL Employee.NEXT = 0;
      Employee.MARKEDONLY(TRUE);

      IF CurrentPickerNo <> '' THEN BEGIN
        Employee.SETRANGE("No.", CurrentPickerNo);
        IF Employee.FINDFIRST THEN
          EmployeeList.SETRECORD(Employee);
        Employee.SETRANGE("No.");
      END;

      EmployeeList.SETTABLEVIEW(Employee);
      EmployeeList.LOOKUPMODE(TRUE);
      IF (EmployeeList.RUNMODAL = ACTION::LookupOK) THEN BEGIN
        EmployeeList.GETRECORD(Employee);
        EXIT(Employee."No.");
      END ELSE
        EXIT(CurrentPickerNo);
    END;

    BEGIN
    END.
  }
}

