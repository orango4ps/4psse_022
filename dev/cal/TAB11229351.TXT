OBJECT Table 11229351 Json Message
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Json Message;
  }
  FIELDS
  {
    { 1   ;   ;Date Time           ;DateTime      ;CaptionML=[ENU=Date Time;
                                                              NOR=Dato tid;
                                                              SVE=Datum/tid] }
    { 10  ;   ;Indent              ;Integer       ;CaptionML=[ENU=Indent;
                                                              NOR=Rykk inn;
                                                              SVE=Indrag] }
    { 20  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 30  ;   ;Column No.          ;Integer       ;CaptionML=[ENU=Column No.;
                                                              NOR=Kolonnenr.;
                                                              SVE=Kolumnnr] }
    { 40  ;   ;Node Id             ;Text100       ;CaptionML=[ENU=Node Id;
                                                              NOR=Node-ID;
                                                              SVE=Nod-ID] }
    { 41  ;   ;Node Prefix         ;Text100       ;CaptionML=ENU=Node Prefix }
    { 50  ;   ;Value               ;Text250       ;CaptionML=[ENU=Value;
                                                              NOR=Verdi;
                                                              SVE=V„rde] }
    { 60  ;   ;Type                ;Text100       ;CaptionML=[ENU=Type;
                                                              NOR=Type;
                                                              SVE=Typ] }
  }
  KEYS
  {
    {    ;Date Time,Line No.                      ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE DeleteOld@1100525000(Time@1100525000 : DateTime);
    BEGIN
      SETFILTER("Date Time",  '< %1', Time - 60000);
      IF FINDSET THEN
        DELETEALL;
      COMMIT;
    END;

    PROCEDURE GetStartObject@1100525003() : Integer;
    VAR
      JsonMessage@1100525000 : Record 11229351;
    BEGIN
      IF Type = 'StartObject' THEN
        EXIT("Line No.");

      JsonMessage.SETRANGE("Date Time", "Date Time");
      JsonMessage.SETRANGE(Type, 'StartObject');
      JsonMessage.SETFILTER("Line No.", '< %1', "Line No.");
      JsonMessage.SETFILTER(Indent, '< %1', Indent);
      IF JsonMessage.FINDLAST THEN
        EXIT(JsonMessage."Line No.");

      EXIT(0);
    END;

    PROCEDURE GetEndObject@1100525008() : Integer;
    VAR
      JsonMessage@1100525000 : Record 11229351;
      LineNo@1100525001 : Integer;
      IndentNo@1100525003 : Integer;
    BEGIN
      IF Type = 'EndObject' THEN
        EXIT("Line No.");

      IF NOT JsonMessage.GET("Date Time", GetStartObject) THEN
        EXIT(0);
      LineNo := JsonMessage."Line No.";
      IndentNo := JsonMessage.Indent;
      JsonMessage.RESET;
      JsonMessage.SETRANGE("Date Time", "Date Time");
      JsonMessage.SETRANGE(Indent, IndentNo);
      JsonMessage.SETFILTER("Line No.", '> %1', LineNo);
      IF JsonMessage.FINDFIRST THEN
        EXIT(JsonMessage."Line No.");

      EXIT(0);
    END;

    PROCEDURE GetStartArray@1100528301() : Integer;
    VAR
      JsonMessage@1100525000 : Record 11229351;
    BEGIN
      IF Type = 'StartArray' THEN
        EXIT("Line No.");

      JsonMessage.SETRANGE("Date Time", "Date Time");
      JsonMessage.SETRANGE(Type, 'StartArray');
      JsonMessage.SETFILTER("Line No.", '< %1', "Line No.");
      JsonMessage.SETFILTER(Indent, '< %1', Indent);
      IF JsonMessage.FINDLAST THEN
        EXIT(JsonMessage."Line No.");

      EXIT(0);
    END;

    PROCEDURE GetEndArray@1100528300() : Integer;
    VAR
      JsonMessage@1100525000 : Record 11229351;
      LineNo@1100525001 : Integer;
      IndentNo@1100525003 : Integer;
    BEGIN
      IF Type = 'EndArray' THEN
        EXIT("Line No.");

      IF NOT JsonMessage.GET("Date Time", GetStartArray) THEN
        EXIT(0);
      LineNo := JsonMessage."Line No.";
      IndentNo := JsonMessage.Indent;
      JsonMessage.RESET;
      JsonMessage.SETRANGE("Date Time", "Date Time");
      JsonMessage.SETRANGE(Indent, IndentNo);
      JsonMessage.SETFILTER("Line No.", '> %1', LineNo);
      IF JsonMessage.FINDFIRST THEN
        EXIT(JsonMessage."Line No.");

      EXIT(0);
    END;

    BEGIN
    {
      Table to handle Json message.
      2017-01 by Aart Tulp
    }
    END.
  }
}

