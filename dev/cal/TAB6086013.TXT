OBJECT Table 6086013 Vendor Documents
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.4.06;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Kreditorenbelege;
               ENU=Vendor Documents;
               NLD=Leveranciersdocumenten];
  }
  FIELDS
  {
    { 1   ;   ;Type                ;Option        ;CaptionML=[DEU=Belegart;
                                                              ENU=Document Type;
                                                              NLD=Documentsoort;
                                                              NOR=Bilagstype;
                                                              SVE=Dokumenttyp];
                                                   OptionCaptionML=[DEU=Offener Einkaufsbeleg,Gebuchter Einkaufsbeleg,Gebuchte Einkaufsgutschrift;
                                                                    ENU=Open Purchase Document,Posted Purchase Invoice,Posted Purchase Cr. Memo;
                                                                    NLD=Open inkoopdocument,Geboekte inkoopfactuur,Geboekte creditnota];
                                                   OptionString=Open Purchase Document,Posted Purchase Invoice,Posted Purchase Cr. Memo }
    { 2   ;   ;Document Type       ;Option        ;CaptionML=[DEU=Belegnr.;
                                                              ENU=Document No.;
                                                              NLD=Documentnr.;
                                                              NOR=Bilagsnr.;
                                                              SVE=Dokumentnr];
                                                   OptionCaptionML=[DEU=Angebot,Auftrag,Rechnung,Gutschrift,Rahmenauftrag,Reklamation;
                                                                    ENU=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order;
                                                                    NLD=Offerte,Order,Factuur,Creditnota,Raamcontract,Retourorder;
                                                                    NOR=Tilbud,Ordre,Faktura,Kreditnota,Rammeordre,Returordre;
                                                                    SVE=Offert,Order,Faktura,Kreditnota,Avropsorder,Returorder];
                                                   OptionString=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order }
    { 3   ;   ;Document No.        ;Code20        ;CaptionML=[DEU=Belegnr.;
                                                              ENU=Document No.;
                                                              NLD=Documentnr.;
                                                              NOR=Bilagsnr.;
                                                              SVE=Dokumentnr] }
    { 4   ;   ;Amount Excl. VAT    ;Decimal       ;CaptionML=[DEU=Betrag ohne MwSt.;
                                                              ENU=Amount Excl. VAT;
                                                              NLD=Bedrag excl. btw;
                                                              NOR=Bel›p eks. mva;
                                                              SVE=Belopp exklusive moms] }
    { 5   ;   ;Amount Incl. VAT    ;Decimal       ;CaptionML=[DEU=Betrag inkl. MwSt.;
                                                              ENU=Amount Incl. VAT;
                                                              NLD=Bedrag incl. btw;
                                                              NOR=Bel›p inkl. mva.;
                                                              SVE=Belopp inkl moms] }
    { 6   ;   ;Date                ;Date          ;CaptionML=[DEU=Datum;
                                                              ENU=Date;
                                                              NLD=Datum;
                                                              NOR=Dato;
                                                              SVE=Datum] }
    { 7   ;   ;Latest Comment      ;Text250       ;CaptionML=[DEU=Letzter Kommentar;
                                                              ENU=Latest Comment;
                                                              NLD=Laatste opmerking] }
    { 8   ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=[DEU=W„hrungscode;
                                                              ENU=Currency Code;
                                                              NLD=Valutacode;
                                                              NOR=Valutakode;
                                                              SVE=Valutakod] }
    { 9   ;   ;Fully Applied       ;Boolean       ;CaptionML=[DEU=Vollst„ndig angewendet;
                                                              ENU=Fully Applied;
                                                              NLD=Volledig vereffend] }
    { 10  ;   ;On Hold             ;Code3         ;CaptionML=[DEU=Abwarten;
                                                              ENU=On Hold;
                                                              NLD=Afwachten;
                                                              NOR=Avvent;
                                                              SVE=Stoppad] }
  }
  KEYS
  {
    {    ;Type,Document Type,Document No.         ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE UpdateTable@1160040000(VendorNo@1160040000 : Code[20]);
    VAR
      PurchHeader@1160040001 : Record 38;
      PurchInvHeader@1160040002 : Record 122;
      PurchCrMemoHeader@1160040003 : Record 124;
      VendLedgEntry@1160040004 : Record 25;
      PurchDocMgt@1160040005 : Codeunit 6085709;
      AmountExclVAT@1160040006 : Decimal;
      AmountInclVAT@1160040007 : Decimal;
      LineAmountExclVAT@1160040008 : Decimal;
      LineAmountInclVAT@1160040009 : Decimal;
    BEGIN
      DELETEALL;

      PurchHeader.SETCURRENTKEY("Buy-from Vendor No.");
      PurchHeader.SETRANGE("Buy-from Vendor No.",VendorNo);
      PurchHeader.SETFILTER("Document Type",'%1|%2|%3|%4',PurchHeader."Document Type"::Order,PurchHeader."Document Type"::Invoice,
        PurchHeader."Document Type"::"Return Order",PurchHeader."Document Type"::"Credit Memo");
      IF PurchHeader.FINDSET THEN
        REPEAT
          PurchDocMgt.GetPurchStatusAmounts(PurchHeader,AmountExclVAT,AmountInclVAT,LineAmountExclVAT,LineAmountInclVAT);

          Type := Type::"Open Purchase Document";
          "Document Type" := PurchHeader."Document Type";
          "Document No." := PurchHeader."No.";
          "Currency Code" := PurchHeader."Currency Code";
          IF AmountExclVAT <> 0 THEN BEGIN
            "Amount Excl. VAT" := AmountExclVAT;
            "Amount Incl. VAT" := AmountInclVAT
          END ELSE BEGIN
            "Amount Excl. VAT" := LineAmountExclVAT;
            "Amount Incl. VAT" := LineAmountInclVAT;
          END;
          Date := PurchHeader."Document Date";
          "Latest Comment" := GetLastAppvlComment(DATABASE::"Purchase Header",PurchHeader."Document Type",PurchHeader."No.");
          INSERT(TRUE);
        UNTIL PurchHeader.NEXT = 0;


      PurchInvHeader.SETCURRENTKEY("Buy-from Vendor No.");
      PurchInvHeader.SETRANGE("Buy-from Vendor No.",VendorNo);
      IF PurchInvHeader.FINDSET THEN
        REPEAT
          PurchInvHeader.CALCFIELDS(Amount,"Amount Including VAT");

          Type := Type::"Posted Purchase Invoice";
          "Document Type" := 0;
          "Document No." := PurchInvHeader."No.";
          "Currency Code" := PurchInvHeader."Currency Code";
          "Amount Excl. VAT" := PurchInvHeader.Amount;
          "Amount Incl. VAT" := PurchInvHeader."Amount Including VAT";
          Date := PurchInvHeader."Document Date";
          "Latest Comment" := GetLastPostAppvlComment(DATABASE::"Purch. Inv. Header",PurchInvHeader."No.");

          IF FindVendLedgEntry(VendorNo,VendLedgEntry."Document Type"::Invoice,PurchInvHeader."No.",VendLedgEntry) THEN BEGIN
            "Fully Applied" := NOT VendLedgEntry.Open;
            "On Hold" := VendLedgEntry."On Hold";
          END ELSE BEGIN
            "Fully Applied" := FALSE;
            "On Hold" := '';
          END;

          INSERT(TRUE);
        UNTIL PurchInvHeader.NEXT = 0;


      PurchCrMemoHeader.SETCURRENTKEY("Buy-from Vendor No.");
      PurchCrMemoHeader.SETRANGE("Buy-from Vendor No.",VendorNo);
      IF PurchCrMemoHeader.FINDSET THEN
        REPEAT
          PurchCrMemoHeader.CALCFIELDS(Amount,"Amount Including VAT");

          Type := Type::"Posted Purchase Cr. Memo";
          "Document Type" := 0;
          "Document No." := PurchCrMemoHeader."No.";
          "Currency Code" := PurchCrMemoHeader."Currency Code";
          "Amount Excl. VAT" := PurchCrMemoHeader.Amount;
          "Amount Incl. VAT" := PurchCrMemoHeader."Amount Including VAT";
          Date := PurchCrMemoHeader."Document Date";
          "Latest Comment" := GetLastPostAppvlComment(DATABASE::"Purch. Cr. Memo Hdr.",PurchCrMemoHeader."No.");

          IF FindVendLedgEntry(VendorNo,VendLedgEntry."Document Type"::"Credit Memo",PurchCrMemoHeader."No.",VendLedgEntry) THEN BEGIN
            "Fully Applied" := NOT VendLedgEntry.Open;
            "On Hold" := VendLedgEntry."On Hold";
          END ELSE BEGIN
            "Fully Applied" := FALSE;
            "On Hold" := '';
          END;

          INSERT(TRUE);
         UNTIL PurchCrMemoHeader.NEXT = 0;

      IF FINDFIRST THEN;
    END;

    PROCEDURE GetLastAppvlComment@1160040006(TableID@1160040002 : Integer;DocType@1160040001 : Integer;DocNo@1160040003 : Code[20]) : Text[1024];
    VAR
      ApprvlCmtLine@1160040000 : Record 455;
    BEGIN
      ApprvlCmtLine.SETCURRENTKEY("Table ID","Document Type","Document No.");
      ApprvlCmtLine.SETRANGE("Table ID",TableID);
      ApprvlCmtLine.SETRANGE("Document Type",DocType);
      ApprvlCmtLine.SETRANGE("Document No.",DocNo);
      IF ApprvlCmtLine.FINDLAST THEN
        EXIT(ApprvlCmtLine.Comment);
    END;

    PROCEDURE GetLastPostAppvlComment@1160040004(TableID@1160040002 : Integer;DocNo@1160040003 : Code[20]) : Text[1024];
    VAR
      PostApprvlCmtLine@1160040000 : Record 457;
    BEGIN
      PostApprvlCmtLine.SETCURRENTKEY("Table ID","Document No.");
      PostApprvlCmtLine.SETRANGE("Table ID",TableID);
      PostApprvlCmtLine.SETRANGE("Document No.",DocNo);
      IF PostApprvlCmtLine.FINDLAST THEN
        EXIT(PostApprvlCmtLine.Comment);
    END;

    PROCEDURE FindVendLedgEntry@1160040001(VendorNo@1160040003 : Code[20];DocType@1160040001 : Integer;DocNo@1160040002 : Code[20];VAR VendLedgEntry@1160040004 : Record 25) : Boolean;
    BEGIN
      VendLedgEntry.SETCURRENTKEY("Document No.");
      VendLedgEntry.SETRANGE("Document No.",DocNo);
      VendLedgEntry.SETRANGE("Document Type",DocType);
      VendLedgEntry.SETRANGE("Vendor No.",VendorNo);
      EXIT(VendLedgEntry.FINDFIRST);
    END;

    BEGIN
    END.
  }
}

