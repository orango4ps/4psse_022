OBJECT Codeunit 11124927 Create Demo Data WHA
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1100526002 : TextConst 'DEU=Anmeldedaten fÅr die Lager App;ENU=Login details for the Warehouse App;NLD=Login-gegevens voor de Magazijn App';
      Text001@1100526001 : TextConst
        'DEU="Sehr geehrte/r%1,%4 %4vielen Dank, dass Sie ein Demokonto eingerichtet haben.%4Unten finden Sie die Anweisungen fÅr die Inbetriebnahme der 4PS Lagerhaus App: %4%4 1. Laden Sie die App im Store herunter (<a href=""https://play.google.com/store/apps/details?id=com.fps.wha"">Android</a>, <a href=""https://itunes.apple.com/nl/app/4ps-magazijn/id1445161075"">iOS</a>, <a href="""">UWP, Windows</a>))%4%4 2. Nach dem Installieren, klicken Sie <a href=""https://wha.4ps.nl/login/?username=%2&proxy=%5≥>hier (Android)</a> oder <a href=""fps.wha://login/?username=%2&proxy=%5≥>hier (iOS, UWP, Window)</a>, um mit der App zu beginnen.%4 <i>Die App îffnet sich und die Verbindung mit dem Back-Office wird hergestellt.</i>%4Sie finden einen Arbeits-Link auf <a href=""%5≥>dieser Seite,</a> falls der Link oben nicht funktioniert (das ist bei einigen E-Mail-Clients der Fall).%4%4"',
        'ENU="Dear %1,%4 %4Thank you for creating a demo account.%4Below you can find instructions on how to start using the 4PS Warehouse App: %4%4 1. Download the app from the store (<a href=""https://play.google.com/store/apps/details?id=com.fps.wha"">Android</a>, <a href=""https://itunes.apple.com/nl/app/4ps-magazijn/id1445161075"">iOS</a>, <a href="""">UWP, Windows</a>))%4%4 2. After installation click <a href=""https://wha.4ps.nl/login/?username=%2&proxy=%5"">here (Android)</a> or <a href=""fps.wha://login/?username=%2&proxy=%5"">here (iOS, UWP, Window)</a>  to start the app.%4 <i>The app opens and the connection with the back-office will be set.</i>%4You can find a working link on <a href=""%5"">this page</a> of the above link doesn''t work (which is the case in some mail clients).%4%4"',
        'NLD="Beste %1,%4 %4Bedankt voor het aanvragen van een demo account.%4Hieronder de instructies die u moet doorlopen om gebruik te maken van de 4PS Magazijn App: %4%4 1. Download de 4PS Magazijn app vanuit de store (<a href=""https://play.google.com/store/apps/details?id=com.fps.wha"">Android</a>, <a href=""https://itunes.apple.com/nl/app/4ps-magazijn/id1445161075"">iOS</a>, <a href="""">UWP, Windows</a>))%4%4 2. Na installatie klik <a href=""https://wha.4ps.nl/login/?username=%2&proxy=%5"">hier (Android)</a> of <a href=""fps.wha://login/?username=%2&proxy=%5"">hier (iOS, UWP, Window)</a>  om de app te starten.%4 <i>De app opent automatisch en de connectie met de demo-omgeving wordt gezet in de app.</i>%4Als bovenstaande link niet werkt (is het geval in sommige mail clients) kunt u op <a href=""%5"">deze pagina</a> een link vinden die het wel doet.%4%4"';
      Text002@1100526000 : TextConst 'DEU=3. Melden Sie sich mit folgenden Referenzen an:%4Benutzername: %2%4Passwort: %3%4%4Mit freundlichen GrÅ·en%4 4PS;ENU=3. Log in with the following credentials:%4User Name: %2%4Password: %3%4%4With kind regards,%4 4PS;NLD=3. Log in op de app met de volgende gegevens:%4Gebruikersnaam: %2%4Wachtwoord: %3%4%4Met vriendelijke groeten,%4 4PS';
      MailTemplateManagement@1100525000 : Codeunit 11012622;

    PROCEDURE CreateDemoUser@1100525000(Employee@1100525001 : Record 5200) : Boolean;
    VAR
      Resource@1100526001 : Record 11229278;
      AppUserGAP@1100526000 : Record 11124975;
      AppUserSetupGAP@1100526002 : Record 11124976;
      ProductsGAP@1100526003 : Record 11124990;
      Password@1100526004 : Text;
    BEGIN
      IF NOT Resource.GET(Employee."No.") THEN BEGIN
        Resource.INIT;
        Resource."No." := Employee."No.";
        Resource.VALIDATE(Name, Employee.FullName);
        Resource.VALIDATE(Company, COMPANYNAME);
        Resource.VALIDATE(Type, Resource.Type::Employee);
        Resource.VALIDATE("Source No.", Employee."No.");
        Resource.VALIDATE("Synchronize FSA", Resource."Synchronize FSA"::"Changes Only");
        Resource.INSERT(TRUE);
      END;

      IF NOT AppUserGAP.GET(Employee."No.") THEN BEGIN
        AppUserGAP.Username := Employee."No.";
        AppUserGAP.VALIDATE(Name, Employee.FullName);
        AppUserGAP.VALIDATE(Email, Employee."Company E-Mail");
        AppUserGAP.INSERT(TRUE);
      END;

      Password := GeneratePassword;
      AppUserGAP.VALIDATE(Password, Password);
      AppUserGAP.VALIDATE(Active, TRUE);
      AppUserGAP.MODIFY(TRUE);

      AppUserSetupGAP.GET(AppUserGAP.Username);
      AppUserSetupGAP.VALIDATE("Resource No.", Resource."No.");
      AppUserSetupGAP.MODIFY(TRUE);

      CreateWarehouseOrdersForResource(Resource."No.");

      COMMIT;
      SendEmail(AppUserGAP, Password);

      EXIT(TRUE);
    END;

    PROCEDURE CreateWarehouseOrdersForResource@1100526004(ResourceNo@1100526000 : Code[20]);
    VAR
      WarehouseOrder@1100526001 : Record 11126687;
    BEGIN
      CreateWarehouseOrdersForResource2(ResourceNo, WarehouseOrder.Process::"Project Delivery");
      CreateWarehouseOrdersForResource2(ResourceNo, WarehouseOrder.Process::"Project Retour");
    END;

    LOCAL PROCEDURE CreateWarehouseOrdersForResource2@1100526000(ResourceNo@1100526000 : Code[20];Process@1100526007 : Option);
    VAR
      WarehouseOrder1@1100526002 : Record 11126687;
      WarehouseOrder2@1100526005 : Record 11126687;
      WarehouseOrderLine1@1100526001 : Record 11126688;
      WarehouseOrderLine2@1100526006 : Record 11126688;
      Count@1100526003 : Integer;
      RequestedNofOrders@1100526004 : Integer;
    BEGIN
      RequestedNofOrders := 2;
      WarehouseOrder1.SETFILTER("No.", '%1', 'DEMO*');
      WarehouseOrder1.SETFILTER("Project No.", '<>%1', '');
      //WarehouseOrder1.SETFILTER(Element, '<>%1', '');
      WarehouseOrder1.SETFILTER("Location Code", '<>%1', '');
      WarehouseOrder1.SETRANGE(Process, Process);
      IF WarehouseOrder1.FINDSET THEN BEGIN
        REPEAT
          WarehouseOrder2.INIT;
          WarehouseOrder2."No." := '';
          WarehouseOrder2.VALIDATE(Process, WarehouseOrder1.Process);
          WarehouseOrder2.VALIDATE("Project No.", WarehouseOrder1."Project No.");
          WarehouseOrder2.VALIDATE(Element, WarehouseOrder1.Element);
          WarehouseOrder2.VALIDATE("Location Code", WarehouseOrder1."Location Code");
          WarehouseOrder2.VALIDATE("Picker No.", ResourceNo);
          WarehouseOrder2.INSERT(TRUE);
          WarehouseOrderLine1.SETRANGE("Order No.", WarehouseOrder1."No.");
          IF WarehouseOrderLine1.FINDSET THEN BEGIN
            REPEAT
              WarehouseOrderLine2.INIT;
              WarehouseOrderLine2.VALIDATE("Order No.", WarehouseOrder2."No.");
              WarehouseOrderLine2.VALIDATE("Line No.", WarehouseOrderLine1."Line No.");
              WarehouseOrderLine2.VALIDATE("Item No.", WarehouseOrderLine1."Item No.");
              WarehouseOrderLine2.VALIDATE("Requested Quantity", 1);
              WarehouseOrderLine2.INSERT(TRUE);
            UNTIL WarehouseOrderLine1.NEXT = 0;
          END;
          Count += 1;
        UNTIL (WarehouseOrder1.NEXT = 0) OR (Count = RequestedNofOrders);
      END;
    END;

    LOCAL PROCEDURE GeneratePassword@1100528302() : Text;
    VAR
      SetupGAP@1100526000 : Record 11124982;
      StringFunctions@1100525000 : Codeunit 11012273;
      PasswordPlain@1100525001 : Text;
      Length@1100526001 : Integer;
    BEGIN
      SetupGAP.GET;
      Length := SetupGAP."Minimum Password Length";
      IF Length < 8 THEN
        Length := 8;
      StringFunctions.GeneratePasswordPlainText(PasswordPlain, Length);
      EXIT(PasswordPlain);
    END;

    LOCAL PROCEDURE SendEmail@1100528101(AppUserGAP@1100526000 : Record 11124975;Password@1100526001 : Text);
    VAR
      MailTemplate@1100525000 : Record 11229450;
      BodyText@1100528100 : Text;
      SMTPMail@1100528102 : Codeunit 400;
      Subject@1100525001 : Text;
      MailTemplateNo@1100525002 : Integer;
    BEGIN
      MailTemplateNo := 75;
      Subject := Text000;
      MailTemplateManagement.GetMailTemplate(MailTemplate, MailTemplateNo, '');
      IF (MailTemplate."Use Mail Template Subject Text") THEN
        Subject := MailTemplateManagement.GetLayoutTextSubstitutedSubjectOnly(MailTemplateNo, '', AppUserGAP);
      BodyText := MailTemplateManagement.GetLayoutTextSubstitutedTotalBodyOnly(MailTemplateNo, '', AppUserGAP);
      IF (BodyText = '') THEN
        BodyText := GetBodyText(AppUserGAP, Password);

      SMTPMail.CreateMessage(
        MailTemplate."From Name",
        MailTemplate.From,
        AppUserGAP.Email,
        Subject,
        BodyText,
        TRUE);

      SMTPMail.Send;
    END;

    LOCAL PROCEDURE GetBodyText@1100528106(AppUserGAP@1100526000 : Record 11124975;Password@1100526001 : Text) : Text;
    VAR
      EmpolySetup@1100526002 : Record 11126692;
      BodyText@1100528102 : Text;
      StringFunctions@1100525003 : Codeunit 11012273;
    BEGIN
      EmpolySetup.GET;
      BodyText := STRSUBSTNO(Text001, AppUserGAP.Name, AppUserGAP.Username, Password, STRSUBSTNO('%1%2', StringFunctions.CarriageReturn, StringFunctions.LineFeed), EmpolySetup."Proxy URL");
      BodyText := BodyText + STRSUBSTNO(Text002, AppUserGAP.Name, AppUserGAP.Username, Password, STRSUBSTNO('%1%2', StringFunctions.CarriageReturn, StringFunctions.LineFeed), EmpolySetup."Proxy URL");
      EXIT(BodyText);
    END;

    BEGIN
    END.
  }
}

