OBJECT Page 11126563 Common Document Properties
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Gemeinsame Dokumenteneigenschaften;
               ENU=Common Document Properties;
               NLD=Gemeenschappelijke documenteigenschappen];
    PopulateAllFields=Yes;
    PageType=StandardDialog;
  }
  CONTROLS
  {
    { 1100528700;;Container;
                ContainerType=ContentArea }

    { 1100528725;1;Group  ;
                CaptionML=[DEU=Belegeigenschaften;
                           ENU=Document Properties;
                           NLD=Documenteigenschappen;
                           NOR=Dokumentegenskaper;
                           SVE=Dokumentegenskaper];
                GroupType=Group }

    { 1100528701;2;Field  ;
                SourceExpr=DocumentProperties.Description;
                CaptionClass=DocumentProperties.FIELDCAPTION(Description) }

    { 1100528718;2;Field  ;
                SourceExpr=DocumentProperties.Reference;
                CaptionClass=DocumentProperties.FIELDCAPTION(Reference) }

    { 1100528719;2;Field  ;
                SourceExpr=DocumentProperties.Subject;
                CaptionClass=DocumentProperties.FIELDCAPTION(Subject) }

    { 1100528702;2;Field  ;
                SourceExpr=DocumentProperties."Contact No.";
                CaptionClass=DocumentProperties.FIELDCAPTION("Contact No.");
                OnValidate=BEGIN
                             ValidateContactNo;
                           END;

                OnLookup=VAR
                           Contact@1100528700 : Record 5050;
                         BEGIN
                           IF Contact.GET(DocumentProperties."Contact No.") THEN;
                           Contact.SETFILTER("Person No.", '%1', '');
                           IF PAGE.RUNMODAL(0, Contact) = ACTION::LookupOK THEN BEGIN
                             DocumentProperties."Contact No." := Contact."No.";
                             ValidateContactNo;
                           END;
                         END;
                          }

    { 1100528704;2;Field  ;
                SourceExpr=DocumentProperties."Contact Person No.";
                CaptionClass=DocumentProperties.FIELDCAPTION("Contact Person No.");
                OnValidate=BEGIN
                             ValidateContactPersonNo;
                           END;

                OnLookup=VAR
                           Contact@1100528700 : Record 5050;
                         BEGIN
                           IF Contact.GET(DocumentProperties."Contact Person No.") THEN;
                           Contact.SETRANGE(Type, Contact.Type::Person);
                           Contact.SETRANGE("Company No.", DocumentProperties."Contact No.");
                           IF PAGE.RUNMODAL(0, Contact) = ACTION::LookupOK THEN BEGIN
                             DocumentProperties."Contact Person No." := Contact."No.";
                             ValidateContactPersonNo;
                           END;
                         END;
                          }

    { 1100528713;2;Field  ;
                SourceExpr=DocumentProperties."Internal Company";
                CaptionClass=DocumentProperties.FIELDCAPTION("Internal Company");
                Enabled=FALSE;
                Editable=FALSE }

    { 1100528714;2;Field  ;
                SourceExpr=DocumentProperties."Addressed To";
                CaptionClass=DocumentProperties.FIELDCAPTION("Addressed To");
                TableRelation=Employee }

    { 1100528715;2;Field  ;
                SourceExpr=DocumentProperties."Original To";
                CaptionClass=DocumentProperties.FIELDCAPTION("Original To");
                TableRelation=Employee }

    { 1100528706;2;Field  ;
                SourceExpr=DocumentProperties."Document Type";
                CaptionClass=DocumentProperties.FIELDCAPTION("Document Type");
                OnValidate=BEGIN
                             ValidateDocumentType;
                           END;

                OnLookup=VAR
                           DocumentType@1100528701 : Record 11012405;
                         BEGIN
                           IF DocumentType.GET(DocumentProperties."Document Type") THEN;
                           IF PAGE.RUNMODAL(0, DocumentType) = ACTION::LookupOK THEN BEGIN
                             DocumentProperties."Document Type" := DocumentType.Type;
                             ValidateDocumentType;
                           END;
                         END;
                          }

    { 1100528708;2;Field  ;
                SourceExpr=DocumentProperties."Document Category";
                CaptionClass=DocumentProperties.FIELDCAPTION("Document Category");
                TableRelation="Document Category" }

    { 1100528711;2;Field  ;
                SourceExpr=DocumentProperties."Project Element";
                CaptionClass=DocumentProperties.FIELDCAPTION("Project Element");
                OnValidate=BEGIN
                             ValidateProjectElement;
                           END;

                OnLookup=VAR
                           BaseElement@1100528704 : Record 11012060;
                           ProjectElement@1100528703 : Record 11012010;
                           Job@1100528301 : Record 11072003;
                           Elements@1100528702 : Page 11012118;
                           ProjectElements@1100528701 : Page 11012028;
                         BEGIN
                           CASE RecId.TABLENO OF
                             DATABASE::Job: BEGIN
                               Job.GET(GetSourceCodeFromRecId(RecId ));
                               ProjectElement.SETRANGE("Project No.", Job."No.");
                               ProjectElements.SETTABLEVIEW(ProjectElement);
                               ProjectElement."Project No." := Job."No.";
                               ProjectElement.Element := DocumentProperties."Project Element";
                               ProjectElements.SETRECORD(ProjectElement);
                               ProjectElements.LOOKUPMODE := TRUE;
                               IF ProjectElements.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                 ProjectElements.GETRECORD(ProjectElement);
                                 DocumentProperties."Project Element" := ProjectElement.Element;
                                 ValidateProjectElement;
                               END;
                             END
                             ELSE BEGIN
                               BaseElement.Code := DocumentProperties."Project Element";
                               Elements.SETRECORD(BaseElement);
                               Elements.LOOKUPMODE := TRUE;
                               IF Elements.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                 Elements.GETRECORD(BaseElement);
                                 DocumentProperties."Project Element" := BaseElement.Code;
                                 ValidateProjectElement;
                               END;
                             END;
                           END;
                         END;
                          }

    { 1100528400;2;Field  ;
                SourceExpr=DocumentProperties.Status;
                CaptionClass=DocumentProperties.FIELDCAPTION(Status) }

    { 1100528720;2;Field  ;
                SourceExpr=DocumentProperties."Creation Date";
                CaptionClass=DocumentProperties.FIELDCAPTION("Creation Date") }

    { 1100528721;2;Field  ;
                SourceExpr=DocumentProperties."Creation Time";
                CaptionClass=DocumentProperties.FIELDCAPTION("Creation Time") }

    { 1100528722;2;Field  ;
                SourceExpr=DocumentProperties."Document Date";
                CaptionClass=DocumentProperties.FIELDCAPTION("Document Date") }

    { 1100528723;2;Field  ;
                SourceExpr=DocumentProperties."Version External Doc.";
                CaptionClass=DocumentProperties.FIELDCAPTION("Version External Doc.") }

  }
  CODE
  {
    VAR
      DocumentProperties@1100528701 : TEMPORARY Record 11012746;
      Text008@1100528700 : TextConst 'DEU=Geben Sie hier Kontakt %1 und die Kontaktperson in Feld %2 ein.;ENU=Enter contact %1 here, and enter contact person in field %2.;NLD=Voer hier het bedrijf %1 in, en geef de contactpersoon op in het veld %2.;NOR=Angi kontakten %1 her, og angi kontaktperson i feltet ''Person''.;SVE=Ange kontakten %1 h„r, och ange kontaktperson i f„ltet ''Person''.';
      RecId@1100528702 : RecordID;

    PROCEDURE SetDocumentProperties@1100528700(VAR DocumentPropertiesInOut@1100528700 : Record 11012746);
    BEGIN
      DocumentProperties.COPY(DocumentPropertiesInOut);
      DocumentProperties.Status := DocumentProperties.Status::Concept;
      DocumentProperties.INSERT;
    END;

    PROCEDURE GetDocumentProperties@1100528702(VAR DocumentPropertiesInOut@1100528700 : Record 11012746);
    BEGIN
      DocumentPropertiesInOut.COPY(DocumentProperties);
    END;

    PROCEDURE SetRecId@1100528706(RecIdIn@1100528700 : RecordID;VAR DocumentPropertiesInOut@1100528300 : Record 11012746);
    VAR
      Job@1100528301 : Record 11072003;
    BEGIN
      RecId := RecIdIn;
      CASE RecId.TABLENO OF
        DATABASE::Job: BEGIN
          Job.GET(GetSourceCodeFromRecId(RecId ));
          DocumentProperties."Document Portal Source Type" := DocumentProperties."Document Portal Source Type"::Project;
          DocumentProperties."Document Portal Source No." := Job."No.";
        END;
      END;
    END;

    LOCAL PROCEDURE ValidateContactNo@1100528703();
    VAR
      Contact@1100528701 : Record 5050;
    BEGIN
      IF DocumentProperties."Contact No." <> '' THEN BEGIN
        Contact.GET(DocumentProperties."Contact No.");
        IF Contact.IsContactPerson THEN
           ERROR(Text008, Contact."Company No.", DocumentProperties.FIELDCAPTION("Contact Person No."));
        DocumentProperties."Contact Name" := Contact.Name;
        DocumentProperties."Contact Name 2" := Contact."Name 2";
        DocumentProperties."Document Group" := Contact."Document Group";
      END ELSE BEGIN
        DocumentProperties."Contact Name" := '';
        DocumentProperties."Contact Name 2" := '';
        DocumentProperties."Document Group" := '';
      END;
      DocumentProperties."Contact Person No." := '';
      ValidateContactPersonNo;
    END;

    LOCAL PROCEDURE ValidateContactPersonNo@1100528704();
    VAR
      Contact@1100528700 : Record 5050;
    BEGIN
      IF DocumentProperties."Contact Person No." <> '' THEN BEGIN
        Contact.SETRANGE(Type, Contact.Type::Person);
        Contact.SETRANGE("Company No.", DocumentProperties."Contact No.");
        Contact.SETRANGE("No.", DocumentProperties."Contact Person No.");
        Contact.FINDFIRST;
        DocumentProperties."Contact Person Name" := Contact.Name;
        DocumentProperties."Contact Person Name 2" := Contact."Name 2";
        IF Contact."Company No." = '' THEN
          DocumentProperties."Document Group" := Contact."Document Group";
      END ELSE BEGIN
        DocumentProperties."Contact Person Name" := '';
        DocumentProperties."Contact Person Name 2" := '';
      END;
    END;

    LOCAL PROCEDURE ValidateDocumentType@1100528701();
    VAR
      DocumentType@1100528701 : Record 11012405;
    BEGIN
      IF DocumentProperties."Document Type" <> '' THEN BEGIN
        DocumentType.GET(DocumentProperties."Document Type");
      END;
    END;

    LOCAL PROCEDURE ValidateProjectElement@1100528705();
    VAR
      Job@1100528300 : Record 11072003;
      BaseElement@1100528701 : Record 11012060;
      ProjectElement@1100528700 : Record 11012010;
    BEGIN
      IF DocumentProperties."Project Element" <> '' THEN BEGIN
        CASE RecId.TABLENO OF
          DATABASE::Job: BEGIN
            Job.GET(GetSourceCodeFromRecId(RecId ));
            ProjectElement.GET(Job."No.", DocumentProperties."Project Element");
          END;
          ELSE
            BaseElement.GET(DocumentProperties."Project Element");
        END;
      END;
    END;

    LOCAL PROCEDURE GetSourceCodeFromRecId@1100528300(RecId@1100528300 : RecordID) : Code[20];
    VAR
      Job@1100528301 : Record 11072003;
      RecRef@1100528302 : RecordRef;
    BEGIN
      CASE RecId.TABLENO OF
        DATABASE::Job: BEGIN
          RecRef.GET(RecId);
          RecRef.SETTABLE(Job);
          EXIT(Job."No.");
        END;
      END;
      EXIT('');
    END;

    BEGIN
    END.
  }
}

