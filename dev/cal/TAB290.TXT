OBJECT Table 290 VAT Amount Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04,4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=VAT Amount Line;
               NOR=Mva-bel›pslinje;
               SVE=Momsbelopp rad];
  }
  FIELDS
  {
    { 1   ;   ;VAT %               ;Decimal       ;CaptionML=[ENU=VAT %;
                                                              NOR=Mva-%;
                                                              SVE=Momssats %];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 2   ;   ;VAT Base            ;Decimal       ;CaptionML=[ENU=VAT Base;
                                                              NOR=Mva-grunnlag;
                                                              SVE=Nettobelopp];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 3   ;   ;VAT Amount          ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("VAT %");
                                                                TESTFIELD("VAT Base");
                                                                IF "VAT Amount" / "VAT Base" < 0 THEN
                                                                  ERROR(Text002,FIELDCAPTION("VAT Amount"));
                                                                "VAT Difference" := "VAT Amount" - "Calculated VAT Amount";
                                                              END;

                                                   CaptionML=[ENU=VAT Amount;
                                                              NOR=Mva-bel›p;
                                                              SVE=Momsbelopp];
                                                   AutoFormatType=1 }
    { 4   ;   ;Amount Including VAT;Decimal       ;CaptionML=[ENU=Amount Including VAT;
                                                              NOR=Bel›p inkl. mva.;
                                                              SVE=Belopp inkl. moms];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 5   ;   ;VAT Identifier      ;Code20        ;CaptionML=[ENU=VAT Identifier;
                                                              NOR=Mva-type;
                                                              SVE=Moms-ID];
                                                   Editable=No }
    { 6   ;   ;Line Amount         ;Decimal       ;CaptionML=[ENU=Line Amount;
                                                              NOR=Linjebel›p;
                                                              SVE=Radbelopp];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 7   ;   ;Inv. Disc. Base Amount;Decimal     ;CaptionML=[ENU=Inv. Disc. Base Amount;
                                                              NOR=Fakt.rab.grunnlag;
                                                              SVE=Fakt.rabatt nettobelopp];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 8   ;   ;Invoice Discount Amount;Decimal    ;OnValidate=BEGIN
                                                                TESTFIELD("Inv. Disc. Base Amount");
                                                                IF "Invoice Discount Amount" / "Inv. Disc. Base Amount" > 1 THEN
                                                                  ERROR(
                                                                    InvoiceDiscAmtIsGreaterThanBaseAmtErr,
                                                                    FIELDCAPTION("Invoice Discount Amount"),"Inv. Disc. Base Amount");
                                                                "VAT Base" := CalcLineAmount;
                                                              END;

                                                   CaptionML=[ENU=Invoice Discount Amount;
                                                              NOR=Fakturarabattbel›p;
                                                              SVE=Fakturarabatt];
                                                   AutoFormatType=1 }
    { 9   ;   ;VAT Calculation Type;Option        ;CaptionML=[ENU=VAT Calculation Type;
                                                              NOR=Mva-beregningstype;
                                                              SVE=Momsber„kningstyp];
                                                   OptionCaptionML=[ENU=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax;
                                                                    NOR=Normal mva.,Snudd avregning,Full mva.,Salgsmva.;
                                                                    SVE=Normal moms,Omv„nd EG-moms,Enbart moms,Oms„ttningsskatt];
                                                   OptionString=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax;
                                                   Editable=No }
    { 10  ;   ;Tax Group Code      ;Code20        ;TableRelation="Tax Group";
                                                   CaptionML=[ENU=Tax Group Code;
                                                              NOR=Mva-gruppekode;
                                                              SVE=Skattegruppskod];
                                                   Editable=No }
    { 11  ;   ;Quantity            ;Decimal       ;CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 12  ;   ;Modified            ;Boolean       ;CaptionML=[ENU=Modified;
                                                              NOR=Endret;
                                                              SVE=Žndrad] }
    { 13  ;   ;Use Tax             ;Boolean       ;CaptionML=[ENU=Use Tax;
                                                              NOR=Amerikansk mva.;
                                                              SVE=Anv„nd skatt] }
    { 14  ;   ;Calculated VAT Amount;Decimal      ;CaptionML=[ENU=Calculated VAT Amount;
                                                              NOR=Beregnet mva-bel›p;
                                                              SVE=Ber„knat momsbelopp];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 15  ;   ;VAT Difference      ;Decimal       ;CaptionML=[ENU=VAT Difference;
                                                              NOR=Mva-differanse;
                                                              SVE=Momsdifferens];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 16  ;   ;Positive            ;Boolean       ;CaptionML=[ENU=Positive;
                                                              NOR=Positiv;
                                                              SVE=Positiv] }
    { 17  ;   ;Includes Prepayment ;Boolean       ;CaptionML=[ENU=Includes Prepayment;
                                                              NOR=Inkluderer forskudd;
                                                              SVE=Inklusive f”rskottsbetalning] }
    { 18  ;   ;VAT Clause Code     ;Code20        ;TableRelation="VAT Clause";
                                                   CaptionML=[ENU=VAT Clause Code;
                                                              NOR=Mva-setningskode;
                                                              SVE=Kod f”r momsklausul] }
    { 19  ;   ;Tax Category        ;Code10        ;CaptionML=[ENU=Tax Category;
                                                              NOR=Avgiftskategori;
                                                              SVE=Momskategori] }
    { 20  ;   ;Pmt. Discount Amount;Decimal       ;CaptionML=[ENU=Pmt. Discount Amount;
                                                              NOR=Kontantrabattbel›p];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 11012000;;Reverse Charge VAT ;Boolean       ;CaptionML=[ENU=Reverse Charge VAT;
                                                              SVE=terf”rt till„gg];
                                                   Editable=No }
    { 11012010;;Currency Code      ;Code10        ;TableRelation=Currency;
                                                   CaptionML=[ENU=Currency;
                                                              SVE=Valuta];
                                                   Editable=No }
    { 11012020;;VAT manually       ;Boolean       ;CaptionML=[ENU=VAT manually;
                                                              SVE=Moms manuellt];
                                                   Editable=No }
    { 11012030;;Item Discount (LCY);Decimal       ;CaptionML=[ENU=Item Discount;
                                                              SVE=Artikelrabatt] }
  }
  KEYS
  {
    {    ;VAT Identifier,VAT Calculation Type,Tax Group Code,Use Tax,Positive,Currency Code;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1% VAT;NOR=%1% Mva.;SVE=%1% moms';
      Text001@1001 : TextConst 'ENU=VAT Amount;NOR=Mva-bel›p;SVE=Momsbelopp';
      Text002@1002 : TextConst 'ENU=%1 must not be negative.;NOR=%1 kan ikke v‘re negativ.;SVE=%1 f†r inte vara negativt.';
      InvoiceDiscAmtIsGreaterThanBaseAmtErr@1003 : TextConst '@@@=1 Invoice Discount Amount that should be set 2 Maximum Amount that you can assign;ENU=The maximum %1 that you can apply is %2.;NOR=Maksimalt %1 som du kan bruke, er %2.;SVE=Det maximala %1 som du kan anv„nda „r %2.';
      Text004@1004 : TextConst 'ENU="%1 for %2 must not exceed %3 = %4.";NOR="%1 for %2 m† ikke overstige %3 = %4.";SVE="%1 f”r %2 f†r inte ”verskrida %3 = %4."';
      Currency@1005 : Record 4;
      AllowVATDifference@1006 : Boolean;
      GlobalsInitialized@1008 : Boolean;
      Text005@1009 : TextConst 'ENU="%1 must not exceed %2 = %3.";NOR="%1 m† ikke overskride %2 = %3.";SVE="%1 f†r inte ”verskrida %2 = %3."';

    [External]
    PROCEDURE CheckVATDifference@11(NewCurrencyCode@1000 : Code[10];NewAllowVATDifference@1001 : Boolean);
    VAR
      GLSetup@1003 : Record 98;
    BEGIN
      InitGlobals(NewCurrencyCode,NewAllowVATDifference);
      IF NOT AllowVATDifference THEN
        TESTFIELD("VAT Difference",0);
      IF ABS("VAT Difference") > Currency."Max. VAT Difference Allowed" THEN
        IF NewCurrencyCode <> '' THEN
          ERROR(
            Text004,FIELDCAPTION("VAT Difference"),Currency.Code,
            Currency.FIELDCAPTION("Max. VAT Difference Allowed"),Currency."Max. VAT Difference Allowed")
        ELSE BEGIN
          IF GLSetup.GET THEN;
          IF ABS("VAT Difference") > GLSetup."Max. VAT Difference Allowed" THEN
            ERROR(
              Text005,FIELDCAPTION("VAT Difference"),
              GLSetup.FIELDCAPTION("Max. VAT Difference Allowed"),GLSetup."Max. VAT Difference Allowed");
        END;
    END;

    LOCAL PROCEDURE InitGlobals@4(NewCurrencyCode@1000 : Code[10];NewAllowVATDifference@1001 : Boolean);
    BEGIN
      IF GlobalsInitialized THEN
        EXIT;

      Currency.Initialize(NewCurrencyCode);
      AllowVATDifference := NewAllowVATDifference;
      GlobalsInitialized := TRUE;
    END;

    [External]
    PROCEDURE InsertLine@1() : Boolean;
    VAR
      VATAmountLine@1000 : Record 290;
    BEGIN
      IF NOT (("VAT Base" <> 0) OR ("Amount Including VAT" <> 0)) THEN
        EXIT(FALSE);

      //**4PS To prevent too much VAT Specification on Invoices.
      //Positive := "Line Amount" >= 0; //**4PS.o
      VATAmountLine := Rec;
      IF FIND THEN BEGIN
        "Line Amount" += VATAmountLine."Line Amount";
        "Inv. Disc. Base Amount" += VATAmountLine."Inv. Disc. Base Amount";
        "Invoice Discount Amount" += VATAmountLine."Invoice Discount Amount";
        Quantity += VATAmountLine.Quantity;
        "VAT Base" += VATAmountLine."VAT Base";
        "Amount Including VAT" += VATAmountLine."Amount Including VAT";
        "VAT Difference" += VATAmountLine."VAT Difference";
        "VAT Amount" := "Amount Including VAT" - "VAT Base";
        "Calculated VAT Amount" += VATAmountLine."Calculated VAT Amount";
        OnInsertLineOnBeforeModify(Rec,VATAmountLine);
        MODIFY;
      END ELSE BEGIN
        "VAT Amount" := "Amount Including VAT" - "VAT Base";
        INSERT;
      END;

      EXIT(TRUE);
    END;

    [External]
    PROCEDURE InsertNewLine@24(VATIdentifier@1000 : Code[20];VATCalcType@1001 : Option;TaxGroupCode@1002 : Code[20];UseTax@1005 : Boolean;TaxRate@1003 : Decimal;IsPositive@1004 : Boolean;IsPrepayment@1006 : Boolean);
    BEGIN
      INIT;
      "VAT Identifier" := VATIdentifier;
      "VAT Calculation Type" := VATCalcType;
      "Tax Group Code" := TaxGroupCode;
      "Use Tax" := UseTax;
      "VAT %" := TaxRate;
      Modified := TRUE;
      Positive := IsPositive;
      "Includes Prepayment" := IsPrepayment;
      INSERT;
    END;

    [External]
    PROCEDURE GetLine@2(Number@1000 : Integer);
    BEGIN
      IF Number = 1 THEN
        FIND('-')
      ELSE
        NEXT;
    END;

    [External]
    PROCEDURE VATAmountText@3() Result : Text[30];
    VAR
      TempVATAmountLine@1003 : TEMPORARY Record 290;
      FullCount@1000 : Integer;
      VATPercentage@1001 : Decimal;
    BEGIN
      //**4PS.sn
      IF OnlyReverseChargeVAT() THEN
        EXIT(FIELDCAPTION("Reverse Charge VAT"));
      //**4PS.en

      VATPercentage := 0;
      FullCount := COUNT;
      IF FullCount = 1 THEN BEGIN
        FINDFIRST;
        IF "VAT %" <> 0 THEN
          VATPercentage := "VAT %";
        EXIT(STRSUBSTNO(Text000,"VAT %")); //**4PS.n
      END ELSE
        IF FullCount > 1 THEN BEGIN
          TempVATAmountLine.COPY(Rec,TRUE);
          TempVATAmountLine.FINDFIRST;
          IF TempVATAmountLine."VAT %" <> 0 THEN BEGIN
            TempVATAmountLine.SETRANGE("VAT %",TempVATAmountLine."VAT %");
            IF TempVATAmountLine.COUNT = FullCount THEN
            //VATPercentage := TempVATAmountLine."VAT %"; //**4PS.o
              EXIT(STRSUBSTNO(Text000,TempVATAmountLine."VAT %")); //**4PS.n
          END;
        END;
      IF VATPercentage = 0 THEN
        Result := Text001
      ELSE
        Result := STRSUBSTNO(Text000,VATPercentage);
      OnAfterVATAmountText(VATPercentage,FullCount,Result);
    END;

    [External]
    PROCEDURE GetTotalLineAmount@10(SubtractVAT@1000 : Boolean;CurrencyCode@1001 : Code[10]) : Decimal;
    VAR
      LineAmount@1002 : Decimal;
    BEGIN
      IF SubtractVAT THEN
        Currency.Initialize(CurrencyCode);

      LineAmount := 0;

      IF FIND('-') THEN
        REPEAT
          IF SubtractVAT THEN
            LineAmount :=
              LineAmount + ROUND("Line Amount" / (1 + "VAT %" / 100),Currency."Amount Rounding Precision")
          ELSE
            LineAmount := LineAmount + "Line Amount";
        UNTIL NEXT = 0;

      EXIT(LineAmount);
    END;

    [External]
    PROCEDURE GetTotalVATAmount@5() : Decimal;
    BEGIN
      CALCSUMS("VAT Amount");
      EXIT("VAT Amount");
    END;

    [External]
    PROCEDURE GetTotalInvDiscAmount@9() : Decimal;
    BEGIN
      CALCSUMS("Invoice Discount Amount");
      EXIT("Invoice Discount Amount");
    END;

    [External]
    PROCEDURE GetTotalInvDiscBaseAmount@6(SubtractVAT@1000 : Boolean;CurrencyCode@1001 : Code[10]) : Decimal;
    VAR
      InvDiscBaseAmount@1002 : Decimal;
    BEGIN
      IF SubtractVAT THEN
        Currency.Initialize(CurrencyCode);

      InvDiscBaseAmount := 0;

      IF FIND('-') THEN
        REPEAT
          IF SubtractVAT THEN
            InvDiscBaseAmount :=
              InvDiscBaseAmount +
              ROUND("Inv. Disc. Base Amount" / (1 + "VAT %" / 100),Currency."Amount Rounding Precision")
          ELSE
            InvDiscBaseAmount := InvDiscBaseAmount + "Inv. Disc. Base Amount";
        UNTIL NEXT = 0;
      EXIT(InvDiscBaseAmount);
    END;

    [External]
    PROCEDURE GetTotalVATBase@14() : Decimal;
    BEGIN
      CALCSUMS("VAT Base");
      EXIT("VAT Base");
    END;

    [External]
    PROCEDURE GetTotalAmountInclVAT@17() : Decimal;
    BEGIN
      CALCSUMS("Amount Including VAT");
      EXIT("Amount Including VAT");
    END;

    [External]
    PROCEDURE GetTotalVATDiscount@19(CurrencyCode@1001 : Code[10];NewPricesIncludingVAT@1000 : Boolean) : Decimal;
    VAR
      VATDiscount@1002 : Decimal;
      VATBase@1003 : Decimal;
    BEGIN
      Currency.Initialize(CurrencyCode);

      VATDiscount := 0;

      IF FIND('-') THEN
        REPEAT
          IF NewPricesIncludingVAT THEN
            VATBase += CalcLineAmount * "VAT %" / (100 + "VAT %")
          ELSE
            VATBase += "VAT Base" * "VAT %" / 100;
          VATDiscount :=
            VATDiscount +
            ROUND(
              VATBase,
              Currency."Amount Rounding Precision",Currency.VATRoundingDirection) -
            "VAT Amount" + "VAT Difference";
          VATBase := VATBase - ROUND(VATBase,Currency."Amount Rounding Precision",Currency.VATRoundingDirection);
        UNTIL NEXT = 0;
      EXIT(VATDiscount);
    END;

    [External]
    PROCEDURE GetAnyLineModified@7() : Boolean;
    BEGIN
      IF FIND('-') THEN
        REPEAT
          IF Modified THEN
            EXIT(TRUE);
        UNTIL NEXT = 0;
      EXIT(FALSE);
    END;

    [External]
    PROCEDURE SetInvoiceDiscountAmount@13(NewInvoiceDiscount@1000 : Decimal;NewCurrencyCode@1001 : Code[10];NewPricesIncludingVAT@1002 : Boolean;NewVATBaseDiscPct@1005 : Decimal);
    VAR
      TotalInvDiscBaseAmount@1003 : Decimal;
      NewRemainder@1004 : Decimal;
    BEGIN
      InitGlobals(NewCurrencyCode,FALSE);
      TotalInvDiscBaseAmount := GetTotalInvDiscBaseAmount(FALSE,Currency.Code);
      IF TotalInvDiscBaseAmount = 0 THEN
        EXIT;
      FIND('-');
      REPEAT
        IF "Inv. Disc. Base Amount" <> 0 THEN BEGIN
          IF TotalInvDiscBaseAmount = 0 THEN
            NewRemainder := 0
          ELSE
            NewRemainder :=
              NewRemainder + NewInvoiceDiscount * "Inv. Disc. Base Amount" / TotalInvDiscBaseAmount;
          IF "Invoice Discount Amount" <> ROUND(NewRemainder,Currency."Amount Rounding Precision") THEN BEGIN
            VALIDATE(
              "Invoice Discount Amount",ROUND(NewRemainder,Currency."Amount Rounding Precision"));
            CalcVATFields(NewCurrencyCode,NewPricesIncludingVAT,NewVATBaseDiscPct);
            Modified := TRUE;
            MODIFY;
          END;
          NewRemainder := NewRemainder - "Invoice Discount Amount";
        END;
      UNTIL NEXT = 0;
    END;

    [External]
    PROCEDURE SetInvoiceDiscountPercent@16(NewInvoiceDiscountPct@1000 : Decimal;NewCurrencyCode@1001 : Code[10];NewPricesIncludingVAT@1002 : Boolean;CalcInvDiscPerVATID@1003 : Boolean;NewVATBaseDiscPct@1005 : Decimal);
    VAR
      NewRemainder@1004 : Decimal;
    BEGIN
      InitGlobals(NewCurrencyCode,FALSE);
      IF FIND('-') THEN
        REPEAT
          IF "Inv. Disc. Base Amount" <> 0 THEN BEGIN
            NewRemainder :=
              NewRemainder + NewInvoiceDiscountPct * "Inv. Disc. Base Amount" / 100;
            IF "Invoice Discount Amount" <> ROUND(NewRemainder,Currency."Amount Rounding Precision") THEN BEGIN
              VALIDATE(
                "Invoice Discount Amount",ROUND(NewRemainder,Currency."Amount Rounding Precision"));
              CalcVATFields(NewCurrencyCode,NewPricesIncludingVAT,NewVATBaseDiscPct);
              "VAT Difference" := 0;
              Modified := TRUE;
              MODIFY;
            END;
            IF CalcInvDiscPerVATID THEN
              NewRemainder := 0
            ELSE
              NewRemainder := NewRemainder - "Invoice Discount Amount";
          END;
        UNTIL NEXT = 0;
    END;

    LOCAL PROCEDURE GetCalculatedVAT@12(NewCurrencyCode@1000 : Code[10];NewPricesIncludingVAT@1001 : Boolean;NewVATBaseDiscPct@1002 : Decimal) : Decimal;
    BEGIN
      InitGlobals(NewCurrencyCode,FALSE);

      IF NewPricesIncludingVAT THEN
        EXIT(
          ROUND(
            CalcLineAmount * "VAT %" / (100 + "VAT %") * (1 - NewVATBaseDiscPct / 100),
            Currency."Amount Rounding Precision",Currency.VATRoundingDirection));

      EXIT(
        ROUND(
          CalcLineAmount * "VAT %" / 100 * (1 - NewVATBaseDiscPct / 100),
          Currency."Amount Rounding Precision",Currency.VATRoundingDirection));
    END;

    [External]
    PROCEDURE CalcLineAmount@156() LineAmount : Decimal;
    BEGIN
      LineAmount := "Line Amount" - "Invoice Discount Amount";

      OnAfterCalcLineAmount(Rec,LineAmount);
    END;

    [External]
    PROCEDURE CalcVATFields@23(NewCurrencyCode@1000 : Code[10];NewPricesIncludingVAT@1001 : Boolean;NewVATBaseDiscPct@1002 : Decimal);
    BEGIN
      InitGlobals(NewCurrencyCode,FALSE);

      "VAT Amount" := GetCalculatedVAT(NewCurrencyCode,NewPricesIncludingVAT,NewVATBaseDiscPct);

      IF NewPricesIncludingVAT THEN BEGIN
        IF NewVATBaseDiscPct = 0 THEN BEGIN
          "Amount Including VAT" := CalcLineAmount;
          "VAT Base" := "Amount Including VAT" - "VAT Amount";
        END ELSE BEGIN
          "VAT Base" :=
            ROUND(CalcLineAmount / (1 + "VAT %" / 100),Currency."Amount Rounding Precision");
          "Amount Including VAT" := "VAT Base" + "VAT Amount";
        END;
      END ELSE BEGIN
        "VAT Base" := CalcLineAmount;
        "Amount Including VAT" := "VAT Base" + "VAT Amount";
      END;
      "Calculated VAT Amount" := "VAT Amount";
      "VAT Difference" := 0;
      Modified := TRUE;
    END;

    LOCAL PROCEDURE CalcValueLCY@18(Value@1003 : Decimal;PostingDate@1006 : Date;CurrencyCode@1005 : Code[10];CurrencyFactor@1004 : Decimal;UseSalesExchange@1100525000 : Boolean) : Decimal;
    VAR
      CurrencyExchangeRate@1000 : Record 330;
    BEGIN
      //EXIT(CurrencyExchangeRate.ExchangeAmtFCYToLCY(PostingDate,CurrencyCode,Value,CurrencyFactor)); //**4PS.o
      EXIT(CurrencyExchangeRate.ExchangeAmtFCYToLCY(0,'',PostingDate,CurrencyCode,Value,CurrencyFactor,UseSalesExchange)); //**4PS.n FIXED Sales or Purchase?
    END;

    [External]
    PROCEDURE GetBaseLCY@20(PostingDate@1003 : Date;CurrencyCode@1002 : Code[10];CurrencyFactor@1001 : Decimal;UseSalesExchange@1100525000 : Boolean) : Decimal;
    BEGIN
      //EXIT(ROUND(CalcValueLCY("VAT Base",PostingDate,CurrencyCode,CurrencyFactor))); //**4PS.o
      EXIT(ROUND(CalcValueLCY("VAT Base",PostingDate,CurrencyCode,CurrencyFactor,UseSalesExchange))); //**4PS.n
    END;

    [External]
    PROCEDURE GetAmountLCY@21(PostingDate@1000 : Date;CurrencyCode@1001 : Code[10];CurrencyFactor@1002 : Decimal;UseSalesExchange@1100525000 : Boolean) : Decimal;
    BEGIN
      EXIT(
      //**4PS.so
      //ROUND(CalcValueLCY("Amount Including VAT",PostingDate,CurrencyCode,CurrencyFactor)) -
      //ROUND(CalcValueLCY("VAT Base",PostingDate,CurrencyCode,CurrencyFactor)));
      //**4PS.eo
      //**4PS.sn
        ROUND(CalcValueLCY("Amount Including VAT",PostingDate,CurrencyCode,CurrencyFactor,UseSalesExchange)) -
        ROUND(CalcValueLCY("VAT Base",PostingDate,CurrencyCode,CurrencyFactor,UseSalesExchange)));
      //**4PS.en
    END;

    [External]
    PROCEDURE DeductVATAmountLine@22(VAR VATAmountLineDeduct@1001 : Record 290);
    BEGIN
      IF FINDSET THEN
        REPEAT
          VATAmountLineDeduct := Rec;
          IF VATAmountLineDeduct.FIND THEN BEGIN
            "VAT Base" -= VATAmountLineDeduct."VAT Base";
            "VAT Amount" -= VATAmountLineDeduct."VAT Amount";
            "Amount Including VAT" -= VATAmountLineDeduct."Amount Including VAT";
            "Line Amount" -= VATAmountLineDeduct."Line Amount";
            "Inv. Disc. Base Amount" -= VATAmountLineDeduct."Inv. Disc. Base Amount";
            "Invoice Discount Amount" -= VATAmountLineDeduct."Invoice Discount Amount";
            "Calculated VAT Amount" -= VATAmountLineDeduct."Calculated VAT Amount";
            "VAT Difference" -= VATAmountLineDeduct."VAT Difference";
            MODIFY;
          END;
        UNTIL NEXT = 0;
    END;

    [External]
    PROCEDURE SumLine@25(LineAmount@1001 : Decimal;InvDiscAmount@1002 : Decimal;VATDifference@1004 : Decimal;AllowInvDisc@1003 : Boolean;Prepayment@1005 : Boolean);
    BEGIN
      "Line Amount" += LineAmount;
      IF AllowInvDisc THEN
        "Inv. Disc. Base Amount" += LineAmount;
      "Invoice Discount Amount" += InvDiscAmount;
      "VAT Difference" += VATDifference;
      IF Prepayment THEN
        "Includes Prepayment" := TRUE;
      MODIFY;
    END;

    [External]
    PROCEDURE UpdateLines@100(VAR TotalVATAmount@1010 : Decimal;Currency@1012 : Record 4;CurrencyFactor@1003 : Decimal;PricesIncludingVAT@1009 : Boolean;VATBaseDiscountPerc@1008 : Decimal;TaxAreaCode@1007 : Code[20];TaxLiable@1005 : Boolean;PostingDate@1004 : Date);
    VAR
      PrevVATAmountLine@1001 : Record 290;
      SalesTaxCalculate@1006 : Codeunit 398;
    BEGIN
      IF FINDSET THEN
        REPEAT
          IF (PrevVATAmountLine."VAT Identifier" <> "VAT Identifier") OR
             (PrevVATAmountLine."VAT Calculation Type" <> "VAT Calculation Type") OR
             (PrevVATAmountLine."Tax Group Code" <> "Tax Group Code") OR
             (PrevVATAmountLine."Use Tax" <> "Use Tax")
          THEN
            PrevVATAmountLine.INIT;
          IF PricesIncludingVAT THEN
            CASE "VAT Calculation Type" OF
              "VAT Calculation Type"::"Normal VAT",
              "VAT Calculation Type"::"Reverse Charge VAT":
                BEGIN
                  "VAT Base" :=
                    ROUND(CalcLineAmount / (1 + "VAT %" / 100),Currency."Amount Rounding Precision") - "VAT Difference";
                  "VAT Amount" :=
                    "VAT Difference" +
                    ROUND(
                      PrevVATAmountLine."VAT Amount" +
                      (CalcLineAmount - "VAT Base" - "VAT Difference") *
                      (1 - VATBaseDiscountPerc / 100),
                      Currency."Amount Rounding Precision",Currency.VATRoundingDirection);
                  //**4PS.sn
                  IF "VAT manually" THEN
                    "VAT Amount" := "Amount Including VAT" - "VAT Base"
                  ELSE
                  //**4PS.en
                    "Amount Including VAT" := "VAT Base" + "VAT Amount";
                  IF Positive THEN
                    PrevVATAmountLine.INIT
                  ELSE BEGIN
                    PrevVATAmountLine := Rec;
                    PrevVATAmountLine."VAT Amount" :=
                      (CalcLineAmount - "VAT Base" - "VAT Difference") *
                      (1 - VATBaseDiscountPerc / 100);
                    PrevVATAmountLine."VAT Amount" :=
                      PrevVATAmountLine."VAT Amount" -
                      ROUND(PrevVATAmountLine."VAT Amount",Currency."Amount Rounding Precision",Currency.VATRoundingDirection);
                  END;
                END;
              "VAT Calculation Type"::"Full VAT":
                BEGIN
                  "VAT Base" := 0;
                  "VAT Amount" := "VAT Difference" + CalcLineAmount;
                  "Amount Including VAT" := "VAT Amount";
                END;
              "VAT Calculation Type"::"Sales Tax":
                BEGIN
                  "Amount Including VAT" := CalcLineAmount;
                  IF "Use Tax" THEN
                    "VAT Base" := "Amount Including VAT"
                  ELSE
                    "VAT Base" :=
                      ROUND(
                        SalesTaxCalculate.ReverseCalculateTax(
                          TaxAreaCode,"Tax Group Code",TaxLiable,PostingDate,"Amount Including VAT",Quantity,CurrencyFactor),
                        Currency."Amount Rounding Precision");
                  OnAfterSalesTaxCalculateReverseCalculateTax(Rec,Currency);
                  "VAT Amount" := "VAT Difference" + "Amount Including VAT" - "VAT Base";
                  IF "VAT Base" = 0 THEN
                    "VAT %" := 0
                  ELSE
                    "VAT %" := ROUND(100 * "VAT Amount" / "VAT Base",0.00001);
                END;
            END
          ELSE
            CASE "VAT Calculation Type" OF
              "VAT Calculation Type"::"Normal VAT",
              "VAT Calculation Type"::"Reverse Charge VAT":
                BEGIN
                  "VAT Base" := CalcLineAmount;
                  "VAT Amount" :=
                    "VAT Difference" +
                    ROUND(
                      PrevVATAmountLine."VAT Amount" +
                      "VAT Base" * "VAT %" / 100 * (1 - VATBaseDiscountPerc / 100),
                      Currency."Amount Rounding Precision",Currency.VATRoundingDirection);
                  //**4PS.sn
                  IF "VAT manually" THEN
                    "VAT Amount" := "Amount Including VAT" - CalcLineAmount
                  ELSE
                  //**4PS.en
                    "Amount Including VAT" := CalcLineAmount + "VAT Amount";
                  IF Positive THEN
                    PrevVATAmountLine.INIT
                  ELSE
                    IF NOT "Includes Prepayment" THEN BEGIN
                      PrevVATAmountLine := Rec;
                      PrevVATAmountLine."VAT Amount" :=
                        "VAT Base" * "VAT %" / 100 * (1 - VATBaseDiscountPerc / 100);
                      PrevVATAmountLine."VAT Amount" :=
                        PrevVATAmountLine."VAT Amount" -
                        ROUND(PrevVATAmountLine."VAT Amount",Currency."Amount Rounding Precision",Currency.VATRoundingDirection);
                    END;
                END;
              "VAT Calculation Type"::"Full VAT":
                BEGIN
                  "VAT Base" := 0;
                  "VAT Amount" := "VAT Difference" + CalcLineAmount;
                  "Amount Including VAT" := "VAT Amount";
                END;
              "VAT Calculation Type"::"Sales Tax":
                BEGIN
                  "VAT Base" := CalcLineAmount;
                  IF "Use Tax" THEN
                    "VAT Amount" := 0
                  ELSE
                    "VAT Amount" :=
                      SalesTaxCalculate.CalculateTax(
                        TaxAreaCode,"Tax Group Code",TaxLiable,PostingDate,"VAT Base",Quantity,CurrencyFactor);
                  OnAfterSalesTaxCalculateCalculateTax(Rec,Currency);
                  IF "VAT Base" = 0 THEN
                    "VAT %" := 0
                  ELSE
                    "VAT %" := ROUND(100 * "VAT Amount" / "VAT Base",0.00001);
                  "VAT Amount" :=
                    "VAT Difference" +
                    ROUND("VAT Amount",Currency."Amount Rounding Precision",Currency.VATRoundingDirection);
                  "Amount Including VAT" := "VAT Base" + "VAT Amount";
                END;
            END;

          TotalVATAmount -= "VAT Amount";
          "Calculated VAT Amount" := "VAT Amount" - "VAT Difference";
          MODIFY;
        UNTIL NEXT = 0;
    END;

    [External]
    PROCEDURE CopyFromPurchInvLine@8(PurchInvLine@1000 : Record 123);
    BEGIN
      "VAT Identifier" := PurchInvLine."VAT Identifier";
      "VAT Calculation Type" := PurchInvLine."VAT Calculation Type";
      "Tax Group Code" := PurchInvLine."Tax Group Code";
      "Use Tax" := PurchInvLine."Use Tax";
      "VAT %" := PurchInvLine."VAT %";
      "VAT Base" := PurchInvLine.Amount;
      "VAT Amount" := PurchInvLine."Amount Including VAT" - PurchInvLine.Amount;
      "Amount Including VAT" := PurchInvLine."Amount Including VAT";
      "Line Amount" := PurchInvLine."Line Amount";
      IF PurchInvLine."Allow Invoice Disc." THEN
        "Inv. Disc. Base Amount" := PurchInvLine."Line Amount";
      "Invoice Discount Amount" := PurchInvLine."Inv. Discount Amount";
      Quantity := PurchInvLine."Quantity (Base)";
      "Calculated VAT Amount" :=
        PurchInvLine."Amount Including VAT" - PurchInvLine.Amount - PurchInvLine."VAT Difference";
      "VAT Difference" := PurchInvLine."VAT Difference";

      OnAfterCopyFromPurchInvLine(Rec,PurchInvLine);
    END;

    [External]
    PROCEDURE CopyFromPurchCrMemoLine@26(PurchCrMemoLine@1000 : Record 125);
    BEGIN
      "VAT Identifier" := PurchCrMemoLine."VAT Identifier";
      "VAT Calculation Type" := PurchCrMemoLine."VAT Calculation Type";
      "Tax Group Code" := PurchCrMemoLine."Tax Group Code";
      "Use Tax" := PurchCrMemoLine."Use Tax";
      "VAT %" := PurchCrMemoLine."VAT %";
      "VAT Base" := PurchCrMemoLine.Amount;
      "VAT Amount" := PurchCrMemoLine."Amount Including VAT" - PurchCrMemoLine.Amount;
      "Amount Including VAT" := PurchCrMemoLine."Amount Including VAT";
      "Line Amount" := PurchCrMemoLine."Line Amount";
      IF PurchCrMemoLine."Allow Invoice Disc." THEN
        "Inv. Disc. Base Amount" := PurchCrMemoLine."Line Amount";
      "Invoice Discount Amount" := PurchCrMemoLine."Inv. Discount Amount";
      Quantity := PurchCrMemoLine."Quantity (Base)";
      "Calculated VAT Amount" :=
        PurchCrMemoLine."Amount Including VAT" - PurchCrMemoLine.Amount - PurchCrMemoLine."VAT Difference";
      "VAT Difference" := PurchCrMemoLine."VAT Difference";

      OnAfterCopyFromPurchCrMemoLine(Rec,PurchCrMemoLine);
    END;

    [External]
    PROCEDURE CopyFromSalesInvLine@28(SalesInvLine@1000 : Record 113);
    BEGIN
      "VAT Identifier" := SalesInvLine."VAT Identifier";
      "VAT Calculation Type" := SalesInvLine."VAT Calculation Type";
      "Tax Group Code" := SalesInvLine."Tax Group Code";
      "VAT %" := SalesInvLine."VAT %";
      "VAT Base" := SalesInvLine.Amount;
      "VAT Amount" := SalesInvLine."Amount Including VAT" - SalesInvLine.Amount;
      "Amount Including VAT" := SalesInvLine."Amount Including VAT";
      "Line Amount" := SalesInvLine."Line Amount";
      IF SalesInvLine."Allow Invoice Disc." THEN
        "Inv. Disc. Base Amount" := SalesInvLine."Line Amount";
      "Invoice Discount Amount" := SalesInvLine."Inv. Discount Amount";
      Quantity := SalesInvLine."Quantity (Base)";
      "Calculated VAT Amount" :=
        SalesInvLine."Amount Including VAT" - SalesInvLine.Amount - SalesInvLine."VAT Difference";
      "VAT Difference" := SalesInvLine."VAT Difference";

      OnAfterCopyFromSalesInvLine(Rec,SalesInvLine);
    END;

    [External]
    PROCEDURE CopyFromSalesCrMemoLine@27(SalesCrMemoLine@1000 : Record 115);
    BEGIN
      "VAT Identifier" := SalesCrMemoLine."VAT Identifier";
      "VAT Calculation Type" := SalesCrMemoLine."VAT Calculation Type";
      "Tax Group Code" := SalesCrMemoLine."Tax Group Code";
      "VAT %" := SalesCrMemoLine."VAT %";
      "VAT Base" := SalesCrMemoLine.Amount;
      "VAT Amount" := SalesCrMemoLine."Amount Including VAT" - SalesCrMemoLine.Amount;
      "Amount Including VAT" := SalesCrMemoLine."Amount Including VAT";
      "Line Amount" := SalesCrMemoLine."Line Amount";
      IF SalesCrMemoLine."Allow Invoice Disc." THEN
        "Inv. Disc. Base Amount" := SalesCrMemoLine."Line Amount";
      "Invoice Discount Amount" := SalesCrMemoLine."Inv. Discount Amount";
      Quantity := SalesCrMemoLine."Quantity (Base)";
      "Calculated VAT Amount" := SalesCrMemoLine."Amount Including VAT" - SalesCrMemoLine.Amount - SalesCrMemoLine."VAT Difference";
      "VAT Difference" := SalesCrMemoLine."VAT Difference";

      OnAfterCopyFromSalesCrMemoLine(Rec,SalesCrMemoLine);
    END;

    [External]
    PROCEDURE CopyFromServInvLine@29(ServInvLine@1000 : Record 5993);
    BEGIN
      "VAT Identifier" := ServInvLine."VAT Identifier";
      "VAT Calculation Type" := ServInvLine."VAT Calculation Type";
      "Tax Group Code" := ServInvLine."Tax Group Code";
      "VAT %" := ServInvLine."VAT %";
      "VAT Base" := ServInvLine.Amount;
      "VAT Amount" := ServInvLine."Amount Including VAT" - ServInvLine.Amount;
      "Amount Including VAT" := ServInvLine."Amount Including VAT";
      "Line Amount" := ServInvLine."Line Amount";
      IF ServInvLine."Allow Invoice Disc." THEN
        "Inv. Disc. Base Amount" := ServInvLine."Line Amount";
      "Invoice Discount Amount" := ServInvLine."Inv. Discount Amount";
      Quantity := ServInvLine."Quantity (Base)";
      "Calculated VAT Amount" :=
        ServInvLine."Amount Including VAT" - ServInvLine.Amount - ServInvLine."VAT Difference";
      "VAT Difference" := ServInvLine."VAT Difference";

      OnAfterCopyFromServInvLine(Rec,ServInvLine);
    END;

    [External]
    PROCEDURE CopyFromServCrMemoLine@30(ServCrMemoLine@1000 : Record 5995);
    BEGIN
      "VAT Identifier" := ServCrMemoLine."VAT Identifier";
      "VAT Calculation Type" := ServCrMemoLine."VAT Calculation Type";
      "Tax Group Code" := ServCrMemoLine."Tax Group Code";
      "VAT %" := ServCrMemoLine."VAT %";
      "VAT Base" := ServCrMemoLine.Amount;
      "VAT Amount" := ServCrMemoLine."Amount Including VAT" - ServCrMemoLine.Amount;
      "Amount Including VAT" := ServCrMemoLine."Amount Including VAT";
      "Line Amount" := ServCrMemoLine."Line Amount";
      IF ServCrMemoLine."Allow Invoice Disc." THEN
        "Inv. Disc. Base Amount" := ServCrMemoLine."Line Amount";
      "Invoice Discount Amount" := ServCrMemoLine."Inv. Discount Amount";
      Quantity := ServCrMemoLine."Quantity (Base)";
      "Calculated VAT Amount" :=
        ServCrMemoLine."Amount Including VAT" - ServCrMemoLine.Amount - ServCrMemoLine."VAT Difference";
      "VAT Difference" := ServCrMemoLine."VAT Difference";

      OnAfterCopyFromServCrMemoLine(Rec,ServCrMemoLine);
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCalcLineAmount@38(VAR VATAmountLine@1000 : Record 290;VAR LineAmount@1001 : Decimal);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyFromPurchInvLine@15(VAR VATAmountLine@1000 : Record 290;PurchInvLine@1001 : Record 123);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyFromPurchCrMemoLine@31(VAR VATAmountLine@1000 : Record 290;PurchCrMemoLine@1001 : Record 125);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyFromSalesInvLine@32(VAR VATAmountLine@1000 : Record 290;SalesInvoiceLine@1001 : Record 113);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyFromSalesCrMemoLine@33(VAR VATAmountLine@1000 : Record 290;SalesCrMemoLine@1001 : Record 115);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyFromServInvLine@34(VAR VATAmountLine@1000 : Record 290;ServiceInvoiceLine@1001 : Record 5993);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCopyFromServCrMemoLine@35(VAR VATAmountLine@1000 : Record 290;ServiceCrMemoLine@1001 : Record 5995);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSalesTaxCalculateCalculateTax@37(VAR VATAmountLine@1000 : Record 290;Currency@1001 : Record 4);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSalesTaxCalculateReverseCalculateTax@39(VAR VATAmountLine@1000 : Record 290;Currency@1001 : Record 4);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterVATAmountText@40(VATPercentage@1000 : Decimal;FullCount@1001 : Integer;VAR Result@1002 : Text[30]);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnInsertLineOnBeforeModify@36(VAR VATAmountLine@1000 : Record 290;FromVATAmountLine@1001 : Record 290);
    BEGIN
    END;

    PROCEDURE OnlyReverseChargeVAT@1100485000() : Boolean;
    BEGIN
      //**4PS
      IF FIND('-') THEN BEGIN
        IF "Reverse Charge VAT" THEN BEGIN
          REPEAT
            IF NOT "Reverse Charge VAT" THEN
              EXIT(FALSE);
          UNTIL NEXT = 0;
          EXIT(TRUE);
        END;
      END;
      EXIT(FALSE);
    END;

    PROCEDURE GetTotalItemDiscount@1100485001(SubtractVAT@1000 : Boolean;CurrencyCode@1001 : Code[10]) : Decimal;
    VAR
      Amount@1002 : Decimal;
    BEGIN
      //**4PS
      IF SubtractVAT THEN
        Currency.Initialize(CurrencyCode);

      Amount := 0;

      IF FIND('-') THEN
        REPEAT
          IF SubtractVAT THEN
            Amount :=
              Amount + ROUND("Item Discount (LCY)" / (1 + "VAT %" / 100),Currency."Amount Rounding Precision")
          ELSE
            Amount := Amount + "Item Discount (LCY)";
        UNTIL NEXT = 0;

      EXIT(Amount);
    END;

    BEGIN
    END.
  }
}

