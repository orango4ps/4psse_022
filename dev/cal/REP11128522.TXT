OBJECT Report 11128522 Create NO Elec. Serv. Cr.Memos
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVNO9.00 (10645),4PSSE.NO;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create Elec. Service Cr. Memos;
               NOR=Opprette Elektroniske Servicekreditnotaer;
               SVE=Skapa elektroniska service kreditnoter];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 6248;    ;DataItem;                    ;
               DataItemTable=Table5994;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=VAR
                               ServCrMemoHeader@1101100000 : Record 5994;
                             BEGIN
                               Counter := 0;

                               // Any electronic service credit memos?
                               ServCrMemoHeader.COPY("Service Cr.Memo Header");
                               ServCrMemoHeader.FILTERGROUP(6);
                               ServCrMemoHeader.SETRANGE("E-Invoice",TRUE);
                               IF NOT ServCrMemoHeader.FINDFIRST THEN
                                 ERROR(Text003);

                               // All electronic service credit memos?
                               ServCrMemoHeader.SETRANGE("E-Invoice",FALSE);
                               IF ServCrMemoHeader.FINDFIRST THEN
                                 IF NOT CONFIRM(Text000,TRUE) THEN
                                   CurrReport.QUIT;
                               ServCrMemoHeader.SETRANGE("E-Invoice");

                               // Some already sent?
                               ServCrMemoHeader.SETRANGE("E-Invoice Created",TRUE);
                               IF ServCrMemoHeader.FINDFIRST THEN
                                 IF NOT CONFIRM(Text001) THEN
                                   CurrReport.QUIT;

                               SETRANGE("E-Invoice",TRUE);
                             END;

               OnAfterGetRecord=VAR
                                  EInvoiceExpServCrMemo@1080000 : Codeunit 11128512;
                                BEGIN
                                  EInvoiceExpServCrMemo.EInvoiceExpServCrMemo("Service Cr.Memo Header");
                                  EInvoiceExpServCrMemo.GetExportedFileInfo(TempEInvoiceTransferFile);
                                  TempEInvoiceTransferFile."Line No." := Counter + 1;
                                  TempEInvoiceTransferFile.INSERT;
                                  COMMIT;
                                  Counter := Counter + 1;
                                END;

               OnPostDataItem=VAR
                                EInvoiceExportCommon@1080000 : Codeunit 11128519;
                              BEGIN
                                EInvoiceExportCommon.DownloadEInvoiceFile(TempEInvoiceTransferFile);
                                MESSAGE(Text002,Counter);
                              END;

               ReqFilterFields=No.,Customer No.,Bill-to Customer No.,GLN,E-Invoice Created }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TempEInvoiceTransferFile@1080000 : TEMPORARY Record 11128516;
      Counter@1080005 : Integer;
      Text000@1080006 : TextConst 'ENU=One or more credit memo documents that match your filter criteria are not electronic credit memos and will be skipped.\\Do you want to continue?;NOR=Ett eller flere kreditnotadokumenter som samsvarer med filterkriteriene, er ikke elektroniske kreditnotaer og hoppes over.\\Vil du fortsette?';
      Text001@1080007 : TextConst 'ENU=One or more credit memo documents that match your filter criteria have been created before.\\Do you want to continue?;NOR=Ett eller flere kreditnotadokumenter som samsvarer med filterkriteriene, er allerede opprettet.\\Vil du fortsette?';
      Text002@1080008 : TextConst 'ENU=Successfully created %1 electronic credit memo documents.;NOR=Opprettet %1 elektroniske kreditnotadokumenter.';
      Text003@1080009 : TextConst 'ENU=Nothing to create.;NOR=Det finnes ingenting † opprette.;SVE=Det finns inget att skapa.';

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

