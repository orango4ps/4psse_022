OBJECT Report 94 Close Income Statement
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03;
  }
  PROPERTIES
  {
    CaptionML=[DEU=GuV-Konten Nullstellung;
               ENU=Close Income Statement;
               NLD=Afsluiten WenV-rekening;
               NOR=Lukk resultatregnskapet;
               SVE=Avslut av resultatkonton];
    ApplicationArea=#Basic,#Suite;
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  s@1000 : Text[1024];
                BEGIN
                  IF EndDateReq = 0D THEN
                    ERROR(Text000);
                  ValidateEndDate(TRUE);
                  IF DocNo = '' THEN
                    ERROR(Text001);

                  SelectedDim.GetSelectedDim(USERID,3,REPORT::"Close Income Statement",'',TempSelectedDim);
                  s := CheckDimPostingRules(TempSelectedDim);
                  IF s <> '' THEN
                    IF NOT CONFIRM(s + Text007,FALSE) THEN
                      ERROR('');

                  GenJnlBatch.GET(GenJnlLine."Journal Template Name",GenJnlLine."Journal Batch Name");
                  SourceCodeSetup.GET;
                  GLSetup.GET;
                  IF GLSetup."Additional Reporting Currency" <> '' THEN BEGIN
                    IF RetainedEarningsGLAcc."No." = '' THEN
                      ERROR(Text002);
                    IF NOT CONFIRM(
                         Text003 +
                         Text005 +
                         Text007,FALSE)
                    THEN
                      ERROR('');
                  END;

                  Window.OPEN(Text008 + Text009 + Text019 + Text010 + Text011);

                  ClosePerGlobalDim1 := FALSE;
                  ClosePerGlobalDim2 := FALSE;
                  ClosePerGlobalDimOnly := TRUE;

                  IF TempSelectedDim.FIND('-') THEN
                    REPEAT
                      IF TempSelectedDim."Dimension Code" = GLSetup."Global Dimension 1 Code" THEN
                        ClosePerGlobalDim1 := TRUE;
                      IF TempSelectedDim."Dimension Code" = GLSetup."Global Dimension 2 Code" THEN
                        ClosePerGlobalDim2 := TRUE;
                      IF (TempSelectedDim."Dimension Code" <> GLSetup."Global Dimension 1 Code") AND
                         (TempSelectedDim."Dimension Code" <> GLSetup."Global Dimension 2 Code")
                      THEN
                        ClosePerGlobalDimOnly := FALSE;
                    UNTIL TempSelectedDim.NEXT = 0;

                  GenJnlLine.SETRANGE("Journal Template Name",GenJnlLine."Journal Template Name");
                  GenJnlLine.SETRANGE("Journal Batch Name",GenJnlLine."Journal Batch Name");
                  IF NOT GenJnlLine.FINDLAST THEN;
                  GenJnlLine.INIT;
                  GenJnlLine."Posting Date" := FiscYearClosingDate;
                  GenJnlLine."Document No." := DocNo;
                  GenJnlLine.Description := PostingDescription;
                  GenJnlLine."Posting No. Series" := GenJnlBatch."Posting No. Series";
                  CLEAR(GenJnlPostLine);
                END;

    OnPostReport=VAR
                   UpdateAnalysisView@1000 : Codeunit 410;
                 BEGIN
                   Window.CLOSE;
                   COMMIT;
                   IF GLSetup."Additional Reporting Currency" <> '' THEN BEGIN
                     MESSAGE(Text016);
                     UpdateAnalysisView.UpdateAll(0,TRUE);
                   END ELSE
                     MESSAGE(Text017);
                 END;

    UsageCategory=Tasks;
    AdditionalSearchTermsML=[DEU=ErklÑrung des Jahresabschlusses,ErklÑrung des Buchhaltungsperiodenabschlusses,ErklÑrung des GeschÑftsjahresabschlussses;
                             ENU=year closing statement,close accounting period statement,close fiscal year statement;
                             NLD=verklaring van jaarafsluiting,verklaring van afsluiting boekingsperiode,verklaring van afsluiting boekjaar;
                             NOR=saldoutdrag ved Ürsavslutning,saldoutdrag ved avslutning av regnskapsperiode,saldoutdrag ved avslutning av regnskapsÜr];
  }
  DATASET
  {
    { 6710;    ;DataItem;                    ;
               DataItemTable=Table15;
               DataItemTableView=SORTING(No.)
                                 WHERE(Account Type=CONST(Posting),
                                       Income/Balance=CONST(Income Statement));
               OnPreDataItem=BEGIN
                               NoOfAccounts := COUNT;
                             END;

               OnAfterGetRecord=BEGIN
                                  ThisAccountNo := ThisAccountNo + 1;
                                  Window.UPDATE(1,"No.");
                                  Window.UPDATE(4,ROUND(ThisAccountNo / NoOfAccounts * 10000,1));
                                  Window.UPDATE(2,'');
                                  Window.UPDATE(3,0);
                                END;

               OnPostDataItem=BEGIN
                                IF ((TotalAmount <> 0) OR ((TotalAmountAddCurr <> 0) AND (GLSetup."Additional Reporting Currency" <> ''))) AND
                                   (PostToRetainedEarningsAcc = PostToRetainedEarningsAcc::Balance)
                                THEN BEGIN
                                  GenJnlLine."Business Unit Code" := '';
                                  GenJnlLine."Shortcut Dimension 1 Code" := '';
                                  GenJnlLine."Shortcut Dimension 2 Code" := '';
                                  GenJnlLine."Dimension Set ID" := 0;
                                  GenJnlLine."Line No." := GenJnlLine."Line No." + 10000;
                                  GenJnlLine."Account No." := RetainedEarningsGLAcc."No.";
                                  GenJnlLine."Source Code" := SourceCodeSetup."Close Income Statement";
                                  GenJnlLine."Reason Code" := GenJnlBatch."Reason Code";
                                  GenJnlLine."Currency Code" := '';
                                  GenJnlLine."Additional-Currency Posting" :=
                                    GenJnlLine."Additional-Currency Posting"::None;
                                  GenJnlLine.VALIDATE(Amount,TotalAmount);
                                  GenJnlLine."Source Currency Amount" := TotalAmountAddCurr;
                                  HandleGenJnlLine;
                                  Window.UPDATE(1,GenJnlLine."Account No.");
                                END;
                              END;
                               }

    { 7069;1   ;DataItem;                    ;
               DataItemTable=Table17;
               DataItemTableView=SORTING(G/L Account No.,Posting Date);
               OnPreDataItem=BEGIN
                               Window.UPDATE(2,Text013);
                               Window.UPDATE(3,0);

                               IF ClosePerGlobalDimOnly OR ClosePerBusUnit THEN
                                 CASE TRUE OF
                                   ClosePerBusUnit AND (ClosePerGlobalDim1 OR ClosePerGlobalDim2):
                                     SETCURRENTKEY(
                                       "G/L Account No.","Business Unit Code",
                                       "Global Dimension 1 Code","Global Dimension 2 Code","Posting Date");
                                   ClosePerBusUnit AND NOT (ClosePerGlobalDim1 OR ClosePerGlobalDim2):
                                     SETCURRENTKEY(
                                       "G/L Account No.","Business Unit Code","Posting Date");
                                   NOT ClosePerBusUnit AND (ClosePerGlobalDim1 OR ClosePerGlobalDim2):
                                     SETCURRENTKEY(
                                       "G/L Account No.","Global Dimension 1 Code","Global Dimension 2 Code","Posting Date");
                                 END;

                               SETRANGE("Posting Date",FiscalYearStartDate,FiscYearClosingDate);

                               MaxEntry := COUNT;

                               EntryNoAmountBuf.DELETEALL;
                               EntryCount := 0;

                               LastWindowUpdateDateTime := CURRENTDATETIME;
                             END;

               OnAfterGetRecord=VAR
                                  TempDimBuf@1000 : TEMPORARY Record 360;
                                  TempDimBuf2@1001 : TEMPORARY Record 360;
                                  DimensionBufferID@1002 : Integer;
                                  RowOffset@1003 : Integer;
                                BEGIN
                                  EntryCount := EntryCount + 1;
                                  IF CURRENTDATETIME - LastWindowUpdateDateTime > 1000 THEN BEGIN
                                    LastWindowUpdateDateTime := CURRENTDATETIME;
                                    Window.UPDATE(3,ROUND(EntryCount / MaxEntry * 10000,1));
                                  END;

                                  IF GroupSum THEN BEGIN
                                    CalcSumsInFilter("G/L Entry",RowOffset);
                                    GetGLEntryDimensions("Entry No.",TempDimBuf,"Dimension Set ID");
                                  END;

                                  IF (Amount <> 0) OR ("Additional-Currency Amount" <> 0) THEN BEGIN
                                    IF NOT GroupSum THEN BEGIN
                                      TotalAmount += Amount;
                                      IF GLSetup."Additional Reporting Currency" <> '' THEN
                                        TotalAmountAddCurr += "Additional-Currency Amount";

                                      GetGLEntryDimensions("Entry No.",TempDimBuf,"Dimension Set ID");
                                    END;

                                    IF TempSelectedDim.FIND('-') THEN
                                      REPEAT
                                        IF TempDimBuf.GET(DATABASE::"G/L Entry","Entry No.",TempSelectedDim."Dimension Code")
                                        THEN BEGIN
                                          TempDimBuf2."Table ID" := TempDimBuf."Table ID";
                                          TempDimBuf2."Dimension Code" := TempDimBuf."Dimension Code";
                                          TempDimBuf2."Dimension Value Code" := TempDimBuf."Dimension Value Code";
                                          TempDimBuf2.INSERT;
                                        END;
                                      UNTIL TempSelectedDim.NEXT = 0;

                                    DimensionBufferID := DimBufMgt.GetDimensionId(TempDimBuf2);

                                    EntryNoAmountBuf.RESET;
                                    IF ClosePerBusUnit AND FIELDACTIVE("Business Unit Code") THEN
                                      EntryNoAmountBuf."Business Unit Code" := "Business Unit Code"
                                    ELSE
                                      EntryNoAmountBuf."Business Unit Code" := '';
                                    EntryNoAmountBuf."Entry No." := DimensionBufferID;
                                    IF EntryNoAmountBuf.FIND THEN BEGIN
                                      EntryNoAmountBuf.Amount := EntryNoAmountBuf.Amount + Amount;
                                      EntryNoAmountBuf.Amount2 := EntryNoAmountBuf.Amount2 + "Additional-Currency Amount";
                                      EntryNoAmountBuf.MODIFY;
                                    END ELSE BEGIN
                                      EntryNoAmountBuf.Amount := Amount;
                                      EntryNoAmountBuf.Amount2 := "Additional-Currency Amount";
                                      EntryNoAmountBuf.INSERT;
                                    END;
                                  END;

                                  IF GroupSum THEN
                                    NEXT(RowOffset);
                                END;

               OnPostDataItem=VAR
                                TempDimBuf2@1000 : TEMPORARY Record 360;
                                GlobalDimVal1@1004 : Code[20];
                                GlobalDimVal2@1005 : Code[20];
                                NewDimensionID@1002 : Integer;
                              BEGIN
                                EntryNoAmountBuf.RESET;
                                MaxEntry := EntryNoAmountBuf.COUNT;
                                EntryCount := 0;
                                Window.UPDATE(2,Text012);
                                Window.UPDATE(3,0);

                                IF EntryNoAmountBuf.FIND('-') THEN
                                  REPEAT
                                    EntryCount := EntryCount + 1;
                                    IF CURRENTDATETIME - LastWindowUpdateDateTime > 1000 THEN BEGIN
                                      LastWindowUpdateDateTime := CURRENTDATETIME;
                                      Window.UPDATE(3,ROUND(EntryCount / MaxEntry * 10000,1));
                                    END;

                                    IF (EntryNoAmountBuf.Amount <> 0) OR (EntryNoAmountBuf.Amount2 <> 0) THEN BEGIN
                                      GenJnlLine."Line No." := GenJnlLine."Line No." + 10000;
                                      GenJnlLine."Account No." := "G/L Account No.";
                                      GenJnlLine."Source Code" := SourceCodeSetup."Close Income Statement";
                                      GenJnlLine."Reason Code" := GenJnlBatch."Reason Code";
                                      GenJnlLine.VALIDATE(Amount,-EntryNoAmountBuf.Amount);
                                      GenJnlLine."Source Currency Amount" := -EntryNoAmountBuf.Amount2;
                                      GenJnlLine."Business Unit Code" := EntryNoAmountBuf."Business Unit Code";

                                      TempDimBuf2.DELETEALL;
                                      DimBufMgt.RetrieveDimensions(EntryNoAmountBuf."Entry No.",TempDimBuf2);
                                      NewDimensionID := DimMgt.CreateDimSetIDFromDimBuf(TempDimBuf2);
                                      GenJnlLine."Dimension Set ID" := NewDimensionID;
                                      DimMgt.UpdateGlobalDimFromDimSetID(NewDimensionID,GlobalDimVal1,GlobalDimVal2);
                                      GenJnlLine."Shortcut Dimension 1 Code" := '';
                                      IF ClosePerGlobalDim1 THEN
                                        GenJnlLine."Shortcut Dimension 1 Code" := GlobalDimVal1;
                                      GenJnlLine."Shortcut Dimension 2 Code" := '';
                                      IF ClosePerGlobalDim2 THEN
                                        GenJnlLine."Shortcut Dimension 2 Code" := GlobalDimVal2;

                                      IF PostToRetainedEarningsAcc = PostToRetainedEarningsAcc::Details THEN BEGIN
                                        GenJnlLine."Bal. Account Type" := GenJnlLine."Bal. Account Type"::"G/L Account";
                                        GenJnlLine."Bal. Account No." := RetainedEarningsGLAcc."No.";
                                      END;

                                      HandleGenJnlLine;
                                    END;
                                  UNTIL EntryNoAmountBuf.NEXT = 0;

                                EntryNoAmountBuf.DELETEALL;
                              END;

               DataItemLink=G/L Account No.=FIELD(No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=VAR
                   GLAccount@1001 : Record 15;
                   GLAccountCategory@1000 : Record 570;
                 BEGIN
                   IF PostingDescription = '' THEN
                     PostingDescription :=
                       COPYSTR(ObjTransl.TranslateObject(ObjTransl."Object Type"::Report,REPORT::"Close Income Statement"),1,30);
                   EndDateReq := 0D;
                   AccountingPeriod.SETRANGE("New Fiscal Year",TRUE);
                   AccountingPeriod.SETRANGE("Date Locked",TRUE);
                   IF AccountingPeriod.FINDLAST THEN BEGIN
                     EndDateReq := AccountingPeriod."Starting Date" - 1;
                     IF NOT ValidateEndDate(FALSE) THEN
                       EndDateReq := 0D;
                   END ELSE
                     IF EndDateReq = 0D THEN
                       ERROR(NoFiscalYearsErr);
                   ValidateJnl;
                   ColumnDim := DimSelectionBuf.GetDimSelectionText(3,REPORT::"Close Income Statement",'');
                   IF RetainedEarningsGLAcc."No." = '' THEN BEGIN
                     GLAccountCategory.SETRANGE("Account Category",GLAccountCategory."Account Category"::Equity);
                     GLAccountCategory.SETRANGE(
                       "Additional Report Definition",GLAccountCategory."Additional Report Definition"::"Retained Earnings");
                     IF GLAccountCategory.FINDFIRST THEN BEGIN
                       GLAccount.SETRANGE("Account Subcategory Entry No.",GLAccountCategory."Entry No.");
                       IF GLAccount.FINDFIRST THEN
                         RetainedEarningsGLAcc."No." := GLAccount."No.";
                     END;
                   END;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1   ;2   ;Field     ;
                  Name=FiscalYearEndingDate;
                  CaptionML=[DEU=GeschÑftsjahr Enddatum;
                             ENU=Fiscal Year Ending Date;
                             NLD=Einddatum boekjaar;
                             NOR=RegnskapsÜr sluttdato];
                  ToolTipML=[DEU=Gibt das letzte Datum im abgeschlossenen GeschÑftsjahr an. Dieses Datum wird zum Ermitteln des Ultimodatums verwendet.;
                             ENU=Specifies the last date in the closed fiscal year. This date is used to determine the closing date.;
                             NLD=Hiermee wordt de laatste datum van het afgesloten fiscale jaar opgegeven. Deze datum wordt gebruikt om de sluitingsdatum te bepalen.;
                             NOR=Angir den siste datoen i det avsluttede regnskapsÜret. Denne datoen brukes til Ü fastslÜ avslutningsdatoen.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=EndDateReq;
                  OnValidate=BEGIN
                               ValidateEndDate(TRUE);
                             END;
                              }

      { 2   ;2   ;Field     ;
                  Name=GenJournalTemplate;
                  CaptionML=[DEU=Fibu Buch.-Blattvorlage;
                             ENU=Gen. Journal Template;
                             NLD=Fin. dagboeksjabloon;
                             NOR=Finanskladdemal;
                             SVE=Redovisningsjournalmall];
                  ToolTipML=[DEU=Gibt die Fibu Buch.-Blattvorlage an, die von der Stapelverarbeitung verwendet wird.;
                             ENU=Specifies the general journal template that is used by the batch job.;
                             NLD=Hiermee wordt de dagboeksjabloon opgegeven die wordt gebruikt door de batchverwerking.;
                             NOR=Angir finanskladdemalen som brukes av kjõrselen.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=GenJnlLine."Journal Template Name";
                  TableRelation="Gen. Journal Template";
                  OnValidate=BEGIN
                               GenJnlLine."Journal Batch Name" := '';
                               DocNo := '';
                             END;
                              }

      { 3   ;2   ;Field     ;
                  Name=GenJournalBatch;
                  Lookup=Yes;
                  CaptionML=[DEU=Fibu Buch.-Blattname;
                             ENU=Gen. Journal Batch;
                             NLD=Fin. dagboekbatch;
                             NOR=Finanskladd;
                             SVE=Redovisningsjournal];
                  ToolTipML=[DEU=Gibt den Fibu Buch.-Blattnamen an, der von der Stapelverarbeitung verwendet wird.;
                             ENU=Specifies the general journal batch that is used by the batch job.;
                             NLD=Hiermee wordt de dagboekbatch opgegeven die wordt gebruikt door de batchverwerking.;
                             NOR=Angir finanskladden som brukes av kjõrselen.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=GenJnlLine."Journal Batch Name";
                  OnValidate=BEGIN
                               IF GenJnlLine."Journal Batch Name" <> '' THEN BEGIN
                                 GenJnlLine.TESTFIELD("Journal Template Name");
                                 GenJnlBatch.GET(GenJnlLine."Journal Template Name",GenJnlLine."Journal Batch Name");
                               END;
                               ValidateJnl;
                             END;

                  OnLookup=BEGIN
                             GenJnlLine.TESTFIELD("Journal Template Name");
                             GenJnlTemplate.GET(GenJnlLine."Journal Template Name");
                             GenJnlBatch.FILTERGROUP(2);
                             GenJnlBatch.SETRANGE("Journal Template Name",GenJnlLine."Journal Template Name");
                             GenJnlBatch.FILTERGROUP(0);
                             GenJnlBatch."Journal Template Name" := GenJnlLine."Journal Template Name";
                             GenJnlBatch.Name := GenJnlLine."Journal Batch Name";
                             IF PAGE.RUNMODAL(0,GenJnlBatch) = ACTION::LookupOK THEN BEGIN
                               Text := GenJnlBatch.Name;
                               EXIT(TRUE);
                             END;
                           END;
                            }

      { 8   ;2   ;Field     ;
                  Name=DocumentNo;
                  CaptionML=[DEU=Belegnr.;
                             ENU=Document No.;
                             NLD=Documentnr.;
                             NOR=Bilagsnr.;
                             SVE=Dokumentnr];
                  ToolTipML=[DEU=Gibt die Nummer des Dokuments an, das vom Bericht oder der Stapelverarbeitung verarbeitet wird.;
                             ENU=Specifies the number of the document that is processed by the report or batch job.;
                             NLD=Hiermee wordt het nummer opgegeven van het document dat wordt verwerkt door het rapport of de batchverwerking.;
                             NOR=Angir nummeret for dokumentet som behandles av rapporten eller kjõrselen.;
                             SVE=Anger antalet dokument som rapporten eller buntjobbet bearbetar.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=DocNo }

      { 11  ;2   ;Field     ;
                  Name=RetainedEarningsAcc;
                  CaptionML=[DEU=Abschlusskonto GuV;
                             ENU=Retained Earnings Acc.;
                             NLD=Resultaat lopend boekjaar;
                             NOR=Konto for fri egenkapital;
                             SVE=Balanserad vinst eller fîrlust];
                  ToolTipML=[DEU=Gibt das Abschlusskonto GuV an, auf das die Stapelverarbeitung BetrÑge bucht. Dieses Konto sollte mit dem Konto identisch sein, das von der Stapelverarbeitung "GuV-Konten Nullstellung" verwendet wird.;
                             ENU=Specifies the retained earnings account that the batch job posts to. This account should be the same as the account that is used by the Close Income Statement batch job.;
                             NLD=Hiermee wordt de rekening voor de resultaten in het lopende boekjaar opgegeven waarnaar de batchverwerking wordt geboekt. Dit dient dezelfde rekening te zijn als de rekening die wordt gebruikt door de batchverwerking Afsluiten WenV-rekening.;
                             NOR=Angir kontoen for fri egenkapital som kjõrselen bokfõres til. Denne kontoen mÜ vëre den samme som kontoen som brukes av kjõrselen Lukk resultatregnskapet.;
                             SVE=Anger det konto fîr balanserad vinst eller fîrlust som bokfîringen ska gîras till nÑr buntjobbet kîrs. Detta konto bîr vara detsamma som det som anvÑnds i buntjobbet Avslut av resultatkonton.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=RetainedEarningsGLAcc."No.";
                  TableRelation="G/L Account" WHERE (Account Type=CONST(Posting),
                                                     Account Category=FILTER(' '|Equity),
                                                     Income/Balance=CONST(Balance Sheet));
                  OnValidate=BEGIN
                               IF RetainedEarningsGLAcc."No." <> '' THEN BEGIN
                                 RetainedEarningsGLAcc.FIND;
                                 RetainedEarningsGLAcc.CheckGLAcc;
                               END;
                             END;
                              }

      { 6   ;2   ;Field     ;
                  Name=PostToRetainedEarningsAccount;
                  CaptionML=[DEU=Buchen in Konto fÅr nicht ausgeschÅttete Gewinne;
                             ENU=Post to Retained Earnings Acc.;
                             NLD=Boeken naar rekening voor ingehouden winst;
                             NOR=Bokfõr til konto for fri egenkapital];
                  ToolTipML=[DEU=Zeigt an, ob die resultierenden Posten mit dem Konto fÅr nicht ausgeschÅttete Gewinne als Gegenkonto auf jeder Zeile gebucht werden (Details) oder dass die nicht ausgeschÅttete Gewinne als extra Zeile mit einem öbersichtsbetrag gebucht werden (Saldo).;
                             ENU=Specifies if the resulting entries are posted with the Retained Earnings account as a balancing account on each line (Details) or if retained earnings are posted as an extra line with a summarized amount (Balance).;
                             NLD=Hiermee wordt opgegeven of de resulterende posten met de rekening voor ingehouden winst als tegenrekening op elke regel worden geboekt (Details) of dat ingehouden winst als een extra regel met een overzichtsbedrag wordt geboekt (Saldo).;
                             NOR=Angir om de resulterende postene blir bokfõrt med kontoen for fri egenkapital som en motkonto pÜ hver linje (detaljer), eller om fri egenkapital blir bokfõrt som en ekstra linje med et sammendragsbelõp (saldo).];
                  OptionCaptionML=[DEU=Saldo,Details;
                                   ENU=Balance,Details;
                                   NLD=Saldo,Details;
                                   NOR=Saldo,detaljer];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=PostToRetainedEarningsAcc }

      { 4   ;2   ;Field     ;
                  Name=PostingDescription;
                  CaptionML=[DEU=Buchungsbeschreibung;
                             ENU=Posting Description;
                             NLD=Boekingsomschrijving;
                             NOR=Bokfõringsbeskrivelse;
                             SVE=Bokfîringsbeskrivning];
                  ToolTipML=[DEU=Legt die Beschreibung fest, die die Buchung begleitet.;
                             ENU=Specifies the description that accompanies the posting.;
                             NLD=Hiermee wordt de omschrijving opgegeven bij de boeking.;
                             NOR=Angir beskrivelsen som fõlger med bokfõringen.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=PostingDescription }

      { 14  ;2   ;Group     ;
                  CaptionML=[DEU=Nullstellung nach;
                             ENU=Close by;
                             NLD=Afsluiten per;
                             NOR=Lukk per] }

      { 5   ;3   ;Field     ;
                  CaptionML=[DEU=Konzernmandantencode;
                             ENU=Business Unit Code;
                             NLD=Bedrijfsunit;
                             NOR=Konsernkode;
                             SVE=AffÑrsenhetskod];
                  ToolTipML=[DEU=Gibt den Code fÅr den Konzernmandanten in einer Unternehmensgruppenstruktur an.;
                             ENU=Specifies the code for the business unit, in a company group structure.;
                             NLD=Hiermee wordt de code opgegeven voor de business unit, in de groepstructuur van een bedrijf.;
                             NOR=Angir koden for konsernet, i en selskapsgruppestruktur.;
                             SVE=Anger koden fîr affÑrsenheten i ett fîretags gruppstruktur.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=ClosePerBusUnit }

      { 13  ;3   ;Field     ;
                  Name=Dimensions;
                  CaptionML=[DEU=Dimensionen;
                             ENU=Dimensions;
                             NLD=Dimensies;
                             NOR=Dimensjoner;
                             SVE=Dimensioner];
                  ToolTipML=[DEU=Legt Dimensionen fest, wie etwa einen Bereich, ein Projekt oder eine Abteilung, die Sie Verkaufs- oder Einkaufsbelegen zuweisen kînnen, um die Kosten zu verteilen und den Transaktionsverlauf zu analysieren.;
                             ENU=Specifies dimensions, such as area, project, or department, that you can assign to sales and purchase documents to distribute costs and analyze transaction history.;
                             NLD=Hiermee worden dimensies, zoals gebied, project of afdeling, opgegeven die u kunt toewijzen aan verkoop- en inkoopdocumenten om de kosten te verdelen en de transactiegeschiedenis te analyseren.;
                             NOR=Angir dimensjoner, for eksempel omrÜde, prosjekt eller avdeling, som du kan tilordne til salgs- og kjõpsdokumenter for Ü distribuere kostnader og analysere transaksjonshistorikk.];
                  ApplicationArea=#Dimensions;
                  SourceExpr=ColumnDim;
                  Editable=FALSE;
                  OnAssistEdit=VAR
                                 TempSelectedDim2@1002 : TEMPORARY Record 369;
                                 s@1001 : Text[1024];
                               BEGIN
                                 DimSelectionBuf.SetDimSelectionMultiple(3,REPORT::"Close Income Statement",ColumnDim);

                                 SelectedDim.GetSelectedDim(USERID,3,REPORT::"Close Income Statement",'',TempSelectedDim2);
                                 s := CheckDimPostingRules(TempSelectedDim2);
                                 IF s <> '' THEN
                                   MESSAGE(s);
                               END;
                                }

      { 15  ;2   ;Field     ;
                  Name=InventoryPeriodClosed;
                  CaptionML=[DEU=Lagerbuchungsperiode geschlossen;
                             ENU=Inventory Period Closed;
                             NLD=Voorraadperiode afgesloten;
                             NOR=Lagerperiode lukket;
                             SVE=Lagerperiod stÑngd];
                  ToolTipML=[DEU=Gibt an, dass die Lagerbuchungsperiode abgeschlossen wurde.;
                             ENU=Specifies that the inventory period has been closed.;
                             NLD=Hiermee wordt opgegeven dat de voorraadperiode is afgesloten.;
                             NOR=Angir at lagerperioden er lukket.;
                             SVE=Anger att lagerperioden har stÑngts.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=IsInvtPeriodClosed }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      AccountingPeriod@1012 : Record 50;
      SourceCodeSetup@1013 : Record 242;
      GenJnlTemplate@1014 : Record 80;
      GenJnlBatch@1015 : Record 232;
      GenJnlLine@1016 : Record 81;
      RetainedEarningsGLAcc@1020 : Record 15;
      GLSetup@1021 : Record 98;
      DimSelectionBuf@1022 : Record 368;
      ObjTransl@1004 : Record 377;
      SelectedDim@1023 : Record 369;
      TempSelectedDim@1024 : TEMPORARY Record 369;
      EntryNoAmountBuf@1025 : TEMPORARY Record 386;
      NoSeriesMgt@1026 : Codeunit 396;
      GenJnlPostLine@1027 : Codeunit 12;
      DimMgt@1028 : Codeunit 408;
      DimBufMgt@1029 : Codeunit 411;
      Window@1031 : Dialog;
      FiscalYearStartDate@1032 : Date;
      FiscYearClosingDate@1033 : Date;
      EndDateReq@1034 : Date;
      DocNo@1035 : Code[20];
      PostingDescription@1036 : Text[100];
      ClosePerBusUnit@1037 : Boolean;
      ClosePerGlobalDim1@1038 : Boolean;
      ClosePerGlobalDim2@1039 : Boolean;
      ClosePerGlobalDimOnly@1040 : Boolean;
      TotalAmount@1041 : Decimal;
      TotalAmountAddCurr@1042 : Decimal;
      ColumnDim@1043 : Text[250];
      NoOfAccounts@1050 : Integer;
      ThisAccountNo@1049 : Integer;
      Text000@1053 : TextConst 'DEU=Geben Sie das Enddatum des GeschÑftsjahrs ein.;ENU=Enter the ending date for the fiscal year.;NLD=Voer de einddatum voor het boekjaar in.;NOR=Angi sluttdato for regnskapsÜret.';
      Text001@1052 : TextConst 'DEU=Geben Sie eine Belegnr. ein.;ENU=Enter a Document No.;NLD=Voer een documentnummer in.;NOR=Angi et bilagsnr.';
      Text002@1051 : TextConst 'DEU=Geben Sie das Abschlusskonto GuV ein.;ENU=Enter Retained Earnings Account No.;NLD=Voer de rekening Resultaat Lopend Boekjaar in.;NOR=Angi nummer for kontoen for fri egenkapital;SVE=Skriv in kontot fîr balanserad vinst eller fîrlust';
      Text003@1048 : TextConst 'DEU="Bei Verwendung einer BerichtswÑhrung bucht diese Stapelverarbeitung Jahresabschlussposten direkt in der Finanzbuchuchhaltung.  ";ENU="By using an additional reporting currency, this batch job will post closing entries directly to the general ledger.  ";NLD="Als u gebruikmaakt van een extra rapportagevaluta, worden ultimoposten door deze batchverwerking rechtstreeks in het grootboek geboekt.  ";NOR="Hvis det brukes tilleggsrapporteringsvaluta, bokfõrer denne kjõrselen avslutningsposter direkte i Finans.  "';
      Text005@1047 : TextConst 'DEU="Diese Jahresabschl.-Posten werden nicht in ein Fibu Buch.-Blatt Åbertragen, bevor sie in der Finanzbuchhaltung gebucht werden.\\ ";ENU="These closing entries will not be transferred to a general journal before the program posts them to the general ledger.\\ ";NLD="Deze ultimoposten worden niet naar een dagboek overgebracht voordat het programma deze in het grootboek heeft geboekt.\\ ";NOR="Disse avslutningspostene overfõres ikke til en finanskladd fõr programmet bokfõrer dem i Finans.\\ "';
      Text007@1046 : TextConst 'DEU=\Mîchten Sie fortfahren?;ENU=\Do you want to continue?;NLD=\Wilt u doorgaan?;NOR=\Vil du fortsette?';
      Text008@1045 : TextConst 'DEU=Fibu Buch.-Blattzeilen werden erstellt ...\\;ENU=Creating general journal lines...\\;NLD=Maken dagboekregels...\\;NOR=Oppretter finanskladdelinjer...\\';
      Text009@1030 : TextConst 'DEU=Kontonr.            #1##################\;ENU=Account No.         #1##################\;NLD=Rekeningnr.         #1##################\;NOR=Kontonummer         #1##################\';
      Text010@1019 : TextConst 'DEU=In DurchfÅhrung     #2##################\;ENU=Now performing      #2##################\;NLD=Wordt nu uitgevoerd:#2##################\;NOR=Utfõres nÜ          #2##################\';
      Text011@1011 : TextConst 'DEU="                    @3@@@@@@@@@@@@@@@@@@\";ENU="                    @3@@@@@@@@@@@@@@@@@@\";NLD="                    @3@@@@@@@@@@@@@@@@@@\";NOR="                    @3@@@@@@@@@@@@@@@@@@\"';
      Text019@1010 : TextConst 'DEU="                    @4@@@@@@@@@@@@@@@@@@\";ENU="                    @4@@@@@@@@@@@@@@@@@@\";NLD="                    @4@@@@@@@@@@@@@@@@@@\";NOR="                    @4@@@@@@@@@@@@@@@@@@\"';
      Text012@1058 : TextConst 'DEU=Fibu Buch.-Blattzeilen werden erstellt;ENU=Creating Gen. Journal lines;NLD=Maken dagboekregels;NOR=Oppretter finanskladdelinjer';
      Text013@1008 : TextConst 'DEU=BetrÑge werden berechnet;ENU=Calculating Amounts;NLD=Bedragen berekenen;NOR=Beregner belõp';
      Text014@1007 : TextConst 'DEU=Das GeschÑftsjahr muss abgeschlossen sein, bevor die GuV nullgestellt werden kann.;ENU=The fiscal year must be closed before the income statement can be closed.;NLD=U moet het boekjaar afsluiten voor u de resultatenrekening kunt afsluiten.;NOR=RegnskapsÜret mÜ vëre lukket fõr resultatregnskapet kan lukkes.';
      Text015@1006 : TextConst 'DEU=Das GeschÑftsjahr existiert nicht.;ENU=The fiscal year does not exist.;NLD=Het boekjaar bestaat niet.;NOR=RegnskapsÜret finnes ikke.';
      Text017@1005 : TextConst 'DEU=Die Buchungsblattzeilen wurden erfolgreich erstellt.;ENU=The journal lines have successfully been created.;NLD=De dagboekregels zijn gemaakt.;NOR=Kladdelinjene er opprettet.';
      Text016@1003 : TextConst 'DEU=Die Ultimoposten wurden erfolgreich gebucht.;ENU=The closing entries have successfully been posted.;NLD=De ultimoposten zijn geboekt.;NOR=Avslutningspostene er bokfõrt.';
      Text020@1001 : TextConst 'DEU=Die folgenden Sachkonten weisen notwendige Dimensionscodes auf, die nicht ausgewÑhlt wurden:;ENU=The following G/L Accounts have mandatory dimension codes that have not been selected:;NLD=De volgende GB-rekeningen bevatten verplichte dimensiecodes die niet zijn geselecteerd:;NOR=Fõlgende finanskonti har obligatoriske dimensjonskoder som ikke er valgt:';
      Text021@1000 : TextConst 'DEU=\\Um auf diese Konten buchen zu kînnen, mÅssen Sie auch diese Dimensionen auswÑhlen:;ENU=\\In order to post to these accounts you must also select these dimensions:;NLD=\\Als u naar deze rekeningen wilt boeken, moet u ook deze dimensies selecteren:;NOR=\\For Ü kunne bokfõre disse kontoene mÜ du ogsÜ velge disse dimensjonene:';
      MaxEntry@1002 : Integer;
      EntryCount@1057 : Integer;
      LastWindowUpdateDateTime@1044 : DateTime;
      NoFiscalYearsErr@1009 : TextConst 'DEU=Keine abgeschlossenen GeschÑftsjahre vorhanden.;ENU=No closed fiscal year exists.;NLD=Er bestaat geen afgesloten boekjaar.;NOR=Det finnes ingen avsluttede regnskapsÜr.';
      PostToRetainedEarningsAcc@1017 : 'Balance,Details';

    LOCAL PROCEDURE ValidateEndDate@1(RealMode@1000 : Boolean) : Boolean;
    VAR
      OK@1001 : Boolean;
    BEGIN
      IF EndDateReq = 0D THEN
        EXIT;

      OK := AccountingPeriod.GET(EndDateReq + 1);
      IF OK THEN
        OK := AccountingPeriod."New Fiscal Year";
      IF OK THEN BEGIN
        IF NOT AccountingPeriod."Date Locked" THEN BEGIN
          IF NOT RealMode THEN
            EXIT;
          ERROR(Text014);
        END;
        FiscYearClosingDate := CLOSINGDATE(EndDateReq);
        AccountingPeriod.SETRANGE("New Fiscal Year",TRUE);
        OK := AccountingPeriod.FIND('<');
        FiscalYearStartDate := AccountingPeriod."Starting Date";
      END;
      IF NOT OK THEN BEGIN
        IF NOT RealMode THEN
          EXIT;
        ERROR(Text015);
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ValidateJnl@2();
    BEGIN
      DocNo := '';
      IF GenJnlBatch.GET(GenJnlLine."Journal Template Name",GenJnlLine."Journal Batch Name") THEN
        IF GenJnlBatch."No. Series" <> '' THEN
          DocNo := NoSeriesMgt.TryGetNextNo(GenJnlBatch."No. Series",EndDateReq);
    END;

    LOCAL PROCEDURE HandleGenJnlLine@4();
    BEGIN
      OnBeforeHandleGenJnlLine(GenJnlLine);

      GenJnlLine."Additional-Currency Posting" :=
        GenJnlLine."Additional-Currency Posting"::None;
      IF GLSetup."Additional Reporting Currency" <> '' THEN BEGIN
        GenJnlLine."Source Currency Code" := GLSetup."Additional Reporting Currency";
        IF ZeroGenJnlAmount THEN BEGIN
          GenJnlLine."Additional-Currency Posting" :=
            GenJnlLine."Additional-Currency Posting"::"Additional-Currency Amount Only";
          GenJnlLine.VALIDATE(Amount,GenJnlLine."Source Currency Amount");
          GenJnlLine."Source Currency Amount" := 0;
        END;
        IF GenJnlLine.Amount <> 0 THEN BEGIN
          GenJnlPostLine.RUN(GenJnlLine);
          IF DocNo = NoSeriesMgt.GetNextNo(GenJnlBatch."No. Series",EndDateReq,FALSE) THEN
            NoSeriesMgt.SaveNoSeries;
        END;
      END ELSE
        IF NOT ZeroGenJnlAmount THEN
          GenJnlLine.INSERT;
    END;

    LOCAL PROCEDURE CalcSumsInFilter@18(VAR GLEntrySource@1000 : Record 17;VAR Offset@1001 : Integer);
    VAR
      GLEntry@1002 : Record 17;
    BEGIN
      GLEntry.COPYFILTERS(GLEntrySource);
      IF ClosePerBusUnit THEN BEGIN
        GLEntry.SETRANGE("Business Unit Code",GLEntrySource."Business Unit Code");
        GenJnlLine."Business Unit Code" := GLEntrySource."Business Unit Code";
      END;
      IF ClosePerGlobalDim1 THEN BEGIN
        GLEntry.SETRANGE("Global Dimension 1 Code",GLEntrySource."Global Dimension 1 Code");
        IF ClosePerGlobalDim2 THEN
          GLEntry.SETRANGE("Global Dimension 2 Code",GLEntrySource."Global Dimension 2 Code");
      END;

      GLEntry.CALCSUMS(Amount);
      GLEntrySource.Amount := GLEntry.Amount;
      TotalAmount += GLEntrySource.Amount;
      IF GLSetup."Additional Reporting Currency" <> '' THEN BEGIN
        GLEntry.CALCSUMS("Additional-Currency Amount");
        GLEntrySource."Additional-Currency Amount" := GLEntry."Additional-Currency Amount";
        TotalAmountAddCurr += GLEntrySource."Additional-Currency Amount";
      END;
      Offset := GLEntry.COUNT - 1;
    END;

    LOCAL PROCEDURE GetGLEntryDimensions@7(EntryNo@1000 : Integer;VAR DimBuf@1001 : Record 360;DimensionSetID@1002 : Integer);
    VAR
      DimSetEntry@1004 : Record 480;
    BEGIN
      DimSetEntry.SETRANGE("Dimension Set ID",DimensionSetID);
      IF DimSetEntry.FINDSET THEN
        REPEAT
          DimBuf."Table ID" := DATABASE::"G/L Entry";
          DimBuf."Entry No." := EntryNo;
          DimBuf."Dimension Code" := DimSetEntry."Dimension Code";
          DimBuf."Dimension Value Code" := DimSetEntry."Dimension Value Code";
          DimBuf.INSERT;
        UNTIL DimSetEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE CheckDimPostingRules@6(VAR SelectedDim@1002 : Record 369) : Text[1024];
    VAR
      DefaultDim@1000 : Record 352;
      ErrorText@1001 : Text[1024];
      DimText@1004 : Text[1024];
      PrevAcc@1003 : Code[20];
      Handled@1005 : Boolean;
    BEGIN
      OnBeforeCheckDimPostingRules(SelectedDim,ErrorText,Handled);
      IF Handled THEN
        EXIT(ErrorText);

      DefaultDim.SETRANGE("Table ID",DATABASE::"G/L Account");
      DefaultDim.SETFILTER(
        "Value Posting",'%1|%2',
        DefaultDim."Value Posting"::"Same Code",DefaultDim."Value Posting"::"Code Mandatory");

      IF DefaultDim.FIND('-') THEN
        REPEAT
          SelectedDim.SETRANGE("Dimension Code",DefaultDim."Dimension Code");
          IF NOT SelectedDim.FIND('-') THEN BEGIN
            IF STRPOS(DimText,DefaultDim."Dimension Code") < 1 THEN
              DimText := DimText + ' ' + FORMAT(DefaultDim."Dimension Code");
            IF PrevAcc <> DefaultDim."No." THEN BEGIN
              PrevAcc := DefaultDim."No.";
              IF ErrorText = '' THEN
                ErrorText := Text020;
              ErrorText := ErrorText + ' ' + FORMAT(DefaultDim."No.");
            END;
          END;
          SelectedDim.SETRANGE("Dimension Code");
        UNTIL (DefaultDim.NEXT = 0) OR (STRLEN(ErrorText) > MAXSTRLEN(ErrorText) - MAXSTRLEN(DefaultDim."No.") - STRLEN(Text021) - 1);
      IF ErrorText <> '' THEN
        ErrorText := COPYSTR(ErrorText + Text021 + DimText,1,MAXSTRLEN(ErrorText));
      EXIT(ErrorText);
    END;

    LOCAL PROCEDURE IsInvtPeriodClosed@3() : Boolean;
    VAR
      AccPeriod@1001 : Record 50;
      InvtPeriod@1000 : Record 5814;
    BEGIN
      IF EndDateReq = 0D THEN
        EXIT;
      AccPeriod.GET(EndDateReq + 1);
      AccPeriod.NEXT(-1);
      EXIT(InvtPeriod.IsInvtPeriodClosed(AccPeriod."Starting Date"));
    END;

    [External]
    PROCEDURE InitializeRequestTest@5(EndDate@1000 : Date;GenJournalLine@1001 : Record 81;GLAccount@1002 : Record 15;CloseByBU@1003 : Boolean);
    BEGIN
      EndDateReq := EndDate;
      GenJnlLine := GenJournalLine;
      ValidateJnl;
      RetainedEarningsGLAcc := GLAccount;
      ClosePerBusUnit := CloseByBU;
    END;

    LOCAL PROCEDURE ZeroGenJnlAmount@8() : Boolean;
    BEGIN
      EXIT((GenJnlLine.Amount = 0) AND (GenJnlLine."Source Currency Amount" <> 0))
    END;

    LOCAL PROCEDURE GroupSum@10() : Boolean;
    BEGIN
      EXIT(ClosePerGlobalDimOnly AND (ClosePerBusUnit OR ClosePerGlobalDim1));
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCheckDimPostingRules@9(VAR SelectedDimension@1002 : Record 369;VAR ErrorText@1000 : Text[1024];VAR Handled@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeHandleGenJnlLine@11(VAR GenJournalLine@1000 : Record 81);
    BEGIN
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

