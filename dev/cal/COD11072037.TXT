OBJECT Codeunit 11072037 FA Reclass. Transfer Batch
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03,4PS14.00 (5643);
  }
  PROPERTIES
  {
    TableNo=11072084;
    Permissions=TableData 11072083=imd;
    OnRun=BEGIN
            FAReclassJnlLine.COPY(Rec);
            Code;
            Rec := FAReclassJnlLine;
          END;

  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'DEU=Buch.-Blattname       #1##########\\;ENU=Journal Batch Name    #1##########\\;NLD=Dagboekbatch          #1##########\\;NOR=Kladdenavn            #1##########\\;SVE=Journalnamn           #1##########\\';
      Text002@1002 : TextConst 'DEU=Zeilen prfen         #2######\;ENU=Checking lines        #2######\;NLD=Controleren regels    #2######\;NOR=Kontrollerer linjer   #2######\;SVE=Kontrollerar rader    #2######\';
      Text003@1003 : TextConst 'DEU=Zeilen buchen         #3###### @4@@@@@@@@@@@@@;ENU=Posting lines         #3###### @4@@@@@@@@@@@@@;NLD=Boeken regels         #3###### @4@@@@@@@@@@@@@;NOR=Bokf›rer linjer       #3###### @4@@@@@@@@@@@@@;SVE=Bokf”ring rader       #3###### @4@@@@@@@@@@@@@';
      FAReclassJnlLine@1004 : Record 11072084;
      FAReclassJnlTempl@1005 : Record 11072082;
      FAReclassJnlBatch@1006 : Record 11072083;
      FAReclassJnlLine2@1007 : Record 11072084;
      FAReclassJnlLine3@1008 : Record 11072084;
      FAReclassCheckLine@1009 : Codeunit 11072035;
      FAReclassTransferLine@1010 : Codeunit 11072036;
      Window@1011 : Dialog;
      OneFAReclassDone@1012 : Boolean;
      LineCounter@1013 : Integer;
      StartLineNo@1014 : Integer;
      NoOfRecords@1015 : Integer;
      NoCommit@1210190000 : Boolean;
      TmpFAJnlLine@1100409001 : TEMPORARY Record 5621;
      DoFillTmpFAJnlLine@1100409000 : Boolean;

    LOCAL PROCEDURE Code@1();
    BEGIN
      WITH FAReclassJnlLine DO BEGIN
        SETRANGE("Journal Template Name","Journal Template Name");
        SETRANGE("Journal Batch Name","Journal Batch Name");
        LOCKTABLE;

        FAReclassJnlTempl.GET("Journal Template Name");
        FAReclassJnlBatch.GET("Journal Template Name","Journal Batch Name");

        IF NOT FIND('=><') THEN BEGIN
          "Line No." := 0;
          EXIT;
        END;

        Window.OPEN(
          Text001 +
          Text002 +
          Text003);
        Window.UPDATE(1,"Journal Batch Name");

        LineCounter := 0;
        StartLineNo := "Line No.";
        REPEAT
          LineCounter := LineCounter + 1;
          Window.UPDATE(2,LineCounter);
          FAReclassCheckLine.RUN(FAReclassJnlLine);
          IF NEXT = 0 THEN
            FIND('-');
        UNTIL "Line No." = StartLineNo;
        NoOfRecords := LineCounter;

        LineCounter := 0;
        OneFAReclassDone := FALSE;
        SETCURRENTKEY("Journal Template Name","Journal Batch Name","FA Posting Date");
        FIND('-');
        REPEAT
          LineCounter := LineCounter + 1;
          Window.UPDATE(3,LineCounter);
          Window.UPDATE(4,ROUND(LineCounter / NoOfRecords * 10000,1));
          IF DoFillTmpFAJnlLine THEN FAReclassTransferLine.SetFillTmpFAJnlLine();  //**4PS.n C000883
          FAReclassTransferLine.FAReclassLine(FAReclassJnlLine,OneFAReclassDone);
          IF DoFillTmpFAJnlLine THEN FAReclassTransferLine.GetTmpFAJnlLine(TmpFAJnlLine);  //**4PS.n C000883
        UNTIL NEXT = 0;

        INIT;
        IF OneFAReclassDone THEN
          "Line No." := 1
        ELSE
          "Line No." := 0;

        IF OneFAReclassDone THEN BEGIN
          FAReclassJnlLine2.COPYFILTERS(FAReclassJnlLine);
          FAReclassJnlLine2.SETFILTER("FA No.",'<>%1','');
          IF FAReclassJnlLine2.FINDLAST THEN; // Remember the last line
          DELETEALL;

          FAReclassJnlLine3.SETRANGE("Journal Template Name","Journal Template Name");
          FAReclassJnlLine3.SETRANGE("Journal Batch Name","Journal Batch Name");
          IF FAReclassJnlTempl."Increment Batch Name" THEN
            IF NOT FAReclassJnlLine3.FINDLAST THEN
              IF INCSTR("Journal Batch Name") <> '' THEN BEGIN
                FAReclassJnlBatch.GET("Journal Template Name","Journal Batch Name");
                FAReclassJnlBatch.DELETE;
                FAReclassJnlBatch.Name := INCSTR("Journal Batch Name");
                IF FAReclassJnlBatch.INSERT THEN;
                "Journal Batch Name" := FAReclassJnlBatch.Name;
              END;

          FAReclassJnlLine3.SETRANGE("Journal Batch Name","Journal Batch Name");
          IF NOT FAReclassJnlLine3.FINDLAST THEN BEGIN
            FAReclassJnlLine3.INIT;
            FAReclassJnlLine3."Journal Template Name" := "Journal Template Name";
            FAReclassJnlLine3."Journal Batch Name" := "Journal Batch Name";
            FAReclassJnlLine3."Line No." := 10000;
            FAReclassJnlBatch.GET("Journal Template Name","Journal Batch Name");
            FAReclassJnlLine3."Posting Date" := FAReclassJnlLine2."Posting Date";
            OnBeforeFAReclassJnlLineInsert(FAReclassJnlLine,FAReclassJnlLine2,FAReclassJnlLine3);
            FAReclassJnlLine3.INSERT;
          END;
        END;

        IF NOT NoCommit THEN //**4PS.n
          COMMIT;
        CLEAR(FAReclassCheckLine);
        CLEAR(FAReclassTransferLine);
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeFAReclassJnlLineInsert@2(VAR FAReclassJournalLine@1000 : Record 11072084;VAR FAReclassJournalLine2@1001 : Record 11072084;VAR FAReclassJournalLine3@1002 : Record 11072084);
    BEGIN
    END;

    PROCEDURE DoNotCommit@1210190000();
    BEGIN
      //**4PS
      NoCommit := TRUE;
    END;

    PROCEDURE SetFillTmpFAJnlLine@1100409000();
    BEGIN
      //**4PS C000883
      DoFillTmpFAJnlLine := TRUE;
      TmpFAJnlLine.RESET;
      TmpFAJnlLine.DELETEALL;
    END;

    PROCEDURE GetTmpFAJnlLine@1100409001(VAR FAJnlLineTmp@1100409000 : TEMPORARY Record 5621);
    BEGIN
      //**4PS C000883
      WITH TmpFAJnlLine DO BEGIN
        IF FINDFIRST THEN BEGIN
          REPEAT
            IF NOT FAJnlLineTmp.GET("Journal Template Name", "Journal Batch Name", "Line No.") THEN BEGIN
              FAJnlLineTmp := TmpFAJnlLine;
              FAJnlLineTmp.INSERT;
            END;
          UNTIL NEXT = 0;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

