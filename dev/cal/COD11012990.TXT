OBJECT Codeunit 11012990 External Doc Registration WS
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text001@1100529100 : TextConst 'DEU=Beleg %1 besteht nicht.;ENU=Document %1 does not exist.;NLD=Document %1 bestaat niet.';
      Text002@1100529101 : TextConst 'DEU=Keine DocNo.;ENU=No DocNo.;NLD=Geen DocNo.';
      Text003@1100529102 : TextConst 'DEU=Kein Content.;ENU=No Content.;NLD=Geen Content.';
      Text004@1100529103 : TextConst 'DEU=Kein FileName.;ENU=No FileName.;NLD=Geen FileName.';
      Text005@1100529104 : TextConst 'DEU=DocNo nicht vorhanden in %1.;ENU=DocNo not valid for %1.;NLD=DocNo bestaat niet in %1.';

    [External]
    PROCEDURE UploadFile@1100529100(DocNo@1100529107 : Code[20];CreditMemo@1100528200 : Boolean;Posted@1100528201 : Boolean;Base64EncodedContents@1100529102 : BigText;VAR FileName@1100528209 : Text;VAR DocumentNo@1100528208 : Code[20];VAR ErrorMessage@1100529101 : Text) : Boolean;
    VAR
      DocumentProperties@1100528210 : Record 11012746;
      PurchaseHeader@1100528204 : Record 38;
      PurchInvHeader@1100528202 : Record 122;
      PurchCrMemoHdr@1100528203 : Record 124;
      DocumentManagementWebservice@1100529100 : Codeunit 11012405;
      TableNo@1100528205 : Integer;
      EntityKey@1100525000 : ARRAY [5] OF Code[20];
      VendorNo@1100528213 : Code[20];
      KeyValueInteger@1100528206 : Integer;
      ModifyDateTime@1100528207 : DateTime;
      ContactBusinessRelation@1100528214 : Record 5054;
    BEGIN
      IF DocNo = '' THEN BEGIN
        ErrorMessage := Text002;
        EXIT(FALSE);
      END;
      IF Base64EncodedContents.LENGTH = 0 THEN BEGIN
        ErrorMessage := Text003;
        EXIT(FALSE);
      END;
      IF FileName = '' THEN BEGIN
        ErrorMessage := Text004;
        EXIT(FALSE);
      END;
      IF Posted THEN BEGIN
        IF CreditMemo THEN BEGIN
          IF NOT PurchCrMemoHdr.GET(DocNo) THEN BEGIN
            ErrorMessage := STRSUBSTNO(Text005, PurchCrMemoHdr.TABLECAPTION);
            EXIT(FALSE);
          END;
          TableNo := DATABASE::"Purch. Cr. Memo Hdr.";
          EntityKey[1] := PurchCrMemoHdr."No.";
          VendorNo := PurchCrMemoHdr."Buy-from Vendor No.";
        END ELSE BEGIN
          IF NOT PurchInvHeader.GET(DocNo) THEN BEGIN
            ErrorMessage := STRSUBSTNO(Text005, PurchInvHeader.TABLECAPTION);
            EXIT(FALSE);
          END;
          TableNo := DATABASE::"Purch. Inv. Header";
          EntityKey[1] := PurchInvHeader."No.";
          VendorNo := PurchInvHeader."Buy-from Vendor No.";
        END;
      END ELSE BEGIN
        IF CreditMemo THEN BEGIN
          IF NOT PurchaseHeader.GET(PurchaseHeader."Document Type"::"Credit Memo", DocNo) THEN BEGIN
            ErrorMessage := STRSUBSTNO(Text005,PurchaseHeader.TABLECAPTION);
            EXIT(FALSE);
          END;
        END ELSE BEGIN
          IF NOT PurchaseHeader.GET(PurchaseHeader."Document Type"::Invoice, DocNo) THEN BEGIN
            ErrorMessage := STRSUBSTNO(Text005,PurchaseHeader.TABLECAPTION);
            EXIT(FALSE);
          END;
        END;
        TableNo := DATABASE::"Purchase Header";
        KeyValueInteger := PurchaseHeader."Document Type";
        EntityKey[1] := FORMAT(KeyValueInteger);
        EntityKey[2] := PurchaseHeader."No.";
        VendorNo := PurchaseHeader."Buy-from Vendor No.";
      END;

      IF NOT DocumentManagementWebservice.SaveDocumentBase64Encoded(TableNo,EntityKey,DocumentNo,Base64EncodedContents,ErrorMessage,FileName,ModifyDateTime,'') THEN
        EXIT(FALSE);

      IF VendorNo <> '' THEN
        IF ContactBusinessRelation.FindByRelation(ContactBusinessRelation."Link to Table"::Vendor,VendorNo) THEN
          IF DocumentProperties.GET(DocumentNo) AND (ContactBusinessRelation."Contact No." <> '') THEN BEGIN
            DocumentProperties.VALIDATE("Contact No.", ContactBusinessRelation."Contact No.");
            DocumentProperties.MODIFY(TRUE);
          END;

      EXIT(TRUE);
    END;

    [External]
    PROCEDURE DownloadFile@1100528105(DocumentNo@1100528102 : Code[20];VAR Base64EncodedContents@1100528101 : BigText;VAR ErrorMessage@1100528100 : Text;VAR FileName@1100529101 : Text;VAR ModifyDateTime@1100529105 : DateTime;VAR FileLength@1100529103 : BigInteger) : Boolean;
    VAR
      DocumentProperties@1100528103 : Record 11012746;
      DocumentManagementWebservice@1100529100 : Codeunit 11012405;
    BEGIN
      IF NOT DocumentProperties.GET(DocumentNo) THEN BEGIN
        ErrorMessage := STRSUBSTNO(Text001, DocumentNo);
        EXIT(FALSE);
      END;
      DocumentManagementWebservice.GetPropertiesFromFile(DocumentNo,FileName,ModifyDateTime,FileLength);
      EXIT(DocumentManagementWebservice.GetDocumentBase64Encoded(DocumentNo,Base64EncodedContents,ErrorMessage));
    END;

    BEGIN
    END.
  }
}

