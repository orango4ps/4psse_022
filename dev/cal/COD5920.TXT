OBJECT Codeunit 5920 ServItemManagement
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03,4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to create a %1?;NOR=Vil du opprette en %1?;SVE=Vill du skapa %1?';
      Text001@1001 : TextConst 'ENU=Service item %1 was created. This service item does not belong to any service contract.;NOR=Servicevare %1 ble opprettet. Denne servicevaren tilh›rer ikke noen servicekontrakt.;SVE=Serviceartikel %1 har skapats. Serviceartikeln tillh”r inte n†got servicekontrakt.';
      Item@1002 : Record 27;
      ServItemGr@1003 : Record 5904;
      ServItem@1004 : Record 5940;
      ServMgtSetup@1006 : Record 5911;
      ServHeader@1009 : Record 5900;
      ServInvoiceLine@1015 : Record 5902;
      ItemUnitOfMeasure@1020 : Record 5404;
      TempReservEntry@1014 : TEMPORARY Record 337;
      GLSetup@1021 : Record 98;
      ServiceItemTEMP@1200 : TEMPORARY Record 5940;
      ServiceItemCompTEMP@1201 : TEMPORARY Record 5941;
      ResSkillMgt@1013 : Codeunit 5931;
      NoSeriesMgt@1007 : Codeunit 396;
      ServLogMgt@1008 : Codeunit 5906;
      ServOrderMgt@1018 : Codeunit 5900;
      NextLineNo@1010 : Integer;
      Index@1011 : Integer;
      Text002@1012 : TextConst 'ENU=You have inserted a %1 on the selected %2.\Would you like to copy this information into the %1 field for the newly created %3?;NOR=Du har satt inn en %1 p† den valgte %2.\Vil du kopiere informasjonen til feltet %1 for %3 som nettopp er opprettet?;SVE=Du har infogad en %1 p† vald %2.\Vill du kopiera den h„r informationen till f„ltet %1 f”r den nyligen skapade %3?';
      Text003@1016 : TextConst 'ENU=Posting cannot be completed successfully. %1 %2  belongs to the %3 that requires creating service items. Check if the %4 field contains a whole number.;NOR=Bokf›ringen kan ikke fullf›res p† riktig m†te. %1 %2  tilh›rer %3 som krever oppretting av servicevarer. Kontroller om feltet %4 inneholder et heltall.;SVE=Bokf”ringen kan inte slutf”ras. %1 %2 tillh”r %3 som kr„vs f”r att skapa serviceartiklar. Kontrollera om f„ltet %4 inneh†ller ett heltal.';
      Text004@1017 : TextConst 'ENU=Posting cannot be completed successfully. For the items that are used to replace or create service item components, the %1 field on the %2 must contain a whole number.;NOR=Bokf›ring kan ikke fullf›res. Feltet %1 i %2 m† inneholde et heltall for elementene som brukes til † erstatte eller opprette servicevarekomponenter.;SVE=Det g†r inte att slutf”ra bokf”ringen. F”r artiklar som anv„nds f”r att ers„tta eller skapa serviceartikelkomponenter m†ste f„ltet %1 p† %2 inneh†lla ett helt tal.';
      Text005@1120 : TextConst 'ENU=The service item that is linked to the order has been deleted.;NOR=Servicevaren som er koblet til ordren, er slettet.;SVE=Serviceartikeln som „r l„nkad till ordern har tagits bort.';

    [External]
    PROCEDURE AddOrReplaceSIComponent@5(VAR ServLine@1000 : Record 5902;ServHeader@1010 : Record 5900;ServShptDocNo@1007 : Code[20];ServShptLineNo@1008 : Integer;VAR TempTrackingSpecification@1009 : TEMPORARY Record 336);
    VAR
      ServItemComponent@1003 : Record 5941;
      ServItemComponent2@1002 : Record 5941;
      NewServItemComponent@1001 : Record 5941;
      ComponentLine@1004 : Integer;
      x@1006 : Integer;
      TrackingLinesExist@1005 : Boolean;
    BEGIN
      IF (ServLine.Type <> ServLine.Type::Item) OR (ServLine."Qty. to Ship" = 0) THEN
        EXIT;

      OnBeforeReplaceSIComponent(ServLine,ServHeader,ServShptDocNo,ServShptLineNo,TempTrackingSpecification);

      WITH ServLine DO
        CASE "Spare Part Action" OF
          "Spare Part Action"::"Component Replaced":
            BEGIN
              CheckWholeNumber(ServLine);
              NewServItemComponent.LOCKTABLE;
              NewServItemComponent.RESET;
              NewServItemComponent.SETRANGE(Active,FALSE);
              NewServItemComponent.SETRANGE("Parent Service Item No.","Service Item No.");
              IF NewServItemComponent.FINDLAST THEN
                ComponentLine := NewServItemComponent."Line No."
              ELSE
                ComponentLine := 0;

              IF ServItemComponent2.GET(TRUE,"Service Item No.","Component Line No.") THEN BEGIN
                ServItemComponent := ServItemComponent2;
                ServItemComponent."Service Order No." := "Document No.";
                CLEAR(ServLogMgt);
                ServLogMgt.ServItemComponentRemoved(ServItemComponent);
                ServItemComponent.DELETE;
                ServItemComponent2.Active := FALSE;
                ServItemComponent2."Line No." := ComponentLine + 10000;
                ServItemComponent2."From Line No." := ServItemComponent."Line No.";
                ServItemComponent2."Service Order No." := ServHeader."No.";
                ServItemComponent2.INSERT;
                IF ServItemComponent2.Type = ServItemComponent2.Type::"Service Item" THEN BEGIN
                  ServItem.GET(ServItemComponent2."No.");
                  ServItem.Status := ServItem.Status::Defective;
                  ServItem.MODIFY;
                END;
              END;

              InitNewServItemComponent(NewServItemComponent,ServLine,ServItemComponent."Line No.",ServHeader."Posting Date");
              TempTrackingSpecification.SetSourceFilter(DATABASE::"Service Line","Document Type","Document No.","Line No.",FALSE);
              TrackingLinesExist := TempTrackingSpecification.FIND('-');
              IF TrackingLinesExist THEN BEGIN
                NewServItemComponent."Serial No." := TempTrackingSpecification."Serial No.";
                IF TempTrackingSpecification.NEXT = 0 THEN
                  TrackingLinesExist := FALSE;
              END;
              OnBeforeInsertNewServItemComponent(NewServItemComponent,ServLine);
              NewServItemComponent.INSERT;
              CLEAR(ServLogMgt);
              ServLogMgt.ServItemComponentAdded(NewServItemComponent);

              NewServItemComponent.SETRANGE(Active,TRUE);
              IF NewServItemComponent.FINDLAST THEN
                ComponentLine := NewServItemComponent."Line No."
              ELSE
                ComponentLine := 0;

              FOR x := 2 TO "Qty. to Ship" DO BEGIN
                ComponentLine := ComponentLine + 10000;
                InitNewServItemComponent(NewServItemComponent,ServLine,ComponentLine,ServHeader."Posting Date");
                IF TrackingLinesExist THEN BEGIN
                  NewServItemComponent."Serial No." := TempTrackingSpecification."Serial No.";
                  IF TempTrackingSpecification.NEXT = 0 THEN
                    TrackingLinesExist := FALSE;
                END;
                OnBeforeInsertNewServItemComponent(NewServItemComponent,ServLine);
                NewServItemComponent.INSERT;
                CLEAR(ServLogMgt);
                ServLogMgt.ServItemComponentAdded(NewServItemComponent);
              END;
            END;
          "Spare Part Action"::"Component Installed":
            BEGIN
              CheckWholeNumber(ServLine);
              NewServItemComponent.LOCKTABLE;
              NewServItemComponent.RESET;
              NewServItemComponent.SETRANGE(Active,TRUE);
              NewServItemComponent.SETRANGE("Parent Service Item No.","Service Item No.");
              IF NewServItemComponent.FINDLAST THEN
                ComponentLine := NewServItemComponent."Line No."
              ELSE
                ComponentLine := 0;

              TempTrackingSpecification.SetSourceFilter(DATABASE::"Service Line","Document Type","Document No.","Line No.",FALSE);
              TrackingLinesExist := TempTrackingSpecification.FIND('-');

              FOR x := 1 TO "Qty. to Ship" DO BEGIN
                ComponentLine := ComponentLine + 10000;
                InitNewServItemComponent(NewServItemComponent,ServLine,ComponentLine,ServHeader."Posting Date");
                NewServItemComponent."Service Order No." := "Document No.";
                IF TrackingLinesExist THEN BEGIN
                  NewServItemComponent."Serial No." := TempTrackingSpecification."Serial No.";
                  IF TempTrackingSpecification.NEXT = 0 THEN
                    TrackingLinesExist := FALSE;
                END;
                OnBeforeInsertNewServItemComponent(NewServItemComponent,ServLine);
                NewServItemComponent.INSERT;
                CLEAR(ServLogMgt);
                ServLogMgt.ServItemComponentAdded(NewServItemComponent);
              END;
            END;
          "Spare Part Action"::Permanent,
          "Spare Part Action"::"Temporary":
            BEGIN
              ServItem.GET("Service Item No.");
              ServOrderMgt.ReplacementCreateServItem(ServItem,ServLine,
                ServShptDocNo,ServShptLineNo,TempTrackingSpecification);
            END;
        END;
    END;

    LOCAL PROCEDURE InitNewServItemComponent@11(VAR NewServItemComponent@1000 : Record 5941;ServiceLine@1001 : Record 5902;NextLineNo@1002 : Integer;PostingDate@1003 : Date);
    BEGIN
      WITH NewServItemComponent DO BEGIN
        INIT;
        "Parent Service Item No." := ServiceLine."Service Item No.";
        "Line No." := NextLineNo;
        Active := TRUE;
        "No." := ServiceLine."No.";
        Type := Type::Item;
        "Date Installed" := PostingDate;
        Description := ServiceLine.Description;
        "Description 2" := ServiceLine."Description 2";
        "Variant Code" := ServiceLine."Variant Code";
        "Service Order No." := ServiceLine."Document No.";
        "Last Date Modified" := TODAY;
      END;
    END;

    LOCAL PROCEDURE InsertServItemComponent@13(VAR ServiceItemComponent@1000 : Record 5941;BOMComponent@1004 : Record 90;BOMComponent2@1001 : Record 90;SalesHeader@1002 : Record 36;SalesShipmentLine@1003 : Record 111);
    BEGIN
      WITH ServiceItemComponent DO BEGIN
        INIT;
        Active := TRUE;
        "Parent Service Item No." := ServItem."No.";
        "Line No." := NextLineNo;
        Type := Type::Item;
        "No." := BOMComponent2."No.";
        "Date Installed" := SalesHeader."Posting Date";
        Description := BOMComponent2.Description;
        "Serial No." := '';
        "Variant Code" := BOMComponent2."Variant Code";
        OnBeforeInsertServItemComponent(ServiceItemComponent,ServItem,SalesHeader,SalesShipmentLine,BOMComponent,BOMComponent2);
        INSERT;
      END;
    END;

    [External]
    PROCEDURE CreateServItemOnSalesLineShpt@3(SalesHeader@1000 : Record 36;SalesLine@1003 : Record 37;SalesShipmentLine@1008 : Record 111);
    VAR
      ServItemComponent@1004 : Record 5941;
      ItemTrackingCode@1005 : Record 6502;
      BOMComp@1001 : Record 90;
      BOMComp2@1002 : Record 90;
      PurchaseHeader@1012 : Record 38;
      TrackingLinesExist@1006 : Boolean;
      x@1007 : Integer;
      ServItemWithSerialNoExist@1009 : Boolean;
      IsHandled@1010 : Boolean;
      WarrantyStartDate@1011 : Date;
    BEGIN
      IsHandled := FALSE;
      OnBeforeCreateServItemOnSalesLineShpt(SalesHeader,SalesLine,SalesShipmentLine,IsHandled);
      IF IsHandled THEN
        EXIT;

      IF NOT (SalesHeader."Document Type" IN [SalesHeader."Document Type"::Order,SalesHeader."Document Type"::Invoice]) THEN
        EXIT;

      IF (SalesLine.Type = SalesLine.Type::Item) AND (SalesLine."Qty. to Ship (Base)" > 0) THEN BEGIN
        Item.GET(SalesLine."No.");
        IF NOT ItemTrackingCode.GET(Item."Item Tracking Code") THEN
          ItemTrackingCode.INIT;
        IF ServItemGr.GET(Item."Service Item Group") AND ServItemGr."Create Service Item" THEN BEGIN
          IF SalesLine."Qty. to Ship (Base)" <> ROUND(SalesLine."Qty. to Ship (Base)",1) THEN
            ERROR(
              Text003,
              Item.TABLECAPTION,
              Item."No.",
              ServItemGr.TABLECAPTION,
              SalesLine.FIELDCAPTION("Qty. to Ship (Base)"));

          TempReservEntry.SETRANGE("Item No.",SalesLine."No.");
          TempReservEntry.SETRANGE("Location Code",SalesLine."Location Code");
          TempReservEntry.SETRANGE("Variant Code",SalesLine."Variant Code");
          TempReservEntry.SETRANGE("Source Subtype",SalesLine."Document Type");
          TempReservEntry.SETRANGE("Source ID",SalesLine."Document No.");
          TempReservEntry.SETRANGE("Source Ref. No.",SalesLine."Line No.");
          TrackingLinesExist := TempReservEntry.FINDSET;

          ServiceItemTEMP.DELETEALL;
          ServiceItemCompTEMP.DELETEALL;

          GLSetup.GET;
          ServMgtSetup.GET;
          ServMgtSetup.TESTFIELD("Service Item Nos.");
          FOR x := 1 TO SalesLine."Qty. to Ship (Base)" DO BEGIN
            CLEAR(ServItem);
            ServItemWithSerialNoExist := FALSE;
            IF TempReservEntry."Serial No." <> '' THEN BEGIN
              ServItem.SETRANGE("Item No.",SalesLine."No.");
              ServItem.SETRANGE("Serial No.",TempReservEntry."Serial No.");
              IF ServItem.FINDFIRST THEN
                ServItemWithSerialNoExist := TRUE;
            END;
            IF (TempReservEntry."Serial No." = '') OR (NOT ServItemWithSerialNoExist) THEN BEGIN
              ServItem.INIT;
              NoSeriesMgt.InitSeries(
                ServMgtSetup."Service Item Nos.",ServItem."No. Series",0D,ServItem."No.",ServItem."No. Series");
              ServItem.INSERT;
            END;
            ServItem."Sales/Serv. Shpt. Document No." := SalesShipmentLine."Document No.";
            ServItem."Sales/Serv. Shpt. Line No." := SalesShipmentLine."Line No.";
            ServItem."Shipment Type" := ServItem."Shipment Type"::Sales;
            ServItem.VALIDATE(Description,
              COPYSTR(SalesLine.Description,1,MAXSTRLEN(ServItem.Description)));
            ServItem."Description 2" := COPYSTR(
                STRSUBSTNO('%1 %2',SalesHeader."Document Type",SalesHeader."No."),
                1,MAXSTRLEN(ServItem."Description 2"));
            ServItem.VALIDATE("Customer No.",SalesHeader."Sell-to Customer No.");
            ServItem.VALIDATE("Ship-to Code",SalesHeader."Ship-to Code");
            ServItem.OmitAssignResSkills(TRUE);
            ServItem.VALIDATE("Item No.",Item."No.");
            ServItem.OmitAssignResSkills(FALSE);
            IF TrackingLinesExist THEN
              ServItem."Serial No." := TempReservEntry."Serial No.";
            ServItem."Variant Code" := SalesLine."Variant Code";

            ItemUnitOfMeasure.GET(Item."No.",SalesLine."Unit of Measure Code");

            ServItem.VALIDATE("Sales Unit Cost",ROUND(SalesLine."Unit Cost (LCY)" /
                ItemUnitOfMeasure."Qty. per Unit of Measure",GLSetup."Unit-Amount Rounding Precision"));
            IF SalesHeader."Currency Code" <> '' THEN
              ServItem.VALIDATE(
                "Sales Unit Price",
                AmountToLCY(
                  ROUND(SalesLine."Unit Price" /
                    ItemUnitOfMeasure."Qty. per Unit of Measure",GLSetup."Unit-Amount Rounding Precision"),
                  SalesHeader."Currency Factor",
                  SalesHeader."Currency Code",
                  SalesHeader."Posting Date"))
            ELSE
              ServItem.VALIDATE("Sales Unit Price",ROUND(SalesLine."Unit Price" /
                  ItemUnitOfMeasure."Qty. per Unit of Measure",GLSetup."Unit-Amount Rounding Precision"));
            ServItem."Vendor No." := Item."Vendor No.";
            ServItem."Vendor Item No." := Item."Vendor Item No.";
            ServItem."Unit of Measure Code" := Item."Base Unit of Measure";
            ServItem."Sales Date" := SalesHeader."Posting Date";
            ServItem."Installation Date" := SalesHeader."Posting Date";
            ServItem."Warranty % (Parts)" := ServMgtSetup."Warranty Disc. % (Parts)";
            ServItem."Warranty % (Labor)" := ServMgtSetup."Warranty Disc. % (Labor)";

            IF TrackingLinesExist AND (TempReservEntry."Warranty Date" <> 0D) THEN
              WarrantyStartDate := TempReservEntry."Warranty Date"
            ELSE BEGIN
              WarrantyStartDate := SalesHeader."Posting Date";
              IF (WarrantyStartDate = 0D) AND SalesLine."Drop Shipment" THEN
                IF PurchaseHeader.GET(PurchaseHeader."Document Type"::Order,SalesLine."Purchase Order No.") THEN
                  WarrantyStartDate := PurchaseHeader."Posting Date";
            END;
            CalcServiceItemWarrantyDates(
              ServItem,WarrantyStartDate,ItemTrackingCode."Warranty Date Formula",ServMgtSetup."Default Warranty Duration");

            OnCreateServItemOnSalesLineShpt(ServItem,SalesHeader,SalesLine);

            ServItem.MODIFY;
            CLEAR(ServiceItemTEMP);
            ServiceItemTEMP := ServItem;
            IF ServiceItemTEMP.INSERT THEN;
            ResSkillMgt.AssignServItemResSkills(ServItem);
            IF SalesLine."BOM Item No." <> '' THEN BEGIN
              CLEAR(BOMComp);
              BOMComp.SETRANGE("Parent Item No.",SalesLine."BOM Item No.");
              BOMComp.SETRANGE(Type,BOMComp.Type::Item);
              BOMComp.SETRANGE("No.",SalesLine."No.");
              BOMComp.SETRANGE("Installed in Line No.",0);
              IF BOMComp.FINDSET THEN
                REPEAT
                  CLEAR(BOMComp2);
                  BOMComp2.SETRANGE("Parent Item No.",SalesLine."BOM Item No.");
                  BOMComp2.SETRANGE("Installed in Line No.",BOMComp."Line No.");
                  NextLineNo := 0;
                  IF BOMComp2.FINDSET THEN
                    REPEAT
                      FOR Index := 1 TO ROUND(BOMComp2."Quantity per",1) DO BEGIN
                        NextLineNo := NextLineNo + 10000;
                        InsertServItemComponent(ServItemComponent,BOMComp,BOMComp2,SalesHeader,SalesShipmentLine);
                        CLEAR(ServiceItemCompTEMP);
                        ServiceItemCompTEMP := ServItemComponent;
                        ServiceItemCompTEMP.INSERT;
                      END;
                    UNTIL BOMComp2.NEXT = 0;
                UNTIL BOMComp.NEXT = 0;
            END;
            CLEAR(ServLogMgt);
            ServLogMgt.ServItemAutoCreated(ServItem);
            TrackingLinesExist := TempReservEntry.NEXT = 1;
          END;
        END;
      END;
    END;

    LOCAL PROCEDURE CalcServiceItemWarrantyDates@17(VAR ServiceItem@1000 : Record 5940;StartingWarrantyDate@1001 : Date;ItemTrackingWarrantyDateFormula@1002 : DateFormula;ServMgtSetupDefaultWarrantyDuration@1003 : DateFormula);
    BEGIN
      ServiceItem."Warranty Starting Date (Parts)" := StartingWarrantyDate;
      ServiceItem."Warranty Starting Date (Labor)" := StartingWarrantyDate;
      IF FORMAT(ItemTrackingWarrantyDateFormula) <> '' THEN
        ServiceItem."Warranty Ending Date (Parts)" := CALCDATE(ItemTrackingWarrantyDateFormula,StartingWarrantyDate)
      ELSE
        ServiceItem."Warranty Ending Date (Parts)" := CALCDATE(ServMgtSetupDefaultWarrantyDuration,StartingWarrantyDate);
      ServiceItem."Warranty Ending Date (Labor)" := CALCDATE(ServMgtSetupDefaultWarrantyDuration,StartingWarrantyDate);
    END;

    [External]
    PROCEDURE CreateServItemOnServItemLine@2(VAR ServItemLine@1000 : Record 5901);
    VAR
      ConfirmManagement@1001 : Codeunit 27;
    BEGIN
      WITH ServItemLine DO BEGIN
        TESTFIELD("Service Item No.",'');
        TESTFIELD("Document No.");
        TESTFIELD(Description);
        IF NOT ConfirmManagement.ConfirmProcess(STRSUBSTNO(Text000,LOWERCASE(ServItem.TABLECAPTION)),TRUE) THEN
          EXIT;

        CLEAR(ServItem);
        ServItem.INIT;
        ServMgtSetup.GET;
        ServMgtSetup.TESTFIELD("Service Item Nos.");
        NoSeriesMgt.InitSeries(
          ServMgtSetup."Service Item Nos.",ServItem."No. Series",0D,ServItem."No.",
          ServItem."No. Series");
        ServItem.INSERT;
        ServItem.VALIDATE(Description,Description);
        CLEAR(ServHeader);
        ServHeader.GET("Document Type","Document No.");
        ServItem.VALIDATE("Customer No.",ServHeader."Customer No.");
        ServItem.VALIDATE("Ship-to Code",ServHeader."Ship-to Code");
        ServItem.OmitAssignResSkills(TRUE);
        ServItem.VALIDATE("Item No.","Item No.");
        ServItem.OmitAssignResSkills(FALSE);
        ServItem."Variant Code" := "Variant Code";
        ServItem.VALIDATE("Serial No.","Serial No.");
        ServItem."Response Time (Hours)" := "Response Time (Hours)";
        ServItem."Sales Unit Cost" := 0;
        ServItem."Sales Unit Price" := 0;
        ServItem."Vendor No." := "Vendor No.";
        ServItem."Vendor Item No." := "Vendor Item No.";
        ServItem."Unit of Measure Code" := '';
        ServItem."Sales Date" := 0D;
        ServItem."Installation Date" := ServHeader."Posting Date";
        ServItem."Warranty Starting Date (Parts)" := "Warranty Starting Date (Parts)";
        ServItem."Warranty Ending Date (Parts)" := "Warranty Ending Date (Parts)";
        ServItem."Warranty Starting Date (Labor)" := "Warranty Starting Date (Labor)";
        ServItem."Warranty Ending Date (Labor)" := "Warranty Ending Date (Labor)";
        ServItem."Warranty % (Parts)" := "Warranty % (Parts)";
        ServItem."Warranty % (Labor)" := "Warranty % (Labor)";
        ServItem."Service Item Group Code" := "Service Item Group Code";
        IF "Service Price Group Code" <> '' THEN
          IF ConfirmManagement.ConfirmProcess(
               STRSUBSTNO(
                 Text002,FIELDCAPTION("Service Price Group Code"),
                 TABLECAPTION,ServItem.TABLECAPTION),TRUE)
          THEN
            ServItem."Service Price Group Code" := "Service Price Group Code";

        OnCreateServItemOnServItemLineOnBeforeServItemModify(ServItem,ServHeader,ServItemLine);
        ServItem.MODIFY;
        ResSkillMgt.AssignServItemResSkills(ServItem);
        CLEAR(ServLogMgt);
        ServLogMgt.ServItemAutoCreated(ServItem);
        MESSAGE(Text001,ServItem."No.");
        "Service Item No." := ServItem."No.";
        "Contract No." := '';

        OnCreateServItemOnServItemLine(ServItem,ServItemLine);

        MODIFY;
        CreateDim(
          DATABASE::"Service Item","Service Item No.",
          DATABASE::"Service Item Group","Service Item Group Code",
          DATABASE::"Responsibility Center","Responsibility Center");
      END;

      ServLogMgt.ServItemToServOrder(ServItemLine);

      ServInvoiceLine.SETCURRENTKEY("Document Type","Document No.","Service Item Line No.");
      ServInvoiceLine.SETRANGE("Document Type",ServItemLine."Document Type");
      ServInvoiceLine.SETRANGE("Document No.",ServItemLine."Document No.");
      ServInvoiceLine.SETRANGE("Service Item Line No.",ServItemLine."Line No.");
      ServInvoiceLine.MODIFYALL("Service Item No.",ServItemLine."Service Item No.");
    END;

    LOCAL PROCEDURE AmountToLCY@22(FCAmount@1000 : Decimal;CurrencyFactor@1004 : Decimal;CurrencyCode@1005 : Code[10];CurrencyDate@1006 : Date) : Decimal;
    VAR
      CurrExchRate@1002 : Record 330;
      Currency@1003 : Record 4;
    BEGIN
      Currency.GET(CurrencyCode);
      Currency.TESTFIELD("Unit-Amount Rounding Precision");
      EXIT(
        ROUND(
          CurrExchRate.ExchangeAmtFCYToLCY(
            0, '', //**4PS.n
            CurrencyDate,CurrencyCode,
      //    FCAmount,CurrencyFactor), //**4PS.o
            FCAmount,CurrencyFactor,TRUE), //**4PS.n
          Currency."Unit-Amount Rounding Precision"));
    END;

    [External]
    PROCEDURE CopyReservationEntry@1(SalesHeader@1003 : Record 36);
    VAR
      SalesLine@1000 : Record 37;
    BEGIN
      SalesLine.SETRANGE("Document Type",SalesHeader."Document Type");
      SalesLine.SETRANGE("Document No.",SalesHeader."No.");
      SalesLine.SETFILTER("Qty. to Ship",'<>0');
      IF SalesLine.FINDSET THEN
        REPEAT
          CopyReservationEntryLine(SalesLine);
        UNTIL SalesLine.NEXT = 0;
    END;

    [External]
    PROCEDURE CopyReservationEntryService@8(ServHeader@1003 : Record 5900);
    VAR
      ReservEntry@1000 : Record 337;
    BEGIN
      ReservEntry.RESET;
      CLEAR(TempReservEntry);

      ReservEntry.SETRANGE("Source Subtype",ServHeader."Document Type");
      ReservEntry.SETRANGE("Source ID",ServHeader."No.");
      ReservEntry.SETRANGE(Positive,FALSE);
      ReservEntry.SETRANGE("Source Type",DATABASE::"Service Line");
      ReservEntry.SETRANGE("Source Batch Name",'');
      ReservEntry.SETRANGE("Source Prod. Order Line",0);
      ReservEntry.SETFILTER("Serial No.",'<>%1','');
      ReservEntry.SETFILTER("Qty. to Handle (Base)",'<>%1',0);

      IF ReservEntry.FINDSET THEN
        REPEAT
          TempReservEntry := ReservEntry;
          TempReservEntry.INSERT;
        UNTIL ReservEntry.NEXT = 0;
    END;

    [External]
    PROCEDURE CreateServItemOnSalesInvoice@4(VAR SalesHeader@1000 : Record 36);
    VAR
      SalesLine@1001 : Record 37;
      DummySalesShptLine@1002 : Record 111;
    BEGIN
      SalesLine.SETRANGE("Document Type",SalesHeader."Document Type");
      SalesLine.SETRANGE("Document No.",SalesHeader."No.");
      IF SalesLine.FINDSET THEN
        REPEAT
          CreateServItemOnSalesLineShpt(SalesHeader,SalesLine,DummySalesShptLine);
        UNTIL SalesLine.NEXT = 0;
    END;

    LOCAL PROCEDURE CheckWholeNumber@6(VAR ServLine@1000 : Record 5902);
    BEGIN
      WITH ServLine DO BEGIN
        IF Quantity <> ROUND(Quantity,1) THEN
          ERROR(Text004,FIELDCAPTION(Quantity),TABLECAPTION);
        IF "Qty. to Ship" <> ROUND("Qty. to Ship",1) THEN
          ERROR(Text004,FIELDCAPTION("Qty. to Ship"),TABLECAPTION);
      END;
    END;

    [External]
    PROCEDURE ReturnServItemComp@1206(VAR ServItemTmp@1200 : TEMPORARY Record 5940;VAR ServItemCmpTmp@1201 : TEMPORARY Record 5941);
    BEGIN
      ServItemTmp.DELETEALL;
      IF ServiceItemTEMP.FIND('-') THEN
        REPEAT
          ServItemTmp := ServiceItemTEMP;
          ServItemTmp.INSERT;
        UNTIL ServiceItemTEMP.NEXT = 0;
      ServItemCmpTmp.DELETEALL;
      IF ServiceItemCompTEMP.FIND('-') THEN
        REPEAT
          ServItemCmpTmp := ServiceItemCompTEMP;
          ServItemCmpTmp.INSERT;
        UNTIL ServiceItemCompTEMP.NEXT = 0;
    END;

    [External]
    PROCEDURE DeleteServItemOnSaleCreditMemo@1100(SalesHeader@1101 : Record 36);
    VAR
      SalesLine@1102 : Record 37;
      ServItem@1103 : Record 5940;
      ReservationEntry@1104 : Record 337;
      ServItemDeleted@1105 : Boolean;
    BEGIN
      IF NOT (SalesHeader."Document Type" IN [SalesHeader."Document Type"::"Credit Memo",SalesHeader."Document Type"::"Return Order"]) THEN
        EXIT;

      ServItemDeleted := FALSE;
      SalesLine.SETRANGE("Document Type",SalesHeader."Document Type");
      SalesLine.SETRANGE("Document No.",SalesHeader."No.");
      IF SalesLine.FINDSET THEN
        REPEAT
          IF SalesLine.Type = SalesLine.Type::Item THEN BEGIN
            ReservationEntry.SETRANGE("Item No.",SalesLine."No.");
            ReservationEntry.SETRANGE("Location Code",SalesLine."Location Code");
            ReservationEntry.SETRANGE("Source ID",SalesLine."Document No.");
            ReservationEntry.SETRANGE("Source Ref. No.",SalesLine."Line No.");
            IF ReservationEntry.FINDSET THEN
              REPEAT
                ServItem.SETRANGE("Item No.",SalesLine."No.");
                ServItem.SETRANGE("Customer No.",SalesLine."Sell-to Customer No.");
                ServItem.SETRANGE("Serial No.",ReservationEntry."Serial No.");
                IF ServItem.FINDFIRST THEN BEGIN
                  IF ServItem.CheckIfCanBeDeleted <> '' THEN BEGIN
                    ServItem.VALIDATE(Status,ServItem.Status::" ");
                    ServItem.MODIFY(TRUE);
                  END ELSE
                    IF ServItem.DELETE(TRUE) THEN
                      ServItemDeleted := TRUE;
                END;
              UNTIL ReservationEntry.NEXT = 0;
          END;
        UNTIL SalesLine.NEXT = 0;
      IF ServItemDeleted THEN
        MESSAGE(Text005);
    END;

    LOCAL PROCEDURE CopyReservationEntryLine@10(SalesLine@1003 : Record 37);
    VAR
      ReservEntry@1000 : Record 337;
    BEGIN
      ReservEntry.RESET;
      ReservEntry.SETRANGE("Source Subtype",SalesLine."Document Type");
      ReservEntry.SETRANGE("Source ID",SalesLine."Document No.");
      ReservEntry.SETRANGE(Positive,FALSE);
      ReservEntry.SETRANGE("Source Type",DATABASE::"Sales Line");
      ReservEntry.SETRANGE("Source Ref. No.",SalesLine."Line No.");
      ReservEntry.SETRANGE("Source Batch Name",'');
      ReservEntry.SETRANGE("Source Prod. Order Line",0);
      ReservEntry.SETFILTER("Qty. to Handle (Base)",'<>%1',0);

      IF ReservEntry.FINDSET THEN
        REPEAT
          TempReservEntry := ReservEntry;
          TempReservEntry.INSERT;
        UNTIL ReservEntry.NEXT = 0;
    END;

    [External]
    PROCEDURE CopyReservation@286(PurchaseHeader@1000 : Record 38);
    VAR
      PurchaseLine@1001 : Record 39;
      SalesLine@1002 : Record 37;
    BEGIN
      PurchaseLine.SETRANGE("Document Type",PurchaseHeader."Document Type");
      PurchaseLine.SETRANGE("Document No.",PurchaseHeader."No.");
      PurchaseLine.SETRANGE("Drop Shipment",TRUE);
      PurchaseLine.SETFILTER(Quantity,'<>0');
      IF PurchaseLine.FINDSET THEN
        REPEAT
          IF SalesLine.GET(SalesLine."Document Type"::Order,PurchaseLine."Sales Order No.",PurchaseLine."Sales Order Line No.") THEN
            CopyReservationEntryLine(SalesLine);
        UNTIL PurchaseLine.NEXT = 0;
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCreateServItemOnSalesLineShpt@12(SalesHeader@1000 : Record 36;SalesLine@1001 : Record 37;SalesShipmentLine@1002 : Record 111;VAR IsHandled@1003 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeInsertServItemComponent@9(VAR ServiceItemComponent@1000 : Record 5941;ServiceItem@1001 : Record 5940;SalesHeader@1002 : Record 36;SalesShipmentLine@1003 : Record 111;BOMComponent@1004 : Record 90;BOMComponent2@1005 : Record 90);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeInsertNewServItemComponent@7(VAR ServiceItemComponent@1000 : Record 5941;ServiceLine@1001 : Record 5902);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeReplaceSIComponent@287(VAR ServiceLine@1000 : Record 5902;ServiceHeader@1001 : Record 5900;ServShptDocNo@1002 : Code[20];ServShptLineNo@1003 : Integer;VAR TempTrackingSpecification@1004 : TEMPORARY Record 336);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnCreateServItemOnSalesLineShpt@288(VAR ServiceItem@1000 : Record 5940;SalesHeader@1001 : Record 36;SalesLine@1002 : Record 37);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnCreateServItemOnServItemLine@289(VAR ServiceItem@1000 : Record 5940;ServiceItemLine@1001 : Record 5901);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnCreateServItemOnServItemLineOnBeforeServItemModify@14(VAR ServiceItem@1000 : Record 5940;ServiceHeader@1002 : Record 5900;ServiceItemLine@1001 : Record 5901);
    BEGIN
    END;

    BEGIN
    END.
  }
}

