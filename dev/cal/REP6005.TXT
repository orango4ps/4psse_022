OBJECT Report 6005 Batch Post Service Cr. Memos
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Servicegutschriften stapelbuchen;
               ENU=Batch Post Service Cr. Memos;
               NLD=Batchboeken servicecreditnota's;
               NOR=Massebokf›r salgskreditnotaer (service)];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  OnBeforePreReport;
                END;

    OnPostReport=BEGIN
                   OnAfterPostReport;
                 END;

  }
  DATASET
  {
    { 1634;    ;DataItem;                    ;
               DataItemTable=Table5900;
               DataItemTableView=SORTING(Document Type,No.)
                                 WHERE(Document Type=CONST(Credit Memo));
               OnPreDataItem=BEGIN
                               IF ReplacePostingDate AND (PostingDateReq = 0D) THEN
                                 ERROR(Text000);
                               CounterTotal := COUNT;
                               Window.OPEN(Text001);
                             END;

               OnAfterGetRecord=BEGIN
                                  IF CalcInvDisc THEN
                                    CalculateInvoiceDiscount;

                                  Counter := Counter + 1;
                                  Window.UPDATE(1,"No.");
                                  Window.UPDATE(2,ROUND(Counter / CounterTotal * 10000,1));
                                  CLEAR(ServPost);
                                  ServPost.SetPostingDate(ReplacePostingDate,ReplaceDocumentDate,PostingDateReq);
                                  ServPost.SetPostingOptions(TRUE,FALSE,TRUE); // ship, consume, invoice
                                  IF ServPost.RUN("Service Header") THEN BEGIN
                                    CounterOK := CounterOK + 1;
                                    IF MARKEDONLY THEN
                                      MARK(FALSE);
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;
                                MESSAGE(Text002,CounterOK,CounterTotal);
                              END;

               ReqFilterFields=No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   SalesSetup.GET;
                   CalcInvDisc := SalesSetup."Calc. Inv. Discount";
                   ReplacePostingDate := FALSE;
                   ReplaceDocumentDate := FALSE;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1   ;2   ;Field     ;
                  Name=PostingDate;
                  CaptionML=[DEU=Buchungsdatum;
                             ENU=Posting Date;
                             NLD=Boekingsdatum;
                             NOR=Bokf›ringsdato;
                             SVE=Bokf”ringsdatum];
                  ToolTipML=[DEU=Gibt das Datum an, das das Programm beim Buchen als Buchungs- oder Belegdatum verwendet, sofern Sie das Feld "Buchungsdatum ersetzen" oder "Belegdatum ersetzen" oder beide Felder ausw„hlen.;
                             ENU=Specifies the date that the program uses as the posting date or document date when you post, if you select the Replace Posting Date field or the Replace Document Date field or both.;
                             NLD=Hiermee wordt de datum opgegeven die bij het boeken wordt gebruikt als de documentdatum of boekingsdatum als u het veld Documentdatum vervangen, het veld Boekingsdatum vervangen of beide velden selecteert.;
                             NOR=Angir datoen som programmet bruker som bokf›rings- eller dokumentdato n†r du bokf›rer, hvis du merker av for Erstatt bokf›ringsdato eller Erstatt dokumentdato eller begge alternativene.];
                  ApplicationArea=#Service;
                  SourceExpr=PostingDateReq }

      { 2   ;2   ;Field     ;
                  Name=ReplacePostingDate;
                  CaptionML=[DEU=Buchungsdatum ersetzen;
                             ENU=Replace Posting Date;
                             NLD=Boekingsdatum vervangen;
                             NOR=Erstatt bokf›ringsdato;
                             SVE=Ers„tt bokf”ringsdatum];
                  ToolTipML=[DEU=Gibt an, ob das Buchungsdatum der Gutschrift durch das im Feld "Buchungsdatum" eingegebene Datum ersetzt werden soll.;
                             ENU=Specifies if you want to replace the posting date of the credit memo with the date that you entered in the Posting Date field.;
                             NLD=Hiermee wordt opgegeven of de boekingsdatum van de creditnota wordt vervangen door de datum die is ingevoerd in het veld Boekingsdatum.;
                             NOR=Angir om du vil erstatte bokf›ringsdatoen for kreditnotaen med datoen du angav i feltet Bokf›ringsdato.];
                  ApplicationArea=#Service;
                  SourceExpr=ReplacePostingDate;
                  OnValidate=BEGIN
                               IF ReplacePostingDate THEN
                                 MESSAGE(Text003);
                             END;
                              }

      { 3   ;2   ;Field     ;
                  CaptionML=[DEU=Belegdatum ersetzen;
                             ENU=Replace Document Date;
                             NLD=Documentdatum vervangen;
                             NOR=Erstatt bilagsdato;
                             SVE=Ers„tt dokumentdatum];
                  ToolTipML=[DEU=Gibt an, ob das Belegdatum der Gutschrift durch das Datum im Feld "Buchungsdatum" ersetzt werden soll.;
                             ENU=Specifies if you want to replace the document date of the credit memo with the date in the Posting Date field.;
                             NLD=Hiermee wordt opgegeven of de documentdatum van de creditnota wordt vervangen door de datum die is ingevoerd in het veld Boekingsdatum.;
                             NOR=Angir om du vil erstatte dokumentdatoen for kreditnotaen med datoen i feltet Bokf›ringsdato.];
                  ApplicationArea=#Service;
                  SourceExpr=ReplaceDocumentDate }

      { 5   ;2   ;Field     ;
                  CaptionML=[DEU=Rechnungsrab. berechnen;
                             ENU=Calc. Inv. Discount;
                             NLD=Factuurkorting berekenen;
                             NOR=Beregn fakturarabatt;
                             SVE=Ber„kna fakturarabatt];
                  ToolTipML=[DEU=Gibt an, ob der Lagerbestandrabatt berechnet werden soll.;
                             ENU=Specifies whether the inventory discount should be calculated.;
                             NLD=Hiermee wordt opgegeven of de voorraadkorting moet worden berekend.;
                             NOR=Angir om lagerrabatten skal beregnes.];
                  ApplicationArea=#Service;
                  SourceExpr=CalcInvDisc;
                  OnValidate=BEGIN
                               SalesSetup.GET;
                               SalesSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Geben Sie das Buchungsdatum ein.;ENU=Enter the posting date.;NLD=Voer de boekingdatum opnieuw in.;NOR=Angi bokf›ringsdatoen.;SVE=Ange bokf”ringsdatum.';
      Text001@1001 : TextConst 'DEU=Gutschriften buchen  #1########## @2@@@@@@@@@@@@@;ENU=Posting credit memos  #1########## @2@@@@@@@@@@@@@;NLD=Boeken creditnota''s   #1########## @2@@@@@@@@@@@@@;NOR=Bokf›rer kreditnotaer #1########## @2@@@@@@@@@@@@@';
      Text002@1002 : TextConst 'DEU=%1 Gutschriften von insgesamt %2 wurden gebucht.;ENU=%1 credit memos out of a total of %2 have now been posted.;NLD=%1 van de %2 creditnota''s zijn geboekt.;NOR=%1 kreditnotaer av til sammen %2 er n† bokf›rt.';
      Text003@1003 : TextConst 'DEU=Der dem neuen Buchungsdatum im Servicekopf zugeordnete Wechselkurs wird nicht auf die Servicezeilen angewendet.;ENU=The exchange rate associated with the new posting date on the service header will not apply to the service lines.;NLD=De wisselkoers voor de nieuwe boekingsdatum in de servicekop is niet van toepassing op de serviceregels.;NOR=Valutakursen som er knyttet til den nye bokf›ringsdatoen i servicehodet, gjelder ikke servicelinjene.';
      ServLine@1005 : Record 5902;
      SalesSetup@1006 : Record 311;
      ServCalcDisc@1007 : Codeunit 5950;
      ServPost@1008 : Codeunit 5980;
      Window@1009 : Dialog;
      PostingDateReq@1010 : Date;
      CounterTotal@1011 : Integer;
      Counter@1012 : Integer;
      CounterOK@1013 : Integer;
      ReplacePostingDate@1016 : Boolean;
      ReplaceDocumentDate@1017 : Boolean;
      CalcInvDisc@1018 : Boolean;

    LOCAL PROCEDURE CalculateInvoiceDiscount@1();
    BEGIN
      ServLine.RESET;
      ServLine.SETRANGE("Document Type","Service Header"."Document Type");
      ServLine.SETRANGE("Document No.","Service Header"."No.");
      IF ServLine.FINDFIRST THEN
        IF ServCalcDisc.RUN(ServLine) THEN BEGIN
          "Service Header".GET("Service Header"."Document Type","Service Header"."No.");
          COMMIT;
        END;
    END;

    [External]
    PROCEDURE InitializeRequest@2(PostingDateReqFrom@1000 : Date;ReplacePostingDateFrom@1003 : Boolean;ReplaceDocumentDateFrom@1002 : Boolean;CalcInvDiscFrom@1001 : Boolean);
    BEGIN
      PostingDateReq := PostingDateReqFrom;
      ReplacePostingDate := ReplacePostingDateFrom;
      ReplaceDocumentDate := ReplaceDocumentDateFrom;
      CalcInvDisc := CalcInvDiscFrom;
    END;

    [Integration(TRUE,TRUE)]
    LOCAL PROCEDURE OnAfterPostReport@3();
    BEGIN
    END;

    [Integration(TRUE,TRUE)]
    LOCAL PROCEDURE OnBeforePreReport@4();
    BEGIN
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

