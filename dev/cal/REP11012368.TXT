OBJECT Report 11012368 Create Project Authorities
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Projektinstanzen aktualisieren;
               ENU=Create Project Authorities;
               NLD=Projectinstanties bijwerken;
               NOR=Opprett prosjektledere;
               SVE=Skapa projektbehîrighet];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  PurchasesPayablesSetup.GET();
                  PurchasesPayablesSetup.TESTFIELD("Default Authority Type");
                  ContactBusinessRelation.SETCURRENTKEY("Link to Table","No.");
                END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 8019;    ;DataItem;                    ;
               DataItemTable=Table11072003;
               DataItemTableView=SORTING(No.);
               ReqFilterFields=No. }

    { 2306;1   ;DataItem;PurchaseLineQuote   ;
               DataItemTable=Table39;
               DataItemTableView=SORTING(Document Type,Job No.,Order Date)
                                 WHERE(Document Type=CONST(Quote));
               OnPreDataItem=BEGIN
                               IF NOT BasedOnQuotes THEN
                                 CurrReport.BREAK;

                               SETFILTER("Expected Receipt Date", '%1..', ReferenceDate);
                             END;

               OnAfterGetRecord=BEGIN
                                  CreateAuthorities("Job No.","Buy-from Vendor No.");
                                END;

               DataItemLink=Job No.=FIELD(No.) }

    { 9088;1   ;DataItem;PurchaseLineOrder   ;
               DataItemTable=Table39;
               DataItemTableView=SORTING(Document Type,Job No.,Order Date)
                                 WHERE(Document Type=CONST(Order));
               OnPreDataItem=BEGIN
                               IF NOT BasedOnOrders THEN
                                 CurrReport.BREAK;

                               SETFILTER("Posting Date", '%1..', ReferenceDate);
                             END;

               OnAfterGetRecord=BEGIN
                                  CreateAuthorities("Job No.","Buy-from Vendor No.");
                                END;

               DataItemLink=Job No.=FIELD(No.) }

    { 7289;1   ;DataItem;PurchaseLineInvoice ;
               DataItemTable=Table123;
               DataItemTableView=SORTING(Job No.,Buy-from Vendor No.);
               OnPreDataItem=BEGIN
                               IF NOT BasedOnInvoices THEN
                                 CurrReport.BREAK;

                               SETFILTER("Posting Date", '%1..', ReferenceDate);
                             END;

               OnAfterGetRecord=BEGIN
                                  CreateAuthorities("Job No.","Buy-from Vendor No.");
                                END;

               DataItemLink=Job No.=FIELD(No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525001;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100525002;2;Group  ;
                  CaptionML=[DEU=GegrÅndet auf;
                             ENU=Based on;
                             NLD=Op basis van;
                             NOR=Basert pÜ;
                             SVE=Baserad pÜ];
                  GroupType=Group }

      { 1100525003;3;Field  ;
                  Name=Quotes;
                  CaptionML=[DEU=Angebote;
                             ENU=Quotes;
                             NLD=Offertes;
                             NOR=Tilbud;
                             SVE=Offerter];
                  SourceExpr=BasedOnQuotes }

      { 1100525004;3;Field  ;
                  Name=Orders;
                  CaptionML=[DEU=AuftrÑge;
                             ENU=Orders;
                             NLD=Orders;
                             NOR=Ordrer;
                             SVE=Order];
                  SourceExpr=BasedOnOrders }

      { 1100525005;3;Field  ;
                  Name=Invoices;
                  CaptionML=[DEU=Rechnungen;
                             ENU=Invoices;
                             NLD=Facturen;
                             NOR=Fakturaer;
                             SVE=Fakturor];
                  SourceExpr=BasedOnInvoices }

      { 1100525006;2;Field  ;
                  CaptionML=[DEU=Stichtag;
                             ENU=Reference Date;
                             NLD=Peildatum;
                             NOR=Referansedato;
                             SVE=Referensdatum];
                  SourceExpr=ReferenceDate }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      PurchasesPayablesSetup@1100525004 : Record 312;
      VendorAuthorities@1100525003 : Record 11020639;
      ProjectAuthority@1100525008 : Record 11020216;
      ContactBusinessRelation@1100525007 : Record 5054;
      BasedOnQuotes@1100525000 : Boolean;
      BasedOnOrders@1100525001 : Boolean;
      BasedOnInvoices@1100525002 : Boolean;
      ReferenceDate@1100525009 : Date;

    PROCEDURE CreateAuthorities@1100525002(Job@1100525000 : Code[20];Vendor@1100525001 : Code[20]);
    VAR
      JobRec@1100528300 : Record 11072003;
    BEGIN
      IF Job <> '' THEN BEGIN
        JobRec.GET(Job);
        ContactBusinessRelation.SETRANGE("Link to Table",ContactBusinessRelation."Link to Table"::Vendor);
        ContactBusinessRelation.SETRANGE("No.", Vendor);
        IF ContactBusinessRelation.FINDFIRST THEN BEGIN
          VendorAuthorities.RESET;
          VendorAuthorities.SETRANGE("Vendor No.", Vendor);
          IF (JobRec.IsJobInProjectPortal) THEN
            VendorAuthorities.SETRANGE("Project Portal Authority", TRUE);
          IF VendorAuthorities.FINDSET THEN BEGIN
            REPEAT
              ProjectAuthority.INIT;
              ProjectAuthority."Project No." := Job;
              ProjectAuthority."Authority Type" := VendorAuthorities."Authority Type";
              ProjectAuthority."Contact No." := ContactBusinessRelation."Contact No.";
              ProjectAuthority."Contact Person No." := '';
              IF ProjectAuthority.INSERT THEN;
            UNTIL VendorAuthorities.NEXT = 0;
          END ELSE BEGIN
            ProjectAuthority.INIT;
            ProjectAuthority."Project No." := Job;
            ProjectAuthority."Authority Type" := PurchasesPayablesSetup."Default Authority Type";
            ProjectAuthority."Contact No." := ContactBusinessRelation."Contact No.";
            ProjectAuthority."Contact Person No." := '';
            IF ProjectAuthority.INSERT THEN;
          END;
        END;
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

