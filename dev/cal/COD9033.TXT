OBJECT Codeunit 9033 Invite External Accountant
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      ErrorAcquiringTokenErr@1000 : TextConst '@@@={Locked};ENU=Failed to acquire an access token.  This is necessary to invite the external accountant.  Contact your administrator.;NOR=Failed to acquire an access token.  This is necessary to invite the external accountant.  Contact your administrator.';
      InviteReedemUrlTxt@1001 : TextConst '@@@={Locked};ENU=inviteRedeemUrl;NOR=inviteRedeemUrl';
      InvitedUserIdTxt@1002 : TextConst '@@@={Locked};ENU=invitedUser;NOR=invitedUser';
      IdTxt@1003 : TextConst '@@@={Locked};ENU=id;NOR=id';
      ConsumedUnitsTxt@1004 : TextConst '@@@={Locked};ENU=consumedUnits;NOR=consumedUnits';
      ErrorTxt@1005 : TextConst '@@@={Locked};ENU=error;NOR=error';
      MessageTxt@1006 : TextConst '@@@={Locked};ENU=message;NOR=message';
      ValueTxt@1007 : TextConst '@@@={Locked};ENU=value;NOR=value';
      SkuIdTxt@1009 : TextConst '@@@={Locked};ENU=skuId;NOR=skuId';
      PrepaidUnitsTxt@1010 : TextConst '@@@={Locked};ENU=prepaidUnits;NOR=prepaidUnits';
      EnabledUnitsTxt@1011 : TextConst '@@@={Locked};ENU=enabled;NOR=enabled';
      GlobalAdministratorRoleTemplateIdTxt@1012 : TextConst '@@@={Locked};ENU=62e90394-69f5-4237-9190-012177145e10;NOR=62e90394-69f5-4237-9190-012177145e10';
      UserAdministratorRoleTemplateIdTxt@1013 : TextConst '@@@={Locked};ENU=fe930be7-5e62-47db-91af-98c3a49a38b1;NOR=fe930be7-5e62-47db-91af-98c3a49a38b1';
      ExternalAccountantLicenseSkuIdTxt@1014 : TextConst '@@@={Locked};ENU=9a1e33ed-9697-43f3-b84c-1b0959dbb1d4;NOR=9a1e33ed-9697-43f3-b84c-1b0959dbb1d4';
      TrialViralLicenseSkuIdTxt@1034 : TextConst '@@@={Locked};ENU=6a4a1628-9b9a-424d-bed5-4118f0ede3fd;NOR=6a4a1628-9b9a-424d-bed5-4118f0ede3fd';
      InviteExternalAccountantTelemetryCategoryTxt@1019 : TextConst '@@@={Locked};ENU=AL Dynamics 365 Invite External Accountant;NOR=AL Dynamics 365 Invite External Accountant';
      InviteExternalAccountantTelemetryStartTxt@1020 : TextConst '@@@={Locked};ENU=Invite External Accountant process started.;NOR=Invite External Accountant process started.';
      InviteExternalAccountantTelemetryEndTxt@1021 : TextConst '@@@={Locked};ENU=Invite External Accountant process ended with the following result:  %1:  License is %2.;NOR=Invite External Accountant process ended with the following result:  %1:  License is %2.';
      InviteExternalAccountantTelemetryLicenseFailTxt@1022 : TextConst '@@@={Locked};ENU=Invite External Accountant wizard failed to start due to there being no external accountant license available.;NOR=Invite External Accountant wizard failed to start due to there being no external accountant license available.';
      InviteExternalAccountantTelemetryAADPermissionFailTxt@1023 : TextConst '@@@={Locked};ENU=Invite External Accountant wizard failed to start due to the user not having the necessary AAD permissions.;NOR=Invite External Accountant wizard failed to start due to the user not having the necessary AAD permissions.';
      InviteExternalAccountantTelemetryUserTablePermissionFailTxt@1028 : TextConst '@@@={Locked};ENU=Invite External Accountant wizard failed to start due to the session not being admin or the user being Super in all companies.;NOR=Invite External Accountant wizard failed to start due to the session not being admin or the user being Super in all companies.';
      InviteExternalAccountantTelemetryCreateNewUserSuccessTxt@1029 : TextConst '@@@={Locked};ENU=Invite External Accountant wizard successfully created a new user.;NOR=Invite External Accountant wizard successfully created a new user.';
      InviteExternalAccountantTelemetryCreateNewUserFailedTxt@1030 : TextConst '@@@={Locked};ENU=Invite External Accountant wizard was unable to create a new user.;NOR=Invite External Accountant wizard was unable to create a new user.';
      ResponseCodeTxt@1024 : TextConst '@@@={Locked};ENU=responseCode;NOR=responseCode';
      EmailAddressIsConsumerAccountTxt@1025 : TextConst 'ENU=The email address looks like a personal email address.  Enter your accountant''s work email address to continue.;NOR=E-postadressen ser ut som en personlig e-postadresse. Angi regnskapsf›rerens e-postadresse (arbeid) for † fortsette.';
      EmailAddressIsInvalidTxt@1026 : TextConst 'ENU=The email address is invalid.  Enter your accountant''s work email address to continue.;NOR=E-postadressen er ugyldig. Angi regnskapsf›rerens e-postadresse (arbeid) for † fortsette.';
      InsufficientDataReturnedFromInvitationsApiTxt@1027 : TextConst 'ENU=Insufficient information was returned when inviting the user. Please contact your administrator.;NOR=Det ble ikke returnert nok informasjon da brukeren ble invitert. Kontakt systemansvarlig.';
      WidsClaimNameTok@1032 : TextConst '@@@={Locked};ENU=WIDS;NOR=WIDS';
      ExternalAccountantLicenseAvailabilityErr@1033 : TextConst 'ENU=Failed to determine if an External Accountant license is available. Please try again later.;NOR=Kan ikke fastsl† om en lisens for ekstern revisor er tilgjengelig. Pr›v p† nytt senere.';

    [Internal]
    PROCEDURE InvokeInvitationsRequest@2(DisplayName@1007 : Text;EmailAddress@1026 : Text;WebClientUrl@1027 : Text;VAR InvitedUserId@1000 : GUID;VAR InviteReedemUrl@1009 : Text;VAR ErrorMessage@1001 : Text) : Boolean;
    VAR
      JSONManagement@1002 : Codeunit 5459;
      JsonObject@1017 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
      InviteUserIdJsonObject@1024 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
      InviteUserIdObjectValue@1022 : Text;
      ResponseContent@1020 : Text;
      Body@1008 : Text;
      FoundInviteRedeemUrlValue@1028 : Boolean;
      FoundInvitedUserObjectValue@1029 : Boolean;
      FoundInviteUserIdValue@1030 : Boolean;
    BEGIN
      Body := '{';
      Body := Body + '"invitedUserDisplayName" : "' + DisplayName + '",';
      Body := Body + '"invitedUserEmailAddress" : "' + EmailAddress + '",';
      Body := Body + '"inviteRedirectUrl" : "' + WebClientUrl + '",';
      Body := Body + '"sendInvitationMessage" : "false"';
      Body := Body + '}';

      IF InvokeRequestWithGraphAccessToken(GetGraphInvitationsUrl,'POST',Body,ResponseContent) THEN BEGIN
        JSONManagement.InitializeObject(ResponseContent);
        JSONManagement.GetJSONObject(JsonObject);
        FoundInviteRedeemUrlValue :=
          JSONManagement.GetStringPropertyValueFromJObjectByName(JsonObject,InviteReedemUrlTxt,InviteReedemUrl);
        FoundInvitedUserObjectValue :=
          JSONManagement.GetStringPropertyValueFromJObjectByName(JsonObject,InvitedUserIdTxt,InviteUserIdObjectValue);
        JSONManagement.InitializeObject(InviteUserIdObjectValue);
        JSONManagement.GetJSONObject(InviteUserIdJsonObject);
        FoundInviteUserIdValue := JSONManagement.GetGuidPropertyValueFromJObjectByName(InviteUserIdJsonObject,IdTxt,InvitedUserId);

        IF FoundInviteRedeemUrlValue AND FoundInvitedUserObjectValue AND FoundInviteUserIdValue THEN
          EXIT(TRUE);

        ErrorMessage := InsufficientDataReturnedFromInvitationsApiTxt;
        EXIT(FALSE);
      END;

      ErrorMessage := GetMessageFromErrorJSON(ResponseContent);
      EXIT(FALSE);
    END;

    [Internal]
    PROCEDURE InvokeUserProfileUpdateRequest@12(VAR GuestGraphUser@1002 : DotNet "'Microsoft.Dynamics.Nav.LicensingService.Model, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.LicensingService.Model.UserInfo";CountryLetterCode@1008 : Text;VAR ErrorMessage@1003 : Text) : Boolean;
    VAR
      Body@1004 : Text;
      ResponseContent@1006 : Text;
      GraphUserUrl@1007 : Text;
    BEGIN
      GraphUserUrl := GetGraphUserUrl + '/' + GuestGraphUser.ObjectId;
      Body := '{"usageLocation" : "' + CountryLetterCode + '"}';

      // Set usage location on guest user to current tenant's country letter code.
      IF InvokeRequestWithGraphAccessToken(GraphUserUrl,'PATCH',Body,ResponseContent) THEN
        EXIT(TRUE);

      ErrorMessage := GetMessageFromErrorJSON(ResponseContent);
      EXIT(FALSE);
    END;

    [Internal]
    PROCEDURE InvokeUserAssignLicenseRequest@22(VAR GraphUser@1003 : DotNet "'Microsoft.Dynamics.Nav.LicensingService.Model, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.LicensingService.Model.UserInfo";TargetLicense@1008 : Text;VAR ErrorMessage@1007 : Text) : Boolean;
    VAR
      Body@1004 : Text;
      ResponseContent@1005 : Text;
      Url@1006 : Text;
    BEGIN
      Url := GetGraphUserUrl + '/' + GraphUser.ObjectId + '/assignLicense';

      Body := '{';
      Body := Body + '"addLicenses": [';
      Body := Body + '{';
      Body := Body + '"disabledPlans": [],';
      Body := Body + '"skuId": "' + TargetLicense + '"';
      Body := Body + '}],';
      Body := Body + '"removeLicenses": []';
      Body := Body + '}';

      IF InvokeRequestWithGraphAccessToken(Url,'POST',Body,ResponseContent) THEN
        EXIT(TRUE);

      ErrorMessage := GetMessageFromErrorJSON(ResponseContent);
      EXIT(FALSE);
    END;

    [Internal]
    PROCEDURE InvokeIsExternalAccountantLicenseAvailable@23(VAR ErrorMessage@1001 : Text;VAR TargetLicense@1012 : Text) : Boolean;
    VAR
      JSONManagement@1002 : Codeunit 5459;
      JsonObject@1003 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
      JsonArray@1004 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JArray";
      PrepaidUnitsJsonObject@1005 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
      NumberSkus@1006 : Integer;
      ResponseContent@1007 : Text;
      SkuIdValue@1008 : Text;
      ConsumedUnitsValue@1009 : Decimal;
      PrepaidUnitsValue@1010 : Text;
      EnabledUnitsValue@1011 : Decimal;
    BEGIN
      IF InvokeRequestWithGraphAccessToken(GetGraphSubscribedSkusUrl,'GET','',ResponseContent) THEN BEGIN
        IF NOT JSONManagement.TryParseJObjectFromString(JsonObject,ResponseContent) THEN
          ERROR(ExternalAccountantLicenseAvailabilityErr);

        IF NOT JSONManagement.GetArrayPropertyValueFromJObjectByName(JsonObject,ValueTxt,JsonArray) THEN
          ERROR(ExternalAccountantLicenseAvailabilityErr);
        JSONManagement.InitializeCollectionFromJArray(JsonArray);

        NumberSkus := JSONManagement.GetCollectionCount;
        WHILE NumberSkus > 0 DO BEGIN
          NumberSkus := NumberSkus - 1;
          IF JSONManagement.GetJObjectFromCollectionByIndex(JsonObject,NumberSkus) THEN BEGIN
            IF NOT JSONManagement.GetStringPropertyValueFromJObjectByName(JsonObject,SkuIdTxt,SkuIdValue) THEN
              ERROR(ExternalAccountantLicenseAvailabilityErr);
            IF NOT JSONManagement.GetDecimalPropertyValueFromJObjectByName(JsonObject,ConsumedUnitsTxt,ConsumedUnitsValue) THEN
              ERROR(ExternalAccountantLicenseAvailabilityErr);
            // Check to see if there is an external accountant license available.
            IF (SkuIdValue = ExternalAccountantLicenseSkuIdTxt) OR (SkuIdValue = TrialViralLicenseSkuIdTxt) THEN BEGIN
              TargetLicense := SkuIdValue;
              IF NOT JSONManagement.GetStringPropertyValueFromJObjectByName(JsonObject,PrepaidUnitsTxt,PrepaidUnitsValue) THEN
                ERROR(ExternalAccountantLicenseAvailabilityErr);
              IF NOT JSONManagement.TryParseJObjectFromString(PrepaidUnitsJsonObject,PrepaidUnitsValue) THEN
                ERROR(ExternalAccountantLicenseAvailabilityErr);
              IF NOT JSONManagement.GetDecimalPropertyValueFromJObjectByName(PrepaidUnitsJsonObject,EnabledUnitsTxt,EnabledUnitsValue) THEN
                ERROR(ExternalAccountantLicenseAvailabilityErr);

              IF ConsumedUnitsValue < EnabledUnitsValue THEN
                EXIT(TRUE);

              EXIT(FALSE);
            END;
          END;
        END;
      END;

      ErrorMessage := GetMessageFromErrorJSON(ResponseContent);
      EXIT(FALSE);
    END;

    [Internal]
    PROCEDURE VerifySMTPIsEnabledAndSetup@36() : Boolean;
    VAR
      SMTPMail@1010 : Codeunit 400;
    BEGIN
      IF NOT SMTPMail.IsEnabled THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    [Internal]
    PROCEDURE InvokeEmailAddressIsAADAccount@38(EmailAddress@1010 : Text;VAR ErrorMessage@1011 : Text) : Boolean;
    VAR
      JSONManagement@1012 : Codeunit 5459;
      JsonObject@1013 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
      ResponseContent@1016 : Text;
      Url@1017 : Text;
      Body@1018 : Text;
      ResponseCodeValue@1019 : Text;
    BEGIN
      Url := GetSignupPrefix + 'api/signupservice/usersignup?api-version=1';
      Body := '{"emailaddress": "' + EmailAddress + '","skuid": "StoreForBusinessIW","skipVerificationEmail": true}';

      InvokeRequestWithSignupAccessToken(Url,'POST',Body,ResponseContent);
      JSONManagement.InitializeObject(ResponseContent);
      JSONManagement.GetJSONObject(JsonObject);
      JSONManagement.GetStringPropertyValueFromJObjectByName(JsonObject,ResponseCodeTxt,ResponseCodeValue);
      CASE ResponseCodeValue OF
        'Success':
          EXIT(TRUE);
        'UserExists':
          EXIT(TRUE);
        'ConsumerDomainNotAllowed':
          ErrorMessage := EmailAddressIsConsumerAccountTxt;
        ELSE
          ErrorMessage := EmailAddressIsInvalidTxt;
      END;

      EXIT(FALSE);
    END;

    [Internal]
    PROCEDURE InvokeIsUserAdministrator@3() : Boolean;
    VAR
      AzureADUserManagement@1008 : Codeunit 9010;
      ClaimValue@1000 : Text;
    BEGIN
      ClaimValue := AzureADUserManagement.GetCurrentUserTokenClaim(WidsClaimNameTok);
      IF ClaimValue = '' THEN
        EXIT(FALSE);

      IF STRPOS(UPPERCASE(ClaimValue),UPPERCASE(GlobalAdministratorRoleTemplateIdTxt)) > 0 THEN
        EXIT(TRUE);
      IF STRPOS(UPPERCASE(ClaimValue),UPPERCASE(UserAdministratorRoleTemplateIdTxt)) > 0 THEN
        EXIT(TRUE);

      EXIT(FALSE);
    END;

    [Internal]
    PROCEDURE CreateNewUser@42(InvitedUserId@1003 : GUID);
    VAR
      AzureADUserManagement@1001 : Codeunit 9010;
      GuestGraphUser@1002 : DotNet "'Microsoft.Dynamics.Nav.LicensingService.Model, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.LicensingService.Model.UserInfo";
      Graph@1004 : DotNet "'Microsoft.Dynamics.Nav.AzureADGraphClient, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.AzureADGraphClient.GraphQuery";
      Count@1005 : Integer;
    BEGIN
      Graph := Graph.GraphQuery;

      REPEAT
        SLEEP(2000);
        Count := Count + 1;
        Graph.TryGetUserByObjectId(InvitedUserId,GuestGraphUser);
      UNTIL (Graph.GetUserAssignedPlans(GuestGraphUser).Count > 1) OR (Count = 10);

      IF Graph.GetUserAssignedPlans(GuestGraphUser).Count > 1 THEN BEGIN
        OnInvitationCreateNewUser(TRUE);
        AzureADUserManagement.CreateNewUserFromGraphUser(GuestGraphUser);
      END ELSE
        OnInvitationCreateNewUser(FALSE);
    END;

    [Internal]
    PROCEDURE TryGetGuestGraphUser@46(InvitedUserId@1001 : GUID;VAR GuestGraphUser@1002 : DotNet "'Microsoft.Dynamics.Nav.LicensingService.Model, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.LicensingService.Model.UserInfo") : Boolean;
    VAR
      Graph@1003 : DotNet "'Microsoft.Dynamics.Nav.AzureADGraphClient, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.AzureADGraphClient.GraphQuery";
      FoundUser@1004 : Boolean;
      Count@1005 : Integer;
    BEGIN
      Graph := Graph.GraphQuery;

      REPEAT
        SLEEP(2000);
        Count := Count + 1;
        FoundUser := Graph.TryGetUserByObjectId(InvitedUserId,GuestGraphUser);
      UNTIL FoundUser OR (Count = 10);

      EXIT(FoundUser);
    END;

    [External]
    PROCEDURE UpdateAssistedSetup@37();
    VAR
      AssistedSetup@1010 : Record 1803;
    BEGIN
      IF AssistedSetup.WRITEPERMISSION THEN
        IF AssistedSetup.GET(PAGE::"Invite External Accountant") THEN BEGIN
          AssistedSetup.VALIDATE(Status,AssistedSetup.Status::Completed);
          AssistedSetup.MODIFY(TRUE);
        END;
    END;

    LOCAL PROCEDURE InvokeRequestWithGraphAccessToken@39(Url@1010 : Text;Verb@1011 : Text;Body@1012 : Text;VAR ResponseContent@1013 : Text) : Boolean;
    BEGIN
      EXIT(InvokeRequest(Url,Verb,Body,GetGraphPrefix,ResponseContent));
    END;

    LOCAL PROCEDURE InvokeRequestWithSignupAccessToken@40(Url@1010 : Text;Verb@1011 : Text;Body@1012 : Text;VAR ResponseContent@1013 : Text) : Boolean;
    BEGIN
      EXIT(InvokeRequest(Url,Verb,Body,GetSignupPrefix,ResponseContent));
    END;

    LOCAL PROCEDURE InvokeRequest@24(Url@1007 : Text;Verb@1008 : Text;Body@1010 : Text;AuthResourceUrl@1031 : Text;VAR ResponseContent@1009 : Text) : Boolean;
    VAR
      TempBlob@1021 : Record 99008535;
      AzureADMgt@1006 : Codeunit 6300;
      IdentityManagement@1020 : Codeunit 9801;
      HttpWebRequestMgt@1022 : Codeunit 1297;
      WebRequestHelper@1023 : Codeunit 1299;
      HttpStatusCode@1024 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.HttpStatusCode";
      ResponseHeaders@1025 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.Specialized.NameValueCollection";
      WebException@1026 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Net.WebException";
      InStr@1027 : InStream;
      AccessToken@1005 : Text;
      ServiceUrl@1029 : Text;
      ChunkText@1028 : Text;
      WasSuccessful@1030 : Boolean;
    BEGIN
      AccessToken := AzureADMgt.GetGuestAccessToken(AuthResourceUrl,IdentityManagement.GetAadTenantId);

      IF AccessToken = '' THEN
        ERROR(ErrorAcquiringTokenErr);

      HttpWebRequestMgt.Initialize(Url);
      HttpWebRequestMgt.DisableUI;
      HttpWebRequestMgt.SetReturnType('application/json');
      HttpWebRequestMgt.SetContentType('application/json');
      HttpWebRequestMgt.SetMethod(Verb);
      HttpWebRequestMgt.AddHeader('Authorization','Bearer ' + AccessToken);
      IF Verb <> 'GET' THEN
        HttpWebRequestMgt.AddBodyAsText(Body);

      TempBlob.INIT;
      TempBlob.Blob.CREATEINSTREAM(InStr);
      IF HttpWebRequestMgt.GetResponse(InStr,HttpStatusCode,ResponseHeaders) THEN
        WasSuccessful := TRUE
      ELSE BEGIN
        WebRequestHelper.GetWebResponseError(WebException,ServiceUrl);
        WebException.Response.GetResponseStream.CopyTo(InStr);
        WasSuccessful := FALSE;
      END;

      WHILE NOT InStr.EOS DO BEGIN
        InStr.READTEXT(ChunkText);
        ResponseContent += ChunkText;
      END;

      EXIT(WasSuccessful);
    END;

    LOCAL PROCEDURE GetMessageFromErrorJSON@25(ResponseContent@1001 : Text) : Text;
    VAR
      JSONManagement@1002 : Codeunit 5459;
      JsonObject@1003 : DotNet "'Newtonsoft.Json'.Newtonsoft.Json.Linq.JObject";
      ErrorObjectValue@1004 : Text;
      MessageValue@1005 : Text;
    BEGIN
      JSONManagement.InitializeObject(ResponseContent);
      JSONManagement.GetJSONObject(JsonObject);
      IF JSONManagement.GetStringPropertyValueFromJObjectByName(JsonObject,ErrorTxt,ErrorObjectValue) THEN BEGIN
        JSONManagement.InitializeObject(ErrorObjectValue);
        JSONManagement.GetJSONObject(JsonObject);
        JSONManagement.GetStringPropertyValueFromJObjectByName(JsonObject,MessageTxt,MessageValue);
      END;

      EXIT(MessageValue);
    END;

    LOCAL PROCEDURE GetGraphInvitationsUrl@4() : Text;
    BEGIN
      IF IsPPE THEN
        EXIT('https://graph.microsoft-ppe.com/v1.0/invitations');

      EXIT('https://graph.microsoft.com/v1.0/invitations');
    END;

    LOCAL PROCEDURE GetGraphUserUrl@26() : Text;
    BEGIN
      IF IsPPE THEN
        EXIT('https://graph.microsoft-ppe.com/v1.0/users');

      EXIT('https://graph.microsoft.com/v1.0/users');
    END;

    LOCAL PROCEDURE GetGraphSubscribedSkusUrl@28() : Text;
    BEGIN
      IF IsPPE THEN
        EXIT('https://graph.microsoft-ppe.com/v1.0/subscribedSkus');

      EXIT('https://graph.microsoft.com/v1.0/subscribedSkus');
    END;

    LOCAL PROCEDURE GetGraphPrefix@5() : Text;
    BEGIN
      IF IsPPE THEN
        EXIT('https://graph.microsoft-ppe.com');

      EXIT('https://graph.microsoft.com');
    END;

    LOCAL PROCEDURE GetSignupPrefix@41() : Text;
    BEGIN
      IF IsPPE THEN
        EXIT('https://signupppe.microsoft.com/');

      EXIT('https://signup.microsoft.com/');
    END;

    [External]
    PROCEDURE IsPPE@29() : Boolean;
    VAR
      EnvironmentMgt@1000 : Codeunit 9005;
    BEGIN
      IF EnvironmentMgt.IsPPE THEN
        EXIT(TRUE);

      EXIT(FALSE);
    END;

    [EventSubscriber(Page,9033,OnInvitationStart)]
    LOCAL PROCEDURE SendTelemetryForInvitationStart@31();
    BEGIN
      SENDTRACETAG('0000178',InviteExternalAccountantTelemetryCategoryTxt,VERBOSITY::Normal,
        InviteExternalAccountantTelemetryStartTxt,DATACLASSIFICATION::SystemMetadata);
    END;

    [EventSubscriber(Page,9033,OnInvitationNoExternalAccountantLicenseFail)]
    LOCAL PROCEDURE SendTelemetryForInvitationNoExternalAccountantLicenseFail@32();
    BEGIN
      SENDTRACETAG('0000179',InviteExternalAccountantTelemetryCategoryTxt,VERBOSITY::Error,
        InviteExternalAccountantTelemetryLicenseFailTxt,DATACLASSIFICATION::SystemMetadata);
    END;

    [EventSubscriber(Page,9033,OnInvitationNoAADPermissionsFail)]
    LOCAL PROCEDURE SendTelemetryForInvitationNoAADPermissionsFail@33();
    BEGIN
      SENDTRACETAG('000017A',InviteExternalAccountantTelemetryCategoryTxt,VERBOSITY::Error,
        InviteExternalAccountantTelemetryAADPermissionFailTxt,DATACLASSIFICATION::SystemMetadata);
    END;

    [EventSubscriber(Page,9033,OnInvitationNoUserTablePermissionsFail)]
    LOCAL PROCEDURE SendTelemetryForInvitationNoUserTableWritePermissionsFail@43();
    BEGIN
      SENDTRACETAG('00001DK',InviteExternalAccountantTelemetryCategoryTxt,VERBOSITY::Error,
        InviteExternalAccountantTelemetryUserTablePermissionFailTxt,DATACLASSIFICATION::SystemMetadata);
    END;

    [EventSubscriber(Page,9033,OnInvitationEnd)]
    LOCAL PROCEDURE SendTelemetryForInvitationEnd@34(WasInvitationSuccessful@1001 : Boolean;Result@1002 : Text;Progress@1003 : Text);
    BEGIN
      IF WasInvitationSuccessful THEN
        SENDTRACETAG('000017B',InviteExternalAccountantTelemetryCategoryTxt,VERBOSITY::Normal,
          STRSUBSTNO(InviteExternalAccountantTelemetryEndTxt,Result,Progress),DATACLASSIFICATION::SystemMetadata)
      ELSE
        SENDTRACETAG('000017C',InviteExternalAccountantTelemetryCategoryTxt,VERBOSITY::Error,
          STRSUBSTNO(InviteExternalAccountantTelemetryEndTxt,Result,Progress),DATACLASSIFICATION::SystemMetadata);
    END;

    [EventSubscriber(Codeunit,9033,OnInvitationCreateNewUser)]
    LOCAL PROCEDURE SendTelemetryForInvitationCreateNewUser@45(UserCreated@1000 : Boolean);
    BEGIN
      IF UserCreated THEN
        SENDTRACETAG('00001DL',InviteExternalAccountantTelemetryCategoryTxt,VERBOSITY::Normal,
          InviteExternalAccountantTelemetryCreateNewUserSuccessTxt,DATACLASSIFICATION::SystemMetadata)
      ELSE
        SENDTRACETAG('00001DM',InviteExternalAccountantTelemetryCategoryTxt,VERBOSITY::Error,
          InviteExternalAccountantTelemetryCreateNewUserFailedTxt,DATACLASSIFICATION::SystemMetadata);
    END;

    [Integration]
    LOCAL PROCEDURE OnInvitationCreateNewUser@44(UserCreated@1000 : Boolean);
    BEGIN
      // This event is called when the invitation process tries to create a user.
    END;

    BEGIN
    END.
  }
}

