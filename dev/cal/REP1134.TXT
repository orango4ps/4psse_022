OBJECT Report 1134 Copy Cost Budget
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Kostenbudget kopieren;
               ENU=Copy Cost Budget;
               NLD=Kostenbudget kopiâren;
               NOR=Kopier kostbudsjett];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   IF NoOfCopies = 0 THEN
                     NoOfCopies := 1;
                   IF Factor = 0 THEN
                     Factor := 1;
                 END;

    OnPreReport=BEGIN
                  EVALUATE(DateFormula,DateChangeFormula);
                END;

  }
  DATASET
  {
    { 3233;    ;DataItem;                    ;
               DataItemTable=Table1109;
               DataItemTableView=SORTING(Budget Name,Cost Type No.,Date);
               OnPreDataItem=BEGIN
                               IF Factor = 0 THEN
                                 ERROR(Text000);

                               IF NoOfCopies < 1 THEN
                                 ERROR(Text001);

                               IF (NoOfCopies > 1) AND (DateChangeFormula = '') THEN
                                 ERROR(Text002);

                               IF GETFILTER("Budget Name") = '' THEN
                                 ERROR(Text008);

                               IF CostBudgetEntryReqForm."Budget Name" = '' THEN
                                 ERROR(Text009);

                               IF CostBudgetEntryReqForm."Budget Name" <> '' THEN
                                 CostBudgetNameTarget.GET(CostBudgetEntryReqForm."Budget Name")
                               ELSE
                                 CostBudgetNameTarget.GET(GETFILTER("Budget Name"));

                               IF NOT CONFIRM(
                                    Text004,FALSE,GETFILTER("Budget Name"),CostBudgetNameTarget.Name,Factor,NoOfCopies,GETFILTER(Date),DateChangeFormula)
                               THEN
                                 ERROR('');

                               LOCKTABLE;

                               IF CostBudgetEntryTarget.FINDLAST THEN
                                 LastEntryNo := CostBudgetEntryTarget."Entry No.";

                               NextEntryNo := LastEntryNo + 1;

                               Window.OPEN(Text005);

                               Window.UPDATE(1,COUNT);
                               Window.UPDATE(2,NoOfCopies);
                             END;

               OnAfterGetRecord=VAR
                                  CostAccMgt@1000 : Codeunit 1100;
                                BEGIN
                                  IF "Entry No." > LastEntryNo THEN
                                    CurrReport.BREAK;

                                  CostBudgetEntryTarget := "Cost Budget Entry";

                                  CostBudgetEntryTarget.Description := STRSUBSTNO(Text006,GETFILTER("Budget Name"));
                                  CostBudgetEntryTarget."Budget Name" := CostBudgetEntryReqForm."Budget Name";

                                  IF CostBudgetEntryReqForm."Cost Type No." <> '' THEN
                                    CostBudgetEntryTarget."Cost Type No." := CostBudgetEntryReqForm."Cost Type No.";
                                  IF CostBudgetEntryReqForm."Cost Center Code" <> '' THEN
                                    CostBudgetEntryTarget."Cost Center Code" := CostBudgetEntryReqForm."Cost Center Code";
                                  IF CostBudgetEntryReqForm."Cost Object Code" <> '' THEN
                                    CostBudgetEntryTarget."Cost Object Code" := CostBudgetEntryReqForm."Cost Object Code";

                                  CostBudgetEntryTarget.Amount := ROUND(Amount * Factor,0.01);
                                  CostBudgetEntryTarget.Allocated := FALSE;

                                  FOR i := 1 TO NoOfCopies DO BEGIN
                                    CostBudgetEntryTarget."Entry No." := NextEntryNo;
                                    NextEntryNo := NextEntryNo + 1;
                                    IF DateChangeFormula <> '' THEN
                                      CostBudgetEntryTarget.Date := CALCDATE(DateFormula,CostBudgetEntryTarget.Date);
                                    CostBudgetEntryTarget.INSERT;
                                    NoInserted := NoInserted + 1;

                                    IF CostBudgetRegNo = 0 THEN
                                      CostBudgetRegNo :=
                                        CostAccMgt.InsertCostBudgetRegister(
                                          CostBudgetEntryTarget."Entry No.",CostBudgetEntryTarget."Budget Name",CostBudgetEntryTarget.Amount)
                                    ELSE
                                      CostAccMgt.UpdateCostBudgetRegister(
                                        CostBudgetRegNo,CostBudgetEntryTarget."Entry No.",CostBudgetEntryTarget.Amount);

                                    IF (NoInserted MOD 100) = 0 THEN
                                      Window.UPDATE(3,NoInserted);
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;

                                IF NoInserted = 0 THEN BEGIN
                                  MESSAGE(Text010);
                                  ERROR('');
                                END;

                                IF NOT CONFIRM(Text007,TRUE,NoInserted,CostBudgetNameTarget.Name) THEN
                                  ERROR('');
                              END;

               ReqFilterFields=Budget Name,Cost Type No.,Cost Center Code,Cost Object Code,Date }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnInit=BEGIN
               CostBudgetEntryReqForm.INIT;
             END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 21  ;2   ;Group     ;
                  CaptionML=[DEU=Kopieren nach...;
                             ENU=Copy to...;
                             NLD=Kopiâren naar...;
                             NOR=Kopier til...] }

      { 1   ;3   ;Field     ;
                  Name=Budget Name;
                  Lookup=Yes;
                  CaptionML=[DEU=Budgetname;
                             ENU=Budget Name;
                             NLD=Budget;
                             NOR=Budsjettnavn;
                             SVE=Budgetnamn];
                  ToolTipML=[DEU=Gibt den Namen des Budgets an.;
                             ENU=Specifies the name of the budget.;
                             NLD=Hiermee wordt de naam van het budget opgegeven.;
                             NOR=Angir navnet pÜ budsjettet.;
                             SVE=Anger budgetens namn.];
                  ApplicationArea=#CostAccounting;
                  SourceExpr=CostBudgetEntryReqForm."Budget Name";
                  TableRelation="Cost Budget Name" }

      { 2   ;3   ;Field     ;
                  Name=Cost Type No.;
                  Lookup=Yes;
                  CaptionML=[DEU=Kostenartnr.;
                             ENU=Cost Type No.;
                             NLD=Nr. kostensoort;
                             NOR=Kosttypenr.;
                             SVE=Kostnadstypsnr];
                  ToolTipML=[DEU=Gibt die Kostenartnummer an.;
                             ENU=Specifies the cost type number.;
                             NLD=Hiermee wordt het nummer van de kostensoort opgegeven.;
                             NOR=Angir kosttypenummeret.];
                  ApplicationArea=#CostAccounting;
                  SourceExpr=CostBudgetEntryReqForm."Cost Type No.";
                  TableRelation="Cost Type" }

      { 4   ;3   ;Field     ;
                  Name=Cost Center Code;
                  Lookup=Yes;
                  CaptionML=[DEU=Kostenstellencode;
                             ENU=Cost Center Code;
                             NLD=Kostenplaatscode;
                             NOR=Kostsenterkode;
                             SVE=Kod fîr kostnadsstÑlle];
                  ToolTipML=[DEU=Gibt den geltenden Kostenstellencode an.;
                             ENU=Specifies the cost center code that applies.;
                             NLD=Hiermee wordt de kostenplaatscode opgegeven die van toepassing is.;
                             NOR=Angir kostsenterkoden som skal brukes.];
                  ApplicationArea=#CostAccounting;
                  SourceExpr=CostBudgetEntryReqForm."Cost Center Code";
                  TableRelation="Cost Center" }

      { 5   ;3   ;Field     ;
                  Name=Cost Object Code;
                  Lookup=Yes;
                  CaptionML=[DEU=KostentrÑgercode;
                             ENU=Cost Object Code;
                             NLD=Kostenobjectcode;
                             NOR=Kostobjektkode;
                             SVE=Kod fîr kostnadsbÑrare];
                  ToolTipML=[DEU=Gibt den Code des Kostenelements an.;
                             ENU=Specifies the code of the cost element.;
                             NLD=Hiermee wordt de code opgegeven van het kostenelement.;
                             NOR=Angir koden for kostelementet.];
                  ApplicationArea=#CostAccounting;
                  SourceExpr=CostBudgetEntryReqForm."Cost Object Code";
                  TableRelation="Cost Object" }

      { 3   ;2   ;Field     ;
                  Name=Amount multiplication factor;
                  CaptionML=[DEU=Multiplikationsfaktor fÅr Betrag;
                             ENU=Amount multiplication factor;
                             NLD=Vermenigvuldigingsfactor bedrag;
                             NOR=Belõpsmultiplikasjonsfaktor];
                  ToolTipML=[DEU=Gibt einen Korrekturfaktor an, mit dem die BetrÑge, die Sie kopieren mîchten, multipliziert werden. Durch die Eingabe eines Korrekturfaktors kînnen Sie die BetrÑge erhîhen oder verringern, die in das neue Budget kopiert werden.;
                             ENU=Specifies an adjustment factor that multiplies the amounts you want to copy. By entering an adjustment factor, you can increase or decrease the amounts that are to be copied to the new budget.;
                             NLD=Hiermee wordt een herwaarderingsfactor opgegeven om de bedragen te vermenigvuldigen die u wilt kopiâren. Door een herwaarderingsfactor in te voeren kunt u de bedragen die naar het nieuwe budget moeten worden gekopieerd, verhogen of verlagen.;
                             NOR=Angir en justeringsfaktor som multipliserer belõpene du vil kopiere. Ved Ü angi en justeringsfaktor, kan du õke eller minske belõpene som skal kopieres til det nye budsjettet.];
                  ApplicationArea=#CostAccounting;
                  SourceExpr=Factor }

      { 8   ;2   ;Field     ;
                  Name=No. of Copies;
                  CaptionML=[DEU=Anzahl Kopien;
                             ENU=No. of Copies;
                             NLD=Aantal exemplaren;
                             NOR=Antall eksemplarer;
                             SVE=Antal kopior];
                  ToolTipML=[DEU=Gibt an, wie oft das Kostenbudget kopiert wird.;
                             ENU=Specifies how many times the cost budget is copied.;
                             NLD=Hiermee wordt opgegeven hoe vaak het kostenbudget wordt gekopieerd.;
                             NOR=Angir hvor mange ganger kostbudsjettet skal kopieres.];
                  ApplicationArea=#CostAccounting;
                  SourceExpr=NoOfCopies }

      { 6   ;2   ;Field     ;
                  Name=Date Change Formula;
                  CaptionML=[DEU=Datumswechsel Formel;
                             ENU=Date Change Formula;
                             NLD=Datumformule;
                             NOR=Endre datoformel;
                             SVE=DatumÑndringsformel];
                  ToolTipML=[DEU=Gibt an, wie die Datumsangaben der kopierten Posten geÑndert werden. Verwenden Sie hierzu eine Datumsformel. Wenn Sie beispielsweise das Budget der letzten Woche in die laufende Woche kopieren mîchten, verwenden Sie die Formel 1W (eine Woche).;
                             ENU="Specifies how the dates on the entries that are copied will be changed. Use a date formula; for example, to copy last week's budget to this week, use the formula 1W (one week).";
                             NLD=Hiermee wordt opgegeven hoe de datums op de gekopieerde posten worden gewijzigd. Als u bijvoorbeeld het budget van de vorige week wilt kopiâren naar deze week, gebruikt u de datumformule 1W (ÇÇn week).;
                             NOR=Angir hvordan datoene pÜ postene som kopieres, blir endret. Bruk en datoformel. Hvis du for eksempel vil kopiere siste ukes budsjett til denne uken, bruker du formelen 1U (Çn uke).];
                  ApplicationArea=#CostAccounting;
                  DateFormula=Yes;
                  SourceExpr=DateChangeFormula }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      CostBudgetEntryReqForm@1000 : Record 1109;
      CostBudgetEntryTarget@1001 : Record 1109;
      CostBudgetNameTarget@1002 : Record 1110;
      Window@1003 : Dialog;
      DateChangeFormula@1004 : Code[10];
      DateFormula@1005 : DateFormula;
      LastEntryNo@1006 : Integer;
      NextEntryNo@1007 : Integer;
      NoOfCopies@1008 : Integer;
      Factor@1009 : Decimal;
      i@1010 : Integer;
      NoInserted@1011 : Integer;
      CostBudgetRegNo@1012 : Integer;
      Text000@1013 : TextConst 'DEU=Der Multiplikationsfaktor darf nicht null sein.;ENU=The multiplication factor must not be 0.;NLD=De vermenigvuldigingsfactor mag niet 0 zijn.;NOR=Multiplikasjonsfaktoren kan ikke vëre 0.';
      Text001@1014 : TextConst 'DEU=Die Kopienanzahl muss mindestens 1 betragen.;ENU=Number of copies must be at least 1.;NLD=Aantal exemplaren moet ten minste 1 zijn.;NOR=Antall eksemplarer mÜ vëre minst 1.';
      Text002@1015 : TextConst 'DEU=Bei Erstellung mehrerer Kopien muss eine Formel fÅr den Datumswechsel definiert werden.;ENU=If more than one copy is created, a formula for date change must be defined.;NLD=Als meerdere exemplaren worden gemaakt, moet een formule voor datumwijziging worden gedefinieerd.;NOR=Hvis flere eksemplarer opprettes, mÜ en formel for datoendring defineres.';
      Text004@1017 : TextConst '@@@="%3=multiplication factor (decimal);%4=No of copies (integer)";DEU=Das Budget ''%1'' wird in das Budget ''%2'' kopiert. Die BudgetbetrÑge werden mit dem Faktor %3 multipliziert. \%4 Kopien werden erstellt, und das Datum aus dem Bereich %5 wird um %6 erhîht.\\Mîchten Sie das Budget kopieren?;ENU=Budget %1 will be copied to Budget %2. The budget amounts will be multiplied by a factor of %3. \%4 copies will be created and the date from range %5 will be incremented by %6.\\Do you want do copy the budget?;NLD=Budget %1 wordt gekopieerd naar Budget %2. De budgetbedragen worden vermenigvuldigd met een factor %3. \%4 kopieân worden gemaakt en het begindatumbereik %5 wordt vermeerderd met %6.\\Wilt u het budget kopiâren?;NOR=Budsjettet %1 vil bli kopiert til budsjettet %2. Budsjettbelõpene vil bli multiplisert med faktoren %3. \%4 eksemplarer vil bli opprettet, og dato fra-omrÜdet %5 vil bli õkt med %6.\\Vil du kopiere budsjettet?';
      Text005@1018 : TextConst 'DEU=Budgetposten werden kopiert...\Postenanzahl #1#####\Kopienanzahl #2#####\Kopiert      #3#####;ENU=Copying budget entries\No of entries #1#####\No of copies  #2#####\Copied        #3#####;NLD=Budgetposten kopiâren\Aantal posten #1#####\Aantal exemplaren #2#####\Gekopieerd        #3#####;NOR=Kopierer budsjettposter\Antall poster #1#####\Antall eksemplarer  #2#####\Kopiert        #3#####';
      Text006@1019 : TextConst '@@@=%1 - Budget Name.;DEU=Kopie des Kostenbudgets %1;ENU=Copy of cost budget %1;NLD=Kopie van kostenbudget %1;NOR=Kopi av kostbudsjett %1';
      Text007@1020 : TextConst 'DEU=Im Budget ''%2'' wurden %1 Posten generiert.\\Mîchten Sie Posten kopieren?;ENU=%1 entries generated in budget %2.\\Do you want to copy entries?;NLD=%1 posten gegenereerd in budget %2.\\Wilt u de posten kopiâren?;NOR=%1 poster er generert i budsjettet %2.\\Vil du kopiere poster?';
      Text008@1021 : TextConst 'DEU=Definieren Sie den Namen des Herkunftsbudgets.;ENU=Define name of source budget.;NLD=Definieer de naam van het bronbudget.;NOR=Definer navn pÜ kildebudsjett.';
      Text009@1022 : TextConst 'DEU=Definieren Sie den Namen des Zielbudgets.;ENU=Define name of target budget.;NLD=Definieer de naam van het doelbudget.;NOR=Definer navn pÜ mÜlbudsjett.';
      Text010@1023 : TextConst 'DEU=Keine EintrÑge kopiert.;ENU=No entries were copied.;NLD=Er zijn geen posten gekopieerd.;NOR=Det ble ikke kopiert noen poster.';

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

