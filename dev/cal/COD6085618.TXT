OBJECT Codeunit 6085618 Update DC Doc. Counters
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.5.00;
  }
  PROPERTIES
  {
    TableNo=6085575;
    SingleInstance=Yes;
    OnRun=VAR
            DCSetup@1160040001 : Record 6085573;
            Document@1160040003 : Record 6085590;
            FileSysMgt@1160040002 : Codeunit 6085584;
          BEGIN
            "Documents for OCR" := 0;
            "Documents for Import" := 0;
            "Documents with Error" := 0;

            IF CODC.IsCompanyActive2(FALSE,FALSE,FALSE) THEN BEGIN
              IF CODC.IsCloudActive(FALSE) THEN BEGIN
                DCSetup.GET;
                IF (LastUpdate < CURRENTDATETIME-1000 * DCSetup."Cloud OCR Cache Timeout") OR
                  (DCSetup."Cloud OCR Cache Timeout" = 0) OR (ForceUpdate)
                THEN BEGIN
                  CODC.GetAllCategoryCount(TempDocCat);
                  LastUpdate := CURRENTDATETIME;

                  ForceUpdate := FALSE;
                END;

                IF NOT TempDocCat.GET(Code) THEN
                  CLEAR(TempDocCat);

                "Documents for OCR" := TempDocCat."Documents for OCR";
                "Documents for Import" := TempDocCat."Documents for Import";
                "Documents with Error" := TempDocCat."Documents with Error";
              END;

              IF NOT DCSetup."Use Cloud OCR" THEN BEGIN
                "Documents for OCR" += FileSysMgt.GetFilesInDir(GetCategoryPath(1),'*.pdf');
                "Documents for Import" += FileSysMgt.GetFilesInDir(GetCategoryPath(2),'*.tiff');
                "Documents with Error" += FileSysMgt.GetFilesInDir(GetCategoryPath(3),'*.pdf');
              END;
            END;

            "Documents to Register" := GetNoOfDocWithStatus(Code,Document.Status::Open);
            "Registered Documents" := GetNoOfDocWithStatus(Code,Document.Status::Registered);
            "Rejected Documents" := GetNoOfDocWithStatus(Code,Document.Status::Rejected);
          END;

  }
  CODE
  {
    VAR
      CODC@6085573 : Codeunit 6085617;
      TempDocCat@1160040001 : TEMPORARY Record 6085575;
      LastUpdate@1160040000 : DateTime;
      ForceUpdate@1160040002 : Boolean;

    PROCEDURE SetForceUpdate@1160040000(NewForceUpdate@1160040000 : Boolean);
    BEGIN
      ForceUpdate := NewForceUpdate;
    END;

    PROCEDURE GetNoOfDocWithStatus@1160040001(DocCatCode@1160040000 : Code[20];Status@1160040001 : Integer) : Integer;
    VAR
      Document@1160040002 : Record 6085590;
    BEGIN
      Document.SETCURRENTKEY("Document Category Code",Status);
      Document.SETRANGE("Document Category Code",DocCatCode);
      Document.SETRANGE(Status,Status);
      EXIT(Document.COUNT);
    END;

    PROCEDURE ClearCODC@6085573();
    BEGIN
      CLEAR(CODC);
    END;

    BEGIN
    END.
  }
}

