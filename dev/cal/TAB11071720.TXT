OBJECT Table 11071720 Service Loc. Structure Line
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               ServiceLocStructureLine@1100526200 : Record 11071720;
             BEGIN
               IF NOT "Object Line" THEN BEGIN
                 ServiceLocStructureLine.SETRANGE("Service Location No.", "Service Location No.");
                 ServiceLocStructureLine.SETRANGE("Structure No.", "Structure No.");
                 ServiceLocStructureLine.SETRANGE("No.", "No.");
                 ServiceLocStructureLine.SETRANGE("Object Line", TRUE);
                 ServiceLocStructureLine.DELETEALL(TRUE);
               END;
             END;

    CaptionML=[ENU=Location Structure Line;
               NOR=Lokasjonstrukturrad;
               SVE=Lagerst„llestrukturrad];
    LookupPageID=Page11126832;
    DrillDownPageID=Page11126832;
  }
  FIELDS
  {
    { 10  ;   ;Service Location No.;Code20        ;TableRelation="Service Location".No.;
                                                   CaptionML=[ENU=Service Location;
                                                              NOR=Servicelokasjon;
                                                              SVE=Serviceplats] }
    { 25  ;   ;Structure No.       ;Integer       ;TableRelation="Service Location Structure".No. WHERE (Service Location No.=FIELD(Service Location No.));
                                                   CaptionML=[ENU=Structure No.;
                                                              NOR=Strukturnr;
                                                              SVE=Strukturnr];
                                                   NotBlank=Yes }
    { 30  ;   ;No.                 ;Code20        ;OnValidate=VAR
                                                                ServiceLocation@1100485001 : Record 11012801;
                                                                FormatManagement@1100485000 : Codeunit 11012022;
                                                                ElementLevel0@1100528600 : Code[20];
                                                              BEGIN
                                                                ServiceLocation.GET("Service Location No.");
                                                                ServiceLocation.TESTFIELD("Location Structure Format");
                                                                IF STRPOS(ServiceLocation."Location Structure Format", '.') > 0 THEN
                                                                  FormatManagement.CreateServLocStructureElement(
                                                                    "Service Location No.", "Structure No.", ElementLevel0, "Element Level 1", "Element Level 2", "No.", Level, TRUE);
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 32  ;   ;Service Object No.  ;Code20        ;TableRelation=Service-Object.No. WHERE (Service Location No.=FIELD(Service Location No.));
                                                   OnValidate=VAR
                                                                ServiceLocationStructure@1100528600 : Record 11071719;
                                                                ServiceLocStructureLine@1100528601 : Record 11071720;
                                                              BEGIN
                                                                ServiceLocationStructure.SETRANGE("Service Location No.", "Service Location No.");
                                                                ServiceLocationStructure.SETRANGE("No.", "Structure No.");
                                                                ServiceLocationStructure.SETRANGE("Default for LTMP", TRUE);
                                                                IF NOT ServiceLocationStructure.ISEMPTY THEN BEGIN
                                                                  ServiceLocStructureLine.SETRANGE("Service Location No.", "Service Location No.");
                                                                  ServiceLocStructureLine.SETRANGE("Structure No.", "Structure No.");
                                                                  ServiceLocStructureLine.SETFILTER("No.", '<>%1', "No.");
                                                                  ServiceLocStructureLine.SETRANGE("Service Object No.", "Service Object No.");
                                                                  ServiceLocStructureLine.SETRANGE("Object Line", TRUE);
                                                                  IF ServiceLocStructureLine.FINDFIRST THEN
                                                                    ERROR(Text002, "Service Object No.", ServiceLocStructureLine."No.");
                                                                END;
                                                                IF "Service Object No." <> '' THEN
                                                                  "Object Line" := TRUE;
                                                              END;

                                                   CaptionML=[ENU=Service Object;
                                                              NOR=Serviceobjekt;
                                                              SVE=Serviceobjekt] }
    { 35  ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 37  ;   ;Description 2       ;Text80        ;CaptionML=[ENU=Description 2;
                                                              NOR=Beskrivelse 2;
                                                              SVE=Beskrivning 2] }
    { 40  ;   ;Element Level 1     ;Code20        ;CaptionML=[ENU=Element Level 1;
                                                              NOR=Elementniv† 1;
                                                              SVE=Elementniv† 1];
                                                   Editable=No }
    { 50  ;   ;Element Level 2     ;Code20        ;CaptionML=[ENU=Element Level 2;
                                                              NOR=Elementniv† 2;
                                                              SVE=Elementniv† 2];
                                                   Editable=No }
    { 60  ;   ;Level               ;Integer       ;CaptionML=[ENU=Level;
                                                              NOR=Niv†;
                                                              SVE=Niv†];
                                                   Editable=No }
    { 70  ;   ;Object Line         ;Boolean       ;CaptionML=[ENU=Object Line;
                                                              NOR=Objektrad;
                                                              SVE=Objektrad];
                                                   Editable=No }
    { 80  ;   ;Standard Object No. ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object."Standard Object No." WHERE (No.=FIELD(Service Object No.)));
                                                   CaptionML=[ENU=Standard Object No.;
                                                              NOR=Standardobjektnr;
                                                              SVE=Standardobjektsnr];
                                                   Editable=No }
    { 100 ;   ;Standard Object Description;Text100;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Standard Object".Description WHERE (No.=FIELD(Standard Object No.)));
                                                   CaptionML=[ENU=Standard Object Description;
                                                              NOR=Standard objektsbeskrivelse;
                                                              SVE=Standardobjektsbeskrivning];
                                                   Editable=No }
    { 105 ;   ;Standard Object Description 2;Text50;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Standard Object"."Description 2" WHERE (No.=FIELD(Standard Object No.)));
                                                   CaptionML=[ENU=Standard Object Description 2;
                                                              SVE=Standardobjektsbeskrivning 2];
                                                   Editable=No }
    { 110 ;   ;Object Description  ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object.Description WHERE (No.=FIELD(Service Object No.)));
                                                   CaptionML=[ENU=Object Description;
                                                              NOR=Objektbeskrivelse;
                                                              SVE=Objektbeskrivning];
                                                   Editable=No }
    { 115 ;   ;Object Description 2;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object."Description 2" WHERE (No.=FIELD(Service Object No.)));
                                                   CaptionML=ENU=Object Description 2;
                                                   Editable=No }
    { 120 ;   ;Blocked             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object.Blocked WHERE (No.=FIELD(Service Object No.)));
                                                   CaptionML=[ENU=Blocked;
                                                              NOR=Blokkert;
                                                              SVE=Sp„rrad];
                                                   Editable=No }
    { 130 ;   ;Customer Feature    ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object."Customer Feature" WHERE (No.=FIELD(Service Object No.)));
                                                   CaptionML=[ENU=Customer Feature;
                                                              NOR=Kundefunksjon;
                                                              SVE=Kundfunktion];
                                                   Editable=No }
    { 140 ;   ;Object Customer     ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object."Object Customer" WHERE (No.=FIELD(Service Object No.)));
                                                   CaptionML=[ENU=Object Customer;
                                                              NOR=Objektkunde;
                                                              SVE=Objektkund];
                                                   Editable=No }
    { 150 ;   ;Brand Code          ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object."Brand Code" WHERE (No.=FIELD(Service Object No.)));
                                                   TableRelation="Service Object Brand";
                                                   CaptionML=[ENU=Brand;
                                                              NOR=Varemerke;
                                                              SVE=M„rke];
                                                   Editable=No }
    { 160 ;   ;Brand Description   ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Object Brand".Description WHERE (Code=FIELD(Brand Code)));
                                                   CaptionML=[ENU=Brand Description;
                                                              SVE=M„rke];
                                                   Editable=No }
    { 170 ;   ;Type Code           ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object."Type Code" WHERE (No.=FIELD(Service Object No.)));
                                                   TableRelation="Service Object Type".Code WHERE (Brand Code=FIELD(Brand Code),
                                                                                                   Model Code=FIELD(Model Code));
                                                   CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Typ];
                                                   Editable=No }
    { 180 ;   ;Type Description    ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Object Type".Description WHERE (Brand Code=FIELD(Brand Code),
                                                                                                               Model Code=FIELD(Model Code),
                                                                                                               Code=FIELD(Type Code)));
                                                   CaptionML=[ENU=Type Description;
                                                              NOR=Typebeskrivelse;
                                                              SVE=Radtypbeskrivning];
                                                   Editable=No }
    { 190 ;   ;Model Code          ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object."Model Code" WHERE (No.=FIELD(Service Object No.)));
                                                   TableRelation="Service Object Model".Code WHERE (Brand Code=FIELD(Brand Code));
                                                   CaptionML=[ENU=Model;
                                                              NOR=Modell;
                                                              SVE=Modell];
                                                   Editable=No }
    { 200 ;   ;Model Description   ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Object Model".Description WHERE (Brand Code=FIELD(Brand Code),
                                                                                                                Code=FIELD(Model Code)));
                                                   CaptionML=[ENU=Model Description;
                                                              NOR=Modellbeskrivelse;
                                                              SVE=Modell];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Service Location No.,Structure No.,No.,Service Object No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1210190000 : TextConst 'ENU=First Select Lines;NOR=Velg rader f›rst;SVE=V„lj rad(er) f”rst';
      Text002@1100528601 : TextConst 'ENU=Service Object %1 already defined at %2;NOR=Serviceobjekt %1 er allerede definert ved %2';

    PROCEDURE CreateObjectsWithinStructure@1100528600(VAR ITmpContractConsObjWizardLine@1100528600 : TEMPORARY Record 11071698);
    VAR
      ServiceObject@1100528601 : Record 11071691;
      ServiceLocStructureLine@1100528602 : Record 11071720;
      ServiceSetup@1210190000 : Record 11012800;
      ObjectSpecificationMgt@1210190001 : Codeunit 11012848;
    BEGIN
      IF NOT ITmpContractConsObjWizardLine.FINDSET THEN EXIT;
      REPEAT
        ServiceObject.INIT;
        ServiceObject."No." := '';
        ServiceObject.INSERT(TRUE);
        ServiceObject.VALIDATE("Standard Object No.", ITmpContractConsObjWizardLine."Standard Object No.");
        ServiceObject.VALIDATE(Description, ITmpContractConsObjWizardLine.Description);
        ServiceObject.VALIDATE("Description 2", ITmpContractConsObjWizardLine."Description 2");
        ServiceObject.VALIDATE("Service Location No.", "Service Location No.");
        ServiceObject.VALIDATE("In use since", ITmpContractConsObjWizardLine."In use since");
        ServiceObject.VALIDATE(Building, ITmpContractConsObjWizardLine.Building);
        ServiceObject.VALIDATE(Floor, ITmpContractConsObjWizardLine.Floor);
        ServiceObject.VALIDATE(Room, ITmpContractConsObjWizardLine.Room);
        ServiceObject.VALIDATE(Text, ITmpContractConsObjWizardLine.Comment);
        ServiceObject.VALIDATE("Warranty Starting Date Cust.", ITmpContractConsObjWizardLine."Warranty Starting Date");
      //  ServiceObject.VALIDATE("Warranty Ending Date", ITmpContractConsObjWizardLine."Warranty Ending Date");
        ServiceObject.MODIFY(TRUE);

        ServiceSetup.GET;
        IF ITmpContractConsObjWizardLine."Year of Manufacture" <> 0 THEN BEGIN
          ServiceSetup.TESTFIELD("Spec. Code Year of Manufact.");
          ObjectSpecificationMgt.SetObjectSpecValue(ServiceObject."No.", ServiceSetup."Spec. Code Year of Manufact.",
            FORMAT(ITmpContractConsObjWizardLine."Year of Manufacture"));
        END;
        IF ITmpContractConsObjWizardLine."Serial No." <> '' THEN BEGIN
          ServiceSetup.TESTFIELD("Spec. Code Serial No.");
          ObjectSpecificationMgt.SetObjectSpecValue(ServiceObject."No.", ServiceSetup."Spec. Code Serial No.",
            ITmpContractConsObjWizardLine."Serial No.");
        END;

        ServiceLocStructureLine.INIT;
        ServiceLocStructureLine.VALIDATE("Service Location No.", "Service Location No.");
        ServiceLocStructureLine.VALIDATE("Structure No.", "Structure No.");
        ServiceLocStructureLine.VALIDATE("No.", "No.");
        ServiceLocStructureLine.VALIDATE("Object Line", TRUE);
        ServiceLocStructureLine.VALIDATE("Service Object No.", ServiceObject."No.");
        ServiceLocStructureLine.INSERT(TRUE);
      UNTIL ITmpContractConsObjWizardLine.NEXT = 0;
    END;

    PROCEDURE CreateContractForSelObjects@1100528601(VAR IServiceLocStructureLine@1100528600 : Record 11071720);
    VAR
      ServiceLocStructureLine@1100528601 : Record 11071720;
      ServiceContract@1100528602 : Record 11012812;
      ContractObject@1100528603 : Record 11071702;
      ServiceContractTemplate@1100528605 : Record 11071754;
      ServiceLocation@1100528608 : Record 11012801;
      MaintenanceManagementCU@1100528606 : Codeunit 11012829;
      AttachToServiceContractFRM@1100528604 : Page 11125306;
      ActualContractNo@1100528607 : Code[20];
      LinkType@1100528609 : 'New Contract,Existing Contract';
      ExistingServiceContractNo@1100528610 : Code[20];
      ServicePackageCode@1100528611 : Code[10];
      ServiceContractTemplateCode@1210190000 : Code[20];
      ServiceContractStatus@1210190001 : Option;
      ServiceContractStartingDate@1210190002 : Date;
    BEGIN
      ServiceLocStructureLine.COPY(IServiceLocStructureLine);
      IF NOT ServiceLocStructureLine.FINDSET THEN
        ERROR(Text001);
      ServiceLocStructureLine.SETRANGE("Object Line", TRUE);
      REPEAT
        IF MaintenanceManagementCU.GetNoOfActualContractsForObj(ServiceLocStructureLine."Service Object No.",
          TODAY, ActualContractNo) <> 0 THEN
          ServiceLocStructureLine.MARK(FALSE);
      UNTIL ServiceLocStructureLine.NEXT = 0;

      IF ServiceLocStructureLine.FINDSET THEN BEGIN
        AttachToServiceContractFRM.LOOKUPMODE(TRUE);
        IF AttachToServiceContractFRM.RUNMODAL <> ACTION::LookupOK THEN
          EXIT;
        AttachToServiceContractFRM.GetSettings(
          LinkType, ExistingServiceContractNo, ServiceContractTemplateCode, ServiceContractStatus, ServicePackageCode,
          ServiceContractStartingDate);

        IF LinkType = LinkType::"New Contract" THEN BEGIN
          IF ServiceContractTemplateCode <> '' THEN BEGIN
            ServiceContractTemplate.GET(ServiceContractTemplateCode);
            ServiceContract.TRANSFERFIELDS(ServiceContractTemplate)
          END;
          ServiceContract.INSERT(TRUE);
          ServiceLocation.GET(ServiceLocStructureLine."Service Location No.");
          ServiceContract.VALIDATE("Customer No.", ServiceLocation."Customer No.");
          ServiceContract.VALIDATE("Service Package", ServicePackageCode);
          ServiceContract.VALIDATE("Starting Date", ServiceContractStartingDate);
          ServiceContract.VALIDATE(Status, ServiceContractStatus);
          ServiceContract.MODIFY(TRUE);
        END ELSE
          ServiceContract.GET(ExistingServiceContractNo);

        REPEAT
          IF MaintenanceManagementCU.GetNoOfActualContractsForObj(
            ServiceLocStructureLine."Service Object No.", TODAY, ActualContractNo) = 0
          THEN BEGIN
            ContractObject.INIT;
            ContractObject."Service Contract No."  := ServiceContract."No.";
            ContractObject.VALIDATE("Service Object No.", ServiceLocStructureLine."Service Object No.");
            ContractObject.VALIDATE("Service Package", ServiceContract."Service Package");
            ContractObject.VALIDATE("Starting Date", ServiceContract."Starting Date");
            ContractObject.VALIDATE("Ending Date", ServiceContract."Ending Date");
            ContractObject.INSERT(TRUE);
          END;
        UNTIL ServiceLocStructureLine.NEXT = 0;
      END;
      IServiceLocStructureLine.CLEARMARKS;
    END;

    BEGIN
    END.
  }
}

