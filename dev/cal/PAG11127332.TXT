OBJECT Page 11127332 Gen. Issue FB
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Problem;
               ENU=Issue;
               NLD=Issue];
    SourceTable=Table11125907;
    PageType=CardPart;
  }
  CONTROLS
  {
    { 1100528700;0;Container;
                ContainerType=ContentArea }

    { 1100528703;1;Field  ;
                Name=Solution Description;
                CaptionML=[DEU=L”sung;
                           ENU=Solution;
                           NLD=Oplossing];
                SourceExpr=GetNoOfGeneralTextLines(GetSolutionDescriptionID);
                OnDrillDown=BEGIN
                              DrillDownGeneralText(GetSolutionDescriptionID);
                            END;
                             }

    { 1100528706;1;Field  ;
                Name=Comments;
                CaptionML=[DEU=Bemerkungen;
                           ENU=Comments;
                           NLD=Opmerkingen];
                SourceExpr=GetNoOfGeneralTextLines(GetCommentsID);
                OnDrillDown=BEGIN
                              DrillDownGeneralText(GetCommentsID);
                            END;
                             }

    { 1100528705;1;Field  ;
                Name=Pinpoints;
                CaptionML=[DEU=Zeichnungen;
                           ENU=Pinpoints;
                           NLD=Pinpoints];
                SourceExpr=GetNoOfPinpoints;
                OnDrillDown=BEGIN
                              DrillDownPinpoints;
                            END;
                             }

    { 1100528701;1;Field  ;
                Name=Before Photos;
                CaptionML=[DEU=Fotos nachher;
                           ENU=Before Photos;
                           NLD=Foto's voor];
                SourceExpr=GetNoOfBeforePhotos;
                OnDrillDown=BEGIN
                              DrillDownBeforePhotos;
                            END;
                             }

    { 1100528702;1;Field  ;
                Name=After Photos;
                CaptionML=[DEU=Fotos vorher;
                           ENU=After Photos;
                           NLD=Foto's na];
                SourceExpr=GetNoOfAfterPhotos;
                OnDrillDown=BEGIN
                              DrillDownAfterPhotos;
                            END;
                             }

    { 1100528704;1;Field  ;
                Name=Checklist Copies;
                CaptionML=[DEU=Checklistenkopien;
                           ENU=Checklist Copies;
                           NLD=Checklijstexemplaren];
                SourceExpr=GetNoOfChecklistCopies;
                OnDrillDown=BEGIN
                              DrillDownChecklistCopies;
                            END;
                             }

  }
  CODE
  {

    LOCAL PROCEDURE GetNoOfGeneralTextLines@1100528706(ID@1100528701 : Integer) : Integer;
    VAR
      GeneralText@1100528700 : Record 11020260;
    BEGIN
      IF "No." = '' THEN EXIT(0);
      GeneralText.SETRANGE(Table, GeneralText.Table::GenIssue);
      GeneralText.SETRANGE(Code, "No.");
      GeneralText.SETRANGE("Table Line No.", ID);
      EXIT(GeneralText.COUNT);
    END;

    LOCAL PROCEDURE DrillDownGeneralText@1100528701(ID@1100528701 : Integer);
    VAR
      GeneralText@1100528700 : Record 11020260;
    BEGIN
      IF "No." = '' THEN EXIT;
      GeneralText.SETRANGE(Table, GeneralText.Table::GenIssue);
      GeneralText.SETRANGE(Code, "No.");
      GeneralText.SETRANGE("Table Line No.", ID);
      PAGE.RUNMODAL(0, GeneralText);
    END;

    LOCAL PROCEDURE GetNoOfPinpoints@1100528713() : Integer;
    VAR
      DocumentUsage@1100528700 : Record 11072450;
    BEGIN
      DocumentUsage.SETRANGE("Linked To", RECORDID);
      DocumentUsage.SETRANGE(Usage, DocumentUsage.Usage::Point);
      EXIT(DocumentUsage.COUNT);
    END;

    LOCAL PROCEDURE GetNoOfBeforePhotos@1100528700() : Integer;
    VAR
      DocumentUsage@1100528700 : Record 11072450;
    BEGIN
      EXIT(GetNoOfPhotos(DocumentUsage.Usage::BeforePhoto));
    END;

    LOCAL PROCEDURE GetNoOfAfterPhotos@1100528702() : Integer;
    VAR
      DocumentUsage@1100528700 : Record 11072450;
    BEGIN
      EXIT(GetNoOfPhotos(DocumentUsage.Usage::AfterPhoto));
    END;

    LOCAL PROCEDURE GetNoOfPhotos@1100528705(Usage@1100528702 : Option) NoOfPhotos : Integer;
    VAR
      DocumentUsage@1100528700 : Record 11072450;
      DocumentLink@1100528701 : Record 11012747;
    BEGIN
      DocumentUsage.SETRANGE("Linked To", RECORDID);
      DocumentUsage.SETRANGE(Usage, Usage);
      IF DocumentUsage.FINDSET THEN
        REPEAT
          DocumentLink.SETRANGE("Document No.", DocumentUsage."Document No.");
          DocumentLink.SETRANGE("Record ID", DocumentUsage."Linked To");
          DocumentLink.SETRANGE("Internal Company", COMPANYNAME);
          IF NOT DocumentLink.ISEMPTY THEN
            NoOfPhotos += 1;
        UNTIL DocumentUsage.NEXT = 0;
    END;

    LOCAL PROCEDURE DrillDownPinpoints@1100528712();
    VAR
      DocumentUsage@1100528700 : Record 11072450;
    BEGIN
      DrillDownDocumentUsage(DocumentUsage.Usage::Point);
    END;

    LOCAL PROCEDURE DrillDownBeforePhotos@1100528709();
    VAR
      DocumentUsage@1100528700 : Record 11072450;
    BEGIN
      DrillDownDocumentUsage(DocumentUsage.Usage::BeforePhoto);
    END;

    LOCAL PROCEDURE DrillDownAfterPhotos@1100528710();
    VAR
      DocumentUsage@1100528700 : Record 11072450;
    BEGIN
      DrillDownDocumentUsage(DocumentUsage.Usage::AfterPhoto);
    END;

    LOCAL PROCEDURE DrillDownDocumentUsage@1100528707(Usage@1100528700 : Option);
    VAR
      DocumentUsage@1100528701 : Record 11072450;
    BEGIN
      DocumentUsage.FILTERGROUP(4);
      DocumentUsage.SETRANGE("Linked To", RECORDID);
      DocumentUsage.SETRANGE(Usage, Usage);
      DocumentUsage.FILTERGROUP(0);
      PAGE.RUNMODAL(0, DocumentUsage);
    END;

    LOCAL PROCEDURE GetNoOfChecklistCopies@1100528708() : Integer;
    VAR
      GenChecklistCopyLink@1100528700 : Record 11125904;
    BEGIN
      GenChecklistCopyLink.SETRANGE("Linked To", RECORDID);
      EXIT(GenChecklistCopyLink.COUNT);
    END;

    LOCAL PROCEDURE DrillDownChecklistCopies@1100528711();
    VAR
      GenChecklistCopy@1100528700 : Record 11125902;
      GenIssueLink@1100528701 : Record 11125908;
      Job@1100528702 : Record 11072003;
      RecRef@1100528703 : RecordRef;
    BEGIN
      GenChecklistCopy.FILTERGROUP(4);
      GenChecklistCopy.SETRANGE("Linked To Filter", RECORDID);
      GenChecklistCopy.SETRANGE("Linked To Exists", TRUE);
      GenIssueLink.SETRANGE("Issue No.", "No.");
      GenIssueLink.SETRANGE("Table No.", DATABASE::Job);
      IF GenIssueLink.FINDFIRST THEN
        GenChecklistCopy.SETRANGE("Allowed Checklists Of Filter", GenIssueLink."Linked To");
      GenChecklistCopy.FILTERGROUP(0);
      PAGE.RUN(0, GenChecklistCopy);
    END;

    LOCAL PROCEDURE GetDetailedDescriptionID@1100528703() : Integer;
    BEGIN
      EXIT(10);
    END;

    LOCAL PROCEDURE GetSolutionDescriptionID@1100528704() : Integer;
    BEGIN
      EXIT(20);
    END;

    LOCAL PROCEDURE GetCommentsID@1100528714() : Integer;
    BEGIN
      EXIT(30);
    END;

    BEGIN
    END.
  }
}

