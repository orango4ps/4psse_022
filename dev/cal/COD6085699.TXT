OBJECT Codeunit 6085699 URL Management
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW17.10.00.5.00.07;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE GetApprovalHyperlink@6085576(_UserId@6085574 : Code[50]) : Text[1024];
    VAR
      DCSetup@6085578 : Record 6085573;
      ContiniaCompanySetup@6085576 : Record 6192773;
      ContiniaWebPortal@6085575 : Record 6192774;
      ContiniaUserSetup@6085573 : Record 6086002;
    BEGIN
      DCSetup.GET;
      ContiniaCompanySetup.GET;
      ContiniaCompanySetup.TESTFIELD("Company Code");

      IF ContiniaUserSetup.GET(_UserId) THEN BEGIN
        CASE ContiniaUserSetup."Approval Client" OF
          ContiniaUserSetup."Approval Client"::" ": EXIT(GetNAVClientUrl(CLIENTTYPE::Windows));
          ContiniaUserSetup."Approval Client"::"NAV Client": EXIT(GetNAVClientUrl(CLIENTTYPE::Windows));
          ContiniaUserSetup."Approval Client"::"NAV Web Client": EXIT(GetNAVClientUrl(CLIENTTYPE::Web));
          ContiniaUserSetup."Approval Client"::"Continia Web Portal":
            BEGIN
              ContiniaCompanySetup.TESTFIELD("Web Portal Code");
              ContiniaWebPortal.GET(ContiniaCompanySetup."Web Portal Code");
              EXIT(STRSUBSTNO('%1/%2/purchase/approval',ContiniaWebPortal."Web Site Url",LOWERCASE(ContiniaCompanySetup."Company Code")))
            END;
        END;
      END ELSE
        EXIT(GetNAVClientUrl(CLIENTTYPE::Windows));
    END;

    LOCAL PROCEDURE GetNAVClientUrl@6085592(_ClientType@6085575 : ClientType) : Text[1024];
    VAR
      DCSetup@6085574 : Record 6085573;
      Url@6085573 : Text[1024];
    BEGIN
      DCSetup.GET;

      CASE _ClientType OF
        CLIENTTYPE::Windows:
          BEGIN
            IF DCSetup."Dynamics NAV Server Name" <> '' THEN BEGIN
              DCSetup.TESTFIELD("Dynamics NAV Server Instance");
              DCSetup.TESTFIELD("Dynamics NAV Server Port");

              Url := STRSUBSTNO('DynamicsNAV://%1:%2/%3/%4/runpage?page=%5',DCSetup."Dynamics NAV Server Name",
                DCSetup."Dynamics NAV Server Port",DCSetup."Dynamics NAV Server Instance",UrlEncode(COMPANYNAME),6085710);

              IF DCSetup."Dynamics NAV Server Tenant" <> '' THEN
                EXIT(Url + '&tenant=' + DCSetup."Dynamics NAV Server Tenant")
              ELSE
                EXIT(Url);
            END ELSE
              EXIT(GETURL(CLIENTTYPE::Windows,COMPANYNAME,OBJECTTYPE::Page,6085710));
          END;

        CLIENTTYPE::Web:
          BEGIN
            IF DCSetup."Dynamics NAV Web Server Name" <> '' THEN BEGIN
              DCSetup.TESTFIELD("Dynamics NAV Web Server Inst.");
              DCSetup.TESTFIELD("Dynamics NAV Web Server Port");

              Url := STRSUBSTNO('http://%1:%2/%3/WebClient?company=%4&page=%5',DCSetup."Dynamics NAV Web Server Name",
                DCSetup."Dynamics NAV Web Server Port",DCSetup."Dynamics NAV Web Server Inst.",UrlEncode(COMPANYNAME),6085710);

              IF DCSetup."Dynamics NAV Web Server Tenant" <> '' THEN
                EXIT(Url + '&tenant=' + DCSetup."Dynamics NAV Web Server Tenant")
              ELSE
                EXIT(Url);
            END ELSE
              EXIT(GETURL(CLIENTTYPE::Web,COMPANYNAME,OBJECTTYPE::Page,6085710));
          END;
      END;
    END;

    PROCEDURE GetSOAPUrl@6085573() : Text[1024];
    BEGIN
      EXIT(GETURL(CLIENTTYPE::SOAP));
    END;

    PROCEDURE UrlEncode@6085574(Text@6085573 : Text[1024]) : Text[1024];
    VAR
      HttpUtility@6085574 : DotNet "'System.Web, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b03f5f7f11d50a3a'.System.Web.HttpUtility";
    BEGIN
      EXIT(HttpUtility.UrlPathEncode(Text));
    END;

    BEGIN
    END.
  }
}

