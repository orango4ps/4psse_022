OBJECT Codeunit 5982 Service-Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,NAVDK.NO;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1002 : TextConst 'ENU=&Ship,&Invoice,Ship &and Invoice,Ship and &Consume;NOR=&Lever,&Fakturer,Lever &og fakturer,L&ever og forbruk;SVE=&Leverera,&Fakturera,Leverera &och fakturera,Leverera och f&”rbruka';
      Text001@1001 : TextConst 'ENU=Do you want to post and print the %1?;NOR=Vil du bokf›re og skrive ut %1?;SVE=Vill du bokf”ra och skriva ut %1?';
      ServiceHeader@1003 : Record 5900;
      ServShptHeader@1004 : Record 5990;
      ServInvHeader@1005 : Record 5992;
      ServCrMemoHeader@1006 : Record 5994;
      ServicePost@1009 : Codeunit 5980;
      OIOXMLExportServiceInvoice@1060000 : Codeunit 11128586;
      OIOXMLExportServiceCrMemo@1060001 : Codeunit 11128587;
      Selection@1010 : Integer;
      Ship@1011 : Boolean;
      Consume@1007 : Boolean;
      Invoice@1000 : Boolean;

    [External]
    PROCEDURE PostDocument@5(VAR Rec@1000 : Record 5900);
    VAR
      DummyServLine@1001 : TEMPORARY Record 5902;
    BEGIN
      ServiceHeader.COPY(Rec);
      Code(DummyServLine);
      Rec := ServiceHeader;
    END;

    LOCAL PROCEDURE Code@2(VAR PassedServLine@1002 : Record 5902);
    VAR
      ConfirmManagement@1003 : Codeunit 27;
      HideDialog@1000 : Boolean;
      IsHandled@1001 : Boolean;
    BEGIN
      HideDialog := FALSE;
      IsHandled := FALSE;
      OnBeforeConfirmPost(ServiceHeader,HideDialog,Ship,Consume,Invoice,IsHandled);
      IF IsHandled THEN
        EXIT;

      WITH ServiceHeader DO BEGIN
        CASE "Document Type" OF
          "Document Type"::Order:
            BEGIN
              Selection := STRMENU(Text000,3);
              IF Selection = 0 THEN
                EXIT;
              Ship := Selection IN [1,3,4];
              Consume := Selection IN [4];
              Invoice := Selection IN [2,3];
            END
          ELSE
            IF NOT ConfirmManagement.ConfirmProcess(STRSUBSTNO(Text001,"Document Type"),TRUE) THEN
              EXIT;
        END;

        OnAfterConfirmPost(ServiceHeader,Ship,Consume,Invoice);

        ServicePost.PostWithLines(ServiceHeader,PassedServLine,Ship,Consume,Invoice);
        OnAfterPost(ServiceHeader);

        GetReport(ServiceHeader);
        COMMIT;
      END;
    END;

    LOCAL PROCEDURE GetReport@3(VAR ServiceHeader@1000 : Record 5900);
    VAR
      EInvoiceExportServiceInvoice@1101100000 : Codeunit 11128511;
      EInvoiceExportServiceCrMemo@1101100001 : Codeunit 11128512;
      GeneralLedgerSetup@1100285500 : Record 98;
    BEGIN
      GeneralLedgerSetup.GET;
      WITH ServiceHeader DO
        CASE "Document Type" OF
          "Document Type"::Order:
            BEGIN
              IF Ship THEN BEGIN
                ServShptHeader."No." := "Last Shipping No.";
                ServShptHeader.SETRECFILTER;
                ServShptHeader.PrintRecords(FALSE);
              END;
              IF Invoice THEN BEGIN
                ServInvHeader."No." := "Last Posting No.";
                ServInvHeader.SETRECFILTER;
                ServInvHeader.PrintRecords(FALSE);

                //NAVDK
                IF GeneralLedgerSetup."Danish Localization Active" AND ("EAN No." <> '') THEN
                  IF ServInvHeader.FIND('=') THEN
                    OIOXMLExportServiceInvoice.RUN(ServInvHeader);
                //NAVNO
                IF GeneralLedgerSetup."Norwegian Localization Active" AND "E-Invoice" THEN
                  IF ServInvHeader.FIND('=') THEN
                    EInvoiceExportServiceInvoice.ExportServInvoice(ServInvHeader);
              END;
            END;
          "Document Type"::Invoice:
            BEGIN
              IF "Last Posting No." = '' THEN
                ServInvHeader."No." := "No."
              ELSE
                ServInvHeader."No." := "Last Posting No.";
              ServInvHeader.SETRECFILTER;
              ServInvHeader.PrintRecords(FALSE);

              //NAVDK
              IF GeneralLedgerSetup."Danish Localization Active" AND ("EAN No." <> '') THEN
                IF ServInvHeader.FIND('=') THEN
                  OIOXMLExportServiceInvoice.RUN(ServInvHeader);
              //NAVNO
              IF GeneralLedgerSetup."Norwegian Localization Active" AND "E-Invoice" THEN
                IF ServInvHeader.FIND('=') THEN
                  EInvoiceExportServiceInvoice.ExportServInvoice(ServInvHeader);
            END;
          "Document Type"::"Credit Memo":
            BEGIN
              IF "Last Posting No." = '' THEN
                ServCrMemoHeader."No." := "No."
              ELSE
                ServCrMemoHeader."No." := "Last Posting No.";
              ServCrMemoHeader.SETRECFILTER;
              ServCrMemoHeader.PrintRecords(FALSE);
              //NAVDK
              IF GeneralLedgerSetup."Danish Localization Active" AND ("EAN No." <> '') THEN
                IF ServCrMemoHeader.FIND('=') THEN
                  OIOXMLExportServiceCrMemo.RUN(ServCrMemoHeader);
              //NAVNO
              IF GeneralLedgerSetup."Norwegian Localization Active" AND "E-Invoice" THEN
                IF ServCrMemoHeader.FIND('=') THEN
                  EInvoiceExportServiceCrMemo.RUN(ServCrMemoHeader);
            END;
        END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterConfirmPost@1(ServiceHeader@1000 : Record 5900;Ship@1001 : Boolean;Consume@1002 : Boolean;Invoice@1003 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPost@6(VAR ServiceHeader@1000 : Record 5900);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeConfirmPost@4(VAR ServiceHeader@1003 : Record 5900;VAR HideDialog@1004 : Boolean;VAR Ship@1002 : Boolean;VAR Consume@1001 : Boolean;VAR Invoice@1000 : Boolean;VAR IsHandled@1005 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

