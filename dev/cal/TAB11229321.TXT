OBJECT Table 11229321 Handling Unit
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               FillHandlingUnitCode;
             END;

    OnDelete=VAR
               PurchRcptLineHandUnit@1100525000 : Record 11229322;
             BEGIN
               PurchRcptLineHandUnit.SETRANGE("Handling Unit Code","No.");
               PurchRcptLineHandUnit.DELETEALL;
             END;

    CaptionML=ENU=Handling Unit;
    LookupPageID=Page11126645;
  }
  FIELDS
  {
    { 10  ;   ;No.                 ;Code20        ;CaptionML=[ENU=No.;
                                                              NOR=Nr;
                                                              SVE=No.];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 20  ;   ;External Number     ;Code20        ;CaptionML=ENU=External Number }
    { 30  ;   ;Unit                ;Code20        ;TableRelation="Unit of Measure";
                                                   CaptionML=[ENU=Unit;
                                                              NOR=Enhet;
                                                              SVE=Unit] }
    { 40  ;   ;Length              ;Decimal       ;OnValidate=BEGIN
                                                                CalcVolume;
                                                              END;

                                                   CaptionML=[ENU=Length;
                                                              NOR=Lengde;
                                                              SVE=L„ngd];
                                                   MinValue=0 }
    { 50  ;   ;Width               ;Decimal       ;OnValidate=BEGIN
                                                                CalcVolume;
                                                              END;

                                                   CaptionML=[ENU=Width;
                                                              NOR=Bredde;
                                                              SVE=Bredd];
                                                   MinValue=0 }
    { 60  ;   ;Height              ;Decimal       ;OnValidate=BEGIN
                                                                CalcVolume;
                                                              END;

                                                   CaptionML=[ENU=Height;
                                                              NOR=H›yde;
                                                              SVE=H”jd];
                                                   MinValue=0 }
    { 70  ;   ;Weight              ;Decimal       ;CaptionML=[ENU=Weight;
                                                              NOR=Vekt;
                                                              SVE=Vikt];
                                                   MinValue=0 }
    { 80  ;   ;Volume              ;Decimal       ;CaptionML=[ENU=Volume;
                                                              NOR=Volum;
                                                              SVE=Volym];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PurchaseSetup@1100525000 : Record 312;
      NoSeriesMgt@1100525001 : Codeunit 396;

    PROCEDURE FillHandlingUnitCode@1100525001();
    VAR
      NoSeries@1100525000 : Code[10];
    BEGIN
      IF "No." <> '' THEN
        EXIT;
      PurchaseSetup.GET;
      PurchaseSetup.TESTFIELD("Handling Unit Nos.");
      NoSeriesMgt.InitSeries(PurchaseSetup."Handling Unit Nos.",'',0D,"No.",NoSeries);
    END;

    PROCEDURE InsertPurchRcptLineHandUnit@1100525000(VAR PurchRcptLineLoc@1100525000 : Record 121);
    VAR
      PurchRcptLineHandUnit@1100525001 : Record 11229322;
    BEGIN
      IF PurchRcptLineLoc.ISEMPTY THEN
        EXIT;
      PurchRcptLineHandUnit.SETRANGE("Handling Unit Code","No.");
      PurchRcptLineLoc.FINDSET;
      REPEAT
        PurchRcptLineHandUnit.SETRANGE("Purch. Rcpt. No.",PurchRcptLineLoc."Document No.");
        PurchRcptLineHandUnit.SETRANGE("Purch. Rcpt. Line No.",PurchRcptLineLoc."Line No.");
        IF PurchRcptLineHandUnit.ISEMPTY THEN BEGIN
          PurchRcptLineHandUnit.INIT;
          PurchRcptLineHandUnit."Purch. Rcpt. No." := PurchRcptLineLoc."Document No.";
          PurchRcptLineHandUnit.VALIDATE("Purch. Rcpt. Line No.",PurchRcptLineLoc."Line No.");
          PurchRcptLineHandUnit."Handling Unit Code" := "No.";
          PurchRcptLineHandUnit.INSERT;
        END;
      UNTIL PurchRcptLineLoc.NEXT = 0;
    END;

    LOCAL PROCEDURE CalcVolume@1100525002();
    BEGIN
      Volume := Length * Width * Height;
    END;

    BEGIN
    {
      4PS 06-06-16 UKR-C21923 KD: Record weight and dimensions on receipt (BI004) - Object created
    }
    END.
  }
}

