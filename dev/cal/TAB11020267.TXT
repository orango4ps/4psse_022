OBJECT Table 11020267 Reporting Filter Header
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               "Table No." := DATABASE::Job;
             END;

    OnDelete=BEGIN
               ReportingFilterFieldsRec.SETRANGE("Filter Code", Code);
               ReportingFilterFieldsRec.DELETEALL;
             END;

    CaptionML=[ENU=Reporting Filter Header;
               NOR=Rapporteringsfilter, Tittel;
               SVE=Rapporteringsfilter, rubrik];
    LookupPageID=Page11020323;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 3   ;   ;Table No.           ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   CaptionML=[ENU=Table No.;
                                                              NOR=Tabellnr;
                                                              SVE=Tabellnr] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ReportingFilterFieldsRec@1100525000 : Record 11020268;

    PROCEDURE SaveFilter@1100485000(iRecordRef@1100485000 : RecordRef;iReportFilterRec@1100485004 : Record 11020267);
    VAR
      lFieldRec@1100485002 : Record 2000000041;
      lFieldRef@1100485001 : FieldRef;
      lReportingFilterFields@1100485003 : Record 11020268;
    BEGIN
      lReportingFilterFields.RESET;
      lReportingFilterFields.SETRANGE("Filter Code",iReportFilterRec.Code);
      lReportingFilterFields.DELETEALL;

      lFieldRec.SETRANGE(TableNo,iRecordRef.NUMBER);
      IF lFieldRec.FIND('-') THEN
        REPEAT
          lFieldRef := iRecordRef.FIELD(lFieldRec."No.");
          IF lFieldRef.GETFILTER <> '' THEN BEGIN
            lReportingFilterFields."Filter Code" := iReportFilterRec.Code;
            lReportingFilterFields."Field No." := lFieldRec."No.";
            lReportingFilterFields.Filter := lFieldRef.GETFILTER;
            lReportingFilterFields.INSERT;
          END;
        UNTIL lFieldRec.NEXT = 0;
    END;

    PROCEDURE SetFilter@1100485001(VAR vRecordRef@1100485001 : RecordRef;iReportFilterRec@1100485000 : Record 11020267);
    VAR
      lFieldRef@1100485003 : FieldRef;
      lReportingFilterFields@1100485002 : Record 11020268;
    BEGIN
      vRecordRef.RESET;

      lReportingFilterFields.SETRANGE("Filter Code",iReportFilterRec.Code);
      IF lReportingFilterFields.FIND('-') THEN
        REPEAT
          lFieldRef := vRecordRef.FIELD(lReportingFilterFields."Field No.");
          lFieldRef.SETFILTER(lReportingFilterFields.Filter);
        UNTIL lReportingFilterFields.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

