OBJECT Report 11125722 Shift Requested Act. Cap.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Angefragte Akt.-Kapazit„t verschieben;
               ENU=Shift Requested Act. Cap.;
               NLD=Gevraagde act. capaciteit verschuiven;
               NOR=Skift anmodet faktisk kapasitet;
               SVE=Skifta beg„rd †tg. Kap.];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   EVALUATE(TimePeriod, '0D');
                 END;

  }
  DATASET
  {
    { 6269;    ;DataItem;                    ;
               DataItemTable=Table11125708;
               DataItemTableView=SORTING(Project No.,Activity Code,Line No.);
               OnAfterGetRecord=VAR
                                  JobsSetup@1100528401 : Record 315;
                                  NewStartingDate@1100528404 : Date;
                                  NewEndingDate@1100528403 : Date;
                                BEGIN
                                  JobsSetup.GET;
                                  ShiftRequestedActCapacity("Requested Activity Capacity", NewStartingDate, NewEndingDate);
                                  IF JobsSetup."Shift Ass. Cap. with Req Cap." THEN
                                    ShiftLinkedAssActCapacities("Requested Activity Capacity", NewStartingDate, NewEndingDate);
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525001;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100525002;2;Field  ;
                  CaptionML=[DEU=Zeitperiode;
                             ENU=Time Period;
                             NLD=Tijdsperiode;
                             NOR=Tidsperiode;
                             SVE=Tidsperiod];
                  SourceExpr=TimePeriod }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TimePeriod@1100528400 : DateFormula;

    LOCAL PROCEDURE ShiftRequestedActCapacity@1100528401(RequestedActivityCapacity@1100528401 : Record 11125708;VAR NewStartingDate@1100528402 : Date;VAR NewEndingDate@1100528403 : Date);
    VAR
      ReqActCapMgt@1100528400 : Codeunit 11125707;
    BEGIN
      RequestedActivityCapacity."Starting Date" :=
        CALCDATE(TimePeriod, RequestedActivityCapacity."Starting Date");
      RequestedActivityCapacity."Ending Date" :=
        ReqActCapMgt.CalculateEndingDate(RequestedActivityCapacity);
      RequestedActivityCapacity.MODIFY(TRUE);
      NewStartingDate := RequestedActivityCapacity."Starting Date";
      NewEndingDate := RequestedActivityCapacity."Ending Date";
    END;

    LOCAL PROCEDURE ShiftLinkedAssActCapacities@1100528400(OrigRequestedActivityCapacity@1100528403 : Record 11125708;NewStartingDate@1100528401 : Date;NewEndingDate@1100528402 : Date);
    VAR
      AssignedActivityCapacity@1100528400 : Record 11125710;
    BEGIN
      AssignedActivityCapacity.SETRANGE("Project No.", OrigRequestedActivityCapacity."Project No.");
      AssignedActivityCapacity.SETRANGE("Activity Code", OrigRequestedActivityCapacity."Activity Code");
      AssignedActivityCapacity.SETRANGE("Line No. Request", OrigRequestedActivityCapacity."Line No.");
      AssignedActivityCapacity.SETRANGE("Starting Date", OrigRequestedActivityCapacity."Starting Date");
      AssignedActivityCapacity.SETRANGE("Ending Date", OrigRequestedActivityCapacity."Ending Date");
      IF AssignedActivityCapacity.FINDSET THEN
        REPEAT
          ShiftAssignedActCapacity(AssignedActivityCapacity, NewStartingDate, NewEndingDate);
        UNTIL AssignedActivityCapacity.NEXT = 0;
    END;

    LOCAL PROCEDURE ShiftAssignedActCapacity@1100528407(AssignedActivityCapacity@1100528400 : Record 11125710;NewStartingDate@1100528402 : Date;NewEndingDate@1100528401 : Date);
    BEGIN
      AssignedActivityCapacity."Starting Date" := NewStartingDate;
      AssignedActivityCapacity."Ending Date" := NewEndingDate;
      AssignedActivityCapacity.MODIFY(TRUE);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

