OBJECT Table 11072682 Plant Inspection Version
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CheckKeyFieldsLevel;
             END;

    OnDelete=VAR
               PlantInspectionLine@1100528600 : Record 11072684;
               CancelledPlantInspectionLine@1100528601 : Record 11072683;
             BEGIN
               PlantInspectionLine.SETRANGE(Level, Level);
               PlantInspectionLine.SETRANGE("Plant Type", "Plant Type");
               PlantInspectionLine.SETRANGE("Plant No.", "Plant No.");
               PlantInspectionLine.SETRANGE("Maintenance Scheme", "Maintenance Scheme");
               PlantInspectionLine.SETRANGE("Base Inspection List", "Base Inspection List");
               PlantInspectionLine.SETRANGE("Index Date", "Index Date");
               PlantInspectionLine.DELETEALL(TRUE);

               CancelledPlantInspectionLine.SETRANGE(Level, Level);
               CancelledPlantInspectionLine.SETRANGE("Plant Type", "Plant Type");
               CancelledPlantInspectionLine.SETRANGE("Plant No.", "Plant No.");
               CancelledPlantInspectionLine.SETRANGE("Maintenance Scheme", "Maintenance Scheme");
               CancelledPlantInspectionLine.SETRANGE("Base Inspection List", "Base Inspection List");
               CancelledPlantInspectionLine.SETRANGE("Index Date", "Index Date");
               CancelledPlantInspectionLine.DELETEALL(TRUE);
             END;

    OnRename=VAR
               PlantInspectionLine@1210190001 : Record 11072684;
               PlantInspectionLine2@1210190000 : Record 11072684;
               CancelledPlantInspLine@1100529000 : Record 11072683;
               CancelledPlantInspLine2@1100529001 : Record 11072683;
             BEGIN
               TESTFIELD(Level, xRec.Level);
               TESTFIELD("Plant Type", xRec."Plant Type");
               TESTFIELD("Plant No.", xRec."Plant No.");
               TESTFIELD("Maintenance Scheme", xRec."Maintenance Scheme");
               TESTFIELD("Base Inspection List", xRec."Base Inspection List");
               CheckKeyFieldsLevel;
               IF ("Index Date" <> 0D) AND ("Index Date" <> xRec."Index Date") THEN BEGIN
                 PlantInspectionLine.SETRANGE(Level, Rec.Level);
                 PlantInspectionLine.SETRANGE("Plant Type", Rec."Plant Type");
                 PlantInspectionLine.SETRANGE("Plant No.", Rec."Plant No.");
                 PlantInspectionLine.SETRANGE("Maintenance Scheme", Rec."Maintenance Scheme");
                 PlantInspectionLine.SETRANGE("Base Inspection List", Rec."Base Inspection List");
                 PlantInspectionLine.SETRANGE("Index Date", xRec."Index Date");
                 IF PlantInspectionLine.FINDSET THEN BEGIN
                   REPEAT
                     CancelledPlantInspLine.RESET;
                     CancelledPlantInspLine.SETCURRENTKEY(
                       Level,"Plant Type","Plant No.","Maintenance Scheme","Base Inspection List","Index Date",
                       "Level (Cancelled)","Plant Type (Cancelled)","Maintenance Scheme (Cancelled)","Base Insp. List (Cancelled)","Line No. (Cancelled)");
                     CancelledPlantInspLine.SETRANGE(Level, PlantInspectionLine.Level);
                     CancelledPlantInspLine.SETRANGE("Plant Type", PlantInspectionLine."Plant Type");
                     CancelledPlantInspLine.SETRANGE("Maintenance Scheme", PlantInspectionLine."Maintenance Scheme");
                     CancelledPlantInspLine.SETRANGE("Base Inspection List", PlantInspectionLine."Base Inspection List");
                     CancelledPlantInspLine.SETRANGE("Index Date", PlantInspectionLine."Index Date");
                     IF CancelledPlantInspLine.FINDFIRST THEN BEGIN
                       CancelledPlantInspLine2 := CancelledPlantInspLine;
                       CancelledPlantInspLine2."Index Date" := "Index Date";
                       CancelledPlantInspLine2.INSERT;
                       CancelledPlantInspLine.DELETE;
                     END;
                     PlantInspectionLine2 := PlantInspectionLine;
                     PlantInspectionLine2."Index Date" := "Index Date";
                     PlantInspectionLine2.INSERT(TRUE);
                     PlantInspectionLine.DELETE(TRUE);
                   UNTIL PlantInspectionLine.NEXT = 0;
                 END;
               END;
             END;

    CaptionML=ENU=Plant Inspection Version;
    LookupPageID=Page11229866;
    DrillDownPageID=Page11229866;
  }
  FIELDS
  {
    { 10  ;   ;Level               ;Option        ;CaptionML=[ENU=Level;
                                                              NOR=Niv†;
                                                              SVE=Niv†];
                                                   OptionCaptionML=ENU=Base Inspection List,Plant Type,Plant Number;
                                                   OptionString=Base Inspection List,Plant Type,Plant Number;
                                                   Editable=No }
    { 15  ;   ;Plant Type          ;Code20        ;TableRelation="Plant Type".Code;
                                                   CaptionML=[ENU=Plant Type;
                                                              NOR=Maskintype;
                                                              SVE=Maskintyp];
                                                   Editable=No }
    { 20  ;   ;Plant No.           ;Code20        ;TableRelation="Plant Number".No. WHERE (Plant Type=FIELD(Plant Type));
                                                   CaptionML=[ENU=Plant No.;
                                                              NOR=Niv†kode;
                                                              SVE=Niv†kod];
                                                   Editable=No }
    { 25  ;   ;Maintenance Scheme  ;Code20        ;TableRelation="Plant Stnd. Maintenance Scheme".Code;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Maintenance Scheme Description");
                                                              END;

                                                   CaptionML=[ENU=Maintenance Scheme;
                                                              NOR=Vedlikeholdsskjema;
                                                              SVE=Underh†llsschema];
                                                   Editable=No }
    { 27  ;   ;Maintenance Scheme Description;Text50;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Stnd. Maintenance Scheme".Description WHERE (Code=FIELD(Maintenance Scheme)));
                                                   CaptionML=[ENU=Maintenance Scheme Description;
                                                              NOR=Beskrivelse av vedlikeholdskjema;
                                                              SVE=Beskrivning av underh†llsschema];
                                                   Editable=No }
    { 30  ;   ;Index Date          ;Date          ;CaptionML=[ENU=Index Date;
                                                              NOR=Indeksdato;
                                                              SVE=Indexdatum];
                                                   NotBlank=Yes }
    { 40  ;   ;Base Inspection List;Code20        ;TableRelation="Base Plant Inspection List".Code;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Inspection List Description");
                                                              END;

                                                   CaptionML=[ENU=Base Inspection List;
                                                              NOR=Basisinspeksjonsliste;
                                                              SVE=Basinspektionslista];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 41  ;   ;Inspection List Description;Text50 ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Base Plant Inspection List".Description WHERE (Code=FIELD(Base Inspection List)));
                                                   CaptionML=[ENU=Inspection List;
                                                              SVE=Inspektionslista];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Level,Plant Type,Plant No.,Maintenance Scheme,Base Inspection List,Index Date;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    LOCAL PROCEDURE CheckKeyFieldsLevel@1100525001();
    BEGIN
      CASE Level OF
        Level::"Base Inspection List":
          BEGIN
            TESTFIELD("Plant Type", '');
            TESTFIELD("Plant No.", '');
            TESTFIELD("Maintenance Scheme", '');
          END;
        Level::"Plant Type":
          BEGIN
            TESTFIELD("Plant Type");
            TESTFIELD("Plant No.", '');
            TESTFIELD("Maintenance Scheme");
          END;
        Level::"Plant Number":
          BEGIN
            TESTFIELD("Plant Type");
            TESTFIELD("Plant No.");
            TESTFIELD("Maintenance Scheme");
          END;
      END;
      TESTFIELD("Base Inspection List");
      TESTFIELD("Index Date");
    END;

    PROCEDURE NewerVersionExists@1100528600() : Boolean;
    VAR
      PlantInspectionVersion@1100528600 : Record 11072682;
    BEGIN
      PlantInspectionVersion := Rec;
      PlantInspectionVersion.SETRECFILTER;
      PlantInspectionVersion.SETFILTER("Index Date", '>%1', "Index Date");
      EXIT(NOT PlantInspectionVersion.ISEMPTY);
    END;

    PROCEDURE GetPlantDescription@1100528803() : Text;
    VAR
      PlantType@1100528802 : Record 11012551;
      PlantNumber@1100528801 : Record 11012552;
    BEGIN
      CASE Level OF
        Level::"Plant Type":
          IF PlantType.GET("Plant Type") THEN
            EXIT(PlantType.Description);
        Level::"Plant Number":
          IF PlantNumber.GET("Plant Type", "Plant No.") THEN
            EXIT(PlantNumber.Description);
      END;
      EXIT('');
    END;

    BEGIN
    END.
  }
}

