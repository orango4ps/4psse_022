OBJECT Table 11012592 Exit Order Bulk Plant
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               GetOrderHeader;
               OrderRec.TESTFIELD(Posted,FALSE);
             END;

    OnModify=BEGIN
               GetOrderHeader;
               OrderRec.TESTFIELD(Posted,FALSE);
             END;

    OnDelete=BEGIN
               GetOrderHeader;
               OrderRec.TESTFIELD(Posted,FALSE);
             END;

    OnRename=BEGIN
               GetOrderHeader;
               OrderRec.TESTFIELD(Posted,FALSE);
             END;

    CaptionML=[ENU=Exit Order Bulk Plant;
               NOR=Sluttordre bulkMaskin;
               SVE=Slutorder bulkMaskin];
  }
  FIELDS
  {
    { 10  ;   ;Exit-order No.      ;Code20        ;TableRelation="Exit Order".No.;
                                                   CaptionML=[ENU=Exit Order No.;
                                                              NOR=Sluttordrenr;
                                                              SVE=Slutordernr] }
    { 20  ;   ;Exit-order Line No. ;Integer       ;TableRelation="Exit Order Line"."Line No." WHERE (Exit-order No.=FIELD(Exit-order No.));
                                                   CaptionML=[ENU=Exit Order Line No.;
                                                              NOR=Sluttordre, Radnr.;
                                                              SVE=Slutorder, radnr] }
    { 30  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 40  ;   ;Plant Type          ;Code20        ;TableRelation="Plant Type";
                                                   CaptionML=[ENU=Plant Type;
                                                              NOR=Maskintype;
                                                              SVE=Maskintyp] }
    { 45  ;   ;External            ;Boolean       ;CaptionML=[ENU=External;
                                                              NOR=Eksternt;
                                                              SVE=Externt];
                                                   Description=Must be filled with 'External' on Plant Type level (only for external with "No." of Plant No.) }
    { 50  ;   ;Plant No.           ;Code10        ;TableRelation=IF (External=CONST(No)) "Plant Number".No. WHERE (Plant Type=FIELD(Plant Type),
                                                                                                                   Posted=CONST(Yes),
                                                                                                                   Fixed Asset=FILTER(<>''),
                                                                                                                   Stock No.=FILTER(0))
                                                                                                                   ELSE IF (External=CONST(Yes)) "Plant Number".No. WHERE (Plant Type=FIELD(Plant Type),
                                                                                                                                                                           External=CONST(Yes),
                                                                                                                                                                           Posted=CONST(Yes),
                                                                                                                                                                           Stock No.=FILTER(0));
                                                   OnValidate=BEGIN
                                                                IF PlantNoUsedOnOtherLine() THEN
                                                                  ERROR(Text000, FIELDCAPTION("Plant No."), "Plant No.");

                                                                CALCFIELDS(Description, "Fixed Asset");

                                                                IF "Plant No." <> xRec."Plant No." THEN BEGIN
                                                                  IF (NOT External) THEN
                                                                    MaxQuantity := GetFixedAssetQuantity() - GetNotPostedQuanOtherExitLines()
                                                                  ELSE
                                                                    MaxQuantity := GetExternalRentQuantity();
                                                                  IF (Quantity > MaxQuantity) THEN
                                                                    Quantity := MaxQuantity;
                                                                END;
                                                                IF (Quantity < 0) THEN
                                                                  Quantity := 0;
                                                              END;

                                                   CaptionML=[ENU=Plant No.;
                                                              NOR=Maskinnr.;
                                                              SVE=Maskinnr] }
    { 51  ;   ;Description         ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Number".Description WHERE (Plant Type=FIELD(Plant Type),
                                                                                                        No.=FIELD(Plant No.)));
                                                   CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning];
                                                   Editable=No }
    { 52  ;   ;Fixed Asset         ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Number"."Fixed Asset" WHERE (Plant Type=FIELD(Plant Type),
                                                                                                          No.=FIELD(Plant No.)));
                                                   TableRelation="Fixed Asset";
                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Fixed Asset;
                                                              NOR=Driftsmiddel;
                                                              SVE=Anl„ggningstillg†ngar];
                                                   Editable=No }
    { 60  ;   ;Quantity            ;Decimal       ;OnValidate=VAR
                                                                lvQuan@1100485000 : Decimal;
                                                              BEGIN
                                                                IF (Quantity = 0) THEN
                                                                  EXIT;

                                                                IF (NOT External) THEN BEGIN
                                                                  MaxQuantity := GetFixedAssetQuantity();
                                                                  IF (Quantity > MaxQuantity) THEN
                                                                    FIELDERROR(Quantity, STRSUBSTNO(Text001, Quantity, MaxQuantity));
                                                                  lvQuan := GetNotPostedQuanOtherExitLines();
                                                                  IF (Quantity > MaxQuantity-lvQuan) THEN
                                                                    ERROR(Text003, Quantity+lvQuan, MaxQuantity, lvQuan, OtherOrderLinesTxt);
                                                                END ELSE BEGIN
                                                                  MaxQuantity := GetExternalRentQuantity();
                                                                  IF (Quantity > MaxQuantity) THEN
                                                                    FIELDERROR(Quantity, STRSUBSTNO(Text002, Quantity, MaxQuantity));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 70  ;   ;Total Quantity      ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Exit Order Bulk Plant".Quantity WHERE (Exit-order No.=FIELD(Exit-order No.),
                                                                                                           Exit-order Line No.=FIELD(Exit-order Line No.)));
                                                   CaptionML=[ENU=Total Quantity;
                                                              NOR=Totalt antall;
                                                              SVE=Totalt antal];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 80  ;   ;Disposed Fixed Asset;Code20        ;TableRelation="Fixed Asset";
                                                   CaptionML=[ENU=Disposed Fixed Asset;
                                                              NOR=Avviklede anleggsmidler;
                                                              SVE=Avyttrad anl„ggningstillg†ng] }
    { 90  ;   ;Vendor No.          ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Exit Order Line"."Vendor No." WHERE (Exit-order No.=FIELD(Exit-order No.),
                                                                                                            Line No.=FIELD(Exit-order Line No.)));
                                                   CaptionML=[ENU=Vendor No.;
                                                              NOR=Leverand›rnr.;
                                                              SVE=Leverant”rsnr];
                                                   Editable=No }
    { 100 ;   ;Exit-date           ;Date          ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Exit Order Line".Exit-date WHERE (Exit-order No.=FIELD(Exit-order No.),
                                                                                                         Line No.=FIELD(Exit-order Line No.)));
                                                   CaptionML=[ENU=Exit-date;
                                                              NOR=Sluttdato;
                                                              SVE=Avsluta-datum];
                                                   Editable=No }
    { 110 ;   ;Posted              ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Exit Order".Posted WHERE (No.=FIELD(Exit-order No.)));
                                                   CaptionML=[ENU=Posted;
                                                              NOR=Bokf›rt;
                                                              SVE=Bokf”rd];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Exit-order No.,Exit-order Line No.,Line No.;
                                                   SumIndexFields=Quantity;
                                                   MaintainSIFTIndex=No;
                                                   Clustered=Yes }
    {    ;Plant Type,Plant No.                     }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100485002 : TextConst 'ENU=%1 ''%2'' is already present on another line.;NOR=%1 ''%2'' finnes allerede p† en annen rad.;SVE=%1 ''%2'' finns redan p† en annan rad.';
      Text001@11012009 : TextConst 'ENU=(%1) may not be greater then FA-Quantity (%2 );NOR=(%1) kan ikke v‘re st›rre enn Driftsmidler-antall (%2);SVE=(%1) f†r inte vara st”rre „n FA-antal (%2)';
      PlantSetupRec@11012001 : Record 11012550;
      OrderRec@11012002 : Record 11012559;
      OrderLineRec@11012003 : Record 11012560;
      PlantTypeRec@1100485003 : Record 11012551;
      FixedAssetRec@1100485000 : Record 5600;
      MaxQuantity@1100485001 : Decimal;
      Text002@1100485005 : TextConst 'ENU=(%1) may not be greater then external rented quantity (%2 );NOR=(%1) kan ikke v‘re st›rre enn eksternt leid antall (%2);SVE=(%1) f†r inte vara st”rre „n externt hyrt antal (%2)';
      Text003@1100485006 : TextConst 'ENU=Total Quantity (%1) may not be greater then FA-Quantity (%2 ), already %3 of Plant No.present on other Exit Line(s): %4.;NOR=Totalt antall (%1) kan ikke v‘re st›rre enn anleggsmiddelantall (%2 ). %3 av maskinnr. finnes allerede p† en annen/andre Sluttrad(er): %4.;SVE=Totalt antal (%1) kan inte vara st”rre „n FA-antal (%2 ). %3 av Maskinnr finns redan p† en annan/andra avsluta-rad(er): %4.';
      OtherOrderLinesTxt@1100485007 : Text[1024];

    PROCEDURE InitRecord@2();
    BEGIN
      IF OrderLineRec.GET("Exit-order No.", "Exit-order Line No.") THEN BEGIN
        "Plant Type" := OrderLineRec."Plant Type";
        PlantTypeRec.GET("Plant Type");
        External := PlantTypeRec.External;
      END;
    END;

    PROCEDURE GetOrderHeader@5();
    BEGIN
      TESTFIELD("Exit-order No.");
      IF ("Exit-order No." <> OrderRec."No.") THEN
        OrderRec.GET("Exit-order No.");
    END;

    PROCEDURE PlantNoUsedOnOtherLine@1100485002() : Boolean;
    VAR
      lvExitBulkPlantRec@1100485000 : Record 11012592;
    BEGIN
      lvExitBulkPlantRec.SETRANGE("Exit-order No.", "Exit-order No.");
      lvExitBulkPlantRec.SETRANGE("Exit-order Line No.", "Exit-order Line No.");
      lvExitBulkPlantRec.SETFILTER("Line No.", '<>%1', "Line No.");
      lvExitBulkPlantRec.SETRANGE("Plant No.", "Plant No.");
      IF lvExitBulkPlantRec.FIND('-') THEN
        EXIT(TRUE);

      EXIT(FALSE);
    END;

    PROCEDURE LookupFixedAsset@1100485001(IEditable@1100485000 : Boolean);
    BEGIN
      PlantSetupRec.GET;

      FixedAssetRec.RESET;
      IF PlantSetupRec."Fixed FA Nos." THEN
        FixedAssetRec.SETFILTER("No.", '%1', "Plant Type" + '-*');
      FixedAssetRec.SETRANGE("Plant Type", "Plant Type");
      FixedAssetRec.SETRANGE(Inactive, FALSE);
      FixedAssetRec."Plant Type" := "Plant Type";
      FixedAssetRec."No." := "Fixed Asset";
      IF PAGE.RUNMODAL(0, FixedAssetRec) = ACTION::LookupOK THEN BEGIN
        IF IEditable AND (FixedAssetRec."No." <> '') THEN BEGIN
          FixedAssetRec.CALCFIELDS("Plant Type", "Plant No.");
          IF (FixedAssetRec."Plant Type" = "Plant Type") THEN
            VALIDATE("Plant No.", FixedAssetRec."Plant No.");
        END;
      END;
    END;

    PROCEDURE GetFixedAssetQuantity@1100485000() : Decimal;
    VAR
      lvQuantity@1100485000 : Decimal;
    BEGIN
      lvQuantity := 0;
      IF ("Fixed Asset" <> '') THEN BEGIN
        IF FixedAssetRec.GET("Fixed Asset") THEN
          lvQuantity := FixedAssetRec.GetQuantity('', FALSE); //ps 13795, 29-12-08 arg changed from TRUE to FALSE
      END;

      EXIT(lvQuantity);
    END;

    PROCEDURE GetFADepreciationBookData@1100485003(VAR OFADeprBookRec@1100485000 : Record 5612);
    VAR
      DeprBookCode@1100530000 : Code[10];
    BEGIN
      DeprBookCode := FixedAssetRec.GetFA_MainDeprBook("Fixed Asset",TRUE);
      //IF DeprBookCode = '' THEN BEGIN
      //  FASetupRec.GET;
      //  DeprBookCode := FASetupRec."Default Depr. Book";
      //END;
      IF NOT OFADeprBookRec.GET("Fixed Asset", DeprBookCode) THEN
        OFADeprBookRec.INIT
      ELSE BEGIN
        OFADeprBookRec.CALCFIELDS("Book Value");
      END;
    END;

    PROCEDURE GetExternalRentQuantity@1100485004() : Decimal;
    VAR
      lvRentEntryRec@1100485000 : Record 11012535;
    BEGIN
      CALCFIELDS("Vendor No.", "Exit-date");
      lvRentEntryRec.SETRANGE(Removed, FALSE);
      lvRentEntryRec.SETRANGE("Vendor No.", "Vendor No.");
      lvRentEntryRec.SETRANGE("Plant Type", "Plant Type");
      lvRentEntryRec.SETRANGE("Plant No.", "Plant No.");
      IF NOT lvRentEntryRec.FIND('-') THEN
        EXIT(0);

      lvRentEntryRec.SETFILTER("Date Filter", '..%1', "Exit-date");
      lvRentEntryRec.CALCFIELDS("Cum. Qty. Vendor-Plant No.");
      EXIT(lvRentEntryRec."Cum. Qty. Vendor-Plant No.");
    END;

    PROCEDURE GetNotPostedQuanOtherExitLines@1100485005() : Decimal;
    VAR
      lvExitBulkPlantRec@1100485000 : Record 11012592;
      lvQuantity@1100485002 : Decimal;
    BEGIN
      OtherOrderLinesTxt := '';
      IF ("Plant Type" = '') OR ("Plant No." = '') THEN
        EXIT(0);

      lvExitBulkPlantRec.SETCURRENTKEY("Plant Type", "Plant No.");
      lvExitBulkPlantRec.SETRANGE("Plant Type", "Plant Type");
      lvExitBulkPlantRec.SETRANGE("Plant No.", "Plant No.");
      lvExitBulkPlantRec.SETRANGE(Posted, FALSE);
      lvExitBulkPlantRec.SETFILTER(Quantity, '>%1', 0);
      IF NOT lvExitBulkPlantRec.FIND('-') THEN
        EXIT(0);

      lvQuantity := 0;
      REPEAT
        IF (lvExitBulkPlantRec."Exit-order No." <> "Exit-order No.") OR
           (lvExitBulkPlantRec."Exit-order Line No." <> "Exit-order Line No.") OR
           (lvExitBulkPlantRec."Line No." <> "Line No.")
        THEN BEGIN
          lvQuantity := lvQuantity +lvExitBulkPlantRec.Quantity;
          IF OtherOrderLinesTxt = '' THEN
            OtherOrderLinesTxt := lvExitBulkPlantRec."Exit-order No." + '-' + FORMAT(lvExitBulkPlantRec."Exit-order Line No.")
          ELSE BEGIN
            IF STRLEN(OtherOrderLinesTxt) < 950 THEN
              OtherOrderLinesTxt := OtherOrderLinesTxt + ' ,' +
                lvExitBulkPlantRec."Exit-order No." + '-' + FORMAT(lvExitBulkPlantRec."Exit-order Line No.")
          END;
        END;
      UNTIL lvExitBulkPlantRec.NEXT = 0;

      EXIT(lvQuantity);
    END;

    BEGIN
    END.
  }
}

