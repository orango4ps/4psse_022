OBJECT Codeunit 12057070 ExFlow Component Mgt
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF500000;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      EXF001@1100285000 : TextConst 'ENU=Checklist to get add-ins to work: 1. Register add-in under Control Add-Ins. Make sure manifest is uploaded (if applicable). 2. Put DLL file in the service tier/client add-in folder. Make sure the file is not blocked and that the server/client has full access.;SVE=Checklista f”r att f† ig†ng ExFlow till„gg: 1. Registrera till„gget under Kontrolltill„gg. Ladda upp manifestfilen (om till„mpligt). 3. Kopiera DLL filen till service/klient add-in katalogen. S„kerst„ll att filen inte „r blockerad och att servern/klienten har access.';

    PROCEDURE SetPDF@1100285000(ExternalPrimaryKey@1100285000 : Code[50];PDFImage@1100285001 : BigText);
    VAR
      ExBlob@1100285002 : Record 12013591;
      Bytes@1100285005 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Array";
      Convert@1100285006 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Convert";
      MemoryStream@1100285007 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.MemoryStream";
      OutStream@1100285008 : OutStream;
      recExfPurchImpHeader@1100285010 : Record 12013648;
      recExfPurchImpLine@1100285009 : Record 12013649;
      recExfPurchImpLine2@1100285011 : Record 12013649;
      TempLineNo@1100285012 : Integer;
    BEGIN
      ExBlob.INIT;
      ExBlob."No." := 0;
      ExBlob."File Extension" := 'PDF';
      ExBlob."External Primary Key" := ExternalPrimaryKey;

      Bytes := Convert.FromBase64String(PDFImage);
      MemoryStream := MemoryStream.MemoryStream(Bytes);
      ExBlob.BLOB.CREATEOUTSTREAM(OutStream);
      MemoryStream.WriteTo(OutStream);

      ExBlob.INSERT(TRUE);

      // Fix since line does not have all the fields at the time of OnInsert in the page.
      recExfPurchImpHeader.RESET;
      recExfPurchImpHeader.SETRANGE("External Primary Key", ExternalPrimaryKey);
      IF recExfPurchImpHeader.FIND('-') THEN BEGIN
        recExfPurchImpHeader."Image Buffer ID" := ExBlob."No.";
        recExfPurchImpHeader.MODIFY;

        recExfPurchImpLine.RESET;
        recExfPurchImpLine.SETRANGE("Import Document No.", 0);
        recExfPurchImpLine.SETRANGE("External Primary Key", recExfPurchImpHeader."External Primary Key");
        IF recExfPurchImpLine.FINDSET(TRUE,TRUE) THEN BEGIN
          recExfPurchImpLine2.SETRANGE("Import Document No.", recExfPurchImpHeader."Import Document No.");
          IF recExfPurchImpLine2.FINDLAST THEN
            TempLineNo := recExfPurchImpLine2."Line No." + 10000
          ELSE
            TempLineNo := 10000;

          REPEAT
            recExfPurchImpLine2.INIT;
            recExfPurchImpLine2.TRANSFERFIELDS(recExfPurchImpLine);
            recExfPurchImpLine2."Import Document No." := recExfPurchImpHeader."Import Document No.";
            recExfPurchImpLine2."Line No." := TempLineNo;
            recExfPurchImpLine2.INSERT;
            TempLineNo += 10000;

            recExfPurchImpLine2.GET(recExfPurchImpLine."Import Document No.",recExfPurchImpLine."Line No.");
            recExfPurchImpLine2.DELETE;
          UNTIL recExfPurchImpLine.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE InitExFlowAddins@1100285009();
    BEGIN
      InitRTCPDFPrevWeb;
    END;

    LOCAL PROCEDURE InitRTCPDFPrevWeb@1100285007();
    BEGIN
      InitControlAddIn('SignUpPdfControl','436e8aeec51b873c','ExFlow PDF Viewer');
    END;

    LOCAL PROCEDURE InitControlAddIn@1100285002(ControlAddInName@1100285001 : Text;KeyToken@1100285002 : Text;Desc@1100285003 : Text);
    VAR
      ClientAddin@1100285000 : Record 2000000069;
    BEGIN
      IF NOT ClientAddin.GET(ControlAddInName,KeyToken) THEN BEGIN
        ClientAddin.INIT;
        ClientAddin."Add-in Name" := ControlAddInName;
        ClientAddin."Public Key Token" := KeyToken;
        ClientAddin.Description := Desc;
        ClientAddin.INSERT;
      END;
    END;

    PROCEDURE DownloadExFlowComponents@1100285004();
    VAR
      URL@1100285001 : Text[1024];
    BEGIN
      URL := 'http://exflownavaddins.signup.se/download/ExFlowNAVAddIns.zip';
      HYPERLINK(URL);
    END;

    BEGIN
    END.
  }
}

