OBJECT Table 1293 Payment Application Proposal
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               UpdateSortingOrder;
             END;

    OnModify=BEGIN
               UpdateSortingOrder;
             END;

    OnDelete=BEGIN
               TESTFIELD("Applies-to Entry No.",0);
               IF Applied THEN
                 Unapply;
             END;

    OnRename=BEGIN
               VerifyLineIsNotApplied;
             END;

    CaptionML=[ENU=Payment Application Proposal;
               NOR=Betalingsutligningsforslag;
               SVE=Fîrslag fîr betalningskoppling];
  }
  FIELDS
  {
    { 1   ;   ;Bank Account No.    ;Code20        ;TableRelation="Bank Account";
                                                   CaptionML=[ENU=Bank Account No.;
                                                              NOR=Bankkontonr.;
                                                              SVE=Bankkontonr] }
    { 2   ;   ;Statement No.       ;Code20        ;TableRelation="Bank Acc. Reconciliation"."Statement No." WHERE (Bank Account No.=FIELD(Bank Account No.),
                                                                                                                   Statement Type=FIELD(Statement Type));
                                                   CaptionML=[ENU=Statement No.;
                                                              NOR=Utdragsnr.;
                                                              SVE=Kontoutdragsnr] }
    { 3   ;   ;Statement Line No.  ;Integer       ;CaptionML=[ENU=Statement Line No.;
                                                              NOR=Utdragslinjenr.;
                                                              SVE=Kontoutdragsradnr] }
    { 20  ;   ;Statement Type      ;Option        ;CaptionML=[ENU=Statement Type;
                                                              NOR=Utdragstype;
                                                              SVE=Kontoutdragets typ];
                                                   OptionCaptionML=[ENU=Bank Reconciliation,Payment Application;
                                                                    NOR=Bankavstemming,Betalingsutligning;
                                                                    SVE=BankavstÑmning,Betalningskoppling];
                                                   OptionString=Bank Reconciliation,Payment Application }
    { 21  ;   ;Account Type        ;Option        ;OnValidate=BEGIN
                                                                VerifyLineIsNotApplied;
                                                              END;

                                                   CaptionML=[ENU=Account Type;
                                                              NOR=Kontotype;
                                                              SVE=Kontotyp];
                                                   OptionCaptionML=[ENU=G/L Account,Customer,Vendor,Bank Account,Fixed Asset,IC Partner;
                                                                    NOR=Finans,Kunde,Leverandõr,Bank,Aktiva,KI-partner;
                                                                    SVE=Redov.konto,Kund,Leverantîr,Bankkonto,AnlÑggningstillgÜng,Konc.int. partner];
                                                   OptionString=G/L Account,Customer,Vendor,Bank Account,Fixed Asset,IC Partner }
    { 22  ;   ;Account No.         ;Code20        ;TableRelation=IF (Account Type=CONST(G/L Account)) "G/L Account" WHERE (Account Type=CONST(Posting),
                                                                                                                           Blocked=CONST(No))
                                                                                                                           ELSE IF (Account Type=CONST(Customer)) Customer
                                                                                                                           ELSE IF (Account Type=CONST(Vendor)) Vendor
                                                                                                                           ELSE IF (Account Type=CONST(Bank Account)) "Bank Account"
                                                                                                                           ELSE IF (Account Type=CONST(Fixed Asset)) "Fixed Asset"
                                                                                                                           ELSE IF (Account Type=CONST(IC Partner)) "IC Partner";
                                                   OnValidate=BEGIN
                                                                VerifyLineIsNotApplied;
                                                              END;

                                                   CaptionML=[ENU=Account No.;
                                                              NOR=Kontonr.;
                                                              SVE=Kontonr] }
    { 23  ;   ;Applies-to Entry No.;Integer       ;TableRelation=IF (Account Type=CONST(G/L Account)) "G/L Entry"
                                                                 ELSE IF (Account Type=CONST(Customer)) "Cust. Ledger Entry" WHERE (Open=CONST(Yes))
                                                                 ELSE IF (Account Type=CONST(Vendor)) "Vendor Ledger Entry" WHERE (Open=CONST(Yes))
                                                                 ELSE IF (Account Type=CONST(Bank Account)) "Bank Account Ledger Entry" WHERE (Open=CONST(Yes));
                                                   CaptionML=[ENU=Applies-to Entry No.;
                                                              NOR=Utligningspostnr.;
                                                              SVE=Kopplas till lîpnr] }
    { 24  ;   ;Applied Amount      ;Decimal       ;OnValidate=BEGIN
                                                                IF ("Applied Amount" = 0) AND (xRec."Applied Amount" <> 0) THEN
                                                                  Unapply
                                                                ELSE
                                                                  UpdateAppliedAmt;
                                                              END;

                                                   CaptionML=[ENU=Applied Amount;
                                                              NOR=Utlignet belõp;
                                                              SVE=Kopplat belopp] }
    { 25  ;   ;Applied             ;Boolean       ;OnValidate=VAR
                                                                BankAccReconLine@1000 : Record 274;
                                                              BEGIN
                                                                IF xRec.Applied = Applied THEN
                                                                  EXIT;

                                                                IF NOT Applied THEN
                                                                  Unapply;

                                                                IF Applied THEN BEGIN
                                                                  IF "Document Type" = "Document Type"::"Credit Memo" THEN
                                                                    CrMemoSelectedToApply
                                                                  ELSE BEGIN
                                                                    BankAccReconLine.GET("Statement Type","Bank Account No.","Statement No.","Statement Line No.");
                                                                    IF BankAccReconLine.Difference = 0 THEN
                                                                      ERROR(PaymentAppliedErr);
                                                                  END;

                                                                  Apply(GetRemainingAmountAfterPosting,"Applies-to Entry No." <> 0);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Applied;
                                                              NOR=Utlignet;
                                                              SVE=Kopplat] }
    { 29  ;   ;Applied Pmt. Discount;Decimal      ;CaptionML=[ENU=Applied Pmt. Discount;
                                                              NOR=Utlignet kontantrabatt;
                                                              SVE=Kopplad kassarabatt];
                                                   AutoFormatExpr="Currency Code" }
    { 30  ;   ;Quality             ;Integer       ;CaptionML=[ENU=Quality;
                                                              NOR=Kvalitet;
                                                              SVE=Kvalitet] }
    { 31  ;   ;Posting Date        ;Date          ;CaptionML=[ENU=Posting Date;
                                                              NOR=Bokfõringsdato;
                                                              SVE=Bokfîringsdatum] }
    { 32  ;   ;Document Type       ;Option        ;CaptionML=[ENU=Document Type;
                                                              NOR=Bilagstype;
                                                              SVE=Dokumenttyp];
                                                   OptionCaptionML=[ENU=" ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund";
                                                                    NOR=" ,Betaling,Faktura,Kreditnota,Rentenota,Purring,Refusjon";
                                                                    SVE=" ,Betalning,Faktura,Kreditnota,RÑntefaktura,BetalningspÜminnelse,èterbetalning"];
                                                   OptionString=[ ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund] }
    { 33  ;   ;Document No.        ;Code20        ;CaptionML=[ENU=Document No.;
                                                              NOR=Bilagsnr.;
                                                              SVE=Dokumentnr] }
    { 34  ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 35  ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=[ENU=Currency Code;
                                                              NOR=Valutakode;
                                                              SVE=Valutakod];
                                                   Editable=No }
    { 36  ;   ;Due Date            ;Date          ;CaptionML=[ENU=Due Date;
                                                              NOR=Forfallsdato;
                                                              SVE=Fîrfallodatum];
                                                   Editable=No }
    { 37  ;   ;External Document No.;Code35       ;CaptionML=[ENU=External Document No.;
                                                              NOR=Eksterndokumentnr.;
                                                              SVE=Externt dokumentnr] }
    { 50  ;   ;Match Confidence    ;Option        ;InitValue=None;
                                                   CaptionML=[ENU=Match Confidence;
                                                              NOR=Konfidensintervall;
                                                              SVE=MatchningssÑkerhet];
                                                   OptionCaptionML=[ENU=None,Low,Medium,High,High - Text-to-Account Mapping,Manual,Accepted;
                                                                    NOR=Ingen,Lav,Middels,Hõy,Hõy - Tekst-til-konto-tilordning,Manuell,Godtatt;
                                                                    SVE=Ingen,LÜg,Medium,Hîg,Hîgˇ- mappa text till konto,Manuell,Accepterad];
                                                   OptionString=None,Low,Medium,High,High - Text-to-Account Mapping,Manual,Accepted;
                                                   Editable=No }
    { 51  ;   ;Pmt. Disc. Due Date ;Date          ;OnValidate=BEGIN
                                                                ChangeDiscountAmounts;
                                                              END;

                                                   CaptionML=[ENU=Pmt. Disc. Due Date;
                                                              NOR=Forfallsdato for kontantrabatt;
                                                              SVE=Fîrfallodatum fîr kassarabatt] }
    { 52  ;   ;Remaining Pmt. Disc. Possible;Decimal;
                                                   OnValidate=BEGIN
                                                                ChangeDiscountAmounts;
                                                              END;

                                                   CaptionML=[ENU=Remaining Pmt. Disc. Possible;
                                                              NOR=Mulig resterende kont.rab.;
                                                              SVE=èterstÜende kassarabatt mîjlig] }
    { 53  ;   ;Pmt. Disc. Tolerance Date;Date     ;OnValidate=BEGIN
                                                                ChangeDiscountAmounts;
                                                              END;

                                                   CaptionML=[ENU=Pmt. Disc. Tolerance Date;
                                                              NOR=Dato for kontantrabattoleranse;
                                                              SVE=Datum fîr kassarabattolerans] }
    { 60  ;   ;Applied Amt. Incl. Discount;Decimal;OnValidate=BEGIN
                                                                IF ("Applied Amt. Incl. Discount" = 0) AND Applied THEN
                                                                  Unapply
                                                                ELSE
                                                                  VALIDATE("Applied Amount","Applied Amt. Incl. Discount");
                                                              END;

                                                   CaptionML=[ENU=Applied Amt. Incl. Discount;
                                                              NOR=Utlignet belõp inkl. rabatt;
                                                              SVE=Kopplat belopp inkl. rabatt] }
    { 61  ;   ;Remaining Amount    ;Decimal       ;CaptionML=[ENU=Remaining Amount;
                                                              NOR=Restbelõp;
                                                              SVE=èterstÜende belopp];
                                                   Editable=No }
    { 62  ;   ;Remaining Amt. Incl. Discount;Decimal;
                                                   CaptionML=[ENU=Remaining Amt. Incl. Discount;
                                                              NOR=Restbelõp inkl. rabatt;
                                                              SVE=èterstÜende belopp inkl. rabatt];
                                                   Editable=No }
    { 63  ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Type;
                                                              SVE=Typ];
                                                   OptionCaptionML=[ENU=Bank Account Ledger Entry,Check Ledger Entry;
                                                                    NOR=Bankkontopost,Sjekkpost;
                                                                    SVE=Bankkontotransaktion,Checktransaktion];
                                                   OptionString=Bank Account Ledger Entry,Check Ledger Entry }
    { 100 ;   ;Sorting Order       ;Integer       ;CaptionML=[ENU=Sorting Order;
                                                              NOR=Sorteringsrekkefõlge;
                                                              SVE=Sorteringsordning];
                                                   Editable=No }
    { 101 ;   ;Stmt To Rem. Amount Difference;Decimal;
                                                   CaptionML=[ENU=Stmt To Rem. Amount Difference;
                                                              NOR=Belõpsdifferanse mellom utdrag og rest;
                                                              SVE=Diff. transaktionsbelopp och Üterst. belopp];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Statement Type,Bank Account No.,Statement No.,Statement Line No.,Account Type,Account No.,Applies-to Entry No.;
                                                   Clustered=Yes }
    {    ;Applied,Quality                          }
    {    ;Sorting Order                            }
    {    ;Applied,Account Type,Account No.,Type    }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      StmtAmtIsFullyAppliedErr@1001 : TextConst 'ENU=The statement amount is already fully applied.;NOR=Kontoutdragsbelõpet er allerede fullstendig utlignet.;SVE=Transaktionsbeloppet har redan kopplats fullt ut.';
      EntryDoesntExistErr@1002 : TextConst 'ENU=The entry does not exist.;NOR=Posten finnes ikke.;SVE=Transaktionen finns inte.';
      CannotChangeAppliedLineErr@1000 : TextConst 'ENU=You cannot change the line because the entry is applied. Remove the applied entry first.;NOR=Du kan ikke endre linjen ettersom posten er utlignet. Fjern den utlignede posten fõrst.;SVE=Du kan inte Ñndra raden eftersom transaktionen har tillÑmpats. Ta bort den kopplade transaktionen fîrst.';
      TransactionDateIsBeforePostingDateMsg@1004 : TextConst '@@@="%1 Transaction Date; %2: Posting Date";ENU=The transaction date %1 is before the posting date %2.;NOR=Transaksjonsdatoen %1 er fõr bokfõringsdatoen %2.;SVE=Transaktionsdatumet %1 Ñr tidigare Ñn bokfîringsdatumet %2.';
      PaymentAppliedErr@1003 : TextConst 'ENU=The payment is fully applied. To apply the payment to this entry, you must first unapply the payment from another entry.;NOR=Betalingen er fullstendig utlignet. Hvis du vil utligne betalingen for denne posten, mÜ du oppheve utligning av betalingen fra en annen post.;SVE=Betalningen Ñr fullgjord. Innan du kan koppla betalningen till den hÑr transaktionen mÜste du ta bort kopplingen frÜn en annan transaktion.';
      WantToApplyCreditMemoAndInvoicesMsg@1005 : TextConst 'ENU=If you want to apply credit memos and invoices, we recommend that you start by applying credit memos and then apply all others entries.;NOR=Hvis du vil utligne kreditnotaer og fakturaer, anbefaler vi at du fõrst utligner kreditnotaene og deretter de andre postene.;SVE=Om du vill koppla kreditnotor och fakturor bîr du fîrst koppla kreditnotor, och sedan koppla îvriga transaktioner.';

    LOCAL PROCEDURE Unapply@7();
    VAR
      AppliedPmtEntry@1000 : Record 1294;
    BEGIN
      IF NOT GetAppliedPaymentEntry(AppliedPmtEntry) THEN
        ERROR(EntryDoesntExistErr);

      AppliedPmtEntry.DELETE(TRUE);

      TRANSFERFIELDS(AppliedPmtEntry,FALSE);
      Applied := FALSE;

      "Applied Amt. Incl. Discount" := 0;

      MODIFY(TRUE);
    END;

    LOCAL PROCEDURE UpdateAppliedAmt@1();
    VAR
      AmountToApply@1001 : Decimal;
    BEGIN
      AmountToApply := "Applied Amount";
      IF Applied THEN
        Unapply;

      IF AmountToApply = 0 THEN
        EXIT;

      Apply(AmountToApply,FALSE);
    END;

    [External]
    PROCEDURE GetAppliedPaymentEntry@3(VAR AppliedPaymentEntry@1000 : Record 1294) : Boolean;
    BEGIN
      EXIT(
        AppliedPaymentEntry.GET(
          "Statement Type","Bank Account No.",
          "Statement No.","Statement Line No.",
          "Account Type","Account No.","Applies-to Entry No."));
    END;

    LOCAL PROCEDURE GetLedgEntryInfo@8();
    VAR
      TempAppliedPmtEntry@1000 : TEMPORARY Record 1294;
    BEGIN
      TempAppliedPmtEntry.TRANSFERFIELDS(Rec);
      TempAppliedPmtEntry.GetLedgEntryInfo;
      TRANSFERFIELDS(TempAppliedPmtEntry);
    END;

    [External]
    PROCEDURE TransferFromBankAccReconLine@14(BankAccReconLine@1000 : Record 274);
    BEGIN
      "Statement Type" := BankAccReconLine."Statement Type";
      "Bank Account No." := BankAccReconLine."Bank Account No.";
      "Statement No." := BankAccReconLine."Statement No.";
      "Statement Line No." := BankAccReconLine."Statement Line No.";
    END;

    [External]
    PROCEDURE CreateFromAppliedPaymentEntry@2(AppliedPaymentEntry@1000 : Record 1294);
    VAR
      BankAccount@1001 : Record 270;
    BEGIN
      INIT;
      TRANSFERFIELDS(AppliedPaymentEntry);
      UpdatePaymentDiscInfo;

      IF AppliedPaymentEntry."Applied Amount" <> 0 THEN
        Applied := TRUE;

      BankAccount.GET(AppliedPaymentEntry."Bank Account No.");

      UpdatePaymentDiscInfo;
      UpdateRemainingAmount(BankAccount);
      UpdateRemainingAmountExclDiscount;
      UpdateTypeOption(AppliedPaymentEntry."Applies-to Entry No.");
      "Applied Amt. Incl. Discount" := "Applied Amount" - "Applied Pmt. Discount";
      INSERT(TRUE);
    END;

    [External]
    PROCEDURE CreateFromBankStmtMacthingBuffer@10(TempBankStmtMatchingBuffer@1001 : TEMPORARY Record 1250;BankAccReconciliationLine@1003 : Record 274;BankAccount@1002 : Record 270);
    VAR
      BankPmtApplRule@1000 : Record 1252;
    BEGIN
      INIT;
      "Account Type" := TempBankStmtMatchingBuffer."Account Type";
      "Account No." := TempBankStmtMatchingBuffer."Account No.";

      IF TempBankStmtMatchingBuffer."Entry No." < 0 THEN
        "Applies-to Entry No." := 0
      ELSE
        "Applies-to Entry No." := TempBankStmtMatchingBuffer."Entry No.";

      GetLedgEntryInfo;
      Quality := TempBankStmtMatchingBuffer.Quality;
      "Match Confidence" := BankPmtApplRule.GetMatchConfidence(TempBankStmtMatchingBuffer.Quality);

      UpdatePaymentDiscInfo;
      UpdateRemainingAmount(BankAccount);
      UpdateRemainingAmountExclDiscount;

      IF "Applies-to Entry No." > 0 THEN
        UpdateTypeOption("Applies-to Entry No.");

      "Stmt To Rem. Amount Difference" := ABS(BankAccReconciliationLine."Statement Amount" - "Remaining Amount");
      "Applied Amt. Incl. Discount" := "Applied Amount" - "Applied Pmt. Discount";
    END;

    LOCAL PROCEDURE UpdateSortingOrder@6();
    VAR
      BankPmtApplRule@1000 : Record 1252;
    BEGIN
      "Sorting Order" := -Quality;
      IF Applied THEN
        "Sorting Order" -= BankPmtApplRule.GetHighestPossibleScore;
    END;

    LOCAL PROCEDURE Apply@5(AmtToApply@1000 : Decimal;SuggestDiscAmt@1001 : Boolean);
    VAR
      AppliedPaymentEntry@1002 : Record 1294;
      BankAccReconciliationLine@1003 : Record 274;
      MatchBankPayments@1004 : Codeunit 1255;
    BEGIN
      AppliedPaymentEntry.TRANSFERFIELDS(Rec);
      BankAccReconciliationLine.GET(
        AppliedPaymentEntry."Statement Type",AppliedPaymentEntry."Bank Account No.",
        AppliedPaymentEntry."Statement No.",AppliedPaymentEntry."Statement Line No.");
      MatchBankPayments.SetApplicationDataInCVLedgEntry(
        "Account Type","Applies-to Entry No.",BankAccReconciliationLine.GetAppliesToID);

      IF AmtToApply = 0 THEN
        ERROR(StmtAmtIsFullyAppliedErr);

      IF SuggestDiscAmt THEN
        AppliedPaymentEntry.VALIDATE("Applies-to Entry No.")
      ELSE
        AppliedPaymentEntry.VALIDATE("Applied Amount",AmtToApply);

      AppliedPaymentEntry.INSERT(TRUE);

      TRANSFERFIELDS(AppliedPaymentEntry);
      Applied := TRUE;
      UpdateRemainingAmountExclDiscount;
      "Applied Amt. Incl. Discount" := "Applied Amount" - "Applied Pmt. Discount";
      MODIFY(TRUE);

      IF BankAccReconciliationLine."Transaction Date" < "Posting Date" THEN
        MESSAGE(STRSUBSTNO(TransactionDateIsBeforePostingDateMsg,BankAccReconciliationLine."Transaction Date","Posting Date"));
    END;

    [External]
    PROCEDURE GetRemainingAmountAfterPostingValue@23() : Decimal;
    BEGIN
      IF "Applies-to Entry No." = 0 THEN
        EXIT(0);

      EXIT(GetRemainingAmountAfterPosting);
    END;

    LOCAL PROCEDURE GetRemainingAmountAfterPosting@4() : Decimal;
    VAR
      TempAppliedPaymentEntry@1000 : TEMPORARY Record 1294;
    BEGIN
      TempAppliedPaymentEntry.TRANSFERFIELDS(Rec);
      EXIT(
        TempAppliedPaymentEntry.GetRemAmt -
        TempAppliedPaymentEntry."Applied Amount" -
        TempAppliedPaymentEntry.GetAmtAppliedToOtherStmtLines);
    END;

    [External]
    PROCEDURE RemoveApplications@9();
    VAR
      AppliedPaymentEntry@1000 : Record 1294;
      CurrentTempPaymentApplicationProposal@1001 : TEMPORARY Record 1293;
    BEGIN
      CurrentTempPaymentApplicationProposal := Rec;

      AddFilterOnAppliedPmtEntry(AppliedPaymentEntry);

      IF AppliedPaymentEntry.FINDSET THEN
        REPEAT
          GET(
            AppliedPaymentEntry."Statement Type",AppliedPaymentEntry."Bank Account No.",
            AppliedPaymentEntry."Statement No.",AppliedPaymentEntry."Statement Line No.",
            AppliedPaymentEntry."Account Type",AppliedPaymentEntry."Account No.",
            AppliedPaymentEntry."Applies-to Entry No.");
          Unapply;
        UNTIL AppliedPaymentEntry.NEXT = 0;

      Rec := CurrentTempPaymentApplicationProposal;
    END;

    [External]
    PROCEDURE AccountNameDrillDown@35();
    VAR
      Customer@1002 : Record 18;
      Vendor@1001 : Record 23;
      GLAccount@1000 : Record 15;
      BankAccount@1005 : Record 270;
      AccountType@1004 : Option;
      AccountNo@1003 : Code[20];
    BEGIN
      AccountType := GetAppliedToAccountType;
      AccountNo := GetAppliedToAccountNo;
      CASE AccountType OF
        "Account Type"::Customer:
          BEGIN
            Customer.GET(AccountNo);
            PAGE.RUN(PAGE::"Customer Card",Customer);
          END;
        "Account Type"::Vendor:
          BEGIN
            Vendor.GET(AccountNo);
            PAGE.RUN(PAGE::"Vendor Card",Vendor);
          END;
        "Account Type"::"G/L Account":
          BEGIN
            GLAccount.GET(AccountNo);
            PAGE.RUN(PAGE::"G/L Account Card",GLAccount);
          END;
        "Account Type"::"Bank Account":
          BEGIN
            BankAccount.GET(AccountNo);
            PAGE.RUN(PAGE::"Bank Account Card",BankAccount);
          END;
      END;
    END;

    [External]
    PROCEDURE GetAccountName@11() : Text;
    VAR
      Customer@1002 : Record 18;
      Vendor@1001 : Record 23;
      GLAccount@1000 : Record 15;
      BankAccount@1006 : Record 270;
      AccountType@1005 : Option;
      AccountNo@1004 : Code[20];
      Name@1003 : Text;
    BEGIN
      AccountType := GetAppliedToAccountType;
      AccountNo := GetAppliedToAccountNo;
      Name := '';

      CASE AccountType OF
        "Account Type"::Customer:
          IF Customer.GET(AccountNo) THEN
            Name := Customer.Name;
        "Account Type"::Vendor:
          IF Vendor.GET(AccountNo) THEN
            Name := Vendor.Name;
        "Account Type"::"G/L Account":
          IF GLAccount.GET(AccountNo) THEN
            Name := GLAccount.Name;
        "Account Type"::"Bank Account":
          IF BankAccount.GET(AccountNo) THEN
            Name := BankAccount.Name;
      END;

      EXIT(Name);
    END;

    LOCAL PROCEDURE GetAppliedToAccountType@36() : Integer;
    VAR
      BankAccountLedgerEntry@1003 : Record 271;
    BEGIN
      IF "Account Type" = "Account Type"::"Bank Account" THEN
        IF BankAccountLedgerEntry.GET("Applies-to Entry No.") THEN
          EXIT(BankAccountLedgerEntry."Bal. Account Type");
      EXIT("Account Type");
    END;

    LOCAL PROCEDURE GetAppliedToAccountNo@37() : Code[20];
    VAR
      BankAccountLedgerEntry@1004 : Record 271;
    BEGIN
      IF "Account Type" = "Account Type"::"Bank Account" THEN
        IF BankAccountLedgerEntry.GET("Applies-to Entry No.") THEN
          EXIT(BankAccountLedgerEntry."Bal. Account No.");
      EXIT("Account No.");
    END;

    LOCAL PROCEDURE ChangeDiscountAmounts@20();
    BEGIN
      UpdateLedgEntryDisc;
      UpdateRemainingAmountExclDiscount;

      IF "Applied Pmt. Discount" <> 0 THEN BEGIN
        "Applied Amount" -= "Applied Pmt. Discount";
        "Applied Pmt. Discount" := 0;
      END;

      UpdateAppliedAmt;
    END;

    LOCAL PROCEDURE UpdateLedgEntryDisc@42();
    VAR
      BankAccReconLine@1002 : Record 274;
      AppliedPmtEntry@1001 : Record 1294;
      BankAccReconPost@1000 : Codeunit 370;
    BEGIN
      TESTFIELD("Applies-to Entry No.");
      AppliedPmtEntry.TRANSFERFIELDS(Rec);
      BankAccReconLine.GET("Statement Type","Bank Account No.","Statement No.","Statement Line No.");

      CASE "Account Type" OF
        "Account Type"::Customer:
          BankAccReconPost.ApplyCustLedgEntry(
            AppliedPmtEntry,'',BankAccReconLine."Transaction Date",
            "Pmt. Disc. Due Date","Pmt. Disc. Tolerance Date","Remaining Pmt. Disc. Possible");
        "Account Type"::Vendor:
          BankAccReconPost.ApplyVendLedgEntry(
            AppliedPmtEntry,'',BankAccReconLine."Transaction Date",
            "Pmt. Disc. Due Date","Pmt. Disc. Tolerance Date","Remaining Pmt. Disc. Possible");
      END;
    END;

    LOCAL PROCEDURE UpdateRemainingAmountExclDiscount@12();
    VAR
      BankAccReconLine@1000 : Record 274;
    BEGIN
      "Remaining Amt. Incl. Discount" := "Remaining Amount";

      IF NOT ("Document Type" IN ["Document Type"::"Credit Memo","Document Type"::Invoice]) THEN
        EXIT;

      BankAccReconLine.GET("Statement Type","Bank Account No.","Statement No.","Statement Line No.");
      IF BankAccReconLine."Transaction Date" > "Pmt. Disc. Due Date" THEN
        EXIT;

      "Remaining Amt. Incl. Discount" -= "Remaining Pmt. Disc. Possible";
    END;

    LOCAL PROCEDURE UpdateRemainingAmount@15(BankAccount@1000 : Record 270);
    VAR
      CustLedgEntry@1001 : Record 21;
      VendLedgEntry@1002 : Record 25;
      BankAccLedgEntry@1003 : Record 271;
      RemainingAmount@1005 : Decimal;
      RemainingAmountLCY@1006 : Decimal;
    BEGIN
      "Remaining Amount" := 0;

      IF "Applies-to Entry No." = 0 THEN
        EXIT;

      CASE "Account Type" OF
        "Account Type"::"Bank Account":
          BEGIN
            BankAccLedgEntry.SETRANGE(Open,TRUE);
            BankAccLedgEntry.SETRANGE("Bank Account No.","Account No.");
            BankAccLedgEntry.GET("Applies-to Entry No.");
            "Remaining Amount" := BankAccLedgEntry."Remaining Amount";
            EXIT;
          END;
        "Account Type"::Customer:
          BEGIN
            CustLedgEntry.SETRANGE(Open,TRUE);
            CustLedgEntry.SETRANGE("Customer No.","Account No.");
            CustLedgEntry.SETAUTOCALCFIELDS("Remaining Amount","Remaining Amt. (LCY)");
            CustLedgEntry.GET("Applies-to Entry No.");

            RemainingAmount := CustLedgEntry."Remaining Amount";
            RemainingAmountLCY := CustLedgEntry."Remaining Amt. (LCY)";
          END;
        "Account Type"::Vendor:
          BEGIN
            VendLedgEntry.SETRANGE(Open,TRUE);
            VendLedgEntry.SETRANGE("Vendor No.","Account No.");
            VendLedgEntry.SETAUTOCALCFIELDS("Remaining Amount","Remaining Amt. (LCY)");
            VendLedgEntry.GET("Applies-to Entry No.");

            RemainingAmount := VendLedgEntry."Remaining Amount";
            RemainingAmountLCY := VendLedgEntry."Remaining Amt. (LCY)";
          END;
      END;

      IF BankAccount.IsInLocalCurrency THEN
        "Remaining Amount" := RemainingAmountLCY
      ELSE
        "Remaining Amount" := RemainingAmount;
    END;

    LOCAL PROCEDURE UpdatePaymentDiscInfo@13();
    VAR
      AppliedPaymentEntry@1000 : Record 1294;
    BEGIN
      AppliedPaymentEntry.TRANSFERFIELDS(Rec);
      AppliedPaymentEntry.GetDiscInfo("Pmt. Disc. Due Date","Pmt. Disc. Tolerance Date","Remaining Pmt. Disc. Possible");
      IF "Remaining Pmt. Disc. Possible" = 0 THEN BEGIN
        "Pmt. Disc. Due Date" := 0D;
        "Pmt. Disc. Tolerance Date" := 0D;
      END;
    END;

    LOCAL PROCEDURE VerifyLineIsNotApplied@16();
    BEGIN
      IF Applied THEN
        ERROR(CannotChangeAppliedLineErr);
    END;

    [External]
    PROCEDURE AppliesToEntryNoDrillDown@17();
    VAR
      CustLedgEntry@1005 : Record 21;
      VendLedgEntry@1004 : Record 25;
      BankAccLedgEntry@1003 : Record 271;
      GLEntry@1002 : Record 17;
    BEGIN
      IF "Applies-to Entry No." = 0 THEN
        EXIT;

      CASE "Account Type" OF
        "Account Type"::"G/L Account":
          BEGIN
            GLEntry.SETRANGE("G/L Account No.","Account No.");
            PAGE.RUNMODAL(0,GLEntry);
          END;
        "Account Type"::Customer:
          BEGIN
            CustLedgEntry.SETRANGE(Open,TRUE);
            CustLedgEntry.SETRANGE("Customer No.","Account No.");
            CustLedgEntry.GET("Applies-to Entry No.");
            PAGE.RUNMODAL(0,CustLedgEntry);
          END;
        "Account Type"::Vendor:
          BEGIN
            VendLedgEntry.SETRANGE(Open,TRUE);
            VendLedgEntry.SETRANGE("Vendor No.","Account No.");
            VendLedgEntry.GET("Applies-to Entry No.");
            PAGE.RUNMODAL(0,VendLedgEntry);
          END;
        "Account Type"::"Bank Account":
          BEGIN
            BankAccLedgEntry.SETRANGE(Open,TRUE);
            BankAccLedgEntry.SETRANGE("Bank Account No.","Account No.");
            BankAccLedgEntry.GET("Applies-to Entry No.");
            PAGE.RUNMODAL(0,BankAccLedgEntry);
          END;
      END;
    END;

    LOCAL PROCEDURE CrMemoSelectedToApply@18();
    VAR
      AppliedPaymentEntry@1000 : Record 1294;
    BEGIN
      AddFilterOnAppliedPmtEntry(AppliedPaymentEntry);
      IF AppliedPaymentEntry.COUNT > 0 THEN BEGIN
        AppliedPaymentEntry.SETRANGE("Document Type","Document Type"::"Credit Memo");
        IF AppliedPaymentEntry.COUNT = 0 THEN
          MESSAGE(WantToApplyCreditMemoAndInvoicesMsg);
      END;
    END;

    LOCAL PROCEDURE AddFilterOnAppliedPmtEntry@22(VAR AppliedPaymentEntry@1001 : Record 1294);
    BEGIN
      AppliedPaymentEntry.SETRANGE("Statement Type","Statement Type");
      AppliedPaymentEntry.SETRANGE("Bank Account No.","Bank Account No.");
      AppliedPaymentEntry.SETRANGE("Statement No.","Statement No.");
      AppliedPaymentEntry.SETRANGE("Statement Line No.","Statement Line No.");
    END;

    LOCAL PROCEDURE UpdateTypeOption@19(EntryNo@1000 : Integer);
    VAR
      CheckLedgerEntry@1001 : Record 272;
    BEGIN
      CheckLedgerEntry.SETRANGE("Bank Account Ledger Entry No.",EntryNo);
      IF CheckLedgerEntry.FINDFIRST THEN
        Type := Type::"Check Ledger Entry"
      ELSE
        Type := Type::"Bank Account Ledger Entry";
    END;

    BEGIN
    END.
  }
}

