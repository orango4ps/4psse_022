OBJECT Report 77812 AL Translate From NAV Project
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=TTool;
  }
  PROPERTIES
  {
    CaptionML=ENU=AL Translate From NAV Project;
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF NOT TTNAVProject.GET(NAVProjectCode) THEN
                    ERROR(ProjectCodeEmptyErr);

                  IF TTNAVProject."Source Language ID" <> TTALProject."Source Language ID" THEN
                    ERROR(SourceNotEqualErr,TTNAVProject.Code,TTALProject.Code);

                  IF TTNAVProject."Target Language ID" <> TTALProject."Target Language ID" THEN
                    ERROR(TargetNotEqualErr,TTNAVProject.Code,TTALProject.Code);
                END;

  }
  DATASET
  {
    { 1100525000;;DataItem;                  ;
               DataItemTable=Table77812;
               OnPreDataItem=BEGIN
                               MaxCounter := "TT AL Caption".COUNT;
                               OpenWindow(ProcessingLinesLbl,MaxCounter);
                             END;

               OnAfterGetRecord=BEGIN
                                  Counter := Counter + 1;
                                  UpdateWindow;

                                  IF ALTranslationManagement.FindMatchNAVProject("TT AL Caption",TTALProject,TTNAVProject) THEN BEGIN
                                    "TT AL Caption".UpdateProperties;
                                    "TT AL Caption".MODIFY;
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                CloseWindow;
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100525000;;Container;
                  Name=Control 1;
                  ContainerType=ContentArea }

      { 1100525001;1;Field  ;
                  CaptionML=ENU=Translate from NAV Project Code;
                  SourceExpr=NAVProjectCode;
                  TableRelation="TT NAV Project" }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TTALProject@1100525000 : Record 77810;
      TTNAVProject@1100525001 : Record 77782;
      Window@1100525007 : Dialog;
      WindowLastUpdated@1100525006 : Time;
      Counter@1100525005 : Integer;
      MaxCounter@1100525004 : Integer;
      NAVProjectCode@1100525003 : Code[20];
      ProjectCodeEmptyErr@1100525009 : TextConst 'ENU=Project Code must be filled in.';
      SourceNotEqualErr@1100525008 : TextConst 'ENU=Source language of project %1 must be equal to source language of product %2.';
      TargetNotEqualErr@1100525002 : TextConst 'ENU=Target language of project %1 must be equal to target language of product %2.';
      ProcessingLinesLbl@1100525011 : TextConst 'ENU=Processing lines @3@@@@@@@@@@@@@';
      LineOfLbl@1100525010 : TextConst 'ENU=Line             #1###### of #2######\';
      ALTranslationManagement@1100525012 : Codeunit 77810;

    PROCEDURE SetTTALProject@1100525006(TTALProject2@1100525000 : Record 77810);
    BEGIN
      TTALProject.GET(TTALProject2.Code);
    END;

    PROCEDURE OpenWindow@1100525002(Txt@1100525000 : Text[80];MaxCounter2@1100525001 : Integer);
    BEGIN
      Window.OPEN(LineOfLbl + Txt);
      Counter := 0;
      Window.UPDATE(2,MaxCounter2);
      WindowLastUpdated := TIME;
    END;

    PROCEDURE UpdateWindow@1100525003();
    BEGIN
      IF TIME - WindowLastUpdated > 300 THEN BEGIN
        Window.UPDATE(1,Counter);
        Window.UPDATE(3,ROUND(Counter / MaxCounter * 10000,1));
        WindowLastUpdated := TIME;
      END;
    END;

    LOCAL PROCEDURE CloseWindow@1100525004();
    BEGIN
      Window.CLOSE;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

