OBJECT Codeunit 11012014 Determine Progress Date
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    TableNo=11072003;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      ProgressRec@11012000 : Record 11012037;

    PROCEDURE DetermineDate@1(VAR ProjectRec@11012000 : Record 11072003;SingleProject@1100525000 : Boolean);
    VAR
      EndDat@11012001 : Date;
    BEGIN
      EndDat := ProjectRec.GETRANGEMAX("Period Filter");

      ProgressRec.RESET;                                                                                    //**4PS01.sn
      IF ProjectRec.CURRENTCOMPANY <> COMPANYNAME THEN
        ProgressRec.CHANGECOMPANY(ProjectRec.CURRENTCOMPANY);
      IF (ProjectRec."Single/Main/Sub Project" = ProjectRec."Single/Main/Sub Project"::"Main Project") AND (NOT SingleProject) THEN BEGIN
        ProgressRec.SETRANGE("Main Project No.", ProjectRec."No.");
        ProgressRec.SETCURRENTKEY("Main Project No.","Progress Date");
      END ELSE BEGIN
        ProgressRec.SETCURRENTKEY("Project No.","Progress Date");                                           //**4PS01.en
        ProgressRec.SETRANGE("Project No.",ProjectRec."No.");
      END;
      ProgressRec.SETRANGE("Progress Date",0D,EndDat);

      IF ProgressRec.FIND('+') THEN
        ProjectRec.SETRANGE("Progress Filter",ProgressRec."Progress Date")
      ELSE
        ProjectRec.SETRANGE("Progress Filter",0D);
    END;

    PROCEDURE DeterminePreviousDate@2(VAR ProjectRec@11012000 : Record 11072003;SingleProject@1100525000 : Boolean);
    VAR
      StartDate@11012001 : Date;
      EndDatePrevPeriod@11012002 : Date;
    BEGIN
      StartDate := ProjectRec.GETRANGEMIN("Period Filter");
      IF StartDate > 00000101D THEN
        EndDatePrevPeriod := CALCDATE('<-1D>',StartDate);  //db, 15-03-11

      ProgressRec.RESET;                                                                                   //**4PS01.sn
      IF ProjectRec.CURRENTCOMPANY <> COMPANYNAME THEN
        ProgressRec.CHANGECOMPANY(ProjectRec.CURRENTCOMPANY);
      IF (ProjectRec."Single/Main/Sub Project" = ProjectRec."Single/Main/Sub Project"::"Main Project") AND (NOT SingleProject) THEN BEGIN
        ProgressRec.SETRANGE("Main Project No.",ProjectRec."No.");
        ProgressRec.SETCURRENTKEY("Main Project No.","Progress Date");
      END ELSE BEGIN
        ProgressRec.SETCURRENTKEY("Project No.","Progress Date");                                         //**4PS01.en
        ProgressRec.SETRANGE("Project No.",ProjectRec."No.");
      END;
      ProgressRec.SETRANGE("Progress Date",0D,EndDatePrevPeriod);

      IF ProgressRec.FINDLAST THEN
        ProjectRec.SETRANGE(PrevProgressFilter,ProgressRec."Progress Date")
      ELSE
        ProjectRec.SETRANGE(PrevProgressFilter,0D);
    END;

    PROCEDURE DetermineBothDates@3(VAR ProjectRec@11012000 : Record 11072003;SingleProject@1100525000 : Boolean);
    BEGIN
      DetermineDate(ProjectRec,SingleProject);
      DeterminePreviousDate(ProjectRec,SingleProject);
    END;

    BEGIN
    {
      4PS01 JD, 20 feb 2007, Project Cost Control forms and reports must give overview of the costs of a mainproject as well.
    }
    END.
  }
}

