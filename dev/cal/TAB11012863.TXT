OBJECT Table 11012863 Consignment
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               LicenceCu.CheckLicenceConsignment;
             END;

    OnModify=BEGIN
               LicenceCu.CheckLicenceConsignment;
               CheckOverlapConsignment;
             END;

    OnDelete=BEGIN
               ConsTimeRec.SETRANGE("Consignment Code",Code);
               ConsTimeRec.DELETEALL(TRUE);

               ConsRosterRec.SETCURRENTKEY("Consignment Code");
               ConsRosterRec.SETRANGE("Consignment Code",Code);
               ConsRosterRec.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=Consignment;
               NOR=Fullservicepakke;
               SVE=Fullservicepaket];
    LookupPageID=Page11020359;
    DrillDownPageID=Page11020359;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 3   ;   ;Territory Filter    ;Code50        ;TableRelation=Territory;
                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Territory Filter;
                                                              NOR=Distriktsfilter;
                                                              SVE=Distriktsfilter] }
    { 4   ;   ;Global Dimension 1 Filter;Code50   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Global Dimension 1 Filter;
                                                              NOR=Filter for global dimensjon 1;
                                                              SVE=Global dimension 1 filter];
                                                   CaptionClass='1,3,1' }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ConsTimeRec@1100485000 : Record 11012864;
      ConsRosterRec@1100485001 : Record 11012865;
      LicenceCu@1100485002 : Codeunit 11012131;
      Text000@1100525000 : TextConst 'ENU=Overlap with %1 %2: \%3 %4 \%5 %6';

    PROCEDURE CheckTerritoryAndDepartment@1100485000(TerritoryCode@1100485000 : Code[20];DimensionCode@1100485001 : Code[20]) Result : Boolean;
    VAR
      TerritoryRec@1100485002 : Record 286;
      DimValRec@1100485003 : Record 349;
      TerritoryOK@1100485004 : Boolean;
      DimensionOK@1100485005 : Boolean;
    BEGIN
      Result := ("Territory Filter" = '') AND ("Global Dimension 1 Filter" = '');

      IF NOT Result THEN BEGIN
        IF ("Territory Filter" <> '') AND (TerritoryCode <> '') THEN BEGIN
          TerritoryOK := FALSE;
          TerritoryRec.SETFILTER(Code, "Territory Filter");
          IF TerritoryRec.FINDSET(FALSE, FALSE) THEN
            REPEAT
              IF TerritoryRec.Code = TerritoryCode THEN
                TerritoryOK := TRUE;
            UNTIL TerritoryRec.NEXT = 0;
        END ELSE BEGIN
          TerritoryOK := TRUE;
        END;

        IF ("Global Dimension 1 Filter" <> '') AND (DimensionCode <> '') THEN BEGIN
          DimensionOK := FALSE;
          DimValRec.SETFILTER(Code, "Global Dimension 1 Filter");
          DimValRec.SETRANGE("Global Dimension No.", 1);
          IF DimValRec.FINDSET(FALSE, FALSE) THEN
            REPEAT
              IF DimValRec.Code = DimensionCode THEN
                DimensionOK := TRUE;
            UNTIL DimValRec.NEXT = 0;
        END ELSE BEGIN
          DimensionOK := TRUE;
        END;
        Result := TerritoryOK AND DimensionOK;
      END;
    END;

    LOCAL PROCEDURE CheckOverlapConsignment@1100485005();
    VAR
      ConsRec@1100485001 : Record 11012863;
    BEGIN
      ConsRec.SETFILTER(Code, '<>%1', Code);
      IF ConsRec.FINDSET(FALSE,FALSE) THEN BEGIN
        REPEAT
          CheckOverlapFilters(0, ConsRec, "Territory Filter", "Global Dimension 1 Filter");
        UNTIL ConsRec.NEXT = 0;
      END;
    END;

    PROCEDURE CheckOverlapFilters@1100485001(lvAction@1100485002 : Integer;VAR ConsRec@1100485003 : Record 11012863;TerritoryFilter@1100485001 : Text[1024];DimensionFilter@1100485000 : Text[1024]);
    VAR
      TerritoryRec@1100485005 : Record 286;
      DimValRec@1100485004 : Record 349;
    BEGIN
      IF (TerritoryFilter = '') AND (ConsRec."Territory Filter" <> '') THEN BEGIN
        TerritoryRec.SETFILTER(Code, ConsRec."Territory Filter");
      END ELSE IF (TerritoryFilter <> '') AND (ConsRec."Territory Filter" = '') THEN BEGIN
        TerritoryRec.SETFILTER(Code, TerritoryFilter);
      END ELSE IF (TerritoryFilter <> '') AND (ConsRec."Territory Filter" <> '') THEN BEGIN
        TerritoryRec.SETFILTER(Code, CheckBraces(TerritoryFilter) + '&' + CheckBraces(ConsRec."Territory Filter"));
      END;

      IF (DimensionFilter = '') AND (ConsRec."Global Dimension 1 Filter" <> '') THEN BEGIN
        DimValRec.SETFILTER(Code, ConsRec."Global Dimension 1 Filter");
      END ELSE IF (DimensionFilter <> '') AND (ConsRec."Global Dimension 1 Filter" = '') THEN BEGIN
        DimValRec.SETFILTER(Code, DimensionFilter);
      END ELSE IF (DimensionFilter <> '') AND (ConsRec."Global Dimension 1 Filter" <> '') THEN BEGIN
        DimValRec.SETFILTER(Code, CheckBraces(DimensionFilter) + '&' + CheckBraces(ConsRec."Global Dimension 1 Filter"));
      END;
      DimValRec.SETRANGE("Global Dimension No.", 1);

      IF TerritoryRec.FINDFIRST AND DimValRec.FINDFIRST THEN BEGIN
        IF lvAction = 0 THEN  //check overlap consignment
          MESSAGE(Text000,  //db, 11-07-14: C016567
            ConsRec.TABLECAPTION, ConsRec.Code,
            TerritoryRec.TABLECAPTION, TerritoryRec.Code,
            DimValRec.TABLECAPTION, DimValRec.Code);
        IF lvAction = 1 THEN  //not used
          ConsRec.MARK := TRUE;
      END;
    END;

    LOCAL PROCEDURE CheckBraces@1100485008(TextIn@1100485000 : Text[1024]) Result : Text[1024];
    BEGIN
      IF STRPOS(TextIn, '|') > 0 THEN
        EXIT('(' + TextIn + ')');
      IF STRPOS(TextIn, '&') > 0 THEN
        EXIT('(' + TextIn + ')');
      IF STRPOS(TextIn, '..') > 0 THEN
        EXIT('(' + TextIn + ')');

      EXIT(TextIn);
    END;

    PROCEDURE LookupDimension@1100485002();
    VAR
      DimValRec@1100485000 : Record 349;
    BEGIN
      DimValRec.SETRANGE("Global Dimension No.", 1);
      DimValRec.SETFILTER(Code, "Global Dimension 1 Filter");
      IF DimValRec.FINDFIRST THEN
        DimValRec.SETRANGE(Code);
      IF PAGE.RUNMODAL(PAGE::"Dimension Value List", DimValRec) = ACTION::LookupOK THEN
        "Global Dimension 1 Filter" := "Global Dimension 1 Filter" + DimValRec.Code;
    END;

    PROCEDURE LookupTerritory@1100485003();
    VAR
      TerritoryRec@1100485001 : Record 286;
    BEGIN
      TerritoryRec.SETFILTER(Code, "Territory Filter");
      IF TerritoryRec.FINDFIRST THEN
        TerritoryRec.SETRANGE(Code);
      IF PAGE.RUNMODAL(PAGE::Territories, TerritoryRec) = ACTION::LookupOK THEN
        "Territory Filter" := "Territory Filter" + TerritoryRec.Code;
    END;

    BEGIN
    END.
  }
}

