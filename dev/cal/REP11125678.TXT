OBJECT Report 11125678 Export Bid Form Cover Page ZSX
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Deckblatt Angebotsformular exportieren;
               ENU=Export Bid Form Cover Page;
               NLD=Exporteer inschrijvingsstaat;
               NOR=Eksporter Vud fra Forside];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   GLSetup.GET;
                 END;

    OnPreReport=BEGIN
                  IF DELCHR(FileName, '<>') = '' THEN
                    ERROR(Text001);

                  IF FileType <> FileType::ZSX THEN BEGIN
                    ServerTempFileName := FileMgt.ServerTempFileName('TXT');
                    gvFile.CREATE(ServerTempFileName,TEXTENCODING::Windows);
                    gvFile.TEXTMODE(TRUE);
                    gvFile.CREATEOUTSTREAM(OutStreamStrm);
                  END;
                END;

    OnPostReport=BEGIN
                   IF FileType <> FileType::ZSX THEN BEGIN
                     gvFile.CLOSE();
                     FileMgt.DownloadToFile(ServerTempFileName, FileName);
                     MESSAGE(Text000, gvCounter);
                   END;
                 END;

  }
  DATASET
  {
    { 1530;    ;DataItem;                    ;
               DataItemTable=Table11012151;
               DataItemTableView=SORTING(No.);
               MaxIteration=1;
               OnAfterGetRecord=VAR
                                  ExportZSX@1100528700 : Codeunit 11125676;
                                BEGIN
                                  IF FileType = FileType::ZSX THEN BEGIN
                                    ExportZSX.ExportZSX(Estimate, FileName);
                                    CurrReport.BREAK;
                                  END;

                                  SpecRec.SETRANGE("Specification No.", Estimate."Construction Spec. No.");
                                  IF NOT SpecRec.FINDFIRST THEN SpecRec.INIT;

                                  FillHeader;
                                  FillPageHeader;
                                  FillBidForm;
                                  FillElement;
                                  FillFooter;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   FileType := FileType::ZSX;  //db, 18-04-17: FileType ZSU no longer supported in CROW2015 (use ZSX).
                 END;

    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525001;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100409000;2;Field  ;
                  CaptionML=[DEU=Dateityp;
                             ENU=File Type;
                             NLD=Bestandstype;
                             NOR=Filtype;
                             SVE=Filtyp];
                  OptionCaptionML=[DEU=ZSX;
                                   ENU=ZSX;
                                   NLD=ZSX];
                  SourceExpr=FileType;
                  Editable=FALSE }

      { 1100525002;2;Field  ;
                  CaptionML=[DEU=Dateiname;
                             ENU=File Name;
                             NLD=Bestandsnaam;
                             NOR=Filnavn;
                             SVE=Filnamn];
                  SourceExpr=FileName;
                  OnAssistEdit=VAR
                                 FileManagement@1100525000 : Codeunit 419;
                                 FilterString@1100525001 : Text;
                               BEGIN
                                 FilterString := FileManagement.GetToFilterText('', '.zsx');
                                 FileName := FileManagement.SaveFileDialog('', FileName, FilterString);
                               END;
                                }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      GLSetup@1100525009 : Record 98;
      SpecRec@1100525006 : Record 11125672;
      FileMgt@1100525013 : Codeunit 419;
      LineTxt@1100525005 : Text[250];
      filler@1100525004 : Text[250];
      OutStreamStrm@1100525003 : OutStream;
      FileName@1100525002 : Text[260];
      ServerTempFileName@1100525012 : Text[260];
      gvFile@1100525001 : File;
      gvCounter@1100525000 : Integer;
      Text000@1100525010 : TextConst 'DEU=%1 Zeilen verarbeitet;ENU=%1 Lines processed;NLD=%1 regels verwerkt;NOR=%1 Rader Behandlet;SVE=%1 rader har behandlats';
      Text001@1100525008 : TextConst 'DEU=Bitte, geben Sie einen Dateinamen ein.;ENU=Please specify a file name.;NLD=Geef a.u.b. een bestandsnaam op.;NOR=Angi et filnavn.;SVE=Ange ett filnamn.';
      FileType@1100525007 : 'ZSX';

    PROCEDURE WriteLine@1210190051(CountRec@1100525000 : Boolean);
    BEGIN
      //Schrijf regel naar file
      OutStreamStrm.WRITETEXT(LineTxt);

      //Carriage return/Linefeed
      OutStreamStrm.WRITETEXT();

      gvCounter := gvCounter + 1;
    END;

    PROCEDURE FillHeader@1100525005();
    BEGIN
      LineTxt := '';
      LineTxt := LineTxt + '00';  //code besteksdeel
      LineTxt := LineTxt + '10';  //recordsoort
      LineTxt := LineTxt + '01';  //recordnummer
      LineTxt := LineTxt + '0313';  //versie RSU-beschrijving
      LineTxt := LineTxt + FillSeqLine(SpecRec."Master Spec. Release Version",6,' ','>');  //jaartal versie catalogus
      LineTxt := LineTxt + FillSeqLine(filler,7,' ','>');  //filler
      LineTxt := LineTxt + 'Z';  //soort eenheid
      LineTxt := LineTxt + FillSeqLine(Estimate."Construction Spec. No.",37,' ','>');  //besteknummer
      LineTxt := LineTxt + FillSeqLine(filler,2,' ','>');  //staatnummer
      LineTxt := LineTxt + FillSeqLine(filler,6,' ','>');  //release
      LineTxt := LineTxt + FORMAT(WORKDATE,0,'<Year4><Month,2><Day,2>');  //datum waarop bestand is aangemaakt
      LineTxt := LineTxt + FillSeqLine(filler,4,'*','<');  //gebruikersnummer
      LineTxt := LineTxt + FillSeqLine(GLSetup."LCY Code",3,' ','>');  //munteenheid
      WriteLine(FALSE);
    END;

    PROCEDURE FillPageHeader@1100525009();
    VAR
      SpecHeaderRec@1100525001 : Record 11125667;
      LineNo@1100525000 : Integer;
    BEGIN
      SpecHeaderRec.SETRANGE("Specification No.", Estimate."Construction Spec. No.");
      SpecHeaderRec.SETRANGE(Section, SpecHeaderRec.Section::"Bid Form");
      IF SpecHeaderRec.FINDSET THEN BEGIN
        REPEAT
          LineNo := LineNo + 1;
          LineTxt := '';
          LineTxt := LineTxt + '00';  //code besteksdeel
          LineTxt := LineTxt + '20';  //recordsoort
          LineTxt := LineTxt + FillSeqLine(FORMAT(LineNo),2,'0','<');  //recordnummer
          LineTxt := LineTxt + FillSeqLine(filler,4,'0','<');  //rest indicatief
          LineTxt := LineTxt + FillSeqLine(SpecHeaderRec.Text,65,' ','>');  //tekst
          LineTxt := LineTxt + '1';  //0=LF;1=CR
          WriteLine(TRUE);
        UNTIL SpecHeaderRec.NEXT = 0;
      END;
    END;

    PROCEDURE FillBidForm@1100525008();
    VAR
      SpecLineRec@1100525001 : Record 11125672;
      LineNo@1100525000 : Integer;
    BEGIN
      SpecLineRec.SETRANGE("Specification No.", Estimate."Construction Spec. No.");
      SpecLineRec.SETRANGE(Type, SpecLineRec.Type::"Bid Form Specification");
      IF SpecLineRec.FINDSET THEN BEGIN
        REPEAT
          LineNo := LineNo + 1;
          LineTxt := '';
          LineTxt := LineTxt + '20';  //code besteksdeel
          LineTxt := LineTxt + FillSeqLine(FORMAT(LineNo),2,'0','<');  //regelnummer
          LineTxt := LineTxt + FillSeqLine(filler,6,'0','<');  //rest indicatief
          LineTxt := LineTxt + FillSeqLine(SpecLineRec.Text,65,' ','>');  //tekst
          LineTxt := LineTxt + '1';  //0=LF;1=CR
          LineTxt := LineTxt + FillSeqLine(SpecLineRec."Master Spec. Release Version",6,' ','<');  //release
          WriteLine(TRUE);
        UNTIL SpecLineRec.NEXT = 0;
      END;
    END;

    PROCEDURE FillElement@1100525007();
    VAR
      EstElemRec@1100525000 : Record 11125679;
      ElemStatus@1100525001 : Text[30];
    BEGIN
      EstElemRec.SETRANGE("Estimate No.", Estimate."No.");
      EstElemRec.SETRANGE(Title, TRUE);
      EstElemRec.SETFILTER(Type, '..%1', EstElemRec.Type::"Commitment (Main Line)");
      IF EstElemRec.FINDSET THEN BEGIN
        REPEAT
          ElemStatus := '';
          CASE EstElemRec."Quantity Type" OF
            EstElemRec."Quantity Type"::V: ElemStatus := '0';
            EstElemRec."Quantity Type"::N: ElemStatus := '1';
            EstElemRec."Quantity Type"::M: ElemStatus := '2';
            EstElemRec."Quantity Type"::O: ElemStatus := '3';
            EstElemRec."Quantity Type"::A: ElemStatus := '4';
          END;
          LineTxt := '';
          LineTxt := LineTxt + '23';  //code besteksdeel
          LineTxt := LineTxt + FillSeqLine(EstElemRec."Tender Paragraph No.",6,' ','>');  //bestekspost
          LineTxt := LineTxt + FillSeqLine(filler,2,'0','<');  //rest indicatief
          LineTxt := LineTxt + FillSeqLine(EstElemRec.Text,50,' ','>');  //tekst
          IF EstElemRec.Type < EstElemRec.Type::"Commitment (Main Line)" THEN BEGIN
            LineTxt := LineTxt + FillSeqLine(filler,4,' ','>');
            LineTxt := LineTxt + FillSeqLine(filler,9,' ','>');
            LineTxt := LineTxt + FillSeqLine(filler,1,' ','>');
            LineTxt := LineTxt + FillSeqLine(filler,8,' ','>');
            LineTxt := LineTxt + FillSeqLine(filler,8,' ','>');
            LineTxt := LineTxt + FillSeqLine(filler,15,' ','>');
            LineTxt := LineTxt + FillSeqLine(filler,6,' ','>');
          END ELSE BEGIN
            LineTxt := LineTxt + FillSeqLine(EstElemRec."External Unit of Measure Code",4,' ','>');  //eenheid res.verplichting
            // jhoek.070211.begin
            // LineTxt := LineTxt + FillSeqLine(FormatDec(EstElemRec.Quantity,0),9,' ','<');  //hoeveelheid res.verplichting
            LineTxt := LineTxt + FillSeqLine(FormatDec(EstElemRec."Actual Quantity",0),9,' ','<');  //hoeveelheid res.verplichting
            // jhoek.070211.end
            LineTxt := LineTxt + FillSeqLine(ElemStatus,1,' ','>');  //code hoeveelheid
            LineTxt := LineTxt + FillSeqLine(FormatDec(EstElemRec."Actual Unit Price",2),8,' ','<');  //prijs per eenheid
            LineTxt := LineTxt + FillSeqLine(filler,8,' ','<');  //verrekenprijs
            LineTxt := LineTxt + FillSeqLine(FormatDec(EstElemRec."Actual Amount",1),15,' ','<');  //totaalbedrag
            LineTxt := LineTxt + FillSeqLine(SpecRec."Master Spec. Release Version",6,' ','>');  //release
          END;
          WriteLine(TRUE);
        UNTIL EstElemRec.NEXT = 0;
      END;
    END;

    PROCEDURE FillFooter@1100525010();
    BEGIN
      LineTxt := '';
      LineTxt := LineTxt + '50';  //code besteksdeel
      LineTxt := LineTxt + FillSeqLine(filler,8,'0','<');  //indicatief
      LineTxt := LineTxt + FillSeqLine(filler,2,'0','<');  //regelnummer
      LineTxt := LineTxt + 'Z';  //soort eenheid
      WriteLine(FALSE);
    END;

    PROCEDURE FillSeqLine@4(String@11012001 : Text[80];TotLength@11012002 : Integer;WithCharacter@11012003 : Text[1];TrueBool@11012004 : Char) Text@11012000 : Text[80];
    VAR
      Length@11012005 : Integer;
      lvText000@1100525000 : TextConst 'DEU=Nur < und > sind als Parameter in der Funktion Ausrichten gestattet.;ENU=Only < and > are allowed as parameter in function outline;NLD=Alleen < en > zijn toegestaan als Parameter in functie uitlijnen.;NOR=Kun < og > tillates som parametre i funksjondisposisjonen;SVE=Endast < och > till†ts som parametrar i funktionsutkastet';
    BEGIN
      Length:=STRLEN(String);
      IF TotLength>Length THEN BEGIN
        CASE TrueBool OF
          '<': String:=PADSTR('',TotLength-Length,WithCharacter)+String;
          '>': String:=String+PADSTR('',TotLength-Length,WithCharacter);
          ELSE ERROR(lvText000);
        END;
      END;

      EXIT(String);
    END;

    PROCEDURE FormatDec@1100525000(QuanDec@1100525000 : Decimal;RoundType@1100525001 : Integer) QuanTxt : Text[250];
    BEGIN
      IF RoundType <> 0 THEN
        QuanDec := ROUND(QuanDec, 0.01);

      QuanTxt := FORMAT(QuanDec,0,'<Precision,2:><Standard format,0>');
      QuanTxt := DELCHR(QuanTxt, '=', '.');
      QuanTxt := DELCHR(QuanTxt, '=', ',');
      EXIT(QuanTxt);
    END;

    PROCEDURE SetFileName@1100525006(FileName2@1100525000 : Text[260]);
    BEGIN
      FileName := FileName2;
    END;

    BEGIN
    {
      // Object names kept consistent with dataport 11012277...
    }
    END.
  }
  RDLDATA
  {
  }
}

