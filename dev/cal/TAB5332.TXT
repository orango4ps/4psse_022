OBJECT Table 5332 Coupling Record Buffer
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    ReplicateData=No;
    CaptionML=[ENU=Coupling Record Buffer;
               NOR=Buffer for sammenkoblingspost];
  }
  FIELDS
  {
    { 1   ;   ;NAV Name            ;Text250       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=NAV Name;
                                                              NOR=NAV-navn] }
    { 2   ;   ;CRM Name            ;Text250       ;OnValidate=VAR
                                                                CRMIntegrationRecord@1001 : Record 5331;
                                                              BEGIN
                                                                IF FindCRMRecordByName("CRM Name") THEN BEGIN
                                                                  IF "Saved CRM ID" <> "CRM ID" THEN
                                                                    CRMIntegrationRecord.AssertRecordIDCanBeCoupled("NAV Record ID","CRM ID");
                                                                  CalcCRMName;
                                                                END ELSE
                                                                  ERROR(NoSuchCRMRecordErr,"CRM Name",CRMProductName.SHORT);
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookUpCRMName;
                                                            END;

                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=CRM Name;
                                                              NOR=CRM-navn] }
    { 3   ;   ;NAV Table ID        ;Integer       ;OnValidate=VAR
                                                                IntegrationTableMapping@1000 : Record 5335;
                                                              BEGIN
                                                                IntegrationTableMapping.SETRANGE("Table ID","NAV Table ID");
                                                                IntegrationTableMapping.SETRANGE("Delete After Synchronization",FALSE);
                                                                IF IntegrationTableMapping.FINDFIRST THEN
                                                                  "CRM Table Name" := IntegrationTableMapping.Name
                                                                ELSE
                                                                  "CRM Table Name" := '';
                                                              END;

                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=NAV Table ID;
                                                              NOR=ID for NAV-tabell] }
    { 4   ;   ;CRM Table ID        ;Integer       ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=CRM Table ID;
                                                              NOR=ID for CRM-tabell] }
    { 5   ;   ;Sync Action         ;Option        ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Sync Action;
                                                              NOR=Synkroniseringshandling];
                                                   OptionCaptionML=[ENU=Do Not Synchronize,To Integration Table,From Integration Table;
                                                                    NOR=Ikke synkroniser,Til integreringstabell,Fra integreringstabell];
                                                   OptionString=Do Not Synchronize,To Integration Table,From Integration Table }
    { 8   ;   ;NAV Record ID       ;RecordID      ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=NAV Record ID;
                                                              NOR=ID for NAV-post] }
    { 9   ;   ;CRM ID              ;GUID          ;OnValidate=BEGIN
                                                                CalcCRMName;
                                                              END;

                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=CRM ID;
                                                              NOR=CRM/ID] }
    { 10  ;   ;Create New          ;Boolean       ;OnValidate=VAR
                                                                NullGUID@1000 : GUID;
                                                              BEGIN
                                                                IF "Create New" THEN BEGIN
                                                                  "Saved Sync Action" := "Sync Action";
                                                                  "Saved CRM ID" := "CRM ID";
                                                                  VALIDATE("Sync Action","Sync Action"::"To Integration Table");
                                                                  CLEAR(NullGUID);
                                                                  VALIDATE("CRM ID",NullGUID);
                                                                END ELSE BEGIN
                                                                  VALIDATE("Sync Action","Saved Sync Action");
                                                                  VALIDATE("CRM ID","Saved CRM ID");
                                                                END;
                                                              END;

                                                   DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Create New;
                                                              NOR=Opprett ny;
                                                              SVE=Skapa ny] }
    { 11  ;   ;Saved Sync Action   ;Option        ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Saved Sync Action;
                                                              NOR=Lagret synkroniseringshandling];
                                                   OptionCaptionML=[ENU=Do Not Synchronize,To Integration Table,From Integration Table;
                                                                    NOR=Ikke synkroniser,Til integreringstabell,Fra integreringstabell];
                                                   OptionString=Do Not Synchronize,To Integration Table,From Integration Table }
    { 12  ;   ;Saved CRM ID        ;GUID          ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=Saved CRM ID;
                                                              NOR=Lagret CRM-ID] }
    { 13  ;   ;CRM Table Name      ;Code20        ;DataClassification=SystemMetadata;
                                                   CaptionML=[ENU=CRM Table Name;
                                                              NOR=Navn p† CRM-tabell];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;NAV Name                                ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      InitialSynchDisabledErr@1001 : TextConst 'ENU=No initial synchronization direction was specified because initial synchronization was disabled.;NOR=Ingen f›rste synkroniseringsretning er angitt fordi f›rste synkronisering er deaktivert.';
      NoSuchCRMRecordErr@1000 : TextConst '@@@="%1 = The record name entered by the user, %2 = CRM product name";ENU=A record with the name %1 does not exist in %2.;NOR=Det finnes ingen post med navnet %1 i %2.';
      CRMSetupDefaults@1002 : Codeunit 5334;
      LookupCRMTables@1003 : Codeunit 5332;
      CRMProductName@1004 : Codeunit 5344;

    [External]
    PROCEDURE Initialize@1(NAVRecordID@1000 : RecordID);
    VAR
      IntegrationTableMapping@1004 : Record 5335;
      RecordRef@1003 : RecordRef;
    BEGIN
      RecordRef := NAVRecordID.GETRECORD;
      RecordRef.FIND;

      INIT;
      VALIDATE("NAV Table ID",NAVRecordID.TABLENO);
      "NAV Record ID" := NAVRecordID;
      "NAV Name" := NameValue(RecordRef);
      "CRM Table ID" := CRMSetupDefaults.GetCRMTableNo("NAV Table ID");
      IF CRMSetupDefaults.GetDefaultDirection("NAV Table ID") = IntegrationTableMapping.Direction::FromIntegrationTable THEN
        VALIDATE("Sync Action","Sync Action"::"From Integration Table")
      ELSE
        VALIDATE("Sync Action","Sync Action"::"To Integration Table");
      IF FindCRMId THEN
        IF CalcCRMName THEN BEGIN
          VALIDATE("Sync Action","Sync Action"::"Do Not Synchronize");
          "Saved CRM ID" := "CRM ID";
        END;
    END;

    LOCAL PROCEDURE FindCRMId@8() : Boolean;
    VAR
      CRMIntegrationRecord@1002 : Record 5331;
    BEGIN
      EXIT(CRMIntegrationRecord.FindIDFromRecordID("NAV Record ID","CRM ID"))
    END;

    LOCAL PROCEDURE FindCRMRecordByName@7(VAR CRMName@1004 : Text[250]) : Boolean;
    VAR
      RecordRef@1002 : RecordRef;
      FieldRef@1001 : FieldRef;
      Found@1003 : Boolean;
    BEGIN
      RecordRef.OPEN("CRM Table ID");
      FieldRef := RecordRef.FIELD(CRMSetupDefaults.GetNameFieldNo("CRM Table ID"));
      FieldRef.SETRANGE(CRMName);
      IF RecordRef.FINDFIRST THEN
        Found := TRUE
      ELSE BEGIN
        RecordRef.CURRENTKEYINDEX(2); // "Name" key should be the second key in a CRM table
        FieldRef := RecordRef.FIELD(CRMSetupDefaults.GetNameFieldNo("CRM Table ID"));
        FieldRef.SETFILTER("CRM Name" + '*');
        IF RecordRef.FINDFIRST THEN
          Found := TRUE;
      END;
      IF Found THEN BEGIN
        CRMName := NameValue(RecordRef);
        "CRM ID" := PrimaryKeyValue(RecordRef);
      END;
      RecordRef.CLOSE;
      EXIT(Found);
    END;

    [External]
    PROCEDURE LookUpCRMName@4();
    VAR
      CRMIntegrationRecord@1003 : Record 5331;
    BEGIN
      IF LookupCRMTables.Lookup("CRM Table ID","NAV Table ID","Saved CRM ID","CRM ID") THEN BEGIN
        IF "Saved CRM ID" <> "CRM ID" THEN
          CRMIntegrationRecord.AssertRecordIDCanBeCoupled("NAV Record ID","CRM ID");
        CalcCRMName;
      END;
    END;

    LOCAL PROCEDURE CalcCRMName@9() Found : Boolean;
    VAR
      RecordRef@1003 : RecordRef;
    BEGIN
      RecordRef.OPEN("CRM Table ID");
      Found := FindCRMRecRefByPK(RecordRef,"CRM ID");
      IF Found THEN
        "CRM Name" := NameValue(RecordRef)
      ELSE
        "CRM Name" := '';
      RecordRef.CLOSE;
    END;

    [External]
    PROCEDURE GetInitialSynchronizationDirection@13() : Integer;
    VAR
      IntegrationTableMapping@1000 : Record 5335;
    BEGIN
      IF "Sync Action" = "Sync Action"::"Do Not Synchronize" THEN
        ERROR(InitialSynchDisabledErr);

      IF "Sync Action" = "Sync Action"::"To Integration Table" THEN
        EXIT(IntegrationTableMapping.Direction::ToIntegrationTable);

      EXIT(IntegrationTableMapping.Direction::FromIntegrationTable);
    END;

    [External]
    PROCEDURE GetPerformInitialSynchronization@6() : Boolean;
    BEGIN
      EXIT("Sync Action" <> "Sync Action"::"Do Not Synchronize");
    END;

    LOCAL PROCEDURE NameValue@10(RecordRef@1000 : RecordRef) : Text[250];
    VAR
      FieldRef@1001 : FieldRef;
    BEGIN
      FieldRef := RecordRef.FIELD(CRMSetupDefaults.GetNameFieldNo(RecordRef.NUMBER));
      EXIT(COPYSTR(FORMAT(FieldRef.VALUE),1,MAXSTRLEN("CRM Name")));
    END;

    LOCAL PROCEDURE PrimaryKeyValue@12(RecordRef@1000 : RecordRef) : GUID;
    VAR
      FieldRef@1002 : FieldRef;
      PrimaryKeyRef@1001 : KeyRef;
    BEGIN
      PrimaryKeyRef := RecordRef.KEYINDEX(1);
      FieldRef := PrimaryKeyRef.FIELDINDEX(1);
      EXIT(FieldRef.VALUE);
    END;

    LOCAL PROCEDURE FindCRMRecRefByPK@5(VAR RecordRef@1000 : RecordRef;CRMId@1001 : GUID) : Boolean;
    VAR
      FieldRef@1002 : FieldRef;
      PrimaryKeyRef@1003 : KeyRef;
    BEGIN
      PrimaryKeyRef := RecordRef.KEYINDEX(1);
      FieldRef := PrimaryKeyRef.FIELDINDEX(1);
      FieldRef.SETRANGE(CRMId);
      EXIT(RecordRef.FINDFIRST);
    END;

    BEGIN
    END.
  }
}

