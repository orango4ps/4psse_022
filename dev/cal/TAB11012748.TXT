OBJECT Table 11012748 Possible Document Link
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    CaptionML=[ENU=Possible Document Link;
               NOR=Mulig Dokumentlink;
               SVE=M”jlig dokumentl„nk];
    LookupPageID=Page11072480;
  }
  FIELDS
  {
    { 10  ;   ;Table No.           ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Table));
                                                   OnValidate=BEGIN
                                                                IF xRec."Table No." <> 0 THEN BEGIN
                                                                  CLEAR("Filter Expression");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Table No.;
                                                              NOR=Tabellnr.;
                                                              SVE=Tabellnr];
                                                   Editable=No }
    { 20  ;   ;Filter Expression   ;Text100       ;CaptionML=[ENU=Filter Expression;
                                                              NOR=Filteruttrykk;
                                                              SVE=Filteruttryck];
                                                   Editable=No }
    { 100 ;   ;Use Caption from Object Type;Option;CaptionML=[ENU=Use Caption from Object Type;
                                                              NOR=Bruk Bildetekst fra Objekttype;
                                                              SVE=Anv„nd rubrik fr†n objektyp];
                                                   OptionCaptionML=[ENU=,Table,,,,,,,Page;
                                                                    NOR=,Tabell,,,,,,,Side];
                                                   OptionString=,Table,,,,,,,Page }
    { 101 ;   ;Use Caption from Object ID;Integer ;TableRelation=IF (Use Caption from Object Type=CONST(Table)) AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Table))
                                                                 ELSE IF (Use Caption from Object Type=CONST(Page)) AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Page));
                                                   CaptionML=[ENU=Use Caption from Object ID;
                                                              NOR=Bruk Bildetekst fra Objekt-ID;
                                                              SVE=Anv„nd rubrik fr†n objekt-ID] }
    { 110 ;   ;Link Name           ;Text249       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=FIELD(Use Caption from Object Type),
                                                                                                                Object ID=FIELD(Use Caption from Object ID)));
                                                   CaptionML=[ENU=Link Name;
                                                              NOR=Lenkenavn;
                                                              SVE=L„nknamn];
                                                   Editable=No }
    { 500 ;   ;Table Caption       ;Text50        ;CaptionML=[ENU=Table Caption;
                                                              NOR=Tabelltittel;
                                                              SVE=Tabellrubrik];
                                                   Description=Only used in page 11072480;
                                                   Editable=No }
    { 510 ;   ;Link Type           ;Option        ;InitValue=Standard;
                                                   CaptionML=[ENU=Link Type;
                                                              NOR=Linktype;
                                                              SVE=L„nktyp];
                                                   OptionCaptionML=ENU=,Promoted,Standard;
                                                   OptionString=,Promoted,Standard;
                                                   Description=Used to sort correctly }
  }
  KEYS
  {
    {    ;Table No.,Filter Expression             ;Clustered=Yes }
    {    ;Link Type,Table Caption                  }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text008@1100528700 : TextConst 'ENU=%1 not found!;NOR=%1 ikke funnet;SVE=%1 hittades inte';

    PROCEDURE GetLinkName@1199198000(TableNo@1199198001 : Integer;FilterExpression@1199198002 : Text[100]) : Text[80];
    VAR
      PossibleDocumentLink@1100530000 : Record 11012748;
    BEGIN
      // jhoek.190111.begin
      IF PossibleDocumentLink.GET(TableNo, FilterExpression) THEN
        PossibleDocumentLink.CALCFIELDS("Link Name");
      EXIT(PossibleDocumentLink."Link Name");
      // jhoek.190111.end
    END;

    PROCEDURE GetPossibleDocLinkFromText@1100528700(LinkName@1100528700 : Text[250];VAR PossibleDocumentLink2@1100528706 : Record 11012748) : Boolean;
    VAR
      PossibleDocumentLink@1100528701 : Record 11012748;
      i@1100528704 : Integer;
      Cont@1100528702 : Boolean;
    BEGIN
      CLEAR(PossibleDocumentLink2);

      IF NOT EVALUATE(PossibleDocumentLink."Table No.", LinkName) THEN BEGIN
        IF PossibleDocumentLink.FINDSET THEN BEGIN
          Cont := TRUE;
          REPEAT
            PossibleDocumentLink.CALCFIELDS("Link Name");
            IF UPPERCASE(PossibleDocumentLink."Link Name") = UPPERCASE(LinkName) THEN BEGIN
              PossibleDocumentLink.MARK(TRUE);
              Cont := FALSE;
            END ELSE IF STRPOS(UPPERCASE(PossibleDocumentLink."Link Name"), UPPERCASE(LinkName)) = 1 THEN
              PossibleDocumentLink.MARK(TRUE);
          UNTIL (PossibleDocumentLink.NEXT = 0) OR (Cont = FALSE);
        END;
        PossibleDocumentLink.MARKEDONLY(TRUE);
      END ELSE
        PossibleDocumentLink.SETFILTER("Table No.", LinkName);

      i := PossibleDocumentLink.COUNT;

      CASE TRUE OF
        i = 0:
          BEGIN
            ERROR(Text008, PossibleDocumentLink.TABLECAPTION);
          END;
        i = 1:
          BEGIN
            PossibleDocumentLink.FINDFIRST;
            PossibleDocumentLink2.GET(PossibleDocumentLink."Table No.", PossibleDocumentLink."Filter Expression");
            EXIT(TRUE);
          END;
        i > 1:
          BEGIN
            EXIT(SelectPossibleDocLinkFromList(PossibleDocumentLink, PossibleDocumentLink2));
          END;
      END;
    END;

    PROCEDURE SelectPossibleDocLinkFromList@1100528701(VAR PossibleDocumentLinkView@1100528700 : Record 11012748;VAR PossibleDocumentLinkSelected@1100528702 : Record 11012748) : Boolean;
    VAR
      PossibleDocumentLinkList@1100528703 : Page 11072480;
    BEGIN
      CLEAR(PossibleDocumentLinkSelected);

      PossibleDocumentLinkList.SETTABLEVIEW(PossibleDocumentLinkView);
      PossibleDocumentLinkList.LOOKUPMODE(TRUE);
      IF PossibleDocumentLinkList.RUNMODAL = ACTION::LookupOK THEN BEGIN
        PossibleDocumentLinkList.GETRECORD(PossibleDocumentLinkSelected);
        EXIT(TRUE);
      END;

      EXIT(FALSE);
    END;

    BEGIN
    {
      Adding an extra possible document link goes as follows:
      1. Add the new link type to function GeneratePossibleDocLinks in codeunit Document Link Management.
      In the automatic data conversion call function InsertPossibleDocLinks (which calls GeneratePossibleDocLinks).
      This inserts the new link type to the Possible Document Link table.
      2. To support lookup in the DocumentLink subform add the function Set<entity> in codeunit Document Link Management.
      3. Add the function from 2. to the case statement in the function SetTable in the same codeunit
      4. Add the new link type to the Case Statement in the function GetLinkDocRecRef of the code unit "Document Link Management"

      Exceptions:
      5. When not all primary keyfields of the entity are relevant you should also make an exception in function
      GetLongDescription in codeunint Document Link Management
      6. When an entity needs an extra filter on its table (link Purchase Order in table 38) also check function GetTableName
      in this table.

      Accessing the document management functionality from the form of the new entity can be done in several ways. A typical
      example can be found in form 11071940 (Parcel)
    }
    END.
  }
}

