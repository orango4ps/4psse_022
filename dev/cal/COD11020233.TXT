OBJECT Codeunit 11020233 Mail Template Layout Generator
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      MailTemplateManagement@1100528300 : Codeunit 11012622;
      Text001@1100527800 : TextConst 'ENU=Layouts for this template already exist, remove these first.';

    [External]
    PROCEDURE PrefillWorkOrderWorkSheetMailTemplate@1100527602();
    VAR
      MailTemplateNo@1100528301 : Integer;
      MailTemplate@1100527600 : Record 11229450;
      MailTemplateLayout@1100528300 : Record 11229451;
    BEGIN
      MailTemplateNo := 43; // Work Order Worksheet
      IF MailTemplateManagement.GetMailTemplate(MailTemplate, MailTemplateNo, '') THEN BEGIN
        MailTemplate.CALCFIELDS(Tables, Layouts, Attachments);
        IF MailTemplate.Layouts = 0 THEN BEGIN
          CreateTableAndFields(MailTemplateNo, 1, DATABASE::"Work Order", DATABASE::"Work Order");
          CreateTableAndFields(MailTemplateNo, 1, DATABASE::"Company Information", 0);
          CreateTableAndFields(MailTemplateNo, 2, DATABASE::"Service Order", DATABASE::"Work Order");
          CreateTableAndFields(MailTemplateNo, 3, DATABASE::Customer, DATABASE::"Service Order");
          CreateTableAndFields(MailTemplateNo, 4, DATABASE::Contact, DATABASE::Customer);

          CreateTableFilter(MailTemplateNo, 1, DATABASE::"Company Information", 2, ''); // Get Primary Key
          CreateTableLink(MailTemplateNo, 2, DATABASE::"Service Order", DATABASE::"Work Order", 10, 40); // No. - Source No.
          CreateTableLink(MailTemplateNo, 3, DATABASE::Customer, DATABASE::"Service Order", 1, 20); // No. - Customer No.
          CreateTableLink(MailTemplateNo, 4, DATABASE::Contact, DATABASE::Customer, 1, 8); // No. - Contact

          IF NOT MailTemplateLayout.GET(MailTemplateNo, '') THEN BEGIN
            MailTemplateLayout."Mail Template No." := MailTemplateNo;
            MailTemplateLayout."Language Code" := '';
            MailTemplateLayout.INSERT(TRUE);

            // LAYOUT STRUCTURE HERE
            CreateSubjectLine(MailTemplateNo, STRSUBSTNO('Worksheet %1', GetElementNameValue(MailTemplateNo, 10, DATABASE::"Work Order")));
            CreateBodyLine(MailTemplateNo, 'Client', TRUE);
            CreateBodyLine(MailTemplateNo, GetElementNameValue(MailTemplateNo, 2, DATABASE::Customer), TRUE); // Name
            CreateBodyLine(MailTemplateNo, GetElementNameValue(MailTemplateNo, 8, DATABASE::Customer), TRUE); // Contact
            CreateBodyLine(MailTemplateNo, GetElementNameValue(MailTemplateNo, 5, DATABASE::Customer), TRUE); // Address
            CreateBodyLine(MailTemplateNo, STRSUBSTNO('%1 %2', GetElementNameValue(MailTemplateNo, 91, DATABASE::Customer), GetElementNameValue(MailTemplateNo, 7, DATABASE::Customer)), TRUE); // Post Code, City
            CreateBodyLine(MailTemplateNo, STRSUBSTNO('Phone %1', GetElementNameValue(MailTemplateNo, 9, DATABASE::Customer)), TRUE);// Phone No
            CreateWhitespaceLines(MailTemplateNo, 2);
            CreateBodyLine(MailTemplateNo, GetElementNameValue(MailTemplateNo, 20, DATABASE::"Work Order"), TRUE); // Source Company
            CreateBodyLine(MailTemplateNo, GetElementNameValue(MailTemplateNo, 4, DATABASE::"Company Information"), TRUE); // Address
            CreateBodyLine(MailTemplateNo, GetElementNameValue(MailTemplateNo, 11012100, DATABASE::"Company Information"), TRUE); // Postal Address
            CreateBodyLine(MailTemplateNo, STRSUBSTNO('%1 %2', GetElementNameValue(MailTemplateNo, 30, DATABASE::"Company Information"), GetElementNameValue(MailTemplateNo, 6, DATABASE::"Company Information")), TRUE); // Post Code, City
            CreateBodyLine(MailTemplateNo, STRSUBSTNO('Phone %1', GetElementNameValue(MailTemplateNo, 7, DATABASE::"Company Information")), TRUE); // Phone No
            CreateWhitespaceLines(MailTemplateNo, 3);
            CreateBodyLine(MailTemplateNo, 'Serviceaddress', TRUE);
            CreateBodyLine(MailTemplateNo, GetElementNameValue(MailTemplateNo, 21, DATABASE::"Service Order"), TRUE); // Name
            CreateBodyLine(MailTemplateNo, GetElementNameValue(MailTemplateNo, 23, DATABASE::"Service Order"), TRUE); // Address
            CreateBodyLine(MailTemplateNo, GetElementNameValue(MailTemplateNo, 25, DATABASE::"Service Order"), TRUE); // City
            CreateBodyLine(MailTemplateNo, STRSUBSTNO('Phone %1', GetElementNameValue(MailTemplateNo, 27, DATABASE::"Service Order")), TRUE); // Phone No
            CreateBodyLine(MailTemplateNo, STRSUBSTNO('Phone 2 %1', GetElementNameValue(MailTemplateNo, 230, DATABASE::"Service Order")), TRUE); // Phone No 2
            CreateBodyLine(MailTemplateNo, STRSUBSTNO('Mobile %1', GetElementNameValue(MailTemplateNo, 220, DATABASE::"Service Order")), TRUE); // Mobile Phone No
            CreateBodyLine(MailTemplateNo, STRSUBSTNO('Contactperson %1', GetElementNameValue(MailTemplateNo, 32, DATABASE::"Service Order")), TRUE); // Contact Name
            CreateBodyLine(MailTemplateNo, STRSUBSTNO('Customer No. %1 %2', GetElementNameValue(MailTemplateNo, 20, DATABASE::"Service Order"), GetElementNameValue(MailTemplateNo, 2, DATABASE::Customer)), TRUE); // Customer No. Customer Name
            CreateBodyLine(MailTemplateNo, STRSUBSTNO('Service Order %1 %2', GetElementNameValue(MailTemplateNo, 10, DATABASE::"Service Order"), GetElementNameValue(MailTemplateNo, 150, DATABASE::"Service Order")), TRUE); // No. Problem
            CreateBodyLine(MailTemplateNo, STRSUBSTNO('Work Order %1', GetElementNameValue(MailTemplateNo, 10, DATABASE::"Work Order")), TRUE); // No.
            CreateBodyLine(MailTemplateNo, STRSUBSTNO('Your Reference %1', GetElementNameValue(MailTemplateNo, 30, DATABASE::"Service Order")), TRUE); // Your Reference
            CreateBodyLine(MailTemplateNo, STRSUBSTNO('Subcontractor %1', GetElementNameValue(MailTemplateNo, 495, DATABASE::"Service Order")), TRUE); // Subcontractor
            CreateBodyLine(MailTemplateNo, STRSUBSTNO('Starting Date %1 %2', GetElementNameValue(MailTemplateNo, 52, DATABASE::"Service Order"), GetElementNameValue(MailTemplateNo, 53, DATABASE::"Service Order")), TRUE); // Start Date Start Time
            CreateBodyLine(MailTemplateNo, STRSUBSTNO('Ending Date %1 %2', GetElementNameValue(MailTemplateNo, 54, DATABASE::"Service Order"), GetElementNameValue(MailTemplateNo, 55, DATABASE::"Service Order")), TRUE); // Ending Date Ending Time
            CreateWhitespaceLines(MailTemplateNo, 2);
            CreateBodyLine(MailTemplateNo, 'Description problem', TRUE);
            CreateBodyLine(MailTemplateNo, GetElementNameValue(MailTemplateNo, 2060, DATABASE::"Service Order"), TRUE); // Customer Problem
            CreateWhitespaceLines(MailTemplateNo, 2);
            CreateBodyLine(MailTemplateNo, 'Solution', TRUE);
            CreateBodyLine(MailTemplateNo, GetElementNameValue(MailTemplateNo, 170, DATABASE::"Service Order"), TRUE); // Solution
            CreateWhitespaceLines(MailTemplateNo, 3);
            CreateBodyLine(MailTemplateNo, 'Description Work', TRUE);
            CreateBodyLine(MailTemplateNo, 'Date      Time From       Time Up To        Hours       Travel Time       Route From          Route To        Distance Km', TRUE);
            CreateWhitespaceLines(MailTemplateNo, 5);
            CreateBodyLine(MailTemplateNo, 'Usage', TRUE);
            CreateBodyLine(MailTemplateNo, 'Quantity          Description           Price           Total', TRUE);
            CreateWhitespaceLines(MailTemplateNo, 9);
            CreateBodyLine(MailTemplateNo, 'Customer Approval', TRUE);
            CreateBodyLine(MailTemplateNo, 'Date', TRUE);
            CreateBodyLine(MailTemplateNo, 'Signature', TRUE);
            CreateWhitespaceLines(MailTemplateNo, 2);
            CreateBodyLine(MailTemplateNo, 'Approval', TRUE);
            CreateBodyLine(MailTemplateNo, 'Date', TRUE);
            CreateBodyLine(MailTemplateNo, 'Signature', TRUE);
            CreateWhitespaceLines(MailTemplateNo, 2);
          END;
        END ELSE
          IF GUIALLOWED THEN
            MESSAGE(Text001);
      END;
    END;

    LOCAL PROCEDURE "--- locals ---"@1100528300();
    BEGIN
    END;

    LOCAL PROCEDURE CreateTableAndFields@1100528303(MailTemplateNo@1100528300 : Integer;Level@1100528301 : Integer;TableNo@1100528302 : Integer;ParentTableNo@1100528305 : Integer);
    VAR
      MailTemplateTable@1100528304 : Record 11229453;
      Field@1100528303 : Record 2000000041;
    BEGIN
      IF NOT MailTemplateTable.GET(MailTemplateNo, Level, TableNo, 0) THEN BEGIN
        MailTemplateTable.INIT;
        MailTemplateTable."Mail Template No." := MailTemplateNo;
        MailTemplateTable.Level := Level;
        MailTemplateTable.VALIDATE("Table No.", TableNo);
        MailTemplateTable."Seq. No." := 0;
        IF ParentTableNo <> 0 THEN
          MailTemplateTable."Parent Table No." := ParentTableNo;
        MailTemplateTable.INSERT(TRUE);
      END;
      IF MailTemplateTable.GET(MailTemplateNo, Level, TableNo, 0) THEN BEGIN
        MailTemplateTable.CALCFIELDS(Fields);
        IF MailTemplateTable.Fields = 0 THEN BEGIN
          Field.SETRANGE(TableNo, TableNo);
          Field.SETRANGE(Class, Field.Class::Normal);
          Field.SETRANGE(Enabled, TRUE);
          Field.SETFILTER(Type, '<>%1&<>%2&<>%3&<>%4&<>%5', Field.Type::Duration, Field.Type::BLOB, Field.Type::Media, Field.Type::DateFormula, Field.Type::GUID);
          IF Field.FINDSET THEN
            REPEAT
              CreateField(MailTemplateNo, Field."No.", Field.FieldName, TableNo, Level);
            UNTIL Field.NEXT = 0;
        END;
      END;
    END;

    LOCAL PROCEDURE CreateTableLink@1100527614(TemplateNo@1100528300 : Integer;Level@1100527600 : Integer;Table1@1100527601 : Integer;Table2@1100527602 : Integer;Field1@1100527603 : Integer;Field2@1100527604 : Integer);
    VAR
      MailTemplateTableLink@1100527605 : Record 11229454;
      LastEntryNo@1100527606 : Integer;
    BEGIN
      LastEntryNo := -10000;
      MailTemplateTableLink.SETRANGE("Mail Template No.", TemplateNo);
      MailTemplateTableLink.SETRANGE("Table No.", Table1);
      IF MailTemplateTableLink.FINDLAST THEN
        LastEntryNo := MailTemplateTableLink."Table Seq. No.";
      MailTemplateTableLink.INIT;
      MailTemplateTableLink."Mail Template No." := TemplateNo;
      MailTemplateTableLink.Level := Level;
      MailTemplateTableLink.VALIDATE("Table No.", Table1);
      MailTemplateTableLink.VALIDATE("Field No.", Field1);
      MailTemplateTableLink."Table Seq. No." := LastEntryNo + 10000;
      MailTemplateTableLink.VALIDATE("Reference Table No.", Table2);
      MailTemplateTableLink.VALIDATE("Reference Field No.", Field2);
      MailTemplateTableLink.INSERT(TRUE);
    END;

    LOCAL PROCEDURE CreateTableFilter@1100528329(TemplateNo@1100528300 : Integer;Level@1100528301 : Integer;TableNo@1100528302 : Integer;FieldNo@1100528303 : Integer;Filter@1100528306 : Text);
    VAR
      MailTemplateTableFilter@1100528304 : Record 11229456;
      LastEntryNo@1100528305 : Integer;
    BEGIN
      LastEntryNo := -10000;
      MailTemplateTableFilter.SETRANGE("Mail Template No.", TemplateNo);
      MailTemplateTableFilter.SETRANGE("Table No.", TableNo);
      IF MailTemplateTableFilter.FINDLAST THEN
        LastEntryNo := MailTemplateTableFilter."Table Seq. No.";
      MailTemplateTableFilter.INIT;
      MailTemplateTableFilter."Mail Template No." := TemplateNo;
      MailTemplateTableFilter.Level := Level;
      MailTemplateTableFilter.VALIDATE("Table No.", TableNo);
      MailTemplateTableFilter.VALIDATE("Field No.", FieldNo);
      MailTemplateTableFilter."Table Seq. No." := LastEntryNo + 10000;
      MailTemplateTableFilter.Filter := Filter;
      MailTemplateTableFilter.INSERT(TRUE);
    END;

    LOCAL PROCEDURE CreateField@1100527612(TemplateNo@1100528300 : Integer;FieldNo@1100527600 : Integer;FieldName@1100527603 : Text;TableNo@1100527605 : Integer;Level@1100527606 : Integer);
    VAR
      MailTemplateTableField@1100527601 : Record 11229455;
      NewMailTemplateTableField@1100527602 : Record 11229455;
      LastSeqNo@1100527604 : Integer;
    BEGIN
      LastSeqNo := 0;
      MailTemplateTableField.SETRANGE("Mail Template No.", TemplateNo);
      MailTemplateTableField.SETRANGE("Table No.", TableNo);
      IF MailTemplateTableField.FINDLAST THEN
        LastSeqNo := MailTemplateTableField."Seq. No.";
      NewMailTemplateTableField."Mail Template No." := TemplateNo;
      NewMailTemplateTableField.Level := Level;
      NewMailTemplateTableField."Table No." := TableNo;
      NewMailTemplateTableField."Seq. No." := LastSeqNo + 1000;
      NewMailTemplateTableField.VALIDATE("Field No.", FieldNo);

      MailTemplateTableField.SETRANGE("Element Name", NewMailTemplateTableField."Element Name");
      IF NOT MailTemplateTableField.FINDFIRST THEN
        NewMailTemplateTableField.INSERT(TRUE);
    END;

    LOCAL PROCEDURE GetElementNameValue@1100527603(TemplateNo@1100528300 : Integer;FieldNo@1100527600 : Integer;TableNo@1100527602 : Integer) : Text;
    VAR
      MailTemplateTableField@1100527601 : Record 11229455;
    BEGIN
      MailTemplateTableField.SETRANGE("Mail Template No.", TemplateNo);
      MailTemplateTableField.SETRANGE("Table No.", TableNo);
      MailTemplateTableField.SETRANGE("Field No.", FieldNo);
      MailTemplateTableField.SETRANGE(Type, MailTemplateTableField.Type::Value);
      IF MailTemplateTableField.FINDFIRST THEN
        EXIT(STRSUBSTNO('_%%1%_', MailTemplateTableField."Element Name"));
    END;

    LOCAL PROCEDURE GetElementNameCaption@1100527606(TemplateNo@1100528300 : Integer;FieldNo@1100527600 : Integer;TableNo@1100527602 : Integer) : Text;
    VAR
      MailTemplateTableField@1100527601 : Record 11229455;
    BEGIN
      MailTemplateTableField.SETRANGE("Mail Template No.", TemplateNo);
      MailTemplateTableField.SETRANGE("Table No.", TableNo);
      MailTemplateTableField.SETRANGE("Field No.", FieldNo);
      MailTemplateTableField.SETRANGE(Type, MailTemplateTableField.Type::FieldCaption);
      IF MailTemplateTableField.FINDFIRST THEN
        EXIT(STRSUBSTNO('_%%1%_', MailTemplateTableField."Element Name"));
    END;

    LOCAL PROCEDURE CreateWhitespaceLines@1100528305(MailTemplateNo@1100528300 : Integer;NumberOfLines@1100528301 : Integer);
    VAR
      i@1100528302 : Integer;
    BEGIN
      FOR i := 0 TO NumberOfLines DO
        CreateBodyLine(MailTemplateNo, '', TRUE);
    END;

    LOCAL PROCEDURE CreateSubjectLine@1100527624(TemplateNo@1100528300 : Integer;Text@1100527600 : Text);
    VAR
      MailTemplateLayoutLine@1100527602 : Record 11229457;
      NewMailTemplateLayoutLine@1100527603 : Record 11229457;
      LastLineNo@1100527604 : Integer;
    BEGIN
      CreateLine(TemplateNo, Text, FALSE, MailTemplateLayoutLine."Layout Type"::Subject);
    END;

    LOCAL PROCEDURE CreateBodyLine@1100527630(TemplateNo@1100528300 : Integer;Text@1100527600 : Text;LineBreak@1100527601 : Boolean);
    VAR
      MailTemplateLayoutLine@1100527602 : Record 11229457;
      NewMailTemplateLayoutLine@1100527603 : Record 11229457;
      LastLineNo@1100527604 : Integer;
    BEGIN
      CreateLine(TemplateNo, Text, LineBreak, MailTemplateLayoutLine."Layout Type"::Body);
    END;

    LOCAL PROCEDURE CreateSignatureLine@1100527631(TemplateNo@1100528300 : Integer;Text@1100527600 : Text;LineBreak@1100527601 : Boolean);
    VAR
      MailTemplateLayoutLine@1100527602 : Record 11229457;
      NewMailTemplateLayoutLine@1100527603 : Record 11229457;
      LastLineNo@1100527604 : Integer;
    BEGIN
      CreateLine(TemplateNo, Text, LineBreak, MailTemplateLayoutLine."Layout Type"::Signature);
    END;

    LOCAL PROCEDURE CreateLine@1100527632(TemplateNo@1100528300 : Integer;Text@1100527600 : Text;LineBreak@1100527601 : Boolean;Type@1100527605 : Integer);
    VAR
      MailTemplateLayoutLine@1100527602 : Record 11229457;
      NewMailTemplateLayoutLine@1100527603 : Record 11229457;
      LastLineNo@1100527604 : Integer;
    BEGIN
      MailTemplateLayoutLine.SETRANGE("Mail Template No.", TemplateNo);
      LastLineNo := 0;
      IF MailTemplateLayoutLine.FINDLAST THEN
        LastLineNo := MailTemplateLayoutLine."Line No.";

      NewMailTemplateLayoutLine."Mail Template No." := TemplateNo;
      NewMailTemplateLayoutLine."Language Code" := '';
      NewMailTemplateLayoutLine."Layout Type" := Type;
      NewMailTemplateLayoutLine."Text Line" := Text;
      NewMailTemplateLayoutLine."Line Break" := LineBreak;
      NewMailTemplateLayoutLine."Line No." := MailTemplateLayoutLine."Line No." + 10000;
      NewMailTemplateLayoutLine.INSERT(TRUE);
    END;

    BEGIN
    END.
  }
}

