OBJECT Table 11072330 DPT Subsite Project
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               GetDescription;
             END;

    OnModify=BEGIN
               IF ("Subsite Structure" <> xRec."Subsite Structure") THEN
                 CheckRecordChangeAllowed;
             END;

    OnDelete=VAR
               DPTDocSetupPeripheralApp@1100528301 : Record 11072452;
               DPTSubsiteDocTypeProject@1100528300 : Record 11072331;
               DPTHeaderText@1100528303 : Record 11229943;
             BEGIN
               CheckRecordChangeAllowed;
               DPTSubsiteDocTypeProject.SETRANGE("Project No.", "Project No.");
               DPTSubsiteDocTypeProject.SETRANGE("Subsite Code", Code);
               DPTSubsiteDocTypeProject.DELETEALL(TRUE);

               DPTHeaderText.SETRANGE("Table No.", DATABASE::"DPT Subsite Project");
               DPTHeaderText.SETRANGE("Field No.", FIELDNO(Code));
               DPTHeaderText.SETRANGE("Source No.", "Project No.");
               DPTHeaderText.SETRANGE("Subsite Code", Code);
               DPTHeaderText.DELETEALL;

               DPTDocSetupPeripheralApp.SETRANGE("Project No.", "Project No.");
               DPTDocSetupPeripheralApp.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               ERROR(Text000);
             END;

    CaptionML=ENU=DPT Subsite (Project);
    LookupPageID=Page11229653;
    DrillDownPageID=Page11229653;
  }
  FIELDS
  {
    { 10  ;   ;Project No.         ;Code20        ;TableRelation=Job;
                                                   CaptionML=[ENU=Project No.;
                                                              NOR=Prosjektnr;
                                                              SVE=Projektnr];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 11  ;   ;Project Description ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Job.Description WHERE (No.=FIELD(Project No.)));
                                                   CaptionML=SVE=Projektbeskrivning;
                                                   Editable=No }
    { 20  ;   ;Code                ;Code20        ;OnValidate=BEGIN
                                                                GetDescription;
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 30  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 40  ;   ;Subsite Structure   ;Option        ;OnValidate=BEGIN
                                                                CheckRecordChangeAllowed;
                                                              END;

                                                   CaptionML=ENU=Subsite Structure;
                                                   OptionCaptionML=ENU=1 Document Library,Document Library per Document Type,Permission per Document Type;
                                                   OptionString=OneDocLibrary,DocLibraryPerDocType,PermissionPerDocType }
    { 50  ;   ;Quote Request Subsite;Boolean      ;CaptionML=ENU=Quote Request Subsite;
                                                   Editable=No }
    { 60  ;   ;Synchronized On     ;DateTime      ;CaptionML=ENU=Synchronized On;
                                                   Editable=No }
    { 70  ;   ;Template            ;Text50        ;TableRelation=IF (Template Type=CONST(Redirect)) "DPT Subsite Doc Type Project"."Document Type" WHERE (Project No.=FIELD(Project No.),
                                                                                                                                                          Subsite Code=FIELD(Code));
                                                   CaptionML=[ENU=Template;
                                                              NOR=Mal;
                                                              SVE=Mall] }
    { 80  ;   ;Template Type       ;Option        ;OnValidate=BEGIN
                                                                CheckTemplateType;
                                                              END;

                                                   CaptionML=[ENU=Template Type;
                                                              NOR=Maltype;
                                                              SVE=Malltyp];
                                                   OptionCaptionML=ENU=Default,Page,Redirect,Purchase Action;
                                                   OptionString=Default,Page,Redirect,PurchAction }
    { 90  ;   ;Use Document CheckIn/-Out;Boolean  ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                CheckRecordChangeAllowed;
                                                                SetUseDocumentVersionDocType;
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Manual Check-In/Check-Out }
    { 100 ;   ;Use Doc CheckIn/-Out Comp List;Boolean;
                                                   InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                CheckRecordChangeAllowed;
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Manual Check-In/Check-Out in Company Lists }
  }
  KEYS
  {
    {    ;Project No.,Code                        ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      OnlyAllowedOnceTxt@1100528301 : TextConst 'ENU=%1 %2 is only allowed once.';
      Text000@1100528300 : TextConst 'ENU=Rename is not allowed.;NOR=Gi nytt navn ikke tillatt;SVE=Att ge nytt namn „r inte till†tet.';

    PROCEDURE CreateAllSubsites@1100528302(ProjectNo@1100528300 : Code[20]);
    BEGIN
      CreateSubsites(ProjectNo, FALSE);
    END;

    PROCEDURE CreateQuoteRequestSubsite@1100528303(ProjectNo@1100528301 : Code[20]);
    BEGIN
      CreateSubsites(ProjectNo, TRUE);
    END;

    LOCAL PROCEDURE "---Locals---"@1100528300();
    BEGIN
    END;

    LOCAL PROCEDURE CreateSubsites@1100528305(ProjectNo@1100528300 : Code[20];OnlyQuoteSubsite@1100528301 : Boolean);
    VAR
      Job@1100528303 : Record 11072003;
      DPTDefaultSubsite@1100528304 : Record 11229941;
      DPTSubsiteProject@1100528302 : Record 11072330;
      DPTHeaderText@1100528305 : Record 11229943;
    BEGIN
      DPTDefaultSubsite.SETRANGE("Source Type", DPTDefaultSubsite."Source Type"::Project);
      IF (OnlyQuoteSubsite) THEN
        DPTDefaultSubsite.SETRANGE("Quote Request Subsite", TRUE)
      ELSE
        DPTDefaultSubsite.SETRANGE("Quote Request Subsite", FALSE);
      IF (NOT DPTDefaultSubsite.FINDSET) THEN
        EXIT;
      Job.GET(ProjectNo);

      REPEAT
        IF (NOT DPTSubsiteProject.GET(ProjectNo, DPTDefaultSubsite.Code)) THEN BEGIN
          DPTSubsiteProject.INIT;
          DPTSubsiteProject.Code := DPTDefaultSubsite.Code;
          DPTSubsiteProject."Project No." := ProjectNo;
          DPTSubsiteProject."Quote Request Subsite" := DPTDefaultSubsite."Quote Request Subsite";
          DPTSubsiteProject."Subsite Structure" := DPTDefaultSubsite."Subsite Structure";
          DPTSubsiteProject.Template := DPTDefaultSubsite.Template;
          DPTSubsiteProject."Template Type" := DPTDefaultSubsite."Template Type";
          DPTSubsiteProject."Use Doc CheckIn/-Out Comp List" := DPTDefaultSubsite."Use Doc CheckIn/-Out Comp List";
          DPTSubsiteProject."Use Document CheckIn/-Out" := DPTDefaultSubsite."Use Document CheckIn/-Out";
          DPTSubsiteProject.INSERT(TRUE);
          DPTHeaderText.CopySubsiteText(DATABASE::"DPT Subsite Project", DPTSubsiteProject.FIELDNO(Code),
            DPTSubsiteProject."Project No.", DPTSubsiteProject.Code);
        END;
      UNTIL (DPTDefaultSubsite.NEXT = 0);
    END;

    LOCAL PROCEDURE CheckRecordChangeAllowed@1100528301();
    VAR
      DPTInterface@1100528302 : Codeunit 11012451;
    BEGIN
      TESTFIELD("Synchronized On", 0DT);
      DPTInterface.SetDPTType(1);
      DPTInterface.CheckDocumentsLinkedToSubsiteDocumentType("Project No.", Code, '');
    END;

    LOCAL PROCEDURE CheckTemplateType@1100528307();
    VAR
      DPTSubsiteProject@1100528300 : Record 11072330;
    BEGIN
      IF ("Template Type" = "Template Type"::PurchAction) THEN BEGIN
        DPTSubsiteProject.SETRANGE("Project No.", "Project No.");
        DPTSubsiteProject.SETRANGE("Template Type", "Template Type"::PurchAction);
        IF (NOT DPTSubsiteProject.ISEMPTY) THEN
          ERROR(OnlyAllowedOnceTxt, FIELDCAPTION("Template Type"), "Template Type"::PurchAction);
      END;
    END;

    LOCAL PROCEDURE GetDescription@1100528306();
    VAR
      DPTDefaultSubsite@1100528300 : Record 11229941;
    BEGIN
      IF (NOT DPTDefaultSubsite.GET(DPTDefaultSubsite."Source Type"::Project, Code)) THEN
        DPTDefaultSubsite.INIT;
      IF (Description = '') THEN
        Description := DPTDefaultSubsite.Description;
    END;

    LOCAL PROCEDURE SetUseDocumentVersionDocType@1100528304();
    VAR
      DPTSubsiteDocTypeProject@1100528300 : Record 11072331;
    BEGIN
      DPTSubsiteDocTypeProject.SETRANGE("Project No.", "Project No.");
      DPTSubsiteDocTypeProject.SETRANGE("Subsite Code", Code);
      DPTSubsiteDocTypeProject.SETRANGE("Synchronized On", 0DT);
      DPTSubsiteDocTypeProject.MODIFYALL("Use Document CheckIn/-Out", "Use Document CheckIn/-Out", TRUE);
    END;

    BEGIN
    END.
  }
}

