OBJECT Table 1224 Data Exch. Mapping
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               DataExchFieldMapping.SETRANGE("Data Exch. Def Code","Data Exch. Def Code");
               DataExchFieldMapping.SETRANGE("Table ID","Table ID");
               DataExchFieldMapping.DELETEALL;
             END;

    OnRename=BEGIN
               IF HasFieldMappings THEN
                 ERROR(RenameErr);
             END;

    CaptionML=[ENU=Data Exch. Mapping;
               NOR=Tilordning for datautveksl.;
               SVE=Mappning av dataintegration];
  }
  FIELDS
  {
    { 1   ;   ;Data Exch. Def Code ;Code20        ;TableRelation="Data Exch. Def";
                                                   CaptionML=[ENU=Data Exch. Def Code;
                                                              NOR=Kode for datautveksl.def.;
                                                              SVE=Kod f”r dataintegrationsdefinition];
                                                   NotBlank=Yes }
    { 2   ;   ;Table ID            ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Table));
                                                   CaptionML=[ENU=Table ID;
                                                              NOR=Tabell-ID;
                                                              SVE=Tabell-ID];
                                                   NotBlank=Yes }
    { 3   ;   ;Name                ;Text250       ;CaptionML=[ENU=Name;
                                                              NOR=Navn;
                                                              SVE=Namn] }
    { 4   ;   ;Mapping Codeunit    ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Codeunit));
                                                   CaptionML=[ENU=Mapping Codeunit;
                                                              NOR=Codeunit for tilordning;
                                                              SVE=Mappningskodenhet] }
    { 6   ;   ;Data Exch. No. Field ID;Integer    ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table ID));
                                                   CaptionML=[ENU=Data Exch. No. Field ID;
                                                              NOR=Felt-ID for datautveksl.nr.;
                                                              SVE=F„lt-ID f”r dataintegrationsnr];
                                                   Description=The ID of the field in the target table that contains the Data Exchange No.. }
    { 7   ;   ;Data Exch. Line Field ID;Integer   ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table ID));
                                                   CaptionML=[ENU=Data Exch. Line Field ID;
                                                              NOR=Felt-ID for linje for datautveksl.;
                                                              SVE=F„lt-ID f”r dataintegrationsrad];
                                                   Description=The ID of the field in the target table that contains the Data Exchange Line No.. }
    { 8   ;   ;Data Exch. Line Def Code;Code20    ;TableRelation="Data Exch. Line Def".Code WHERE (Data Exch. Def Code=FIELD(Data Exch. Def Code));
                                                   CaptionML=[ENU=Data Exch. Line Def Code;
                                                              NOR=Kode for linjedef. for datautveksl.;
                                                              SVE=Def.kod f”r dataintegrationsrad];
                                                   NotBlank=Yes }
    { 9   ;   ;Pre-Mapping Codeunit;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Codeunit));
                                                   CaptionML=[ENU=Pre-Mapping Codeunit;
                                                              NOR=Codeunit for forh†ndstilordning;
                                                              SVE=F”rmappningskodenhet] }
    { 10  ;   ;Post-Mapping Codeunit;Integer      ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Codeunit));
                                                   CaptionML=[ENU=Post-Mapping Codeunit;
                                                              NOR=Codeunit for ettertilordning;
                                                              SVE=Eftermappningskodenhet] }
    { 20  ;   ;Use as Intermediate Table;Boolean  ;CaptionML=[ENU=Use as Intermediate Table;
                                                              NOR=Bruk som forel›pig tabell;
                                                              SVE=Anv„nd som cachelagringstabell] }
  }
  KEYS
  {
    {    ;Data Exch. Def Code,Data Exch. Line Def Code,Table ID;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DataExchFieldMapping@1000 : Record 1225;
      RecordNameFormatTok@1003 : TextConst 'ENU=%1 to %2;NOR=%1 til %2;SVE=%1 till %2';
      RenameErr@1004 : TextConst 'ENU=You cannot rename the record if one or more field mapping lines exist.;NOR=Du kan ikke gi posten nytt navn hvis det finnes ‚n eller flere felttilordningslinjer.;SVE=Du kan inte byta namn p† posten om en eller flera f„ltmappningsrader finns.';

    [External]
    PROCEDURE InsertRec@1(DataExchDefCode@1002 : Code[20];DataExchLineDefCode@1006 : Code[20];TableId@1003 : Integer;NewName@1001 : Text[250];MappingCodeunit@1004 : Integer;DataExchNoFieldId@1000 : Integer;DataExchLineFieldId@1005 : Integer);
    BEGIN
      INIT;
      VALIDATE("Data Exch. Def Code",DataExchDefCode);
      VALIDATE("Data Exch. Line Def Code",DataExchLineDefCode);
      VALIDATE("Table ID",TableId);
      VALIDATE(Name,NewName);
      VALIDATE("Mapping Codeunit",MappingCodeunit);
      VALIDATE("Data Exch. No. Field ID",DataExchNoFieldId);
      VALIDATE("Data Exch. Line Field ID",DataExchLineFieldId);
      INSERT;
    END;

    [External]
    PROCEDURE InsertRecForExport@2(DataExchDefCode@1002 : Code[20];DataExchLineDefCode@1006 : Code[20];TableId@1003 : Integer;NewName@1001 : Text[250];ProcessingCodeunit@1004 : Integer);
    BEGIN
      INIT;
      VALIDATE("Data Exch. Def Code",DataExchDefCode);
      VALIDATE("Data Exch. Line Def Code",DataExchLineDefCode);
      VALIDATE("Table ID",TableId);
      VALIDATE(Name,NewName);
      VALIDATE("Mapping Codeunit",ProcessingCodeunit);
      INSERT;
    END;

    [External]
    PROCEDURE InsertRecForImport@3(DataExchDefCode@1002 : Code[20];DataExchLineDefCode@1006 : Code[20];TableId@1003 : Integer;NewName@1001 : Text[250];DataExchNoFieldId@1000 : Integer;DataExchLineFieldId@1005 : Integer);
    BEGIN
      INIT;
      VALIDATE("Data Exch. Def Code",DataExchDefCode);
      VALIDATE("Data Exch. Line Def Code",DataExchLineDefCode);
      VALIDATE("Table ID",TableId);
      VALIDATE(Name,NewName);
      VALIDATE("Data Exch. No. Field ID",DataExchNoFieldId);
      VALIDATE("Data Exch. Line Field ID",DataExchLineFieldId);
      INSERT;
    END;

    [External]
    PROCEDURE CreateDataExchMapping@8(TableID@1000 : Integer;CodeunitID@1001 : Integer;DataExchNoFieldID@1002 : Integer;DataExchLineFieldID@1003 : Integer);
    BEGIN
      InsertRec("Data Exch. Def Code","Data Exch. Line Def Code",TableID,
        CreateName(TableID,"Data Exch. Def Code"),CodeunitID,DataExchNoFieldID,DataExchLineFieldID);
    END;

    LOCAL PROCEDURE CreateName@5(TableID@1000 : Integer;Code@1002 : Code[20]) : Text[250];
    VAR
      recRef@1001 : RecordRef;
    BEGIN
      recRef.OPEN(TableID);
      EXIT(STRSUBSTNO(RecordNameFormatTok,Code,recRef.CAPTION));
    END;

    LOCAL PROCEDURE HasFieldMappings@6() : Boolean;
    VAR
      DataExchFieldMapping@1000 : Record 1225;
    BEGIN
      DataExchFieldMapping.SETRANGE("Data Exch. Def Code","Data Exch. Def Code");
      DataExchFieldMapping.SETRANGE("Data Exch. Line Def Code","Data Exch. Line Def Code");
      DataExchFieldMapping.SETRANGE("Table ID",xRec."Table ID");
      DataExchFieldMapping.SETFILTER("Column No.",'<>%1',0);
      EXIT(NOT DataExchFieldMapping.ISEMPTY);
    END;

    [External]
    PROCEDURE PositivePayUpdateCodeunits@4() : Boolean;
    VAR
      DataExchDef@1000 : Record 1222;
      DataExchLineDef@1001 : Record 1227;
    BEGIN
      DataExchDef.SETRANGE(Code,"Data Exch. Def Code");
      IF DataExchDef.FINDFIRST THEN
        IF DataExchDef.Type = DataExchDef.Type::"Positive Pay Export" THEN BEGIN
          DataExchLineDef.SETRANGE("Data Exch. Def Code","Data Exch. Def Code");
          DataExchLineDef.SETRANGE(Code,"Data Exch. Line Def Code");
          IF DataExchLineDef.FINDFIRST THEN BEGIN
            CASE DataExchLineDef."Line Type" OF
              DataExchLineDef."Line Type"::Header:
                BEGIN
                  "Pre-Mapping Codeunit" := CODEUNIT::"Exp. Pre-Mapping Head Pos. Pay";
                  "Mapping Codeunit" := CODEUNIT::"Exp. Mapping Head Pos. Pay";
                END;
              DataExchLineDef."Line Type"::Detail:
                BEGIN
                  "Pre-Mapping Codeunit" := CODEUNIT::"Exp. Pre-Mapping Det Pos. Pay";
                  "Mapping Codeunit" := CODEUNIT::"Exp. Mapping Det Pos. Pay";
                END;
              DataExchLineDef."Line Type"::Footer:
                BEGIN
                  "Pre-Mapping Codeunit" := CODEUNIT::"Exp. Pre-Mapping Foot Pos. Pay";
                  "Mapping Codeunit" := CODEUNIT::"Exp. Mapping Foot Pos. Pay";
                END;
            END;
            EXIT(TRUE);
          END;
        END;

      IF DataExchDef.Type <> DataExchDef.Type::"Positive Pay Export" THEN BEGIN
        "Pre-Mapping Codeunit" := 0;
        "Mapping Codeunit" := 0;
      END;

      EXIT(FALSE);
    END;

    BEGIN
    END.
  }
}

