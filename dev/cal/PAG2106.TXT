OBJECT Page 2106 O365 Item Card
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Price;
               NOR=Pris;
               SVE=Pris];
    SourceTable=Table27;
    DataCaptionExpr=Description;
    PageType=Card;
    RefreshOnActivate=Yes;
    PromotedActionCategoriesML=[ENU=New,Process,Report,Item,History,Special Prices & Discounts,Approve,Request Approval,Details;
                                NOR=Ny,Prosess,Rapport,Vare,Historikk,Spesialpriser og -rabatter,Godkjenn,Be om godkjenning,Detaljer;
                                SVE=Ny,Process,Rapport,Artikel,Historik,Specialpriser och -rabatter,Godk„nn,Beg„r godk„nnande,Detaljer];
    OnInit=VAR
             O365SalesInitialSetup@6115 : Record 2110;
           BEGIN
             IsUsingVAT := O365SalesInitialSetup.IsUsingVAT;
             IsPhoneApp := ClientTypeManagement.GetCurrentClientType = CLIENTTYPE::Phone;
           END;

    OnOpenPage=BEGIN
                 IF Description = '' THEN
                   ItemCardStatus := ItemCardStatus::Delete
                 ELSE
                   ItemCardStatus := ItemCardStatus::Keep;
               END;

    OnNewRecord=BEGIN
                  OnNewRec;
                END;

    OnInsertRecord=BEGIN
                     IF Description = '' THEN
                       ItemCardStatus := ItemCardStatus::Delete
                     ELSE
                       ItemCardStatus := ItemCardStatus::Keep;

                     EXIT(TRUE);
                   END;

    OnModifyRecord=BEGIN
                     IF Description = '' THEN
                       ItemCardStatus := ItemCardStatus::Prompt
                     ELSE
                       ItemCardStatus := ItemCardStatus::Keep;

                     EXIT(TRUE);
                   END;

    OnQueryClosePage=BEGIN
                       EXIT(CanExitAfterProcessingItem);
                     END;

    OnAfterGetCurrRecord=VAR
                           VATProductPostingGroup@1000 : Record 324;
                           UnitOfMeasure@1001 : Record 204;
                         BEGIN
                           CreateItemFromTemplate;
                           IF VATProductPostingGroup.GET("VAT Prod. Posting Group") THEN
                             VATProductPostingGroupDescription := VATProductPostingGroup.Description;
                           IF UnitOfMeasure.GET("Base Unit of Measure") THEN
                             UnitOfMeasureDescription := UnitOfMeasure.GetDescriptionInCurrentLanguage;
                           IsPageEditable := CurrPage.EDITABLE;
                         END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                Name=Item;
                CaptionML=[ENU=Price;
                           NOR=Pris;
                           SVE=Pris];
                GroupType=Group }

    { 2   ;2   ;Field     ;
                Name=Description2;
                ToolTipML=[ENU=Specifies what you are selling.;
                           NOR=Angir hva du selger.;
                           SVE=Anger vad du s„ljer.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=Description;
                Visible=NOT IsPhoneApp }

    { 4   ;2   ;Field     ;
                Name=Description;
                ToolTipML=[ENU=Specifies what you are selling.;
                           NOR=Angir hva du selger.;
                           SVE=Anger vad du s„ljer.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=Description;
                Visible=IsPhoneApp;
                ShowCaption=No }

    { 38  ;2   ;Field     ;
                CaptionML=[ENU=Price;
                           NOR=Pris;
                           SVE=Pris];
                ToolTipML=[ENU=Specifies the price for one unit.;
                           NOR=Angir prisen for ‚n enhet.;
                           SVE=Anger pris f”r en styck.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr="Unit Price" }

    { 30  ;1   ;Group     ;
                CaptionML=[ENU=Details;
                           NOR=Detaljer;
                           SVE=Information];
                GroupType=Group }

    { 6   ;2   ;Field     ;
                CaptionML=[ENU=Price is per;
                           NOR=Pris er per;
                           SVE=Priset „r per];
                ToolTipML=[ENU=Specifies the price for one unit.;
                           NOR=Angir prisen for ‚n enhet.;
                           SVE=Anger pris f”r en styck.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                SourceExpr=UnitOfMeasureDescription;
                Editable=IsPageEditable;
                OnLookup=VAR
                           TempUnitOfMeasure@1000 : TEMPORARY Record 204;
                         BEGIN
                           TempUnitOfMeasure.CreateListInCurrentLanguage(TempUnitOfMeasure);
                           IF PAGE.RUNMODAL(PAGE::"O365 Units of Measure List",TempUnitOfMeasure) = ACTION::LookupOK THEN BEGIN
                             VALIDATE("Base Unit of Measure",TempUnitOfMeasure.Code);
                             UnitOfMeasureDescription := TempUnitOfMeasure.Description;
                           END;
                         END;

                QuickEntry=FALSE }

    { 8   ;2   ;Field     ;
                CaptionML=[ENU=Tax Group;
                           NOR=Mva-gruppe;
                           SVE=Skattegrupp];
                ToolTipML=[ENU=Specifies the tax group code for the tax-detail entry.;
                           NOR=Angir mva-gruppekoden for mva-detaljposten.;
                           SVE=Anger skatteuppgiftstransaktionens skattegruppskod.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                NotBlank=Yes;
                SourceExpr="Tax Group Code";
                Visible=NOT IsUsingVAT }

    { 13  ;2   ;Field     ;
                CaptionML=[ENU=VAT;
                           NOR=Merverdiavgift;
                           SVE=Moms];
                ToolTipML=[ENU=Specifies the VAT rate for this price.;
                           NOR=Angir mva-satsen for denne prisen.;
                           SVE=Anger det h„r prisets momssats.];
                ApplicationArea=#Basic,#Suite,#Invoicing;
                NotBlank=Yes;
                SourceExpr=VATProductPostingGroupDescription;
                Visible=IsUsingVAT;
                Editable=IsPageEditable;
                OnLookup=VAR
                           VATProductPostingGroup@1000 : Record 324;
                         BEGIN
                           IF PAGE.RUNMODAL(PAGE::"O365 VAT Product Posting Gr.",VATProductPostingGroup) = ACTION::LookupOK THEN BEGIN
                             VALIDATE("VAT Prod. Posting Group",VATProductPostingGroup.Code);
                             VATProductPostingGroupDescription := VATProductPostingGroup.Description;
                           END;
                         END;

                QuickEntry=FALSE }

  }
  CODE
  {
    VAR
      ClientTypeManagement@1007 : Codeunit 4030;
      ItemCardStatus@1000 : 'Keep,Delete,Prompt';
      ProcessNewItemOptionQst@1002 : TextConst 'ENU=Keep editing,Discard;NOR=Fortsett redigering,Forkast;SVE=Beh†ll redigering,Ignorera';
      ProcessNewItemInstructionTxt@1001 : TextConst 'ENU=Description is missing. Keep the price?;NOR=Beskrivelse mangler. Vil du beholde prisen?;SVE=Beskrivningen saknas. Vill du beh†lla priset?';
      VATProductPostingGroupDescription@1027 : Text[100];
      NewMode@1006 : Boolean;
      IsUsingVAT@6115 : Boolean;
      IsPageEditable@1003 : Boolean;
      UnitOfMeasureDescription@1004 : Text[50];
      IsPhoneApp@1005 : Boolean;

    LOCAL PROCEDURE OnNewRec@16();
    VAR
      DocumentNoVisibility@1000 : Codeunit 1400;
    BEGIN
      IF GUIALLOWED AND DocumentNoVisibility.ItemNoSeriesIsDefault THEN
        NewMode := TRUE;
    END;

    LOCAL PROCEDURE CanExitAfterProcessingItem@2() : Boolean;
    VAR
      Response@1000 : ',KeepEditing,Discard';
    BEGIN
      IF "No." = '' THEN
        EXIT(TRUE);

      IF ItemCardStatus = ItemCardStatus::Delete THEN BEGIN
        // workaround for bug: delete for new empty record returns false
        IF DELETE(TRUE) THEN;
        EXIT(TRUE);
      END;

      IF GUIALLOWED AND (ItemCardStatus = ItemCardStatus::Prompt) THEN
        CASE STRMENU(ProcessNewItemOptionQst,Response::KeepEditing,ProcessNewItemInstructionTxt) OF
          Response::Discard:
            EXIT(DELETE(TRUE));
          ELSE
            EXIT(FALSE);
        END;

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE CreateItemFromTemplate@3();
    VAR
      ItemTemplate@1001 : Record 1301;
      Item@1000 : Record 27;
      O365SalesManagement@1002 : Codeunit 2107;
    BEGIN
      IF NewMode THEN BEGIN
        IF ItemTemplate.NewItemFromTemplate(Item) THEN BEGIN
          COPY(Item);
          O365SalesManagement.SetItemDefaultValues(Item);
          CurrPage.UPDATE;
        END;
        ItemCardStatus := ItemCardStatus::Delete;
        NewMode := FALSE;
      END;
    END;

    BEGIN
    END.
  }
}

