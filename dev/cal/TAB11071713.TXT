OBJECT Table 11071713 Object Imp. Worksheet
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 ServSetup.GET;
                 ServSetup.TESTFIELD("Object Imp. Worksheet Nos.");
                 NoSeriesMgt.InitSeries(ServSetup."Object Imp. Worksheet Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;
             END;

    OnDelete=VAR
               ObjectImpWorksheetLine@1100525000 : Record 11071714;
             BEGIN
               ObjectImpWorksheetLine.SETRANGE("Worksheet No.", "No.");
               ObjectImpWorksheetLine.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=Object Import Worksheet;
               NOR=Kalkyleregneark for objektimport;
               SVE=Kalkylblad f”r objektimport];
    LookupPageID=Page11125250;
    DrillDownPageID=Page11125250;
  }
  FIELDS
  {
    { 10  ;   ;No.                 ;Code10        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  ServSetup.GET;
                                                                  NoSeriesMgt.TestManual(ServSetup."Object Imp. Worksheet Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              NOR=Nr;
                                                              SVE=Nr] }
    { 20  ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              NOR=Nr.serie;
                                                              SVE=Nr-serier];
                                                   Editable=No }
    { 30  ;   ;File Name           ;Text250       ;CaptionML=[ENU=File Name;
                                                              NOR=Filnavn;
                                                              SVE=Filnamn] }
    { 40  ;   ;Customer No.        ;Code20        ;TableRelation=Customer;
                                                   CaptionML=[ENU=Customer No.;
                                                              NOR=Kundenr;
                                                              SVE=Kundnr] }
    { 50  ;   ;Customer Name       ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Customer.Name WHERE (No.=FIELD(Customer No.)));
                                                   CaptionML=[ENU=Customer Name;
                                                              NOR=Kundnavn;
                                                              SVE=Betalarens namn];
                                                   Editable=No }
    { 55  ;   ;Import Type         ;Option        ;OnValidate=BEGIN
                                                                IF "Import Type" = "Import Type"::"Service Survey" THEN BEGIN
                                                                  "Update Existing Location" := FALSE;
                                                                  "Update Existing Object" := FALSE;
                                                                END;
                                                                UpdateTypeAndCodeInLines;
                                                              END;

                                                   CaptionML=[ENU=Import Type;
                                                              NOR=Importtype;
                                                              SVE=Import typ];
                                                   OptionCaptionML=ENU=Service Contract,Service Survey,Service Location + Object;
                                                   OptionString=Service Contract,Service Survey,Service Location+Object }
    { 60  ;   ;Code                ;Code20        ;TableRelation=IF (Import Type=CONST(Service Contract)) Service-Contract
                                                                 ELSE IF (Import Type=CONST(Service Survey)) "Service Survey" WHERE (Input Method=CONST(Object));
                                                   OnValidate=VAR
                                                                ServiceContract@1100526200 : Record 11012812;
                                                                ServiceSurvey@1100528600 : Record 11071803;
                                                                ObjectImpWorksheetLine@1100525000 : Record 11071714;
                                                              BEGIN
                                                                IF (xRec.Code <> '') AND (Code = '') THEN BEGIN
                                                                  ObjectImpWorksheetLine.SETRANGE("Worksheet No.", "No.");
                                                                  IF NOT ObjectImpWorksheetLine.ISEMPTY THEN
                                                                    ERROR(Text001);
                                                                END;
                                                                IF Code <> '' THEN
                                                                  IF "Import Type" = "Import Type"::"Service Location+Object" THEN
                                                                    FIELDERROR(Code);

                                                                IF "Customer No." = '' THEN BEGIN
                                                                  CASE "Import Type" OF
                                                                    "Import Type"::"Service Contract":
                                                                      IF ServiceContract.GET(Code) THEN BEGIN
                                                                        "Customer No." := ServiceContract."Customer No.";
                                                                        CALCFIELDS("Customer Name");
                                                                      END;
                                                                    "Import Type"::"Service Survey":
                                                                      IF ServiceSurvey.GET(Code) THEN BEGIN
                                                                        "Customer No." := ServiceSurvey."Customer No.";
                                                                        CALCFIELDS("Customer Name");
                                                                      END;
                                                                  END;
                                                                END;
                                                                UpdateTypeAndCodeInLines;
                                                              END;

                                                   OnLookup=VAR
                                                              ServiceContract@1210190000 : Record 11012812;
                                                              ServiceSurvey@1100528600 : Record 11071803;
                                                            BEGIN
                                                              CASE "Import Type" OF
                                                                "Import Type"::"Service Contract":
                                                                  BEGIN
                                                                    IF Code <> '' THEN
                                                                      IF ServiceContract.GET(Code) THEN;
                                                                    IF PAGE.RUNMODAL(0, ServiceContract) = ACTION::LookupOK THEN
                                                                      VALIDATE(Code, ServiceContract."No.");
                                                                  END;
                                                                "Import Type"::"Service Survey":
                                                                  BEGIN
                                                                    ServiceSurvey.SETRANGE("Input Method", ServiceSurvey."Input Method"::Object);
                                                                    IF ServiceSurvey.GET(Code) THEN;
                                                                    IF PAGE.RUNMODAL(0, ServiceSurvey) = ACTION::LookupOK THEN
                                                                      VALIDATE(Code, ServiceSurvey."No.");
                                                                  END;
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod] }
    { 70  ;   ;Company Name        ;Text30        ;TableRelation=Company;
                                                   OnValidate=VAR
                                                                Company@1100528600 : Record 2000000006;
                                                              BEGIN
                                                                IF "Company Name" = COMPANYNAME THEN
                                                                  "Company Name" := '';
                                                                IF "Company Name" <> '' THEN
                                                                  Company.GET("Company Name");
                                                              END;

                                                   CaptionML=[ENU=Company Name;
                                                              NOR=Navn p† selskap;
                                                              SVE=F”retagsnamn] }
    { 80  ;   ;Update Existing Location;Boolean   ;OnValidate=BEGIN
                                                                IF "Update Existing Location" THEN
                                                                  IF NOT ("Import Type" IN ["Import Type"::"Service Contract", "Import Type"::"Service Location+Object"]) THEN
                                                                    FIELDERROR("Import Type");
                                                              END;

                                                   CaptionML=ENU=Update Existing Location }
    { 90  ;   ;Update Existing Object;Boolean     ;OnValidate=BEGIN
                                                                IF "Update Existing Object" THEN
                                                                  IF NOT ("Import Type" IN ["Import Type"::"Service Contract", "Import Type"::"Service Location+Object"]) THEN
                                                                    FIELDERROR("Import Type");
                                                              END;

                                                   CaptionML=ENU=Update Existing Object }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ServSetup@1100526200 : Record 11012800;
      NoSeriesMgt@1100526201 : Codeunit 396;
      Text000@1100526202 : TextConst 'ENU=File %1 not found;NOR=Fil %1 ikke funnet;SVE=Fil %1 hittades inte';
      Text001@1100525000 : TextConst 'ENU=There are lines present, modifying nog allowed;NOR=Det finnes rader, endring er ikke tillatt;SVE=Det finns rader, „ndra nod „r till†tet';
      Text002@1100528500 : TextConst 'ENU=Select a csv file...';
      Text003@1100528501 : TextConst 'ENU=Csv files (*.csv)|*.csv';

    PROCEDURE AssistEdit@2(OldObjectImpWorksheet@1000 : Record 11071713) : Boolean;
    VAR
      ObjectImpWorksheet@1100526200 : Record 11071713;
    BEGIN
      ServSetup.GET;
      ServSetup.TESTFIELD("Object Imp. Worksheet Nos.");
      ObjectImpWorksheet := Rec;
      IF NoSeriesMgt.SelectSeries(ServSetup."Object Imp. Worksheet Nos.", OldObjectImpWorksheet."No. Series","No. Series") THEN
      BEGIN
        ServSetup.GET;
        NoSeriesMgt.SetSeries("No.");
        Rec := ObjectImpWorksheet;
        EXIT(TRUE);
      END;
    END;

    PROCEDURE Import@1100526200();
    VAR
      ObjectImpWorksheetLine@1100526201 : Record 11071714;
      TempBlob@1100525002 : Record 99008535;
      FileMgt@1100525000 : Codeunit 419;
      ClientTypeManagement@1100528500 : Codeunit 4;
      ImportServiceObjects@1100525001 : XMLport 11012800;
      IStream@1100525003 : InStream;
      FromFile@1100528501 : Text;
    BEGIN
      IF "Import Type" <> "Import Type"::"Service Location+Object" THEN
        TESTFIELD(Code);
      ImportServiceObjects.SetDefaults("No.");
      IF NOT ClientTypeManagement.IsCommonWebClientType THEN BEGIN
        TESTFIELD("File Name");
        IF NOT FileMgt.ClientFileExists("File Name") THEN
          ERROR(Text000, "File Name");
        FileMgt.BLOBImport4PS(TempBlob, "File Name", FALSE);
        TempBlob.Blob.CREATEINSTREAM(IStream);
      END ELSE
        IF NOT UPLOADINTOSTREAM(Text002, '', Text003, FromFile, IStream) THEN
          ERROR('');

      ImportServiceObjects.SETSOURCE(IStream);
      ImportServiceObjects.IMPORT;

      ObjectImpWorksheetLine.SETRANGE("Worksheet No.", "No.");
      ObjectImpWorksheetLine.SETRANGE("Create Location", TRUE);
      IF ObjectImpWorksheetLine.FINDSET(TRUE) THEN
        REPEAT
          ObjectImpWorksheetLine.Normalize(FALSE);
          ObjectImpWorksheetLine.MODIFY;
        UNTIL ObjectImpWorksheetLine.NEXT = 0;

      ObjectImpWorksheetLine.SETRANGE("Create Location", FALSE);
      IF ObjectImpWorksheetLine.FINDSET(TRUE) THEN
        REPEAT
          ObjectImpWorksheetLine.Normalize(FALSE);
          ObjectImpWorksheetLine.MODIFY;
        UNTIL ObjectImpWorksheetLine.NEXT = 0;
    END;

    PROCEDURE UpdateTypeAndCodeInLines@1100528600();
    VAR
      ObjectImpWorksheetLine@1100528600 : Record 11071714;
    BEGIN
      ObjectImpWorksheetLine.SETRANGE("Worksheet No.", "No.");
      ObjectImpWorksheetLine.MODIFYALL(Code, '');
      ObjectImpWorksheetLine.MODIFYALL("Import Type", "Import Type");
      ObjectImpWorksheetLine.MODIFYALL(Code, Code);
    END;

    BEGIN
    END.
  }
}

