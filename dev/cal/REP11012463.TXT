OBJECT Report 11012463 Import Design Provisional Work
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Design Zugeordnete Arbeit importieren;
               ENU=Import Design Provisional Work;
               NLD=Tekeninglijst voorlopig werk importeren;
               SVE=Imp. provisoriskt arb. design];
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  FileMgt@1100528704 : Codeunit 419;
                  ImportFile@1100528705 : File;
                  ServerFileName@1100528703 : Text[1024];
                  Line@1100528701 : Text[1024];
                  SplittedArr@1100528707 : ARRAY [10] OF Text[250];
                  ProvisionalWorkSpec@1100528706 : Record 11012775;
                BEGIN
                  IF ImportFileName = '' THEN
                    CurrReport.QUIT;

                  ServerFileName := FileMgt.UploadFileSilent(ImportFileName);
                  ImportFileName := ServerFileName;

                  ImportFile.TEXTMODE := TRUE;
                  ImportFile.WRITEMODE := FALSE;
                  ImportFile.OPEN(ImportFileName);

                  WHILE ImportFile.POS < ImportFile.LEN DO BEGIN
                    ImportFile.READ(Line);

                    StringFunctions.SplitString(Line, ';', SplittedArr);

                    ProvisionalWorkSpec.INIT;
                    ProvisionalWorkSpec."Provisional Work No." := ProvisionalWorkNo;
                    ProvisionalWorkSpec."Specification Type" := ProvisionalWorkSpec."Specification Type"::Design;
                    ProvisionalWorkSpec."Line No." := ProvisionalWorkSpec.GetNextFreeLineNo;
                    ProvisionalWorkSpec.Code := SplittedArr[1];
                    ProvisionalWorkSpec."No." := SplittedArr[2];
                    ProvisionalWorkSpec."Revision Date" := TextToDate(SplittedArr[3]);
                    ProvisionalWorkSpec."Revision No." := SplittedArr[4];
                    ProvisionalWorkSpec.Description := SplittedArr[5];
                    ProvisionalWorkSpec."Design Type" := SplittedArr[6];
                    ProvisionalWorkSpec."Design Format" := SplittedArr[7];
                    ProvisionalWorkSpec.Date := TextToDate(SplittedArr[8]);
                    ProvisionalWorkSpec.INSERT(TRUE);
                  END;

                  ImportFile.CLOSE;

                  ERASE(ServerFileName);
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525001;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100525002;2;Field  ;
                  CaptionML=[DEU=Dateiname;
                             ENU=File Name;
                             NLD=Bestandsnaam;
                             NOR=Filnavn;
                             SVE=Filnamn];
                  SourceExpr=ImportFileName;
                  OnAssistEdit=VAR
                                 FileManagement@1100525000 : Codeunit 419;
                                 FilterString@1100525001 : Text;
                               BEGIN
                                 FilterString := FileManagement.GetToFilterText('', '.csv');
                                 ImportFileName := FileManagement.OpenFileDialog('',ImportFileName,FilterString);
                               END;
                                }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ImportFileName@1100528701 : Text[250];
      ProvisionalWorkNo@1100528700 : Code[20];
      StringFunctions@1100528703 : Codeunit 11012273;

    PROCEDURE SetParameters@1100528700(NewProvisionalWorkNo@1100528700 : Code[20]);
    BEGIN
      ProvisionalWorkNo := NewProvisionalWorkNo;
    END;

    PROCEDURE TextToDate@1100528701(Text@1000000 : Text[1024]) Result : Date;
    BEGIN
      IF STRLEN(Text) >= 8 THEN
        EVALUATE(Result, COPYSTR(Text,7,2) + COPYSTR(Text,5,2) + COPYSTR(Text,1,4))
      ELSE
        EXIT(0D);
    END;

    BEGIN
    {
      Fields
      Code
      No.
      Revision Date YYYYMMDD
      Revision No.
      Description
      Design Type
      Design Format
      Date          YYYYMMDD
    }
    END.
  }
  RDLDATA
  {
  }
}

