OBJECT Page 11126603 BarCode Service Item
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=BarCodeItemService;
               ENU=BarCodeItemService;
               NLD=Afgifte materiaal op serviceorder];
    PageType=NavigatePage;
    ShowFilter=No;
    OnOpenPage=VAR
                 ActiveSession@1100525000 : Record 2000000110;
               BEGIN
                 ActiveSession.SETRANGE("User ID", USERID);
                 IF ActiveSession.COUNT > 1 THEN
                   ERROR(Text001, USERID);

                 CompanyInfo.GET;
                 UserInfo.GET(USERID);
                 //Location from default
                 LocationNo := UserMgt.GetLocation(0,'',UserMgt.GetSalesFilter);
                 LocationRec.GET(LocationNo);
                 InvSetupRec.GET;
                 ScannerRec.RESET;
                 ScannerRec.SETRANGE(ScannerID, COPYSTR(USERID, 1, MAXSTRLEN(ScannerRec.ScannerID)));
                 ScannerRec.SETRANGE(Type, ScannerRec.Type::"Service Delivery");
                 ScannerRec.SETRANGE(Status, 0);
                 ScannerRec.SETFILTER("Service Order", '<>%1', '');
                 IF ScannerRec.FINDFIRST THEN BEGIN
                   LocationNo := ScannerRec."Location Code";
                   ServiceOrderNo := ScannerRec."Service Order";
                 END;
                 ScannerRec.RESET;
               END;

    ActionList=ACTIONS
    {
      { 1100525005;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100525007;1 ;Action    ;
                      Name=Lijst;
                      CaptionML=[DEU=Liste;
                                 ENU=Lijst;
                                 NLD=Lijst];
                      InFooterBar=Yes;
                      Image=List;
                      OnAction=VAR
                                 BarCodeItemList@1100525000 : Page 11126609;
                               BEGIN
                                 ScannerRec.RESET;
                                 ScannerRec.SETRANGE(ScannerID, COPYSTR(USERID, 1, MAXSTRLEN(ScannerRec.ScannerID)));
                                 ScannerRec.SETRANGE(Type, ScannerRec.Type::"Service Delivery");
                                 ScannerRec.SETRANGE(Status, 0);

                                 BarCodeItemList.SETTABLEVIEW(ScannerRec);
                                 BarCodeItemList.LOOKUPMODE(TRUE);
                                 BarCodeItemList.EDITABLE(TRUE);
                                 BarCodeItemList.RUNMODAL;

                                 ScannerRec.RESET;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100525000;;Container;
                Name=top;
                ContainerType=ContentArea }

    { 1100525001;1;Field  ;
                Name=CtrlLocationNo;
                CaptionML=[DEU=Lagerort;
                           ENU=Location;
                           NLD=Magazijn;
                           NOR=Plassering;
                           SVE=LagerstÑlle];
                SourceExpr=LocationNo;
                OnValidate=BEGIN
                             IF NOT LocationRec.GET(LocationNo) THEN BEGIN
                               ERROR('Magazijn onbekend');
                             END;
                           END;

                OnLookup=BEGIN
                           //**4PS.AH Enter customer specific number for 'Location List'
                           IF PAGE.RUNMODAL(11126610, LocationRec) = ACTION::LookupOK THEN BEGIN
                             LocationNo := LocationRec.Code;
                             LocationRec.GET(LocationNo);
                           END;
                         END;

                QuickEntry=FALSE }

    { 1100525002;1;Field  ;
                Name=CntrlServOrderNo;
                CaptionML=[DEU=Serviceauftrag;
                           ENU=Service Order;
                           NLD=Serviceorder;
                           NOR=Serviceordre;
                           SVE=Serviceorder];
                SourceExpr=ServiceOrderNo;
                OnValidate=BEGIN
                             ScannerRec.RESET;
                             ScannerRec.SETRANGE(ScannerID,  COPYSTR(USERID, 1, MAXSTRLEN(ScannerRec.ScannerID)));
                             ScannerRec.SETRANGE(Type, ScannerRec.Type::"Service Delivery");
                             ScannerRec.SETRANGE(Status, 0);
                             ScannerRec.SETFILTER("Service Order", '<>%1', ServiceOrderNo);
                             IF ScannerRec.FINDFIRST THEN
                               ERROR(Text002, ScannerRec."Service Order");

                             ServiceOrderRec.RESET;
                             ServiceOrderRec.SETRANGE(Status, ServiceOrderRec.Status::"In Process", ServiceOrderRec.Status::"Technical Finished");
                             ServiceOrderRec.SETRANGE("No.", ServiceOrderNo);
                             IF NOT ServiceOrderRec.FIND('-') THEN BEGIN
                               ERROR('Serviceorder onbekend');
                             END;
                           END;

                OnLookup=BEGIN
                           ScannerRec.RESET;
                           ScannerRec.SETRANGE(ScannerID,  COPYSTR(USERID, 1, MAXSTRLEN(ScannerRec.ScannerID)));
                           ScannerRec.SETRANGE(Type, ScannerRec.Type::"Service Delivery");
                           ScannerRec.SETRANGE(Status, 0);
                           IF ScannerRec.COUNT <> 0 THEN
                             EXIT;
                           ScannerRec.RESET;

                           ServiceOrderRec.RESET;
                           ServiceOrderRec.SETRANGE(Status, ServiceOrderRec.Status::"In Process", ServiceOrderRec.Status::"Technical Finished");
                           //**4PS.AH Enter customer specific formnumber for 'Serv.-Order List'
                           IF PAGE.RUNMODAL(11126615, ServiceOrderRec) = ACTION::LookupOK THEN BEGIN
                             ServiceOrderNo := ServiceOrderRec."No." ;
                             ServiceOrderRec.GET(ServiceOrderNo);
                           END;
                         END;

                QuickEntry=FALSE }

    { 1100525009;1;Field  ;
                CaptionML=[DEU=Auftraggeber;
                           ENU=Principal;
                           NLD=Opdrachtgever;
                           NOR=Oppdragsgiver;
                           SVE=Uppdragsgivare];
                SourceExpr=ServiceOrderRec.Name;
                Enabled=FALSE;
                Editable=FALSE;
                QuickEntry=FALSE }

    { 1100525003;1;Field  ;
                Name=CntrlItemCode;
                CaptionML=[DEU=Artikelnr.;
                           ENU=Item No.;
                           NLD=Artikelnr.;
                           NOR=Varenr.;
                           SVE=Artikelnr];
                SourceExpr=ItemNo;
                OnValidate=BEGIN
                             IF ItemRec.GET(ItemNo) THEN
                                ItemDesc := ItemRec.Description
                             ELSE BEGIN
                               ItemDesc := '';
                               ItemNo := '';
                               ERROR('Artikel onbekend');
                             END;

                             IF (ItemRec.Blocked) AND (NOT ErrorSet) THEN BEGIN
                               ERROR(STRSUBSTNO('%1 is geblokkeerd', ItemNo));
                               ItemDesc := '';
                               ItemNo := '';
                             END;
                           END;

                OnLookup=BEGIN
                           ItemRec.RESET;
                           ItemRec.SETRANGE(Blocked, FALSE);
                           //**4PS.AH Enter customer specific number for 'Item List'
                           IF PAGE.RUNMODAL(11126614, ItemRec) = ACTION::LookupOK THEN
                             ItemNo := ItemRec."No." ;

                           ItemRec.RESET;

                           ItemRec.GET(ItemNo);
                           ItemDesc := ItemRec.Description;
                         END;

                QuickEntry=TRUE }

    { 1100525011;1;Field  ;
                CaptionML=[DEU=Beschreibung;
                           ENU=Description;
                           NLD=Omschrijving;
                           NOR=Beskrivelse;
                           SVE=Beskrivning];
                SourceExpr=ItemRec.Description;
                Enabled=FALSE;
                Editable=FALSE;
                QuickEntry=FALSE }

    { 1100525004;1;Field  ;
                Name=CntrlQuantity;
                CaptionML=[DEU=Menge;
                           ENU=Quantity;
                           NLD=Hoeveelheid;
                           NOR=Antall;
                           SVE=Antal];
                SourceExpr=Quantity;
                OnValidate=BEGIN
                             IF Quantity <> 0 THEN
                               AddScanLine;
                           END;

                QuickEntry=TRUE }

    { 1100525008;1;Field  ;
                CaptionML=[DEU=Basiseinheitencode;
                           ENU=Base Unit of Measure;
                           NLD=Basiseenheid;
                           NOR=Lagerenhet;
                           SVE=Basenhet];
                SourceExpr=ItemRec."Base Unit of Measure";
                Enabled=FALSE;
                Editable=FALSE;
                QuickEntry=FALSE }

  }
  CODE
  {
    VAR
      CompanyInfo@1100525029 : Record 79;
      UserInfo@1100525028 : Record 91;
      InvSetupRec@1100525027 : Record 313;
      LocationRec@1100525026 : Record 14;
      ScannerRec@1100525025 : Record 11012061;
      ItemRec@1100525024 : Record 27;
      ServiceOrderRec@1100525008 : Record 11012823;
      UserMgt@1100525032 : Codeunit 5700;
      LocationNo@1100525017 : Code[20];
      ItemNo@1100525016 : Code[20];
      ServiceOrderNo@1100525015 : Code[20];
      Quantity@1100525014 : Decimal;
      ItemDesc@1100525013 : Text[100];
      LineNumber@1100525012 : Integer;
      ErrorSet@1100525007 : Boolean;
      Text001@1100525030 : TextConst 'DEU=Mehr als eine Sitzung fÅr %1 geîffnet;ENU=More than one session for %1 open;NLD=Meerdere sessies voor %1 geopend';
      Text002@1100528900 : TextConst 'DEU=Die Zeilen von Serviceauftrag %1 zuerst verarbeiten.;ENU=Process the lines of service order %1 first.;NLD=Eerst de regels op Serviceorder: %1 afhandelen.';

    PROCEDURE AddScanLine@1100409002();
    BEGIN
      IF NOT LocationRec.GET(LocationNo) THEN BEGIN
        EXIT;
      END;

      IF (ServiceOrderNo = '') OR
         (ItemNo = '') OR
         (Quantity = 0) THEN BEGIN
        EXIT;
      END;

      ScannerRec.INIT;
      ScannerRec.RESET;
      ScannerRec.ScannerID := COPYSTR(USERID, 1, MAXSTRLEN(ScannerRec.ScannerID));
      ScannerRec.Type := ScannerRec.Type::"Service Delivery";
      ScannerRec.Date := TODAY;
      ScannerRec."Line No." := '1';
      ScannerRec."Location Code" := LocationNo;
      ScannerRec."Service Order" := ServiceOrderNo;
      ScannerRec."Item No." := ItemNo;
      ScannerRec.Quantity := Quantity;

      REPEAT
        EVALUATE(LineNumber, ScannerRec."Line No.");
        LineNumber += 1;
        ScannerRec."Line No." := FORMAT(LineNumber);
      UNTIL ScannerRec.INSERT(TRUE);

      ItemNo := '';
      ItemDesc := '';
      Quantity := 0;

      CLEAR(ItemRec);
      CLEAR(ScannerRec);
      CLEAR(LocationRec);
      {
      IF ISCLEAR(WSHShell) THEN
        CREATE(WSHShell,FALSE,TRUE);
      WSHShell.SendKeys('+{TAB}');   // <SHIFT><TAB>
      WSHShell.SendKeys('+{TAB}');   // <SHIFT><TAB> to itemcode
      CLEAR(WSHShell);
      }
    END;

    BEGIN
    END.
  }
}

