OBJECT XMLport 11012115 Imp. Open Entries Customers
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=Imp. Open Entries Customers;
    DefaultFieldsValidation=No;
    PreserveWhiteSpace=Yes;
    TextEncoding=WINDOWS;
    OnInitXMLport=BEGIN
                    XmlPortID := 11012115;
                    MainTableID := 81;
                    XMLPortRequestPageID := 11127326;
                  END;

    OnPreXMLport=VAR
                   ConversionXMLPort4PS@1100527700 : Record 11020640;
                 BEGIN
                   IF NOT currXMLport.IMPORTFILE THEN BEGIN
                     AddConversionFileFormatLine;
                     ConversionXMLPort4PS.UpdateFromXMLPort(XmlPortID,MainTableID,MainTableLookupPageID,XMLPortRequestPageID,TRUE,ConversionXMLPort."Journal Template Type"::Sales);
                     COMMIT;
                     ERROR('');
                   END;

                   ConversionXMLPort.GET(XmlPortID);

                   ConvMgt.DeleteConComment(XmlPortID);
                   ConvMgt.CreateConComment(XmlPortID,STRSUBSTNO('Import File: %1.',currXMLport.FILENAME));
                   ConvMgt.CreateConComment(XmlPortID,'Start');
                 END;

    Format=Variable Text;
    FieldDelimiter=<None>;
    FieldSeparator=[;];
  }
  ELEMENTS
  {
    { [{7B539476-C20A-46BC-A552-62FED55766AD}];  ;Root                ;Element ;Text     }

    { [{A667E51D-CE85-4A0D-946E-DE2CCC52ED51}];1 ;GeneralJournalLine  ;Element ;Table   ;
                                                  SourceTable=Table81;
                                                  SourceTableView=SORTING(Field1,Field51,Field2);
                                                  Import::OnBeforeInsertRecord=VAR
                                                                                 CustCde@1100409000 : Code[20];
                                                                                 GenJournalTemplate@1100409001 : Record 80;
                                                                               BEGIN
                                                                                 IF AllInputFieldsAreEmpty THEN
                                                                                   currXMLport.SKIP;

                                                                                 CLEAR("Gen. Journal Line");

                                                                                 WITH "Gen. Journal Line" DO BEGIN

                                                                                   IF PostingDateFromImportFile AND (JDate <> 0D) THEN
                                                                                     "Posting Date" := JDate
                                                                                   ELSE
                                                                                     "Posting Date" := ConversionXMLPort."Posting Date";

                                                                                   CustCde := ConvMgt.DetermineNewCode(
                                                                                     LookupCustomerNo,
                                                                                     CommentRec."Table Name"::Customer,
                                                                                     CustCommentCde,
                                                                                     B);

                                                                                   ConvMgt.GenerateGenJnlLine(
                                                                                     ConversionXMLPort."Journal Template Name",
                                                                                     ConversionXMLPort."Journal Batch Name",
                                                                                     "Posting Date",
                                                                                     A,
                                                                                     ConversionXMLPort."Balance Account No.",
                                                                                     "Gen. Journal Line");

                                                                                   XmlShowStatus.ShowImportCounter;
                                                                                   ImpLineCounter := ImpLineCounter + 1;

                                                                                   VALIDATE("Document Type","Document Type"::Invoice);
                                                                                   IF FDec < 0 THEN
                                                                                     VALIDATE("Document Type","Document Type"::"Credit Memo");
                                                                                   IF EDec = 0 THEN BEGIN
                                                                                     IF EDec > 0 THEN
                                                                                       VALIDATE("Document Type","Document Type"::Refund)
                                                                                     ELSE
                                                                                       VALIDATE("Document Type","Document Type"::Payment)
                                                                                   END;

                                                                                   VALIDATE("Account Type","Account Type"::Customer);
                                                                                   IF (CustCde <> '') AND ConvMgt.IsCustomerPresent(CustCde) THEN
                                                                                     VALIDATE("Account No.",CustCde)
                                                                                   ELSE
                                                                                     ConvMgt.CreateConComment(XmlPortID,ConvMgt.CreateFieldNotFilledCommentTxt(
                                                                                        18,
                                                                                       B,
                                                                                       FIELDCAPTION("Account No."),
                                                                                       MainTableID,
                                                                                       STRSUBSTNO('%1-%2-%3',"Journal Template Name","Journal Batch Name","Line No.")));

                                                                                   "Source Code" := GenJournalTemplate."Source Code";

                                                                                   VALIDATE(Amount,FDec);
                                                                                   IF (H <> '') AND (IDec <> 0) THEN BEGIN
                                                                                     VALIDATE("Currency Code",H);
                                                                                     "Currency Factor" := IDec/FDec;
                                                                                     VALIDATE(Amount,IDec);
                                                                                   END;
                                                                                   //C037921
                                                                                   IF (H = '') AND ("Currency Code" <> '') THEN
                                                                                     VALIDATE("Currency Code",H);

                                                                                   VALIDATE(Description,(STRSUBSTNO(Text20,B,E)));
                                                                                   VALIDATE("Shortcut Dimension 1 Code",G);
                                                                                   VALIDATE("Document Date",CDate);
                                                                                   VALIDATE("Due Date",DDate);

                                                                                   ConvMgt.ClearGJNLPostingFields("Gen. Journal Line");

                                                                                   MODIFY(TRUE);
                                                                                 END;

                                                                                 ClearFields;
                                                                                 currXMLport.SKIP;
                                                                               END;
                                                                                }

    { [{3AC77EC7-9326-4B76-BA5F-EA1F70D4DA67}];2 ;A                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=20 }

    { [{1B889F9B-B9E2-4749-96B0-D22BA1117049}];2 ;B                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=50 }

    { [{D76B46B3-243B-4FF7-8D1D-C7B64CCFA492}];2 ;C                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  IF C <> '' THEN
                                                                                    EVALUATE(CDate,C);
                                                                                END;

                                                  Width=50 }

    { [{11AAC24C-6612-4FE6-9973-FE5EF2EF2C43}];2 ;D                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  IF D <> '' THEN
                                                                                    EVALUATE(DDate,D);
                                                                                END;

                                                  Width=50 }

    { [{F0633544-507F-4C5E-A35B-A0DD95F11E63}];2 ;E                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  IF E <> '' THEN
                                                                                    EVALUATE(EDec,E);
                                                                                END;

                                                  Width=50 }

    { [{98E7308F-19D5-4AF6-89FB-48DB78E52124}];2 ;F                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  IF F <> '' THEN
                                                                                    EVALUATE(FDec,F);
                                                                                END;

                                                  Width=50 }

    { [{23B2F6EB-78F9-48FB-BC70-858F6A53A74C}];2 ;G                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=50 }

    { [{007CC416-7129-43EA-90B0-193FFE5FCA68}];2 ;H                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=50 }

    { [{8F9BF873-E7E2-450A-B9C5-33703DAFE6BE}];2 ;I                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  IF I <> '' THEN
                                                                                    EVALUATE(IDec,I);
                                                                                END;

                                                  Width=50 }

    { [{17D83AA6-D251-4A7A-8475-309C7FF2822C}];2 ;J                   ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  IF J <> '' THEN
                                                                                    EVALUATE(JDate,J);
                                                                                END;
                                                                                 }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100409000;;Container;
                  Name=Options;
                  ContainerType=ContentArea }

      { 1100409001;1;Field  ;
                  Name=LookupCustomerNo;
                  CaptionML=ENU=Find Customer by Old Customer No;
                  SourceExpr=LookupCustomerNo;
                  OnValidate=BEGIN
                               IF NOT LookupCustomerNo THEN
                                 CustCommentCde := '';
                             END;
                              }

      { 1100409002;1;Field  ;
                  Name=CustCommentCde;
                  CaptionML=ENU=Comment Code old Customer;
                  SourceExpr=CustCommentCde;
                  TableRelation="Comment Code" }

      { 1100525000;1;Field  ;
                  Name=PostingDateFromImportFile;
                  CaptionML=ENU=Use Import Date;
                  SourceExpr=PostingDateFromImportFile }

    }
  }
  CODE
  {
    VAR
      ConversionXMLPort@1100409001 : Record 11020640;
      XmlPortID@1100409000 : Integer;
      MainTableID@1100409002 : Integer;
      MainTableLookupPageID@1100527701 : Integer;
      XMLPortRequestPageID@1100527700 : Integer;
      CommentRec@1100525061 : Record 97;
      ConvMgt@1100525059 : Codeunit 11020212;
      ImpLineCounter@1100409007 : Integer;
      XmlShowStatus@1100409006 : Codeunit 11130006;
      PostingDateFromImportFile@1100525002 : Boolean;
      LookupCustomerNo@1100409005 : Boolean;
      CustCommentCde@1100409004 : Code[20];
      CDate@1100409014 : Date;
      DDate@1100409015 : Date;
      FDec@1100525000 : Decimal;
      EDec@1100409008 : Decimal;
      IDec@1100409009 : Decimal;
      JDate@1100409017 : Date;
      Text20@1100409013 : TextConst 'ENU=Customer/Amount: %1/%2;SVE=Kund/belopp: %1/%2';
      Text001@1100409012 : TextConst 'ENU=Invoice Amount in Description;SVE=Fakturabelopp i beskrivning';
      Text002@1100409011 : TextConst 'ENU=Remaining Amount Local Currency;SVE=èterstÜende belopp i lokal valuta';
      Text003@1100409010 : TextConst 'ENU=Remaining Amount LCY;SVE=èterstÜende belopp BVA';

    PROCEDURE AddConversionFileFormatLine@1100529901();
    VAR
      ConversionFileFormat@1100409000 : Record 11020691;
    BEGIN
      // This function stores the file format into table [Conversion File Format].
      // There is no other standard documentation; so please maintain this function in case of customization.
      //
      // Parameters: 1: [XMLport No.],
      //             2: [csv-Column],
      //             3: [NAV Table ID],
      //             4: [NAV Field Name],
      //             5: [Data Type of dataport field],
      //             6: [extra Comment].
      //
      WITH ConversionFileFormat DO BEGIN
        ConversionFileFormat.DeleteColumnLines(XmlPortID);
        InsertColumnLine(XmlPortID,'A',MainTableID,"Gen. Journal Line".FIELDNAME("Document No."),'Code20','');
        InsertColumnLine(XmlPortID,'B',MainTableID,"Gen. Journal Line".FIELDNAME("Account No."),'Code20','OldTxt');
        InsertColumnLine(XmlPortID,'C',MainTableID,"Gen. Journal Line".FIELDNAME("Document Date"),'Date','');
        InsertColumnLine(XmlPortID,'D',MainTableID,"Gen. Journal Line".FIELDNAME("Due Date"),'Date','');
        InsertColumnLine(XmlPortID,'E',0,Text001,'Decimal',Text001);
        InsertColumnLine(XmlPortID,'F',MainTableID,"Gen. Journal Line".FIELDNAME(Amount),'Decimal',Text002);
        InsertColumnLine(XmlPortID,'G',MainTableID,"Gen. Journal Line".FIELDNAME("Shortcut Dimension 1 Code"),'Code20','');
        InsertColumnLine(XmlPortID,'H',MainTableID,"Gen. Journal Line".FIELDNAME("Currency Code"),'Code10','');
        InsertColumnLine(XmlPortID,'I',MainTableID,"Gen. Journal Line".FIELDNAME(Amount),'Decimal',Text003);
        InsertColumnLine(XmlPortID,'J',MainTableID,"Gen. Journal Line".FIELDNAME("Posting Date"),'Date','');
      END;
    END;

    PROCEDURE StripData@1100485000();
    BEGIN
    END;

    PROCEDURE ClearFields@1100529902();
    BEGIN
      CLEAR("Gen. Journal Line");

      CLEAR(A);
      CLEAR(B);
      CLEAR(C);
      CLEAR(D);
      CLEAR(E);
      CLEAR(F);
      CLEAR(G);
      CLEAR(H);
      CLEAR(I);
      CLEAR(J);

      CLEAR(CDate);
      CLEAR(DDate);
      CLEAR(FDec);
      CLEAR(EDec);
      CLEAR(IDec);
      CLEAR(JDate);
    END;

    LOCAL PROCEDURE AllInputFieldsAreEmpty@1100525000() : Boolean;
    BEGIN
      EXIT(
        (A = '') AND
        (B = '') AND
        (C = '') AND
        (D = '') AND
        (E = '') AND
        (F = '') AND
        (G = '') AND
        (H = '') AND
        (I = '') AND
        (J = ''));
    END;

    PROCEDURE InitXMLport@1100529500(iPostingDateFromImportFile@1100529502 : Boolean;iLookupCustomerNo@1100529501 : Boolean;iCustCommentCde@1100529500 : Code[20]);
    BEGIN
      PostingDateFromImportFile := iPostingDateFromImportFile;
      LookupCustomerNo := iLookupCustomerNo;
      CustCommentCde := iCustCommentCde;
    END;

    BEGIN
    END.
  }
}

