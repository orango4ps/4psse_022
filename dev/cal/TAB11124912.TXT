OBJECT Table 11124912 Stnd. Transport Cost Res. Req.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CheckKeyFields;
               IF "Line No." = 0 THEN
                 "Line No." := GetFreeLineNo;
             END;

    OnRename=BEGIN
               CheckKeyFields;
               IF (Level <> xRec.Level) OR
                  ("Customer No." <> xRec."Customer No.") OR ("Address Code" <> xRec."Address Code") OR
                  ("Plant Location" <> xRec."Plant Location") OR
                  ("Resource No." <> xRec."Resource No.")
               THEN
                 CheckTransportResourceUsedOnceOnLevel(TRUE);
             END;

    CaptionML=ENU=Standard Transport Cost (Resource Request);
  }
  FIELDS
  {
    { 10  ;   ;Level               ;Option        ;OnValidate=BEGIN
                                                                CASE Level OF
                                                                  Level::"CustomerAddress-Resource":
                                                                    BEGIN
                                                                      TESTFIELD("Plant Location", '');
                                                                    END;
                                                                  Level::"PlantLocation-Resource":
                                                                    BEGIN
                                                                      TESTFIELD("Customer No.", '');
                                                                      TESTFIELD("Address Code", '');
                                                                    END;
                                                                  Level::Resource:
                                                                    BEGIN
                                                                      TESTFIELD("Customer No.", '');
                                                                      TESTFIELD("Address Code", '');
                                                                      TESTFIELD("Plant Location", '');
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Level;
                                                              NOR=Niv†;
                                                              SVE=Niv†];
                                                   OptionCaptionML=ENU=Customer Address - Resource,Plant Location - Resource,Resource;
                                                   OptionString=CustomerAddress-Resource,PlantLocation-Resource,Resource }
    { 20  ;   ;Customer No.        ;Code20        ;TableRelation=Customer;
                                                   OnValidate=BEGIN
                                                                IF "Customer No." <> '' THEN
                                                                  TESTFIELD(Level, Level::"CustomerAddress-Resource");
                                                                IF ("Customer No." = '') OR ("Customer No." <> xRec."Customer No.") THEN
                                                                  "Address Code" := '';
                                                                CALCFIELDS("Customer Name");
                                                              END;

                                                   CaptionML=[ENU=Customer No.;
                                                              NOR=Kundenr.;
                                                              SVE=Kundnr] }
    { 21  ;   ;Customer Name       ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Customer.Name WHERE (No.=FIELD(Customer No.)));
                                                   CaptionML=[ENU=Customer Name;
                                                              NOR=Kundnavn;
                                                              SVE=Kundens namn];
                                                   Editable=No }
    { 30  ;   ;Address Code        ;Code10        ;TableRelation="Ship-to Address".Code WHERE (Customer No.=FIELD(Customer No.));
                                                   OnValidate=BEGIN
                                                                IF "Address Code" <> '' THEN BEGIN
                                                                  TESTFIELD(Level, Level::"CustomerAddress-Resource");
                                                                  TESTFIELD("Customer No.");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Address Code;
                                                              NOR=Adressekode;
                                                              SVE=Adresskod] }
    { 50  ;   ;Plant Location      ;Code20        ;TableRelation="Plant Location";
                                                   OnValidate=VAR
                                                                PlantLocation@1100529000 : Record 11012554;
                                                              BEGIN
                                                                IF "Plant Location" <> '' THEN BEGIN
                                                                  TESTFIELD(Level, Level::"PlantLocation-Resource");
                                                                  PlantLocation.GET("Plant Location");
                                                                  IF (PlantLocation."Project No." = '') AND (PlantLocation."Service Order No." = '') THEN
                                                                    FIELDERROR("Plant Location", Text001);
                                                                  PlantLocation.TESTFIELD("Company Name", '');
                                                                END;
                                                                CALCFIELDS("Plant Location Description");
                                                              END;

                                                   OnLookup=VAR
                                                              PlantLocation@1100529000 : Record 11012554;
                                                            BEGIN
                                                              PlantLocation.FILTERGROUP(9);
                                                              PlantLocation.SETRANGE("Company Name", '');
                                                              PlantLocation.FILTERGROUP(0);
                                                              IF PAGE.RUNMODAL(0, PlantLocation) = ACTION::LookupOK THEN
                                                                VALIDATE("Plant Location", PlantLocation.Code);
                                                            END;

                                                   CaptionML=[ENU=Plant Location;
                                                              NOR=Maskinplassering;
                                                              SVE=Maskinutplacering] }
    { 51  ;   ;Plant Location Description;Text100 ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Location".Description WHERE (Code=FIELD(Plant Location)));
                                                   CaptionML=[ENU=Plant Location Description;
                                                              NOR=Beskrivelse av maskinplassering;
                                                              SVE=Beskrivning av Maskinsplats];
                                                   Editable=No }
    { 60  ;   ;Resource No.        ;Code20        ;TableRelation="Resource (WOP)" WHERE (Type=CONST(Plant));
                                                   OnValidate=BEGIN
                                                                IF ("Resource No." = "Transport Resource No.") AND ("Transport Resource No." <> '') THEN
                                                                  FIELDERROR("Transport Resource No.");
                                                                IF "Line No." = 0 THEN
                                                                  "Line No." := GetFreeLineNo;
                                                                CALCFIELDS("Resource Name");
                                                              END;

                                                   CaptionML=[ENU=Resource No.;
                                                              NOR=Ressursnr.;
                                                              SVE=Resursnr];
                                                   NotBlank=Yes }
    { 61  ;   ;Resource Name       ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Resource (WOP)".Name WHERE (No.=FIELD(Resource No.)));
                                                   CaptionML=[ENU=Resource Name;
                                                              NOR=Ressursnavn;
                                                              SVE=Resursnamn];
                                                   Editable=No }
    { 70  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 80  ;   ;Transport Type      ;Option        ;OnValidate=BEGIN
                                                                IF "Transport Type" <> "Transport Type"::Transport THEN
                                                                  TESTFIELD("Transport Resource No.", '');
                                                              END;

                                                   CaptionML=ENU=Transport Type;
                                                   OptionCaptionML=ENU=" ,Driving,Transport";
                                                   OptionString=[ ,Driving,Transport] }
    { 90  ;   ;Transport Resource No.;Code20      ;TableRelation="Resource (WOP)" WHERE (Type=CONST(Plant));
                                                   OnValidate=BEGIN
                                                                IF "Transport Resource No." <> '' THEN BEGIN
                                                                  TESTFIELD("Transport Type", "Transport Type"::Transport);
                                                                  IF "Transport Resource No." = "Resource No." THEN
                                                                    FIELDERROR("Transport Resource No.");
                                                                  CheckTransportResourceUsedOnceOnLevel(FALSE);
                                                                END;
                                                                CALCFIELDS("Transport Resource Name");
                                                              END;

                                                   CaptionML=ENU=Transport Resource No. }
    { 91  ;   ;Transport Resource Name;Text100    ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Resource (WOP)".Name WHERE (No.=FIELD(Transport Resource No.)));
                                                   CaptionML=ENU=Transport Resource Name;
                                                   Editable=No }
    { 100 ;   ;Cost Object         ;Code20        ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2),
                                                                                               Cost Type=FILTER(<>Revenue));
                                                   OnValidate=VAR
                                                                DimensionValue@1100527200 : Record 349;
                                                                DimensionMgt@1100527201 : Codeunit 408;
                                                              BEGIN
                                                                IF "Cost Object" <> '' THEN BEGIN
                                                                  DimensionMgt.GetDimValueRec(2, "Cost Object", DimensionValue, TRUE, '');
                                                                  IF ("Cost Object" <> xRec."Cost Object") THEN BEGIN
                                                                    Description := DimensionValue.Name;
                                                                    VALIDATE("Unit of Measure", DimensionValue."Unit of Measure");
                                                                    VALIDATE("Unit Price", DimensionValue."Sales Price");
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Global Dimension 2 Code;
                                                              NOR=Kostnadsobjekt;
                                                              SVE=Kostnadsobjekt];
                                                   CaptionClass='1,1,2' }
    { 110 ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 120 ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Unit Price");
                                                              END;

                                                   CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   DecimalPlaces=0:5;
                                                   BlankZero=Yes }
    { 130 ;   ;Unit of Measure     ;Code10        ;TableRelation="Unit of Measure";
                                                   CaptionML=[ENU=Unit of Measure;
                                                              NOR=Enhet;
                                                              SVE=M†ttenhet] }
    { 140 ;   ;Unit Price          ;Decimal       ;OnValidate=BEGIN
                                                                Amount := ROUND(Quantity * "Unit Price");
                                                              END;

                                                   CaptionML=[ENU=Price;
                                                              NOR=Pris;
                                                              SVE=Pris];
                                                   BlankZero=Yes;
                                                   AutoFormatType=2 }
    { 150 ;   ;Amount              ;Decimal       ;OnValidate=VAR
                                                                GLSetup@1100528300 : Record 98;
                                                              BEGIN
                                                                IF ("Unit Price" <> 0) THEN
                                                                  TESTFIELD(Quantity);
                                                                IF Quantity <> 0 THEN BEGIN
                                                                  "Unit Price" := Amount / Quantity
                                                                END ELSE
                                                                  "Unit Price" := Amount;
                                                                GLSetup.GET;
                                                                "Unit Price" := ROUND("Unit Price",GLSetup."Unit-Amount Rounding Precision");
                                                              END;

                                                   CaptionML=[ENU=Amount;
                                                              NOR=Bel›p;
                                                              SVE=Belopp];
                                                   BlankZero=Yes;
                                                   AutoFormatType=1 }
  }
  KEYS
  {
    {    ;Level,Customer No.,Address Code,Plant Location,Resource No.,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100529000 : TextConst 'ENU=must be linked to project or service order';
      Text002@1100529001 : TextConst 'ENU=''%1'' is already present on this level';

    LOCAL PROCEDURE GetFreeLineNo@1100529002() : Integer;
    VAR
      StndTransportCostResourceRequest@1100529000 : Record 11124912;
    BEGIN
      IF "Resource No." = '' THEN
        EXIT(0);
      StndTransportCostResourceRequest.SETRANGE(Level, Level);
      CASE Level OF
        Level::"CustomerAddress-Resource":
          BEGIN
            IF ("Customer No." = '') OR ("Address Code" = '') THEN
              EXIT(0);
            StndTransportCostResourceRequest.SETRANGE("Customer No.", "Customer No.");
            StndTransportCostResourceRequest.SETRANGE("Address Code", "Address Code");
          END;
        Level::"PlantLocation-Resource":
          BEGIN
            IF "Plant Location" = '' THEN
              EXIT(0);
            StndTransportCostResourceRequest.SETRANGE("Plant Location", "Plant Location");
          END;
      END;
      StndTransportCostResourceRequest.SETRANGE("Resource No.", "Resource No.");
      IF StndTransportCostResourceRequest.FINDLAST THEN
        EXIT(StndTransportCostResourceRequest."Line No." + 10);
      EXIT(10);
    END;

    LOCAL PROCEDURE CheckKeyFields@1100529000();
    BEGIN
      CASE Level OF
        Level::"CustomerAddress-Resource":
          BEGIN
            TESTFIELD("Customer No.");
            TESTFIELD("Address Code");
            TESTFIELD("Resource No.");
            TESTFIELD("Plant Location", '');
          END;
        Level::"PlantLocation-Resource":
          BEGIN
            TESTFIELD("Plant Location");
            TESTFIELD("Resource No.");
            TESTFIELD("Customer No.", '');
            TESTFIELD("Address Code", '');
          END;
        Level::Resource:
          BEGIN
            TESTFIELD("Resource No.");
            TESTFIELD("Customer No.", '');
            TESTFIELD("Address Code", '');
            TESTFIELD("Plant Location", '');
          END;
      END;
    END;

    LOCAL PROCEDURE CheckTransportResourceUsedOnceOnLevel@1100529001(ModeOnRename@1100529001 : Boolean);
    VAR
      StndTransportCostResourceRequest@1100529000 : Record 11124912;
    BEGIN
      IF ("Transport Type" <> "Transport Type"::Transport) OR ("Transport Resource No." = '') THEN
        EXIT;
      StndTransportCostResourceRequest.SETRANGE(Level, Level);
      CASE Level OF
        Level::"CustomerAddress-Resource":
          BEGIN
            StndTransportCostResourceRequest.SETRANGE("Customer No.", "Customer No.");
            StndTransportCostResourceRequest.SETRANGE("Address Code", "Address Code");
          END;
        Level::"PlantLocation-Resource":
          StndTransportCostResourceRequest.SETRANGE("Plant Location", "Plant Location");
      END;
      StndTransportCostResourceRequest.SETRANGE("Resource No.", "Resource No.");
      StndTransportCostResourceRequest.SETRANGE("Transport Type", StndTransportCostResourceRequest."Transport Type"::Transport);
      StndTransportCostResourceRequest.SETRANGE("Transport Resource No.", "Transport Resource No.");
      IF NOT ModeOnRename THEN
        StndTransportCostResourceRequest.SETFILTER("Line No.", '<>%1', "Line No.");
      IF StndTransportCostResourceRequest.FINDFIRST THEN
        StndTransportCostResourceRequest.FIELDERROR("Transport Resource No.", STRSUBSTNO(Text002, "Transport Resource No."));
    END;

    BEGIN
    END.
  }
}

