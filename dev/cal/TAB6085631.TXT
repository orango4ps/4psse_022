OBJECT Table 6085631 Approval User Group Member
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.4.07;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ApprovalUserGroupPermission@1160040001 : Record 6085632;
               AppUserGroupPmsSelection@1160040002 : Record 6085633;
               ContiniaUserPermission@1160040000 : Record 6085634;
               ContiniaUserPmsSelection@1160040003 : Record 6085635;
             BEGIN
               TESTFIELD("Approval User Group Code");
               TESTFIELD("Continia User ID");

               GetContiniaUserSetup(GetCurrCompanyNameTbl);

               ApprovalUserGroupPermission.CHANGECOMPANY(GetCurrCompanyNameTbl);
               ApprovalUserGroupPermission.SETRANGE("Approval User Group Code","Approval User Group Code");
               IF ApprovalUserGroupPermission.FINDSET THEN
                 REPEAT
                   ContiniaUserPermission.CHANGECOMPANY(GetCurrCompanyNameTbl);
                   ContiniaUserPermission.TRANSFERFIELDS(ApprovalUserGroupPermission);
                   ContiniaUserPermission."Continia User ID" := "Continia User ID";
                   ContiniaUserPermission.INSERT;

                   AppUserGroupPmsSelection.CHANGECOMPANY(GetCurrCompanyNameTbl);
                   AppUserGroupPmsSelection.SETRANGE("Approval User Group Code",ApprovalUserGroupPermission."Approval User Group Code");
                   AppUserGroupPmsSelection.SETRANGE(Type,ApprovalUserGroupPermission.Type);
                   AppUserGroupPmsSelection.SETRANGE("Dimension Code",ApprovalUserGroupPermission."Dimension Code");
                   IF AppUserGroupPmsSelection.FINDSET THEN
                     REPEAT
                       ContiniaUserPmsSelection.CHANGECOMPANY(GetCurrCompanyNameTbl);
                       ContiniaUserPmsSelection.TRANSFERFIELDS(AppUserGroupPmsSelection);
                       ContiniaUserPmsSelection."Continia User ID" := "Continia User ID";
                       ContiniaUserPmsSelection.INSERT;
                     UNTIL AppUserGroupPmsSelection.NEXT = 0;
                 UNTIL ApprovalUserGroupPermission.NEXT = 0;
             END;

    OnDelete=VAR
               ContiniaUserPermission@1160040001 : Record 6085634;
               ContiniaUserPmsSelection@1160040000 : Record 6085635;
             BEGIN
               TESTFIELD("Approval User Group Code");
               TESTFIELD("Continia User ID");

               ContiniaUserPermission.CHANGECOMPANY(GetCurrCompanyNameTbl);
               ContiniaUserPermission.SETRANGE("Continia User ID","Continia User ID");
               ContiniaUserPermission.SETRANGE("Approval User Group Code","Approval User Group Code");
               ContiniaUserPermission.DELETEALL;

               ContiniaUserPmsSelection.CHANGECOMPANY(GetCurrCompanyNameTbl);
               ContiniaUserPmsSelection.SETRANGE("Continia User ID","Continia User ID");
               ContiniaUserPmsSelection.SETRANGE("Approval User Group Code","Approval User Group Code");
               ContiniaUserPmsSelection.DELETEALL;
             END;

    OnRename=BEGIN
               ERROR(RenameErr,TABLECAPTION);
             END;

    CaptionML=[DEU=Genehmigungsbenutzergruppenmitgleid;
               ENU=Approval User Group Member;
               NLD=Goedkeuringsgebruikersgroeplid];
    LookupPageID=Page6085661;
    DrillDownPageID=Page6085661;
  }
  FIELDS
  {
    { 1   ;   ;Approval User Group Code;Code20    ;TableRelation="Approval User Group";
                                                   CaptionML=[DEU=Genehmigungsbenutzergruppencode;
                                                              ENU=Approval User Group Code;
                                                              NLD=Goedkeuringsgebruikersgroepcode];
                                                   NotBlank=Yes }
    { 2   ;   ;Continia User ID    ;Code50        ;TableRelation="Continia User Setup";
                                                   CaptionML=[DEU=Continia-Benutzer-ID;
                                                              ENU=Continia User ID;
                                                              NLD=Continia gebruikers-id;
                                                              SVE=Anst„llningsnr];
                                                   NotBlank=Yes }
    { 3   ;   ;Continia User Name  ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Continia User".Name WHERE (User ID=FIELD(Continia User ID)));
                                                   CaptionML=[DEU=Continia-Benutzername;
                                                              ENU=Continia User Name;
                                                              NLD=Continia gebruikersnaam];
                                                   Editable=No }
    { 4   ;   ;Approval User Group Name;Text50    ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Approval User Group".Name WHERE (Code=FIELD(Approval User Group Code)));
                                                   CaptionML=[DEU=Name der Benutzergruppe;
                                                              ENU=User Group Name;
                                                              NLD=Gebruikersgroepnaam;
                                                              SVE=Namn p† anv„ndargrupp];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Approval User Group Code,Continia User ID;
                                                   Clustered=Yes }
    {    ;Continia User ID                         }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      RenameErr@1160040001 : TextConst 'DEU=Sie k”nnen eine %1 nicht umbenennen.;ENU=You cannot rename a %1.;NLD=U kunt een %1 niet hernoemen.;SVE=Du kan inte byta namn p† %1.';
      CurrCompanyName@1160040004 : Text[30];
      ContiniaUserSetupMissingErr@1160040003 : TextConst 'DEU=%1 %2 ''%3'' nicht in %4 vorhanden.;ENU=%1 %2 ''%3'' does not exist in %4.;NLD=%1 %2 ''%3'' bestaat niet in %4.';

    PROCEDURE SetCurrCompanyNameTbl@1160040019(NewCompanyName@1160040000 : Text[30]);
    BEGIN
      CurrCompanyName := NewCompanyName;
    END;

    PROCEDURE GetCurrCompanyNameTbl@1160040024() : Text[30];
    BEGIN
      IF CurrCompanyName = '' THEN
        EXIT(COMPANYNAME)
      ELSE
        EXIT(CurrCompanyName);
    END;

    PROCEDURE GetContiniaUserSetup@1160040000(NewCompanyName@1160040000 : Text[30]);
    VAR
      ContiniaUserSetup@1160040001 : Record 6086002;
    BEGIN
      TESTFIELD("Continia User ID");

      SetCurrCompanyNameTbl(NewCompanyName);
      ContiniaUserSetup.CHANGECOMPANY(GetCurrCompanyNameTbl);
      IF GetCurrCompanyNameTbl <> COMPANYNAME THEN BEGIN
        IF NOT ContiniaUserSetup.GET("Continia User ID") THEN
          ERROR(ContiniaUserSetupMissingErr,ContiniaUserSetup.TABLECAPTION,ContiniaUserSetup.FIELDCAPTION("Continia User ID"),
            "Continia User ID",GetCurrCompanyNameTbl);
      END ELSE
        ContiniaUserSetup.GET("Continia User ID");
    END;

    BEGIN
    END.
  }
}

