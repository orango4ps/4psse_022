OBJECT Report 11012798 Activation Cost Service Order
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Aktivierungskosten Serviceauftrag;
               ENU=Activation Cost Service Order;
               NLD=Activering kosten serviceorder];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   ServSetup.GET;
                   IF ServSetup."Activation Cost" = ServSetup."Activation Cost"::No THEN
                     ServSetup.FIELDERROR("Activation Cost");
                   ServSetup.TESTFIELD("Journal Template Activate Cost");
                   ServSetup.TESTFIELD("Journal Batch Activate Cost");
                   GenJournalTemplate.GET(ServSetup."Journal Template Activate Cost");
                   GenJournalBatch.GET(GenJournalTemplate.Name, ServSetup."Journal Batch Activate Cost");
                   GenJournalBatch.TESTFIELD("No. Series");

                   GLSetup.GET;
                   GLSetup.TESTFIELD("Global Dimension 2 Code");
                 END;

    OnPreReport=VAR
                  FADeprBook@1100529000 : Record 5612;
                  GenJnlLine@1100529001 : Record 81;
                BEGIN
                  IF PostingDate = 0D THEN
                    ERROR(Text001, GenJnlLine.FIELDCAPTION("Posting Date"));
                  IF NOT CreateNewFA THEN BEGIN
                    IF FixedAssetNo = '' THEN
                      ERROR(Text001, FADeprBook.FIELDCAPTION("FA No."));
                    ValidateFixedAssetNo();
                  END ELSE BEGIN
                    IF DeprBookCode = '' THEN
                      ERROR(Text001, FADeprBook.FIELDCAPTION("Depreciation Book Code"));
                    IF FAPostingGroup = '' THEN
                      ERROR(Text001, FADeprBook.FIELDCAPTION("FA Posting Group"));
                    IF DeprStartingDate = 0D THEN
                      ERROR(Text001, FADeprBook.FIELDCAPTION("Depreciation Starting Date"));
                    IF NoOfDeprYears = 0 THEN
                      ERROR(Text001, FADeprBook.FIELDCAPTION("No. of Depreciation Years"));
                  END;
                END;

  }
  DATASET
  {
    { 1100529000;;DataItem;Process           ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number)
                                 WHERE(Number=CONST(1));
               OnAfterGetRecord=BEGIN
                                  ProcessActivation();
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   PostingDate := 0D;
                   FixedAssetNo := '';
                   DeprBookCodePrev := DeprBookCode;
                   DeprStartingDate := 0D;
                   NoOfDeprYears := 0;
                   FAClassCode := '';
                   FASubclassCode := '';
                   FALocationCode := '';
                   GlobalDimension1Code := ServOrder."Global Dimension 1 Code";
                   SetEditable();
                 END;

    }
    CONTROLS
    {
      { 1100529001;;Container;
                  ContainerType=ContentArea }

      { 1100529000;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100529007;2;Field  ;
                  CaptionML=[DEU=Buchungsdatum;
                             ENU=Posting Date;
                             NLD=Boekingsdatum;
                             NOR=Bokfõringsdato;
                             SVE=Bokfîringsdatum];
                  SourceExpr=PostingDate }

      { 1100529002;2;Field  ;
                  CaptionML=[DEU=Neue Anlage;
                             ENU=New Fixed Asset;
                             NLD=Nieuw vast activum];
                  SourceExpr=CreateNewFA;
                  OnValidate=BEGIN
                               SetEditable();
                             END;
                              }

      { 1100529003;2;Field  ;
                  CaptionML=[DEU=Anlagennr.;
                             ENU=Fixed Asset No.;
                             NLD=Vast activumnr.;
                             SVE=AnlÑggningstillgÜngsnr];
                  SourceExpr=FixedAssetNo;
                  TableRelation="Fixed Asset";
                  Editable=ExistEditable;
                  HideValue=NewEditable;
                  OnValidate=BEGIN
                               IF FixedAssetNo <> '' THEN
                                 ValidateFixedAssetNo();
                             END;
                              }

      { 1100529004;2;Field  ;
                  CaptionML=[DEU=AfA Buchcode;
                             ENU=Depreciation Book Code;
                             NLD=Afschrijvingsboek;
                             NOR=AvskrivningstablÜkode;
                             SVE=Avskrivningsregelkod];
                  SourceExpr=DeprBookCode;
                  TableRelation="Depreciation Book";
                  Editable=NewEditable;
                  HideValue=ExistEditable;
                  OnValidate=VAR
                               DeprBook@1100409000 : Record 5611;
                             BEGIN
                               IF (DeprBookCode <> '') AND (DeprBookCode <> DeprBookCodePrev) THEN BEGIN
                                 IF DeprBook.GET(DeprBookCode) THEN BEGIN
                                   IF DeprBook."Default FA Posting Group" <> '' THEN
                                     FAPostingGroup := DeprBook."Default FA Posting Group"
                                 END;
                               END;
                               DeprBookCodePrev := DeprBookCode;
                             END;
                              }

      { 1100409000;2;Field  ;
                  CaptionML=[DEU=Anlagenbuchungsgruppe;
                             ENU=FA Posting Group;
                             NLD=VA-boekingsgroep;
                             NOR=Bokfõringsgruppe - aktiva;
                             SVE=Anl. bokfîringsmall];
                  SourceExpr=FAPostingGroup;
                  TableRelation="FA Posting Group";
                  Editable=NewEditable;
                  HideValue=ExistEditable }

      { 1100529005;2;Field  ;
                  CaptionML=[DEU=Startdatum Normal-AfA;
                             ENU=Depreciation Starting Date;
                             NLD=Begindatum afschr.;
                             NOR=Startdato for avskrivning;
                             SVE=Avskrivning startdatum];
                  SourceExpr=DeprStartingDate;
                  Editable=NewEditable;
                  HideValue=ExistEditable }

      { 1100529006;2;Field  ;
                  CaptionML=[DEU=Nutzungsdauer i. Jahren;
                             ENU=No. of Depreciation Years;
                             NLD=Aantal afschr.jaren;
                             NOR=Antall AvskrivningsÜr;
                             SVE=Antal avskrivningsÜr];
                  DecimalPlaces=2:8;
                  BlankZero=Yes;
                  SourceExpr=NoOfDeprYears;
                  MinValue=0;
                  Editable=NewEditable;
                  HideValue=ExistEditable }

      { 1100528400;2;Field  ;
                  CaptionML=[DEU=Anlagenklassencode;
                             ENU=FA Class Code;
                             NLD=VA-categorie;
                             NOR=Aktivaklassekode;
                             SVE=Anl. indelningskod];
                  SourceExpr=FAClassCode;
                  TableRelation="FA Class";
                  Editable=NewEditable;
                  HideValue=ExistEditable }

      { 1100528401;2;Field  ;
                  CaptionML=[DEU=Anlagensachgruppencode;
                             ENU=FA Subclass Code;
                             NLD=VA-subcategorie;
                             NOR=Undergruppekode for driftsmidler;
                             SVE=Anl. underindelningskod];
                  SourceExpr=FASubclassCode;
                  TableRelation="FA Subclass";
                  Editable=NewEditable;
                  HideValue=ExistEditable }

      { 1100528402;2;Field  ;
                  CaptionML=[DEU=Anlagenstandortcode;
                             ENU=FA Location Code;
                             NLD=Vestiging-VA;
                             NOR=Lokasjonskode for driftsmidler;
                             SVE=Anl. lagerstÑllekod];
                  SourceExpr=FALocationCode;
                  TableRelation="FA Location";
                  Editable=NewEditable;
                  HideValue=ExistEditable }

      { 1100528403;2;Field  ;
                  SourceExpr=GlobalDimension1Code;
                  CaptionClass='1,1,1';
                  TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                  Editable=NewEditable;
                  HideValue=ExistEditable }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      GLSetup@1100529016 : Record 98;
      ServSetup@1100529000 : Record 11012800;
      ServOrder@1100529010 : Record 11012823;
      ServObject@1100529003 : Record 11071691;
      GenJournalTemplate@1100529018 : Record 80;
      GenJournalBatch@1100529017 : Record 232;
      PostingDate@1100529013 : Date;
      DeprStartingDate@1100529007 : Date;
      CreateNewFA@1100529001 : Boolean;
      NewEditable@1100529008 : Boolean INDATASET;
      ExistEditable@1100529009 : Boolean INDATASET;
      NextLineNo@1100529014 : Integer;
      NoOfDeprYears@1100529004 : Decimal;
      ServOrderNo@1100529011 : Code[20];
      FixedAssetNo@1100529002 : Code[20];
      DeprBookCode@1100529005 : Code[10];
      Text001@1100529006 : TextConst 'DEU=''%1'' muss in Registerkarte ''Optionen'' eingegeben werden.;ENU=%1 must be filled;NLD=%1 moet gevuld worden;NOR=%1 mÜ fylles ut;SVE=%1 mÜste fyllas';
      DeprBookCodePrev@1100409001 : Code[10];
      FAPostingGroup@1100409000 : Code[10];
      DocumentNo@1100529012 : Code[20];
      Text002@1100529015 : TextConst 'DEU=Kein %1 mit %2 ''%3'' verknÅpft;ENU=No %1 linked to %2 ''%3'';NLD=Geen %1 gekoppeld aan %2 ''%3''';
      FAClassCode@1100528400 : Code[10];
      FASubclassCode@1100528401 : Code[10];
      FALocationCode@1100528402 : Code[10];
      GlobalDimension1Code@1100528403 : Code[20];

    PROCEDURE SetSelection@1100529000(ServiceOrderNo@1100529000 : Code[20]);
    BEGIN
      ServOrderNo := ServiceOrderNo;
      ReadCheckServOrderAndObject();
    END;

    LOCAL PROCEDURE SetEditable@1100529002();
    BEGIN
      NewEditable := CreateNewFA;
      ExistEditable := (NOT CreateNewFA);
    END;

    LOCAL PROCEDURE ValidateFixedAssetNo@1100529001();
    VAR
      FA@1100529000 : Record 5600;
    BEGIN
      IF NOT CreateNewFA THEN
        FA.CheckLinkServObjectAllowed(FixedAssetNo, '');
    END;

    LOCAL PROCEDURE ProcessActivation@1100529003();
    VAR
      FA@1100409000 : Record 5600;
      FANo@1100529002 : Code[20];
    BEGIN
      DocumentNo := '';
      NextLineNo := 0;
      ReadCheckServOrderAndObject();

      IF CreateNewFA THEN BEGIN
        FANo := CreateFA();
        ServObject."Fixed Asset No." := FANo;
        ServObject.MODIFY(FALSE);
      END ELSE
        FANo := FixedAssetNo;
      PostCorrActivationCost(FANo);

      ServOrder."Service Order Cost Activated" := TRUE;
      ServOrder.MODIFY;
      MESSAGE('%1, %2 %3, %4 %5',
        ServOrder.FIELDCAPTION("Service Order Cost Activated"), ServOrder.TABLECAPTION, ServOrder."No.", FA.TABLECAPTION, FANo);
    END;

    LOCAL PROCEDURE ReadCheckServOrderAndObject@1100529009();
    VAR
      ServOrderObject@1100529000 : Record 11071724;
    BEGIN
      ServOrder.GET(ServOrderNo);
      ServOrder.TESTFIELD("No.");
      IF ServOrder."Service Order Cost Activated" THEN
        ServOrder.FIELDERROR("Service Order Cost Activated");

      ServOrderObject.SETCURRENTKEY("Service Order No.", "Object No.");
      ServOrderObject.SETRANGE("Service Order No.", ServOrder."No.");
      ServOrderObject.SETFILTER("Object No.", '<>%1', '');
      IF NOT ServOrderObject.FINDFIRST THEN
        ERROR(Text002, ServObject.TABLECAPTION, ServOrderObject.FIELDCAPTION("Service Order No."), ServOrderObject."Service Order No." );

      ServObject.GET(ServOrderObject."Object No.");
      ServObject.TESTFIELD("Fixed Asset No.", '');
    END;

    LOCAL PROCEDURE CreateFA@1210190000() : Code[20];
    VAR
      FA@1210190000 : Record 5600;
      DefaultDim@1100529000 : Record 352;
    BEGIN
      FA.INIT;
      FA."No." := '';
      FA.SetSkipCreateDefFADeprBooks(TRUE);
      FA.INSERT(TRUE);
      FA.TESTFIELD("No.");

      FA.VALIDATE(Description, COPYSTR(ServObject.Description, 1, MAXSTRLEN(FA.Description)));
      FA.VALIDATE("Description 2", COPYSTR(ServObject."Description 2", 1, MAXSTRLEN(FA."Description 2")));
      FA.VALIDATE("FA Class Code", FAClassCode);
      FA.VALIDATE("FA Subclass Code", FASubclassCode);
      FA.VALIDATE("FA Location Code", FALocationCode);
      IF GlobalDimension1Code <> '' THEN
        FA.VALIDATE("Global Dimension 1 Code", GlobalDimension1Code);
      IF DefaultDim.GET(DATABASE::"Service Order", ServOrder."No.", GLSetup."Global Dimension 2 Code") THEN
        FA.VALIDATE("Global Dimension 2 Code", DefaultDim."Dimension Value Code");
      FA.MODIFY(TRUE);
      FA.CreateServObjectFADeprBook(FA, DeprBookCode, FAPostingGroup, DeprStartingDate, NoOfDeprYears);
      EXIT(FA."No.");
    END;

    LOCAL PROCEDURE PostCorrActivationCost@1100529004(FANo@1100529003 : Code[20]);
    VAR
      ServOrderBudgetLine@1100529002 : Record 11012875;
      ServLedgEntry@1100529001 : Record 11012819;
      VATApplicable@1100529004 : Boolean;
    BEGIN
      CASE ServSetup."Activation Cost" OF
        ServSetup."Activation Cost"::BudgetCost:
          BEGIN
            WITH ServOrderBudgetLine DO BEGIN
              SETRANGE("Budget Type", "Budget Type"::Order);
              SETRANGE("Service Order No.", ServOrderNo);
              SETRANGE("Cost Type", "Cost Type"::Labor, "Cost Type"::Sundry);
              IF ServSetup.OnlyActivateForCostComponent <> '' THEN
                SETFILTER("Cost Component", ServSetup.OnlyActivateForCostComponent);  //db, 28-07-14: C016870
              IF FINDSET THEN BEGIN
                REPEAT
                  VATApplicable := VatExemptionApplicable(ServOrderBudgetLine, ServLedgEntry);
                  WriteGenJournalLine(FANo, "Cost Object", Description, "Description 2", Amount, VATApplicable);
                UNTIL NEXT = 0;
              END;
            END;
          END;
        ServSetup."Activation Cost"::ActualCost:
          BEGIN
            WITH ServLedgEntry DO BEGIN
              SETCURRENTKEY("Service Order No.");
              SETRANGE("Service Order No.", ServOrderNo);
              SETRANGE("Cost Type", "Cost Type"::Labor, "Cost Type"::Sundry);
              IF ServSetup.OnlyActivateForCostComponent <> '' THEN
                SETFILTER("Cost Component", ServSetup.OnlyActivateForCostComponent);  //db, 28-07-14: C016870
              IF FINDSET THEN BEGIN
                REPEAT
                  VATApplicable := VatExemptionApplicable(ServOrderBudgetLine, ServLedgEntry);
                  WriteGenJournalLine(FANo, "Global Dimension 2 Code", Description, "Description 2", "Total Cost", VATApplicable);
                UNTIL NEXT = 0;
              END;
            END;
          END;
      END;
    END;

    LOCAL PROCEDURE WriteGenJournalLine@1100529006(FANo@1100529002 : Code[20];CostObjectNo@1100529001 : Code[20];Description1@1100529003 : Text[100];Description2@1100529004 : Text[50];Amount@1100529005 : Decimal;VATApplicable@1100529006 : Boolean);
    VAR
      GenJournalLine@1100529000 : Record 81;
      NoSeriesManagement@1100529008 : Codeunit 396;
      VatAmt@1100529007 : Decimal;
    BEGIN
      IF DocumentNo = '' THEN
        DocumentNo := NoSeriesManagement.GetNextNo(GenJournalBatch."No. Series", PostingDate, FALSE);
      IF NextLineNo = 0 THEN BEGIN
        GenJournalLine.LOCKTABLE;
        GenJournalLine.SETRANGE("Journal Template Name", GenJournalTemplate.Name);
        GenJournalLine.SETRANGE("Journal Batch Name", GenJournalBatch.Name);
        IF GenJournalLine.FINDLAST THEN
          NextLineNo := GenJournalLine."Line No." + 1
        ELSE
          NextLineNo := 1;
      END;

      GenJournalLine.INIT;
      GenJournalLine."Line No." := NextLineNo;
      GenJournalLine.VALIDATE("Journal Template Name", ServSetup."Journal Template Activate Cost");
      GenJournalLine.VALIDATE("Journal Batch Name", ServSetup."Journal Batch Activate Cost");
      GenJournalLine."Posting Date" := PostingDate;
      GenJournalLine."Document No." := DocumentNo;
      GenJournalLine."Account Type" := GenJournalLine."Account Type"::"G/L Account";
      GenJournalLine.VALIDATE("Service Order No.", ServOrderNo);
      GenJournalLine.VALIDATE("Shortcut Dimension 2 Code", CostObjectNo);
      GenJournalLine."Origin Type" := GenJournalLine."Origin Type"::Service;
      GenJournalLine."Source Code" := GenJournalTemplate."Source Code";
      IF Description1 <> '' THEN
        GenJournalLine.Description := Description1;
      IF Description2 <> '' THEN
        GenJournalLine."Description 2" := Description2;
      GenJournalLine."System-Created Entry" := TRUE;
      GenJournalLine."Interest Date" := PostingDate;
      GenJournalLine."Gen. Prod. Posting Group" := '';
      GenJournalLine."Gen. Bus. Posting Group" := '';
      GenJournalLine."VAT Prod. Posting Group" :='';
      GenJournalLine."VAT Bus. Posting Group" := '';
      GenJournalLine."Gen. Posting Type" := 0;
      IF VATApplicable THEN BEGIN
        GenJournalLine.VALIDATE("Gen. Posting Type", GenJournalLine."Gen. Posting Type"::Purchase);
        GenJournalLine.VALIDATE("VAT Bus. Posting Group", ServSetup."VAT Bus. Posting Group (Act.)");
        GenJournalLine.VALIDATE("VAT Prod. Posting Group", ServSetup."VAT Prod. Posting Group (Act.)");
        VatAmt := GenJournalLine."VAT %"/100 * Amount;
      END;
      GenJournalLine.VALIDATE(Amount, -(Amount + VatAmt));
      GenJournalLine.INSERT(TRUE);
      NextLineNo := NextLineNo + 1;


      GenJournalLine.INIT;
      GenJournalLine."Line No." := NextLineNo;
      GenJournalLine.VALIDATE("Journal Template Name", ServSetup."Journal Template Activate Cost");
      GenJournalLine.VALIDATE("Journal Batch Name", ServSetup."Journal Batch Activate Cost");
      GenJournalLine."Posting Date" := PostingDate;
      GenJournalLine."Document No." := DocumentNo;
      GenJournalLine."Account Type" := GenJournalLine."Account Type"::"Fixed Asset";
      GenJournalLine.VALIDATE("Account No.", FANo);
      GenJournalLine.VALIDATE("Shortcut Dimension 2 Code", CostObjectNo);
      GenJournalLine.VALIDATE("FA Posting Type",  GenJournalLine."FA Posting Type"::"Acquisition Cost");

      GenJournalLine."Source Code" := GenJournalTemplate."Source Code";
      IF Description1 <> '' THEN
        GenJournalLine.Description := Description1;
      IF Description2 <> '' THEN
        GenJournalLine."Description 2" := Description2;
      GenJournalLine."System-Created Entry" := TRUE;
      GenJournalLine."Interest Date" := PostingDate;
      GenJournalLine."Gen. Prod. Posting Group" := '';
      GenJournalLine."Gen. Bus. Posting Group" := '';
      GenJournalLine."VAT Prod. Posting Group" :='';
      GenJournalLine."VAT Bus. Posting Group" := '';
      GenJournalLine."Gen. Posting Type" := 0;
      //C018634.so
      //IF VATApplicable THEN BEGIN
      //  GenJournalLine.VALIDATE("Gen. Posting Type", GenJournalLine."Gen. Posting Type"::Purchase);
      //  GenJournalLine.VALIDATE("VAT Bus. Posting Group", ServSetup."VAT Bus. Posting Group (Act.)");
      //  GenJournalLine.VALIDATE("VAT Prod. Posting Group", ServSetup."VAT Prod. Posting Group (Act.)");
      //END;
      //C018634.eo
      GenJournalLine.VALIDATE(Amount, Amount + VatAmt);
      GenJournalLine.INSERT(TRUE);
      NextLineNo := NextLineNo + 1;
    END;

    LOCAL PROCEDURE VatExemptionApplicable@1100529007(ServOrderBudgetLine@1100529001 : Record 11012875;ServLedgEntry@1100529000 : Record 11012819) : Boolean;
    BEGIN
      CASE ServSetup."Activation Cost" OF
        ServSetup."Activation Cost"::BudgetCost:
          IF ServSetup."VAT Exemption Applicable" AND
             ((ServSetup."VAT Exemption Labor" AND (ServOrderBudgetLine."Cost Type" = ServOrderBudgetLine."Cost Type"::Labor)) OR
              (ServSetup."VAT Exemption Material" AND (ServOrderBudgetLine."Cost Type" = ServOrderBudgetLine."Cost Type"::Material)) OR
              (ServSetup."VAT Exemption Subcontracting" AND (ServOrderBudgetLine."Cost Type" = ServOrderBudgetLine."Cost Type"::Subcontracting)) OR
              (ServSetup."VAT Exemption Plant" AND (ServOrderBudgetLine."Cost Type" = ServOrderBudgetLine."Cost Type"::Plant)) OR
              (ServSetup."VAT Exemption Sundry" AND (ServOrderBudgetLine."Cost Type" = ServOrderBudgetLine."Cost Type"::Sundry)))
          THEN
            EXIT(TRUE);
        ServSetup."Activation Cost"::ActualCost:
          IF ServSetup."VAT Exemption Applicable" AND
             ((ServSetup."VAT Exemption Labor" AND (ServLedgEntry."Cost Type" = ServLedgEntry."Cost Type"::Labor)) OR
              (ServSetup."VAT Exemption Material" AND (ServLedgEntry."Cost Type" = ServLedgEntry."Cost Type"::Material)) OR
              (ServSetup."VAT Exemption Subcontracting" AND (ServLedgEntry."Cost Type" = ServLedgEntry."Cost Type"::Subcontracting)) OR
              (ServSetup."VAT Exemption Plant" AND (ServLedgEntry."Cost Type" = ServLedgEntry."Cost Type"::Plant)) OR
              (ServSetup."VAT Exemption Sundry" AND (ServLedgEntry."Cost Type" = ServLedgEntry."Cost Type"::Sundry)))
          THEN
            EXIT(TRUE);
      END;
      EXIT(FALSE);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

