OBJECT XMLport 11229162 Document Members WS (PPT)
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=;
    Direction=Export;
    Format/Evaluate=XML Format/Evaluate;
    UseDefaultNamespace=Yes;
  }
  ELEMENTS
  {
    { [{19A392AC-0B07-44D5-9496-9B2FD1B48B7F}];0 ;DocumentMembers     ;Element ;Table   ;
                                                  SourceTable=Table11012746;
                                                  MaxOccurs=Once;
                                                  Export::OnAfterGetRecord=BEGIN
                                                                             Job.SETRANGE("No.", ProjectNo);
                                                                           END;
                                                                            }

    { [{C273DA28-5F60-4A89-90D5-A2907F7E12E6}];1 ;ProjectNo           ;Element ;Text    ;
                                                  VariableName=ProjectNo;
                                                  MaxOccurs=Once }

    { [{F3EFA2E4-F086-4FE1-86FC-6D69A6B5B146}];1 ;DocumentId          ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=Document Properties::No. }

    { [{6BE29689-3824-47A7-B823-64E57F05594A}];1 ;InternalUsers       ;Element ;Text    ;
                                                  VariableName=InternalUsers;
                                                  MaxOccurs=Once }

    { [{896C7478-8AC4-428C-A245-08A1E5E7FCFD}];2 ;InternalUser        ;Element ;Table   ;
                                                  SourceTable=Table11012040;
                                                  MinOccurs=Zero;
                                                  Export::OnPreXMLItem=BEGIN
                                                                         "Project Responsible Person".SETRANGE("Project No.", ProjectNo);
                                                                       END;

                                                  Export::OnAfterGetRecord=VAR
                                                                             UserSetup@1100528300 : Record 91;
                                                                           BEGIN
                                                                             UserSetup.SETRANGE("Employee No.", "Project Responsible Person"."Employee No.");
                                                                             UserSetup.FINDFIRST;

                                                                             IF (NOT CheckDocPermProjectRespPerson) THEN
                                                                               currXMLport.SKIP
                                                                             ELSE
                                                                               AD_UserId := UserSetup."User ID";
                                                                           END;
                                                                            }

    { [{E3126145-7160-48FD-AB49-104767250E01}];3 ;AD_UserId           ;Element ;Text    ;
                                                  VariableName=AD_UserId;
                                                  DataType=OemCode;
                                                  MaxOccurs=Once }

    { [{55F54C63-D71C-4125-B8C9-9178B30556FE}];1 ;ExternalUsers       ;Element ;Text    ;
                                                  MaxOccurs=Once }

    { [{F59DC928-F7CE-4B6A-ACE0-F1A549651D39}];2 ;ExternalUser        ;Element ;Table   ;
                                                  SourceTable=Table11020216;
                                                  MinOccurs=Zero;
                                                  Export::OnPreXMLItem=BEGIN
                                                                         "Project Authority".SETRANGE("Project No.", ProjectNo);
                                                                       END;

                                                  Export::OnAfterGetRecord=VAR
                                                                             Contact@1100528300 : Record 5050;
                                                                           BEGIN
                                                                             IF ("Project Authority"."Contact Person No." = '') THEN
                                                                               Contact.GET("Project Authority"."Contact No.")
                                                                             ELSE
                                                                               Contact.GET("Project Authority"."Contact Person No.");

                                                                             IF (NOT CheckDocPermProjectAuthority) THEN
                                                                               currXMLport.SKIP
                                                                             ELSE
                                                                               FBA_UserId := Contact."E-Mail 2";
                                                                           END;
                                                                            }

    { [{A519913A-E07E-4DAB-98E0-3C19C9E616C5}];3 ;FBA_UserId          ;Element ;Text    ;
                                                  MaxOccurs=Once }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      Job@1100528300 : Record 11072003;

    PROCEDURE SetProjectNo@1100528300(NewProjectNo@1100528300 : Code[20]);
    BEGIN
      ProjectNo := NewProjectNo;
    END;

    LOCAL PROCEDURE CheckDocPermProjectRespPerson@1100528332() : Boolean;
    VAR
      RespPersPermPPT@1100528304 : Record 11229812;
      RecordFound@1100528308 : Boolean;
    BEGIN
      RecordFound := FALSE;

      RespPersPermPPT.SETRANGE(Type, RespPersPermPPT.Type::"Field Filter");
      RespPersPermPPT.SETRANGE("Project No.", "Project Responsible Person"."Project No.");
      RespPersPermPPT.SETRANGE("Line No.", "Project Responsible Person"."Line No.");
      RespPersPermPPT.SETRANGE("Project Portal Subsite", "Document Properties"."Project Portal Subsite");
      RespPersPermPPT.SETRANGE("Document Type", "Document Properties"."Document Type");
      RespPersPermPPT.SETRANGE("Project Element", "Document Properties"."Project Element");
      IF (RespPersPermPPT.FINDFIRST) THEN
        RecordFound := TRUE;
      IF (NOT RecordFound) THEN BEGIN
        RespPersPermPPT.SETRANGE("Project Element", '');
        IF (RespPersPermPPT.FINDFIRST) THEN
          RecordFound := TRUE;
      END;
      IF (NOT RecordFound) THEN BEGIN
        RespPersPermPPT.SETRANGE("Document Type", '');
        IF (RespPersPermPPT.FINDFIRST) THEN
          RecordFound := TRUE;
      END;

      IF (RecordFound AND RespPersPermPPT."Show in View") THEN //OR (NOT RecordFound) THEN
        EXIT(TRUE);

      EXIT(FALSE);
    END;

    LOCAL PROCEDURE CheckDocPermProjectAuthority@1100528327() : Boolean;
    VAR
      AuthorityPermPPT@1100528305 : Record 11229813;
      RecordFound@1100528304 : Boolean;
    BEGIN
      RecordFound := FALSE;

      AuthorityPermPPT.SETRANGE(Type, AuthorityPermPPT.Type::"Field Filter");
      AuthorityPermPPT.SETRANGE("Project No.", "Project Authority"."Project No.");
      AuthorityPermPPT.SETRANGE("Contact No.", "Project Authority"."Contact No.");
      AuthorityPermPPT.SETRANGE("Contact Person No.", "Project Authority"."Contact Person No.");
      AuthorityPermPPT.SETRANGE("Project Portal Subsite", "Document Properties"."Project Portal Subsite");
      AuthorityPermPPT.SETRANGE("Document Type", "Document Properties"."Document Type");
      AuthorityPermPPT.SETRANGE("Project Element", "Document Properties"."Project Element");
      IF (AuthorityPermPPT.FINDFIRST) THEN
        RecordFound := TRUE;
      IF (NOT RecordFound) THEN BEGIN
        AuthorityPermPPT.SETRANGE("Project Element", '');
        IF (AuthorityPermPPT.FINDFIRST) THEN
          RecordFound := TRUE;
      END;
      IF (NOT RecordFound) THEN BEGIN
        AuthorityPermPPT.SETRANGE("Document Type", '');
        IF (AuthorityPermPPT.FINDFIRST) THEN
          RecordFound := TRUE;
      END;

      IF (RecordFound AND AuthorityPermPPT."Show in View") THEN //OR (NOT RecordFound) THEN
        EXIT(TRUE);

      EXIT(FALSE);
    END;

    BEGIN
    END.
  }
}

