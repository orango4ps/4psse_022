OBJECT Codeunit 11072025 SegCriteriaManagement
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04,4PS14.00 (5062);
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [External]
    PROCEDURE InsertContact@2(SegmentNo@1000 : Code[20];ContactNo@1001 : Code[20]);
    VAR
      Cont@1002 : Record 5050;
    BEGIN
      Cont.SETRANGE("No.",ContactNo);

      InsertCriteriaAction(SegmentNo,REPORT::"Add Contacts",FALSE,FALSE,FALSE,FALSE,FALSE);
      InsertCriteriaFilter(SegmentNo,DATABASE::Contact,Cont.GETFILTERS,Cont.GETVIEW(FALSE));
    END;

    [External]
    PROCEDURE DeleteContact@3(SegmentNo@1000 : Code[20];ContactNo@1001 : Code[20]);
    VAR
      Cont@1002 : Record 5050;
    BEGIN
      Cont.SETRANGE("No.",ContactNo);

      InsertCriteriaAction(SegmentNo,REPORT::"Remove Contacts - Reduce",FALSE,FALSE,FALSE,FALSE,FALSE);
      InsertCriteriaFilter(SegmentNo,DATABASE::Contact,Cont.GETFILTERS,Cont.GETVIEW(FALSE));
    END;

    [External]
    PROCEDURE InsertReuseLogged@5(SegmentNo@1000 : Code[20];LoggedSegmentEntryNo@1001 : Integer);
    VAR
      InteractLogEntry@1002 : Record 11072027;
    BEGIN
      InteractLogEntry.SETCURRENTKEY("Logged Segment Entry No.");
      InteractLogEntry.SETRANGE("Logged Segment Entry No.",LoggedSegmentEntryNo);

      InsertCriteriaAction(SegmentNo,REPORT::"Add Contacts",TRUE,FALSE,FALSE,FALSE,FALSE);
      InsertCriteriaFilter(
        SegmentNo,DATABASE::"Interaction Log Entry",InteractLogEntry.GETFILTERS,InteractLogEntry.GETVIEW(FALSE));
    END;

    [External]
    PROCEDURE InsertCriteriaAction@1(SegmentNo@1000 : Code[20];CalledFromReportNo@1001 : Integer;AllowExistingContacts@1002 : Boolean;ExpandContact@1003 : Boolean;AllowCompanyWithPersons@1004 : Boolean;IgnoreExclusion@1005 : Boolean;EntireCompanies@1006 : Boolean);
    VAR
      SegCriteriaLine@1007 : Record 11072048;
      NextLineNo@1008 : Integer;
      IsHandled@1009 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeInsertCriteriaAction(
        SegmentNo,CalledFromReportNo,AllowExistingContacts,ExpandContact,AllowCompanyWithPersons,IgnoreExclusion,EntireCompanies,
        IsHandled);
      IF IsHandled THEN
        EXIT;

      SegCriteriaLine.LOCKTABLE;
      SegCriteriaLine.SETRANGE("Segment No.",SegmentNo);
      IF SegCriteriaLine.FINDLAST THEN
        NextLineNo := SegCriteriaLine."Line No." + 1
      ELSE
        NextLineNo := 1;

      SegCriteriaLine.INIT;
      SegCriteriaLine."Segment No." := SegmentNo;
      SegCriteriaLine."Line No." := NextLineNo;
      SegCriteriaLine.Type := SegCriteriaLine.Type::Action;
      CASE CalledFromReportNo OF
        REPORT::"Add Contacts":
          SegCriteriaLine.Action := SegCriteriaLine.Action::"Add Contacts";
        REPORT::"Remove Contacts - Reduce":
          SegCriteriaLine.Action := SegCriteriaLine.Action::"Remove Contacts (Reduce)";
        REPORT::"Remove Contacts - Refine":
          SegCriteriaLine.Action := SegCriteriaLine.Action::"Remove Contacts (Refine)";
        ELSE
          OnInsertCriteriaActionOnCalledFromReportNoElseCase(SegCriteriaLine,CalledFromReportNo);
      END;
      SegCriteriaLine."Allow Existing Contacts" := AllowExistingContacts;
      SegCriteriaLine."Expand Contact" := ExpandContact;
      SegCriteriaLine."Allow Company with Persons" := AllowCompanyWithPersons;
      SegCriteriaLine."Ignore Exclusion" := IgnoreExclusion;
      SegCriteriaLine."Entire Companies" := EntireCompanies;
      OnBeforeInsertCriteriaActionOnBeforeSegCriteriaLineInsert(SegCriteriaLine);
      SegCriteriaLine.INSERT;
    END;

    [External]
    PROCEDURE InsertCriteriaFilter@4(SegmentNo@1000 : Code[20];TableNo@1001 : Integer;Filter@1002 : Text[250];View@1003 : Text[250]);
    VAR
      SegCriteriaLine@1004 : Record 11072048;
      NextLineNo@1005 : Integer;
      IsHandled@1006 : Boolean;
    BEGIN
      IsHandled := FALSE;
      OnBeforeInsertCriteriaFilter(SegmentNo,TableNo,Filter,View,IsHandled);
      IF IsHandled THEN
        EXIT;

      IF Filter = '' THEN
        EXIT;

      SegCriteriaLine.SETRANGE("Segment No.",SegmentNo);
      IF SegCriteriaLine.FINDLAST THEN
        NextLineNo := SegCriteriaLine."Line No." + 1
      ELSE
        NextLineNo := 1;

      SegCriteriaLine.INIT;
      SegCriteriaLine."Segment No." := SegmentNo;
      SegCriteriaLine."Line No." := NextLineNo;
      SegCriteriaLine.Type := SegCriteriaLine.Type::Filter;
      SegCriteriaLine."Table No." := TableNo;
      SegCriteriaLine.View := View;
      OnBeforeInsertCriteriaFilterOnBeforeSegCriteriaLineInsert(SegCriteriaLine);
      SegCriteriaLine.INSERT;

      SegCriteriaLine.RESET;
      SegCriteriaLine.SETCURRENTKEY("Segment No.",Type);
      SegCriteriaLine.SETRANGE("Segment No.",SegmentNo);
      SegCriteriaLine.SETRANGE(Type,SegCriteriaLine.Type::Action);
      SegCriteriaLine.FINDLAST;
      SegCriteriaLine."No. of Filters" := SegCriteriaLine."No. of Filters" + 1;
      OnBeforeInsertCriteriaFilterOnBeforeSegCriteriaLineModify(SegCriteriaLine);
      SegCriteriaLine.MODIFY;
    END;

    [External]
    PROCEDURE SegCriteriaFilter@6(TableNo@1000 : Integer;View@1001 : Text[250]) : Text[250];
    VAR
      Cont@1002 : Record 5050;
      ContProfileAnswer@1003 : Record 5089;
      ContMailingGrp@1004 : Record 5056;
      InteractLogEntry@1005 : Record 11072027;
      ContJobResp@1006 : Record 5067;
      ContIndustGrp@1007 : Record 5058;
      ContBusRel@1008 : Record 5054;
      ValueEntry@1009 : Record 5802;
      ProfileQuestionnaireLine@1010 : Record 5088;
      Filters@1011 : Text[250];
      PlotRec@1210190000 : Record 11012500;
    BEGIN
      CASE TableNo OF
        DATABASE::Contact:
          BEGIN
            Cont.SETVIEW(View);
            EXIT(Cont.GETFILTERS);
          END;
        DATABASE::"Contact Profile Answer":
          BEGIN
            ContProfileAnswer.SETVIEW(View);
            ContProfileAnswer.COPYFILTER(
              "Profile Questionnaire Code",ProfileQuestionnaireLine."Profile Questionnaire Code");
            ContProfileAnswer.COPYFILTER("Line No.",ProfileQuestionnaireLine."Line No.");
            IF ProfileQuestionnaireLine.COUNT = 1 THEN BEGIN
              ProfileQuestionnaireLine.FINDFIRST;
              EXIT(SELECTSTR(1,ContProfileAnswer.GETFILTERS) + ', ' +
                ProfileQuestionnaireLine.Question + ': ' + ProfileQuestionnaireLine.Description);
            END;
            EXIT(ContProfileAnswer.GETFILTERS);
          END;
        DATABASE::"Contact Mailing Group":
          BEGIN
            ContMailingGrp.SETVIEW(View);
            EXIT(ContMailingGrp.GETFILTERS);
          END;
        DATABASE::"Interaction Log Entry":
          BEGIN
            InteractLogEntry.SETVIEW(View);
            EXIT(InteractLogEntry.GETFILTERS);
          END;
        DATABASE::"Contact Job Responsibility":
          BEGIN
            ContJobResp.SETVIEW(View);
            EXIT(ContJobResp.GETFILTERS);
          END;
        DATABASE::"Contact Industry Group":
          BEGIN
            ContIndustGrp.SETVIEW(View);
            EXIT(ContIndustGrp.GETFILTERS);
          END;
        DATABASE::"Contact Business Relation":
          BEGIN
            ContBusRel.SETVIEW(View);
            EXIT(ContBusRel.GETFILTERS);
          END;
        DATABASE::"Value Entry":
          BEGIN
            ValueEntry.SETVIEW(View);
            EXIT(ValueEntry.GETFILTERS);
          END;
      //**4PS.sn
        DATABASE::Plot:
          BEGIN
            PlotRec.SETVIEW(View);
            EXIT(PlotRec.GETFILTERS);
          END;
      //**4PS.en

      END;

      OnAfterSegCriteriaFilter(TableNo,View,Filters);
      EXIT(Filters);
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSegCriteriaFilter@7(TableNo@1001 : Integer;View@1000 : Text[250];VAR Filters@1002 : Text[250]);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeInsertCriteriaAction@8(SegmentNo@1006 : Code[20];VAR CalledFromReportNo@1005 : Integer;VAR AllowExistingContacts@1004 : Boolean;VAR ExpandContact@1003 : Boolean;VAR AllowCompanyWithPersons@1002 : Boolean;VAR IgnoreExclusion@1001 : Boolean;VAR EntireCompanies@1000 : Boolean;VAR IsHandled@1007 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeInsertCriteriaActionOnBeforeSegCriteriaLineInsert@9(VAR SegCriteriaLine@1000 : Record 11072048);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnInsertCriteriaActionOnCalledFromReportNoElseCase@10(VAR SegCriteriaLine@1000 : Record 11072048;CalledFromReportNo@1001 : Integer);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeInsertCriteriaFilter@11(SegmentNo@1003 : Code[20];VAR TableNo@1002 : Integer;VAR Filter@1001 : Text[250];VAR View@1000 : Text[250];VAR Handled@1004 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeInsertCriteriaFilterOnBeforeSegCriteriaLineInsert@12(VAR SegCriteriaLine@1000 : Record 11072048);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeInsertCriteriaFilterOnBeforeSegCriteriaLineModify@13(VAR SegCriteriaLine@1000 : Record 11072048);
    BEGIN
    END;

    PROCEDURE InsertCriteriaAction4PS@1100529601(SegmentNo@1000 : Code[20];CalledFromReportNo@1001 : Integer;AllowExistingContacts@1002 : Boolean;ExpandContact@1003 : Boolean;AllowCompanyWithPersons@1004 : Boolean;IgnoreExclusion@1005 : Boolean;EntireCompanies@1006 : Boolean;AllowExistingPlots@1100529600 : Boolean);
    VAR
      SegCriteriaLine@1007 : Record 11072048;
      NextLineNo@1008 : Integer;
    BEGIN
      //**4PS
      //copy of InsertCriteriaAction
      SegCriteriaLine.LOCKTABLE;
      SegCriteriaLine.SETRANGE("Segment No.",SegmentNo);
      IF SegCriteriaLine.FINDLAST THEN
        NextLineNo := SegCriteriaLine."Line No." + 1
      ELSE
        NextLineNo := 1;

      SegCriteriaLine.INIT;
      SegCriteriaLine."Segment No." := SegmentNo;
      SegCriteriaLine."Line No." := NextLineNo;
      SegCriteriaLine.Type := SegCriteriaLine.Type::Action;
      CASE CalledFromReportNo OF
        REPORT::"Add Plots":
          SegCriteriaLine.Action := SegCriteriaLine.Action::"Add Plots";
      END;
      SegCriteriaLine."Allow Existing Contacts" := AllowExistingContacts;
      SegCriteriaLine."Expand Contact" := ExpandContact;
      SegCriteriaLine."Allow Company with Persons" := AllowCompanyWithPersons;
      SegCriteriaLine."Ignore Exclusion" := IgnoreExclusion;
      SegCriteriaLine."Entire Companies" := EntireCompanies;
      SegCriteriaLine."Allow Existing Plots" := AllowExistingPlots;
      SegCriteriaLine.INSERT;
    END;

    PROCEDURE InsertCriteriaFilter4PS@1100529600(SegmentNo@1000 : Code[20];TableNo@1001 : Integer;Filter@1002 : Text[250];View@1003 : Text[250];SecondBuyer@1100529600 : Boolean);
    VAR
      SegCriteriaLine@1004 : Record 11072048;
      NextLineNo@1005 : Integer;
    BEGIN
      //**4PS
      //copy of InsertCriteriaFilter
      IF Filter = '' THEN
        EXIT;

      SegCriteriaLine.SETRANGE("Segment No.",SegmentNo);
      IF SegCriteriaLine.FINDLAST THEN
        NextLineNo := SegCriteriaLine."Line No." + 1
      ELSE
        NextLineNo := 1;

      SegCriteriaLine.INIT;
      SegCriteriaLine."Segment No." := SegmentNo;
      SegCriteriaLine."Line No." := NextLineNo;
      SegCriteriaLine.Type := SegCriteriaLine.Type::Filter;
      SegCriteriaLine."Table No." := TableNo;
      SegCriteriaLine.View := View;
      SegCriteriaLine."Second Buyer (Plot)" := SecondBuyer;
      SegCriteriaLine.INSERT;

      SegCriteriaLine.RESET;
      SegCriteriaLine.SETCURRENTKEY("Segment No.",Type);
      SegCriteriaLine.SETRANGE("Segment No.",SegmentNo);
      SegCriteriaLine.SETRANGE(Type, SegCriteriaLine.Type::Action);
      SegCriteriaLine.FINDLAST;
      SegCriteriaLine."No. of Filters" := SegCriteriaLine."No. of Filters" + 1;
      SegCriteriaLine.MODIFY;
    END;

    BEGIN
    END.
  }
}

