OBJECT Codeunit 781 Sales Pipeline Chart Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [External]
    PROCEDURE DrillDown@8(VAR BusinessChartBuffer@1000 : Record 485;VAR SalesCycleStage@1001 : Record 11072043);
    VAR
      OppEntry@1002 : Record 5093;
    BEGIN
      IF SalesCycleStage.FINDSET THEN BEGIN
        SalesCycleStage.NEXT(BusinessChartBuffer."Drill-Down X Index");
        OppEntry.SETRANGE("Sales Cycle Code",SalesCycleStage."Sales Cycle Code");
        OppEntry.SETRANGE("Sales Cycle Stage",SalesCycleStage.Stage);
        PAGE.RUN(PAGE::"Opportunity Entries",OppEntry);
      END;
    END;

    [External]
    PROCEDURE GetOppEntryCount@17(SalesCycleCode@1000 : Code[10];SalesCycleStage@1001 : Integer) : Integer;
    VAR
      OppEntry@1002 : Record 5093;
    BEGIN
      OppEntry.SETRANGE("Sales Cycle Code",SalesCycleCode);
      OppEntry.SETRANGE("Sales Cycle Stage",SalesCycleStage);
      EXIT(OppEntry.COUNT);
    END;

    [External]
    PROCEDURE InsertTempSalesCycleStage@2(VAR TempSalesCycleStage@1000 : TEMPORARY Record 11072043;SalesCycle@1001 : Record 11072042);
    VAR
      SourceSalesCycleStage@1002 : Record 11072043;
    BEGIN
      TempSalesCycleStage.RESET;
      TempSalesCycleStage.DELETEALL;

      SourceSalesCycleStage.SETRANGE("Sales Cycle Code",SalesCycle.Code);
      IF SourceSalesCycleStage.FINDSET THEN
        REPEAT
          TempSalesCycleStage := SourceSalesCycleStage;
          TempSalesCycleStage.INSERT;
        UNTIL SourceSalesCycleStage.NEXT = 0;
    END;

    [External]
    PROCEDURE SetDefaultSalesCycle@1(VAR SalesCycle@1000 : Record 11072042;VAR NextSalesCycleAvailable@1001 : Boolean;VAR PrevSalesCycleAvailable@1002 : Boolean) : Boolean;
    BEGIN
      IF NOT SalesCycle.FINDFIRST THEN
        EXIT(FALSE);

      NextSalesCycleAvailable := TryNextSalesCycle(SalesCycle);
      PrevSalesCycleAvailable := TryPrevSalesCycle(SalesCycle);
      EXIT(TRUE);
    END;

    [External]
    PROCEDURE SetPrevNextSalesCycle@14(VAR SalesCycle@1000 : Record 11072042;VAR NextSalesCycleAvailable@1002 : Boolean;VAR PrevSalesCycleAvailable@1001 : Boolean;Step@1003 : Integer);
    BEGIN
      SalesCycle.NEXT(Step);
      NextSalesCycleAvailable := TryNextSalesCycle(SalesCycle);
      PrevSalesCycleAvailable := TryPrevSalesCycle(SalesCycle);
    END;

    LOCAL PROCEDURE TryNextSalesCycle@12(CurrentSalesCycle@1000 : Record 11072042) : Boolean;
    VAR
      NextSalesCycle@1001 : Record 11072042;
    BEGIN
      NextSalesCycle := CurrentSalesCycle;
      NextSalesCycle.FIND('=><');
      EXIT(NextSalesCycle.NEXT <> 0);
    END;

    LOCAL PROCEDURE TryPrevSalesCycle@13(CurrentSalesCycle@1000 : Record 11072042) : Boolean;
    VAR
      PrevSalesCycle@1001 : Record 11072042;
    BEGIN
      PrevSalesCycle := CurrentSalesCycle;
      PrevSalesCycle.FIND('=><');
      EXIT(PrevSalesCycle.NEXT(-1) <> 0);
    END;

    PROCEDURE UpdateData@3(VAR BusinessChartBuffer@1000 : Record 485;VAR TempSalesCycleStage@1005 : TEMPORARY Record 11072043;SalesCycle@1006 : Record 11072042);
    VAR
      I@1004 : Integer;
    BEGIN
      WITH BusinessChartBuffer DO BEGIN
        Initialize;
        AddMeasure(TempSalesCycleStage.FIELDCAPTION("No. of Opportunities"),1,"Data Type"::Integer,"Chart Type"::Funnel);
        SetXAxis(TempSalesCycleStage.TABLECAPTION,"Data Type"::String);
        InsertTempSalesCycleStage(TempSalesCycleStage,SalesCycle);
        IF TempSalesCycleStage.FINDSET THEN BEGIN
          REPEAT
            I += 1;
            AddColumn(TempSalesCycleStage.Description);
            SetValueByIndex(0,I - 1,GetOppEntryCount(TempSalesCycleStage."Sales Cycle Code",TempSalesCycleStage.Stage));
          UNTIL TempSalesCycleStage.NEXT = 0;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

