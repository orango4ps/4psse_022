OBJECT Table 11071783 Service Surcharge Version
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ServiceContract@1100528600 : Record 11012812;
               ServiceOrder@1100528601 : Record 11012823;
               ServiceContractTemplate@1100528602 : Record 11071754;
             BEGIN
               IF "Index Date" = 0D THEN BEGIN
                 CASE Type OF
                   Type::Contract:
                     BEGIN
                       IF ServiceContract.GET(Code) THEN
                         "Index Date" := ServiceContract."Starting Date";
                     END;
                   Type::"Service Order":
                     BEGIN
                       IF ServiceOrder.GET(Code) THEN
                         "Index Date" := ServiceOrder."Order Date";
                     END;
                   Type::"Contract Template":
                     BEGIN
                       IF ServiceContractTemplate.GET(Code) THEN
                         TESTFIELD("Department Code", ServiceContractTemplate."Global Dimension 1 Code");
                     END;
                 END;
               END;
             END;

    OnDelete=VAR
               ServiceSurchargeLine@1100528600 : Record 11071784;
             BEGIN
               ServiceSurchargeLine.SETRANGE(Type, Type);
               ServiceSurchargeLine.SETRANGE(Code, Code);
               ServiceSurchargeLine.SETRANGE("Source Type", "Source Type");
               ServiceSurchargeLine.SETRANGE("Service Type", "Service Type");
               ServiceSurchargeLine.SETRANGE("Department Code", "Department Code");
               ServiceSurchargeLine.SETRANGE("Index Date", "Index Date");
               ServiceSurchargeLine.DELETEALL(TRUE);
             END;

    OnRename=VAR
               ServiceSurchargeLine@1100528600 : Record 11071784;
               ChangedServiceSurchargeLine@1100528601 : Record 11071784;
               ServiceOrder@1210190000 : Record 11012823;
               ServiceSurchargeManagement@1100528602 : Codeunit 11012834;
             BEGIN
               IF (("Index Date" <> 0D) AND ("Index Date" <> xRec."Index Date")) OR
                 ("Source Type" <> xRec."Source Type") OR
                 ("Service Type" <> xRec."Service Type") OR
                 ("Department Code" <> xRec."Department Code")
               THEN BEGIN
                 ServiceSurchargeLine.SETRANGE(Type, xRec.Type);
                 ServiceSurchargeLine.SETRANGE(Code, xRec.Code);
                 ServiceSurchargeLine.SETRANGE("Source Type", xRec."Source Type");
                 ServiceSurchargeLine.SETRANGE("Service Type", xRec."Service Type");
                 ServiceSurchargeLine.SETRANGE("Department Code", xRec."Department Code");
                 ServiceSurchargeLine.SETRANGE("Index Date", xRec."Index Date");
                 IF ServiceSurchargeLine.FINDSET THEN
                   REPEAT
                     ChangedServiceSurchargeLine.COPY(ServiceSurchargeLine);
                     ChangedServiceSurchargeLine.Type := Type;
                     ChangedServiceSurchargeLine.Code := Code;
                     ChangedServiceSurchargeLine."Source Type" := "Source Type";
                     ChangedServiceSurchargeLine."Service Type" := "Service Type";
                     ChangedServiceSurchargeLine."Department Code" := "Department Code";
                     ChangedServiceSurchargeLine."Index Date" := "Index Date";
                     ChangedServiceSurchargeLine.INSERT;
                     ServiceSurchargeLine.DELETE;
                   UNTIL ServiceSurchargeLine.NEXT = 0;
                 IF Type = Type::"Service Order" THEN BEGIN
                   ServiceOrder.GET(Code);
                   ServiceSurchargeManagement.UpdateSOSurchargeBudgetLines(ServiceOrder);
                 END;
               END;
             END;

    CaptionML=[ENU=Service Surcharge Version;
               NOR=Servicetillegsavgift, versjon;
               SVE=Version av servicetill„ggsavgift];
    LookupPageID=Page11125205;
    DrillDownPageID=Page11125205;
  }
  FIELDS
  {
    { 10  ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=[ENU=Company,Contract,Service Order,,,,,,Contract Template;
                                                                    NOR=Selskap,Kontrakt,Serviceordre,,,,,,Kontraktmal;
                                                                    SVE=F”retag,Kontakt,Serviceorder,,,,,,Kontraktmall];
                                                   OptionString=Company,Contract,Service Order,,,,,,Contract Template;
                                                   Editable=No }
    { 20  ;   ;Code                ;Code20        ;TableRelation=IF (Type=CONST(Contract)) Service-Contract
                                                                 ELSE IF (Type=CONST(Service Order)) "Service Order";
                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod] }
    { 25  ;   ;Source Type         ;Option        ;OnValidate=BEGIN
                                                                IF "Source Type" IN ["Source Type"::Modification, "Source Type"::Replacement] THEN
                                                                  TESTFIELD(Type, Type::Company);
                                                              END;

                                                   CaptionML=[ENU=Source Type;
                                                              NOR=Kildetype;
                                                              SVE=Ursprungstyp];
                                                   OptionCaptionML=[ENU=Call,Contract,Direct,,,,Modification,Replacement;
                                                                    NOR=Samtale,Kontrakt,Direkte,,,,Endring,Erstatning;
                                                                    SVE=Samtal,Kontrakt,Direkt,,,,Žndring,Ers„ttning];
                                                   OptionString=Call,Contract,Direct,,,,Modification,Replacement }
    { 27  ;   ;Service Type        ;Code20        ;TableRelation="Service Type";
                                                   OnValidate=BEGIN
                                                                IF "Service Type" = '' THEN
                                                                  TESTFIELD("Department Code", '');
                                                              END;

                                                   CaptionML=[ENU=Service Type;
                                                              NOR=Servicetype;
                                                              SVE=Servicetyp] }
    { 28  ;   ;Service Type Description;Text50    ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Type".Description WHERE (Code=FIELD(Service Type)));
                                                   CaptionML=[ENU=Service Type Description;
                                                              NOR=Servicetypebeskrivelse;
                                                              SVE=Servicetypsbeskrivning];
                                                   Editable=No }
    { 29  ;   ;Department Code     ;Code20        ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                IF "Department Code" <> '' THEN
                                                                  TESTFIELD("Service Type");
                                                              END;

                                                   CaptionML=[ENU=Department Code;
                                                              NOR=Avdelingskode;
                                                              SVE=Mottagande avdelningskod];
                                                   CaptionClass='1,2,1' }
    { 30  ;   ;Index Date          ;Date          ;CaptionML=[ENU=Index Date;
                                                              NOR=Indeksdato;
                                                              SVE=Indexdatum];
                                                   NotBlank=Yes }
    { 40  ;   ;Expired             ;Boolean       ;CaptionML=[ENU=Expired;
                                                              NOR=Utg†tt;
                                                              SVE=Utg†ngna] }
  }
  KEYS
  {
    {    ;Type,Code,Source Type,Service Type,Department Code,Index Date;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE NewerVersionExists@1100528600() : Boolean;
    VAR
      ServiceSurchargeVersion@1100528600 : Record 11071783;
    BEGIN
      ServiceSurchargeVersion := Rec;
      ServiceSurchargeVersion.SETRECFILTER;
      ServiceSurchargeVersion.SETFILTER("Index Date", '>%1', "Index Date");
      ServiceSurchargeVersion.SETRANGE(Expired, FALSE);
      EXIT(NOT ServiceSurchargeVersion.ISEMPTY);
    END;

    BEGIN
    END.
  }
}

