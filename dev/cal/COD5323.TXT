OBJECT Codeunit 5323 Exchange Add-in Setup
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      ExchangeWebServicesServer@1000 : Codeunit 5321;
      AddinManifestManagement@1001 : Codeunit 1652;
      Initialized@1002 : Boolean;
      InvalidCredentialsErr@1005 : TextConst 'ENU=The provided email address and password are not valid Office 365 or Exchange credentials.;NOR=E-postadressen og passordet som er angitt, er ikke gyldig som Office 365- eller Exchange-legitimasjon.;SVE=Den angivna e-postadressen och l”senordet „r inte giltiga autentiseringsuppgifter f”r Office 365 eller Exchange.';
      NoMailboxErr@1006 : TextConst 'ENU=An Office 365 or Exchange mailbox could not be found for this account.;NOR=Finner ikke noen Office 365- eller Exchange-postboks for denne kontoen.;SVE=Ingen Office 365- eller Exchange-postl†da kunde hittas f”r kontot.';
      AutodiscoverMsg@1008 : TextConst 'ENU=Searching for your mailbox.;NOR=S›ker etter postboksen din.;SVE=S”ker efter din postl†da.';
      WelcomeSubjectTxt@1016 : TextConst '@@@=%1 - Application name;ENU=Welcome to %1 - your Business Inbox in Outlook is ready!;NOR=Velkommen til %1 - forretningsinnboksen i Outlook er klar!;SVE=V„lkommen till %1 - din f”retagsinkorg i Outlook „r klar.';
      WelcomeEmailFromNameTxt@1014 : TextConst '@@@=%1 - Application Name;ENU=%1 Admin;NOR=%1 Admin;SVE=%1-administrat”r';
      SalesEmailAddrTxt@1012 : TextConst '@@@={Locked};ENU=admin@contoso.com;NOR=admin@contoso.com;SVE=jim.glynn@contoso.com';

    [TryFunction]
    LOCAL PROCEDURE Initialize@1003(AuthenticationEmail@1003 : Text[80]);
    VAR
      ExchangeServiceSetup@1000 : Record 5324;
      AzureADMgt@1001 : Codeunit 6300;
      AccessToken@1002 : Text;
    BEGIN
      AccessToken := AzureADMgt.GetAccessToken(AzureADMgt.GetO365Resource,AzureADMgt.GetO365ResourceName,FALSE);

      IF AccessToken <> '' THEN BEGIN
        ExchangeWebServicesServer.InitializeWithOAuthToken(AccessToken,ExchangeWebServicesServer.GetEndpoint);
        IF ValidateCredentials THEN
          EXIT;
      END;

      ExchangeServiceSetup.GET;
      WITH ExchangeServiceSetup DO
        ExchangeWebServicesServer.InitializeWithCertificate("Azure AD App. ID","Azure AD App. Cert. Thumbprint",
          "Azure AD Auth. Endpoint","Exchange Service Endpoint","Exchange Resource Uri");

      ExchangeWebServicesServer.SetImpersonatedIdentity(AuthenticationEmail);
      Initialized := TRUE;
    END;

    [TryFunction]
    [Internal]
    PROCEDURE InitializeServiceWithCredentials@1002(Email@1000 : Text[80];Password@1001 : Text);
    VAR
      WebCredentials@1002 : DotNet "'Microsoft.Exchange.WebServices, Version=15.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Exchange.WebServices.Data.WebCredentials";
      ProgressWindow@1004 : Dialog;
      ErrorText@1005 : Text;
    BEGIN
      WebCredentials := WebCredentials.WebCredentials(Email,Password);

      ProgressWindow.OPEN('#1');
      ProgressWindow.UPDATE(1,AutodiscoverMsg);

      // Production O365 endpoint
      Initialized := ExchangeWebServicesServer.Initialize(Email,ExchangeWebServicesServer.GetEndpoint,WebCredentials,FALSE) AND
        ValidateCredentials;
      ErrorText := GETLASTERRORTEXT;

      // Autodiscover endpoint (can be slow)
      IF NOT Initialized THEN BEGIN
        Initialized := ExchangeWebServicesServer.Initialize(Email,'',WebCredentials,TRUE) AND ValidateCredentials;
        ErrorText := GETLASTERRORTEXT;
      END;

      ProgressWindow.CLOSE;

      IF NOT Initialized THEN
        ERROR(ErrorText);
    END;

    [Internal]
    PROCEDURE CredentialsRequired@1008(AuthenticationEmail@1000 : Text[80]) Required : Boolean;
    BEGIN
      Required := NOT Initialize(AuthenticationEmail);
    END;

    [Internal]
    PROCEDURE PromptForCredentials@1014() : Boolean;
    VAR
      User@1000 : Record 2000000120;
      TempOfficeAdminCredentials@1001 : TEMPORARY Record 1612;
    BEGIN
      TempOfficeAdminCredentials.INIT;
      TempOfficeAdminCredentials.INSERT;

      User.SETRANGE("User Name",USERID);
      IF User.FINDFIRST THEN BEGIN
        TempOfficeAdminCredentials.Email := User."Authentication Email";
        TempOfficeAdminCredentials.MODIFY;
      END;

      IF CredentialsRequired(TempOfficeAdminCredentials.Email) OR (TempOfficeAdminCredentials.Email = '') THEN BEGIN
        CLEARLASTERROR;
        REPEAT
          IF PAGE.RUNMODAL(PAGE::"Office 365 Credentials",TempOfficeAdminCredentials) <> ACTION::LookupOK THEN
            EXIT(FALSE);
        UNTIL InitializeServiceWithCredentials(TempOfficeAdminCredentials.Email,TempOfficeAdminCredentials.GetPassword);
      END;

      EXIT(TRUE);
    END;

    [Internal]
    PROCEDURE ImpersonateUser@1006(Email@1000 : Text[80]);
    BEGIN
      IF NOT Initialized THEN
        Initialize(Email);

      ExchangeWebServicesServer.SetImpersonatedIdentity(Email);
    END;

    [External]
    PROCEDURE SampleEmailsAvailable@1007() : Boolean;
    VAR
      CompanyInformationMgt@1001 : Codeunit 1306;
      PermissionManager@1002 : Codeunit 9002;
    BEGIN
      EXIT(CompanyInformationMgt.IsDemoCompany AND PermissionManager.SoftwareAsAService);
    END;

    [Internal]
    PROCEDURE DeployAddin@1013(OfficeAddin@1000 : Record 1610);
    VAR
      UserPreference@1004 : Record 1306;
      InstructionMgt@1005 : Codeunit 1330;
      Stream@1001 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.MemoryStream";
      Encoding@1006 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.UTF8Encoding";
      ManifestText@1002 : Text;
    BEGIN
      AddinManifestManagement.GenerateManifest(OfficeAddin,ManifestText);
      Encoding := Encoding.UTF8Encoding;
      Stream := Stream.MemoryStream(Encoding.GetBytes(ManifestText));
      ExchangeWebServicesServer.InstallApp(Stream);

      UserPreference.SETRANGE("Instruction Code",InstructionMgt.OfficeUpdateNotificationCode);
      UserPreference.SETRANGE("User ID",USERID);
      UserPreference.DELETEALL;
    END;

    [Internal]
    PROCEDURE DeployAddins@1004(VAR OfficeAddin@1000 : Record 1610);
    BEGIN
      IF OfficeAddin.GetAddins THEN
        REPEAT
          DeployAddin(OfficeAddin);
        UNTIL OfficeAddin.NEXT = 0;
    END;

    [Internal]
    PROCEDURE DeploySampleEmails@1005(EmailAddress@1007 : Text);
    VAR
      User@1003 : Record 2000000120;
      ContactBusinessRelation@1005 : Record 5054;
      Contact@1006 : Record 5050;
      OfficeAddinSampleEmails@1001 : Codeunit 1655;
      RecipientEmail@1002 : Text;
      FromEmail@1004 : Text;
      HTMlBody@1000 : Text;
    BEGIN
      IF EmailAddress <> '' THEN
        RecipientEmail := EmailAddress
      ELSE BEGIN
        User.SETRANGE("User Name",USERID);
        IF User.FINDFIRST THEN
          RecipientEmail := User."Authentication Email";
      END;

      FromEmail := SalesEmailAddrTxt;
      ContactBusinessRelation.SETRANGE("Link to Table",ContactBusinessRelation."Link to Table"::Customer);
      IF ContactBusinessRelation.FINDFIRST THEN BEGIN
        IF Contact.GET(ContactBusinessRelation."Contact No.") THEN BEGIN
          IF Contact."E-Mail" <> '' THEN
            FromEmail := Contact."E-Mail";
        END
      END;

      HTMlBody := OfficeAddinSampleEmails.GetHTMLSampleMsg;
      ExchangeWebServicesServer.SaveHTMLEmailToInbox(STRSUBSTNO(WelcomeSubjectTxt,PRODUCTNAME.MARKETING),HTMlBody,
        FromEmail,STRSUBSTNO(WelcomeEmailFromNameTxt,PRODUCTNAME.FULL),RecipientEmail);
    END;

    [TryFunction]
    LOCAL PROCEDURE ValidateCredentials@1012();
    BEGIN
      IF NOT ExchangeWebServicesServer.ValidCredentials THEN BEGIN
        IF STRPOS(GETLASTERRORTEXT,'401') > 0 THEN
          ERROR(InvalidCredentialsErr);
        ERROR(NoMailboxErr);
      END;
    END;

    BEGIN
    END.
  }
}

