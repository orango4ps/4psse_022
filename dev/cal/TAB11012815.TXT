OBJECT Table 11012815 Service Contract Hour Rate
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Service Contract No.;
    CaptionML=[ENU=Service Contract Hour Rate;
               NOR=Timesats for servicekontrakt;
               SVE=Timtaxa f”r servicekontrakt];
    LookupPageID=Page11012875;
    DrillDownPageID=Page11012875;
  }
  FIELDS
  {
    { 10  ;   ;Service Contract No.;Code20        ;TableRelation=Service-Contract.No.;
                                                   CaptionML=[ENU=Service Contract No.;
                                                              NOR=Servicekontraktnr;
                                                              SVE=Servicekontraktnr];
                                                   NotBlank=Yes }
    { 15  ;   ;Starting Date       ;Date          ;CaptionML=[ENU=Starting Date;
                                                              NOR=Startdato;
                                                              SVE=Startdatum] }
    { 16  ;   ;Ending Date         ;Date          ;CaptionML=[ENU=Ending Date;
                                                              NOR=Sluttdato;
                                                              SVE=Slutdatum] }
    { 20  ;   ;Hour Rate Code      ;Code10        ;TableRelation="Hour Rate" WHERE (Project Filter=FILTER(''),
                                                                                    Principal Filter=FIELD("Bill-to Customer No. (CP)"),
                                                                                    Service Contract Filter=FIELD(Service Contract No.));
                                                   OnValidate=VAR
                                                                HourRate@1100528600 : Record 11012022;
                                                              BEGIN
                                                                IF Description = '' THEN
                                                                  Description := HourRate.RateDescription("Hour Rate Code", "Starting Date", "Ending Date");
                                                              END;

                                                   CaptionML=[ENU=Hour Rate Code;
                                                              NOR=Timesatskode;
                                                              SVE=Timtaxakod];
                                                   NotBlank=Yes }
    { 30  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 40  ;   ;Hour Rate (LCY)     ;Decimal       ;OnValidate=VAR
                                                                CurrencyDate@1100528802 : Date;
                                                                CurrencyExchangeRate@1100528801 : Record 330;
                                                                Currency2@1100528800 : Record 4;
                                                              BEGIN
                                                                // dp00116.n
                                                                Currency2.InitRoundingPrecision;
                                                                IF "Currency Code" <> '' THEN BEGIN
                                                                  CurrencyDate := TODAY;
                                                                  "Hour Rate" :=
                                                                  ROUND(
                                                                    CurrencyExchangeRate.ExchangeAmtLCYToFCY(
                                                                      0, '', CurrencyDate, "Currency Code", "Hour Rate (LCY)",
                                                                       CurrencyExchangeRate.ExchangeRate(0, '', CurrencyDate, "Currency Code",TRUE),TRUE),
                                                                       Currency2."Unit-Amount Rounding Precision");
                                                                END ELSE BEGIN
                                                                  "Hour Rate" := ROUND("Hour Rate (LCY)",Currency2."Unit-Amount Rounding Precision");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Hour Rate (LCY);
                                                              NOR=Timesats (NOK);
                                                              SVE=Timtaxa (BVA)];
                                                   MinValue=0;
                                                   AutoFormatType=2 }
    { 41  ;   ;Hour Rate           ;Decimal       ;OnValidate=VAR
                                                                CurrencyDate@1100528804 : Date;
                                                                CurrencyExchangeRate@1100528803 : Record 330;
                                                                Currency2@1100528802 : Record 4;
                                                              BEGIN
                                                                // dp00116.n
                                                                Currency2.InitRoundingPrecision;
                                                                IF "Currency Code" <> '' THEN BEGIN
                                                                  CurrencyDate := TODAY;
                                                                  "Hour Rate (LCY)" :=
                                                                  ROUND(
                                                                    CurrencyExchangeRate.ExchangeAmtFCYToLCY(
                                                                      0, '', CurrencyDate, "Currency Code", "Hour Rate",
                                                                       CurrencyExchangeRate.ExchangeRate(0, '', CurrencyDate, "Currency Code",TRUE),TRUE),
                                                                       Currency2."Unit-Amount Rounding Precision");
                                                                END ELSE BEGIN
                                                                  "Hour Rate (LCY)" := ROUND("Hour Rate",Currency2."Unit-Amount Rounding Precision");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Hour Rate;
                                                              NOR=Timesats;
                                                              SVE=Timtaxa];
                                                   MinValue=0;
                                                   AutoFormatType=2 }
    { 42  ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   OnValidate=BEGIN
                                                                // dp00116.n
                                                                VALIDATE("Hour Rate");
                                                              END;

                                                   CaptionML=[ENU=Currency Code;
                                                              NOR=Valutakode;
                                                              SVE=Valutakod] }
    { 50  ;   ;Bill-to Customer No. (CP);Code20   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Contract."Bill-to Customer No. (CP)" WHERE (No.=FIELD(Service Contract No.)));
                                                   CaptionML=[ENU=Bill-to Customer No. (Cost Plus);
                                                              SVE=Faktureringskundnr (l”pande)];
                                                   Editable=No }
    { 60  ;   ;Service Contract Description;Text50;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Contract.Description WHERE (No.=FIELD(Service Contract No.)));
                                                   CaptionML=[ENU=Service Contract Description;
                                                              NOR=Servicekontraktsbeskrivelse;
                                                              SVE=Servicekontraktsbeskrivning];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Service Contract No.,Hour Rate Code,Starting Date;
                                                   Clustered=Yes }
    {    ;Service Contract No.,Starting Date,Hour Rate Code }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE CopyRate@1210190000();
    VAR
      HourRate@1100485003 : Record 11012022;
      CustHourRate@1100485002 : Record 11012023;
      ContractRate@1100485001 : Record 11012815;
      ServContract@1100485000 : Record 11012812;
    BEGIN
      ServContract.GET(GETFILTER("Service Contract No."));
      CustHourRate.SETRANGE("Customer No.", ServContract."Bill-to Customer No. (CP)");
      IF CustHourRate.FIND('-') THEN BEGIN
        REPEAT
          ContractRate.INIT;
          ContractRate."Service Contract No." := ServContract."No.";
          ContractRate."Starting Date" := CustHourRate."Starting Date";
          ContractRate."Hour Rate Code" := CustHourRate."Hour Rate Code";
          //ContractRate."Hour Rate" := CustHourRate."Hour Rate (LCY)"; // C005922.o
          ContractRate."Hour Rate" := CustHourRate."Hour Rate"; // C005922.n
          ContractRate."Hour Rate (LCY)" := CustHourRate."Hour Rate (LCY)"; // C005922.n
          IF ContractRate.INSERT THEN;

          ContractRate.FIND;
          IF ContractRate.Description = '' THEN BEGIN
            ContractRate.Description := HourRate.RateDescription("Hour Rate Code", ContractRate."Starting Date", ContractRate."Ending Date");
            ContractRate.MODIFY;
          END;
        UNTIL CustHourRate.NEXT = 0;
      END;

      IF HourRate.FIND('-') THEN BEGIN
        REPEAT
          ContractRate.INIT;
          ContractRate."Service Contract No." := ServContract."No.";
          ContractRate."Starting Date" := HourRate."Starting Date";
          ContractRate."Hour Rate Code" := HourRate.Code;
          //ContractRate."Hour Rate" := HourRate."Hour Rate"; // C005922.o
          ContractRate.VALIDATE("Hour Rate (LCY)", HourRate."Hour Rate"); // C005922.n
          ContractRate.Description := HourRate.Description;
          IF ContractRate.INSERT THEN;
          ContractRate.FIND;
          IF ContractRate.Description = '' THEN BEGIN
            ContractRate.Description := HourRate.RateDescription(ContractRate."Hour Rate Code", ContractRate."Starting Date", ContractRate."Ending Date");
            ContractRate.MODIFY;
          END;
        UNTIL HourRate.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}

