OBJECT Table 11072342 DPT Def Authority Perm ServLoc
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnModify=BEGIN
               CALCFIELDS("Subsite Structure");
               IF ("Subsite Structure" < "Subsite Structure"::PermissionPerDocType) AND (Type = Type::Access) THEN
                 "Document Type" := '';
               CheckPermissions;
             END;

    CaptionML=ENU=DPT Default Authority Permission (Service);
  }
  FIELDS
  {
    { 10  ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Typ];
                                                   OptionCaptionML=ENU=,Access;
                                                   OptionString=,Access;
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 20  ;   ;Authority Type      ;Code20        ;TableRelation="Authority Type";
                                                   CaptionML=[ENU=Authority Type;
                                                              NOR=Myndighetstype;
                                                              SVE=Beh”righetstyp];
                                                   NotBlank=Yes }
    { 21  ;   ;Authority Type Description;Text50  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Authority Type".Description WHERE (Code=FIELD(Authority Type)));
                                                   CaptionML=[ENU=Authority Type Description;
                                                              NOR=Beskrivelse av myndighetstype;
                                                              SVE=Beskrivning av beh”righetstyp];
                                                   Editable=No }
    { 30  ;   ;Subsite Code        ;Code20        ;TableRelation="DPT Default Subsite".Code WHERE (Source Type=CONST(ServiceLocation),
                                                                                                   Quote Request Subsite=CONST(No));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Subsite Structure");
                                                              END;

                                                   CaptionML=ENU=Subsite Code }
    { 31  ;   ;Subsite Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Default Subsite".Description WHERE (Source Type=CONST(ServiceLocation),
                                                                                                               Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Subsite Description;
                                                   Editable=No }
    { 32  ;   ;Subsite Structure   ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Default Subsite"."Subsite Structure" WHERE (Source Type=CONST(ServiceLocation),
                                                                                                                       Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Subsite Structure;
                                                   OptionCaptionML=ENU=1 Document Library,Document Library per Document Type,Permission per Document Type;
                                                   OptionString=OneDocLibrary,DocLibraryPerDocType,PermissionPerDocType;
                                                   Editable=No }
    { 40  ;   ;Document Type       ;Code20        ;TableRelation=IF (Type=CONST(Access),
                                                                     Subsite Structure=CONST(PermissionPerDocType)) "DPT Default Subsite Doc Type"."Document Type" WHERE (Source Type=CONST(ServiceLocation),
                                                                                                                                                                          Subsite Code=FIELD(Subsite Code));
                                                   CaptionML=[ENU=Document Type;
                                                              NOR=Dokumenttype;
                                                              SVE=Dokumenttyp] }
    { 41  ;   ;Document Type Description;Text50   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Document Type".Description WHERE (Type=FIELD(Document Type)));
                                                   CaptionML=[ENU=Document Type Description;
                                                              NOR=Dokumenttype, Beskrivelse;
                                                              SVE=Dokumenttyp, beskrivning];
                                                   Editable=No }
    { 50  ;   ;Permission Level    ;Option        ;OnValidate=BEGIN
                                                                CheckPermissions;
                                                              END;

                                                   CaptionML=ENU=Permission Level;
                                                   OptionCaptionML=ENU=None,Visitor,Member;
                                                   OptionString=None,Visitor,Member }
    { 60  ;   ;Show in View OLD    ;Boolean       ;CaptionML=;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Type,Authority Type,Subsite Code,Document Type;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE AddPortalPermissions@1100528302(CodeFilter@1100528303 : Code[20]);
    VAR
      AuthorityType@1100528301 : Record 11020215;
      DPTDefaultSubsite@1100528300 : Record 11229941;
      DPTDefaultSubsiteDocType@1100528302 : Record 11229942;
    BEGIN
      IF (NOT AuthorityType.FINDSET) THEN
        EXIT;

      REPEAT
        InsertRecord(1, AuthorityType.Code, '', '');

        DPTDefaultSubsite.SETRANGE("Source Type", DPTDefaultSubsite."Source Type"::ServiceLocation);
        IF (CodeFilter <> '') THEN
          DPTDefaultSubsite.SETRANGE(Code, CodeFilter);
        DPTDefaultSubsite.SETRANGE("Quote Request Subsite", FALSE);
        DPTDefaultSubsite.SETRANGE("Subsite Structure", DPTDefaultSubsite."Subsite Structure"::PermissionPerDocType);
        IF (DPTDefaultSubsite.FINDSET) THEN BEGIN
          REPEAT
            DPTDefaultSubsiteDocType.SETRANGE("Source Type", DPTDefaultSubsite."Source Type");
            DPTDefaultSubsiteDocType.SETRANGE("Subsite Code", DPTDefaultSubsite.Code);
            IF (DPTDefaultSubsiteDocType.FINDSET) THEN BEGIN
              InsertRecord(1, AuthorityType.Code, DPTDefaultSubsite.Code, '');
              REPEAT
                InsertRecord(1, AuthorityType.Code, DPTDefaultSubsite.Code, DPTDefaultSubsiteDocType."Document Type");
              UNTIL (DPTDefaultSubsiteDocType.NEXT = 0);
            END;
          UNTIL (DPTDefaultSubsite.NEXT = 0);
        END;

        DPTDefaultSubsite.SETFILTER("Subsite Structure", '<%1', DPTDefaultSubsite."Subsite Structure"::PermissionPerDocType);
        IF (DPTDefaultSubsite.FINDSET) THEN BEGIN
          REPEAT
            InsertRecord(1, AuthorityType.Code, DPTDefaultSubsite.Code, '');
          UNTIL (DPTDefaultSubsite.NEXT = 0);
        END;
      UNTIL (AuthorityType.NEXT = 0);
    END;

    LOCAL PROCEDURE InsertRecord@1100528301(Type@1100528304 : Option;AuthorityCode@1100528301 : Code[20];SubsiteCode@1100528302 : Code[20];DocumentType@1100528303 : Code[20]);
    VAR
      DPTDefAuthorityPermServLoc@1100528300 : Record 11072342;
    BEGIN
      IF (DPTDefAuthorityPermServLoc.GET(Type, AuthorityCode, SubsiteCode, DocumentType)) THEN
        EXIT;

      DPTDefAuthorityPermServLoc.INIT;
      DPTDefAuthorityPermServLoc.Type := Type;
      DPTDefAuthorityPermServLoc."Authority Type" := AuthorityCode;
      DPTDefAuthorityPermServLoc."Subsite Code" := SubsiteCode;
      DPTDefAuthorityPermServLoc."Document Type" := DocumentType;
      CASE Type OF
        1: DPTDefAuthorityPermServLoc."Permission Level" := DPTDefAuthorityPermServLoc."Permission Level"::Visitor;
      END;
      DPTDefAuthorityPermServLoc.INSERT;
    END;

    PROCEDURE CheckPermissions@1100409001();
    BEGIN
      CheckPermissionLevel;
    END;

    LOCAL PROCEDURE CheckPermissionLevel@1100409000();
    VAR
      DPTDefAuthorityPermServLoc@1100528300 : Record 11072342;
    BEGIN
      IF (Type <> Type::Access) THEN
        EXIT;
      IF (("Subsite Code" = '') AND ("Document Type" = '') AND ("Permission Level" = "Permission Level"::None)) THEN BEGIN
        DPTDefAuthorityPermServLoc.SETRANGE(Type , DPTDefAuthorityPermServLoc.Type::Access);
        DPTDefAuthorityPermServLoc.SETRANGE("Authority Type", "Authority Type");
        DPTDefAuthorityPermServLoc.SETFILTER("Subsite Code", '<>%1', "Subsite Code");
        DPTDefAuthorityPermServLoc.MODIFYALL("Permission Level", "Permission Level"::None);
        EXIT;
      END;
      CALCFIELDS("Subsite Structure");
      IF ("Subsite Structure" < "Subsite Structure"::PermissionPerDocType) THEN
        EXIT;

      IF ("Document Type" = '') THEN BEGIN
        DPTDefAuthorityPermServLoc.SETRANGE(Type, Type);
        DPTDefAuthorityPermServLoc.SETRANGE("Authority Type", "Authority Type");
        DPTDefAuthorityPermServLoc.SETRANGE("Subsite Code", "Subsite Code");
        DPTDefAuthorityPermServLoc.SETFILTER("Document Type", '<>%1', '');
        DPTDefAuthorityPermServLoc.MODIFYALL("Permission Level", "Permission Level");
      END ELSE BEGIN
        IF (DPTDefAuthorityPermServLoc.GET(Type, "Authority Type", "Subsite Code", '')) THEN BEGIN
          IF (DPTDefAuthorityPermServLoc."Permission Level" < "Permission Level") THEN BEGIN
            DPTDefAuthorityPermServLoc."Permission Level" := "Permission Level";
            DPTDefAuthorityPermServLoc.MODIFY;
          END;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

