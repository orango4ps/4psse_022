OBJECT Page 511 Change Exchange Rate
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW111.00,4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Change Exchange Rate;
               NOR=Endre valutakurs;
               SVE=Žndra valutakurser];
    DataCaptionExpr=DynamicDataCaption;
    PageType=StandardDialog;
    OnInit=BEGIN
             RefExchRate2Editable := TRUE;
             CurrentExchRate2Editable := TRUE;
             RefExchRateEditable := TRUE;
             CurrentExchRateEditable := TRUE;
             GLSetup.GET;
           END;

    OnOpenPage=BEGIN
                 InitForm;
               END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 17  ;1   ;Group     ;
                CaptionML=[ENU=General;
                           NOR=Generelt;
                           SVE=Allm„nt] }

    { 3   ;2   ;Field     ;
                Name=CurrencyCode;
                CaptionML=[ENU=Currency Code;
                           NOR=Valutakode;
                           SVE=Valutakod];
                ToolTipML=[ENU=Specifies the code for the currency that amounts are shown in.;
                           NOR=Angir koden for valutaen som bel›pene vises i.;
                           SVE=Anger i vilken valuta beloppen visas.];
                ApplicationArea=#Suite;
                SourceExpr=CurrencyCode;
                Editable=CurrencyCodeEditable }

    { 5   ;2   ;Field     ;
                Name=CurrentExchRate;
                CaptionML=[ENU=Exchange Rate Amount;
                           NOR=Valutakursbel›p;
                           SVE=Valutakurs belopp];
                ToolTipML=[ENU=Specifies the amounts that are used to calculate exchange rates for the foreign currency on this line. This field is used in combination with the Relational Exchange Rate Amount field.;
                           NOR=Angir bel›pene som brukes til † beregne valutakurser for fremmedvalutaen p† denne linjen. Dette feltet brukes sammen med feltet Tilh›rende valutakursbel›p.;
                           SVE=Anger beloppen som anv„nds f”r att ber„kna valutakurser f”r utl„ndsk valuta p† den h„r raden. Det h„r f„ltet anv„nds tillsammans med f„ltet Relationsvalutakurs belopp.];
                ApplicationArea=#Suite;
                DecimalPlaces=1:6;
                SourceExpr=CurrentExchRate;
                Editable=CurrentExchRateEditable;
                OnValidate=BEGIN
                             IF CurrentExchRate <= 0 THEN
                               ERROR(Text000);
                             IF RefCurrencyCode = '' THEN
                               CurrencyFactor := CurrentExchRate / RefExchRate
                             ELSE
                               CurrencyFactor := (CurrentExchRate * CurrentExchRate2) / (RefExchRate * RefExchRate2);
                           END;
                            }

    { 6   ;2   ;Field     ;
                Name=RefExchRate;
                CaptionML=[ENU=Relational Exch. Rate Amount;
                           NOR=Tilh›rende valutakursbel›p;
                           SVE=Relationsvalutakurs belopp];
                ToolTipML=[ENU=Specifies the amounts that are used to calculate exchange rates for the foreign currency on this line. This field is used in combination with the Exchange Rate Amount field.;
                           NOR=Angir bel›pene som brukes til † beregne valutakurser for fremmedvalutaen p† denne linjen. Dette feltet brukes sammen med feltet Valutakursbel›p.;
                           SVE=Anger beloppen som anv„nds f”r att ber„kna valutakurser i valutan p† den h„r raden. Det h„r f„ltet anv„nds tillsammans med f„ltet Valutakurs belopp.];
                ApplicationArea=#Suite;
                DecimalPlaces=1:6;
                SourceExpr=RefExchRate;
                Editable=RefExchRateEditable;
                OnValidate=BEGIN
                             IF RefExchRate <= 0 THEN
                               ERROR(Text000);
                             IF RefCurrencyCode = '' THEN
                               CurrencyFactor := CurrentExchRate / RefExchRate
                             ELSE
                               CurrencyFactor := (CurrentExchRate * CurrentExchRate2) / (RefExchRate * RefExchRate2);
                           END;
                            }

    { 4   ;2   ;Field     ;
                Name=RefCurrencyCode;
                CaptionML=[ENU=Relational Currency Code;
                           NOR=Tilh›rende valutakode;
                           SVE=Relationsvalutakod];
                ToolTipML=[ENU=Specifies how you want to set up the two currencies, one of the currencies can be LCY, for which you want to register exchange rates.;
                           NOR=Angir hvordan du vil definere de to valutaene. n av valutaene kan v‘re NOK, som du vil registrere valutakurser for.;
                           SVE=Anger hur du vill anv„nda de tv† valutorna. Den ena kan vara BVA, och f”r den b”r du registrera valutakurser.];
                ApplicationArea=#Suite;
                SourceExpr=ShowCurrencyCode(RefCurrencyCode,TRUE);
                Editable=RefCurrencyCodeEditable }

    { 7   ;2   ;Field     ;
                Name=CurrencyCode2;
                CaptionML=[ENU=Currency Code;
                           NOR=Valutakode;
                           SVE=Valutakod];
                ToolTipML=[ENU=Specifies the code for the currency that amounts are shown in.;
                           NOR=Angir koden for valutaen som bel›pene vises i.;
                           SVE=Anger i vilken valuta beloppen visas.];
                ApplicationArea=#Suite;
                SourceExpr=CurrencyCode2;
                Editable=CurrencyCode2Editable }

    { 8   ;2   ;Field     ;
                Name=CurrentExchRate2;
                CaptionML=[ENU=Exchange Rate Amount;
                           NOR=Valutakursbel›p;
                           SVE=Valutakurs belopp];
                ToolTipML=[ENU=Specifies the amounts that are used to calculate exchange rates for the foreign currency on this line. This field is used in combination with the Relational Exchange Rate Amount field.;
                           NOR=Angir bel›pene som brukes til † beregne valutakurser for fremmedvalutaen p† denne linjen. Dette feltet brukes sammen med feltet Tilh›rende valutakursbel›p.;
                           SVE=Anger beloppen som anv„nds f”r att ber„kna valutakurser f”r utl„ndsk valuta p† den h„r raden. Det h„r f„ltet anv„nds tillsammans med f„ltet Relationsvalutakurs belopp.];
                ApplicationArea=#Suite;
                DecimalPlaces=1:6;
                SourceExpr=CurrentExchRate2;
                Editable=CurrentExchRate2Editable;
                OnValidate=BEGIN
                             IF CurrentExchRate2 <= 0 THEN
                               ERROR(Text000);
                             CurrencyFactor := (CurrentExchRate * CurrentExchRate2) / (RefExchRate * RefExchRate2);
                           END;
                            }

    { 10  ;2   ;Field     ;
                Name=RefExchRate2;
                CaptionML=[ENU=Relational Exch. Rate Amount;
                           NOR=Tilh›rende valutakursbel›p;
                           SVE=Relationsvalutakurs belopp];
                ToolTipML=[ENU=Specifies the amounts that are used to calculate exchange rates for the foreign currency on this line. This field is used in combination with the Exchange Rate Amount field.;
                           NOR=Angir bel›pene som brukes til † beregne valutakurser for fremmedvalutaen p† denne linjen. Dette feltet brukes sammen med feltet Valutakursbel›p.;
                           SVE=Anger beloppen som anv„nds f”r att ber„kna valutakurser i valutan p† den h„r raden. Det h„r f„ltet anv„nds tillsammans med f„ltet Valutakurs belopp.];
                ApplicationArea=#Suite;
                DecimalPlaces=1:6;
                SourceExpr=RefExchRate2;
                Editable=RefExchRate2Editable;
                OnValidate=BEGIN
                             IF RefExchRate2 <= 0 THEN
                               ERROR(Text000);
                             CurrencyFactor := (CurrentExchRate * CurrentExchRate2) / (RefExchRate * RefExchRate2);
                           END;
                            }

    { 9   ;2   ;Field     ;
                Name=RefCurrencyCode2;
                CaptionML=[ENU=Relational Currency Code;
                           NOR=Tilh›rende valutakode;
                           SVE=Relationsvalutakod];
                ToolTipML=[ENU=Specifies how you want to set up the two currencies, one of the currencies can be LCY, for which you want to register exchange rates.;
                           NOR=Angir hvordan du vil definere de to valutaene. n av valutaene kan v‘re NOK, som du vil registrere valutakurser for.;
                           SVE=Anger hur du vill anv„nda de tv† valutorna. Den ena kan vara BVA, och f”r den b”r du registrera valutakurser.];
                ApplicationArea=#Suite;
                SourceExpr=ShowCurrencyCode(RefCurrencyCode2,RefCurrencyCode <> '');
                Editable=RefCurrencyCode2Editable }

    { 15  ;2   ;Field     ;
                Name=UseExchRate;
                CaptionML=[ENU=Use FA Add.-Curr Exch. Rate;
                           NOR=Bruk aktivatilleggsvalutakurs;
                           SVE=Anv„nd anl. kurs alt. valuta];
                ToolTipML=[ENU=Specifies the exchange rate of the additional reporting currency for a fixed asset, if you post in an additional reporting currency and use the Fixed Assets application area.;
                           NOR=Angir valutakursen for tilleggsrapporteringsvalutaen for et aktiva hvis du bokf›rer i en tilleggsrapporteringsvaluta og bruker modulen Aktiva.;
                           SVE=Om du bokf”r en ytterligare rapporteringsvaluta med modulen Anl„ggningstillg†ngar anger detta v„xlingskursen f”r den alternativa rapporteringsvalutan f”r en anl„ggningstillg†ng.];
                ApplicationArea=#FixedAssets;
                SourceExpr=UseExchRate;
                Visible=TRUE;
                Editable=UseExchRateEditable;
                MultiLine=Yes;
                OnValidate=BEGIN
                             IF UseExchRate THEN BEGIN
                               CurrencyCode3 := CurrencyCode4;
                             //CurrencyFactor := CurrExchRate.ExchangeRate(Date2,CurrencyCode3); //**4PS.o
                               CurrencyFactor := CurrExchRate.ExchangeRate(Type,Code,Date2,CurrencyCode3,UseSalesExchange); //**4PS.n
                               InitForm;
                             END ELSE BEGIN
                               CurrencyCode := '';
                               CurrentExchRate := 0;
                               RefExchRate := 0;
                               RefCurrencyCode := '';
                               CurrencyCode2 := '';
                               CurrentExchRate2 := 0;
                               RefExchRate2 := 0;
                               RefCurrencyCode2 := '';
                               CurrentExchRateEditable := FALSE;
                               RefExchRateEditable := FALSE;
                               CurrentExchRate2Editable := FALSE;
                               RefExchRate2Editable := FALSE;
                               //**4PS.sn
                               Type := 0;
                               Code := '';
                               UseSalesExchange := FALSE;
                               //**4PS.en
                             END;
                           END;
                            }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=The value must be greater than 0.;NOR=Verdien m† v‘re st›rre enn 0.;SVE=V„rdet m†ste vara st”rre „n noll.';
      CurrExchRate@1003 : Record 330;
      GLSetup@1004 : Record 98;
      CurrencyCode@1005 : Code[10];
      CurrencyCode2@1006 : Code[10];
      CurrentExchRate@1007 : Decimal;
      CurrentExchRate2@1008 : Decimal;
      RefExchRate@1009 : Decimal;
      RefExchRate2@1010 : Decimal;
      RefCurrencyCode@1011 : Code[10];
      RefCurrencyCode2@1012 : Code[10];
      CurrencyCode3@1013 : Code[10];
      CurrencyFactor@1014 : Decimal;
      Fix@1015 : Option;
      Fix2@1016 : Option;
      UseExchRate@1017 : Boolean;
      DynamicDataCaption@1018 : Text[50];
      CurrencyCode4@1019 : Code[10];
      Date2@1020 : Date;
      Date3@1021 : Date;
      FAUsed@1022 : Boolean;
      Text001@1002 : TextConst '@@@=%1 Caption for  "Fix Exchange Rate Amount" %2 a currencu code %3 Caption for "Relational Currency Code";ENU=The %1 field is not set up properly in the Currency Exchange Rates window. For %2 or the currency set up in the %3 field, the %1 field should be set to both.;NOR=Feltet %1 er ikke riktig definert i Valutakurser-vinduet. For oppsettet for %2 eller valuta i %3-feltet m† %1 settes til begge.;SVE=Inst„llningen i f„ltet %1 i f”nstret Valutav„xelkurser „r felaktig. F”r %2 eller valutan i f„ltet %3 ska f„ltet %1 vara inst„llt p† b†da.';
      CurrentExchRateEditable@19064599 : Boolean INDATASET;
      RefExchRateEditable@19015932 : Boolean INDATASET;
      CurrentExchRate2Editable@19077688 : Boolean INDATASET;
      RefExchRate2Editable@19058415 : Boolean INDATASET;
      UseExchRateEditable@19062907 : Boolean INDATASET;
      CurrencyCodeEditable@19008288 : Boolean INDATASET;
      RefCurrencyCodeEditable@19044351 : Boolean INDATASET;
      CurrencyCode2Editable@19059068 : Boolean INDATASET;
      RefCurrencyCode2Editable@19002206 : Boolean INDATASET;
      Type@1100529502 : ' ,Project,Estimate';
      Code@1100529501 : Code[20];
      UseSalesExchange@1100529500 : Boolean;
      Text11012000@1100529503 : TextConst 'ENU=There is no project exchange rate.';

    [External]
    PROCEDURE SetParameter@2(NewCurrencyCode@1000 : Code[10];NewFactor@1001 : Decimal;Date@1002 : Date);
    BEGIN
      CurrencyCode3 := NewCurrencyCode;
      CurrencyFactor := NewFactor;
      Date3 := Date;
      UseExchRate := FALSE;
      FAUsed := FALSE;
      //**4PS.sn
      Type := 0;
      Code := '';
      UseSalesExchange := FALSE;
      //**4PS.en
    END;

    [External]
    PROCEDURE GetParameter@1() : Decimal;
    BEGIN
      IF UseExchRate OR NOT FAUsed THEN
        EXIT(CurrencyFactor);

      EXIT(0);
    END;

    [External]
    PROCEDURE SetParameterFA@3(NewFactor@1000 : Decimal;CurrencyCode@1001 : Code[10];Date@1002 : Date);
    BEGIN
      IF NewFactor = 0 THEN BEGIN
        CurrencyFactor := 1;
        UseExchRate := FALSE;
      END ELSE BEGIN
        CurrencyFactor := NewFactor;
        UseExchRate := TRUE;
        CurrencyCode3 := CurrencyCode;
      END;
      UseExchRateEditable := TRUE;
      FAUsed := TRUE;
      CurrencyCode4 := CurrencyCode;
      Date2 := Date;
      Date3 := Date;
    END;

    [External]
    PROCEDURE SetCaption@4(DataCaption@1000 : Text[50]);
    BEGIN
      DynamicDataCaption := DataCaption;
    END;

    [External]
    PROCEDURE InitForm@5();
    VAR
      Project@1100529501 : Record 11072003;
      ProjectCurrencyExchangeRate@1100529500 : Record 11020628;
      HasGotProjectCurrencyExchangeRate@1100529502 : Boolean;
      FillSecondSetValues@1100529504 : Boolean;
    BEGIN
      WITH CurrExchRate DO BEGIN
        IF CurrencyCode3 = '' THEN BEGIN
          CurrencyCodeEditable := FALSE;
          CurrentExchRateEditable := FALSE;
          RefExchRateEditable := FALSE;
          RefCurrencyCodeEditable := FALSE;
          CurrencyCode2Editable := FALSE;
          CurrentExchRate2Editable := FALSE;
          RefExchRate2Editable := FALSE;
          RefCurrencyCode2Editable := FALSE;
          EXIT;
        END;

        //**4PS.sn
        IF (Type = Type::Project) AND (Code <> '') THEN
          IF Project.GET(Code) THEN BEGIN
            HasGotProjectCurrencyExchangeRate := ProjectCurrencyExchangeRate.FindCurrency(Code, Date3, CurrencyCode3, CurrExchRate);
            IF Project."Project Exch. Rate Mandatory" AND NOT HasGotProjectCurrencyExchangeRate THEN
              ERROR(Text11012000, CurrencyCode);
          END ELSE
            CLEAR(Project);

        IF NOT HasGotProjectCurrencyExchangeRate THEN BEGIN
        //**4PS.en
          SETRANGE("Currency Code",CurrencyCode3);
          SETRANGE("Starting Date",0D,Date3);
          FINDLAST;
        END; //**4PS.n

        CurrencyCode := "Currency Code";
        CurrentExchRate := "Exchange Rate Amount";
        RefExchRate := "Relational Exch. Rate Amount";
        RefCurrencyCode := "Relational Currency Code";
        Fix := "Fix Exchange Rate Amount";
        //**4PS.so
        //SETRANGE("Currency Code",RefCurrencyCode);
        //SETRANGE("Starting Date",0D,Date3);
        //IF FINDLAST THEN BEGIN
        //**4PS.eo

        //**4PS.sn
        IF (Type = Type::Project) AND (Code <> '') AND (Project."No." = Code) THEN BEGIN
          HasGotProjectCurrencyExchangeRate := ProjectCurrencyExchangeRate.FindCurrency(Code, Date3, RefCurrencyCode, CurrExchRate);
          FillSecondSetValues := HasGotProjectCurrencyExchangeRate;
          IF Project."Project Exch. Rate Mandatory" THEN
            HasGotProjectCurrencyExchangeRate := TRUE;
        END;

        IF NOT HasGotProjectCurrencyExchangeRate THEN BEGIN
          SETRANGE("Currency Code",RefCurrencyCode);
          SETRANGE("Starting Date",0D,Date3);
          FillSecondSetValues := FINDLAST;
        END;

        IF FillSecondSetValues THEN BEGIN
        //**4PS.en
          CurrencyCode2 := "Currency Code";
          CurrentExchRate2 := "Exchange Rate Amount";
          RefExchRate2 := "Relational Exch. Rate Amount";
          RefCurrencyCode2 := "Relational Currency Code";
          Fix2 := "Fix Exchange Rate Amount";
        END;

        CASE Fix OF
          "Fix Exchange Rate Amount"::Currency:
            BEGIN
              CurrentExchRateEditable := FALSE;
              RefExchRateEditable := TRUE;
              IF RefCurrencyCode = '' THEN
                RefExchRate := CurrentExchRate / CurrencyFactor
              ELSE
                RefExchRate := (CurrentExchRate * CurrentExchRate2) / (CurrencyFactor * RefExchRate2);
            END;
          "Fix Exchange Rate Amount"::"Relational Currency":
            BEGIN
              CurrentExchRateEditable := TRUE;
              RefExchRateEditable := FALSE;
              IF RefCurrencyCode = '' THEN
                CurrentExchRate := CurrencyFactor * RefExchRate
              ELSE
                CurrentExchRate := (RefExchRate * RefExchRate2 * CurrencyFactor) / CurrentExchRate2;
            END;
          "Fix Exchange Rate Amount"::Both:
            BEGIN
              CurrentExchRateEditable := FALSE;
              RefExchRateEditable := FALSE;
            END;
        END;

        IF RefCurrencyCode <> '' THEN BEGIN
          IF (Fix <> "Fix Exchange Rate Amount"::Both) AND (Fix2 <> "Fix Exchange Rate Amount"::Both) THEN
            ERROR(Text001,FIELDCAPTION("Fix Exchange Rate Amount"),CurrencyCode,FIELDCAPTION("Relational Currency Code"));
          CASE Fix2 OF
            "Fix Exchange Rate Amount"::Currency:
              BEGIN
                CurrentExchRate2Editable := FALSE;
                RefExchRate2Editable := TRUE;
                RefExchRate2 := (CurrentExchRate * CurrentExchRate2) / (CurrencyFactor * RefExchRate);
              END;
            "Fix Exchange Rate Amount"::"Relational Currency":
              BEGIN
                CurrentExchRate2Editable := TRUE;
                RefExchRate2Editable := FALSE;
                CurrentExchRate2 := (CurrencyFactor * RefExchRate * RefExchRate2) / CurrentExchRate;
              END;
            "Fix Exchange Rate Amount"::Both:
              BEGIN
                CurrentExchRate2Editable := FALSE;
                RefExchRate2Editable := FALSE;
              END;
          END;
        END ELSE BEGIN
          CurrencyCode2Editable := FALSE;
          CurrentExchRate2Editable := FALSE;
          RefExchRate2Editable := FALSE;
          RefCurrencyCode2Editable := FALSE;
          IF CurrencyCode = '' THEN BEGIN
            CurrencyCodeEditable := FALSE;
            CurrentExchRateEditable := FALSE;
            RefExchRateEditable := FALSE;
            RefCurrencyCodeEditable := FALSE;
          END;
        END;
      END;
    END;

    LOCAL PROCEDURE ShowCurrencyCode@7(ShowCurrency@1000 : Code[10];DoShow@1001 : Boolean) : Code[10];
    BEGIN
      IF NOT DoShow THEN
        EXIT('');
      IF ShowCurrency = '' THEN
        EXIT(GLSetup."LCY Code");

      EXIT(ShowCurrency);
    END;

    [External]
    PROCEDURE SetAdditionalParameters@1100529501(NewType@1100529502 : ' ,Project,Estimate';NewCode@1100529501 : Code[20];NewUseSalesExchange@1100529500 : Boolean);
    BEGIN
      //**4PS
      Type := NewType;
      Code := NewCode;
      UseSalesExchange := NewUseSalesExchange;
    END;

    BEGIN
    END.
  }
}

