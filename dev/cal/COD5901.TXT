OBJECT Codeunit 5901 ServLoanerManagement
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    Permissions=TableData 5914=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=The %1 field is empty.;NOR=Feltet %1 er tomt.;SVE=F„ltet %1 „r tomt.';
      Text001@1001 : TextConst 'ENU=You cannot receive loaner %1 because it has not been lent.;NOR=Du kan ikke motta utl†nsobjekt %1 fordi dette ikke er utl†nt.;SVE=Du kan inte inleverera l†neartikel %1 d„rf”r att den inte har l†nats ut.';
      Text002@1002 : TextConst 'ENU=Loaner no. %1 has not been lent connection with %2 no. %3.;NOR=Utl†nsobjektnr. %1 er ikke knyttet til %2 nr. %3.;SVE=l†neartikel nr %1 har inte l†nats ut i samband med %2 nr. %3.';
      Text003@1003 : TextConst 'ENU=There is no %1 to receive.;NOR=Det finnes ingen %1 † motta.;SVE=Det finns ingen %1 att inleverera.';
      Text004@1004 : TextConst 'ENU="There is no loaner to receive on the service shipment item line document no.=%1,line no.=%2.";NOR="Det finnes ikke noe utl†nsobjekt † motta p† varelinjen i servicef›lgeseddelen, bilagsnr. = %1, linjenr. = %2.";SVE="Det finns ingen l†neartikel att ta emot p† artikelraden f”r serviceutleverans, dokumentnr=%1,radnr=%2."';
      Text005@1005 : TextConst 'ENU=Do you want to receive loaner %1?;NOR=Vil du motta utl†nsobjekt %1?;SVE=Vill du erh†lla l†neartikel %1?';
      Text006@1006 : TextConst 'ENU=There is no service shipment header within the filter.\Filters: order no.: %1\Do you want to receive the loaner anyway?;NOR=Det finnes ikke noe servicef›lgeseddelhode i filteret.\Filtre: ordrenr.: %1\Vil du motta utl†nsobjektet likevel?;SVE=Det finns inget serviceutleveranshuvud inom filtret.\Filter: ordernr: %1\Vill du ta emot l†neartikeln „nd†?';

    [External]
    PROCEDURE LendLoaner@10(ServItemLine@1000 : Record 5901);
    VAR
      Loaner@1001 : Record 5913;
      ServHeader@1003 : Record 5900;
      LoanerEntry@1002 : Record 5914;
      ServLogMgt@1004 : Codeunit 5906;
    BEGIN
      IF ServItemLine."Loaner No." <> '' THEN BEGIN
        Loaner.GET(ServItemLine."Loaner No.");
        Loaner.CALCFIELDS(Lent);
        Loaner.TESTFIELD(Lent,FALSE);
        Loaner.TESTFIELD(Blocked,FALSE);

        LoanerEntry.LOCKTABLE;
        LoanerEntry.INIT;
        LoanerEntry."Entry No." := LoanerEntry.GetNextEntryNo;
        LoanerEntry."Loaner No." := ServItemLine."Loaner No.";
        LoanerEntry."Document Type" := ServItemLine."Document Type" + 1;
        LoanerEntry."Document No." := ServItemLine."Document No.";
        LoanerEntry."Service Item Line No." := ServItemLine."Line No.";
        LoanerEntry."Service Item No." := ServItemLine."Service Item No.";
        LoanerEntry."Service Item Group Code" := ServItemLine."Service Item Group Code";
        IF ServHeader.GET(ServItemLine."Document Type",ServItemLine."Document No.") THEN
          LoanerEntry."Customer No." := ServHeader."Customer No.";
        LoanerEntry."Date Lent" := WORKDATE;
        LoanerEntry."Time Lent" := TIME;
        LoanerEntry."Date Received" := 0D;
        LoanerEntry."Time Received" := 0T;
        LoanerEntry.Lent := TRUE;
        LoanerEntry.INSERT;
        CLEAR(ServLogMgt);
        ServLogMgt.LoanerLent(LoanerEntry);
      END ELSE
        ERROR(Text000,ServItemLine.FIELDCAPTION("Loaner No."));
    END;

    [External]
    PROCEDURE ReceiveLoaner@11(VAR ServItemLine@1000 : Record 5901);
    VAR
      Loaner@1001 : Record 5913;
      LoanerEntry@1002 : Record 5914;
      ServLogMgt@1003 : Codeunit 5906;
      ConfirmManagement@1004 : Codeunit 27;
    BEGIN
      IF ServItemLine."Loaner No." <> '' THEN BEGIN
        IF NOT ConfirmManagement.ConfirmProcess(STRSUBSTNO(Text005,ServItemLine."Loaner No."),TRUE) THEN
          EXIT;
        LoanerEntry.RESET;
        LoanerEntry.SETCURRENTKEY("Document Type","Document No.","Loaner No.",Lent);
        LoanerEntry.SETRANGE("Document Type",ServItemLine."Document Type" + 1);
        LoanerEntry.SETRANGE("Document No.",ServItemLine."Document No.");
        LoanerEntry.SETRANGE("Loaner No.",ServItemLine."Loaner No.");
        LoanerEntry.SETRANGE(Lent,TRUE);
        IF LoanerEntry.FINDFIRST THEN BEGIN
          LoanerEntry."Date Received" := WORKDATE;
          LoanerEntry."Time Received" := TIME;
          LoanerEntry.Lent := FALSE;
          LoanerEntry.MODIFY;
          ServItemLine."Loaner No." := '';
          ServItemLine.MODIFY;
          CLEAR(ServLogMgt);
          ServLogMgt.LoanerReceived(LoanerEntry);
          ClearLoanerField(ServItemLine."Document No.",ServItemLine."Line No.",LoanerEntry."Loaner No.");
        END ELSE
          ERROR(
            Text002,ServItemLine."Loaner No.",
            FORMAT(ServItemLine."Document Type"),ServItemLine."Document No.");
      END ELSE
        ERROR(Text003,Loaner.TABLECAPTION);
    END;

    [External]
    PROCEDURE ReceiveLoanerShipment@2(ServShipmentItemLine@1000 : Record 5989);
    VAR
      LoanerEntry@1002 : Record 5914;
      ServShptHeader@1004 : Record 5990;
      ServLogMgt@1003 : Codeunit 5906;
      ConfirmManagement@1001 : Codeunit 27;
    BEGIN
      WITH ServShipmentItemLine DO
        IF "Loaner No." = '' THEN
          ERROR(Text004,"No.","Line No.");

      IF NOT ConfirmManagement.ConfirmProcess(STRSUBSTNO(Text005,ServShipmentItemLine."Loaner No."),TRUE) THEN
        EXIT;
      ServShptHeader.GET(ServShipmentItemLine."No.");
      LoanerEntry.RESET;
      LoanerEntry.SETCURRENTKEY("Document Type","Document No.","Loaner No.",Lent);
      LoanerEntry.SETRANGE("Document Type",LoanerEntry."Document Type"::Order);
      LoanerEntry.SETRANGE("Document No.",ServShptHeader."Order No.");
      LoanerEntry.SETRANGE("Loaner No.",ServShipmentItemLine."Loaner No.");
      LoanerEntry.SETRANGE(Lent,TRUE);
      IF LoanerEntry.FINDFIRST THEN BEGIN
        LoanerEntry."Date Received" := WORKDATE;
        LoanerEntry."Time Received" := TIME;
        LoanerEntry.Lent := FALSE;
        LoanerEntry.MODIFY;
        ServShipmentItemLine."Loaner No." := '';
        ServShipmentItemLine.MODIFY;
        CLEAR(ServLogMgt);
        ServLogMgt.LoanerReceived(LoanerEntry);
        ClearLoanerField(ServShptHeader."Order No.",ServShipmentItemLine."Line No.",LoanerEntry."Loaner No.");
      END ELSE
        ERROR(
          Text002,ServShipmentItemLine."Loaner No.",
          ServShipmentItemLine.FIELDCAPTION("No."),ServShipmentItemLine."No.");
    END;

    LOCAL PROCEDURE ClearLoanerField@4(OrderNo@1000 : Code[20];LineNo@1001 : Integer;LoanerNo@1006 : Code[20]);
    VAR
      ServShptHeader@1002 : Record 5990;
      ServShptItemLine@1003 : Record 5989;
      ServiceHeader@1004 : Record 5900;
      ServItemLine@1005 : Record 5901;
    BEGIN
      IF ServItemLine.GET(ServiceHeader."Document Type"::Order,OrderNo,LineNo) THEN
        IF ServItemLine."Loaner No." = LoanerNo THEN BEGIN
          ServItemLine."Loaner No." := '';
          ServItemLine.MODIFY;
        END;

      ServShptHeader.RESET;
      ServShptHeader.SETCURRENTKEY("Order No.");
      ServShptHeader.SETRANGE("Order No.",OrderNo);
      IF ServShptHeader.FIND('-') THEN
        REPEAT
          ServShptItemLine.RESET;
          IF ServShptItemLine.GET(ServShptHeader."No.",LineNo) THEN
            IF ServShptItemLine."Loaner No." = LoanerNo THEN BEGIN
              ServShptItemLine."Loaner No." := '';
              ServShptItemLine.MODIFY;
            END;
        UNTIL ServShptHeader.NEXT = 0;
    END;

    [External]
    PROCEDURE Receive@3(VAR Loaner@1000 : Record 5913);
    VAR
      ServItemLine@1002 : Record 5901;
      LoanerEntry@1001 : Record 5914;
      ServShptItemLine@1004 : Record 5989;
      ServShptHeader@1003 : Record 5990;
      ConfirmManagement@1005 : Codeunit 27;
    BEGIN
      WITH Loaner DO
        IF Lent THEN BEGIN
          CLEAR(LoanerEntry);
          LoanerEntry.SETCURRENTKEY("Document Type","Document No.","Loaner No.",Lent);
          LoanerEntry.SETRANGE("Document Type","Document Type");
          LoanerEntry.SETRANGE("Document No.","Document No.");
          LoanerEntry.SETRANGE("Loaner No.","No.");
          LoanerEntry.SETRANGE(Lent,TRUE);

          IF LoanerEntry.FINDFIRST THEN
            IF ServItemLine.GET(LoanerEntry."Document Type" - 1,LoanerEntry."Document No.",LoanerEntry."Service Item Line No.") THEN
              ReceiveLoaner(ServItemLine)
            ELSE BEGIN
              ServShptHeader.RESET;
              ServShptHeader.SETCURRENTKEY("Order No.");
              ServShptHeader.SETRANGE("Order No.",LoanerEntry."Document No.");
              IF ServShptHeader.FINDLAST THEN BEGIN
                ServShptItemLine.GET(ServShptHeader."No.",LoanerEntry."Service Item Line No.");
                ReceiveLoanerShipment(ServShptItemLine);
              END ELSE
                IF ConfirmManagement.ConfirmProcess(STRSUBSTNO(Text006,LoanerEntry."Document No."),TRUE) THEN BEGIN
                  // receive loaner anyway
                  LoanerEntry."Date Received" := WORKDATE;
                  LoanerEntry."Time Received" := TIME;
                  LoanerEntry.Lent := FALSE;
                  LoanerEntry.MODIFY;
                END;
            END;
        END ELSE
          ERROR(Text001,"No.");
    END;

    BEGIN
    END.
  }
}

