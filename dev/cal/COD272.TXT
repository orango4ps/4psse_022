OBJECT Codeunit 272 Res. Jnl.-Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04;
  }
  PROPERTIES
  {
    TableNo=207;
    OnRun=BEGIN
            ResJnlLine.COPY(Rec);
            Code;
            COPY(ResJnlLine);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot be filtered when posting recurring journals;NOR=kan ikke filtreres n†r du bokf›rer gjentakelseskladder;SVE=kan inte filtreras n„r †terkommande journaler bokf”rs';
      Text001@1001 : TextConst 'ENU=Do you want to post the journal lines and print the posting report?;NOR=Vil du bokf›re kladdelinjene og skrive ut bokf›ringsrapporten?;SVE=Vill du bokf”ra journalraderna och skriva ut bokf”ringsrapporten?';
      Text002@1002 : TextConst 'ENU=There is nothing to post.;NOR=Det finnes ingenting † bokf›re.;SVE=Det finns inget att bokf”ra.';
      Text003@1003 : TextConst 'ENU=The journal lines were successfully posted.;NOR=Kladdelinjene ble bokf›rt uten problemer.;SVE=Journalraderna „r bokf”rda.';
      Text004@1004 : TextConst 'ENU="The journal lines were successfully posted. ";NOR="Kladdelinjene ble bokf›rt p† riktig m†te. ";SVE="Det gick bra att bokf”ra raderna. "';
      Text005@1005 : TextConst 'ENU=You are now in the %1 journal.;NOR=Du er n† i kladd %1.;SVE=Du „r nu i journalen %1.';
      ResJnlTemplate@1006 : Record 206;
      ResJnlLine@1007 : Record 207;
      ResReg@1008 : Record 240;
      TempJnlBatchName@1010 : Code[10];

    LOCAL PROCEDURE Code@1();
    VAR
      HideDialog@1000 : Boolean;
    BEGIN
      WITH ResJnlLine DO BEGIN
        ResJnlTemplate.GET("Journal Template Name");
        ResJnlTemplate.TESTFIELD("Posting Report ID");
        IF ResJnlTemplate.Recurring AND (GETFILTER("Posting Date") <> '') THEN
          FIELDERROR("Posting Date",Text000);

        HideDialog := FALSE;
        OnBeforePostJournalBatch(ResJnlLine,HideDialog);
        IF NOT HideDialog THEN
          IF NOT CONFIRM(Text001) THEN
            EXIT;

        TempJnlBatchName := "Journal Batch Name";

        CODEUNIT.RUN(CODEUNIT::"Res. Jnl.-Post Batch",ResJnlLine);

        OnAfterPostJournalBatch(ResJnlLine);

        IF ResReg.GET("Line No.") THEN BEGIN
          ResReg.SETRECFILTER;
          REPORT.RUN(ResJnlTemplate."Posting Report ID",FALSE,FALSE,ResReg);
        END;

        IF "Line No." = 0 THEN
          MESSAGE(Text002)
        ELSE
          IF TempJnlBatchName = "Journal Batch Name" THEN
            MESSAGE(Text003)
          ELSE
            MESSAGE(
              Text004 +
              Text005,
              "Journal Batch Name");

        IF NOT FIND('=><') OR (TempJnlBatchName <> "Journal Batch Name") THEN BEGIN
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          FILTERGROUP(0);
          "Line No." := 1;
        END;
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPostJournalBatch@3(VAR ResJournalLine@1000 : Record 207);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostJournalBatch@2(VAR ResJournalLine@1000 : Record 207;VAR HideDialog@1001 : Boolean);
    BEGIN
    END;

    BEGIN
    END.
  }
}

