OBJECT Report 11071707 Copy Settl Q Budget to Budget
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Verrechnungsmenge Budget zu Budget kopieren;
               ENU=Copy Settlement Quantity Budget to Budget;
               NLD=Verrekenbare hoeveelheid begroting naar werkbegroting kopi‰ren];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF NOT gOnDeleteSettlQuanRun THEN BEGIN
                    Window.OPEN(Text000 + Text001 + Text007 + Text002 + Text003);
                  END;

                  Counter := 0;
                  ProjSetupRec.GET;
                END;

    OnPostReport=BEGIN
                   IF CurrReport.USEREQUESTPAGE THEN BEGIN
                     IF ProjSetupRec."Batch update budg from option" THEN BEGIN
                       MESSAGE(Text006);
                     END ELSE BEGIN
                       IF Counter = 0 THEN
                         MESSAGE(Text004)
                       ELSE
                         MESSAGE(Text005,Counter);
                     END;
                   END;
                 END;

  }
  DATASET
  {
    { 8019;    ;DataItem;                    ;
               DataItemTable=Table11072003;
               DataItemTableView=SORTING(No.);
               OnAfterGetRecord=BEGIN
                                  TmpSettlementQuantity.RESET;
                                  TmpSettlementQuantity.DELETEALL;

                                  GetProjElement("No.");
                                  FillCommonCostObjects("No.");
                                END;

               ReqFilterFields=No. }

    { 1100525001;1;DataItem;                 ;
               DataItemTable=Table11012500;
               DataItemTableView=SORTING(Project No.,Plot No.);
               OnPreDataItem=BEGIN
                               FirstPlotNo := TRUE;
                             END;

               OnAfterGetRecord=BEGIN
                                  IF Job."Budget Level for Settl. Q." <> Job."Budget Level for Settl. Q."::Detailed THEN BEGIN
                                    IF FirstPlotNo THEN BEGIN
                                      FirstPlotNo := FALSE;
                                    END ELSE
                                      CurrReport.BREAK;
                                  END;
                                END;

               OnPostDataItem=VAR
                                lvBudgetAdjustment@1100525000 : Record 11012003;
                                lvBudgetLine@1100525001 : Record 11012001;
                              BEGIN
                                //Freeze Adjustment
                                IF (UseAdjustmentNo <> '') AND FreezeAdjustment THEN BEGIN
                                  lvBudgetAdjustment.SETRANGE("Project No.",Job."No.");
                                  lvBudgetAdjustment.SETRANGE("No.", UseAdjustmentNo);
                                  IF lvBudgetAdjustment.FINDFIRST THEN BEGIN
                                    lvBudgetLine.SETRANGE("Project No.",Job."No.");
                                    lvBudgetLine.SETRANGE(Adjustment, UseAdjustmentNo);
                                    IF NOT lvBudgetLine.ISEMPTY THEN BEGIN
                                      lvBudgetAdjustment."Correction Fixed" := TRUE;
                                      lvBudgetAdjustment."Budget Correction Fixed" := TRUE;
                                      lvBudgetAdjustment.MODIFY(TRUE);
                                    END ELSE BEGIN
                                      lvBudgetAdjustment.DELETE(TRUE);
                                    END;
                                  END;
                                END;
                              END;

               DataItemLink=Project No.=FIELD(No.) }

    { 1100528200;2;DataItem;                 ;
               DataItemTable=Table11072253;
               DataItemTableView=SORTING(Project No.,House Model,Type,Main Group,Group,Sub Group,Code,Unit,Line No.)
                                 WHERE(Type=FILTER(Settlement));
               OnPreDataItem=BEGIN
                               TmpSettlementQuantityBudget.RESET;
                               TmpSettlementQuantityBudget.DELETEALL;
                               NextLineNoTmp := 1;

                               PrevSettlQuantCode := '';
                               PrevSettlQuantCodeClearOtherLevels := '';
                               PrevHouseModelCode := '';
                               PrevHouseModelCodeClearOtherLevels := '';
                               VersionDate := 0D;
                             END;

               OnAfterGetRecord=BEGIN
                                  IF NOT SettlementQuantity.GET("Project No.", "House Model", Code) THEN
                                    CurrReport.SKIP;

                                  IF NOT gOnDeleteSettlQuanRun THEN BEGIN
                                    Window.UPDATE(1, "Project No.");
                                    Window.UPDATE(2, "House Model");
                                    Window.UPDATE(3, Code);
                                    Window.UPDATE(4, 0);
                                  END;

                                  Counter := Counter + 1;
                                  IF (Job."Budget Level for Settl. Q." = Job."Budget Level for Settl. Q."::Detailed) THEN
                                    VersionDate := 0D;

                                  IF (gOnDeleteSettlQuanRun OR gOnResetSettlQuanStatusRun) AND
                                     ("Project No." = gDelProjectNo) AND (Code = gDelCode)AND ("House Model" = gDelHouseModel)
                                  THEN
                                    SkipWriteTmpOptBudget := TRUE
                                  ELSE
                                    SkipWriteTmpOptBudget := FALSE;
                                END;

               OnPostDataItem=BEGIN
                                ProcessBudget(PrevHouseModelCode, PrevSettlQuantCode);
                              END;

               ReqFilterFields=Code;
               DataItemLinkReference=Job;
               DataItemLink=Project No.=FIELD(No.) }

    { 1100528201;3;DataItem;IntegerNotDetailed;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number)
                                 WHERE(Number=CONST(1));
               OnPreDataItem=BEGIN
                               //IF Job."Budget Level for Settl. Q." = Job."Budget Level for Settl. Q."::Detailed THEN
                               //  CurrReport.BREAK;
                             END;

               OnAfterGetRecord=BEGIN
                                  IF NOT gOnDeleteSettlQuanRun THEN BEGIN
                                    Window.UPDATE(3, PrevSettlQuantCode);
                                    Window.UPDATE(2, PrevHouseModelCode);
                                  END;

                                  IF (PrevSettlQuantCode <> "Plot Recording Cross List".Code) OR
                                     (PrevHouseModelCode <> "Plot Recording Cross List"."House Model") THEN
                                    ProcessBudget(PrevHouseModelCode, PrevSettlQuantCode);
                                  PrevSettlQuantCode := "Plot Recording Cross List".Code;
                                  PrevHouseModelCode := "Plot Recording Cross List"."House Model";
                                  VersionDate := 0D;

                                  IF NOT gOnDeleteSettlQuanRun THEN BEGIN
                                    Window.UPDATE(3, "Plot Recording Cross List".Code);
                                    Window.UPDATE(2, "Plot Recording Cross List"."House Model");
                                  END;
                                END;
                                 }

    { 8498;3   ;DataItem;BudgetLineClearOtherLevels;
               DataItemTable=Table11012001;
               DataItemTableView=SORTING(Project No.,Adjustment,Extension Contract,Option,Line No.)
                                 WHERE(Adjustment=FILTER(''),
                                       Extension Contract=FILTER(''),
                                       Settl. Q. Line Type=FILTER(SQ Budget|SQ Surcharge));
               OnPreDataItem=BEGIN
                               IF ("Plot Recording Cross List".Code = PrevSettlQuantCodeClearOtherLevels) AND ("Plot Recording Cross List"."House Model" = PrevHouseModelCodeClearOtherLevels) THEN
                                 CurrReport.BREAK;

                               PrevSettlQuantCodeClearOtherLevels := "Plot Recording Cross List".Code;
                               PrevHouseModelCodeClearOtherLevels := "Plot Recording Cross List"."House Model";

                               SETFILTER("Budget Level for Settl. Q.", '<>%1', Job."Budget Level for Settl. Q.");
                             END;

               OnAfterGetRecord=BEGIN
                                  DELETE;
                                END;

               DataItemLink=Project No.=FIELD(Project No.) }

    { 1100525000;3;DataItem;IntegerPlotIfDetailed;
               DataItemTable=Table2000000026;
               OnPreDataItem=BEGIN
                               IF Job."Budget Level for Settl. Q." <> Job."Budget Level for Settl. Q."::Detailed THEN BEGIN
                                 IntegerPlotIfDetailed.SETRANGE(Number, 1);
                                 CLEAR(PlotRec);
                                 PlotRec."Plot No." := '';
                               END ELSE BEGIN
                                 IntegerPlotIfDetailed.SETRANGE(Number, 1);
                                 PlotRec.SETRANGE("Project No.", "Plot Recording Cross List"."Project No.");
                                 PlotRec.SETRANGE("Plot No.",Plot."Plot No.");
                                 PlotRec.FINDSET;
                                 FirstPlot := TRUE;
                                 IF NOT gOnDeleteSettlQuanRun THEN BEGIN
                                   Window.UPDATE(3, "Plot Recording Cross List".Code);
                                   Window.UPDATE(2, "Plot Recording Cross List"."House Model");
                                 END;
                               END;
                             END;

               OnAfterGetRecord=BEGIN
                                  IF Job."Budget Level for Settl. Q." = Job."Budget Level for Settl. Q."::Detailed THEN BEGIN
                                    IF FirstPlot THEN BEGIN
                                      FirstPlot := FALSE;
                                      IF NOT PlotRec.FINDSET THEN
                                        CurrReport.BREAK;
                                    END ELSE BEGIN
                                      IF PlotRec.NEXT = 0 THEN
                                        CurrReport.BREAK;
                                    END;
                                  END;
                                END;
                                 }

    { 4668;4   ;DataItem;                    ;
               DataItemTable=Table11072244;
               DataItemTableView=SORTING(Project No.,House Model,Settlement Quantity Code,Line No.);
               OnPreDataItem=BEGIN
                               IF SkipWriteTmpOptBudget THEN
                                 CurrReport.BREAK;

                               SQLineType := BudgetRec."Settl. Q. Line Type"::"SQ Budget";
                               IF (SettlementQuantity."Version Date" <> 0D) AND ((SettlementQuantity."Version Date" < VersionDate) OR (VersionDate = 0D)) THEN
                                 VersionDate := SettlementQuantity."Version Date";
                             END;

               OnAfterGetRecord=VAR
                                  PlotSQRecordingLine@1100528200 : Record 11072252;
                                  lvPlot@1100528201 : Record 11012500;
                                  TotalQuantity@1100528202 : Decimal;
                                BEGIN
                                  IF NOT gOnDeleteSettlQuanRun THEN
                                    Window.UPDATE(4, "Line No.");

                                  IF (Amount = 0) AND (Quantity = 0) AND ("Time Quantity" = 0) AND ("Cost Object" = '') THEN
                                    CurrReport.SKIP;

                                  TotalQuantity := 0;

                                  lvPlot.SETRANGE("Project No.", "Plot Recording Cross List"."Project No.");
                                  IF Job."Budget Level for Settl. Q." = Job."Budget Level for Settl. Q."::Detailed THEN
                                    lvPlot.SETRANGE("Plot No.",PlotRec."Plot No.")
                                  ELSE
                                    lvPlot.SETRANGE("Plot No.");

                                  IF lvPlot.FINDSET THEN
                                    REPEAT
                                      PlotSQRecordingLine.SETCURRENTKEY("Project No.", "Plot No.",Type);
                                      PlotSQRecordingLine.SETRANGE("Project No.", "Plot Recording Cross List"."Project No.");
                                      PlotSQRecordingLine.SETRANGE("Plot No.", lvPlot."Plot No.");
                                      PlotSQRecordingLine.SETRANGE(Unit, "Plot Recording Cross List".Unit);
                                      PlotSQRecordingLine.SETRANGE("Line No.", "Plot Recording Cross List"."Line No.");
                                      PlotSQRecordingLine.SETRANGE("House Model", "Plot Recording Cross List"."House Model");
                                      PlotSQRecordingLine.SETFILTER(Phase, '%1|%2', PlotSQRecordingLine.Phase::"Warm Recording", PlotSQRecordingLine.Phase::Production);
                                      IF PlotSQRecordingLine.FINDLAST THEN BEGIN
                                        PlotSQRecordingLine.CALCFIELDS("Cold Recording Quantity","Production Recording Quantity");
                                        TotalQuantity := TotalQuantity + PlotSQRecordingLine."Production Recording Quantity";
                                      END ELSE BEGIN;
                                        IF lvPlot."MMR Phase" = lvPlot."MMR Phase"::PreRecording THEN BEGIN
                                          PlotSQRecordingLine.SETRANGE(Phase, PlotSQRecordingLine.Phase::"Cold Recording");
                                          IF PlotSQRecordingLine.FINDLAST THEN BEGIN
                                            PlotSQRecordingLine.CALCFIELDS("Cold Recording Quantity","Production Recording Quantity");
                                            TotalQuantity := TotalQuantity + PlotSQRecordingLine."Cold Recording Quantity"
                                          END;
                                        END;
                                      END;
                                    UNTIL lvPlot.NEXT = 0;

                                  IF TotalQuantity = 0 THEN
                                    CurrReport.SKIP;

                                  //compress to higher level
                                  SettlementQuantityBudget := "Settlement Quantity Budget";
                                  CASE Job."Budget Level for Settl. Q." OF
                                    Job."Budget Level for Settl. Q."::"SQ-Cost Type",
                                    Job."Budget Level for Settl. Q."::"SQ-Element-Cost Type":
                                      BEGIN
                                        SettlementQuantityBudget.Quantity := 1;
                                        SettlementQuantityBudget."Time Quantity" := 1;
                                      END;
                                    Job."Budget Level for Settl. Q."::Detailed,
                                    Job."Budget Level for Settl. Q."::"SQ-Cost Object",
                                    Job."Budget Level for Settl. Q."::"SQ-Element-Cost Object":
                                      BEGIN
                                        SettlementQuantityBudget.Quantity := Quantity * "Time Quantity" * TotalQuantity;
                                        SettlementQuantityBudget."Time Quantity" := 1;
                                      END;
                                  END;

                                  SettlementQuantityBudget.Amount := Amount * TotalQuantity;
                                  SettlementQuantityBudget.Hours := Hours * TotalQuantity;

                                  UpdateTmpSQBudget(SettlementQuantityBudget);
                                END;

               DataItemLinkReference=Plot Recording Cross List;
               DataItemLink=Project No.=FIELD(Project No.),
                            Settlement Quantity Code=FIELD(Code),
                            House Model=FIELD(House Model) }

    { 1100528202;4;DataItem;                 ;
               DataItemTable=Table11072247;
               DataItemTableView=SORTING(Project No.,House Model,Settlement Quantity Code,Line No.);
               OnPreDataItem=BEGIN
                               IF SkipWriteTmpOptBudget THEN
                                 CurrReport.BREAK;

                               SQLineType := BudgetRec."Settl. Q. Line Type"::"SQ Surcharge";
                             END;

               OnAfterGetRecord=VAR
                                  Job@1100528601 : Record 11072003;
                                  BudgetRateManagement@1100528600 : Codeunit 11012358;
                                  BudgetRateFoundAt@1100528602 : Text[250];
                                  PlotSQRecordingLine@1100528200 : Record 11072252;
                                  TotalQuantity@1100528201 : Decimal;
                                  lvPlot@1100528202 : Record 11012500;
                                BEGIN
                                  IF NOT gOnDeleteSettlQuanRun THEN
                                    Window.UPDATE(4, "Line No.");

                                  TotalQuantity := 0;

                                  lvPlot.SETRANGE("Project No.", "Plot Recording Cross List"."Project No.");
                                  IF Job."Budget Level for Settl. Q." = Job."Budget Level for Settl. Q."::Detailed THEN
                                    lvPlot.SETRANGE("Plot No.",PlotRec."Plot No.")
                                  ELSE
                                    lvPlot.SETRANGE("Plot No.");

                                  IF lvPlot.FINDSET THEN
                                    REPEAT
                                      PlotSQRecordingLine.SETCURRENTKEY("Project No.", "Plot No.",Type);
                                      PlotSQRecordingLine.SETRANGE("Project No.", "Plot Recording Cross List"."Project No.");
                                      PlotSQRecordingLine.SETRANGE("Plot No.", lvPlot."Plot No.");
                                      PlotSQRecordingLine.SETRANGE(Unit, "Plot Recording Cross List".Unit);
                                      PlotSQRecordingLine.SETRANGE("Line No.", "Plot Recording Cross List"."Line No.");
                                      PlotSQRecordingLine.SETRANGE("House Model", "Plot Recording Cross List"."House Model");
                                      PlotSQRecordingLine.SETFILTER(Phase, '%1|%2', PlotSQRecordingLine.Phase::"Warm Recording", PlotSQRecordingLine.Phase::Production);
                                      IF PlotSQRecordingLine.FINDLAST THEN BEGIN
                                        PlotSQRecordingLine.CALCFIELDS("Cold Recording Quantity","Production Recording Quantity");
                                        TotalQuantity := TotalQuantity + PlotSQRecordingLine."Production Recording Quantity";
                                      END ELSE BEGIN;
                                        IF lvPlot."MMR Phase" = lvPlot."MMR Phase"::PreRecording THEN BEGIN
                                          PlotSQRecordingLine.SETRANGE(Phase, PlotSQRecordingLine.Phase::"Cold Recording");
                                          IF PlotSQRecordingLine.FINDLAST THEN BEGIN
                                            PlotSQRecordingLine.CALCFIELDS("Cold Recording Quantity","Production Recording Quantity");
                                            TotalQuantity := TotalQuantity + PlotSQRecordingLine."Cold Recording Quantity"
                                          END;
                                        END;
                                      END;
                                    UNTIL lvPlot.NEXT = 0;

                                  IF TotalQuantity = 0 THEN
                                    CurrReport.SKIP;

                                  //naar hoger nivo
                                  SettlementQuantityBudget.INIT;
                                  SettlementQuantityBudget."Project No." := "Project No.";
                                  SettlementQuantityBudget."Settlement Quantity Code" := "Settlement Quantity Code" ;
                                  SettlementQuantityBudget."Line No." := -"Line No.";  //* Use negative Line Nos. in 'TmpOptBudgRec' for Surcharges to prevent duplicates!
                                  SettlementQuantityBudget."Cost Type" := "Cost Type";
                                  SettlementQuantityBudget."Cost Object" := "Cost Object";

                                  SettlementQuantityBudget.Description := Description;
                                  SettlementQuantityBudget.Element := Element;
                                  SettlementQuantityBudget.Chapter := Chapter;
                                  SettlementQuantityBudget.Paragraph := Paragraph;
                                  SettlementQuantityBudget."Time Quantity" := 1;
                                  SettlementQuantityBudget.Quantity := 1;

                                  CASE Job."Budget Level for Settl. Q." OF
                                    Job."Budget Level for Settl. Q."::Detailed,
                                    Job."Budget Level for Settl. Q."::"SQ-Cost Object",
                                    Job."Budget Level for Settl. Q."::"SQ-Element-Cost Object":
                                      BEGIN
                                        SettlementQuantityBudget.Quantity := TotalQuantity;
                                      END;
                                  END;

                                  SettlementQuantityBudget.Amount := "Surcharge Amount" * TotalQuantity;
                                  IF SettlementQuantityBudget."Cost Type" = SettlementQuantityBudget."Cost Type"::Labor THEN BEGIN
                                    SettlementQuantityBudget."Rate Code" := "Rate Code";
                                    IF NOT Job.GET("Project No.") THEN
                                      Job.INIT;
                                    SettlementQuantityBudget.Rate := BudgetRateManagement.GetBudgetRate(
                                      0, "Project No.", '', '', Job."Global Dimension 1 Code", "Rate Code", TODAY, BudgetRateFoundAt);
                                    IF SettlementQuantityBudget.Rate <> 0 THEN BEGIN
                                      SettlementQuantityBudget.Norm := ROUND((SettlementQuantityBudget.Amount / SettlementQuantityBudget.Rate), 0.00001);
                                      SettlementQuantityBudget.Hours := ROUND(SettlementQuantityBudget.Norm);
                                    END ELSE BEGIN
                                      SettlementQuantityBudget.Norm := 0;
                                      SettlementQuantityBudget.Hours := 0;
                                      SettlementQuantityBudget.Rate := SettlementQuantityBudget.Amount;
                                    END;
                                  END ELSE BEGIN
                                    SettlementQuantityBudget.Price := SettlementQuantityBudget.Amount;
                                  END;

                                  IF SettlementQuantityBudget.Amount <> 0 THEN BEGIN
                                    TESTFIELD("Cost Object"); //Cost Object obligatory
                                    DimMgtCU.GetDimValueRec(2, "Cost Object", DimValRec, TRUE, "Project No.");
                                    SettlementQuantityBudget."Cost Component" := DimValRec."Cost Component";
                                  END;

                                  IF SettlementQuantityBudget.Amount <> 0 THEN
                                    UpdateTmpSQBudget(SettlementQuantityBudget);
                                END;

               DataItemLinkReference=Plot Recording Cross List;
               DataItemLink=Project No.=FIELD(Project No.),
                            Settlement Quantity Code=FIELD(Code),
                            House Model=FIELD(House Model) }

    { 6719;4   ;DataItem;IntegerDetailed     ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number)
                                 WHERE(Number=CONST(1));
               OnPreDataItem=BEGIN
                               IF Job."Budget Level for Settl. Q." <> Job."Budget Level for Settl. Q."::Detailed THEN
                                 CurrReport.BREAK;
                             END;

               OnAfterGetRecord=BEGIN
                                  //IF ("Plot Recording Cross List".Code <> PrevSettlQuantCode) OR ("Plot Recording Cross List"."House Model" <> PrevHouseModelCode) OR
                                  //  (PlotRec."Plot No." <> PrevPlotNo) THEN
                                  //  ProcessBudget(PrevHouseModelCode, PrevSettlQuantCode);
                                  //PrevSettlQuantCode := "Plot Recording Cross List".Code;
                                  //PrevHouseModelCode := "Plot Recording Cross List"."House Model";
                                  //PrevPlotNo := PlotRec."Plot No.";
                                END;
                                 }

    { 3896;1   ;DataItem;RemoveBudgetDeletedSQs;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number)
                                 WHERE(Number=CONST(1));
               OnAfterGetRecord=BEGIN
                                  IF TotalBatchUpdate THEN
                                    RemoveSQLinesFromBudget(Job);
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@11012006 : TextConst 'DEU=Aktualisieren Budget...\\;ENU=Updating Budget...\\;NLD=Bijwerken werkbegroting...\\;NOR=Oppdaterer budsjettÿ...\\;SVE=Uppdaterar budgetÿ...\\';
      Text001@11012007 : TextConst 'DEU=Projekt #1##########\;ENU=Project         #1##########\;NLD=Project                                #1##########\';
      Text002@11012008 : TextConst 'DEU=Verrechnungsmenge #3##########\;ENU=Settlement Quantity   #3##########\;NLD=Verrekenbare hoeveelheid  #3##########\';
      Text003@11012010 : TextConst 'DEU=Zeilennr. #4##########\\;ENU=Line No.        #4##########\\;NLD=Regelnr.                              #4##########\\;SVE=Radnr.          #4##########\\';
      Text004@11012013 : TextConst 'DEU=Es sind keine Optionen aktualisiert worden.;ENU=No Options Updated;NLD=Er zijn geen opties bijgewerkt;NOR=Ingen alternativer er oppdatert;SVE=Inga alternativ har uppdaterats';
      Text005@11012014 : TextConst 'DEU=Es sind %1 Optionen aktualisiert worden.;ENU=There are %1 Options updated;NLD=Er zijn %1 opties bijgewerkt;NOR=%1 alternativ er oppdatert;SVE=%1 alternativ har uppdaterats';
      ProjSetupRec@1210190013 : Record 315;
      ProjElemRec@1210190010 : Record 11012010;
      BudgetRec@1210190003 : Record 11012001;
      SettlementQuantityBudget@1210190001 : Record 11072244;
      DimValRec@1210190011 : Record 349;
      ProjPurchActRec@1100525000 : Record 11012026;
      TmpSettlementQuantityBudget@1210190036 : TEMPORARY Record 11072244;
      TmpSettlementQuantity@1100525006 : TEMPORARY Record 11072243;
      SettlementQuantity@1100528200 : Record 11072243;
      PlotRec@1100525007 : Record 11012500;
      DimMgtCU@1210190012 : Codeunit 408;
      Window@11012000 : Dialog;
      SQLineType@1210190002 : Option;
      gOnDeleteSettlQuanRun@1210190034 : Boolean;
      gOnResetSettlQuanStatusRun@1210190046 : Boolean;
      SkipWriteTmpOptBudget@1210190045 : Boolean;
      VersionDate@1210190044 : Date;
      Counter@11012001 : Integer;
      NextLineNoTmp@1210190037 : Integer;
      NextLineNo@1210190007 : Integer;
      gDelProjectNo@1210190043 : Code[20];
      gDelHouseModel@1100525004 : Code[20];
      gDelCode@1210190000 : Code[20];
      ProjElement@1210190004 : Code[20];
      ProjChapter@1210190008 : Code[20];
      ProjParagraph@1210190009 : Code[20];
      CostObjectLabor@1210190014 : Code[20];
      CostObjectMaterial@1210190015 : Code[20];
      CostObjectSubcontr@1210190016 : Code[20];
      CostObjectPlant@1210190017 : Code[20];
      CostObjectSundry@1210190018 : Code[20];
      UnitLabor@1210190028 : Code[10];
      UnitMaterial@1210190027 : Code[10];
      UnitSubcontr@1210190026 : Code[10];
      UnitPlant@1210190025 : Code[10];
      UnitSundry@1210190024 : Code[10];
      TimeUnitLabor@1210190033 : Code[10];
      TimeUnitMaterial@1210190032 : Code[10];
      TimeUnitSubcontr@1210190031 : Code[10];
      TimeUnitPlant@1210190030 : Code[10];
      TimeUnitSundry@1210190029 : Code[10];
      PrevSettlQuantCode@1210190038 : Code[20];
      PrevSettlQuantCodeClearOtherLevels@1210190035 : Code[20];
      CostObjectNameLabor@1210190023 : Text[50];
      CostObjectNameMaterial@1210190022 : Text[50];
      CostObjectNameSubcontr@1210190021 : Text[50];
      CostObjectNamePlant@1210190020 : Text[50];
      CostObjectNameSundry@1210190019 : Text[50];
      TotalBatchUpdate@1100485001 : Boolean;
      Text006@1100485000 : TextConst 'DEU=Das Budget ist aktualisiert worden.;ENU="The budget is updated. ";NLD="De werkbegroting is bijgewerkt. "';
      UseAdjustmentNo@1100528201 : Code[10];
      LastAdjustmentNo@1100528202 : Code[10];
      Text007@1100525001 : TextConst 'DEU=Wohnungsmodell #2##########\;ENU=House Model  #2##########\;NLD=Woningmodel                      #2##########\';
      PrevHouseModelCode@1100525003 : Code[20];
      PrevHouseModelCodeClearOtherLevels@1100525002 : Code[20];
      FreezeAdjustment@1100529500 : Boolean;
      FirstPlot@1100525008 : Boolean;
      FirstPlotNo@1100525019 : Boolean;
      BudgetDescription@1100527350 : Text[50];

    PROCEDURE SetOnDeleteSettlQuanRun@1210190002(IProjectNo@1210190000 : Code[20];IHouseModel@1100525000 : Code[20];ICode@1210190006 : Code[20]);
    BEGIN
      gOnDeleteSettlQuanRun := TRUE;
      gDelProjectNo := IProjectNo;
      gDelCode := ICode;
      gDelHouseModel := IHouseModel;
    END;

    PROCEDURE SetOnResetSettlQuanStatusRun@1210190003(IProjectNo@1210190000 : Code[20];IHouseModel@1100525000 : Code[20];IPlotNo@1210190001 : Code[10];ICode@1210190006 : Code[20]);
    BEGIN
      gOnResetSettlQuanStatusRun := TRUE;
      gDelProjectNo := IProjectNo;
      gDelCode := ICode;
      gDelHouseModel := IHouseModel;
    END;

    PROCEDURE UpdateTmpSQBudget@1210190041(ISettlementQuantityBudget@1210190000 : Record 11072244);
    BEGIN
      WITH ISettlementQuantityBudget DO BEGIN
      { TODO, because data can not be converted.
        IF (Job."Budget Level for Options" = Job."Budget Level for Options"::Detailed) THEN BEGIN
          TmpSettlementQuantityBudget := ISettlementQuantityBudget;
          TmpSettlementQuantityBudget."Source Type" := SQLineType;
          IF NOT TmpSettlementQuantityBudget.INSERT THEN BEGIN
            TmpSettlementQuantityBudget.GET("Project No.","House Model","Settlement Quantity Code","Line No.");
            IF (SQLineType <> BudgetRec."Settl. Q. Line Type"::"SQ Surcharge") THEN
              TmpSettlementQuantityBudget.Quantity := TmpSettlementQuantityBudget.Quantity + Quantity;
            TmpSettlementQuantityBudget.Amount := TmpSettlementQuantityBudget.Amount + Amount;
            TmpSettlementQuantityBudget.Hours := TmpSettlementQuantityBudget.Hours + Hours;
            TmpSettlementQuantityBudget."Surcharge Amnt from Summary" := TmpSettlementQuantityBudget."Surcharge Amnt from Summary" + "Surcharge Amnt from Summary";
            TmpSettlementQuantityBudget.MODIFY;
          END;

          EXIT;
        END;
      }
        CASE Job."Budget Level for Settl. Q." OF
          Job."Budget Level for Settl. Q."::"SQ-Cost Type",
          Job."Budget Level for Settl. Q."::"SQ-Element-Cost Type":
            BEGIN
              IF Job."Budget Level for Settl. Q." = Job."Budget Level for Settl. Q."::"SQ-Cost Type" THEN BEGIN
                Element := ProjElement;
                Chapter := ProjChapter;
                Paragraph := ProjParagraph;
              END;
              GetCommonCostObjectData("Cost Type", "Cost Object", Description, "Unit of Measure", "Unit of Time");
              "Description 2" := '';
              "Item No." := '';
              "Basic Item" := '';
              Manufacturer := '';
              "Trade Item" := '';
              "Vendor (Trade Item)" := '';
              Quantity := 1;
              "Time Quantity" := 1;
              IF "Cost Type" = "Cost Type"::Labor THEN
                "Rate Code" := "Rate Code";   //May differ, so don't do this?

            END;
          Job."Budget Level for Settl. Q."::Detailed,
          Job."Budget Level for Settl. Q."::"SQ-Cost Object",
          Job."Budget Level for Settl. Q."::"SQ-Element-Cost Object":
            BEGIN
              IF (Job."Budget Level for Settl. Q." = Job."Budget Level for Settl. Q."::"SQ-Cost Object") THEN BEGIN
                Element := ProjElement;
                Chapter := ProjChapter;
                Paragraph := ProjParagraph;
              END;
              IF "Cost Type" = "Cost Type"::Labor THEN
                "Rate Code" := "Rate Code";  //May differ, so don't do this?
            END;
        END;

        TmpSettlementQuantityBudget.RESET;
        TmpSettlementQuantityBudget.SETRANGE("Source Type", SQLineType);  //* (Mis)Use of field 'Source Type' for Settl. Q. Line Type.
        TmpSettlementQuantityBudget.SETRANGE("Project No.", "Project No.");
        TmpSettlementQuantityBudget.SETRANGE("Settlement Quantity Code", "Settlement Quantity Code");
        TmpSettlementQuantityBudget.SETRANGE("House Model", "House Model");
        TmpSettlementQuantityBudget.SETRANGE("Cost Component", "Cost Component");
        CASE Job."Budget Level for Settl. Q." OF
          Job."Budget Level for Settl. Q."::"SQ-Cost Type",
          Job."Budget Level for Settl. Q."::"SQ-Element-Cost Type":
            BEGIN
              IF Job."Budget Level for Settl. Q." = Job."Budget Level for Settl. Q."::"SQ-Element-Cost Type" THEN
                TmpSettlementQuantityBudget.SETRANGE(Element, Element);
              TmpSettlementQuantityBudget.SETRANGE("Cost Type", "Cost Type");
            END;
          Job."Budget Level for Settl. Q."::Detailed,
          Job."Budget Level for Settl. Q."::"SQ-Cost Object",
          Job."Budget Level for Settl. Q."::"SQ-Element-Cost Object":
            BEGIN
              IF (Job."Budget Level for Settl. Q." = Job."Budget Level for Settl. Q."::"SQ-Element-Cost Object") OR
                 (Job."Budget Level for Settl. Q." = Job."Budget Level for Settl. Q."::Detailed) THEN
                TmpSettlementQuantityBudget.SETRANGE(Element, Element);
              TmpSettlementQuantityBudget.SETRANGE("Cost Type", "Cost Type");
              TmpSettlementQuantityBudget.SETRANGE("Cost Object", "Cost Object");
              TmpSettlementQuantityBudget.SETRANGE(Description, Description);
              TmpSettlementQuantityBudget.SETRANGE("Description 2", "Description 2");
              TmpSettlementQuantityBudget.SETRANGE("Unit of Measure", "Unit of Measure");
              TmpSettlementQuantityBudget.SETRANGE("Unit of Time", "Unit of Time");
              IF "Cost Type" = "Cost Type"::Material THEN BEGIN
                TmpSettlementQuantityBudget.SETRANGE("Item No.", "Item No.");
                TmpSettlementQuantityBudget.SETRANGE("Basic Item", "Basic Item");
                TmpSettlementQuantityBudget.SETRANGE(Manufacturer, Manufacturer);
                TmpSettlementQuantityBudget.SETRANGE("Trade Item", "Trade Item");
                TmpSettlementQuantityBudget.SETRANGE("Vendor (Trade Item)", "Vendor (Trade Item)");
              END;
            END;
        END;

        //extra filter for summary costs
        IF "Overhead Surcharge from Summ." THEN
          TmpSettlementQuantityBudget.SETRANGE("Overhead Surcharge from Summ.", "Overhead Surcharge from Summ.");

        IF NOT TmpSettlementQuantityBudget.FIND('-') THEN BEGIN
          TmpSettlementQuantityBudget := ISettlementQuantityBudget;
          TmpSettlementQuantityBudget."Line No." := NextLineNoTmp;
          NextLineNoTmp := NextLineNoTmp + 1;
          TmpSettlementQuantityBudget."Source Type" := SQLineType;
          TmpSettlementQuantityBudget.INSERT;
        END ELSE BEGIN
          //* Note: Don't count "Time Quantity", is always '1' when data is compressed
          IF (Job."Budget Level for Settl. Q." <> Job."Budget Level for Settl. Q."::"SQ-Cost Type") AND
             (Job."Budget Level for Settl. Q." <> Job."Budget Level for Settl. Q."::"SQ-Element-Cost Type")
          THEN BEGIN
            TmpSettlementQuantityBudget.Quantity := TmpSettlementQuantityBudget.Quantity + Quantity;
          END;
          TmpSettlementQuantityBudget.Amount := TmpSettlementQuantityBudget.Amount + Amount;
          TmpSettlementQuantityBudget.Hours := TmpSettlementQuantityBudget.Hours + Hours;

          TmpSettlementQuantityBudget."Surcharge Amnt from Summary" := TmpSettlementQuantityBudget."Surcharge Amnt from Summary" + "Surcharge Amnt from Summary";
          TmpSettlementQuantityBudget.MODIFY;
        END;

      END;
    END;

    PROCEDURE ProcessBudget@1210190050(IHouseModel@1100525000 : Code[20];ICode@1210190000 : Code[20]);
    VAR
      lvBudgetAdjustment@1100528203 : Record 11012003;
      lvSettlementQuantity@1100485000 : Record 11072243;
      lvBudgetRecTotalCount@1100528204 : Record 11012001;
      lvStep@1100528202 : Integer;
      TotalQuantity@1100528205 : Decimal;
      TotalAmount@1100528200 : Decimal;
      totalHours@1100528206 : Decimal;
      TmpSettlementQuantityBudget2@1100525001 : TEMPORARY Record 11072244;
    BEGIN
      IF ICode = '' THEN
        EXIT;

      //check adjustment used
      UseAdjustmentNo := '';
      LastAdjustmentNo := '';
      IF Job."Budget Fixed" THEN BEGIN
        lvBudgetAdjustment.SETRANGE("Project No.",Job."No.");
        IF lvBudgetAdjustment.FINDLAST THEN BEGIN
          LastAdjustmentNo := lvBudgetAdjustment."No.";
          IF lvBudgetAdjustment."Correction Fixed" THEN BEGIN
            lvBudgetAdjustment."Correction Fixed" := FALSE;
            lvBudgetAdjustment."Budget Correction Fixed" := FALSE;
            lvBudgetAdjustment.GetDefaultAdjustmentNo();
            lvBudgetAdjustment.Description := BudgetDescription;
            lvBudgetAdjustment.INSERT(TRUE);
          END;
        END ELSE BEGIN
          lvBudgetAdjustment."Project No." := Job."No.";
          lvBudgetAdjustment.GetDefaultAdjustmentNo();
          lvBudgetAdjustment."Correction Fixed" := FALSE;
          lvBudgetAdjustment."Budget Correction Fixed" := FALSE;
          lvBudgetAdjustment.Description := BudgetDescription;
          lvBudgetAdjustment.INSERT(TRUE);
        END;
        UseAdjustmentNo := lvBudgetAdjustment."No.";
      END;
      //
      //** Copy tmp table
      TmpSettlementQuantityBudget.RESET;
      IF TmpSettlementQuantityBudget.FINDSET THEN
        REPEAT
          TmpSettlementQuantityBudget2 := TmpSettlementQuantityBudget;
          TmpSettlementQuantityBudget2.INSERT;
        UNTIL TmpSettlementQuantityBudget.NEXT = 0;
      //
      BudgetRec.RESET;
      BudgetRec.SETCURRENTKEY("Project No.", Adjustment, "Extension Contract", Option, "Settlement Quantity Code", "Line No.");  //?? No such key??
      BudgetRec.SETRANGE("Project No.", Job."No.");
      BudgetRec.SETRANGE("Extension Contract", '');
      BudgetRec.SETRANGE(Option, '');
      BudgetRec.SETRANGE("House Model", IHouseModel);
      BudgetRec.SETRANGE("Settlement Quantity Code", ICode);

      BudgetRec.SETRANGE("Budget Level for Settl. Q.", Job."Budget Level for Settl. Q.");
      IF Job."Budget Level for Settl. Q." = Job."Budget Level for Settl. Q."::Detailed THEN
        BudgetRec.SETRANGE("Plot No.",PlotRec."Plot No.")
      ELSE
        BudgetRec.SETRANGE("Plot No.");

      IF BudgetRec.FINDSET(TRUE, FALSE) THEN BEGIN  //Try to find a matching line in the TmpSettlementQuantityBudget
        REPEAT
          lvBudgetRecTotalCount.COPY(BudgetRec);
          TmpSettlementQuantityBudget.RESET;
          TmpSettlementQuantityBudget.SETRANGE("Source Type", BudgetRec."Settl. Q. Line Type");
          TmpSettlementQuantityBudget.SETRANGE("Project No.", BudgetRec."Project No.");
          TmpSettlementQuantityBudget.SETRANGE("House Model", IHouseModel);
          TmpSettlementQuantityBudget.SETRANGE("Settlement Quantity Code", ICode);
          TmpSettlementQuantityBudget.SETRANGE("Cost Component", BudgetRec."Cost Component");
          TmpSettlementQuantityBudget.SETRANGE("Overhead Surcharge from Summ.", BudgetRec."Overhead Surcharge from Summ.");

          lvBudgetRecTotalCount.SETRANGE("Settl. Q. Line Type", BudgetRec."Settl. Q. Line Type");
          lvBudgetRecTotalCount.SETRANGE("Cost Component", BudgetRec."Cost Component");
          lvBudgetRecTotalCount.SETRANGE("Overhead Surcharge from Summ.", BudgetRec."Overhead Surcharge from Summ.");

          CASE Job."Budget Level for Settl. Q." OF
            Job."Budget Level for Settl. Q."::Detailed:
              BEGIN
                IF BudgetRec."Settl. Q. Line Type" <> BudgetRec."Settl. Q. Line Type"::"SQ Surcharge" THEN
                  TmpSettlementQuantityBudget.SETRANGE("Line No.", BudgetRec."Settl. Q. Line No.")
                ELSE
                  TmpSettlementQuantityBudget.SETRANGE("Line No.", -BudgetRec."Settl. Q. Line No."); //* Surch. with neg. Line No in 'TmpSettlementQuantityBudget'
                lvBudgetRecTotalCount.SETRANGE("Settl. Q. Line No.", BudgetRec."Settl. Q. Line No.");
                //test

                //test
              END;
            Job."Budget Level for Settl. Q."::"SQ-Cost Type",
            Job."Budget Level for Settl. Q."::"SQ-Element-Cost Type":
              BEGIN
                IF Job."Budget Level for Settl. Q." = Job."Budget Level for Settl. Q."::"SQ-Element-Cost Type" THEN BEGIN
                  TmpSettlementQuantityBudget.SETRANGE(Element, BudgetRec.Element);
                  lvBudgetRecTotalCount.SETRANGE(Element, BudgetRec.Element);
                END;
                TmpSettlementQuantityBudget.SETRANGE("Cost Type", BudgetRec."Cost Type");
                lvBudgetRecTotalCount.SETRANGE("Cost Type", BudgetRec."Cost Type");
              END;
            Job."Budget Level for Settl. Q."::"SQ-Cost Object",
            Job."Budget Level for Settl. Q."::"SQ-Element-Cost Object":
              BEGIN
                IF (Job."Budget Level for Settl. Q." = Job."Budget Level for Settl. Q."::"SQ-Element-Cost Object") OR
                   (Job."Budget Level for Settl. Q." = Job."Budget Level for Settl. Q."::Detailed)  THEN
                BEGIN
                  TmpSettlementQuantityBudget.SETRANGE(Element, BudgetRec.Element);
                  lvBudgetRecTotalCount.SETRANGE(Element, BudgetRec.Element);
                END;
                TmpSettlementQuantityBudget.SETRANGE("Cost Type", BudgetRec."Cost Type");
                TmpSettlementQuantityBudget.SETRANGE("Cost Object", BudgetRec."Cost Object");
                TmpSettlementQuantityBudget.SETRANGE(Description, BudgetRec.Description);
                TmpSettlementQuantityBudget.SETRANGE("Description 2", BudgetRec."Description 2");
                TmpSettlementQuantityBudget.SETRANGE("Unit of Measure", BudgetRec."Unit of Measure");
                TmpSettlementQuantityBudget.SETRANGE("Unit of Time", BudgetRec."Unit of Time");

                lvBudgetRecTotalCount.SETRANGE("Cost Type", BudgetRec."Cost Type");
                lvBudgetRecTotalCount.SETRANGE("Cost Object", BudgetRec."Cost Object");
                lvBudgetRecTotalCount.SETRANGE(Description, BudgetRec.Description);
                lvBudgetRecTotalCount.SETRANGE("Description 2", BudgetRec."Description 2");
                lvBudgetRecTotalCount.SETRANGE("Unit of Measure", BudgetRec."Unit of Measure");
                lvBudgetRecTotalCount.SETRANGE("Unit of Time", BudgetRec."Unit of Time");

                IF BudgetRec."Cost Type" = BudgetRec."Cost Type"::Material THEN BEGIN
                  TmpSettlementQuantityBudget.SETRANGE("Item No.", BudgetRec."Item No.");
                  TmpSettlementQuantityBudget.SETRANGE("Basic Item", BudgetRec."Basic Item");
                  TmpSettlementQuantityBudget.SETRANGE(Manufacturer, BudgetRec.Manufacturer);
                  TmpSettlementQuantityBudget.SETRANGE("Trade Item", BudgetRec."Trade Item");
                  TmpSettlementQuantityBudget.SETRANGE("Vendor (Trade Item)", BudgetRec."Vendor (Trade Item)");

                  lvBudgetRecTotalCount.SETRANGE("Item No.", BudgetRec."Item No.");
                  lvBudgetRecTotalCount.SETRANGE("Basic Item", BudgetRec."Basic Item");
                  lvBudgetRecTotalCount.SETRANGE(Manufacturer, BudgetRec.Manufacturer);
                  lvBudgetRecTotalCount.SETRANGE("Trade Item", BudgetRec."Trade Item");
                  lvBudgetRecTotalCount.SETRANGE("Vendor (Trade Item)", BudgetRec."Vendor (Trade Item)");
                END;
              END;
          END;

          IF NOT TmpSettlementQuantityBudget.FIND('-') THEN BEGIN
            //Budget is not present in settlement quantities, so remove or set totals to 0.
            //new 17-12 s
            TotalQuantity  := 0;
            totalHours := 0;
            TotalAmount := 0;
            lvBudgetRecTotalCount.SETCURRENTKEY("Project No.",Adjustment,"Extension Contract",Option,"Settlement Quantity Code","Line No."); //?? no such key ?
            IF lvBudgetRecTotalCount.FINDSET THEN BEGIN
              lvStep := 0;
              REPEAT
                TotalQuantity := TotalQuantity + lvBudgetRecTotalCount."Time Quantity" * lvBudgetRecTotalCount.Quantity;
                TotalAmount :=  TotalAmount + lvBudgetRecTotalCount."Amount (LCY)";
                totalHours := totalHours + lvBudgetRecTotalCount.Hours;
                lvStep += 1;
              UNTIL lvBudgetRecTotalCount.NEXT = 0;
            END;
            IF NOT ((TotalQuantity = 0) AND (TotalAmount = 0) AND (totalHours = 0)) THEN BEGIN
              IF lvStep >0 THEN BEGIN
                IF (UseAdjustmentNo = '') OR
                   ((LastAdjustmentNo=UseAdjustmentNo) AND (UseAdjustmentNo = lvBudgetRecTotalCount.Adjustment)) THEN
                BEGIN
                  //rewrite last line (reset total to 0)
                  CorrectBudgetRec(lvBudgetRecTotalCount, totalHours, TotalQuantity, TotalAmount);

                  IF (lvBudgetRecTotalCount."Amount (LCY)" <> 0) OR (lvBudgetRecTotalCount.Quantity <> 0) THEN
                    lvBudgetRecTotalCount.MODIFY
                  ELSE
                    lvBudgetRecTotalCount.DELETE;

                  Job.UpdateCostControlStatus(1, '', BudgetRec."Cost Object");
                END ELSE BEGIN
                  //Add line to last Adjustment
                  lvBudgetRecTotalCount.Adjustment := UseAdjustmentNo;
                  NewBudgetRecValues(lvBudgetRecTotalCount, totalHours, TotalQuantity, TotalAmount);
                  //Reset Purchase Action
                  lvBudgetRecTotalCount."Purchase Action" := '';
                  IF lvBudgetRecTotalCount."Cost Object" <> '' THEN BEGIN
                    DimMgtCU.GetDimValueRec(2, lvBudgetRecTotalCount."Cost Object", DimValRec, FALSE, '');
                    IF DimValRec."Purchase Action" <> '' THEN BEGIN
                      IF ProjPurchActRec.GET(lvBudgetRecTotalCount."Project No.", DimValRec."Purchase Action") THEN BEGIN
                        IF ProjPurchActRec.Status < ProjPurchActRec.Status::Applied THEN
                          lvBudgetRecTotalCount."Purchase Action" := DimValRec."Purchase Action";
                      END;
                    END;
                  END;
                  //
                  lvBudgetRecTotalCount.INSERT;
                  Job.UpdateCostControlStatus(1, '', BudgetRec."Cost Object");
                END;
              END;
            END;
          END ELSE BEGIN
            //record is present in TmpSettlementQuantityBudget, modify totals
            TotalQuantity  := 0;
            totalHours := 0;
            TotalAmount := 0;
            IF lvBudgetRecTotalCount.FINDSET THEN BEGIN
              lvStep := 0;
              REPEAT
                TotalQuantity := TotalQuantity + lvBudgetRecTotalCount."Time Quantity" * lvBudgetRecTotalCount.Quantity;
                TotalAmount :=  TotalAmount + lvBudgetRecTotalCount."Amount (LCY)";
                totalHours := totalHours  + lvBudgetRecTotalCount.Hours;
                lvStep += 1;
              UNTIL lvBudgetRecTotalCount.NEXT = 0;
            END;

            IF NOT ((TotalQuantity = TmpSettlementQuantityBudget.Quantity) AND
                    (TotalAmount = TmpSettlementQuantityBudget.Amount) AND
                    (totalHours = TmpSettlementQuantityBudget.Hours)) THEN BEGIN
              IF lvStep > 0 THEN BEGIN
                IF (UseAdjustmentNo = '') OR
                   ((LastAdjustmentNo=UseAdjustmentNo) AND (UseAdjustmentNo = lvBudgetRecTotalCount.Adjustment)) THEN
                BEGIN
                  //rewrite last line
                  CorrectBudgetRec(lvBudgetRecTotalCount,
                    totalHours-TmpSettlementQuantityBudget.Hours,
                    TotalQuantity-TmpSettlementQuantityBudget.Quantity,
                    TotalAmount-TmpSettlementQuantityBudget.Amount);

                  IF (lvBudgetRecTotalCount."Amount (LCY)" <> 0) OR (lvBudgetRecTotalCount.Quantity <> 0) THEN
                    lvBudgetRecTotalCount.MODIFY
                  ELSE
                    lvBudgetRecTotalCount.DELETE;

                  Job.UpdateCostControlStatus(1, '', BudgetRec."Cost Object");
                END ELSE BEGIN
                  //Add line to last adjustment
                  lvBudgetRecTotalCount.Adjustment := UseAdjustmentNo;
                  NewBudgetRecValues(lvBudgetRecTotalCount,
                    totalHours-TmpSettlementQuantityBudget.Hours,
                    TotalQuantity-TmpSettlementQuantityBudget.Quantity,
                    TotalAmount-TmpSettlementQuantityBudget.Amount);

                    //Reset Purchase Action
                    lvBudgetRecTotalCount."Purchase Action" := '';
                    IF lvBudgetRecTotalCount."Cost Object" <> '' THEN BEGIN
                      DimMgtCU.GetDimValueRec(2, lvBudgetRecTotalCount."Cost Object", DimValRec, FALSE, '');
                      IF DimValRec."Purchase Action" <> '' THEN BEGIN
                        IF ProjPurchActRec.GET(lvBudgetRecTotalCount."Project No.", DimValRec."Purchase Action") THEN BEGIN
                          IF ProjPurchActRec.Status < ProjPurchActRec.Status::Applied THEN
                            lvBudgetRecTotalCount."Purchase Action" := DimValRec."Purchase Action";
                        END;
                      END;
                    END;
                    //

                  lvBudgetRecTotalCount.INSERT;
                  Job.UpdateCostControlStatus(1, '', BudgetRec."Cost Object");
                END;
              END;
            END;

            IF TotalBatchUpdate THEN
              InsertTmpUpdBudgetLine();

            TmpSettlementQuantityBudget2 := TmpSettlementQuantityBudget;
            IF TmpSettlementQuantityBudget2.FIND('=') THEN
              TmpSettlementQuantityBudget2.DELETE;
          END;

          lvBudgetRecTotalCount.SETRANGE("Settl. Q. Line Type");
          lvBudgetRecTotalCount.SETRANGE("Cost Component");
          lvBudgetRecTotalCount.SETRANGE("Overhead Surcharge from Summ.");
          lvBudgetRecTotalCount.SETRANGE("Settl. Q. Line No.");
          lvBudgetRecTotalCount.SETRANGE(Element);
          lvBudgetRecTotalCount.SETRANGE("Cost Type");
          lvBudgetRecTotalCount.SETRANGE("Cost Object");
          lvBudgetRecTotalCount.SETRANGE(Description);
          lvBudgetRecTotalCount.SETRANGE("Description 2");
          lvBudgetRecTotalCount.SETRANGE("Unit of Measure");
          lvBudgetRecTotalCount.SETRANGE("Unit of Time");
          lvBudgetRecTotalCount.SETRANGE("Item No.");
          lvBudgetRecTotalCount.SETRANGE("Basic Item");
          lvBudgetRecTotalCount.SETRANGE(Manufacturer);
          lvBudgetRecTotalCount.SETRANGE("Trade Item");
          lvBudgetRecTotalCount.SETRANGE("Vendor (Trade Item)");

        UNTIL BudgetRec.NEXT = 0;
      END;

      TmpSettlementQuantityBudget2.RESET;     //Lines that are not matched with budgetline
      IF TmpSettlementQuantityBudget2.FIND('-') THEN BEGIN
        BudgetRec.RESET;
        BudgetRec.SETRANGE("Project No.", TmpSettlementQuantityBudget2."Project No.");
        BudgetRec.SETRANGE(Adjustment, UseAdjustmentNo);
        BudgetRec.SETRANGE("Extension Contract", '');
        BudgetRec.SETRANGE(Option, '');
        NextLineNo := 10;
        IF BudgetRec.FINDLAST THEN
          NextLineNo := NextLineNo + BudgetRec."Line No.";

        REPEAT
          TmpSettlementQuantityBudget := TmpSettlementQuantityBudget2;
          IF TmpSettlementQuantityBudget.FIND('=') THEN BEGIN
            BudgetRec.INIT;
            BudgetRec."Project No." := TmpSettlementQuantityBudget."Project No.";
            BudgetRec.Adjustment := UseAdjustmentNo;
            BudgetRec."Extension Contract" := '';
            BudgetRec."Settlement Quantity Code" := ICode;
            BudgetRec."House Model" := IHouseModel;
            IF Job."Budget Level for Settl. Q." = Job."Budget Level for Settl. Q."::Detailed THEN
              BudgetRec."Plot No." := PlotRec."Plot No."
            ELSE
              BudgetRec."Plot No." := '';
            BudgetRec.Option := '';
            BudgetRec."Line No." := NextLineNo;
            FillBudgetFields();
            FillPurchaseAction();
            BudgetRec.FillMainProject;

            IF BudgetRec."Settlement Quantity Code" <> '' THEN BEGIN
              lvSettlementQuantity.SETRANGE("Project No.", BudgetRec."Project No.");
              lvSettlementQuantity.SETRANGE(Code, BudgetRec."Settlement Quantity Code");
              lvSettlementQuantity.SETRANGE("House Model", BudgetRec."House Model");
              IF lvSettlementQuantity.FINDFIRST THEN
                BudgetRec."Version Date" := lvSettlementQuantity."Version Date";
            END;

            BudgetRec."Department Code" := Job."Global Dimension 1 Code";

            BudgetRec.FillElementPhaseCodeBasedOnElement;
            BudgetRec.INSERT;
            Job.UpdateCostControlStatus(0, '', BudgetRec."Cost Object");
            NextLineNo := NextLineNo + 10;
            IF TotalBatchUpdate THEN
              InsertTmpUpdBudgetLine();
          END;
        UNTIL TmpSettlementQuantityBudget2.NEXT = 0;
      END;

      TmpSettlementQuantityBudget.RESET;
      TmpSettlementQuantityBudget2.RESET;
      TmpSettlementQuantityBudget.DELETEALL;
      TmpSettlementQuantityBudget2.DELETEALL;

      NextLineNoTmp := 1;
    END;

    PROCEDURE FillBudgetFields@10();
    BEGIN
      WITH TmpSettlementQuantityBudget DO BEGIN
        BudgetRec."Settl. Q. Line Type" := "Source Type";
        BudgetRec."Budget Level for Settl. Q." := Job."Budget Level for Settl. Q.";
        BudgetRec."Version Date" := VersionDate;
        IF Job."Budget Level for Settl. Q." <> Job."Budget Level for Settl. Q."::Detailed THEN
          BudgetRec."Settl. Q. Line No." := 0
        ELSE BEGIN
          IF BudgetRec."Settl. Q. Line Type" <> BudgetRec."Settl. Q. Line Type"::"SQ Surcharge" THEN
            BudgetRec."Settl. Q. Line No." := "Line No."
          ELSE
            BudgetRec."Settl. Q. Line No." := -"Line No.";  //* Surch. with neg. Line No in 'TmpSettlementQuantityBudget',
                                                            //* must be pos. 'BudgetRec'
        END;
        BudgetRec."Cost Type" := "Cost Type";
        BudgetRec."Cost Object" := "Cost Object";
        BudgetRec."Cost Component" := "Cost Component";
        BudgetRec.Element := Element;
        BudgetRec.Chapter := Chapter;
        BudgetRec.Paragraph := Paragraph;
        BudgetRec.FillElementPhaseCodeBasedOnElement;
        BudgetRec."Item No." := "Item No.";
        BudgetRec."Basic Item" := "Basic Item";
        BudgetRec.Manufacturer := Manufacturer;
        BudgetRec."Trade Item" := "Trade Item";
        BudgetRec."Vendor (Trade Item)" := "Vendor (Trade Item)";
        BudgetRec.Description := Description;
        BudgetRec."Description 2" := "Description 2";
        BudgetRec."Unit of Measure" := "Unit of Measure";
        BudgetRec."Unit of Time" := "Unit of Time";
        BudgetRec.Quantity := Quantity;
        BudgetRec."Time Quantity" := "Time Quantity";
        BudgetRec."Amount (LCY)" := Amount;

        BudgetRec."Surcharge Amnt from Summary" := TmpSettlementQuantityBudget."Surcharge Amnt from Summary";
        BudgetRec."Overhead Surcharge from Summ." := TmpSettlementQuantityBudget."Overhead Surcharge from Summ.";
        BudgetRec."Location Code (CUF)" := TmpSettlementQuantityBudget."Location Code (CUF)";  //C054894

        IF "Cost Type" = "Cost Type"::Labor THEN BEGIN
          BudgetRec.Norm := Norm;
          BudgetRec."Rate Code" := "Rate Code";
          BudgetRec."Rate (LCY)" := Rate;
          BudgetRec.Hours := Hours;
          BudgetRec."Price (LCY)" := 0;
        END ELSE BEGIN
          BudgetRec.Norm := 0;
          BudgetRec."Rate Code" := '';
          BudgetRec."Rate (LCY)" := 0;
          BudgetRec.Hours := 0;
          BudgetRec."Price (LCY)" := Price;
        END;

        IF BudgetRec."Budget Level for Settl. Q." <> BudgetRec."Budget Level for Settl. Q."::Detailed THEN BEGIN
          IF BudgetRec."Cost Type" = "Cost Type"::Labor THEN BEGIN
            IF BudgetRec.Hours <> 0 THEN BEGIN
              BudgetRec."Rate (LCY)" := ROUND((BudgetRec."Amount (LCY)" / BudgetRec.Hours), 0.00001);
              IF BudgetRec.Quantity = 0 THEN
                BudgetRec.Quantity := 1;
              IF BudgetRec."Time Quantity" = 0 THEN
                BudgetRec."Time Quantity" := 1;
              BudgetRec.Norm := ROUND((BudgetRec.Hours / (BudgetRec.Quantity * BudgetRec."Time Quantity")), 0.00001);
            END ELSE BEGIN
              BudgetRec."Rate (LCY)" := BudgetRec."Amount (LCY)";
              BudgetRec.Norm := 0
            END;
          END ELSE BEGIN
            IF BudgetRec.Quantity <> 0 THEN
              BudgetRec."Price (LCY)" := BudgetRec."Amount (LCY)" / BudgetRec.Quantity
            ELSE
              BudgetRec."Price (LCY)" := BudgetRec."Amount (LCY)";
          END;
        END;

      END;
    END;

    PROCEDURE CorrectBudgetRec@1100528204(VAR CorrectionBudgetRec@1100528200 : Record 11012001;HoursCorrection@1100528201 : Decimal;QuantityCorrection@1100528202 : Decimal;AmountCorrection@1100528203 : Decimal);
    BEGIN
      CorrectionBudgetRec."Amount (LCY)" := CorrectionBudgetRec."Amount (LCY)" - AmountCorrection;
      CorrectionBudgetRec.Quantity := CorrectionBudgetRec.Quantity - QuantityCorrection;

      IF CorrectionBudgetRec."Cost Type" = CorrectionBudgetRec."Cost Type"::Labor THEN BEGIN
        CorrectionBudgetRec.Hours := CorrectionBudgetRec.Hours - HoursCorrection;
        IF CorrectionBudgetRec.Hours <> 0 THEN
          CorrectionBudgetRec."Rate (LCY)" := ROUND((CorrectionBudgetRec."Amount (LCY)" / CorrectionBudgetRec.Hours), 0.00001)
        ELSE
          CorrectionBudgetRec."Rate (LCY)" := CorrectionBudgetRec."Amount (LCY)";
      END ELSE BEGIN
        IF CorrectionBudgetRec.Quantity <> 0 THEN
          CorrectionBudgetRec."Price (LCY)" := CorrectionBudgetRec."Amount (LCY)" / CorrectionBudgetRec.Quantity
        ELSE
          CorrectionBudgetRec."Price (LCY)" := CorrectionBudgetRec."Amount (LCY)";
      END;
    END;

    PROCEDURE NewBudgetRecValues@1100528208(VAR NewBudgetRec@1100528203 : Record 11012001;HoursCorrection@1100528202 : Decimal;QuantityCorrection@1100528201 : Decimal;AmountCorrection@1100528200 : Decimal);
    VAR
      lBudgetRec@1100528204 : Record 11012001;
      lNextLineNo@1100528205 : Integer;
    BEGIN
      lBudgetRec.RESET;
      lBudgetRec.SETRANGE("Project No.", NewBudgetRec."Project No.");
      lBudgetRec.SETRANGE(Adjustment, NewBudgetRec.Adjustment);
      lBudgetRec.SETRANGE("Extension Contract", NewBudgetRec."Extension Contract");
      lBudgetRec.SETRANGE(Option, NewBudgetRec.Option);

      lNextLineNo := 10000;
      IF lBudgetRec.FINDLAST THEN
        lNextLineNo := lNextLineNo + lBudgetRec."Line No."; //C038595

      NewBudgetRec."Line No." := lNextLineNo;

      NewBudgetRec."Amount (LCY)" := -1 *  AmountCorrection;
      NewBudgetRec.Quantity := -1 * QuantityCorrection;

      IF NewBudgetRec."Cost Type" = NewBudgetRec."Cost Type"::Labor THEN BEGIN
        NewBudgetRec.Hours := -1 *  HoursCorrection;
        IF NewBudgetRec.Hours <> 0 THEN
          NewBudgetRec."Rate (LCY)" := ROUND((NewBudgetRec."Amount (LCY)" / NewBudgetRec.Hours), 0.00001)
        ELSE
          NewBudgetRec."Rate (LCY)" := NewBudgetRec."Amount (LCY)";
      END ELSE BEGIN
        IF NewBudgetRec.Quantity <> 0 THEN
          NewBudgetRec."Price (LCY)" := ROUND(NewBudgetRec."Amount (LCY)" / NewBudgetRec.Quantity)
        ELSE
          NewBudgetRec."Price (LCY)" := NewBudgetRec."Amount (LCY)";
      END;
    END;

    PROCEDURE FillPurchaseAction@1100525000();
    BEGIN
      IF (BudgetRec."Purchase Action" = '') AND (BudgetRec."Cost Object" <> '') THEN BEGIN
        DimMgtCU.GetDimValueRec(2, BudgetRec."Cost Object", DimValRec, FALSE, '');
        IF DimValRec."Purchase Action" <> '' THEN BEGIN
          IF ProjPurchActRec.GET(BudgetRec."Project No.", DimValRec."Purchase Action") THEN BEGIN
            IF ProjPurchActRec.Status < ProjPurchActRec.Status::Applied THEN
              BudgetRec."Purchase Action" := DimValRec."Purchase Action";
          END;
        END;
      END;
    END;

    PROCEDURE GetProjElement@1210190000(IProjNo@1210190000 : Code[20]);
    BEGIN
      ProjElement := '';
      ProjChapter := '';
      ProjParagraph := '';

      IF Job."Posting Element Mandatory" THEN BEGIN
        IF (Job."Budget Level for Settl. Q." = Job."Budget Level for Settl. Q."::"SQ-Cost Type") OR
           (Job."Budget Level for Settl. Q." = Job."Budget Level for Settl. Q."::"SQ-Cost Object")
        THEN BEGIN
          ProjElemRec.SETRANGE("Project No.", IProjNo);
          IF ProjElemRec.FIND('-') THEN BEGIN
            ProjElement := ProjElemRec.Element;
            ProjChapter := ProjElemRec.Chapter;
            ProjParagraph := ProjElemRec.Paragraph;
          END;
        END;
      END;
    END;

    PROCEDURE FillCommonCostObjects@1210190001(IProjNo@1210190000 : Code[20]);
    BEGIN
      IF (Job."Budget Level for Settl. Q." = Job."Budget Level for Settl. Q."::"SQ-Cost Type") OR
         (Job."Budget Level for Settl. Q." = Job."Budget Level for Settl. Q."::"SQ-Element-Cost Type")
      THEN BEGIN
        CostObjectLabor := ProjSetupRec."Cost Object Labor in Budget";
        DimMgtCU.GetDimValueRec(2, CostObjectLabor, DimValRec, FALSE, IProjNo);
        CostObjectNameLabor := DimValRec.Name;
        UnitLabor := DimValRec."Unit of Measure";
        TimeUnitLabor := DimValRec."Unit of Time";

        CostObjectMaterial := ProjSetupRec."Cost Object Material in Budget";
        DimMgtCU.GetDimValueRec(2, CostObjectMaterial, DimValRec, FALSE, IProjNo);
        CostObjectNameMaterial := DimValRec.Name;
        UnitMaterial := DimValRec."Unit of Measure";
        TimeUnitMaterial := DimValRec."Unit of Time";

        CostObjectSubcontr := ProjSetupRec."Cost Object Subcontr. Budget";
        DimMgtCU.GetDimValueRec(2, CostObjectSubcontr, DimValRec, FALSE, IProjNo);
        CostObjectNameSubcontr := DimValRec.Name;
        UnitSubcontr := DimValRec."Unit of Measure";
        TimeUnitSubcontr := DimValRec."Unit of Time";

        CostObjectPlant := ProjSetupRec."Cost Object Plant in Budget";
        DimMgtCU.GetDimValueRec(2, CostObjectPlant, DimValRec, FALSE, IProjNo);
        CostObjectNamePlant := DimValRec.Name;
        UnitPlant := DimValRec."Unit of Measure";
        TimeUnitPlant := DimValRec."Unit of Time";

        CostObjectSundry := ProjSetupRec."Cost Object Sundry in Budget";
        DimMgtCU.GetDimValueRec(2, CostObjectSundry, DimValRec, FALSE, IProjNo);
        CostObjectNameSundry := DimValRec.Name;
        UnitSundry := DimValRec."Unit of Measure";
        TimeUnitSundry := DimValRec."Unit of Time";
      END;
    END;

    PROCEDURE GetCommonCostObjectData@1210190012(ICostType@1210190004 : Option;VAR OCostObject@1210190000 : Code[20];VAR OName@1210190001 : Text[100];VAR OUnit@1210190002 : Code[10];VAR OTimeUnit@1210190003 : Code[10]);
    BEGIN
      CASE ICostType OF
        BudgetRec."Cost Type"::Labor:
          BEGIN
            OCostObject := CostObjectLabor;
            OName := CostObjectNameLabor;
            OUnit := UnitLabor;
            OTimeUnit := TimeUnitLabor;
          END;
        BudgetRec."Cost Type"::Material:
          BEGIN
            OCostObject := CostObjectMaterial;
            OName := CostObjectNameMaterial;
            OUnit := UnitMaterial;
            OTimeUnit := TimeUnitMaterial;
          END;
        BudgetRec."Cost Type"::Subcontracting:
          BEGIN
            OCostObject := CostObjectSubcontr;
            OName := CostObjectNameSubcontr;
            OUnit := UnitSubcontr;
            OTimeUnit := TimeUnitSubcontr;
          END;
        BudgetRec."Cost Type"::Plant:
          BEGIN
            OCostObject := CostObjectPlant;
            OName := CostObjectNamePlant;
            OUnit := UnitPlant;
            OTimeUnit := TimeUnitPlant;
          END;
        BudgetRec."Cost Type"::Sundry:
          BEGIN
            OCostObject := CostObjectSundry;
            OName := CostObjectNameSundry;
            OUnit := UnitSundry;
            OTimeUnit := TimeUnitSundry;
          END;
      END;
    END;

    PROCEDURE InsertTmpUpdBudgetLine@1100525001();
    BEGIN
      IF NOT TmpSettlementQuantity.GET(BudgetRec."Project No.", BudgetRec."House Model", BudgetRec."Settlement Quantity Code") THEN BEGIN
        TmpSettlementQuantity."Project No." := BudgetRec."Project No.";
        TmpSettlementQuantity."House Model" := BudgetRec."House Model";
        TmpSettlementQuantity.Code := BudgetRec."Settlement Quantity Code";
        TmpSettlementQuantity.INSERT;
      END;
    END;

    PROCEDURE RemoveSQLinesFromBudget@1100485000(VAR vProjectRec@1100485000 : Record 11072003);
    VAR
      lBudgetLineRec@1100485001 : Record 11012001;
    BEGIN
      //* This is done after the (whole) project is rebuild. Then budget lines of settlement quantities that not present are deleted;
      IF NOT TotalBatchUpdate THEN  //* Only when for the whole project
        EXIT;

      WITH lBudgetLineRec DO BEGIN
        SETRANGE("Project No.",vProjectRec."No.");
        SETRANGE(Adjustment, '');
        SETRANGE("Extension Contract", '');
        SETRANGE(Option,'');
        SETFILTER("Settlement Quantity Code",'<>%1','');
        IF FINDSET(TRUE,FALSE) THEN BEGIN
          REPEAT
            IF NOT TmpSettlementQuantity.GET("Project No.", "House Model", "Settlement Quantity Code") THEN BEGIN
              DELETE;
              Job.UpdateCostControlStatus(2, '', BudgetRec."Cost Object");
            END;
          UNTIL NEXT = 0;
        END;
      END;
    END;

    PROCEDURE SetBatchUpdate@1100485002(itotalBatchUpdate@1100485000 : Boolean);
    BEGIN
      TotalBatchUpdate := itotalBatchUpdate;
    END;

    PROCEDURE SetFreezeAdjustment@1100529505(IFreeze@1100529500 : Boolean);
    BEGIN
        FreezeAdjustment := IFreeze;
    END;

    PROCEDURE SetBudgetAdjustmentDescription@1100527351(IvBudgetDescription@1100527352 : Text[50]);
    BEGIN
      BudgetDescription := IvBudgetDescription;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

