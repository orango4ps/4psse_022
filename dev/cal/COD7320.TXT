OBJECT Codeunit 7320 Whse. Undo Quantity
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.03;
  }
  PROPERTIES
  {
    TableNo=83;
    Permissions=TableData 6509=md,
                TableData 7323=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      WMSMgmt@1001 : Codeunit 7302;
      Text000@1000 : TextConst 'ENU=Assertion failed, %1.;NOR=P†stand mislyktes, %1.;SVE=F”rs„kran misslyckades, %1.';
      Text001@1002 : TextConst 'ENU=There is not enough space to insert correction lines.;NOR=Det er ikke nok plass til † sette inn korrigeringslinjer.;SVE=Det finns inte tillr„ckligt med utrymme att infoga r„ttningsrader.';
      WhseJnlRegisterLine@1003 : Codeunit 7301;

    [External]
    PROCEDURE InsertTempWhseJnlLine@7302(ItemJnlLine@1000 : Record 83;SourceType@1001 : Integer;SourceSubType@1002 : Integer;SourceNo@1007 : Code[20];SourceLineNo@1005 : Integer;RefDoc@1006 : Integer;VAR TempWhseJnlLine@1008 : TEMPORARY Record 7311;VAR NextLineNo@1004 : Integer);
    VAR
      WhseEntry@1009 : Record 7312;
      WhseMgt@1003 : Codeunit 5775;
    BEGIN
      WITH ItemJnlLine DO BEGIN
        WhseEntry.RESET;
        WhseEntry.SetSourceFilter(SourceType,SourceSubType,SourceNo,SourceLineNo,TRUE);
        WhseEntry.SETRANGE("Reference No.","Document No.");
        WhseEntry.SETRANGE("Item No.","Item No.");
        IF WhseEntry.FIND('+') THEN
          REPEAT
            TempWhseJnlLine.INIT;
            IF WhseEntry."Entry Type" = WhseEntry."Entry Type"::"Positive Adjmt." THEN
              "Entry Type" := "Entry Type"::"Negative Adjmt."
            ELSE
              "Entry Type" := "Entry Type"::"Positive Adjmt.";
            Quantity := ABS(WhseEntry.Quantity);
            "Quantity (Base)" := ABS(WhseEntry."Qty. (Base)");
            "Qty. per Unit of Measure" := WhseEntry."Qty. per Unit of Measure";
            WMSMgmt.CreateWhseJnlLine(ItemJnlLine,0,TempWhseJnlLine,FALSE);
            TempWhseJnlLine.SetSource(SourceType,SourceSubType,SourceNo,SourceLineNo,0);
            TempWhseJnlLine."Source Document" :=
              WhseMgt.GetSourceDocument(TempWhseJnlLine."Source Type",TempWhseJnlLine."Source Subtype");
            TempWhseJnlLine."Reference Document" := RefDoc;
            TempWhseJnlLine."Reference No." := "Document No.";
            TempWhseJnlLine."Location Code" := "Location Code";
            TempWhseJnlLine."Zone Code" := WhseEntry."Zone Code";
            TempWhseJnlLine."Bin Code" := WhseEntry."Bin Code";
            TempWhseJnlLine.SetWhseDoc(WhseEntry."Whse. Document Type",WhseEntry."Whse. Document No.",0);
            TempWhseJnlLine."Unit of Measure Code" := WhseEntry."Unit of Measure Code";
            TempWhseJnlLine."Line No." := NextLineNo;
            TempWhseJnlLine."Serial No." := WhseEntry."Serial No.";
            TempWhseJnlLine."Lot No." := WhseEntry."Lot No.";
            TempWhseJnlLine."Expiration Date" := WhseEntry."Expiration Date";
            IF  "Entry Type" = "Entry Type"::"Negative Adjmt." THEN BEGIN
              TempWhseJnlLine."From Zone Code" := TempWhseJnlLine."Zone Code";
              TempWhseJnlLine."From Bin Code" := TempWhseJnlLine."Bin Code";
            END ELSE BEGIN
              TempWhseJnlLine."To Zone Code" := TempWhseJnlLine."Zone Code";
              TempWhseJnlLine."To Bin Code" := TempWhseJnlLine."Bin Code";
            END;
            OnBeforeTempWhseJnlLineInsert(TempWhseJnlLine,WhseEntry,ItemJnlLine);
            TempWhseJnlLine.INSERT;
            NextLineNo := TempWhseJnlLine."Line No." + 10000;
          UNTIL WhseEntry.NEXT(-1) = 0;
      END;
    END;

    [External]
    PROCEDURE PostTempWhseJnlLine@5(VAR TempWhseJnlLine@1000 : TEMPORARY Record 7311);
    BEGIN
      PostTempWhseJnlLineCache(TempWhseJnlLine,WhseJnlRegisterLine);
    END;

    [External]
    PROCEDURE PostTempWhseJnlLineCache@21(VAR TempWhseJnlLine@1000 : TEMPORARY Record 7311;VAR WhseJnlRegLine@1002 : Codeunit 7301);
    BEGIN
      OnBeforePostTempWhseJnlLine(TempWhseJnlLine);
      IF TempWhseJnlLine.FIND('-') THEN
        REPEAT
          WhseJnlRegLine.RegisterWhseJnlLine(TempWhseJnlLine);
        UNTIL TempWhseJnlLine.NEXT = 0;
    END;

    [External]
    PROCEDURE UndoPostedWhseRcptLine@10(VAR PostedWhseRcptLine@1000 : Record 7319);
    BEGIN
      PostedWhseRcptLine.TESTFIELD("Source Type");
      InsertPostedWhseRcptLine(PostedWhseRcptLine);
      DeleteWhsePutAwayRequest(PostedWhseRcptLine);
      DeleteWhseItemEntryRelationRcpt(PostedWhseRcptLine);

      OnAfterUndoPostedWhseRcptLine(PostedWhseRcptLine);
    END;

    [External]
    PROCEDURE UndoPostedWhseShptLine@3(VAR PostedWhseShptLine@1000 : Record 7323);
    BEGIN
      PostedWhseShptLine.TESTFIELD("Source Type");
      InsertPostedWhseShptLine(PostedWhseShptLine);
      DeleteWhsePickRequest(PostedWhseShptLine);
      DeleteWhseItemEntryRelationShpt(PostedWhseShptLine);

      OnAfterUndoPostedWhseShptLine(PostedWhseShptLine);
    END;

    [External]
    PROCEDURE UpdateRcptSourceDocLines@1(VAR PostedWhseRcptLine@1000 : Record 7319);
    BEGIN
      UpdateWhseRcptLine(PostedWhseRcptLine);
      UpdateWhseRequestRcpt(PostedWhseRcptLine);
    END;

    [External]
    PROCEDURE UpdateShptSourceDocLines@6(VAR PostedWhseShptLine@1000 : Record 7323);
    BEGIN
      UpdateWhseShptLine(PostedWhseShptLine);
      UpdateWhseRequestShpt(PostedWhseShptLine);
    END;

    [External]
    PROCEDURE FindPostedWhseRcptLine@11(VAR PostedWhseRcptLine@1000 : Record 7319;UndoType@1006 : Integer;UndoID@1005 : Code[20];SourceType@1004 : Integer;SourceSubtype@1003 : Integer;SourceID@1002 : Code[20];SourceRefNo@1001 : Integer) Ok : Boolean;
    BEGIN
      IF NOT PostedWhseRcptLine.READPERMISSION THEN
        EXIT;
      WITH PostedWhseRcptLine DO BEGIN
        RESET;
        CASE UndoType OF
          DATABASE::"Purch. Rcpt. Line":
            SETRANGE("Posted Source Document","Posted Source Document"::"Posted Receipt");
          DATABASE::"Return Receipt Line":
            SETRANGE("Posted Source Document","Posted Source Document"::"Posted Return Receipt");
          ELSE
            EXIT;
        END;
        SetSourceFilter(SourceType,SourceSubtype,SourceID,SourceRefNo,TRUE);
        SETRANGE("Posted Source No.",UndoID);
        OnFindPostedWhseRcptLineOnAfterSetFilters(PostedWhseRcptLine);
        IF FINDFIRST THEN BEGIN
          IF COUNT > 1 THEN
            ERROR(Text000,TABLECAPTION); // Assert: only one posted line.
          Ok := TRUE;
        END;
      END;
    END;

    [External]
    PROCEDURE FindPostedWhseShptLine@2(VAR PostedWhseShptLine@1000 : Record 7323;UndoType@1006 : Integer;UndoID@1005 : Code[20];SourceType@1004 : Integer;SourceSubtype@1003 : Integer;SourceID@1002 : Code[20];SourceRefNo@1001 : Integer) Ok : Boolean;
    VAR
      PostedWhseShptLine2@1007 : Record 7323;
    BEGIN
      IF NOT PostedWhseShptLine.READPERMISSION THEN
        EXIT;
      WITH PostedWhseShptLine DO BEGIN
        RESET;
        CASE UndoType OF
          DATABASE::"Sales Shipment Line",
          DATABASE::"Service Shipment Line":
            SETRANGE("Posted Source Document","Posted Source Document"::"Posted Shipment");
          DATABASE::"Return Shipment Line":
            SETRANGE("Posted Source Document","Posted Source Document"::"Posted Return Shipment");
          ELSE
            EXIT;
        END;
        SetSourceFilter(SourceType,SourceSubtype,SourceID,SourceRefNo,TRUE);
        SETRANGE("Posted Source No.",UndoID);
        OnFindPostedWhseShptLineOnAfterSetFilters(PostedWhseShptLine);
        IF FINDFIRST THEN BEGIN
          PostedWhseShptLine2.COPYFILTERS(PostedWhseShptLine);
          PostedWhseShptLine2.SETFILTER("No.",'<>%1',"No.");
          PostedWhseShptLine2.SETFILTER("Line No.",'<>%1',"Line No.");
          IF NOT PostedWhseShptLine2.ISEMPTY AND NOT IsATO(UndoType,UndoID,SourceRefNo) THEN
            ERROR(Text000,TABLECAPTION); // Assert: only one posted line.
          Ok := TRUE;
        END;
      END;
    END;

    LOCAL PROCEDURE InsertPostedWhseRcptLine@14(OldPostedWhseRcptLine@1000 : Record 7319);
    VAR
      NewPostedWhseRcptLine@1001 : Record 7319;
      LineSpacing@1002 : Integer;
    BEGIN
      WITH OldPostedWhseRcptLine DO BEGIN
        "Qty. Put Away" := Quantity;
        "Qty. Put Away (Base)" := "Qty. (Base)";
        MODIFY;

        NewPostedWhseRcptLine.SETRANGE("No.","No.");
        NewPostedWhseRcptLine."No." := "No.";
        NewPostedWhseRcptLine."Line No." := "Line No.";
        NewPostedWhseRcptLine.FIND('=');

        IF NewPostedWhseRcptLine.FIND('>') THEN BEGIN
          LineSpacing := (NewPostedWhseRcptLine."Line No." - "Line No.") DIV 2;
          IF LineSpacing = 0 THEN
            ERROR(Text001);
        END ELSE
          LineSpacing := 10000;

        NewPostedWhseRcptLine.RESET;
        NewPostedWhseRcptLine.INIT;
        NewPostedWhseRcptLine.COPY(OldPostedWhseRcptLine);
        NewPostedWhseRcptLine."Line No." := "Line No." + LineSpacing;
        NewPostedWhseRcptLine.Quantity := -Quantity;
        NewPostedWhseRcptLine."Qty. (Base)" := -"Qty. (Base)";
        NewPostedWhseRcptLine."Qty. Put Away" := -"Qty. Put Away";
        NewPostedWhseRcptLine."Qty. Put Away (Base)" := -"Qty. Put Away (Base)";
        NewPostedWhseRcptLine.Status := NewPostedWhseRcptLine.Status::"Completely Put Away";
        OnBeforePostedWhseRcptLineInsert(NewPostedWhseRcptLine,OldPostedWhseRcptLine);
        NewPostedWhseRcptLine.INSERT;

        Status := Status::"Completely Put Away";
        MODIFY;
      END;
    END;

    LOCAL PROCEDURE InsertPostedWhseShptLine@7(OldPostedWhseShptLine@1000 : Record 7323);
    VAR
      NewPostedWhseShptLine@1001 : Record 7323;
      LineSpacing@1002 : Integer;
    BEGIN
      WITH OldPostedWhseShptLine DO BEGIN
        NewPostedWhseShptLine.SETRANGE("No.","No.");
        NewPostedWhseShptLine."No." := "No.";
        NewPostedWhseShptLine."Line No." := "Line No.";
        NewPostedWhseShptLine.FIND('=');

        IF NewPostedWhseShptLine.FIND('>') THEN BEGIN
          LineSpacing := (NewPostedWhseShptLine."Line No." - "Line No.") DIV 2;
          IF LineSpacing = 0 THEN
            ERROR(Text001);
        END ELSE
          LineSpacing := 10000;

        NewPostedWhseShptLine.RESET;
        NewPostedWhseShptLine.INIT;
        NewPostedWhseShptLine.COPY(OldPostedWhseShptLine);
        NewPostedWhseShptLine."Line No." := "Line No." + LineSpacing;
        NewPostedWhseShptLine.Quantity := -Quantity;
        NewPostedWhseShptLine."Qty. (Base)" := -"Qty. (Base)";
        OnBeforePostedWhseShptLineInsert(NewPostedWhseShptLine,OldPostedWhseShptLine);
        NewPostedWhseShptLine.INSERT;
      END;
    END;

    LOCAL PROCEDURE DeleteWhsePutAwayRequest@16(VAR PostedWhseRcptLine@1003 : Record 7319);
    VAR
      PostedWhseRcptLine2@1000 : Record 7319;
      WhsePutAwayRequest@1002 : Record 7324;
      Sum@1001 : Decimal;
    BEGIN
      PostedWhseRcptLine2.SETRANGE("No.",PostedWhseRcptLine."No.");
      IF PostedWhseRcptLine2.FIND('-') THEN BEGIN
        REPEAT
          Sum := Sum + PostedWhseRcptLine2."Qty. (Base)";
        UNTIL PostedWhseRcptLine2.NEXT = 0;

        IF Sum = 0 THEN BEGIN
          WhsePutAwayRequest.SETRANGE("Document Type",WhsePutAwayRequest."Document Type"::Receipt);
          WhsePutAwayRequest.SETRANGE("Document No.",PostedWhseRcptLine."No.");
          WhsePutAwayRequest.DELETEALL;
        END;
      END;
    END;

    LOCAL PROCEDURE DeleteWhsePickRequest@4(VAR PostedWhseShptLine@1003 : Record 7323);
    VAR
      PostedWhseShptLine2@1000 : Record 7323;
      WhsePickRequest@1002 : Record 7325;
      Sum@1001 : Decimal;
    BEGIN
      PostedWhseShptLine2.SETRANGE("No.",PostedWhseShptLine."No.");
      IF PostedWhseShptLine2.FIND('-') THEN BEGIN
        REPEAT
          Sum := Sum + PostedWhseShptLine2."Qty. (Base)";
        UNTIL PostedWhseShptLine2.NEXT = 0;

        IF Sum = 0 THEN BEGIN
          WhsePickRequest.SETRANGE("Document Type",WhsePickRequest."Document Type"::Shipment);
          WhsePickRequest.SETRANGE("Document No.",PostedWhseShptLine."No.");
          IF NOT WhsePickRequest.ISEMPTY THEN
            WhsePickRequest.DELETEALL;
        END;
      END;
    END;

    LOCAL PROCEDURE UpdateWhseRcptLine@15(VAR PostedWhseRcptLine@1002 : Record 7319);
    VAR
      WhseRcptHeader@1001 : Record 7316;
      WhseRcptLine@1000 : Record 7317;
      WhseManagement@1003 : Codeunit 5775;
    BEGIN
      WITH PostedWhseRcptLine DO BEGIN
        WhseManagement.SetSourceFilterForWhseRcptLine(WhseRcptLine,"Source Type","Source Subtype","Source No.","Source Line No.",TRUE);
        IF WhseRcptLine.FINDFIRST THEN BEGIN
          WhseRcptLine.VALIDATE("Qty. Outstanding",WhseRcptLine."Qty. Outstanding" + Quantity);
          WhseRcptLine.VALIDATE("Qty. Received",WhseRcptLine."Qty. Received" - Quantity);
          IF WhseRcptLine."Qty. Received" = 0 THEN BEGIN
            WhseRcptLine.Status := WhseRcptLine.Status::" ";
            WhseRcptHeader.GET(WhseRcptLine."No.");
            WhseRcptHeader."Document Status" := WhseRcptHeader."Document Status"::" ";
            WhseRcptHeader.MODIFY;
          END;
          OnBeforeWhseRcptLineModify(WhseRcptLine,PostedWhseRcptLine);
          WhseRcptLine.MODIFY;
        END;
      END;
    END;

    LOCAL PROCEDURE UpdateWhseShptLine@9(VAR PostedWhseShptLine@1002 : Record 7323);
    VAR
      WhseShptHeader@1001 : Record 7320;
      WhseShptLine@1000 : Record 7321;
    BEGIN
      WITH PostedWhseShptLine DO BEGIN
        WhseShptLine.SetSourceFilter("Source Type","Source Subtype","Source No.","Source Line No.",TRUE);
        IF WhseShptLine.FINDFIRST THEN BEGIN
          WhseShptLine.VALIDATE("Qty. Shipped",WhseShptLine."Qty. Shipped" - Quantity);
          WhseShptLine.VALIDATE("Qty. Outstanding",WhseShptLine."Qty. Outstanding" + Quantity);
          IF WhseShptLine."Qty. Shipped" = 0 THEN BEGIN
            WhseShptLine.Status := WhseShptLine.Status::" ";
            WhseShptHeader.GET(WhseShptLine."No.");
            WhseShptHeader."Document Status" := WhseShptHeader."Document Status"::" ";
            WhseShptHeader.MODIFY;
          END;
          OnBeforeWhseShptLineModify(WhseShptLine,PostedWhseShptLine);
          WhseShptLine.MODIFY;
        END;
      END;
    END;

    LOCAL PROCEDURE DeleteWhseItemEntryRelationRcpt@18(NewPostedWhseRcptLine@1002 : Record 7319);
    BEGIN
      WITH NewPostedWhseRcptLine DO
        DeleteWhseItemEntryRelation(DATABASE::"Posted Whse. Receipt Line","No.","Line No.");
    END;

    LOCAL PROCEDURE DeleteWhseItemEntryRelationShpt@17(NewPostedWhseShptLine@1002 : Record 7323);
    BEGIN
      WITH NewPostedWhseShptLine DO
        DeleteWhseItemEntryRelation(DATABASE::"Posted Whse. Shipment Line","No.","Line No.");
    END;

    LOCAL PROCEDURE DeleteWhseItemEntryRelation@20(SourceType@1004 : Integer;SourceNo@1002 : Code[20];SourceLineNo@1000 : Integer);
    VAR
      WhseItemEntryRelation@1001 : Record 6509;
    BEGIN
      WhseItemEntryRelation.SetSourceFilter(SourceType,0,SourceNo,SourceLineNo,TRUE);
      WhseItemEntryRelation.DELETEALL;
    END;

    LOCAL PROCEDURE UpdateWhseRequestRcpt@12(VAR PostedWhseRcptLine@1002 : Record 7319);
    VAR
      PurchLine@1000 : Record 39;
      SalesLine@1003 : Record 37;
    BEGIN
      WITH PostedWhseRcptLine DO BEGIN
        CASE "Source Type" OF
          DATABASE::"Purchase Line":
            BEGIN
              PurchLine.GET("Source Subtype","Source No.","Source Line No.");
              IF NOT (PurchLine."Quantity Received" < PurchLine.Quantity) THEN
                EXIT;
            END;
          DATABASE::"Sales Line":
            BEGIN
              SalesLine.GET("Source Subtype","Source No.","Source Line No.");
              IF NOT (SalesLine."Return Qty. Received" < SalesLine.Quantity) THEN
                EXIT;
            END;
        END;
        UpdateWhseRequest("Source Type","Source Subtype","Source No.","Location Code");
      END;

      OnAfterUpdateWhseRequestRcpt(PostedWhseRcptLine);
    END;

    LOCAL PROCEDURE UpdateWhseRequestShpt@8(VAR PostedWhseShptLine@1002 : Record 7323);
    VAR
      PurchLine@1000 : Record 39;
      SalesLine@1003 : Record 37;
    BEGIN
      WITH PostedWhseShptLine DO BEGIN
        CASE "Source Type" OF
          DATABASE::"Sales Line":
            BEGIN
              SalesLine.GET("Source Subtype","Source No.","Source Line No.");
              IF NOT (SalesLine."Quantity Shipped" < SalesLine.Quantity) THEN
                EXIT;
            END;
          DATABASE::"Purchase Line":
            BEGIN
              PurchLine.GET("Source Subtype","Source No.","Source Line No.");
              IF NOT (PurchLine."Return Qty. Shipped" < PurchLine.Quantity) THEN
                EXIT;
            END;
        END;
        UpdateWhseRequest("Source Type","Source Subtype","Source No.","Location Code");
      END;

      OnAfterUpdateWhseRequestShpt(PostedWhseShptLine);
    END;

    LOCAL PROCEDURE UpdateWhseRequest@26(SourceType@1006 : Integer;SourceSubType@1005 : Integer;SourceNo@1004 : Code[20];LocationCode@1000 : Code[10]);
    VAR
      WhseRequest@1001 : Record 5765;
    BEGIN
      WITH WhseRequest DO BEGIN
        SETCURRENTKEY("Source Type","Source Subtype","Source No.");
        SETRANGE("Source Type",SourceType);
        SETRANGE("Source Subtype",SourceSubType);
        SETRANGE("Source No.",SourceNo);
        SETRANGE("Location Code",LocationCode);
        IF FINDFIRST AND "Completely Handled" THEN BEGIN
          "Completely Handled" := FALSE;
          MODIFY;
        END;
      END;
    END;

    LOCAL PROCEDURE IsATO@13(UndoType@1005 : Integer;UndoID@1004 : Code[20];SourceRefNo@1000 : Integer) : Boolean;
    VAR
      PostedATOLink@1001 : Record 914;
    BEGIN
      IF UndoType = DATABASE::"Sales Shipment Line" THEN BEGIN
        PostedATOLink.SETRANGE("Document Type",PostedATOLink."Document Type"::"Sales Shipment");
        PostedATOLink.SETRANGE("Document No.",UndoID);
        PostedATOLink.SETRANGE("Document Line No.",SourceRefNo);
        EXIT(NOT PostedATOLink.ISEMPTY);
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterUndoPostedWhseRcptLine@25(VAR PostedWhseReceiptLine@1000 : Record 7319);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterUndoPostedWhseShptLine@27(VAR PostedWhseShipmentLine@1000 : Record 7323);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterUpdateWhseRequestRcpt@1001(VAR PostedWhseReceiptLine@1000 : Record 7319);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterUpdateWhseRequestShpt@1000(VAR PostedWhseShipmentLine@1000 : Record 7323);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostTempWhseJnlLine@19(VAR TempWhseJnlLine@1000 : TEMPORARY Record 7311);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostedWhseRcptLineInsert@31(VAR NewPostedWhseReceiptLine@1000 : Record 7319;OldPostedWhseReceiptLine@1001 : Record 7319);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostedWhseShptLineInsert@29(VAR NewPostedWhseShipmentLine@1000 : Record 7323;OldPostedWhseShipmentLine@1001 : Record 7323);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeTempWhseJnlLineInsert@22(VAR WarehouseJournalLine@1000 : Record 7311;WarehouseEntry@1001 : Record 7312;ItemJournalLine@1002 : Record 83);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeWhseRcptLineModify@24(VAR WarehouseReceiptLine@1000 : Record 7317;VAR PostedWhseReceiptLine@1001 : Record 7319);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeWhseShptLineModify@23(VAR WarehouseShipmentLine@1000 : Record 7321;VAR PostedWhseShipmentLine@1001 : Record 7323);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnFindPostedWhseRcptLineOnAfterSetFilters@28(VAR PostedWhseReceiptLine@1000 : Record 7319);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnFindPostedWhseShptLineOnAfterSetFilters@30(VAR PostedWhseShipmentLine@1000 : Record 7323);
    BEGIN
    END;

    BEGIN
    END.
  }
}

