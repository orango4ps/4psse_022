OBJECT Page 11125304 Copy Location Structure
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Kopieren Standortstruktur;
               ENU=Copy Location Structure;
               NLD=Kopieer locatiestructuur;
               NOR=Kopier lokasjonsstruktur;
               SVE=Kopiera lagerst„llesstruktur];
    SaveValues=Yes;
    PageType=StandardDialog;
    OnOpenPage=BEGIN
                 ToSLNo := DefToSLNo;
               END;

    OnQueryClosePage=BEGIN
                       CloseActionOK := CloseAction IN [ACTION::OK,ACTION::LookupOK];
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1210190000;1;Group  ;
                CaptionML=[DEU=Kopieren von;
                           ENU=Copy from;
                           NLD=Kopi‰ren van;
                           NOR=Kopier fra;
                           SVE=Kopiera fr†n];
                GroupType=Group }

    { 1100528620;2;Field  ;
                CaptionML=[DEU=Servicestandort;
                           ENU=Service Location No.;
                           NLD=Servicelocatienr.;
                           NOR=Servicelokasjon Nr.;
                           SVE=Serviceplatsnr];
                NotBlank=Yes;
                SourceExpr=FromSLNo;
                OnValidate=VAR
                             ServiceLocation@1100528600 : Record 11012801;
                           BEGIN
                             ServiceLocation.GET(FromSLNo);
                             FromSLDescription := ServiceLocation.Description;
                           END;

                OnLookup=VAR
                           ServiceLocation@1100528600 : Record 11012801;
                         BEGIN
                           IF PAGE.RUNMODAL(0, ServiceLocation) = ACTION::LookupOK THEN BEGIN
                              FromSLNo := ServiceLocation."No.";
                              FromSLDescription := ServiceLocation.Description;
                           END;
                           CurrPage.UPDATE(TRUE);
                         END;
                          }

    { 1100528621;2;Field  ;
                CaptionML=[DEU=Beschreibung;
                           ENU=Description;
                           NLD=Omschrijving;
                           NOR=Beskrivelse;
                           SVE=Beskrivning];
                SourceExpr=FromSLDescription;
                Editable=FALSE }

    { 1100528617;2;Field  ;
                CaptionML=[DEU=Strukturnr.;
                           ENU=Structure No.;
                           NLD=Structuurnr.;
                           NOR=Strukturnr;
                           SVE=Strukturnr];
                NotBlank=Yes;
                BlankZero=Yes;
                SourceExpr=FromSLStructureNo;
                OnValidate=VAR
                             ServiceLocationStructure@1100528600 : Record 11071719;
                           BEGIN
                             IF (FromSLNo <> '') AND (FromSLStructureNo <> 0) THEN BEGIN
                               ServiceLocationStructure.GET(FromSLNo, FromSLStructureNo);
                               IF ToSLStructureNo = 0 THEN
                                 ToSLStructureNo := ServiceLocationStructure."No.";
                               IF ToSLStructureDesc = '' THEN
                                 ToSLStructureDesc := ServiceLocationStructure.Description;
                             END;
                             FromSLStructureDesc := ServiceLocationStructure.Description;
                           END;

                OnLookup=VAR
                           ServiceLocationStructure@1100528600 : Record 11071719;
                         BEGIN
                           ServiceLocationStructure.SETRANGE("Service Location No.", FromSLNo);
                           IF PAGE.RUNMODAL(0, ServiceLocationStructure) = ACTION::LookupOK THEN BEGIN
                             FromSLStructureNo := ServiceLocationStructure."No.";
                             FromSLStructureDesc := ServiceLocationStructure.Description;
                             IF ToSLStructureNo = 0 THEN
                               ToSLStructureNo := ServiceLocationStructure."No.";
                             IF ToSLStructureDesc = '' THEN
                               ToSLStructureDesc := ServiceLocationStructure.Description;
                           END;
                           CurrPage.UPDATE(TRUE);
                         END;
                          }

    { 1100528618;2;Field  ;
                CaptionML=[DEU=Beschreibung;
                           ENU=Description;
                           NLD=Omschrijving;
                           NOR=Beskrivelse;
                           SVE=Beskrivning];
                SourceExpr=FromSLStructureDesc;
                Editable=FALSE }

    { 1210190001;1;Group  ;
                CaptionML=[DEU=Kopieren in;
                           ENU=Copy to;
                           NLD=Kopi‰ren naar;
                           NOR=Kopier til;
                           SVE=Kopiera till];
                GroupType=Group }

    { 1100528610;2;Field  ;
                CaptionML=[DEU=Servicestandort;
                           ENU=Service Location No.;
                           NLD=Servicelocatienr.;
                           NOR=Servicelokasjon Nr.;
                           SVE=Serviceplatsnr];
                NotBlank=Yes;
                SourceExpr=ToSLNo;
                OnValidate=VAR
                             ServiceLocation@1100528600 : Record 11012801;
                           BEGIN
                             ServiceLocation.GET(FromSLNo);
                             ToSLDescription := ServiceLocation.Description;
                           END;

                OnLookup=VAR
                           ServiceLocation@1100528600 : Record 11012801;
                         BEGIN
                           IF PAGE.RUNMODAL(0, ServiceLocation) = ACTION::LookupOK THEN BEGIN
                             ToSLNo := ServiceLocation."No.";
                             ToSLDescription := ServiceLocation.Description;
                           END;
                           CurrPage.UPDATE(TRUE);
                         END;
                          }

    { 1100528611;2;Field  ;
                CaptionML=[DEU=Beschreibung;
                           ENU=Description;
                           NLD=Omschrijving;
                           NOR=Beskrivelse;
                           SVE=Beskrivning];
                SourceExpr=ToSLDescription;
                Editable=FALSE }

    { 1100528607;2;Field  ;
                CaptionML=[DEU=Strukturnr.;
                           ENU=Structure No.;
                           NLD=Structuurnr.;
                           NOR=Strukturnr;
                           SVE=Strukturnr];
                NotBlank=Yes;
                BlankZero=Yes;
                SourceExpr=ToSLStructureNo;
                OnValidate=VAR
                             ServiceLocationStructure@1100528600 : Record 11071719;
                           BEGIN
                             IF ServiceLocationStructure.GET(ToSLNo, ToSLStructureNo) THEN
                               ToSLStructureDesc := ServiceLocationStructure.Description;
                           END;

                OnLookup=VAR
                           ServiceLocationStructure@1100528600 : Record 11071719;
                         BEGIN
                           ServiceLocationStructure.SETRANGE("Service Location No.", ToSLNo);
                           IF PAGE.RUNMODAL(0, ServiceLocationStructure) = ACTION::LookupOK THEN BEGIN
                             ToSLStructureNo := ServiceLocationStructure."No.";
                             ToSLStructureDesc := ServiceLocationStructure.Description;
                           END;
                           CurrPage.UPDATE(TRUE);
                         END;
                          }

    { 1100528608;2;Field  ;
                CaptionML=[DEU=Beschreibung;
                           ENU=Description;
                           NLD=Omschrijving;
                           NOR=Beskrivelse;
                           SVE=Beskrivning];
                SourceExpr=ToSLStructureDesc }

  }
  CODE
  {
    VAR
      FromSLNo@1100528602 : Code[20];
      FromSLDescription@1100528603 : Text[50];
      FromSLStructureDesc@1100528600 : Text[80];
      FromSLStructureNo@1100528601 : Integer;
      DefToSLNo@1100528604 : Code[20];
      ToSLNo@1100528608 : Code[20];
      ToSLDescription@1100528605 : Text[50];
      ToSLStructureDesc@1100528606 : Text[80];
      ToSLStructureNo@1100528607 : Integer;
      Text000@1100528610 : TextConst 'DEU=Es sind bereits Strukturzeilen in der angegebenen Bestimmung vorhanden.;ENU=Structure Lines present in Destination.;NLD=Er zijn reeds structuurregels aanwezig in opgegeven bestemming.;NOR=Strukturrader tilstede i destinasjonen;SVE=Strukturrader finns i destinationen.';
      CloseActionOK@1100528400 : Boolean;

    PROCEDURE SetDefaults@1100528600(IServiceLocationNo@1100528600 : Code[20];IServiceLocationStructNo@1100528601 : Integer);
    VAR
      ServiceLocation@1100528602 : Record 11012801;
      ServiceLocationStructure@1100528603 : Record 11071719;
    BEGIN
      FromSLNo := IServiceLocationNo;
      FromSLStructureNo := IServiceLocationStructNo;
      DefToSLNo := IServiceLocationNo;
      ToSLStructureNo := IServiceLocationStructNo;

      ServiceLocation.GET(FromSLNo);
      FromSLDescription := ServiceLocation.Description;
      ToSLDescription := ServiceLocation.Description;

      IF ServiceLocationStructure.GET(IServiceLocationNo, IServiceLocationStructNo) THEN BEGIN
        FromSLStructureDesc := ServiceLocationStructure.Description;
        ToSLStructureDesc := ServiceLocationStructure.Description;
      END;
    END;

    PROCEDURE StartCopy@1100528602();
    VAR
      FromServiceLocationStructure@1100528600 : Record 11071719;
      FromServiceLocStructureLine@1100528601 : Record 11071720;
      ToServiceLocationStructure@1100528603 : Record 11071719;
      ToServiceLocStructureLine@1100528602 : Record 11071720;
    BEGIN
      ValidateStructure;
      FromServiceLocationStructure.GET(FromSLNo, FromSLStructureNo);
      IF NOT ToServiceLocationStructure.GET(ToSLNo, ToSLStructureNo) THEN BEGIN
        ToServiceLocationStructure.INIT;
        ToServiceLocationStructure := FromServiceLocationStructure;
        ToServiceLocationStructure.VALIDATE("Service Location No.", ToSLNo);
        ToServiceLocationStructure.VALIDATE("No.", ToSLStructureNo);
        ToServiceLocationStructure.VALIDATE(Description, ToSLStructureDesc);
        ToServiceLocationStructure.INSERT(TRUE);
      END ELSE BEGIN
        IF ToServiceLocationStructure.Description <> ToSLStructureDesc THEN BEGIN
          ToServiceLocationStructure.VALIDATE(Description, ToSLStructureDesc);
          ToServiceLocationStructure.MODIFY(TRUE);
        END;
      END;
      FromServiceLocStructureLine.SETRANGE("Service Location No.", FromSLNo);
      FromServiceLocStructureLine.SETRANGE("Structure No.", FromSLStructureNo);
      FromServiceLocStructureLine.SETRANGE("Object Line", FALSE);
      IF FromServiceLocStructureLine.FINDSET THEN
        REPEAT
          ToServiceLocStructureLine := FromServiceLocStructureLine;
          ToServiceLocStructureLine.VALIDATE("Service Location No.", ToSLNo);
          ToServiceLocStructureLine.VALIDATE("Structure No.", ToSLStructureNo);
          ToServiceLocStructureLine.INSERT(TRUE);
        UNTIL FromServiceLocStructureLine.NEXT = 0;
    END;

    PROCEDURE ValidateStructure@1100528605();
    VAR
      FromServiceLocation@1100528602 : Record 11012801;
      ToServiceLocation@1100528601 : Record 11012801;
      ServiceLocStructureLine@1100528600 : Record 11071720;
    BEGIN
      ServiceLocStructureLine.SETRANGE("Service Location No.", ToSLNo);
      ServiceLocStructureLine.SETRANGE("Structure No.", ToSLStructureNo);
      IF NOT ServiceLocStructureLine.ISEMPTY THEN
        ERROR(Text000);

      FromServiceLocation.GET(FromSLNo);
      ToServiceLocation.GET(ToSLNo);
      FromServiceLocation.TESTFIELD("Location Structure Format", ToServiceLocation."Location Structure Format");
    END;

    LOCAL PROCEDURE StdDialogConfirm@1100528400() : Boolean;
    BEGIN
      CurrPage.RUNMODAL;
      EXIT(CloseActionOK);
    END;

    LOCAL PROCEDURE StdDialogDoAction@1100528401();
    BEGIN
      StartCopy;
    END;

    PROCEDURE StdDialogRunModal@1100528404();
    BEGIN
      IF StdDialogConfirm THEN
        StdDialogDoAction;
    END;

    BEGIN
    END.
  }
}

