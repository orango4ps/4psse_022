OBJECT XMLport 11012358 Discount Terms (Instalnet) v10
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Discount Terms (Instalnet);
               SVE=Rabattvillkor (Instalnet)];
    DefaultFieldsValidation=No;
    PreserveWhiteSpace=Yes;
    TextEncoding=WINDOWS;
    OnPreXMLport=VAR
                   FPSLicenseManagement@1100527400 : Codeunit 11229289;
                 BEGIN
                   IF FPSLicenseManagement.LicenseAndReadPermissionForTable(CompRelationRec) THEN BEGIN
                     CentrManagementCU.ItemFormEditable(NavBln,DiscBln,DiscGrpBln,RcpBln);
                     IF (gvDiscLevel = 0) AND (DiscBln) THEN
                       ERROR(Text000);
                     IF (gvDiscLevel = 1) AND (DiscGrpBln) THEN
                       ERROR(Text000);
                   END;
                 END;

    Format=Fixed Text;
    UseRequestPage=No;
  }
  ELEMENTS
  {
    { [{AE5B5AAC-6273-41CA-BDFF-3CF86A133295}];  ;Root                ;Element ;Text     }

    { [{AE3B6C07-5E43-4B19-8485-4774F09E7162}];1 ;ImportDiscountTerm  ;Element ;Table   ;
                                                  SourceTable=Table11012736;
                                                  Import::OnAfterInitRecord=BEGIN
                                                                              "Import Discount Term".INIT;
                                                                              CLEAR(DiscCode);
                                                                              CLEAR(ItemCode);
                                                                              CLEAR(DescTxt);
                                                                              CLEAR(Disc1Txt);
                                                                              CLEAR(Disc2Txt);
                                                                              CLEAR(Disc3Txt);
                                                                              CLEAR(PriceTxt);
                                                                              CLEAR(StartDateTxt);
                                                                              CLEAR(EndDateTxt);

                                                                              Counter := Counter + 1;
                                                                            END;

                                                  Import::OnBeforeInsertRecord=VAR
                                                                                 ImportDiscountTerm@1100525000 : Record 11012736;
                                                                               BEGIN
                                                                                 WITH "Import Discount Term" DO BEGIN

                                                                                 IF Counter = 1 THEN BEGIN
                                                                                   //db, 19-01-10: M17848
                                                                                   HelpTxt := COPYSTR(DescTxt, 1, 8);
                                                                                   ProductDate := ConvertDate(HelpTxt);
                                                                                   IF ProductDate = 0D THEN
                                                                                     ProductDate := gvStartDate;
                                                                                   currXMLport.SKIP;
                                                                                 END;

                                                                                 Disc1Dec := 0;
                                                                                 Disc2Dec := 0;
                                                                                 Disc3Dec := 0;
                                                                                 PriceDec := 0;

                                                                                 IF EVALUATE(Disc1Dec,Disc1Txt) THEN;
                                                                                 IF EVALUATE(Disc2Dec,Disc2Txt) THEN;
                                                                                 IF EVALUATE(Disc3Dec,Disc3Txt) THEN;
                                                                                 IF EVALUATE(PriceDec,PriceTxt) THEN;

                                                                                 StartDate := ConvertDate(StartDateTxt);
                                                                                 EndDate := ConvertDate(EndDateTxt);

                                                                                 "Discount Level" := gvDiscLevel;
                                                                                 "Discount Reference" := gvDiscRef;
                                                                                 Vendor := gvVendor;
                                                                                 "Discount Group" := DiscCode;
                                                                                 "Item Code" := ItemCode;
                                                                                 Description := DescTxt;
                                                                                 Description := StringFunctions.DeleteInvalidXMLCharacters(Description);
                                                                                 "Discount Percentage 1" := Disc1Dec / 100;
                                                                                 "Discount Percentage 2" := Disc2Dec / 100;
                                                                                 "Discount Percentage 3" := Disc3Dec / 100;
                                                                                 "Purchase Price" := PriceDec / 100;
                                                                                 "Starting Date" := StartDate;
                                                                                 IF "Starting Date" = 0D THEN
                                                                                   "Starting Date" := gvStartDate;
                                                                                 VALIDATE("Starting Date");
                                                                                 "Ending Date" := EndDate;  //db, 11-01-10: M14768
                                                                                 "Created on" := ProductDate;  //db, 19-01-10: M17848

                                                                                 //db, 01-07-04: store item price in local currency (used function: GetCurrencyRate)
                                                                                 VALIDATE(Currency, '');  //db, 07-12-04 (changed validate tablefield)

                                                                                 CheckDuplicateOnImport;  //db, 28-06-10: M17471

                                                                                 RecRef.GETTABLE("Import Discount Term");
                                                                                 XMLportStatusDialogMgt.ShowImportStatus(RecRef);

                                                                                 IF ImportDiscountTerm.GET(
                                                                                   Vendor,
                                                                                   "Discount Group",
                                                                                   "Item Code",
                                                                                   "Discount Level",
                                                                                   "Discount Reference",
                                                                                   "Starting Date")
                                                                                 THEN BEGIN
                                                                                   UpdateImportDiscountTerm(ImportDiscountTerm);
                                                                                   currXMLport.SKIP;
                                                                                 END;

                                                                                 END; //Belongs to With statement.
                                                                               END;
                                                                                }

    { [{02FB24B1-6E06-4F8A-9BE0-A514F028229F}];2 ;DiscCode            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=20 }

    { [{7FC7FFCA-055D-4F0D-BBD4-D47209EA6B57}];2 ;ItemCode            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=20 }

    { [{5AAE15C7-0F1A-4F40-9171-024353E7B79E}];2 ;DescTxt             ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=50 }

    { [{DF6694AD-A791-4548-B2A5-D390F1C17A77}];2 ;Disc1Txt            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=5 }

    { [{4FF40AA7-CC48-462E-9814-0B3B154C7AA7}];2 ;Disc2Txt            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=5 }

    { [{9D48C808-C7E1-4BEC-998E-6BA78FF45519}];2 ;Disc3Txt            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=5 }

    { [{08E26D35-302F-4853-8E97-50E308A81E94}];2 ;PriceTxt            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=9 }

    { [{8F73460C-55B0-4082-8210-36B26680EDF9}];2 ;StartDateTxt        ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=8 }

    { [{724879C3-CA6B-40E6-8F20-CF2EEA2785B0}];2 ;EndDateTxt          ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=8 }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      VendorRec@1100525028 : Record 11012304;
      CompRelationRec@1100525027 : Record 11012059;
      RecRef@1100525015 : RecordRef;
      XMLportStatusDialogMgt@1100525016 : Codeunit 11130006;
      CentrManagementCU@1100525026 : Codeunit 11012010;
      StringFunctions@1100528400 : Codeunit 11012273;
      NavBln@1100525025 : Boolean;
      DiscBln@1100525024 : Boolean;
      DiscGrpBln@1100525017 : Boolean;
      RcpBln@1100525023 : Boolean;
      Disc1Dec@1100525013 : Decimal;
      Disc2Dec@1100525012 : Decimal;
      Disc3Dec@1100525011 : Decimal;
      PriceDec@1100525010 : Decimal;
      StartDate@1100525009 : Date;
      EndDate@1100525008 : Date;
      Counter@1100525007 : Integer;
      gvVendor@1100525006 : Code[20];
      gvCurrency@1100525005 : Code[10];
      gvDiscLevel@1100525004 : Integer;
      gvDiscRef@1100525003 : Code[20];
      gvStartDate@1100525002 : Date;
      ProductDate@1100525001 : Date;
      HelpTxt@1100525000 : Text[30];
      Text000@1100525014 : TextConst 'ENU=Transaction only allowed in company used as source for central itemmanagement;SVE=Transaktionen „r endast till†ten f”r f”retag som anv„nds som k„lla f”r central artikelhantering';

    PROCEDURE DetermineSelection@1(lvVendor@11012000 : Code[20];lvCurrency@11012001 : Code[10];lvDiscLevel@1100485001 : Integer;lvDiscRef@1100485002 : Code[20];lvStartDate@1100485003 : Date);
    BEGIN
      gvVendor := lvVendor;
      gvCurrency := lvCurrency;
      gvDiscLevel := lvDiscLevel;
      gvDiscRef := lvDiscRef;
      gvStartDate := lvStartDate;
      IF gvStartDate = 0D THEN  //db, 19-01-10: M17848
        gvStartDate := TODAY;

      VendorRec.GET(gvVendor);
      IF VendorRec."Alternative Vendor Code" <> '' THEN BEGIN
        gvVendor := VendorRec."Alternative Vendor Code";
        VendorRec.GET(gvVendor);
      END;
    END;

    PROCEDURE ConvertDate@1100525001(DateTxt@1100525001 : Text[30]) : Date;
    VAR
      lvDate@1100525000 : Date;
    BEGIN
      DateTxt := COPYSTR(DateTxt,7,2) + COPYSTR(DateTxt,5,2) + COPYSTR(DateTxt,1,4);
      IF DateTxt <> '' THEN
        IF EVALUATE(lvDate, DateTxt) THEN;

      EXIT(lvDate);
    END;

    PROCEDURE CheckDuplicateOnImport@1100525000();
    VAR
      DuplRec@1100525002 : Record 11012736;
      ImportCu@1100525001 : Codeunit 11012304;
      MessLine@1100525000 : Integer;
      lvText001@1100525004 : TextConst 'ENU=Duplicate Key: Discount Terme %1 %2 (Amount %3, Percentage %4);SVE=Nyckelkopia: Rabattvillkor %1 %2 (belopp %3, procent %4)';
    BEGIN
      //db, 28-06-10: M17471
      DuplRec.COPY("Import Discount Term");
      IF NOT DuplRec.INSERT THEN BEGIN
        WITH "Import Discount Term" DO BEGIN
          ImportCu.WriteMessage(Vendor,"Item Code","Discount Group",
            STRSUBSTNO(lvText001,"Discount Group",Description,"Purchase Price","Discount Percentage 1"),0,FALSE,MessLine);
        END;
      END;
    END;

    LOCAL PROCEDURE UpdateImportDiscountTerm@1100525006(ImportDiscountTerm@1100525000 : Record 11012736);
    BEGIN
      ImportDiscountTerm.Currency := "Import Discount Term".Currency;
      ImportDiscountTerm.Description := "Import Discount Term".Description;
      ImportDiscountTerm."Discount Percentage 1" := "Import Discount Term"."Discount Percentage 1";
      ImportDiscountTerm."Discount Percentage 2" := "Import Discount Term"."Discount Percentage 2";
      ImportDiscountTerm."Discount Percentage 3" := "Import Discount Term"."Discount Percentage 3";
      ImportDiscountTerm."Purchase Price" := "Import Discount Term"."Purchase Price";
      ImportDiscountTerm."Ending Date" := "Import Discount Term"."Ending Date";
      ImportDiscountTerm."Created on" := "Import Discount Term"."Created on";

      ImportDiscountTerm.MODIFY;
    END;

    BEGIN
    {
      CC: Dataport 11012358

      Discount terms Instalnet (ICC 1.0)

      db, 08-05-06: attention, 2ba has extended discount term to 35 positions (pab2.0).
      db, 20-03-09: M14675, save import as 1 long text, because header record has a deviated layout.
      db, 11-06-09: M15677, problem with moving layout when first column is empty; seperate text fields.
    }
    END.
  }
}

