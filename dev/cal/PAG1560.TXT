OBJECT Page 1560 Report Settings
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04;
  }
  PROPERTIES
  {
    AccessByPermission=TableData 2000000196=IMD;
    CaptionML=[ENU=Report Settings;
               NOR=Rapportinnstillinger;
               SVE=Rapportinst„llningar];
    ApplicationArea=#Basic,#Suite;
    InsertAllowed=No;
    SourceTable=Table2000000196;
    PageType=List;
    UsageCategory=Administration;
    PromotedActionCategoriesML=[ENU=New,Process,Report,Manage;
                                NOR=Ny,Prosess,Rapport,Administrer;
                                SVE=Ny,Process,Rapport,Hantera];
    ShowFilter=No;
    OnAfterGetRecord=BEGIN
                       LookupObjectName("Object ID","Object Type");
                     END;

    OnAfterGetCurrRecord=BEGIN
                           LastUsed := "Parameter Name" = LastUsedTxt;
                         END;

    ActionList=ACTIONS
    {
      { 5       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 11      ;1   ;Action    ;
                      Name=NewSettings;
                      CaptionML=[ENU=New;
                                 NOR=Ny;
                                 SVE=Ny];
                      ToolTipML=[ENU="Create a new report settings entry that sets filters and options for a specific report. ";
                                 NOR="Opprett en ny rapportinnstillingspost som angir filtre og alternativer for en bestemt rapport. ";
                                 SVE="Skapa en ny rapportinst„llningsposten som st„ller in filter och alternativ f”r en viss rapport. "];
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      Image=New;
                      PromotedCategory=Category4;
                      PromotedOnly=Yes;
                      OnAction=VAR
                                 ObjectOptions@1001 : Record 2000000196;
                                 PickReport@1000 : Page 1561;
                                 OptionDataTxt@1003 : Text;
                               BEGIN
                                 PickReport.SetReportObjectId("Object ID");
                                 IF PickReport.RUNMODAL <> ACTION::OK THEN
                                   EXIT;

                                 PickReport.GetObjectOptions(ObjectOptions);
                                 OptionDataTxt := REPORT.RUNREQUESTPAGE(ObjectOptions."Object ID",OptionDataTxt);
                                 IF OptionDataTxt <> '' THEN BEGIN
                                   UpdateOptionData(ObjectOptions,OptionDataTxt);
                                   ObjectOptions.INSERT(TRUE);
                                   Rec := ObjectOptions;
                                 END;
                               END;
                                }
      { 14      ;1   ;Action    ;
                      Name=CopySettings;
                      CaptionML=[ENU=Copy;
                                 NOR=Kopier;
                                 SVE=Kopiera];
                      ToolTipML=[ENU=Make a copy the selected report settings.;
                                 NOR=Ta en kopi av de valgte rapportinnstillingene.;
                                 SVE=Skapa en kopia av valda rapportinst„llningar.];
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      Image=Copy;
                      PromotedCategory=Category4;
                      PromotedOnly=Yes;
                      OnAction=VAR
                                 ObjectOptions@1000 : Record 2000000196;
                               BEGIN
                                 IF "Option Data".HASVALUE THEN
                                   CALCFIELDS("Option Data");

                                 ObjectOptions.TRANSFERFIELDS(Rec);
                                 ObjectOptions."Parameter Name" := STRSUBSTNO(CopyTxt,"Parameter Name");
                                 ObjectOptions.INSERT(TRUE);
                               END;
                                }
      { 6       ;1   ;Action    ;
                      Name=EditSettings;
                      CaptionML=[ENU=Edit;
                                 NOR=Rediger;
                                 SVE=Redigera];
                      ToolTipML=[ENU=Change the options and filters that are defined for the selected report settings.;
                                 NOR=Endre alternativene og filtrene som er definert for de valgte rapportinnstillingene.;
                                 SVE=Žndra angivna alternativ och filter f”r valda rapportinst„llningar.];
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      Enabled=NOT LastUsed;
                      Image=Edit;
                      PromotedCategory=Category4;
                      PromotedOnly=Yes;
                      OnAction=VAR
                                 OptionDataTxt@1002 : Text;
                               BEGIN
                                 OptionDataTxt := REPORT.RUNREQUESTPAGE("Object ID",GetOptionData);
                                 IF OptionDataTxt <> '' THEN BEGIN
                                   UpdateOptionData(Rec,OptionDataTxt);
                                   MODIFY(TRUE);
                                 END;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=Group;
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                Name=Name;
                CaptionML=[ENU=Name;
                           NOR=Navn;
                           SVE=Namn];
                ToolTipML=[ENU=Specifies the name of the settings entry.;
                           NOR=Angir navnet p† oppf›ringen for innstilingene.;
                           SVE=Anger namnet p† inst„llningsposten.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Parameter Name" }

    { 4   ;2   ;Field     ;
                Name=Report ID;
                CaptionML=[ENU=Report ID;
                           NOR=Rapport-ID;
                           SVE=Rapport-ID];
                ToolTipML=[ENU=Specifies the ID of the report that uses the settings.;
                           NOR=Angir ID-en for rapporten som bruker innstillingene.;
                           SVE=Anger ID f”r rapporten som anv„nder inst„llningarna.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Object ID";
                MinValue=1;
                TableRelation=IF (Object Type=CONST(Report)) "Report Metadata".ID;
                OnValidate=BEGIN
                             ValidateObjectID;
                             LookupObjectName("Object ID","Object Type");
                           END;
                            }

    { 8   ;2   ;Field     ;
                Name=Report Name;
                CaptionML=[ENU=Report Name;
                           NOR=Rapportnavn;
                           SVE=Rapportnamn];
                ToolTipML=[ENU=Specifies the name of the report that uses the settings.;
                           NOR=Angir navnet p† rapporten som bruker innstillingene.;
                           SVE=Anger namnet p† den rapport som anv„nder inst„llningarna.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=ReportName;
                Editable=FALSE }

    { 7   ;2   ;Field     ;
                CaptionML=[ENU=Assigned to;
                           NOR=Tilordnet til;
                           SVE=Tilldelat till];
                ToolTipML=[ENU=Specifies who can use the report settings. If the field is blank, the settings are available to all users.;
                           NOR=Angir hvem som kan bruke rapportinnstillingene. Hvis feltet er tomt, er innstillingene tilgjengelige for alle brukere.;
                           SVE=Anger vem som kan anv„nda rapportinst„llningarna. Om f„ltet l„mnas tomt „r inst„llningarna tillg„ngliga f”r alla anv„ndare.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="User Name";
                TableRelation=User."User Name";
                Enabled=NOT LastUsed;
                OnValidate=BEGIN
                             IF "User Name" <> '' THEN
                               "Public Visible" := FALSE
                             ELSE
                               "Public Visible" := TRUE;
                           END;
                            }

    { 17  ;2   ;Field     ;
                CaptionML=[ENU=Created by;
                           NOR=Opprettet av;
                           SVE=Skapades av];
                ToolTipML=[ENU=Specifies the name of the user who created the settings.;
                           NOR=Angir navnet p† brukeren som opprettet innstillingene.;
                           SVE=Anger namnet p† den anv„ndare som skapade inst„llningarna.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Created By";
                TableRelation=User."User Name";
                Editable=FALSE }

    { 9   ;2   ;Field     ;
                CaptionML=[ENU=Shared with all users;
                           NOR=Delt med andre brukere;
                           SVE=Delad med alla anv„ndare];
                ToolTipML=[ENU=Specifies whether the report settings are available to all users or only the user assigned to the settings.;
                           NOR=Angir om rapportinnstillingene er tilgjengelige for alle brukere eller bare brukeren tilordnet til innstillingene.;
                           SVE=Anger om rapportinst„llningarna „r tillg„ngliga f”r alla anv„ndare eller bara den anv„ndare som tilldelats inst„llningarna.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Public Visible";
                Enabled=NOT LastUsed;
                OnValidate=BEGIN
                             IF "User Name" = '' THEN
                               "User Name" := "Created By"
                             ELSE
                               "User Name" := '';
                           END;
                            }

    { 10  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the company to which the settings belong.;
                           NOR=Angir selskapet som innstillingen tilh›rer.;
                           SVE=Anger det f”retag som inst„llningarna tillh”r.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Company Name" }

  }
  CODE
  {
    VAR
      CopyTxt@1002 : TextConst '@@@=%1 is the Parameter Name field from the Object Options record;ENU=Copy of %1;NOR=Kopi av %1;SVE=Kopia av %1';
      LastUsedTxt@1003 : TextConst '@@@=Translation must match RequestPageLatestSavedSettingsName from Lang.resx;ENU=Last used options and filters;NOR=Sist brukte alternativer og filtre;SVE=Senast anv„nda alternativ och filter';
      LastUsed@1004 : Boolean;
      ObjectIdValidationErr@1006 : TextConst 'ENU="The specified object ID is not valid; the object must exist in the application.";NOR=Den angitte objekt-ID-en er ikke gyldig. Objektet m† finnes i programmet.;SVE=Angivet objekt-ID „r ogiltigt. Objektet m†ste finnas i programmet.';
      ReportName@1000 : Text;
      EmptyOptionDataErr@1001 : TextConst 'ENU=Option Data is empty.;NOR=Alternativdata er tomt.;SVE=Alternativdata „r tomt.';

    LOCAL PROCEDURE ValidateObjectID@3();
    VAR
      ObjectMetadata@1000 : Record 2000000071;
    BEGIN
      IF NOT ObjectMetadata.GET("Object Type","Object ID") THEN
        ERROR(ObjectIdValidationErr);
    END;

    LOCAL PROCEDURE LookupObjectName@15(ObjectID@1000 : Integer;ObjectType@1002 : Option);
    VAR
      AllObjWithCaption@1001 : Record 2000000058;
    BEGIN
      IF AllObjWithCaption.GET(ObjectType,ObjectID) THEN
        ReportName := AllObjWithCaption."Object Caption"
      ELSE
        ReportName := '';
    END;

    LOCAL PROCEDURE UpdateOptionData@2(VAR ObjectOptions@1000 : Record 2000000196;OptionDataTxt@1002 : Text);
    VAR
      OutStream@1001 : OutStream;
    BEGIN
      IF OptionDataTxt = '' THEN
        ERROR(EmptyOptionDataErr);

      CLEAR(ObjectOptions."Option Data");
      ObjectOptions."Option Data".CREATEOUTSTREAM(OutStream,TEXTENCODING::UTF8);
      OutStream.WRITETEXT(OptionDataTxt);
    END;

    LOCAL PROCEDURE GetOptionData@24() Result : Text;
    VAR
      InStream@1000 : InStream;
    BEGIN
      IF "Option Data".HASVALUE THEN BEGIN
        CALCFIELDS("Option Data");
        "Option Data".CREATEINSTREAM(InStream,TEXTENCODING::UTF8);
        InStream.READTEXT(Result);
      END;
    END;

    BEGIN
    {
      RENAME does not work when primary key contains an option field, in this case "Object Type".
      Therefore DELETE / INSERT is needed as "User Name" is part of the primary key.
    }
    END.
  }
}

