OBJECT Codeunit 5055 CustVendBank-Update
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW113.00,4PS14.00;
  }
  PROPERTIES
  {
    TableNo=5050;
    Permissions=TableData 18=rimd,
                TableData 23=rimd,
                TableData 287=rimd,
                TableData 288=rimd;
    OnRun=VAR
            ChangeBlockedforContactRel@1100528200 : Page 11129700;
            LoopCount@1100528202 : Integer;
          BEGIN
            // 'ContBusRel' is declared as global (instead of) local var //**4PS.n

            ContBusRel.SETRANGE("Contact No.","No.");
            ContBusRel.SETFILTER("Link to Table",'<>''''');

            //**4PS.sn
            IF NOT SecondaryCompany THEN
              CLEAR(BlockAction);
            IF ContactBlockedChanged THEN BEGIN
              IF Blocked THEN BEGIN
                FOR LoopCount := 1 TO 5 DO
                  BlockAction[LoopCount] := TRUE;  //For the time being this is not a users choice.
                                                   //Although the functionality is already present in Page 11129957 ChangeBlockedforContactRel
              END ELSE BEGIN
                IF GUIALLOWED AND (NOT SecondaryCompany) THEN BEGIN
                  IF ContBusRel.FIND('-') THEN BEGIN
                    ChangeBlockedforContactRel.SetParameter(Rec,ContBusRel,BlockAction);
                    ChangeBlockedforContactRel.EDITABLE(TRUE);
                    IF ChangeBlockedforContactRel.RUNMODAL = ACTION::OK THEN
                      ChangeBlockedforContactRel.GetAction(BlockAction)
                    ELSE
                      ERROR(Text11012001);
                  END;
                END;
              END;
            END;
            ContBusRel.RESET;
            ContBusRel.SETRANGE("Contact No.","No.");
            ContBusRel.SETFILTER("Link to Table",'<>''''');
            //**4PS.en
            IF ContBusRel.FIND('-') THEN
              REPEAT
                CASE ContBusRel."Link to Table" OF
                  ContBusRel."Link to Table"::Customer:
                    UpdateCustomer(Rec,ContBusRel);
                  ContBusRel."Link to Table"::Vendor:
                    UpdateVendor(Rec,ContBusRel);
                  ContBusRel."Link to Table"::"Bank Account":
                    UpdateBankAccount(Rec,ContBusRel);
                  //**4PS.sn
                  ContBusRel."Link to Table"::Employee:
                    UpdateEmployee(Rec,ContBusRel);
                  ContBusRel."Link to Table"::Employer:
                    UpdateEmployer(Rec,ContBusRel);
                  //**4PS.en
                END;
              UNTIL ContBusRel.NEXT = 0;
          END;

  }
  CODE
  {
    VAR
      Cust@1001 : Record 18;
      Vend@1002 : Record 23;
      BankAcc@1003 : Record 270;
      NoSeries@1004 : Code[20];
      PurchaserCode@1005 : Code[20];
      OurContactCode@1006 : Code[20];
      SecondaryCompany@1100485006 : Boolean;
      ContactBlockedChanged@1100528200 : Boolean;
      BlockAction@1100528201 : ARRAY [5] OF Boolean;
      Text11012001@1100528202 : TextConst 'ENU=Cancelled changing Blocked.';
      ContBusRel@1100525000 : Record 5054;
      ContBusRel2@1100525001 : Record 5054;
      EmplRec@1100525002 : Record 5200;
      EmployerRec@1100525003 : Record 11012013;

    [External]
    PROCEDURE UpdateCustomer@1(VAR Cont@1000 : Record 5050;VAR ContBusRel@1001 : Record 5054);
    VAR
      VATRegistrationLogMgt@1002 : Codeunit 249;
      VATRegNo@1003 : Text[20];
      ChangeCustomerBankAccountCode@1100525001 : Boolean;
      UseFieldCaption@1100525000 : Text[100];
      CurrencyCode@1100528700 : Code[10];
    BEGIN
      WITH Cust DO BEGIN
        GET(ContBusRel."No.");
        NoSeries := "No. Series";
        VATRegNo := "VAT Registration No.";

        //**4PS.sn
        CurrencyCode := "Currency Code";
        IF (NOT SecondaryCompany) AND
             ((Name <> Cont.Name) OR
              (Address <> Cont.Address) OR
              (City <> Cont.City) OR
              ("Country/Region Code" <> Cont."Country/Region Code") OR
              ("Post Code" <> Cont."Post Code")) THEN
         BEGIN
           ChangeCustomerBankAccountCode := TRUE;
           UseFieldCaption := FIELDCAPTION(Address);
         END ELSE
           ChangeCustomerBankAccountCode := FALSE;
        //**4PS.en

        TRANSFERFIELDS(Cont);
        "No." := ContBusRel."No.";
        "No. Series" := NoSeries;

        //**4PS.sn
        "Currency Code" := CurrencyCode;
        "Mobile Phone No." := Cont."Mobile Phone No.";
        "Phone No. 2" := Cont."Phone No. 2";
        IF BlockAction[1] THEN BEGIN
          IF Cont.Blocked THEN
            VALIDATE(Blocked, Blocked::All)
          ELSE
            VALIDATE(Blocked, Blocked::" ");
        END;
        "COC Registration No." := Cont."COC Registration No.";
        "COC Location No." := Cont."COC Location No.";
        "City COC" := Cont."City COC";
        "Due Date COC Statement" := Cont."Due Date COC Statement";
        //**4PS.en

        OnAfterUpdateCustomer(Cust,Cont);
        MODIFY;
        IF ("VAT Registration No." <> '') AND ("VAT Registration No." <> VATRegNo) THEN
          VATRegistrationLogMgt.LogCustomer(Cust);

        //**4PS.sn
        CheckRelatedAddress;  //db, 18-12-07 (M12524)
        //**4PS.en
      END;
    END;

    [External]
    PROCEDURE UpdateVendor@2(VAR Cont@1000 : Record 5050;VAR ContBusRel@1001 : Record 5054);
    VAR
      VATRegistrationLogMgt@1002 : Codeunit 249;
      VATRegNo@1003 : Text[20];
      ChangeVendorBankAccountCode@1100525000 : Boolean;
      UseFieldCaption@1100525001 : Text[100];
      CurrencyCode@1100528700 : Code[10];
    BEGIN
      WITH Vend DO BEGIN
        GET(ContBusRel."No.");
        NoSeries := "No. Series";
      //PurchaserCode := "Purchaser Code"; //**4PS.o
        VATRegNo := "VAT Registration No.";

        //**4PS.sn
        CurrencyCode := "Currency Code";
        IF (NOT SecondaryCompany) AND
             ((Vend.Name <> Cont.Name) OR
              (Vend.Address <> Cont.Address) OR
              (Vend.City <> Cont.City) OR
              (Vend."Country/Region Code" <> Cont."Country/Region Code") OR
              (Vend."Post Code" <> Cont."Post Code")) THEN
        BEGIN
          ChangeVendorBankAccountCode := TRUE;
          UseFieldCaption := FIELDCAPTION(Address);
        END ELSE
          ChangeVendorBankAccountCode := FALSE;
        //**4PS.en

        TRANSFERFIELDS(Cont);
        "No." := ContBusRel."No.";
        "No. Series" := NoSeries;
        // "Purchaser Code" := PurchaserCode; //**4PS.o
        //**4PS.sn
        "Purchaser Code" := Cont."Purchaser Code";
        "Currency Code" := CurrencyCode;
        "Mobile Phone No." := Cont."Mobile Phone No.";
        IF BlockAction[2] THEN BEGIN
          IF Cont.Blocked THEN
            VALIDATE(Blocked, Blocked::All)
          ELSE
            VALIDATE(Blocked, Blocked::" ");
        END;
        "COC Registration No." := Cont."COC Registration No.";
        "COC Location No." := Cont."COC Location No.";
        "City COC" := Cont."City COC";
        "Due Date COC Statement" := Cont."Due Date COC Statement";
        //**4PS.en

        OnAfterUpdateVendor(Vend,Cont);
        MODIFY;
        IF ("VAT Registration No." <> '') AND ("VAT Registration No." <> VATRegNo) THEN
          VATRegistrationLogMgt.LogVendor(Vend);

      END;
    END;

    [External]
    PROCEDURE UpdateBankAccount@3(VAR Cont@1000 : Record 5050;VAR ContBusRel@1001 : Record 5054);
    VAR
      CurrencyCode@1100528700 : Code[10];
    BEGIN
      WITH BankAcc DO BEGIN
        GET(ContBusRel."No.");
        NoSeries := "No. Series";
        OurContactCode := "Our Contact Code";
        //VALIDATE("Currency Code",Cont."Currency Code"); //**4PS.o
        CurrencyCode := "Currency Code"; //**4PS.n
        TRANSFERFIELDS(Cont);
        "No." := ContBusRel."No.";
        "No. Series" := NoSeries;
        "Our Contact Code" := OurContactCode;
        "Currency Code" := CurrencyCode; //**4PS.n

        //**4PS.sn
        IF BlockAction[3] THEN
          VALIDATE(Blocked, Cont.Blocked);
        //**4PS.en

        OnAfterUpdateBankAccount(BankAcc,Cont);
        MODIFY;
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterUpdateCustomer@4(VAR Customer@1000 : Record 18;Contact@1001 : Record 5050);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterUpdateVendor@5(VAR Vendor@1000 : Record 23;Contact@1001 : Record 5050);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterUpdateBankAccount@6(VAR BankAccount@1000 : Record 270;Contact@1001 : Record 5050);
    BEGIN
    END;

    PROCEDURE SetCompany@1210190001(CompName@1210190001 : Text[50]);
    BEGIN
      //**4PS
      ContBusRel.CHANGECOMPANY(CompName);
      ContBusRel2.CHANGECOMPANY(CompName);
      Cust.CHANGECOMPANY(CompName);
      Vend.CHANGECOMPANY(CompName);
      BankAcc.CHANGECOMPANY(CompName);
      EmplRec.CHANGECOMPANY(CompName);
      EmployerRec.CHANGECOMPANY(CompName);
    END;

    PROCEDURE UpdateEmployer@1100485000(VAR Cont@1100485001 : Record 5050;VAR ContBusRel@1100485000 : Record 5054);
    BEGIN
      //**4PS
      WITH EmployerRec DO BEGIN
        GET(ContBusRel."No.");
        EmployerTransferFields(Cont,EmployerRec);
        Code := ContBusRel."No.";
        MODIFY;
      END;
    END;

    PROCEDURE EmployerTransferFields@1100485003(VAR Cont@1100485001 : Record 5050;VAR Employer@1100485000 : Record 11012013);
    BEGIN
      //**4PS
      WITH EmployerRec DO BEGIN
        Description := COPYSTR(Cont.Name,1,MAXSTRLEN(Description));
      END;
    END;

    PROCEDURE UpdateEmployee@1100485001(VAR Cont@1000 : Record 5050;VAR ContBusRel@1001 : Record 5054);
    BEGIN
      //**4PS
      WITH EmplRec DO BEGIN
        GET(ContBusRel."No.");
        NoSeries := "No. Series";
        EmployeeTransferFields(Cont,EmplRec);
        "No." := ContBusRel."No.";
        "No. Series" := NoSeries;
        MODIFY;
      END;
    END;

    PROCEDURE EmployeeTransferFields@1210190007(VAR Cont@1210190000 : Record 5050;VAR Empl@1210190001 : Record 5200);
    BEGIN
      //**4PS
      WITH Empl DO BEGIN
      // The sequence of the fields below corresponds with the field
      // sequence of the employee table definition.
      // If change, change also function in table 5200.
        "First Name" := Cont."First Name";
        "Middle Name" := Cont."Middle Name";
        "Last Name" := Cont.Surname;
        Initials := Cont.Initials;
        "Search Name" := Cont."Search Name";
        Address := Cont.Address;
        "Address 2" := Cont."Address 2";
        City := Cont.City;
        "Post Code" := Cont."Post Code";
        County := Cont.County;
        "Phone No." := Cont."Phone No.";
        "Mobile Phone No." := Cont."Mobile Phone No.";
        "E-Mail" := Cont."E-Mail";
        "Birth Date" := Cont."Birth Date 1";
        Gender := Cont.Gender;
        "Country/Region Code" := Cont."Country/Region Code";
        "Fax No." := Cont."Fax No.";
        Title := Cont.Title;
        "Salespers./Purch. Code" := Cont."Salesperson Code";
        "Name Abbreviation" := Cont."Name Abbreviation";
        "Title behind the Name":= Cont."Title Behind the Name";
        FillFullName;
      END;
    END;

    PROCEDURE SetIsSecondaryCompany@1100485002(iSecondaryCompany@1100485000 : Boolean);
    BEGIN
      SecondaryCompany := iSecondaryCompany;
    END;

    PROCEDURE SetBlockedChanged@1100528200(iBlockedChanged@1100528200 : Boolean);
    BEGIN
      //**4PS
      ContactBlockedChanged := iBlockedChanged;
    END;

    PROCEDURE SetBlockAction@1100528203(LoopCount@1100528201 : Integer;iBlockAction@1100528200 : Boolean);
    BEGIN
      //**4PS
      BlockAction[LoopCount] := iBlockAction;
    END;

    PROCEDURE GetBlockAction@1100528205(LoopCount@1100528202 : Integer) oBlockAction : Boolean;
    BEGIN
      //4PS
      oBlockAction := BlockAction[LoopCount];
    END;

    BEGIN
    END.
  }
}

