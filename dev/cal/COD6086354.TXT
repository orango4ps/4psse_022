OBJECT Codeunit 6086354 Continia Config. Mgt. - EM
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.3.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      TempGenJnlBatch@1160040025 : TEMPORARY Record 232;
      TempConfigFile@1001 : TEMPORARY Record 6085608;
      TempExpSetup@1160040008 : TEMPORARY Record 6086300;
      TempExpType@1160040006 : TEMPORARY Record 6086307;
      TempExpPostingSetup@1160040026 : TEMPORARY Record 6086309;
      TempBankCurrMap@1160040016 : TEMPORARY Record 6086310;
      TempExpRemTerms@1160040011 : TEMPORARY Record 6086317;
      TempExpRemLevel@1160040010 : TEMPORARY Record 6086318;
      TempExpPostingDescField@1160040000 : TEMPORARY Record 6086333;
      TempVehicle@1160040024 : TEMPORARY Record 6086337;
      TempFieldType@1160040007 : TEMPORARY Record 6086345;
      TempConfFields@1000 : TEMPORARY Record 6086346;
      TempLookupVal@1160040003 : TEMPORARY Record 6086347;
      TempTableFilter@1160040004 : TEMPORARY Record 6086348;
      TempFieldTrans@1160040005 : TEMPORARY Record 6086352;
      TempMileageRateID@1160040022 : TEMPORARY Record 6086366;
      TempMileageRates@1160040023 : TEMPORARY Record 6086370;
      TempContWebPortal@1160040015 : TEMPORARY Record 6192774;
      COConfigMgt@1160040001 : Codeunit 6086502;
      Window@1160040030 : Dialog;
      DialogCount@1160040034 : Integer;
      TotalCount@1160040031 : Integer;
      Direction@1160040018 : 'Import,Export';
      Step@1160040032 : 'From File,From Temp,To Temp,To File';
      NewCompany@1160040020 : Boolean;
      CntTxt@1160040021 : TextConst 'DEU=Anzahl Datens„tze;ENU="No. of Records: ";NLD=Aantal records:';
      EmptyErr@1160040019 : TextConst 'DEU=%1 Darf nicht leer sein.;ENU=%1 Cannot be empty.;NLD=%1 mag niet leeg zijn.;SVE=%1 m†ste anges.';
      PostedDocuments@1160040027 : TextConst 'DEU=Gebuchte %1;ENU=Posted %1;NLD=Geboekt %1';
      Text001@1160040014 : TextConst 'DEU=Konfiguration wird importiert ...\@1@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@;ENU=Importing Setup...\@1@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@;NLD=Importeren instellingen...\@1@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@';
      Text002@1160040013 : TextConst 'DEU=Konfiguration wird festgelegt ...\@1@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@;ENU=Committing setup...\@1@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@;NLD=Bijwerken instellingen...\@1@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@';
      Text003@1160040012 : TextConst 'DEU=Konfiguration wird verarbeitet ...\@1@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@;ENU=Processing Setup...\@1@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@;NLD=Verwerken instellingen...\@1@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@c';
      Text004@1160040002 : TextConst 'DEU=Konfiguration wird exportiert ...\@1@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@;ENU=Exporting Setup...\@1@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@;NLD=Exporteren instellingen...\@1@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@';

    PROCEDURE ShowTree@1160040002(VAR TempConfigLine@1160040002 : TEMPORARY Record 6085612);
    BEGIN
      SetEMFields;

      IF Direction = Direction::Import THEN
        ImportFile(TempConfigLine)
      ELSE
        ExportTemp(TempConfigLine);
    END;

    PROCEDURE PerfomAction@1160040004(VAR TempConfigLine@1160040000 : TEMPORARY Record 6085612);
    BEGIN
      IF Direction = Direction::Import THEN
        ImportIncluded(TempConfigLine)
      ELSE
        ExportIncluded(TempConfigLine);
    END;

    LOCAL PROCEDURE ImportFile@1160040000(VAR TempConfigLine@1160040000 : TEMPORARY Record 6085612);
    VAR
      RecRef@1160040001 : RecordRef;
      EntryNo@1160040003 : Integer;
    BEGIN
      OpenDialog(Step::"From File",13);

      COConfigMgt.CreateXmlDoc(TempConfigFile);

      RecRef.GETTABLE(TempExpSetup);
      ImportTemp(DATABASE::"Expense Management Setup",RecRef);
      RecRef.GETTABLE(TempGenJnlBatch);
      ImportTemp(DATABASE::"Gen. Journal Batch",RecRef);

      RecRef.GETTABLE(TempContWebPortal);
      ImportTemp(DATABASE::"Continia Web Portal",RecRef);

      RecRef.GETTABLE(TempExpType);
      ImportTemp(DATABASE::"Expense Type",RecRef);
      RecRef.GETTABLE(TempExpPostingSetup);
      ImportTemp(DATABASE::"Expense Posting Setup",RecRef);

      RecRef.GETTABLE(TempFieldType);
      ImportTemp(DATABASE::"Field Type",RecRef);
      RecRef.GETTABLE(TempTableFilter);
      ImportTemp(DATABASE::"Table Filter Field EM",RecRef);
      RecRef.GETTABLE(TempConfFields);
      ImportTemp(DATABASE::"Configured Field",RecRef);
      RecRef.GETTABLE(TempFieldTrans);
      ImportTemp(DATABASE::"Field Translation",RecRef);
      RecRef.GETTABLE(TempLookupVal);
      ImportTemp(DATABASE::"Lookup Value",RecRef);

      RecRef.GETTABLE(TempExpRemTerms);
      ImportTemp(DATABASE::"EM Reminder Terms",RecRef);
      RecRef.GETTABLE(TempExpRemLevel);
      ImportTemp(DATABASE::"EM Reminder Level",RecRef);

      RecRef.GETTABLE(TempBankCurrMap);
      ImportTemp(DATABASE::"Bank Currency Map",RecRef);

      RecRef.GETTABLE(TempExpPostingDescField);
      ImportTemp(DATABASE::"Exp. Posting Desc. Field",RecRef);

      RecRef.GETTABLE(TempMileageRateID);
      ImportTemp(DATABASE::"Mileage Rate ID",RecRef);
      RecRef.GETTABLE(TempMileageRates);
      ImportTemp(DATABASE::"Mileage Rate",RecRef);
      RecRef.GETTABLE(TempVehicle);
      ImportTemp(DATABASE::"Vehicle DC",RecRef);

      EntryNo :=
        CreateTree(TempConfigLine,TempExpSetup,TempContWebPortal,TempExpType,TempFieldType,TempConfFields,TempExpRemTerms,
         TempBankCurrMap,TempMileageRateID);

      COConfigMgt.ClearGlobals;

      CloseDialog;
    END;

    LOCAL PROCEDURE ImportTemp@1160040020(TableNo@1160040004 : Integer;RecRef@1160040003 : RecordRef);
    VAR
      i@1160040001 : Integer;
      NoOfRecords@1160040002 : Integer;
    BEGIN
      UpdateDialog;

      RecRef.DELETEALL;

      NoOfRecords := COConfigMgt.GetTableCount(TableNo);
      FOR i := 0 TO NoOfRecords - 1 DO
        COConfigMgt.ImportRecord(TableNo,i,RecRef);
    END;

    LOCAL PROCEDURE ExportTemp@1160040018(VAR TempConfigLine@1160040000 : TEMPORARY Record 6085612);
    VAR
      ExpSetup@1160040003 : Record 6086300;
      ExpType@1160040001 : Record 6086307;
      BankCurrMap@1160040008 : Record 6086310;
      ExpRemTerms@1160040005 : Record 6086317;
      FieldType@1160040002 : Record 6086345;
      ConfFields@1000 : Record 6086346;
      MileageRateID@1160040007 : Record 6086366;
      ContWebPortal@1001 : Record 6192774;
    BEGIN
      OpenDialog(Step::"To Temp",1);
      CreateTree(TempConfigLine,ExpSetup,ContWebPortal,ExpType,FieldType,ConfFields,ExpRemTerms,BankCurrMap,MileageRateID);
      CloseDialog;
    END;

    LOCAL PROCEDURE CreateTree@1160040001(VAR TempConfigLine@1160040000 : TEMPORARY Record 6085612;VAR ExpSetup@1160040009 : Record 6086300;VAR ContWebPortal@1160040005 : Record 6192774;VAR ExpType@1160040001 : Record 6086307;VAR FieldType@1160040008 : Record 6086345;VAR ConfField@1000 : Record 6086346;VAR ExpRemTerms@1160040003 : Record 6086317;VAR BankCurrMap@1160040006 : Record 6086310;VAR MileageRateID@1160040012 : Record 6086366) LastEntryNo : Integer;
    VAR
      RecRef@1160040010 : RecordRef;
      HeaderEntryNo@1160040007 : Integer;
      Include@1160040004 : Boolean;
      RecID@1160040011 : RecordID;
    BEGIN
      CLEAR(RecID);
      LastEntryNo := 1;
      TempConfigLine.RESET;
      TempConfigLine.DELETEALL;

      IF ExpSetup.GET THEN BEGIN
        Include := (Direction = Direction::Export) OR ((Direction = Direction::Import) AND NewCompany);
        RecRef.GETTABLE(ExpSetup);
        InitConfigLine(LastEntryNo,ExpSetup.TABLECAPTION,CntTxt + FORMAT(ExpSetup.COUNT),Include,0,0,0,
          TempConfigLine.Status::"No Children",DATABASE::"Expense Management Setup",ExpSetup."Primary Key",RecRef.RECORDID,
          TempConfigLine,LastEntryNo);
      END;

      IF ContWebPortal.FINDSET THEN BEGIN
        InitConfigLine(
          LastEntryNo,ContWebPortal.TABLECAPTION,CntTxt + FORMAT(ContWebPortal.COUNT),TRUE,0,0,0,TempConfigLine.Status::"Has Children",
          0,'',RecID,TempConfigLine,LastEntryNo);
        HeaderEntryNo := LastEntryNo;
        REPEAT
          RecRef.GETTABLE(ContWebPortal);
          InitConfigLine(LastEntryNo,ContWebPortal.TABLECAPTION,ContWebPortal.Code,TRUE,HeaderEntryNo,1,1,
            TempConfigLine.Status::"No Children",DATABASE::"Continia Web Portal",ContWebPortal.Code,RecRef.RECORDID,TempConfigLine,
          LastEntryNo);
        UNTIL ContWebPortal.NEXT = 0;
      END;

      IF ExpType.FINDSET THEN BEGIN
        InitConfigLine(LastEntryNo,ExpType.TABLECAPTION,CntTxt + FORMAT(ExpType.COUNT),TRUE,0,0,0,TempConfigLine.Status::"Has Children",
          0,'',RecID,TempConfigLine,LastEntryNo);
        HeaderEntryNo := LastEntryNo;
        REPEAT
          RecRef.GETTABLE(ExpType);
          InitConfigLine(LastEntryNo,ExpType.TABLECAPTION,ExpType.Code,TRUE,HeaderEntryNo,1,1,
            TempConfigLine.Status::"No Children",DATABASE::"Expense Type",ExpType.Code,RecRef.RECORDID,TempConfigLine,LastEntryNo);
        UNTIL ExpType.NEXT = 0;
      END;

      IF FieldType.FINDSET THEN BEGIN
        InitConfigLine(LastEntryNo,FieldType.TABLECAPTION,CntTxt + FORMAT(FieldType.COUNT),TRUE,0,0,0,
          TempConfigLine.Status::"Has Children",0,'',RecID,TempConfigLine,LastEntryNo);
        HeaderEntryNo := LastEntryNo;
        REPEAT
          RecRef.GETTABLE(FieldType);
          InitConfigLine(LastEntryNo,FieldType.TABLECAPTION,FieldType.Code,TRUE,HeaderEntryNo,1,1,
            TempConfigLine.Status::"No Children",DATABASE::"Field Type",FieldType.Code,RecRef.RECORDID,TempConfigLine,LastEntryNo);
        UNTIL FieldType.NEXT = 0;
      END;

      IF ConfField.FINDSET THEN BEGIN
        InitConfigLine(LastEntryNo,ConfField.TABLECAPTION,CntTxt + FORMAT(ConfField.COUNT),TRUE,0,0,0,
          TempConfigLine.Status::"Has Children",0,'',RecID,TempConfigLine,LastEntryNo);
        HeaderEntryNo := LastEntryNo;
        REPEAT
          RecRef.GETTABLE(ConfField);
          InitConfigLine(LastEntryNo,ConfField.TABLECAPTION,FORMAT(ConfField.Type) + ' ' + ConfField."Field Code",
          TRUE,HeaderEntryNo,1,1,TempConfigLine.Status::"No Children",DATABASE::"Configured Field",
            ConfField."Field Code",RecRef.RECORDID,TempConfigLine,LastEntryNo);
        UNTIL ConfField.NEXT = 0;
      END;

      IF ExpRemTerms.FINDSET THEN BEGIN
        InitConfigLine(LastEntryNo,ExpRemTerms.TABLECAPTION,CntTxt + FORMAT(ExpRemTerms.COUNT),TRUE,0,0,0,
          TempConfigLine.Status::"Has Children",0,'',RecID,TempConfigLine,LastEntryNo);
        HeaderEntryNo := LastEntryNo;
        REPEAT
          RecRef.GETTABLE(ExpRemTerms);
          InitConfigLine(LastEntryNo,ExpRemTerms.TABLECAPTION,ExpRemTerms.Code,TRUE,HeaderEntryNo,1,1,
            TempConfigLine.Status::"No Children",DATABASE::"EM Reminder Terms",
            ExpRemTerms.Code,RecRef.RECORDID,TempConfigLine,LastEntryNo);
        UNTIL ExpRemTerms.NEXT = 0;
      END;

      IF BankCurrMap.FINDSET THEN BEGIN
        InitConfigLine(LastEntryNo,BankCurrMap.TABLECAPTION,CntTxt + FORMAT(BankCurrMap.COUNT),TRUE,0,0,0,
          TempConfigLine.Status::"Has Children",0,'',RecID,TempConfigLine,LastEntryNo);
        HeaderEntryNo := LastEntryNo;
        REPEAT
          RecRef.GETTABLE(BankCurrMap);
          InitConfigLine(LastEntryNo,BankCurrMap.TABLECAPTION,BankCurrMap."Currency Code (Bank)",TRUE,HeaderEntryNo,
            1,1,TempConfigLine.Status::"No Children",DATABASE::"Bank Currency Map",BankCurrMap."Bank Code",RecRef.RECORDID,
            TempConfigLine,LastEntryNo);
        UNTIL BankCurrMap.NEXT = 0;
      END;

      IF MileageRateID.FINDSET THEN BEGIN
        InitConfigLine(LastEntryNo,MileageRateID.TABLECAPTION,CntTxt + FORMAT(MileageRateID.COUNT),TRUE,0,0,0,
          TempConfigLine.Status::"Has Children",0,'',RecID,TempConfigLine,LastEntryNo);
        HeaderEntryNo := LastEntryNo;
        REPEAT
          RecRef.GETTABLE(MileageRateID);
          InitConfigLine(LastEntryNo,MileageRateID.TABLECAPTION,MileageRateID."Rate ID",TRUE,HeaderEntryNo,
            1,1,TempConfigLine.Status::"No Children",DATABASE::"Mileage Rate ID",MileageRateID."Rate ID",RecRef.RECORDID,
            TempConfigLine,LastEntryNo);
        UNTIL MileageRateID.NEXT = 0;
      END;

      IF Direction = Direction::Import THEN
        ExcludeExistingRecords(TempConfigLine);
    END;

    LOCAL PROCEDURE InitConfigLine@1160040030(LastEntryNo@1160040002 : Integer;TblName@1160040003 : Text[50];RecordName@1160040004 : Text[50];Include@1160040005 : Boolean;SourceEntryNo@1160040006 : Integer;Indentation@1160040007 : Integer;Level@1160040008 : Integer;Status@1160040009 : 'Has Children,Expanded,No Children';TableNo@1160040010 : Integer;Code@1160040011 : Code[20];RecordID@1160040012 : RecordID;VAR TempConfigLine@1160040000 : TEMPORARY Record 6085612;VAR CurrEntryNo@1160040001 : Integer);
    BEGIN
      CurrEntryNo := LastEntryNo + 1;

      TempConfigLine.INIT;
      TempConfigLine."Entry No." := CurrEntryNo;
      TempConfigLine."Table Name" := TblName;
      TempConfigLine."Record Name" := RecordName;
      TempConfigLine.Include := Include;
      TempConfigLine."Source Entry No." := SourceEntryNo;
      TempConfigLine.Indentation := Indentation;
      TempConfigLine.Level := Level;
      TempConfigLine.Status := Status;
      TempConfigLine."Table No" := TableNo;
      TempConfigLine.Code := Code;
      TempConfigLine."Record ID" := RecordID;
      TempConfigLine.INSERT;
    END;

    LOCAL PROCEDURE ImportIncluded@1160040005(VAR TempConfigLine@1160040000 : TEMPORARY Record 6085612);
    VAR
      GenJnlBatch@1160040013 : Record 232;
      ExpenseType@1160040014 : Record 6086307;
      ExpRemTerm@1160040007 : Record 6086317;
      ExpRemLevel@1160040008 : Record 6086318;
      FieldType@1160040006 : Record 6086345;
      LookupValues@1160040003 : Record 6086347;
      TableFilter@1160040004 : Record 6086348;
      FieldTransl@1160040005 : Record 6086352;
      MileageRateID@1160040009 : Record 6086366;
      MileageRates@1160040010 : Record 6086370;
      EmailTmplImpExp@1160040002 : Codeunit 6086332;
      EMApprovalsBridge@1160040011 : Codeunit 6086369;
      ToRecRef@1160040012 : RecordRef;
    BEGIN
      TempConfigLine.RESET;
      TempConfigLine.SETRANGE(Include,TRUE);
      TempConfigLine.SETFILTER("Table No",'<>%1',0);
      IF TempConfigLine.FINDSET THEN BEGIN
        OpenDialog(Step::"From Temp",TempConfigLine.COUNT);

        REPEAT
          UpdateDialog;

          ImportIncludedTable(TempConfigLine,ToRecRef);

          // IMPORT ASSOCIATED SUB - TABLES:

          CASE TempConfigLine."Table No" OF

            DATABASE::"Expense Management Setup":
              BEGIN
                ImportSubTable(DATABASE::"Exp. Posting Desc. Field",0,'',0,'');
                ImportSubTable(DATABASE::"Gen. Journal Batch",GenJnlBatch.FIELDNO(Name),'EXPENSE',0,'');

                EmailTmplImpExp.GetExpEmailTemplates(TempConfigFile);
                CreateEMSetupDefaultData;
              END;

            DATABASE::"Expense Type":
              BEGIN
                ToRecRef.SETTABLE(ExpenseType);
                IF ExpenseType.ISEMPTY THEN
                  ERROR(EmptyErr,ExpenseType.TABLECAPTION);

                ImportSubTable(DATABASE::"Expense Posting Setup",TempExpPostingSetup.FIELDNO("No."),ExpenseType.Code,0,'');
              END;

            DATABASE::"Field Type":
              BEGIN
                ToRecRef.SETTABLE(FieldType);

                IF FieldType.ISEMPTY THEN
                  ERROR(EmptyErr,FieldType.TABLECAPTION);

                ImportSubTable(DATABASE::"Table Filter Field EM",TableFilter.FIELDNO("Table Filter GUID"),
                  FieldType."Source Table Filter GUID",0,'');
                ImportSubTable(DATABASE::"Field Translation",FieldTransl.FIELDNO("Field Type Code"),FieldType.Code,0,'');
                IF FieldType."Manual Lookup Values" THEN
                  ImportSubTable(DATABASE::"Lookup Value",LookupValues.FIELDNO("Field Type Code"),FieldType.Code,0,'');
              END;

            DATABASE::"EM Reminder Terms":
              BEGIN
                ToRecRef.SETTABLE(ExpRemTerm);

                IF ExpRemTerm.ISEMPTY THEN
                  ERROR(EmptyErr,ExpRemTerm.TABLECAPTION);

                ImportSubTable(DATABASE::"EM Reminder Level",ExpRemLevel.FIELDNO("Reminder Terms Code"),ExpRemTerm.Code,0,'');
              END;

            DATABASE::"Mileage Rate ID":
              BEGIN
                ToRecRef.SETTABLE(MileageRateID);

                IF MileageRateID.ISEMPTY THEN
                  ERROR(EmptyErr,MileageRateID.TABLECAPTION);

                ImportSubTable(DATABASE::"Mileage Rate",MileageRates.FIELDNO("Rate ID"),MileageRateID."Rate ID",0,'');
              END;
          END;

          ToRecRef.CLOSE;

        UNTIL TempConfigLine.NEXT = 0;

        ImportSubTable(DATABASE::"Vehicle DC",0,'',0,'');

        CloseDialog;
      END;

      CleanMissingCodes;

      CODEUNIT.RUN(CODEUNIT::"Create EM Roles");
      EMApprovalsBridge.CreateEMApprovalTemplate;

      TempConfigLine.RESET;
      TempConfigLine.SETRANGE(Include,FALSE);
      TempConfigLine.DELETEALL;
      TempConfigLine.RESET;
    END;

    LOCAL PROCEDURE ImportSubTable@1160040013(TableNo@1160040000 : Integer;Field1@1160040005 : Integer;Filter1@1160040004 : Text[100];Field2@1160040003 : Integer;Filter2@1160040002 : Text[100]);
    VAR
      FromRecRef@1160040001 : RecordRef;
      ToRecRef@1160040006 : RecordRef;
    BEGIN
      CASE TableNo OF
        DATABASE::"Expense Management Setup":
          FromRecRef.GETTABLE(TempExpSetup);
        DATABASE::"Gen. Journal Batch":
          FromRecRef.GETTABLE(TempGenJnlBatch);

        DATABASE::"Expense Type":
          FromRecRef.GETTABLE(TempExpType);
        DATABASE::"Expense Posting Setup":
          FromRecRef.GETTABLE(TempExpPostingSetup);

        DATABASE::"Field Type":
          FromRecRef.GETTABLE(TempFieldType);
        DATABASE::"Configured Field":
          FromRecRef.GETTABLE(TempConfFields);
        DATABASE::"Table Filter Field EM":
          FromRecRef.GETTABLE(TempTableFilter);
        DATABASE::"Field Translation":
          FromRecRef.GETTABLE(TempFieldTrans);
        DATABASE::"Lookup Value":
          FromRecRef.GETTABLE(TempLookupVal);

        DATABASE::"EM Reminder Level":
          FromRecRef.GETTABLE(TempExpRemLevel);
        DATABASE::"EM Reminder Terms":
          FromRecRef.GETTABLE(TempExpRemTerms);

        DATABASE::"Bank Currency Map":
          FromRecRef.GETTABLE(TempBankCurrMap);

        DATABASE::"Exp. Posting Desc. Field":
          FromRecRef.GETTABLE(TempExpPostingDescField);

        DATABASE::"Mileage Rate ID":
          FromRecRef.GETTABLE(TempMileageRateID);
        DATABASE::"Mileage Rate":
          FromRecRef.GETTABLE(TempMileageRates);
        DATABASE::"Vehicle DC":
          FromRecRef.GETTABLE(TempVehicle);
      END;

      IF Field1 > 0 THEN
        FilterRecRef(FromRecRef,Field1,Filter1);

      IF Field2 > 0 THEN
        FilterRecRef(FromRecRef,Field2,Filter2);

      IF FromRecRef.FINDSET THEN BEGIN
        ToRecRef.OPEN(TableNo);
        REPEAT
          //DELETE THE ENTRY.THE USER HAS SELECTED THAT HE WANT TO OVERWRITE
          IF ToRecRef.GET(FromRecRef.RECORDID) THEN
            ToRecRef.DELETE(TRUE);

          ToRecRef.INIT;
          CopyRecRef(FromRecRef,ToRecRef);
          ToRecRef.INSERT(TRUE);
        UNTIL FromRecRef.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE ImportIncludedTable@1160040003(TempConfigLine@1160040000 : TEMPORARY Record 6085612;VAR ToRecRef@1160040002 : RecordRef);
    VAR
      FromRecRef@1160040001 : RecordRef;
    BEGIN
      CASE TempConfigLine."Table No" OF
        DATABASE::"Expense Management Setup":
          FromRecRef.GETTABLE(TempExpSetup);
        DATABASE::"Expense Type":
          FromRecRef.GETTABLE(TempExpType);

        DATABASE::"Continia Web Portal":
          FromRecRef.GETTABLE(TempContWebPortal);

        DATABASE::"Field Type":
          FromRecRef.GETTABLE(TempFieldType);
        DATABASE::"Configured Field":
          FromRecRef.GETTABLE(TempConfFields);
        DATABASE::"Table Filter Field EM":
          FromRecRef.GETTABLE(TempTableFilter);
        DATABASE::"Field Translation":
          FromRecRef.GETTABLE(TempFieldTrans);
        DATABASE::"Lookup Value":
          FromRecRef.GETTABLE(TempLookupVal);

        DATABASE::"EM Reminder Terms":
          FromRecRef.GETTABLE(TempExpRemTerms);
        DATABASE::"EM Reminder Level":
          FromRecRef.GETTABLE(TempExpRemLevel);

        DATABASE::"Bank Currency Map":
          FromRecRef.GETTABLE(TempBankCurrMap);

        DATABASE::"Mileage Rate ID":
          FromRecRef.GETTABLE(TempMileageRateID);
        DATABASE::"Mileage Rate":
          FromRecRef.GETTABLE(TempMileageRates);
      END;

      FromRecRef.GET(TempConfigLine."Record ID");

      //DELETE THE ENTRY.THE USER HAS SELECTED THAT HE WANT TO OVERWRITE
      CLEAR(ToRecRef);
      ToRecRef.OPEN(TempConfigLine."Table No");
      IF ToRecRef.GET(TempConfigLine."Record ID") THEN
        ToRecRef.DELETE(TRUE);
      ToRecRef.CLOSE;

      FromRecRef.GET(TempConfigLine."Record ID");
      ToRecRef.OPEN(TempConfigLine."Table No");
      CopyRecRef(FromRecRef,ToRecRef);
      ToRecRef.INSERT(TRUE);
    END;

    LOCAL PROCEDURE ExportIncluded@1160040006(VAR TempConfigLine@1160040002 : TEMPORARY Record 6085612);
    VAR
      GenJnlBatch@1160040013 : Record 232;
      ExpenseType@1160040014 : Record 6086307;
      ExpRemTerm@1160040007 : Record 6086317;
      ExpRemLevel@1160040006 : Record 6086318;
      Vehicle@1160040012 : Record 6086337;
      FieldType@1160040008 : Record 6086345;
      LookupValues@1160040001 : Record 6086347;
      TableFilter@1160040000 : Record 6086348;
      FieldTransl@1160040003 : Record 6086352;
      MileageRateID@1160040009 : Record 6086366;
      MileageRates@1160040010 : Record 6086370;
      EmailTmplImpExp@1160040005 : Codeunit 6086332;
      XmlNode@1160040015 : Codeunit 6192801;
      RecRef@1160040011 : RecordRef;
      ExpenseSetupHandled@1160040004 : Boolean;
    BEGIN
      TempConfigLine.RESET;
      TempConfigLine.SETRANGE(Include,TRUE);
      TempConfigLine.SETFILTER("Table No",'<>%1',0);
      IF TempConfigLine.FINDSET THEN BEGIN
        COConfigMgt.CreateXmlNode(XmlNode);
        OpenDialog(Step::"To File",TempConfigLine.COUNT);

        REPEAT
          UpdateDialog;

          RecRef.GET(TempConfigLine."Record ID");
          COConfigMgt.ExportRecord(RecRef.NUMBER,RecRef,XmlNode);

          CASE TempConfigLine."Table No" OF

            // EXPORT ASSOCIATED SUB - TABLES
            DATABASE::"Expense Management Setup":
              BEGIN
                ExportTable(XmlNode,DATABASE::"Exp. Posting Desc. Field",0,'',0,'');
                ExportTable(XmlNode,DATABASE::"Gen. Journal Batch",GenJnlBatch.FIELDNO(Name),'EXPENSE',0,'');

                ExpenseSetupHandled := TRUE;
              END;

            DATABASE::"Expense Type":
              BEGIN
                RecRef.SETTABLE(ExpenseType);
                IF ExpenseType.ISEMPTY THEN
                  ERROR(EmptyErr,ExpenseType.TABLECAPTION);

                ExportTable(XmlNode,DATABASE::"Expense Posting Setup",TempExpPostingSetup.FIELDNO("No."),ExpenseType.Code,0,'');
              END;

            DATABASE::"Field Type":
              BEGIN
                RecRef.SETTABLE(FieldType);
                IF FieldType.ISEMPTY THEN
                  ERROR(EmptyErr,FieldType.TABLECAPTION);
                ExportTable(XmlNode,DATABASE::"Table Filter Field EM",TableFilter.FIELDNO("Table Filter GUID"),
                  FieldType."Source Table Filter GUID",0,'');
                ExportTable(XmlNode,DATABASE::"Field Translation",FieldTransl.FIELDNO("Field Type Code"),FieldType.Code,0,'');
                IF FieldType."Manual Lookup Values" THEN
                  ExportTable(XmlNode,DATABASE::"Lookup Value",LookupValues.FIELDNO("Field Type Code"),FieldType.Code,0,'');
              END;

            DATABASE::"EM Reminder Terms":
              BEGIN
                RecRef.SETTABLE(ExpRemTerm);
                IF ExpRemTerm.ISEMPTY THEN
                  ERROR(EmptyErr,ExpRemTerm.TABLECAPTION);

                ExportTable(XmlNode,DATABASE::"EM Reminder Level",ExpRemLevel.FIELDNO("Reminder Terms Code"),ExpRemTerm.Code,0,'');
              END;

            DATABASE::"Mileage Rate ID":
              BEGIN
                RecRef.SETTABLE(MileageRateID);
                IF MileageRateID.ISEMPTY THEN
                  ERROR(EmptyErr,MileageRateID.TABLECAPTION);

                ExportTable(XmlNode,DATABASE::"Mileage Rate",MileageRates.FIELDNO("Rate ID"),MileageRateID."Rate ID",0,'');
              END;

          END;
        UNTIL TempConfigLine.NEXT = 0;

        IF NOT Vehicle.ISEMPTY THEN
          ExportTable(XmlNode,DATABASE::"Vehicle DC",0,'',0,'');

        IF ExpenseSetupHandled THEN
          EmailTmplImpExp.AddExpEmailTemplates(XmlNode);

        COConfigMgt.SaveXmlDoc(XmlNode,TempConfigFile.GetFullFileName);
        COConfigMgt.GetXmlFile(TempConfigFile);

        CloseDialog;
      END;

      TempConfigLine.SETRANGE(Include,FALSE);
      TempConfigLine.DELETEALL;
      TempConfigLine.RESET;
    END;

    LOCAL PROCEDURE ExportTable@1160040010(VAR XmlNode@1160040004 : Codeunit 6192801;TableNo@1160040000 : Integer;Field1@1160040002 : Integer;Filter1@1160040003 : Text[100];Field2@1160040006 : Integer;Filter2@1160040005 : Text[100]);
    VAR
      RecRef@1160040001 : RecordRef;
    BEGIN
      RecRef.OPEN(TableNo);

      IF Field1 > 0 THEN
        FilterRecRef(RecRef,Field1,Filter1);

      IF Field2 > 0 THEN
        FilterRecRef(RecRef,Field2,Filter2);

      IF RecRef.FINDSET THEN
        REPEAT
          COConfigMgt.ExportRecord(RecRef.NUMBER,RecRef,XmlNode);
        UNTIL RecRef.NEXT = 0;
    END;

    LOCAL PROCEDURE CopyRecRef@1160040008(VAR FromRecRef@1160040000 : RecordRef;VAR ToRecRef@1160040001 : RecordRef);
    VAR
      Field@1160040002 : Record 2000000041;
      FromFieldRef@1160040003 : FieldRef;
      ToFieldRef@1160040004 : FieldRef;
    BEGIN
      Field.RESET;
      Field.SETRANGE(TableNo,FromRecRef.NUMBER);
      Field.SETRANGE(Enabled,TRUE);
      Field.SETRANGE(Class,Field.Class::Normal);
      Field.SETFILTER(Type,'<>%1',Field.Type::BLOB);
      IF Field.FINDSET THEN
        REPEAT
          FromFieldRef := FromRecRef.FIELD(Field."No.");
          ToFieldRef := ToRecRef.FIELD(Field."No.");
          ToFieldRef.VALUE := FromFieldRef.VALUE;
        UNTIL Field.NEXT = 0;
    END;

    LOCAL PROCEDURE FilterRecRef@1160040009(VAR RecRef@1160040000 : RecordRef;FldNo@1160040001 : Integer;FieldFilter@1160040002 : Text[100]);
    VAR
      FieldRef@1160040003 : FieldRef;
    BEGIN
      FieldRef := RecRef.FIELD(FldNo);
      FieldRef.SETFILTER(FieldFilter);
    END;

    LOCAL PROCEDURE SetEMFields@1160040007();
    BEGIN
      COConfigMgt.AddExcludeField(DATABASE::"Field Type",TempFieldType.FIELDNO("Last Update Date/Time"));
      COConfigMgt.AddExcludeField(DATABASE::"Configured Field",TempConfFields.FIELDNO("Sent to Continia Online"));
      COConfigMgt.AddExcludeField(DATABASE::"Gen. Journal Batch",11);
      COConfigMgt.AddExcludeField(DATABASE::"Gen. Journal Batch",12);
    END;

    PROCEDURE SetParam@1160040011(NewDirection@1160040002 : 'Import,Export';VAR NewTempFile@1160040001 : TEMPORARY Record 6085608;IsNewCompany@1160040000 : Boolean);
    BEGIN
      Direction := NewDirection;
      TempConfigFile := NewTempFile;
      NewCompany := IsNewCompany;
    END;

    LOCAL PROCEDURE ExcludeExistingRecords@38(VAR TempConfigLine@1000 : TEMPORARY Record 6085612);
    VAR
      TempConfigLine2@1002 : TEMPORARY Record 6085612;
      RecRef@1001 : RecordRef;
      SubLevelCnt@1160040000 : Integer;
    BEGIN
      TempConfigLine.RESET;
      TempConfigLine.SETFILTER("Table No",'<>%1',0);
      IF TempConfigLine.FINDSET(TRUE,FALSE) THEN
        REPEAT
          RecRef.OPEN(TempConfigLine."Table No");
          IF RecRef.GET(TempConfigLine."Record ID") THEN
            IF NOT ((TempConfigLine."Table No" = DATABASE::"Expense Management Setup") AND NewCompany) THEN BEGIN
              TempConfigLine.Include := FALSE;
              TempConfigLine.MODIFY;
            END;

          RecRef.CLOSE;
        UNTIL TempConfigLine.NEXT = 0;

      TempConfigLine.RESET;
      IF TempConfigLine.FINDSET THEN
        REPEAT
          TempConfigLine2 := TempConfigLine;
          TempConfigLine2.INSERT;
        UNTIL TempConfigLine.NEXT = 0;

      TempConfigLine.RESET;
      TempConfigLine.SETRANGE(Level,0);
      IF TempConfigLine.FINDSET(TRUE,FALSE) THEN
        REPEAT
          TempConfigLine2.RESET;
          TempConfigLine2.SETRANGE("Source Entry No.",TempConfigLine."Entry No.");
          SubLevelCnt := TempConfigLine2.COUNT;
          TempConfigLine2.SETRANGE(Include,TRUE);
          IF SubLevelCnt > 0 THEN
            IF TempConfigLine2.ISEMPTY THEN
              TempConfigLine.Include := FALSE
            ELSE
              TempConfigLine.Include := TRUE;
          TempConfigLine.MODIFY;
        UNTIL TempConfigLine.NEXT = 0;

      TempConfigLine2.RESET;
      TempConfigLine2.DELETEALL;
    END;

    LOCAL PROCEDURE OpenDialog@1160040016(NewStep@1160040000 : 'From File,From Temp,To Temp,To File';NewTotalCount@1160040001 : Integer);
    BEGIN
      IF NOT GUIALLOWED THEN
        EXIT;

      TotalCount := NewTotalCount;
      DialogCount := 0;

      CASE NewStep OF
        Step::"From File":
          Window.OPEN(Text001);
        Step::"From Temp":
          Window.OPEN(Text002);
        Step::"To Temp":
          Window.OPEN(Text003);
        Step::"To File":
          Window.OPEN(Text004);
      END;
    END;

    LOCAL PROCEDURE CloseDialog@1160040017();
    BEGIN
      IF GUIALLOWED THEN
        Window.CLOSE;
    END;

    LOCAL PROCEDURE UpdateDialog@1160040019();
    BEGIN
      IF NOT GUIALLOWED THEN
        EXIT;

      DialogCount += 1;
      Window.UPDATE(1,ROUND(DialogCount / TotalCount * 10000,1,'>'));
    END;

    LOCAL PROCEDURE CleanMissingCodes@1160040012();
    VAR
      FieldType@1160040000 : Record 6086345;
      ConfFields@1160040001 : Record 6086346;
    BEGIN
      EXIT;
      ConfFields.RESET;
      IF ConfFields.FINDSET THEN
        REPEAT
          IF NOT FieldType.GET(ConfFields."Field Code") THEN
            ConfFields.DELETE;
        UNTIL ConfFields.NEXT = 0;
    END;

    LOCAL PROCEDURE CreateEMSetupDefaultData@1160040040();
    VAR
      NoSeries@1160040001 : Record 308;
      NoSeriesLine@1160040000 : Record 309;
      EMSetup@1160040002 : Record 6086300;
      Expense@1160040005 : Record 6086320;
      Mileage@1160040004 : Record 6086338;
      Settlement@1160040003 : Record 6086339;
    BEGIN
      EMSetup.GET;
      IF NOT NoSeries.GET(EMSetup."Settlement Nos.") THEN BEGIN
        NoSeries.Code := EMSetup."Settlement Nos.";
        NoSeries.Description := Settlement.GetTableCaptionPlural;
        NoSeries."Default Nos." := TRUE;
        NoSeries.INSERT;

        NoSeriesLine."Series Code" := NoSeries.Code;
        NoSeriesLine."Line No." := 10000;
        NoSeriesLine."Starting No." := 'EM-STL-10000';
        NoSeriesLine."Increment-by No." := 1;
        NoSeriesLine.INSERT(TRUE);
      END;
      IF NOT NoSeries.GET(EMSetup."Settlement Posted Nos.") THEN BEGIN
        NoSeries.Code := EMSetup."Settlement Posted Nos.";
        NoSeries.Description := STRSUBSTNO(PostedDocuments,Settlement.GetTableCaptionPlural);
        NoSeries."Default Nos." := TRUE;
        NoSeries.INSERT;

        NoSeriesLine."Series Code" := NoSeries.Code;
        NoSeriesLine."Line No." := 10000;
        NoSeriesLine."Starting No." := 'EM-PSTL-10000';
        NoSeriesLine."Increment-by No." := 1;
        NoSeriesLine.INSERT(TRUE);
      END;
      IF NOT NoSeries.GET(EMSetup."Expense Posted Nos.") THEN BEGIN
        NoSeries.Code := EMSetup."Expense Posted Nos.";
        NoSeries.Description := STRSUBSTNO(PostedDocuments,Expense.GetTableCaptionPlural);
        NoSeries."Default Nos." := TRUE;
        NoSeries.INSERT;

        NoSeriesLine."Series Code" := NoSeries.Code;
        NoSeriesLine."Line No." := 10000;
        NoSeriesLine."Starting No." := 'EM-PEXP-10000';
        NoSeriesLine."Increment-by No." := 1;
        NoSeriesLine.INSERT(TRUE);
      END;
      IF NOT NoSeries.GET(EMSetup."Mileage Posted Nos.") THEN BEGIN
        NoSeries.Code := EMSetup."Mileage Posted Nos.";
        NoSeries.Description := STRSUBSTNO(PostedDocuments,Mileage.GetTableCaptionPlural);
        NoSeries."Default Nos." := TRUE;
        NoSeries.INSERT;

        NoSeriesLine."Series Code" := NoSeries.Code;
        NoSeriesLine."Line No." := 10000;
        NoSeriesLine."Starting No." := 'EM-PMIL-10000';
        NoSeriesLine."Increment-by No." := 1;
        NoSeriesLine.INSERT(TRUE);
      END;
    END;

    PROCEDURE GetXmlFile@4(VAR TempXmlFile@1000 : TEMPORARY Record 6085608);
    BEGIN
      TempXmlFile := TempConfigFile;
    END;

    BEGIN
    END.
  }
}

