OBJECT Codeunit 442 Sales-Post Prepayments
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.05,4PS14.00,4PSFI,PE5.00;
  }
  PROPERTIES
  {
    TableNo=36;
    Permissions=TableData 37=imd,
                TableData 49=imd,
                TableData 112=imd,
                TableData 113=imd,
                TableData 114=imd,
                TableData 115=imd,
                TableData 252=imd;
    OnRun=BEGIN
            Execute(Rec);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=is not within your range of allowed posting dates;NOR=er ikke innenfor tillatte bokf›ringsdatoer;SVE=„r inte i ditt till†tna intervall f”r bokf”ringsdatum';
      GLSetup@1024 : Record 98;
      SalesSetup@1011 : Record 311;
      GenPostingSetup@1002 : Record 252;
      Text001@1003 : TextConst 'ENU=There is nothing to post.;NOR=Det finnes ingenting † bokf›re.;SVE=Det finns inget att bokf”ra.';
      Text002@1006 : TextConst 'ENU=Posting Prepayment Lines   #2######\;NOR=Bokf›rer forskuddslinjer   #2######\;SVE=Bokf”r f”rskottsbet.rader  #2######\';
      Text003@1004 : TextConst 'ENU=%1 %2 -> Invoice %3;NOR=%1 %2 -> Faktura %3;SVE=%1 %2 -> Faktura %3';
      Text004@1005 : TextConst 'ENU=Posting sales and VAT      #3######\;NOR=Bokf›rer salg og mva.      #3######\;SVE=Bokf. f”rs. och moms       #3######\';
      Text005@1007 : TextConst 'ENU=Posting to customers       #4######\;NOR=Bokf›rer til kunder        #4######\;SVE=Bokf”ring till kunder      #4######\';
      Text006@1022 : TextConst 'ENU=Posting to bal. account    #5######;NOR=Bokf›rer til motkonto      #5######;SVE=Bokf”ring till motkonto    #5######';
      TempGlobalPrepmtInvLineBuf@1025 : TEMPORARY Record 461;
      ErrorMessageMgt@1009 : Codeunit 28;
      GenJnlPostLine@1013 : Codeunit 12;
      Text011@1001 : TextConst 'ENU=%1 %2 -> Credit Memo %3;NOR=%1 %2 -> Kreditnota %3;SVE=%1 %2 -> Kreditnota %3';
      Text012@1015 : TextConst 'ENU=Prepayment %1, %2 %3.;NOR=Forskuddsbetaling %1, %2 %3.;SVE=F”rskottsbetalning %1, %2 %3.';
      Text013@1016 : TextConst 'ENU=It is not possible to assign a prepayment amount of %1 to the sales lines.;NOR=Det er ikke mulig † tilordne et forskuddsbel›p p† %1 til salgslinjene.;SVE=Det g†r inte att tilldela en f”rskottsbetalning om %1 till f”rs„ljningsraderna.';
      Text014@1018 : TextConst 'ENU=VAT Amount;NOR=Mva-bel›p;SVE=Momsbelopp';
      Text015@1017 : TextConst 'ENU=%1% VAT;NOR=%1% Mva.;SVE=%1% moms';
      Text016@1019 : TextConst 'ENU=The new prepayment amount must be between %1 and %2.;NOR=Det nye forskuddsbel›pet m† v‘re mellom %1 og %2.;SVE=Den nya f”rskottsbetalningen m†ste vara mellan %1 och %2.';
      Text017@1020 : TextConst 'ENU=At least one line must have %1 > 0 to distribute prepayment amount.;NOR=Minst ‚n linje m† ha %1 > 0 for † fordele forskuddsbel›p.;SVE=%1 m†ste vara > 0 p† minst en rad f”r att f”rskottsbetalningen ska kunna f”rdelas.';
      Text018@1021 : TextConst 'ENU=must be positive when %1 is not 0;NOR=m† v‘re positiv n†r %1 ikke er 0;SVE=m†ste vara positivt n„r %1 „r skilt fr†n 0.';
      Text019@1014 : TextConst 'ENU=Invoice,Credit Memo;NOR=Faktura,Kreditnota;SVE=Faktura,Kreditnota';
      PrepmtDocumentType@1008 : ',,Invoice,Credit Memo';
      SuppressCommit@1012 : Boolean;
      PreviewMode@1010 : Boolean;

    [External]
    PROCEDURE SetDocumentType@28(DocumentType@1000 : ',,Invoice,Credit Memo');
    BEGIN
      PrepmtDocumentType := DocumentType;
    END;

    LOCAL PROCEDURE Execute@30(VAR SalesHeader@1000 : Record 36);
    BEGIN
      CASE PrepmtDocumentType OF
        PrepmtDocumentType::Invoice:
          Invoice(SalesHeader);
        PrepmtDocumentType::"Credit Memo":
          CreditMemo(SalesHeader);
      END;
    END;

    [External]
    PROCEDURE Invoice@9(VAR SalesHeader@1000 : Record 36);
    VAR
      Handled@1001 : Boolean;
    BEGIN
      OnBeforeInvoice(SalesHeader,Handled);
      IF NOT Handled THEN
        Code(SalesHeader,0);
    END;

    [External]
    PROCEDURE CreditMemo@10(VAR SalesHeader@1000 : Record 36);
    VAR
      Handled@1001 : Boolean;
    BEGIN
      OnBeforeCreditMemo(SalesHeader,Handled);
      IF NOT Handled THEN
        Code(SalesHeader,1);
    END;

    LOCAL PROCEDURE Code@2(VAR SalesHeader2@1013 : Record 36;DocumentType@1022 : 'Invoice,Credit Memo');
    VAR
      SourceCodeSetup@1027 : Record 242;
      SalesHeader@1000 : Record 36;
      SalesLine@1001 : Record 37;
      SalesInvHeader@1002 : Record 112;
      SalesCrMemoHeader@1023 : Record 114;
      TempPrepmtInvLineBuffer@1021 : TEMPORARY Record 461;
      TotalPrepmtInvLineBuffer@1019 : Record 461;
      TotalPrepmtInvLineBufferLCY@1020 : Record 461;
      GenJnlLine@1012 : Record 81;
      TempVATAmountLine@1000000002 : TEMPORARY Record 290;
      TempVATAmountLineDeduct@1015 : TEMPORARY Record 290;
      CustLedgEntry@1031 : Record 21;
      TempSalesLines@1032 : TEMPORARY Record 37;
      GenJnlPostPreview@1009 : Codeunit 19;
      Window@1000000003 : Dialog;
      GenJnlLineDocNo@1006 : Code[20];
      GenJnlLineExtDocNo@1005 : Code[35];
      SrcCode@1004 : Code[10];
      PostingNoSeriesCode@1007 : Code[20];
      ModifyHeader@1008 : Boolean;
      CalcPmtDiscOnCrMemos@1011 : Boolean;
      PostingDescription@1028 : Text[100];
      GenJnlLineDocType@1003 : Integer;
      PrevLineNo@1016 : Integer;
      LineCount@1000000004 : Integer;
      PostedDocTabNo@1025 : Integer;
      LineNo@1026 : Integer;
    BEGIN
      OnBeforePostPrepayments(SalesHeader2,DocumentType,SuppressCommit);

      SalesHeader := SalesHeader2;
      GLSetup.GET;
      SalesSetup.GET;
      WITH SalesHeader DO BEGIN
        CheckPrepmtDoc(SalesHeader,DocumentType);

        UpdateDocNos(SalesHeader,DocumentType,GenJnlLineDocNo,PostingNoSeriesCode,ModifyHeader);

        IF NOT PreviewMode AND ModifyHeader THEN BEGIN
          MODIFY;
          IF NOT SuppressCommit THEN
            COMMIT;
        END;

        Window.OPEN(
          '#1#################################\\' +
          Text002 +
          Text004 +
          Text005 +
          Text006);
        Window.UPDATE(1,STRSUBSTNO('%1 %2',SELECTSTR(1 + DocumentType,Text019),"No."));

        SourceCodeSetup.GET;
        SrcCode := SourceCodeSetup.Sales;
        IF "Prepmt. Posting Description" <> '' THEN
          PostingDescription := "Prepmt. Posting Description"
        ELSE
          PostingDescription :=
            COPYSTR(
              STRSUBSTNO(Text012,SELECTSTR(1 + DocumentType,Text019),"Document Type","No."),
              1,MAXSTRLEN("Posting Description"));

        // Create posted header
        IF SalesSetup."Ext. Doc. No. Mandatory" THEN
          TESTFIELD("External Document No.");
        CASE DocumentType OF
          DocumentType::Invoice:
            BEGIN
              InsertSalesInvHeader(SalesInvHeader,SalesHeader,PostingDescription,GenJnlLineDocNo,SrcCode,PostingNoSeriesCode);
              GenJnlLineDocType := GenJnlLine."Document Type"::Invoice;
              PostedDocTabNo := DATABASE::"Sales Invoice Header";
              Window.UPDATE(1,STRSUBSTNO(Text003,"Document Type","No.",SalesInvHeader."No."));
            END;
          DocumentType::"Credit Memo":
            BEGIN
              CalcPmtDiscOnCrMemos := GetCalcPmtDiscOnCrMemos("Prepmt. Payment Terms Code");
              InsertSalesCrMemoHeader(
                SalesCrMemoHeader,SalesHeader,PostingDescription,GenJnlLineDocNo,SrcCode,PostingNoSeriesCode,
                CalcPmtDiscOnCrMemos);
              GenJnlLineDocType := GenJnlLine."Document Type"::"Credit Memo";
              PostedDocTabNo := DATABASE::"Sales Cr.Memo Header";
              Window.UPDATE(1,STRSUBSTNO(Text011,"Document Type","No.",SalesCrMemoHeader."No."));
            END;
        END;
        IF SalesSetup."Copy Comments Order to Invoice" THEN
          CopyCommentLines("No.",PostedDocTabNo,GenJnlLineDocNo);
        GenJnlLineExtDocNo := "External Document No.";
        // Reverse old lines
        IF DocumentType = DocumentType::Invoice THEN BEGIN
          GetSalesLinesToDeduct(SalesHeader,TempSalesLines);
          IF NOT TempSalesLines.ISEMPTY THEN
            CalcVATAmountLines(SalesHeader,TempSalesLines,TempVATAmountLineDeduct,DocumentType::"Credit Memo");
        END;

        // Create Lines
        TempPrepmtInvLineBuffer.DELETEALL;
        CalcVATAmountLines(SalesHeader,SalesLine,TempVATAmountLine,DocumentType);
        TempVATAmountLine.DeductVATAmountLine(TempVATAmountLineDeduct);
        UpdateVATOnLines(SalesHeader,SalesLine,TempVATAmountLine,DocumentType);
        BuildInvLineBuffer(SalesHeader,SalesLine,DocumentType,TempPrepmtInvLineBuffer,TRUE);
        TempPrepmtInvLineBuffer.FIND('-');
        REPEAT
          LineCount := LineCount + 1;
          Window.UPDATE(2,LineCount);
          IF TempPrepmtInvLineBuffer."Line No." <> 0 THEN
            LineNo := PrevLineNo + TempPrepmtInvLineBuffer."Line No."
          ELSE
            LineNo := PrevLineNo + 10000;
          CASE DocumentType OF
            DocumentType::Invoice:
              BEGIN
                InsertSalesInvLine(SalesInvHeader,LineNo,TempPrepmtInvLineBuffer);
                PostedDocTabNo := DATABASE::"Sales Invoice Line";
              END;
            DocumentType::"Credit Memo":
              BEGIN
                InsertSalesCrMemoLine(SalesCrMemoHeader,LineNo,TempPrepmtInvLineBuffer);
                PostedDocTabNo := DATABASE::"Sales Cr.Memo Line";
              END;
          END;
          PrevLineNo := LineNo;
          InsertExtendedText(
            PostedDocTabNo,GenJnlLineDocNo,TempPrepmtInvLineBuffer."G/L Account No.","Document Date","Language Code",PrevLineNo);
        UNTIL TempPrepmtInvLineBuffer.NEXT = 0;

        // G/L Posting
        LineCount := 0;
        IF NOT "Compress Prepayment" THEN
          TempPrepmtInvLineBuffer.CompressBuffer;
        TempPrepmtInvLineBuffer.SETRANGE(Adjustment,FALSE);
        TempPrepmtInvLineBuffer.FINDSET(TRUE);
        REPEAT
          IF DocumentType = DocumentType::Invoice THEN
            TempPrepmtInvLineBuffer.ReverseAmounts;
          RoundAmounts(SalesHeader,TempPrepmtInvLineBuffer,TotalPrepmtInvLineBuffer,TotalPrepmtInvLineBufferLCY);
          IF "Currency Code" = '' THEN BEGIN
            AdjustInvLineBuffers(SalesHeader,TempPrepmtInvLineBuffer,TotalPrepmtInvLineBuffer,DocumentType);
            TotalPrepmtInvLineBufferLCY := TotalPrepmtInvLineBuffer;
          END ELSE
            AdjustInvLineBuffers(SalesHeader,TempPrepmtInvLineBuffer,TotalPrepmtInvLineBufferLCY,DocumentType);
          TempPrepmtInvLineBuffer.MODIFY;
        UNTIL TempPrepmtInvLineBuffer.NEXT = 0;

        TempPrepmtInvLineBuffer.RESET;
        TempPrepmtInvLineBuffer.SETCURRENTKEY(Adjustment);
        TempPrepmtInvLineBuffer.FIND('+');
        REPEAT
          LineCount := LineCount + 1;
          Window.UPDATE(3,LineCount);

          PostPrepmtInvLineBuffer(
            SalesHeader,TempPrepmtInvLineBuffer,DocumentType,PostingDescription,
            GenJnlLineDocType,GenJnlLineDocNo,GenJnlLineExtDocNo,SrcCode,PostingNoSeriesCode);
        UNTIL TempPrepmtInvLineBuffer.NEXT(-1) = 0;

        // Post customer entry
        Window.UPDATE(4,1);
        PostCustomerEntry(
          SalesHeader,TotalPrepmtInvLineBuffer,TotalPrepmtInvLineBufferLCY,DocumentType,PostingDescription,
          GenJnlLineDocType,GenJnlLineDocNo,GenJnlLineExtDocNo,SrcCode,PostingNoSeriesCode,CalcPmtDiscOnCrMemos);

        UpdatePostedSalesDocument(DocumentType,GenJnlLineDocNo);

        // Balancing account
        IF "Bal. Account No." <> '' THEN BEGIN
          Window.UPDATE(5,1);
          CustLedgEntry.FINDLAST;
          PostBalancingEntry(
            SalesHeader,TotalPrepmtInvLineBuffer,TotalPrepmtInvLineBufferLCY,CustLedgEntry,DocumentType,
            PostingDescription,GenJnlLineDocType,GenJnlLineDocNo,GenJnlLineExtDocNo,SrcCode,PostingNoSeriesCode);
        END;

        // Update lines & header
        UpdateSalesDocument(SalesHeader,SalesLine,DocumentType,GenJnlLineDocNo);
        IF Status <> Status::"Pending Prepayment" THEN
          Status := Status::"Pending Prepayment";
        MODIFY;
      END;

      IF PreviewMode THEN BEGIN
        Window.CLOSE;
        GenJnlPostPreview.ThrowError;
      END;

      SalesHeader2 := SalesHeader;

      OnAfterPostPrepayments(SalesHeader2,DocumentType,SuppressCommit,SalesInvHeader,SalesCrMemoHeader);
    END;

    [External]
    PROCEDURE CheckPrepmtDoc@59(SalesHeader@1000 : Record 36;DocumentType@1002 : 'Invoice,Credit Memo');
    VAR
      Cust@1003 : Record 18;
      GenJnlCheckLine@1001 : Codeunit 11;
      CheckDimensions@1004 : Codeunit 481;
    BEGIN
      WITH SalesHeader DO BEGIN
        TESTFIELD("Document Type","Document Type"::Order);
        TESTFIELD("Sell-to Customer No.");
        TESTFIELD("Bill-to Customer No.");
        TESTFIELD("Posting Date");
        TESTFIELD("Document Date");
        IF GenJnlCheckLine.DateNotAllowed("Posting Date") THEN
          FIELDERROR("Posting Date",Text000);

        IF NOT CheckOpenPrepaymentLines(SalesHeader,DocumentType) THEN
          ERROR(Text001);

        CheckDimensions.CheckSalesPrepmtDim(SalesHeader);
        ErrorMessageMgt.Finish;
        OnCheckSalesPostRestrictions;
        Cust.GET("Sell-to Customer No.");
        Cust.CheckBlockedCustOnDocs(Cust,PrepmtDocTypeToDocType(DocumentType),FALSE,TRUE);
        IF "Bill-to Customer No." <> "Sell-to Customer No." THEN BEGIN
          Cust.GET("Bill-to Customer No.");
          Cust.CheckBlockedCustOnDocs(Cust,PrepmtDocTypeToDocType(DocumentType),FALSE,TRUE);
        END;
        OnAfterCheckPrepmtDoc(SalesHeader,DocumentType,SuppressCommit);
      END;
    END;

    LOCAL PROCEDURE UpdateDocNos@31(VAR SalesHeader@1001 : Record 36;DocumentType@1000 : 'Invoice,Credit Memo';VAR DocNo@1002 : Code[20];VAR NoSeriesCode@1003 : Code[20];VAR ModifyHeader@1006 : Boolean);
    VAR
      NoSeriesMgt@1004 : Codeunit 396;
    BEGIN
      ModifyHeader := FALSE;
      WITH SalesHeader DO
        CASE DocumentType OF
          DocumentType::Invoice:
            BEGIN
              TESTFIELD("Prepayment Due Date");
              TESTFIELD("Prepmt. Cr. Memo No.",'');
              IF "Prepayment No." = '' THEN
                IF NOT PreviewMode THEN BEGIN
                  TESTFIELD("Prepayment No. Series");
                  "Prepayment No." := NoSeriesMgt.GetNextNo("Prepayment No. Series","Posting Date",TRUE);
                  ModifyHeader := TRUE;
                END ELSE
                  "Prepayment No." := '***';
              DocNo := "Prepayment No.";
              NoSeriesCode := "Prepayment No. Series";
            END;
          DocumentType::"Credit Memo":
            BEGIN
              TESTFIELD("Prepayment No.",'');
              IF "Prepmt. Cr. Memo No." = '' THEN
                IF NOT PreviewMode THEN BEGIN
                  TESTFIELD("Prepmt. Cr. Memo No. Series");
                  "Prepmt. Cr. Memo No." := NoSeriesMgt.GetNextNo("Prepmt. Cr. Memo No. Series","Posting Date",TRUE);
                  ModifyHeader := TRUE;
                END ELSE
                  "Prepmt. Cr. Memo No." := '***';
              DocNo := "Prepmt. Cr. Memo No.";
              NoSeriesCode := "Prepmt. Cr. Memo No. Series";
            END;
        END;
    END;

    [External]
    PROCEDURE CheckOpenPrepaymentLines@1(SalesHeader@1000 : Record 36;DocumentType@1003 : Option) Found : Boolean;
    VAR
      SalesLine@1001 : Record 37;
    BEGIN
      WITH SalesLine DO BEGIN
        ApplyFilter(SalesHeader,DocumentType,SalesLine);
        IF FIND('-') THEN
          REPEAT
            IF NOT Found THEN
              Found := PrepmtAmount(SalesLine,DocumentType) <> 0;
            IF ("Prepayment VAT Identifier" = '') AND ("Prepmt. Amt. Inv." = 0) THEN BEGIN
              UpdatePrepmtSetupFields;
              MODIFY;
            END;
          UNTIL NEXT = 0;
      END;
      EXIT(Found);
    END;

    LOCAL PROCEDURE RoundAmounts@5(SalesHeader@1001 : Record 36;VAR PrepmtInvLineBuf@1000 : Record 461;VAR TotalPrepmtInvLineBuf@1002 : Record 461;VAR TotalPrepmtInvLineBufLCY@1003 : Record 461);
    VAR
      VAT@1004 : Boolean;
    BEGIN
      TotalPrepmtInvLineBuf.IncrAmounts(PrepmtInvLineBuf);

      WITH PrepmtInvLineBuf DO
        IF SalesHeader."Currency Code" <> '' THEN BEGIN
          VAT := Amount <> "Amount Incl. VAT";
          "Amount Incl. VAT" :=
            AmountToLCY(SalesHeader,TotalPrepmtInvLineBuf."Amount Incl. VAT",TotalPrepmtInvLineBufLCY."Amount Incl. VAT");
          IF VAT THEN
            Amount := AmountToLCY(SalesHeader,TotalPrepmtInvLineBuf.Amount,TotalPrepmtInvLineBufLCY.Amount)
          ELSE
            Amount := "Amount Incl. VAT";
          "VAT Amount" := "Amount Incl. VAT" - Amount;
          IF "VAT Base Amount" <> 0 THEN
            "VAT Base Amount" := Amount;
        END;

      TotalPrepmtInvLineBufLCY.IncrAmounts(PrepmtInvLineBuf);

      OnAfterRoundAmounts(SalesHeader,PrepmtInvLineBuf,TotalPrepmtInvLineBuf,TotalPrepmtInvLineBufLCY);
    END;

    LOCAL PROCEDURE AmountToLCY@6(SalesHeader@1001 : Record 36;TotalAmt@1000 : Decimal;PrevTotalAmt@1002 : Decimal) : Decimal;
    VAR
      CurrExchRate@1003 : Record 330;
    BEGIN
      CurrExchRate.INIT;
      WITH SalesHeader DO
        EXIT(
          ROUND(
            CurrExchRate.ExchangeAmtFCYToLCY(
      //      "Posting Date","Currency Code",TotalAmt,"Currency Factor")) - //**4PS.o
              0, '', "Posting Date","Currency Code",TotalAmt,"Currency Factor",TRUE)) - //**4PS.n
          PrevTotalAmt);
    END;

    LOCAL PROCEDURE BuildInvLineBuffer@3(SalesHeader@1000 : Record 36;VAR SalesLine@1001 : Record 37;DocumentType@1005 : Option;VAR PrepmtInvLineBuf@1002 : Record 461;UpdateLines@1010 : Boolean);
    VAR
      PrepmtInvLineBuf2@1003 : Record 461;
      TotalPrepmtInvLineBuffer@1009 : Record 461;
      TotalPrepmtInvLineBufferDummy@1008 : Record 461;
      SalesSetup@1011 : Record 311;
    BEGIN
      WITH SalesHeader DO BEGIN
        TempGlobalPrepmtInvLineBuf.RESET;
        TempGlobalPrepmtInvLineBuf.DELETEALL;
        SalesSetup.GET;
        ApplyFilter(SalesHeader,DocumentType,SalesLine);
        IF SalesLine.FIND('-') THEN
          REPEAT
            IF PrepmtAmount(SalesLine,DocumentType) <> 0 THEN BEGIN
              IF SalesLine.Quantity < 0 THEN
                SalesLine.FIELDERROR(Quantity,STRSUBSTNO(Text018,FIELDCAPTION("Prepayment %")));
              IF SalesLine."Unit Price" < 0 THEN
                SalesLine.FIELDERROR("Unit Price",STRSUBSTNO(Text018,FIELDCAPTION("Prepayment %")));

              FillInvLineBuffer(SalesHeader,SalesLine,PrepmtInvLineBuf2);
              IF UpdateLines THEN
                TempGlobalPrepmtInvLineBuf.CopyWithLineNo(PrepmtInvLineBuf2,SalesLine."Line No.");
              PrepmtInvLineBuf.InsertInvLineBuffer(PrepmtInvLineBuf2);
              IF SalesSetup."Invoice Rounding" THEN
                RoundAmounts(SalesHeader,PrepmtInvLineBuf2,TotalPrepmtInvLineBuffer,TotalPrepmtInvLineBufferDummy);
            END;
          UNTIL SalesLine.NEXT = 0;
        IF SalesSetup."Invoice Rounding" THEN
          IF InsertInvoiceRounding(
               SalesHeader,PrepmtInvLineBuf2,TotalPrepmtInvLineBuffer,SalesLine."Line No.")
          THEN
            PrepmtInvLineBuf.InsertInvLineBuffer(PrepmtInvLineBuf2);
      END;
    END;

    [External]
    PROCEDURE BuildInvLineBuffer2@19(SalesHeader@1000 : Record 36;VAR SalesLine@1001 : Record 37;DocumentType@1005 : 'Invoice,Credit Memo,Statistic';VAR PrepmtInvLineBuf@1002 : Record 461);
    BEGIN
      BuildInvLineBuffer(SalesHeader,SalesLine,DocumentType,PrepmtInvLineBuf,FALSE);
    END;

    LOCAL PROCEDURE AdjustInvLineBuffers@41(SalesHeader@1003 : Record 36;VAR PrepmtInvLineBuf@1002 : Record 461;VAR TotalPrepmtInvLineBuf@1001 : Record 461;DocumentType@1000 : 'Invoice,Credit Memo');
    VAR
      VATAdjustment@1004 : ARRAY [2] OF Decimal;
      VAT@1005 : ',Base,Amount';
    BEGIN
      CalcPrepmtAmtInvLCYInLines(SalesHeader,PrepmtInvLineBuf,DocumentType,VATAdjustment);
      IF ABS(VATAdjustment[VAT::Base]) > GLSetup."Amount Rounding Precision" THEN
        InsertCorrInvLineBuffer(PrepmtInvLineBuf,SalesHeader,VATAdjustment[VAT::Base])
      ELSE
        IF (VATAdjustment[VAT::Base] <> 0) OR (VATAdjustment[VAT::Amount] <> 0) THEN BEGIN
          PrepmtInvLineBuf.AdjustVATBase(VATAdjustment);
          TotalPrepmtInvLineBuf.AdjustVATBase(VATAdjustment);
        END;
    END;

    LOCAL PROCEDURE CalcPrepmtAmtInvLCYInLines@42(SalesHeader@1002 : Record 36;VAR PrepmtInvLineBuf@1003 : Record 461;DocumentType@1001 : 'Invoice,Credit Memo';VAR VATAdjustment@1006 : ARRAY [2] OF Decimal);
    VAR
      SalesLine@1000 : Record 37;
      PrepmtInvBufAmount@1004 : ARRAY [2] OF Decimal;
      TotalAmount@1005 : ARRAY [2] OF Decimal;
      LineAmount@1007 : ARRAY [2] OF Decimal;
      Ratio@1010 : ARRAY [2] OF Decimal;
      PrepmtAmtReminder@1008 : ARRAY [2] OF Decimal;
      PrepmtAmountRnded@1009 : ARRAY [2] OF Decimal;
      VAT@1011 : ',Base,Amount';
    BEGIN
      PrepmtInvLineBuf.AmountsToArray(PrepmtInvBufAmount);
      IF DocumentType = DocumentType::Invoice THEN
        ReverseDecArray(PrepmtInvBufAmount);

      TempGlobalPrepmtInvLineBuf.SetFilterOnPKey(PrepmtInvLineBuf);
      TempGlobalPrepmtInvLineBuf.CALCSUMS(Amount,"Amount Incl. VAT");
      TempGlobalPrepmtInvLineBuf.AmountsToArray(TotalAmount);
      FOR VAT := VAT::Base TO VAT::Amount DO
        IF TotalAmount[VAT] = 0 THEN
          Ratio[VAT] := 0
        ELSE
          Ratio[VAT] := PrepmtInvBufAmount[VAT] / TotalAmount[VAT];
      IF TempGlobalPrepmtInvLineBuf.FINDSET THEN
        REPEAT
          TempGlobalPrepmtInvLineBuf.AmountsToArray(LineAmount);
          PrepmtAmountRnded[VAT::Base] :=
            CalcRoundedAmount(LineAmount[VAT::Base],Ratio[VAT::Base],PrepmtAmtReminder[VAT::Base]);
          PrepmtAmountRnded[VAT::Amount] :=
            CalcRoundedAmount(LineAmount[VAT::Amount],Ratio[VAT::Amount],PrepmtAmtReminder[VAT::Amount]);

          SalesLine.GET(SalesHeader."Document Type",SalesHeader."No.",TempGlobalPrepmtInvLineBuf."Line No.");
          IF DocumentType = DocumentType::"Credit Memo" THEN BEGIN
            VATAdjustment[VAT::Base] += SalesLine."Prepmt. Amount Inv. (LCY)" - PrepmtAmountRnded[VAT::Base];
            SalesLine."Prepmt. Amount Inv. (LCY)" := 0;
            VATAdjustment[VAT::Amount] += SalesLine."Prepmt. VAT Amount Inv. (LCY)" - PrepmtAmountRnded[VAT::Amount];
            SalesLine."Prepmt. VAT Amount Inv. (LCY)" := 0;
          END ELSE BEGIN
            SalesLine."Prepmt. Amount Inv. (LCY)" += PrepmtAmountRnded[VAT::Base];
            SalesLine."Prepmt. VAT Amount Inv. (LCY)" += PrepmtAmountRnded[VAT::Amount];
          END;
          SalesLine.MODIFY;
        UNTIL TempGlobalPrepmtInvLineBuf.NEXT = 0;
      TempGlobalPrepmtInvLineBuf.DELETEALL;
    END;

    LOCAL PROCEDURE CalcRoundedAmount@51(LineAmount@1000 : Decimal;Ratio@1001 : Decimal;VAR Reminder@1002 : Decimal) RoundedAmount : Decimal;
    VAR
      Amount@1003 : Decimal;
    BEGIN
      Amount := Reminder + LineAmount * Ratio;
      RoundedAmount := ROUND(Amount);
      Reminder := Amount - RoundedAmount;
    END;

    LOCAL PROCEDURE ReverseDecArray@53(VAR DecArray@1000 : ARRAY [2] OF Decimal);
    VAR
      Idx@1001 : Integer;
    BEGIN
      FOR Idx := 1 TO ARRAYLEN(DecArray) DO
        DecArray[Idx] := -DecArray[Idx];
    END;

    LOCAL PROCEDURE InsertCorrInvLineBuffer@43(VAR PrepmtInvLineBuf@1001 : Record 461;SalesHeader@1002 : Record 36;VATBaseAdjustment@1003 : Decimal);
    VAR
      NewPrepmtInvLineBuf@1000 : Record 461;
      SavedPrepmtInvLineBuf@1004 : Record 461;
      AdjmtAmountACY@1005 : Decimal;
    BEGIN
      SavedPrepmtInvLineBuf := PrepmtInvLineBuf;

      IF SalesHeader."Currency Code" = '' THEN
        AdjmtAmountACY := VATBaseAdjustment
      ELSE
        AdjmtAmountACY := 0;

      NewPrepmtInvLineBuf.FillAdjInvLineBuffer(
        PrepmtInvLineBuf,
        GetPrepmtAccNo(PrepmtInvLineBuf."Gen. Bus. Posting Group",PrepmtInvLineBuf."Gen. Prod. Posting Group"),
        VATBaseAdjustment,AdjmtAmountACY);
      PrepmtInvLineBuf.InsertInvLineBuffer(NewPrepmtInvLineBuf);

      NewPrepmtInvLineBuf.FillAdjInvLineBuffer(
        PrepmtInvLineBuf,
        GetCorrBalAccNo(SalesHeader,VATBaseAdjustment > 0),
        -VATBaseAdjustment,-AdjmtAmountACY);
      PrepmtInvLineBuf.InsertInvLineBuffer(NewPrepmtInvLineBuf);

      PrepmtInvLineBuf := SavedPrepmtInvLineBuf;
    END;

    LOCAL PROCEDURE GetPrepmtAccNo@47(GenBusPostingGroup@1000 : Code[20];GenProdPostingGroup@1001 : Code[20]) : Code[20];
    BEGIN
      IF (GenBusPostingGroup <> GenPostingSetup."Gen. Bus. Posting Group") OR
         (GenProdPostingGroup <> GenPostingSetup."Gen. Prod. Posting Group")
      THEN
        GenPostingSetup.GET(GenBusPostingGroup,GenProdPostingGroup);
      EXIT(GenPostingSetup.GetSalesPrepmtAccount);
    END;

    [External]
    PROCEDURE GetCorrBalAccNo@48(SalesHeader@1000 : Record 36;PositiveAmount@1001 : Boolean) : Code[20];
    VAR
      BalAccNo@1002 : Code[20];
    BEGIN
      IF SalesHeader."Currency Code" = '' THEN
        BalAccNo := GetInvRoundingAccNo(SalesHeader."Customer Posting Group")
      ELSE
        BalAccNo := GetGainLossGLAcc(SalesHeader."Currency Code",PositiveAmount);
      EXIT(BalAccNo);
    END;

    [External]
    PROCEDURE GetInvRoundingAccNo@49(CustomerPostingGroup@1000 : Code[20]) : Code[20];
    VAR
      CustPostingGr@1002 : Record 92;
      GLAcc@1001 : Record 15;
    BEGIN
      CustPostingGr.GET(CustomerPostingGroup);
      GLAcc.GET(CustPostingGr.GetInvRoundingAccount);
      EXIT(CustPostingGr."Invoice Rounding Account");
    END;

    LOCAL PROCEDURE GetGainLossGLAcc@50(CurrencyCode@1000 : Code[10];PositiveAmount@1002 : Boolean) : Code[20];
    VAR
      Currency@1001 : Record 4;
    BEGIN
      Currency.GET(CurrencyCode);
      IF PositiveAmount THEN
        EXIT(Currency.GetRealizedGainsAccount);
      EXIT(Currency.GetRealizedLossesAccount);
    END;

    LOCAL PROCEDURE GetCurrencyAmountRoundingPrecision@7(CurrencyCode@1000 : Code[10]) : Decimal;
    VAR
      Currency@1001 : Record 4;
    BEGIN
      Currency.Initialize(CurrencyCode);
      Currency.TESTFIELD("Amount Rounding Precision");
      EXIT(Currency."Amount Rounding Precision");
    END;

    [External]
    PROCEDURE FillInvLineBuffer@4(SalesHeader@1001 : Record 36;SalesLine@1002 : Record 37;VAR PrepmtInvLineBuf@1000 : Record 461);
    BEGIN
      WITH PrepmtInvLineBuf DO BEGIN
        INIT;
        OnBeforeFillInvLineBuffer(PrepmtInvLineBuf,SalesHeader,SalesLine);
        "G/L Account No." := GetPrepmtAccNo(SalesLine."Gen. Bus. Posting Group",SalesLine."Gen. Prod. Posting Group");

        IF NOT SalesHeader."Compress Prepayment" THEN BEGIN
          "Line No." := SalesLine."Line No.";
          Description := SalesLine.Description;
        END;

        CopyFromSalesLine(SalesLine);
        FillFromGLAcc(SalesHeader."Compress Prepayment");

        SetAmounts(
          SalesLine."Prepayment Amount",SalesLine."Prepmt. Amt. Incl. VAT",SalesLine."Prepayment Amount",
          SalesLine."Prepayment Amount",SalesLine."Prepayment Amount",SalesLine."Prepayment VAT Difference");

        "VAT Amount" := SalesLine."Prepmt. Amt. Incl. VAT" - SalesLine."Prepayment Amount";
        "VAT Amount (ACY)" := SalesLine."Prepmt. Amt. Incl. VAT" - SalesLine."Prepayment Amount";
        "VAT Base Before Pmt. Disc." := -SalesLine."Prepayment Amount";
      END;

      OnAfterFillInvLineBuffer(PrepmtInvLineBuf,SalesLine);
    END;

    LOCAL PROCEDURE InsertInvoiceRounding@25(SalesHeader@1002 : Record 36;VAR PrepmtInvLineBuf@1000 : Record 461;TotalPrepmtInvLineBuf@1001 : Record 461;PrevLineNo@1008 : Integer) : Boolean;
    VAR
      SalesLine@1007 : Record 37;
    BEGIN
      IF InitInvoiceRoundingLine(SalesHeader,TotalPrepmtInvLineBuf."Amount Incl. VAT",SalesLine) THEN BEGIN
        CreateDimensions(SalesLine);
        WITH PrepmtInvLineBuf DO BEGIN
          INIT;
          "Line No." := PrevLineNo + 10000;
          "Invoice Rounding" := TRUE;
          "G/L Account No." := SalesLine."No.";

          CopyFromSalesLine(SalesLine);
          "Gen. Bus. Posting Group" := SalesHeader."Gen. Bus. Posting Group";
          "VAT Bus. Posting Group" := SalesHeader."VAT Bus. Posting Group";

          SetAmounts(
            SalesLine."Line Amount",SalesLine."Amount Including VAT",SalesLine."Line Amount",
            SalesLine."Prepayment Amount",SalesLine."Line Amount",0);

          "VAT Amount" := SalesLine."Amount Including VAT" - SalesLine."Line Amount";
          "VAT Amount (ACY)" := SalesLine."Amount Including VAT" - SalesLine."Line Amount";
        END;
        OnAfterInsertInvoiceRounding(SalesHeader,PrepmtInvLineBuf,TotalPrepmtInvLineBuf,PrevLineNo);
        EXIT(TRUE);
      END;
    END;

    LOCAL PROCEDURE InitInvoiceRoundingLine@29(SalesHeader@1000 : Record 36;TotalAmount@1004 : Decimal;VAR SalesLine@1001 : Record 37) : Boolean;
    VAR
      Currency@1007 : Record 4;
      InvoiceRoundingAmount@1002 : Decimal;
    BEGIN
      Currency.Initialize(SalesHeader."Currency Code");
      Currency.TESTFIELD("Invoice Rounding Precision");
      InvoiceRoundingAmount :=
        -ROUND(
          TotalAmount -
          ROUND(
            TotalAmount,
            Currency."Invoice Rounding Precision",
            Currency.InvoiceRoundingDirection),
          Currency."Amount Rounding Precision");

      IF InvoiceRoundingAmount = 0 THEN
        EXIT(FALSE);

      WITH SalesLine DO BEGIN
        SetHideValidationDialog(TRUE);
        "Document Type" := SalesHeader."Document Type";
        "Document No." := SalesHeader."No.";
        "System-Created Entry" := TRUE;
        Type := Type::"G/L Account";
        VALIDATE("No.",GetInvRoundingAccNo(SalesHeader."Customer Posting Group"));
        VALIDATE(Quantity,1);
        IF SalesHeader."Prices Including VAT" THEN
          VALIDATE("Unit Price",InvoiceRoundingAmount)
        ELSE
          VALIDATE(
            "Unit Price",
            ROUND(
              InvoiceRoundingAmount /
              (1 + (1 - SalesHeader."VAT Base Discount %" / 100) * "VAT %" / 100),
              Currency."Amount Rounding Precision"));
        "Prepayment Amount" := "Unit Price";
        VALIDATE("Amount Including VAT",InvoiceRoundingAmount);
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE CopyCommentLines@22(FromNumber@1002 : Code[20];ToDocType@1000 : Integer;ToNumber@1003 : Code[20]);
    VAR
      SalesCommentLine@1004 : Record 44;
    BEGIN
      WITH SalesCommentLine DO
        CASE ToDocType OF
          DATABASE::"Sales Invoice Header":
            CopyComments("Document Type"::Order,"Document Type"::"Posted Invoice",FromNumber,ToNumber);
          DATABASE::"Sales Cr.Memo Header":
            CopyComments("Document Type"::Order,"Document Type"::"Posted Credit Memo",FromNumber,ToNumber);
        END;
    END;

    LOCAL PROCEDURE InsertExtendedText@17(TabNo@1000 : Integer;DocNo@1003 : Code[20];GLAccNo@1006 : Code[20];DocDate@1001 : Date;LanguageCode@1009 : Code[10];VAR PrevLineNo@1002 : Integer);
    VAR
      TempExtTextLine@1005 : TEMPORARY Record 280;
      SalesInvLine@1007 : Record 113;
      SalesCrMemoLine@1008 : Record 115;
      TransferExtText@1004 : Codeunit 378;
      NextLineNo@1010 : Integer;
    BEGIN
      TransferExtText.PrepmtGetAnyExtText(GLAccNo,TabNo,DocDate,LanguageCode,TempExtTextLine);
      IF TempExtTextLine.FIND('-') THEN BEGIN
        NextLineNo := PrevLineNo + 10000;
        REPEAT
          CASE TabNo OF
            DATABASE::"Sales Invoice Line":
              BEGIN
                SalesInvLine.INIT;
                SalesInvLine."Document No." := DocNo;
                SalesInvLine."Line No." := NextLineNo;
                SalesInvLine.Description := TempExtTextLine.Text;
                SalesInvLine.INSERT;
              END;
            DATABASE::"Sales Cr.Memo Line":
              BEGIN
                SalesCrMemoLine.INIT;
                SalesCrMemoLine."Document No." := DocNo;
                SalesCrMemoLine."Line No." := NextLineNo;
                SalesCrMemoLine.Description := TempExtTextLine.Text;
                SalesCrMemoLine.INSERT;
              END;
          END;
          PrevLineNo := NextLineNo;
          NextLineNo := NextLineNo + 10000;
        UNTIL TempExtTextLine.NEXT = 0;
      END;
    END;

    [External]
    PROCEDURE UpdateVATOnLines@36(SalesHeader@1001 : Record 36;VAR SalesLine@1011 : Record 37;VAR VATAmountLine@1003 : Record 290;DocumentType@1000 : 'Invoice,Credit Memo,Statistic');
    VAR
      TempVATAmountLineRemainder@1004 : TEMPORARY Record 290;
      Currency@1005 : Record 4;
      PrepmtAmt@1002 : Decimal;
      NewAmount@1006 : Decimal;
      NewAmountIncludingVAT@1007 : Decimal;
      NewVATBaseAmount@1008 : Decimal;
      VATAmount@1009 : Decimal;
      VATDifference@1010 : Decimal;
      PrepmtAmtToInvTotal@1013 : Decimal;
      RemainderExists@1012 : Boolean;
    BEGIN
      Currency.Initialize(SalesHeader."Currency Code");

      WITH SalesLine DO BEGIN
        ApplyFilter(SalesHeader,DocumentType,SalesLine);
        LOCKTABLE;
        CALCSUMS("Prepmt. Line Amount","Prepmt. Amt. Inv.");
        PrepmtAmtToInvTotal := "Prepmt. Line Amount" - "Prepmt. Amt. Inv.";
        IF FINDSET THEN
          REPEAT
            PrepmtAmt := PrepmtAmount(SalesLine,DocumentType);
            IF PrepmtAmt <> 0 THEN BEGIN
              VATAmountLine.GET(
                "Prepayment VAT Identifier","Prepmt. VAT Calc. Type","Prepayment Tax Group Code",FALSE,PrepmtAmt >= 0);
              OnUpdateVATOnLinesOnAfterVATAmountLineGet(VATAmountLine);
              IF VATAmountLine.Modified THEN BEGIN
                RemainderExists :=
                  TempVATAmountLineRemainder.GET(
                    "Prepayment VAT Identifier","Prepmt. VAT Calc. Type","Prepayment Tax Group Code",FALSE,PrepmtAmt >= 0);
                OnUpdateVATOnLinesOnAfterGetRemainder(TempVATAmountLineRemainder,RemainderExists);
                IF NOT RemainderExists THEN BEGIN
                  TempVATAmountLineRemainder := VATAmountLine;
                  TempVATAmountLineRemainder.INIT;
                  TempVATAmountLineRemainder.INSERT;
                END;

                IF SalesHeader."Prices Including VAT" THEN BEGIN
                  IF PrepmtAmt = 0 THEN BEGIN
                    VATAmount := 0;
                    NewAmountIncludingVAT := 0;
                  END ELSE BEGIN
                    VATAmount :=
                      TempVATAmountLineRemainder."VAT Amount" +
                      VATAmountLine."VAT Amount" * PrepmtAmt / VATAmountLine."Line Amount";
                    NewAmountIncludingVAT :=
                      TempVATAmountLineRemainder."Amount Including VAT" +
                      VATAmountLine."Amount Including VAT" * PrepmtAmt / VATAmountLine."Line Amount";
                  END;
                  NewAmount :=
                    ROUND(NewAmountIncludingVAT,Currency."Amount Rounding Precision") -
                    ROUND(VATAmount,Currency."Amount Rounding Precision");
                  NewVATBaseAmount :=
                    ROUND(
                      NewAmount * (1 - SalesHeader."VAT Base Discount %" / 100),
                      Currency."Amount Rounding Precision");
                END ELSE BEGIN
                  IF "VAT Calculation Type" = "VAT Calculation Type"::"Full VAT" THEN BEGIN
                    VATAmount := PrepmtAmt;
                    NewAmount := 0;
                    NewVATBaseAmount := 0;
                  END ELSE BEGIN
                    NewAmount := PrepmtAmt;
                    NewVATBaseAmount :=
                      ROUND(
                        NewAmount * (1 - SalesHeader."VAT Base Discount %" / 100),
                        Currency."Amount Rounding Precision");
                    IF VATAmountLine."VAT Base" = 0 THEN
                      VATAmount := 0
                    ELSE
                      VATAmount :=
                        TempVATAmountLineRemainder."VAT Amount" +
                        VATAmountLine."VAT Amount" * NewAmount / VATAmountLine."VAT Base";
                  END;
                  NewAmountIncludingVAT := NewAmount + ROUND(VATAmount,Currency."Amount Rounding Precision");
                END;

                "Prepayment Amount" := NewAmount;
                "Prepmt. Amt. Incl. VAT" :=
                  ROUND(NewAmountIncludingVAT,Currency."Amount Rounding Precision");
                "Prepmt. VAT Base Amt." := NewVATBaseAmount;

                IF (VATAmountLine."Line Amount" - VATAmountLine."Invoice Discount Amount") = 0 THEN
                  VATDifference := 0
                ELSE
                  IF PrepmtAmtToInvTotal = 0 THEN
                    VATDifference :=
                      VATAmountLine."VAT Difference" * ("Prepmt. Line Amount" - "Prepmt. Amt. Inv.") /
                      (VATAmountLine."Line Amount" - VATAmountLine."Invoice Discount Amount")
                  ELSE
                    VATDifference :=
                      VATAmountLine."VAT Difference" * ("Prepmt. Line Amount" - "Prepmt. Amt. Inv.") /
                      PrepmtAmtToInvTotal;

                "Prepayment VAT Difference" := ROUND(VATDifference,Currency."Amount Rounding Precision");

                MODIFY;

                TempVATAmountLineRemainder."Amount Including VAT" :=
                  NewAmountIncludingVAT - ROUND(NewAmountIncludingVAT,Currency."Amount Rounding Precision");
                TempVATAmountLineRemainder."VAT Amount" := VATAmount - NewAmountIncludingVAT + NewAmount;
                TempVATAmountLineRemainder."VAT Difference" := VATDifference - "Prepayment VAT Difference";
                TempVATAmountLineRemainder.MODIFY;
              END;
            END;
          UNTIL NEXT = 0;
      END;

      OnAfterUpdateVATOnLines(SalesHeader,SalesLine,VATAmountLine,DocumentType);
    END;

    [External]
    PROCEDURE CalcVATAmountLines@35(VAR SalesHeader@1001 : Record 36;VAR SalesLine@1009 : Record 37;VAR VATAmountLine@1003 : Record 290;DocumentType@1008 : 'Invoice,Credit Memo,Statistic');
    VAR
      Currency@1004 : Record 4;
      NewAmount@1002 : Decimal;
      NewPrepmtVATDiffAmt@1010 : Decimal;
    BEGIN
      Currency.Initialize(SalesHeader."Currency Code");

      VATAmountLine.DELETEALL;

      WITH SalesLine DO BEGIN
        ApplyFilter(SalesHeader,DocumentType,SalesLine);
        IF FIND('-') THEN
          REPEAT
            NewAmount := PrepmtAmount(SalesLine,DocumentType);
            IF NewAmount <> 0 THEN BEGIN
              IF DocumentType = DocumentType::Invoice THEN
                NewAmount := "Prepmt. Line Amount";
              IF "Prepmt. VAT Calc. Type" IN
                 ["VAT Calculation Type"::"Reverse Charge VAT","VAT Calculation Type"::"Sales Tax"]
              THEN
                "VAT %" := 0;
              IF NOT VATAmountLine.GET(
                   "Prepayment VAT Identifier","Prepmt. VAT Calc. Type","Prepayment Tax Group Code",FALSE,NewAmount >= 0)
              THEN
                VATAmountLine.InsertNewLine(
                  "Prepayment VAT Identifier","Prepmt. VAT Calc. Type","Prepayment Tax Group Code",FALSE,
                  "Prepayment VAT %",NewAmount >= 0,TRUE);

              VATAmountLine."Line Amount" := VATAmountLine."Line Amount" + NewAmount;
              NewPrepmtVATDiffAmt := PrepmtVATDiffAmount(SalesLine,DocumentType);
              IF DocumentType = DocumentType::Invoice THEN
                NewPrepmtVATDiffAmt := "Prepayment VAT Difference" + "Prepmt VAT Diff. to Deduct" +
                  "Prepmt VAT Diff. Deducted";
              VATAmountLine."VAT Difference" := VATAmountLine."VAT Difference" + NewPrepmtVATDiffAmt;
              VATAmountLine.MODIFY;
            END;
          UNTIL NEXT = 0;
      END;

      VATAmountLine.UpdateLines(
        NewAmount,Currency,SalesHeader."Currency Factor",SalesHeader."Prices Including VAT",
        SalesHeader."VAT Base Discount %",SalesHeader."Tax Area Code",SalesHeader."Tax Liable",SalesHeader."Posting Date");

      OnAfterCalcVATAmountLines(SalesHeader,SalesLine,VATAmountLine,DocumentType);
    END;

    [External]
    PROCEDURE SumPrepmt@15(SalesHeader@1000 : Record 36;VAR SalesLine@1011 : Record 37;VAR VATAmountLine@1003 : Record 290;VAR TotalAmount@1001 : Decimal;VAR TotalVATAmount@1002 : Decimal;VAR VATAmountText@1004 : Text[30]);
    VAR
      TempPrepmtInvLineBuf@1005 : TEMPORARY Record 461;
      TotalPrepmtInvLineBuf@1007 : Record 461;
      TotalPrepmtInvLineBufLCY@1008 : Record 461;
      DifVATPct@1009 : Boolean;
      PrevVATPct@1010 : Decimal;
    BEGIN
      CalcVATAmountLines(SalesHeader,SalesLine,VATAmountLine,2);
      UpdateVATOnLines(SalesHeader,SalesLine,VATAmountLine,2);
      BuildInvLineBuffer(SalesHeader,SalesLine,2,TempPrepmtInvLineBuf,FALSE);
      IF TempPrepmtInvLineBuf.FIND('-') THEN BEGIN
        PrevVATPct := TempPrepmtInvLineBuf."VAT %";
        REPEAT
          RoundAmounts(SalesHeader,TempPrepmtInvLineBuf,TotalPrepmtInvLineBuf,TotalPrepmtInvLineBufLCY);
          IF TempPrepmtInvLineBuf."VAT %" <> PrevVATPct THEN
            DifVATPct := TRUE;
        UNTIL TempPrepmtInvLineBuf.NEXT = 0;
      END;

      TotalAmount := TotalPrepmtInvLineBuf.Amount;
      TotalVATAmount := TotalPrepmtInvLineBuf."VAT Amount";
      IF DifVATPct OR (TempPrepmtInvLineBuf."VAT %" = 0) THEN
        VATAmountText := Text014
      ELSE
        VATAmountText := STRSUBSTNO(Text015,PrevVATPct);
    END;

    [External]
    PROCEDURE GetSalesLines@16(SalesHeader@1000 : Record 36;DocumentType@1003 : 'Invoice,Credit Memo,Statistic';VAR ToSalesLine@1001 : Record 37);
    VAR
      SalesSetup@1004 : Record 311;
      FromSalesLine@1002 : Record 37;
      InvRoundingSalesLine@1007 : Record 37;
      TempVATAmountLine@1005 : TEMPORARY Record 290;
      TotalAmt@1006 : Decimal;
      NextLineNo@1008 : Integer;
    BEGIN
      ApplyFilter(SalesHeader,DocumentType,FromSalesLine);
      IF FromSalesLine.FIND('-') THEN BEGIN
        REPEAT
          ToSalesLine := FromSalesLine;
          ToSalesLine.INSERT;
        UNTIL FromSalesLine.NEXT = 0;

        SalesSetup.GET;
        IF SalesSetup."Invoice Rounding" THEN BEGIN
          CalcVATAmountLines(SalesHeader,ToSalesLine,TempVATAmountLine,2);
          UpdateVATOnLines(SalesHeader,ToSalesLine,TempVATAmountLine,2);
          ToSalesLine.CALCSUMS("Prepmt. Amt. Incl. VAT");
          TotalAmt := ToSalesLine."Prepmt. Amt. Incl. VAT";
          ToSalesLine.FINDLAST;
          IF InitInvoiceRoundingLine(SalesHeader,TotalAmt,InvRoundingSalesLine) THEN
            WITH ToSalesLine DO BEGIN
              NextLineNo := "Line No." + 1;
              ToSalesLine := InvRoundingSalesLine;
              "Line No." := NextLineNo;

              IF DocumentType <> DocumentType::"Credit Memo" THEN
                "Prepmt. Line Amount" := "Line Amount"
              ELSE
                "Prepmt. Amt. Inv." := "Line Amount";
              "Prepmt. VAT Calc. Type" := "VAT Calculation Type";
              "Prepayment VAT Identifier" := "VAT Identifier";
              "Prepayment Tax Group Code" := "Tax Group Code";
              "Prepayment VAT Identifier" := "VAT Identifier";
              "Prepayment Tax Group Code" := "Tax Group Code";
              "Prepayment VAT %" := "VAT %";
              INSERT;
            END;
        END;
      END;
    END;

    LOCAL PROCEDURE ApplyFilter@20(SalesHeader@1000 : Record 36;DocumentType@1001 : 'Invoice,Credit Memo,Statistic';VAR SalesLine@1002 : Record 37);
    BEGIN
      WITH SalesLine DO BEGIN
        RESET;
        SETRANGE("Document Type",SalesHeader."Document Type");
        SETRANGE("Document No.",SalesHeader."No.");
        SETFILTER(Type,'<>%1',Type::" ");
        IF DocumentType IN [DocumentType::Invoice,DocumentType::Statistic] THEN
          SETFILTER("Prepmt. Line Amount",'<>0')
        ELSE
          SETFILTER("Prepmt. Amt. Inv.",'<>0');
      END;

      OnAfterApplyFilter(SalesLine,SalesHeader,DocumentType);
    END;

    [External]
    PROCEDURE PrepmtAmount@12(SalesLine@1000 : Record 37;DocumentType@1001 : 'Invoice,Credit Memo,Statistic') : Decimal;
    BEGIN
      WITH SalesLine DO
        CASE DocumentType OF
          DocumentType::Statistic:
            EXIT("Prepmt. Line Amount");
          DocumentType::Invoice:
            EXIT("Prepmt. Line Amount" - "Prepmt. Amt. Inv.");
          ELSE
            EXIT("Prepmt. Amt. Inv." - "Prepmt Amt Deducted");
        END;
    END;

    LOCAL PROCEDURE PostPrepmtInvLineBuffer@38(SalesHeader@1000 : Record 36;PrepmtInvLineBuffer@1003 : Record 461;DocumentType@1008 : 'Invoice,Credit Memo';PostingDescription@1007 : Text[100];DocType@1006 : Option;DocNo@1005 : Code[20];ExtDocNo@1004 : Text[35];SrcCode@1002 : Code[10];PostingNoSeriesCode@1009 : Code[20]);
    VAR
      GenJnlLine@1001 : Record 81;
    BEGIN
      WITH GenJnlLine DO BEGIN
        InitNewLine(
          SalesHeader."Posting Date",SalesHeader."Document Date",PostingDescription,
          PrepmtInvLineBuffer."Global Dimension 1 Code",PrepmtInvLineBuffer."Global Dimension 2 Code",
          PrepmtInvLineBuffer."Dimension Set ID",SalesHeader."Reason Code");

        CopyDocumentFields(DocType,DocNo,ExtDocNo,SrcCode,PostingNoSeriesCode);
        CopyFromSalesHeaderPrepmt(SalesHeader);
        CopyFromPrepmtInvoiceBuffer(PrepmtInvLineBuffer);

        IF NOT PrepmtInvLineBuffer.Adjustment THEN
          "Gen. Posting Type" := "Gen. Posting Type"::Sale;
        Correction :=
          (DocumentType = DocumentType::"Credit Memo") AND GLSetup."Mark Cr. Memos as Corrections";

        OnBeforePostPrepmtInvLineBuffer(GenJnlLine,PrepmtInvLineBuffer,SuppressCommit);
        RunGenJnlPostLine(GenJnlLine);
        OnAfterPostPrepmtInvLineBuffer(GenJnlLine,PrepmtInvLineBuffer,SuppressCommit,GenJnlPostLine);
      END;
    END;

    LOCAL PROCEDURE PostCustomerEntry@33(SalesHeader@1000 : Record 36;TotalPrepmtInvLineBuffer@1008 : Record 461;TotalPrepmtInvLineBufferLCY@1009 : Record 461;DocumentType@1010 : 'Invoice,Credit Memo';PostingDescription@1002 : Text[100];DocType@1003 : Option;DocNo@1004 : Code[20];ExtDocNo@1005 : Text[35];SrcCode@1006 : Code[10];PostingNoSeriesCode@1007 : Code[20];CalcPmtDisc@1011 : Boolean);
    VAR
      GenJnlLine@1001 : Record 81;
    BEGIN
      WITH GenJnlLine DO BEGIN
        InitNewLine(
          SalesHeader."Posting Date",SalesHeader."Document Date",PostingDescription,
          SalesHeader."Shortcut Dimension 1 Code",SalesHeader."Shortcut Dimension 2 Code",
          SalesHeader."Dimension Set ID",SalesHeader."Reason Code");

        CopyDocumentFields(DocType,DocNo,ExtDocNo,SrcCode,PostingNoSeriesCode);

        CopyFromSalesHeaderPrepmtPost(SalesHeader,(DocumentType = DocumentType::Invoice) OR CalcPmtDisc);

        Amount := -TotalPrepmtInvLineBuffer."Amount Incl. VAT";
        "Source Currency Amount" := -TotalPrepmtInvLineBuffer."Amount Incl. VAT";
        "Amount (LCY)" := -TotalPrepmtInvLineBufferLCY."Amount Incl. VAT";
        "Sales/Purch. (LCY)" := -TotalPrepmtInvLineBufferLCY.Amount;
        "Profit (LCY)" := -TotalPrepmtInvLineBufferLCY.Amount;

        Correction := (DocumentType = DocumentType::"Credit Memo") AND GLSetup."Mark Cr. Memos as Corrections";

        OnBeforePostCustomerEntry(GenJnlLine,TotalPrepmtInvLineBuffer,TotalPrepmtInvLineBufferLCY,SuppressCommit);
        GenJnlPostLine.RunWithCheck(GenJnlLine);
        OnAfterPostCustomerEntry(GenJnlLine,TotalPrepmtInvLineBuffer,TotalPrepmtInvLineBufferLCY,SuppressCommit);
      END;
    END;

    LOCAL PROCEDURE PostBalancingEntry@40(SalesHeader@1000 : Record 36;TotalPrepmtInvLineBuffer@1003 : Record 461;TotalPrepmtInvLineBufferLCY@1004 : Record 461;CustLedgEntry@1002 : Record 21;DocumentType@1011 : 'Invoice,Credit Memo';PostingDescription@1010 : Text[100];DocType@1009 : Option;DocNo@1008 : Code[20];ExtDocNo@1007 : Text[35];SrcCode@1006 : Code[10];PostingNoSeriesCode@1005 : Code[20]);
    VAR
      GenJnlLine@1001 : Record 81;
    BEGIN
      WITH GenJnlLine DO BEGIN
        InitNewLine(
          SalesHeader."Posting Date",SalesHeader."Document Date",PostingDescription,
          SalesHeader."Shortcut Dimension 1 Code",SalesHeader."Shortcut Dimension 2 Code",
          SalesHeader."Dimension Set ID",SalesHeader."Reason Code");

        IF DocType = "Document Type"::"Credit Memo" THEN
          CopyDocumentFields("Document Type"::Refund,DocNo,ExtDocNo,SrcCode,PostingNoSeriesCode)
        ELSE
          CopyDocumentFields("Document Type"::Payment,DocNo,ExtDocNo,SrcCode,PostingNoSeriesCode);

        CopyFromSalesHeaderPrepmtPost(SalesHeader,FALSE);
        IF SalesHeader."Bal. Account Type" = SalesHeader."Bal. Account Type"::"Bank Account" THEN
          "Bal. Account Type" := "Bal. Account Type"::"Bank Account";
        "Bal. Account No." := SalesHeader."Bal. Account No.";

        Amount := TotalPrepmtInvLineBuffer."Amount Incl. VAT" + CustLedgEntry."Remaining Pmt. Disc. Possible";
        "Source Currency Amount" := Amount;
        CustLedgEntry.CALCFIELDS(Amount);
        IF CustLedgEntry.Amount = 0 THEN
          "Amount (LCY)" := TotalPrepmtInvLineBufferLCY."Amount Incl. VAT"
        ELSE
          "Amount (LCY)" :=
            TotalPrepmtInvLineBufferLCY."Amount Incl. VAT" +
            ROUND(
              CustLedgEntry."Remaining Pmt. Disc. Possible" / CustLedgEntry."Adjusted Currency Factor");

        Correction := (DocumentType = DocumentType::"Credit Memo") AND GLSetup."Mark Cr. Memos as Corrections";

        "Applies-to Doc. Type" := DocType;
        "Applies-to Doc. No." := DocNo;

        OnBeforePostBalancingEntry(GenJnlLine,CustLedgEntry,TotalPrepmtInvLineBuffer,TotalPrepmtInvLineBufferLCY,SuppressCommit);
        GenJnlPostLine.RunWithCheck(GenJnlLine);
        OnAfterPostBalancingEntry(GenJnlLine,CustLedgEntry,TotalPrepmtInvLineBuffer,TotalPrepmtInvLineBufferLCY,SuppressCommit);
      END;
    END;

    LOCAL PROCEDURE RunGenJnlPostLine@23(VAR GenJnlLine@1000 : Record 81);
    BEGIN
      GenJnlPostLine.RunWithCheck(GenJnlLine);
    END;

    [External]
    PROCEDURE UpdatePrepmtAmountOnSaleslines@13(SalesHeader@1001 : Record 36;NewTotalPrepmtAmount@1000 : Decimal);
    VAR
      Currency@1004 : Record 4;
      SalesLine@1002 : Record 37;
      TotalLineAmount@1003 : Decimal;
      TotalPrepmtAmount@1008 : Decimal;
      TotalPrepmtAmtInv@1010 : Decimal;
      LastLineNo@1009 : Integer;
    BEGIN
      Currency.Initialize(SalesHeader."Currency Code");

      WITH SalesLine DO BEGIN
        SETRANGE("Document Type",SalesHeader."Document Type");
        SETRANGE("Document No.",SalesHeader."No.");
        SETFILTER(Type,'<>%1',Type::" ");
        SETFILTER("Line Amount",'<>0');
        SETFILTER("Prepayment %",'<>0');
        LOCKTABLE;
        IF FIND('-') THEN
          REPEAT
            TotalLineAmount := TotalLineAmount + "Line Amount";
            TotalPrepmtAmtInv := TotalPrepmtAmtInv + "Prepmt. Amt. Inv.";
            LastLineNo := "Line No.";
          UNTIL NEXT = 0
        ELSE
          ERROR(Text017,FIELDCAPTION("Prepayment %"));
        IF TotalLineAmount = 0 THEN
          ERROR(Text013,NewTotalPrepmtAmount);
        IF NOT (NewTotalPrepmtAmount IN [TotalPrepmtAmtInv ..TotalLineAmount]) THEN
          ERROR(Text016,TotalPrepmtAmtInv,TotalLineAmount);
        IF FIND('-') THEN
          REPEAT
            IF "Line No." <> LastLineNo THEN
              VALIDATE(
                "Prepmt. Line Amount",
                ROUND(
                  NewTotalPrepmtAmount * "Line Amount" / TotalLineAmount,
                  Currency."Amount Rounding Precision"))
            ELSE
              VALIDATE("Prepmt. Line Amount",NewTotalPrepmtAmount - TotalPrepmtAmount);
            TotalPrepmtAmount := TotalPrepmtAmount + "Prepmt. Line Amount";
            MODIFY;
          UNTIL NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE CreateDimensions@26(VAR SalesLine@1009 : Record 37);
    VAR
      SourceCodeSetup@1006 : Record 242;
      DimMgt@1010 : Codeunit 408;
      TableID@1007 : ARRAY [10] OF Integer;
      No@1008 : ARRAY [10] OF Code[20];
    BEGIN
      SourceCodeSetup.GET;
      TableID[1] := DATABASE::"G/L Account";
      No[1] := SalesLine."No.";
      TableID[2] := DATABASE::Job;
      No[2] := SalesLine."Job No.";
      //**4PS.so
      //TableID[3] := DATABASE::"Responsibility Center"; //**4PS 10-06-2010
      //No[3] := SalesLine."Responsibility Center"; //**4PS 10-06-2010
      //**4PS.eo
      //**4PS.sn
      TableID[3] := DATABASE::"Service Order";
      No[3] := SalesLine."Service Order No.";
      TableID[4] := DATABASE::"Responsibility Center";
      No[4] := SalesLine."Responsibility Center";
      //**4PS.en
      SalesLine."Shortcut Dimension 1 Code" := '';
      SalesLine."Shortcut Dimension 2 Code" := '';
      SalesLine."Dimension Set ID" :=
        DimMgt.GetRecDefaultDimID(
          SalesLine,0,TableID,No,SourceCodeSetup.Sales,
          SalesLine."Shortcut Dimension 1 Code",SalesLine."Shortcut Dimension 2 Code",SalesLine."Dimension Set ID",DATABASE::Customer);
    END;

    LOCAL PROCEDURE PrepmtDocTypeToDocType@21(DocumentType@1000 : 'Invoice,Credit Memo') : Integer;
    BEGIN
      CASE DocumentType OF
        DocumentType::Invoice:
          EXIT(2);
        DocumentType::"Credit Memo":
          EXIT(3);
      END;
      EXIT(2);
    END;

    [External]
    PROCEDURE GetSalesLinesToDeduct@24(SalesHeader@1000 : Record 36;VAR SalesLines@1002 : Record 37);
    VAR
      SalesLine@1001 : Record 37;
    BEGIN
      ApplyFilter(SalesHeader,1,SalesLine);
      IF SalesLine.FINDSET THEN
        REPEAT
          IF (PrepmtAmount(SalesLine,0) <> 0) AND (PrepmtAmount(SalesLine,1) <> 0) THEN BEGIN
            SalesLines := SalesLine;
            SalesLines.INSERT;
          END;
        UNTIL SalesLine.NEXT = 0;
    END;

    LOCAL PROCEDURE PrepmtVATDiffAmount@37(SalesLine@1000 : Record 37;DocumentType@1001 : 'Invoice,Credit Memo,Statistic') : Decimal;
    BEGIN
      WITH SalesLine DO
        CASE DocumentType OF
          DocumentType::Statistic:
            EXIT("Prepayment VAT Difference");
          DocumentType::Invoice:
            EXIT("Prepayment VAT Difference");
          ELSE
            EXIT("Prepmt VAT Diff. to Deduct");
        END;
    END;

    LOCAL PROCEDURE UpdateSalesDocument@8(VAR SalesHeader@1001 : Record 36;VAR SalesLine@1000 : Record 37;DocumentType@1002 : 'Invoice,Credit Memo';GenJnlLineDocNo@1003 : Code[20]);
    BEGIN
      WITH SalesHeader DO BEGIN
        SalesLine.RESET;
        SalesLine.SETRANGE("Document Type","Document Type");
        SalesLine.SETRANGE("Document No.","No.");
        IF DocumentType = DocumentType::Invoice THEN BEGIN
          "Last Prepayment No." := GenJnlLineDocNo;
          "Prepayment No." := '';
          SalesLine.SETFILTER("Prepmt. Line Amount",'<>0');
          IF SalesLine.FINDSET(TRUE) THEN
            REPEAT
              IF SalesLine."Prepmt. Line Amount" <> SalesLine."Prepmt. Amt. Inv." THEN BEGIN
                SalesLine."Prepmt. Amt. Inv." := SalesLine."Prepmt. Line Amount";
                SalesLine."Prepmt. Amount Inv. Incl. VAT" := SalesLine."Prepmt. Amt. Incl. VAT";
                SalesLine.CalcPrepaymentToDeduct;
                SalesLine."Prepmt VAT Diff. to Deduct" :=
                  SalesLine."Prepmt VAT Diff. to Deduct" + SalesLine."Prepayment VAT Difference";
                SalesLine."Prepayment VAT Difference" := 0;
                SalesLine.MODIFY;
              END;
            UNTIL SalesLine.NEXT = 0;
        END ELSE BEGIN
          "Last Prepmt. Cr. Memo No." := GenJnlLineDocNo;
          "Prepmt. Cr. Memo No." := '';
          SalesLine.SETFILTER("Prepmt. Amt. Inv.",'<>0');
          IF SalesLine.FINDSET(TRUE) THEN
            REPEAT
              SalesLine."Prepmt. Amt. Inv." := SalesLine."Prepmt Amt Deducted";
              IF "Prices Including VAT" THEN
                SalesLine."Prepmt. Amount Inv. Incl. VAT" := SalesLine."Prepmt. Amt. Inv."
              ELSE
                SalesLine."Prepmt. Amount Inv. Incl. VAT" :=
                  ROUND(
                    SalesLine."Prepmt. Amt. Inv." * (100 + SalesLine."Prepayment VAT %") / 100,
                    GetCurrencyAmountRoundingPrecision(SalesLine."Currency Code"));
              SalesLine."Prepmt. Amt. Incl. VAT" := SalesLine."Prepmt. Amount Inv. Incl. VAT";
              SalesLine."Prepayment Amount" := SalesLine."Prepmt. Amt. Inv.";
              SalesLine."Prepmt Amt to Deduct" := 0;
              SalesLine."Prepmt VAT Diff. to Deduct" := 0;
              SalesLine."Prepayment VAT Difference" := 0;
              SalesLine.MODIFY;
            UNTIL SalesLine.NEXT = 0;
        END;
      END;
    END;

    LOCAL PROCEDURE UpdatePostedSalesDocument@102(DocumentType@1002 : 'Invoice,Credit Memo';DocumentNo@1004 : Code[20]);
    VAR
      CustLedgerEntry@1000 : Record 21;
      SalesInvoiceHeader@1001 : Record 112;
      SalesCrMemoHeader@1003 : Record 114;
    BEGIN
      CASE DocumentType OF
        DocumentType::Invoice:
          BEGIN
            CustLedgerEntry.SETRANGE("Document Type",CustLedgerEntry."Document Type"::Invoice);
            CustLedgerEntry.SETRANGE("Document No.",DocumentNo);
            CustLedgerEntry.FINDFIRST;
            SalesInvoiceHeader.GET(DocumentNo);
            SalesInvoiceHeader."Cust. Ledger Entry No." := CustLedgerEntry."Entry No.";
            SalesInvoiceHeader.MODIFY;
          END;
        DocumentType::"Credit Memo":
          BEGIN
            CustLedgerEntry.SETRANGE("Document Type",CustLedgerEntry."Document Type"::"Credit Memo");
            CustLedgerEntry.SETRANGE("Document No.",DocumentNo);
            CustLedgerEntry.FINDFIRST;
            SalesCrMemoHeader.GET(DocumentNo);
            SalesCrMemoHeader."Cust. Ledger Entry No." := CustLedgerEntry."Entry No.";
            SalesCrMemoHeader.MODIFY;
          END;
      END;

      OnAfterUpdatePostedSalesDocument(DocumentType,DocumentNo,SuppressCommit);
    END;

    LOCAL PROCEDURE InsertSalesInvHeader@11(VAR SalesInvHeader@1001 : Record 112;SalesHeader@1002 : Record 36;PostingDescription@1003 : Text[100];GenJnlLineDocNo@1004 : Code[20];SrcCode@1005 : Code[10];PostingNoSeriesCode@1000 : Code[20]);
    VAR
      CreateReference@1090001 : Codeunit 11126182;
    BEGIN
      WITH SalesHeader DO BEGIN
        SalesInvHeader.INIT;
        SalesInvHeader.TRANSFERFIELDS(SalesHeader);
        SalesInvHeader."Posting Description" := PostingDescription;
        SalesInvHeader."Payment Terms Code" := "Prepmt. Payment Terms Code";
        SalesInvHeader."Due Date" := "Prepayment Due Date";
        SalesInvHeader."Pmt. Discount Date" := "Prepmt. Pmt. Discount Date";
        SalesInvHeader."Payment Discount %" := "Prepmt. Payment Discount %";
        SalesInvHeader."No." := GenJnlLineDocNo;
        SalesInvHeader."Pre-Assigned No. Series" := '';
        SalesInvHeader."Source Code" := SrcCode;
        SalesInvHeader."User ID" := USERID;
        SalesInvHeader."No. Printed" := 0;
        SalesInvHeader."Prepayment Invoice" := TRUE;
        SalesInvHeader."Prepayment Order No." := "No.";
        SalesInvHeader."No. Series" := PostingNoSeriesCode;
        //NAVFI
        GLSetup.GET;
        IF GLSetup."Finnish localization active" THEN
          SalesInvHeader."Reference No." := CreateReference.CreateSalesInvReference (SalesInvHeader."No.","Bill-to Customer No.");
        //NAVFI
        OnBeforeSalesInvHeaderInsert(SalesInvHeader,SalesHeader,SuppressCommit);
        SalesInvHeader.INSERT;
        OnAfterSalesInvHeaderInsert(SalesInvHeader,SalesHeader,SuppressCommit);
      END;
    END;

    LOCAL PROCEDURE InsertSalesInvLine@18(SalesInvHeader@1001 : Record 112;LineNo@1004 : Integer;PrepmtInvLineBuffer@1000 : Record 461);
    VAR
      SalesInvLine@1002 : Record 113;
    BEGIN
      WITH PrepmtInvLineBuffer DO BEGIN
        SalesInvLine.INIT;
        SalesInvLine."Document No." := SalesInvHeader."No.";
        SalesInvLine."Line No." := LineNo;
        SalesInvLine."Sell-to Customer No." := SalesInvHeader."Sell-to Customer No.";
        SalesInvLine."Bill-to Customer No." := SalesInvHeader."Bill-to Customer No.";
        SalesInvLine.Type := SalesInvLine.Type::"G/L Account";
        SalesInvLine."No." := "G/L Account No.";
        SalesInvLine."Posting Date" := SalesInvHeader."Posting Date";
        SalesInvLine."Shortcut Dimension 1 Code" := "Global Dimension 1 Code";
        SalesInvLine."Shortcut Dimension 2 Code" := "Global Dimension 2 Code";
        SalesInvLine."Dimension Set ID" := "Dimension Set ID";
        SalesInvLine.Description := Description;
        SalesInvLine.Quantity := 1;
        IF SalesInvHeader."Prices Including VAT" THEN BEGIN
          SalesInvLine."Unit Price" := "Amount Incl. VAT";
          SalesInvLine."Line Amount" := "Amount Incl. VAT";
        END ELSE BEGIN
          SalesInvLine."Unit Price" := Amount;
          SalesInvLine."Line Amount" := Amount;
        END;
        SalesInvLine."Gen. Bus. Posting Group" := "Gen. Bus. Posting Group";
        SalesInvLine."Gen. Prod. Posting Group" := "Gen. Prod. Posting Group";
        SalesInvLine."VAT Bus. Posting Group" := "VAT Bus. Posting Group";
        SalesInvLine."VAT Prod. Posting Group" := "VAT Prod. Posting Group";
        SalesInvLine."VAT %" := "VAT %";
        SalesInvLine.Amount := Amount;
        SalesInvLine."VAT Difference" := "VAT Difference";
        SalesInvLine."Amount Including VAT" := "Amount Incl. VAT";
        SalesInvLine."VAT Calculation Type" := "VAT Calculation Type";
        SalesInvLine."VAT Base Amount" := "VAT Base Amount";
        SalesInvLine."VAT Identifier" := "VAT Identifier";
        // PEB0019
        IF PrepmtInvLineBuffer."Invoice Rounding" THEN
          SalesInvLine."Charge Type" := SalesInvLine."Charge Type"::Rounding;
        // 0019
        OnBeforeSalesInvLineInsert(SalesInvLine,SalesInvHeader,PrepmtInvLineBuffer,SuppressCommit);
        SalesInvLine.INSERT;
        OnAfterSalesInvLineInsert(SalesInvLine,SalesInvHeader,PrepmtInvLineBuffer,SuppressCommit);
      END;
    END;

    LOCAL PROCEDURE InsertSalesCrMemoHeader@27(VAR SalesCrMemoHeader@1001 : Record 114;SalesHeader@1002 : Record 36;PostingDescription@1003 : Text[100];GenJnlLineDocNo@1004 : Code[20];SrcCode@1005 : Code[10];PostingNoSeriesCode@1000 : Code[20];CalcPmtDiscOnCrMemos@1006 : Boolean);
    BEGIN
      WITH SalesHeader DO BEGIN
        SalesCrMemoHeader.INIT;
        SalesCrMemoHeader.TRANSFERFIELDS(SalesHeader);
        SalesCrMemoHeader."Payment Terms Code" := "Prepmt. Payment Terms Code";
        SalesCrMemoHeader."Pmt. Discount Date" := "Prepmt. Pmt. Discount Date";
        SalesCrMemoHeader."Payment Discount %" := "Prepmt. Payment Discount %";
        IF ("Prepmt. Payment Terms Code" <> '') AND NOT CalcPmtDiscOnCrMemos THEN BEGIN
          SalesCrMemoHeader."Payment Discount %" := 0;
          SalesCrMemoHeader."Pmt. Discount Date" := 0D;
        END;
        SalesCrMemoHeader."Posting Description" := PostingDescription;
        SalesCrMemoHeader."Due Date" := "Prepayment Due Date";
        SalesCrMemoHeader."No." := GenJnlLineDocNo;
        SalesCrMemoHeader."Pre-Assigned No. Series" := '';
        SalesCrMemoHeader."Source Code" := SrcCode;
        SalesCrMemoHeader."User ID" := USERID;
        SalesCrMemoHeader."No. Printed" := 0;
        SalesCrMemoHeader."Prepayment Credit Memo" := TRUE;
        SalesCrMemoHeader."Prepayment Order No." := "No.";
        SalesCrMemoHeader.Correction := GLSetup."Mark Cr. Memos as Corrections";
        SalesCrMemoHeader."No. Series" := PostingNoSeriesCode;
        OnBeforeSalesCrMemoHeaderInsert(SalesCrMemoHeader,SalesHeader,SuppressCommit);
        SalesCrMemoHeader.INSERT;
        OnAfterSalesCrMemoHeaderInsert(SalesCrMemoHeader,SalesHeader,SuppressCommit);
      END;
    END;

    LOCAL PROCEDURE InsertSalesCrMemoLine@32(SalesCrMemoHeader@1001 : Record 114;LineNo@1002 : Integer;PrepmtInvLineBuffer@1003 : Record 461);
    VAR
      SalesCrMemoLine@1000 : Record 115;
    BEGIN
      WITH PrepmtInvLineBuffer DO BEGIN
        SalesCrMemoLine.INIT;
        SalesCrMemoLine."Document No." := SalesCrMemoHeader."No.";
        SalesCrMemoLine."Line No." := LineNo;
        SalesCrMemoLine."Sell-to Customer No." := SalesCrMemoHeader."Sell-to Customer No.";
        SalesCrMemoLine."Bill-to Customer No." := SalesCrMemoHeader."Bill-to Customer No.";
        SalesCrMemoLine.Type := SalesCrMemoLine.Type::"G/L Account";
        SalesCrMemoLine."No." := "G/L Account No.";
        SalesCrMemoLine."Posting Date" := SalesCrMemoHeader."Posting Date";
        SalesCrMemoLine."Shortcut Dimension 1 Code" := "Global Dimension 1 Code";
        SalesCrMemoLine."Shortcut Dimension 2 Code" := "Global Dimension 2 Code";
        SalesCrMemoLine."Dimension Set ID" := "Dimension Set ID";
        SalesCrMemoLine.Description := Description;
        SalesCrMemoLine.Quantity := 1;
        IF SalesCrMemoHeader."Prices Including VAT" THEN BEGIN
          SalesCrMemoLine."Unit Price" := "Amount Incl. VAT";
          SalesCrMemoLine."Line Amount" := "Amount Incl. VAT";
        END ELSE BEGIN
          SalesCrMemoLine."Unit Price" := Amount;
          SalesCrMemoLine."Line Amount" := Amount;
        END;
        SalesCrMemoLine."Gen. Bus. Posting Group" := "Gen. Bus. Posting Group";
        SalesCrMemoLine."Gen. Prod. Posting Group" := "Gen. Prod. Posting Group";
        SalesCrMemoLine."VAT Bus. Posting Group" := "VAT Bus. Posting Group";
        SalesCrMemoLine."VAT Prod. Posting Group" := "VAT Prod. Posting Group";
        SalesCrMemoLine."VAT %" := "VAT %";
        SalesCrMemoLine.Amount := Amount;
        SalesCrMemoLine."VAT Difference" := "VAT Difference";
        SalesCrMemoLine."Amount Including VAT" := "Amount Incl. VAT";
        SalesCrMemoLine."VAT Calculation Type" := "VAT Calculation Type";
        SalesCrMemoLine."VAT Base Amount" := "VAT Base Amount";
        SalesCrMemoLine."VAT Identifier" := "VAT Identifier";
        // PEB0019
        IF PrepmtInvLineBuffer."Invoice Rounding" THEN
          SalesCrMemoLine."Charge Type" := SalesCrMemoLine."Charge Type"::Rounding;
        // 0019
        OnBeforeSalesCrMemoLineInsert(SalesCrMemoLine,SalesCrMemoHeader,PrepmtInvLineBuffer,SuppressCommit);
        SalesCrMemoLine.INSERT;
        OnAfterSalesCrMemoLineInsert(SalesCrMemoLine,SalesCrMemoHeader,PrepmtInvLineBuffer,SuppressCommit);
      END;
    END;

    LOCAL PROCEDURE GetCalcPmtDiscOnCrMemos@14(PrepmtPmtTermsCode@1000 : Code[10]) : Boolean;
    VAR
      PaymentTerms@1001 : Record 3;
    BEGIN
      IF PrepmtPmtTermsCode = '' THEN
        EXIT(FALSE);
      PaymentTerms.GET(PrepmtPmtTermsCode);
      EXIT(PaymentTerms."Calc. Pmt. Disc. on Cr. Memos");
    END;

    [External]
    PROCEDURE GetPreviewMode@77() : Boolean;
    BEGIN
      EXIT(PreviewMode);
    END;

    [External]
    PROCEDURE GetSuppressCommit@78() : Boolean;
    BEGIN
      EXIT(SuppressCommit);
    END;

    [External]
    PROCEDURE SetSuppressCommit@63(NewSuppressCommit@1000 : Boolean);
    BEGIN
      SuppressCommit := NewSuppressCommit;
    END;

    PROCEDURE SetPreviewMode@76(NewPreviewMode@1000 : Boolean);
    BEGIN
      PreviewMode := NewPreviewMode;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterApplyFilter@80(VAR SalesLine@1000 : Record 37;SalesHeader@1001 : Record 36;DocumentType@1002 : Option);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCalcVATAmountLines@75(SalesHeader@1003 : Record 36;VAR SalesLine@1002 : Record 37;VAR VATAmountLine@1001 : Record 290;DocumentType@1000 : 'Invoice,Credit Memo,Statistic');
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterCheckPrepmtDoc@60(SalesHeader@1000 : Record 36;DocumentType@1001 : 'Invoice,Credit Memo';CommitIsSuppressed@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterFillInvLineBuffer@64(VAR PrepmtInvLineBuf@1000 : Record 461;SalesLine@1001 : Record 37);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInsertInvoiceRounding@72(SalesHeader@1000 : Record 36;VAR PrepmtInvLineBuffer@1001 : Record 461;VAR TotalPrepmtInvLineBuf@1002 : Record 461;VAR PrevLineNo@1003 : Integer);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPostPrepayments@56(VAR SalesHeader@1000 : Record 36;DocumentType@1001 : 'Invoice,Credit Memo';CommitIsSuppressed@1002 : Boolean;VAR SalesInvoiceHeader@1003 : Record 112;VAR SalesCrMemoHeader@1004 : Record 114);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPostBalancingEntry@58(VAR GenJnlLine@1010 : Record 81;CustLedgEntry@1000 : Record 21;TotalPrepmtInvLineBuffer@1009 : Record 461;TotalPrepmtInvLineBufferLCY@1008 : Record 461;CommitIsSuppressed@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPostCustomerEntry@62(VAR GenJnlLine@1003 : Record 81;TotalPrepmtInvLineBuffer@1001 : Record 461;TotalPrepmtInvLineBufferLCY@1000 : Record 461;CommitIsSuppressed@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterPostPrepmtInvLineBuffer@55(VAR GenJnlLine@1000 : Record 81;PrepmtInvLineBuffer@1001 : Record 461;CommitIsSuppressed@1002 : Boolean;VAR GenJnlPostLine@1003 : Codeunit 12);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterRoundAmounts@73(SalesHeader@1002 : Record 36;VAR PrepmtInvLineBuffer@1001 : Record 461;VAR TotalPrepmtInvLineBuf@1000 : Record 461;VAR TotalPrepmtInvLineBufLCY@1004 : Record 461);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSalesInvHeaderInsert@67(VAR SalesInvoiceHeader@1000 : Record 112;SalesHeader@1001 : Record 36;CommitIsSuppressed@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSalesInvLineInsert@69(VAR SalesInvLine@1000 : Record 113;SalesInvHeader@1001 : Record 112;PrepmtInvLineBuffer@1002 : Record 461;CommitIsSuppressed@1003 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSalesCrMemoHeaderInsert@70(VAR SalesCrMemoHeader@1000 : Record 114;SalesHeader@1001 : Record 36;CommitIsSuppressed@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterSalesCrMemoLineInsert@71(VAR SalesCrMemoLine@1000 : Record 115;SalesCrMemoHeader@1001 : Record 114;PrepmtInvLineBuffer@1002 : Record 461;CommitIsSuppressed@1003 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterUpdatePostedSalesDocument@68(DocumentType@1000 : 'Invoice,Credit Memo';DocumentNo@1001 : Code[20];CommitIsSuppressed@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterUpdateVATOnLines@74(SalesHeader@1000 : Record 36;VAR SalesLine@1001 : Record 37;VAR VATAmountLine@1002 : Record 290;DocumentType@1003 : 'Invoice,Credit Memo,Statistic');
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeInvoice@65(VAR SalesHeader@1000 : Record 36;VAR Handled@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCreditMemo@66(VAR SalesHeader@1001 : Record 36;VAR Handled@1000 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeFillInvLineBuffer@34(VAR PrepaymentInvLineBuffer@1000 : Record 461;SalesHeader@1001 : Record 36;SalesLine@1002 : Record 37);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostPrepayments@52(VAR SalesHeader@1000 : Record 36;DocumentType@1001 : 'Invoice,Credit Memo';CommitIsSuppressed@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeSalesInvHeaderInsert@39(VAR SalesInvHeader@1000 : Record 112;SalesHeader@1001 : Record 36;CommitIsSuppressed@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeSalesInvLineInsert@44(VAR SalesInvLine@1000 : Record 113;SalesInvHeader@1001 : Record 112;PrepmtInvLineBuffer@1002 : Record 461;CommitIsSuppressed@1003 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeSalesCrMemoHeaderInsert@46(VAR SalesCrMemoHeader@1000 : Record 114;SalesHeader@1001 : Record 36;CommitIsSuppressed@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeSalesCrMemoLineInsert@45(VAR SalesCrMemoLine@1000 : Record 115;SalesCrMemoHeader@1001 : Record 114;PrepmtInvLineBuffer@1002 : Record 461;CommitIsSuppressed@1003 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostBalancingEntry@57(VAR GenJnlLine@1010 : Record 81;CustLedgEntry@1000 : Record 21;TotalPrepmtInvLineBuffer@1009 : Record 461;TotalPrepmtInvLineBufferLCY@1008 : Record 461;CommitIsSuppressed@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostCustomerEntry@61(VAR GenJnlLine@1003 : Record 81;TotalPrepmtInvLineBuffer@1001 : Record 461;TotalPrepmtInvLineBufferLCY@1000 : Record 461;CommitIsSuppressed@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforePostPrepmtInvLineBuffer@54(VAR GenJnlLine@1000 : Record 81;PrepmtInvLineBuffer@1001 : Record 461;CommitIsSuppressed@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnUpdateVATOnLinesOnAfterGetRemainder@79(VAR VATAmountLineRemainder@1000 : Record 290;VAR RemainderExists@1001 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnUpdateVATOnLinesOnAfterVATAmountLineGet@81(VAR VATAmountLine@1000 : Record 290);
    BEGIN
    END;

    BEGIN
    {
      PEB *** Begin ***
      PEB0019 Charge Type to all Documents
      PEB
      PEB *** End ***
    }
    END.
  }
}

