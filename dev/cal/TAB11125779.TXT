OBJECT Table 11125779 Glazing Service Call Assembly
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               VALIDATE("VAT Prod. Posting Group", UpdateVatProdPostingGrp);  //RFC 547
             END;

    CaptionML=[ENU=Glazing Service Call Assembly;
               NOR=Innglassingsservicesamtale, montering;
               SVE=Servicesamtalskapsl.inglasning];
    LookupPageID=Page11125790;
    DrillDownPageID=Page11125790;
  }
  FIELDS
  {
    { 1   ;   ;Glazing Service Call No.;Code20    ;TableRelation="Glazing Service Call";
                                                   CaptionML=[ENU=Glazing Service Call No.;
                                                              NOR=Servicesamtalenr. for Innglassing;
                                                              SVE=Servicesamtalsnr f”r inglasning];
                                                   NotBlank=Yes }
    { 2   ;   ;Glazing Service Call Line No.;Integer;
                                                   TableRelation="Glazing Service Call Line"."Line No." WHERE (Glazing Service Call No.=FIELD(Glazing Service Call No.));
                                                   OnValidate=VAR
                                                                GlazingServiceCallLine@1100525000 : Record 11125778;
                                                              BEGIN
                                                                IF NOT GlazingServiceCallLine.GET("Glazing Service Call No.", "Glazing Service Call Line No.") THEN
                                                                  GlazingServiceCallLine.INIT;

                                                                VALIDATE("Surface Area Driemaat (m2)", GlazingServiceCallLine."Surface Area Driemaat (m2)");
                                                                VALIDATE("Surface Area (m2)", GlazingServiceCallLine."Surface Area (m2)");
                                                                VALIDATE("Linear Meters", GlazingServiceCallLine."Linear Meters");
                                                                VALIDATE("Quantity (Glazing Call Line)", GlazingServiceCallLine.Quantity);
                                                                VALIDATE("Shape Surcharge %", GlazingServiceCallLine."Shape Surcharge %");
                                                              END;

                                                   CaptionML=[ENU=Glazing Service Call Line No.;
                                                              NOR=Servicesamtalsradnr. for Innglassing;
                                                              SVE=Servicesamtalsradnr f”r inglasning];
                                                   NotBlank=Yes }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr] }
    { 10  ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   OnValidate=VAR
                                                                Item@1100525000 : Record 27;
                                                              BEGIN
                                                                IF NOT Item.GET("Item No.") THEN
                                                                  Item.INIT
                                                                //mg.sn, 13-12-11: M31429
                                                                ELSE BEGIN
                                                                  Item.TESTFIELD(Blocked, FALSE);
                                                                END;
                                                                //mg.en, 13-12-11: M31429

                                                                Description := Item.Description;
                                                                "Description 2" := Item."Description 2";
                                                                UpdateBasicPrice(FIELDNO("Item No."));
                                                                "Standard Cost" := Item."Unit Cost";
                                                                "Global Dimension 2 Code" := Item."Global Dimension 2 Code";
                                                                VALIDATE("Glazing Application Code", Item."Glazing Application Code");
                                                                VALIDATE("Item Weight", Item.Weight);
                                                                VALIDATE("Min. Sfc. Area Driemaat (m2)", Item."Min. Sfc. Area Driemaat (m2)");

                                                                // jhoek.231110.begin
                                                                //CASE Item."Glazing Ordering Code" = '' OF
                                                                //  TRUE: "Ordering Code" := "Item No.";
                                                                //  FALSE: "Ordering Code" := Item."Glazing Ordering Code";
                                                                //END;
                                                                "Ordering Code" := Item."Glazing Ordering Code";
                                                                // jhoek.231110.end

                                                                VALIDATE("VAT Prod. Posting Group", UpdateVatProdPostingGrp);  //RFC 547
                                                                UpdateTotals;

                                                                //db, 31-08-15: function LookupItemNoWithSalesPrice from OnLookup moved to OnAssistEdit on page
                                                              END;

                                                   CaptionML=[ENU=Item No.;
                                                              NOR=Varenr.;
                                                              SVE=Artikelnr] }
    { 11  ;   ;Glazing Application Code;Code20    ;TableRelation="Glazing Application";
                                                   OnValidate=VAR
                                                                GlazingApplication@1100525000 : Record 11125763;
                                                              BEGIN
                                                                IF NOT GlazingApplication.GET("Glazing Application Code") THEN
                                                                  GlazingApplication.INIT;

                                                                "Min. Breastwork Height" := GlazingApplication."Min. Breastwork Height";
                                                                "Tilted Mounting Disallowed" := GlazingApplication."Tilted Mounting Disallowed";
                                                              END;

                                                   CaptionML=[ENU=Glazing Application Code;
                                                              NOR=Programkode for innglassing;
                                                              SVE=Programkod f”r inglasning];
                                                   Editable=No }
    { 12  ;   ;Min. Sfc. Area Driemaat (m2);Decimal;
                                                   CaptionML=[ENU=Min. Sfc. Area Driemaat (m2);
                                                              NOR=Min. ytomr†de Driemaat (m2);
                                                              SVE=Min. ytomr†de Driemaat (m2)];
                                                   MinValue=0;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 15  ;   ;Ordering Code       ;Code20        ;CaptionML=[ENU=Ordering Code;
                                                              NOR=Ordrekode;
                                                              SVE=Orderkod];
                                                   Editable=No }
    { 20  ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 21  ;   ;Description 2       ;Text50        ;CaptionML=[ENU=Description 2;
                                                              NOR=Beskrivelse 2;
                                                              SVE=Beskrivning 2] }
    { 25  ;   ;Global Dimension 2 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2),
                                                                                               Cost Type=CONST(Material));
                                                   CaptionML=[ENU=Global Dimension 2 Code;
                                                              NOR=Global Dimensjon 2-kode;
                                                              SVE=Global dimension 2 kod];
                                                   CaptionClass='1,1,2' }
    { 30  ;   ;Cost Driver         ;Option        ;InitValue=Surface Area;
                                                   OnValidate=BEGIN
                                                                Quantity := 0;
                                                                UpdateQuantity;
                                                              END;

                                                   CaptionML=[ENU=Cost Driver;
                                                              NOR=Kostnadsdriver;
                                                              SVE=Kostnadsdrivare];
                                                   OptionCaptionML=[ENU=" ,,,,,Surface Area,,,,,Linear Meters";
                                                                    NOR=" ,,,,,Flateareal,,,,,L›pemeter";
                                                                    SVE=" ,,,,,Ytarea,,,,,L”pmeter"];
                                                   OptionString=[ ,,,,,Surface Area,,,,,Linear Meters] }
    { 35  ;   ;Surface Area (m2)   ;Decimal       ;OnValidate=BEGIN
                                                                UpdateTotalWeight;
                                                              END;

                                                   CaptionML=[ENU=Surface Area (m2);
                                                              NOR=Overflate (m2);
                                                              SVE=Yta (m2)];
                                                   Editable=No }
    { 40  ;   ;Surface Area Driemaat (m2);Decimal ;OnValidate=BEGIN
                                                                UpdateQuantity;
                                                              END;

                                                   CaptionML=[ENU=Surface Area Driemaat (m2);
                                                              NOR=Overflateareal (m2;
                                                              SVE=Yta Driemaat (m2)];
                                                   Editable=No }
    { 50  ;   ;Linear Meters       ;Decimal       ;OnValidate=BEGIN
                                                                UpdateQuantity;
                                                              END;

                                                   CaptionML=[ENU=Linear Meters;
                                                              NOR=L›pemeter;
                                                              SVE=L”pmeter];
                                                   Editable=No }
    { 60  ;   ;Quantity (Glazing Call Line);Decimal;
                                                   OnValidate=BEGIN
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=[ENU=Quantity (Glazing Call Line);
                                                              NOR=Antall (Innglassingssamtalerad);
                                                              SVE=Ant.(samtalslinje inglasning)];
                                                   Editable=No }
    { 70  ;   ;Item Weight         ;Decimal       ;OnValidate=BEGIN
                                                                UpdateTotalWeight;
                                                              END;

                                                   CaptionML=[ENU=Item Weight;
                                                              NOR=Vekt artikkel;
                                                              SVE=Artikelvikt];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   Editable=No }
    { 71  ;   ;Total Weight        ;Decimal       ;CaptionML=[ENU=Total Weight;
                                                              NOR=Totalvekt;
                                                              SVE=Totalvikt];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   Editable=No }
    { 100 ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                IF CurrFieldNo = FIELDNO(Quantity) THEN
                                                                  IF Quantity <> 0 THEN
                                                                    TESTFIELD("Cost Driver", "Cost Driver"::" ");

                                                                UpdateBasicPrice(FIELDNO(Quantity));
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   DecimalPlaces=0:5 }
    { 110 ;   ;Unit Price          ;Decimal       ;OnValidate=BEGIN
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=[ENU=Unit Price;
                                                              NOR=Enhetspris;
                                                              SVE=A-pris];
                                                   Editable=No }
    { 120 ;   ;Standard Cost       ;Decimal       ;OnValidate=BEGIN
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=[ENU=Standard Cost;
                                                              NOR=Kostpris (standard);
                                                              SVE=Standardkostnad];
                                                   Editable=No }
    { 125 ;   ;Net Sales Amount    ;Decimal       ;CaptionML=ENU=Net Sales Amount;
                                                   Editable=No }
    { 130 ;   ;Surface Surcharge % ;Decimal       ;OnValidate=BEGIN
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=[ENU=Surface Surcharge %;
                                                              NOR=Tilleggsgebyr for overflate %;
                                                              SVE=Yta till„ggsavgiftsprocent];
                                                   DecimalPlaces=0:2;
                                                   BlankZero=Yes }
    { 131 ;   ;Surface Surcharge Amt. (Sales);Decimal;
                                                   CaptionML=[ENU=Surface Surcharge Amount (Sales);
                                                              NOR=Tilleggsgebyr for overflate (Salg);
                                                              SVE=Till„ggsbel. yta (f”rs„ljn.)];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 132 ;   ;Surface Surcharge Amt. (Cost);Decimal;
                                                   CaptionML=[ENU=Surface Surcharge Amt. (Cost);
                                                              NOR=Tilleggsgebyr for overflate (Kostnad);
                                                              SVE=Till„ggsbelopp yta (Kostnad)];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 140 ;   ;Shape Surcharge %   ;Decimal       ;OnValidate=BEGIN
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=[ENU=Shape Surcharge %;
                                                              NOR=Form for tilleggsgebyr %;
                                                              SVE=Form f”r till„ggsavg.procent];
                                                   DecimalPlaces=0:2;
                                                   BlankZero=Yes }
    { 141 ;   ;Shape Surcharge Amount (Cost);Decimal;
                                                   CaptionML=[ENU=Shape Surcharge Amount (Cost);
                                                              NOR=Form for tilleggsgebyr (Kostnad);
                                                              SVE=Form f”r till„ggsbel.(kstn)];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 142 ;   ;Shape Surcharge Amount (Sales);Decimal;
                                                   CaptionML=[ENU=Shape Surcharge Amount (Sales);
                                                              NOR=Form for tilleggsgebyr (Salg);
                                                              SVE=Form f”r till„ggsbel.(f”rs.)];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 200 ;   ;Sales Amount        ;Decimal       ;CaptionML=[ENU=Sales Amount;
                                                              NOR=Salgsbel›p;
                                                              SVE=F”rs„ljningsbelopp];
                                                   Editable=No }
    { 210 ;   ;Standard Cost Amount;Decimal       ;CaptionML=[ENU=Standard Cost Amount;
                                                              NOR=Standardkost bel›p;
                                                              SVE=Standardkostnadsbelopp];
                                                   Editable=No }
    { 300 ;   ;Min. Breastwork Height;Decimal     ;CaptionML=[ENU=Min. Breastwork Height;
                                                              NOR=Min. h›yde for arbeid i brysth›yde;
                                                              SVE=Br”stv„rn, minsta h”jd];
                                                   Editable=No }
    { 310 ;   ;Tilted Mounting Disallowed;Boolean ;CaptionML=[ENU=Tilted Mounting Disallowed;
                                                              NOR=Tiltet montering er ikke tillatt;
                                                              SVE=Vinklad montering inte till†ten];
                                                   Editable=No }
    { 400 ;   ;Total Cost Amount   ;Decimal       ;CaptionML=[ENU=Total Cost Amount;
                                                              NOR=Totalt kostbel›p;
                                                              SVE=Totalt kostnadsbelopp];
                                                   Editable=No }
    { 410 ;   ;Total Sales Amount  ;Decimal       ;CaptionML=[ENU=Total Sales Amount;
                                                              NOR=Totalt salgsbel›p;
                                                              SVE=Totalt f”rs„ljningsbelopp];
                                                   Editable=No }
    { 1050;   ;VAT Prod. Posting Group;Code20     ;TableRelation="VAT Product Posting Group".Code;
                                                   CaptionML=[ENU=VAT Prod. Posting Group;
                                                              NOR=Moms produktbokf›ringsmal;
                                                              SVE=Moms produktbokf”ringsmall];
                                                   Editable=No }
    { 1060;   ;VAT %               ;Decimal       ;CaptionML=[ENU=VAT %;
                                                              NOR=Momssats %;
                                                              SVE=Momssats %];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 1070;   ;Total Sales VAT Amount;Decimal     ;CaptionML=[ENU=Total Sales VAT Amount;
                                                              NOR=Totalt salg, mva.-bel›p;
                                                              SVE=Totalt, f”rs„ljningsmomsbelopp];
                                                   Editable=No }
    { 1080;   ;Total Sales Amount incl. VAT;Decimal;
                                                   CaptionML=[ENU=Total Sales Amount incl. VAT;
                                                              NOR=Totalt salgsbel›p inkl. mva.;
                                                              SVE=Totalt f”rs„ljningsbelopp inkl. moms];
                                                   Editable=No }
    { 1090;   ;Basic Price         ;Decimal       ;OnValidate=BEGIN
                                                                UpdateUnitPrice;
                                                              END;

                                                   CaptionML=[ENU=Basic Price;
                                                              NOR=Grunnpris;
                                                              SVE=Grundpris];
                                                   Editable=No }
    { 1100;   ;Item Discount %     ;Decimal       ;OnValidate=BEGIN
                                                                UpdateUnitPrice;
                                                              END;

                                                   CaptionML=[ENU=Item Discount %;
                                                              NOR=Artikkelrabatt %;
                                                              SVE=Artikelrabatt %];
                                                   Editable=No }
    { 1110;   ;Correction Factor (Cost);Decimal   ;InitValue=1;
                                                   OnValidate=BEGIN
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=ENU=Correction Factor (Cost);
                                                   DecimalPlaces=2:5 }
    { 1120;   ;Correction Factor (Sales);Decimal  ;InitValue=1;
                                                   OnValidate=BEGIN
                                                                UpdateTotals;
                                                              END;

                                                   CaptionML=ENU=Correction Factor (Sales);
                                                   DecimalPlaces=2:5 }
  }
  KEYS
  {
    {    ;Glazing Service Call No.,Glazing Service Call Line No.,Line No.;
                                                   SumIndexFields=Sales Amount,Standard Cost Amount,Total Weight,Total Sales Amount,Total Cost Amount,Total Sales VAT Amount,Total Sales Amount incl. VAT;
                                                   MaintainSIFTIndex=No;
                                                   Clustered=Yes }
    {    ;Glazing Application Code                 }
    {    ;VAT Prod. Posting Group                 ;SumIndexFields=Sales Amount,Standard Cost Amount,Total Weight,Total Sales Amount,Total Cost Amount,Total Sales VAT Amount,Total Sales Amount incl. VAT }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE UpdateBasicPrice@1100525017(CalledByFieldNo@1100525000 : Integer);
    VAR
      SalesPriceCalcMgt@1100525001 : Codeunit 7000;
    BEGIN
      IF "Item No." = '' THEN  BEGIN
        VALIDATE("Basic Price", 0);
        EXIT;
      END;

      SalesPriceCalcMgt.FindGlazSvcCallAssemblyPrice(Rec,CalledByFieldNo);
    END;

    PROCEDURE UpdateQuantity@1100525001();
    BEGIN
      CASE "Cost Driver" OF
        "Cost Driver"::"Surface Area": VALIDATE(Quantity, "Surface Area Driemaat (m2)");
        "Cost Driver"::"Linear Meters": VALIDATE(Quantity, "Linear Meters");
      END;
    END;

    PROCEDURE UpdateTotals@1100525005();
    VAR
      NetCostAmount@1100525000 : Decimal;
    BEGIN
      "Net Sales Amount" := "Unit Price" * Quantity * "Correction Factor (Sales)";
      NetCostAmount := "Standard Cost" * Quantity * "Correction Factor (Cost)";

      "Surface Surcharge Amt. (Cost)" := NetCostAmount * ("Surface Surcharge %" / 100);
      "Surface Surcharge Amt. (Sales)" := "Net Sales Amount" * ("Surface Surcharge %" / 100);

      "Shape Surcharge Amount (Cost)" := NetCostAmount * ("Shape Surcharge %" / 100);
      "Shape Surcharge Amount (Sales)" := "Net Sales Amount" * ("Shape Surcharge %" / 100);

      "Sales Amount" := "Net Sales Amount" + "Surface Surcharge Amt. (Sales)" + "Shape Surcharge Amount (Sales)";
      "Standard Cost Amount" := NetCostAmount + "Surface Surcharge Amt. (Cost)" + "Shape Surcharge Amount (Cost)";

      "Total Cost Amount" := "Standard Cost Amount" * "Quantity (Glazing Call Line)";
      "Total Sales Amount" := "Sales Amount" * "Quantity (Glazing Call Line)";

      CalcVATAmount;  // RFC 547
    END;

    PROCEDURE UpdateTotalWeight@1100525000();
    BEGIN
      VALIDATE("Total Weight", "Surface Area (m2)"* "Item Weight");
    END;

    PROCEDURE UpdateVatProdPostingGrp@1100525018() VatProdPostingGrp : Code[20];
    VAR
      ServiceSetup@1100525001 : Record 11012800;
      ItemRec@1100525002 : Record 27;
      ServLocRec@1100525003 : Record 11012801;
      GlazingServiceCall@1100525004 : Record 11125777;
      Customer@1100525005 : Record 18;
      DimValRec@1100525006 : Record 349;
      DimMgt@1100525000 : Codeunit 408;
    BEGIN
      // RFC 547
      VatProdPostingGrp := '';
      GlazingServiceCall.GET("Glazing Service Call No.");
      ServiceSetup.GET;

      IF (ServiceSetup."Source VAT Prod. Posting Group" =
          ServiceSetup."Source VAT Prod. Posting Group"::"Item/CostObject") THEN
      BEGIN

        IF NOT ItemRec.GET("Item No.") THEN
          ItemRec.INIT;

        IF (ItemRec."VAT Prod. Posting Group" <> '') THEN
        BEGIN
          VatProdPostingGrp := ItemRec."VAT Prod. Posting Group";
        END;

        IF VatProdPostingGrp = '' THEN BEGIN
          DimMgt.GetDimValueRec(2,"Global Dimension 2 Code",DimValRec,FALSE,'');
          IF (DimValRec."VAT Prod. Posting Group" <> '') THEN
          BEGIN
            VatProdPostingGrp := DimValRec."VAT Prod. Posting Group";
          END;
        END;
      END;

      IF VatProdPostingGrp = '' THEN BEGIN
        IF ServLocRec.GET(GlazingServiceCall."Service Location No.") THEN BEGIN
          IF ServLocRec."VAT Prod. Posting Group" <> '' THEN
            VatProdPostingGrp := ServLocRec."VAT Prod. Posting Group";
        END;
      END;

      IF VatProdPostingGrp = '' THEN BEGIN
        IF Customer.GET(GlazingServiceCall."Customer No.") THEN
          VatProdPostingGrp := Customer."VAT Prod. Posting Group";
      END;

      IF VatProdPostingGrp = '' THEN BEGIN
        VatProdPostingGrp := ServiceSetup."VAT Prod. Posting Group"
      END;
    END;

    PROCEDURE CalcVATAmount@1100525019();
    VAR
      GlazingServiceCall@1100525001 : Record 11125777;
    BEGIN
      // RFC 547
      GlazingServiceCall.GET("Glazing Service Call No.");
      CalcVATAmount2(GlazingServiceCall);
    END;

    PROCEDURE CalcVATAmount2@1100525002(GlazingServiceCall@1100525001 : Record 11125777);
    VAR
      VATPostingSetup@1100525000 : Record 325;
      Currency@1100525002 : Record 4;
    BEGIN
      // RFC 547
      IF NOT VATPostingSetup.GET(GlazingServiceCall."VAT Bus. Posting Group",
                                 "VAT Prod. Posting Group")
      THEN
        VATPostingSetup.INIT;

      "VAT %" := 0;
      "Total Sales VAT Amount" := 0;
      "Total Sales Amount incl. VAT" := "Total Sales Amount";
      IF VATPostingSetup."VAT Calculation Type" = VATPostingSetup."VAT Calculation Type"::"Normal VAT" THEN BEGIN
        "VAT %" := VATPostingSetup."VAT %";
        Currency.InitRoundingPrecision;
        "Total Sales Amount incl. VAT" := ROUND(
           "Total Sales Amount" * (100 + "VAT %") /100,
           Currency."Unit-Amount Rounding Precision");
        "Total Sales VAT Amount" := "Total Sales Amount incl. VAT" - "Total Sales Amount";
      END;
    END;

    PROCEDURE LookupItemNoWithSalesPrice@1100528400();
    VAR
      GlazingServiceCall@1100528401 : Record 11125777;
      Item@1100528400 : Record 27;
    BEGIN
      //kkleermaker, 110721, M23540
      //db, 31-08-15: function was local (for lookup table field ItemNo, but now used on page)
      GlazingServiceCall.GET("Glazing Service Call No.");
      Item.LookupItemNoWithSalesPrice(
        "Item No.",
        Quantity,
        1,
        GlazingServiceCall."Customer No.",
        GlazingServiceCall.Date);
      VALIDATE("Item No.");
    END;

    PROCEDURE UpdateUnitPrice@1100528600();
    BEGIN
      VALIDATE("Unit Price", "Basic Price" * (100 - "Item Discount %") / 100);
    END;

    BEGIN
    END.
  }
}

