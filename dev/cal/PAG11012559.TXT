OBJECT Page 11012559 Plant Inventory
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Werkzeuglager;
               ENU=Plant Inventory;
               NLD=Materieelvoorraad;
               NOR=Maskinlager;
               SVE=Maskinlager];
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table11012555;
    SourceTableView=WHERE(Removed=CONST(No));
    PageType=List;
    UsageCategory=Tasks;
    OnInit=BEGIN
             ShowInventoryLocationsSameProjectEnabled := TRUE;
           END;

    OnOpenPage=BEGIN
                 PlantSetup.GET;
                 IF (NOT PlantSetup."Rate Codes") AND (Details = Details::RateCode) AND (NOT gSetDetail) THEN
                   Details := Details::PlantNo;
                 IF gSetDetail THEN
                   Details := gDetails;
                 DetailsEditable := (NOT gSetMultiSelection);
                 PrintBtnVisible := (gCompany = '');
                 SetVisibleOnDetails();
               END;

    OnNextRecord=VAR
                   CurrentSteps@1100525001 : Integer;
                   Step@1100525002 : Integer;
                   NumberOfSteps@1100525003 : Integer;
                   EOF@1100525004 : Boolean;
                 BEGIN
                   Rec2.COPY(Rec);

                   IF Details = Details::InventEntry THEN BEGIN
                     CurrentSteps := Rec2.NEXT(Steps);
                     IF CurrentSteps <> 0 THEN
                       Rec := Rec2;
                     EXIT(CurrentSteps);
                   END;

                   //Less Detailed
                   IF Steps < 0 THEN
                     Step := -1
                   ELSE
                     Step := 1;

                   CurrentSteps := 0;
                   EOF := Rec2.NEXT(Step) = 0;
                   IF NOT EOF THEN
                     FOR NumberOfSteps := 1 TO ABS(Steps) DO BEGIN
                       WHILE SameRec(Rec, Rec2) AND NOT EOF DO
                         EOF := Rec2.NEXT(Step) = 0;
                       IF NOT EOF THEN BEGIN
                         Rec := Rec2;
                         CurrentSteps := CurrentSteps + Step;
                       END;
                     END;

                   EXIT(CurrentSteps);
                 END;

    OnAfterGetRecord=VAR
                       locPlantType@1100529700 : Record 11012551;
                     BEGIN
                       ReadDataOtherCompany();
                       IF (GETFILTER("Present from") = '') THEN  //* Fill "Date Filter" with "Present from" filter
                         SETRANGE("Date Filter")
                       ELSE
                         SETFILTER("Date Filter",'..%1',GETRANGEMAX("Present from"));

                       EmployeeHideValue := (NOT CheckShowEmployee());
                       GetInspectionDate();
                       MeterReadingInspection();  //DP00614
                       RentalAgreementLocaction := GetLocationCurrentRentalAgreement(RentalAgreementLocStyleExpr);
                       IF (Details = Details::InventEntry) AND ("Arrival Order Type" = "Arrival Order Type"::ReturnedExit) THEN
                         Element := GetElementReturnedExit();
                       CALCFIELDS(Bulk);
                       IF gCompany <> '' THEN BEGIN
                         "Location Description" := gCompPlantLoc.Description;
                         "Employee Location" := (gCompPlantLoc."Employee No." <> '');
                         "Plant Type Description" := gCompPlantType.Description;
                         "Plant No. Description" := gCompPlantNumber.Description;
                         "Plant Type External" := gCompPlantType.External;
                         "Plant No. External" := gCompPlantNumber.External;
                         "Inspection No." := gCompPlantNumber."Inspection No.";
                         Bulk := gCompPlantType.Bulk;
                         "Weight / Unit" := gCompPlantType.Weight;
                         "Current Price / Unit" := gCompPlantType."Current Price";
                       END;
                       PlantNumberCardEnabled := ((Details <> Details::PlantType) AND ("Plant No." <> '0') AND (gCompany = ''));
                       ExpectedReturnOnHideValue := (NOT ExpectedReturnOnVisible) OR ((Details <> Details::InventEntry) AND Bulk);  //C039437
                       RentalAgreementHideValue := ((Details = Details::PlantType) OR Bulk);

                       SETRANGE("Plant Group Filter","Plant Group");
                       Qty := CompressedQuantity(Details,FALSE);
                       SoldQty := SoldQuantity(Details);
                       NegativeQty := CompressedQuantity(Details,TRUE);
                       SETRANGE("Plant Group Filter");

                       IF locPlantType.GET("Plant Type") THEN BEGIN
                         AlternativeQty := locPlantType."Quantity Alt. Unit of Measure" * Qty;
                         AlternativeUnit := locPlantType."Alternative Unit of Measure";
                       END ELSE BEGIN
                         AlternativeQty := 0;
                         AlternativeUnit := '';
                       END;

                       IF NOT PlantNumber2.GET("Plant Type", "Plant No.") THEN
                         PlantNumber2.INIT;
                     END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 31      ;1   ;Action    ;
                      Name=PrintBtn;
                      Ellipsis=Yes;
                      CaptionML=[DEU=D&rucken;
                                 ENU=&Print;
                                 NLD=&Afdrukken;
                                 NOR=Skriv &ut;
                                 SVE=Skriv &ut];
                      Promoted=Yes;
                      Visible=PrintBtnVisible;
                      Image=Print;
                      PromotedCategory=Report;
                      OnAction=VAR
                                 PlantInventory@1100529000 : Record 11012555;
                                 ReportSelections@1100529001 : Record 77;
                                 DocumentPrint@1100529002 : Codeunit 229;
                                 InventoryReport@1100525000 : Report 11012553;
                               BEGIN
                                 ReportSelections.SETRANGE(Usage, 1598);
                                 IF (NOT ReportSelections.FINDFIRST) OR ((ReportSelections.COUNT = 1) AND (ReportSelections."Report ID" = 11012553)) THEN BEGIN
                                   PlantInventory.COPY(Rec);
                                   PlantInventory.SETRANGE(Removed);  //* Hard filter in report, must not be a selection filter
                                   PlantInventory.SETRANGE("Sold Filter");
                                   InventoryReport.SetDetails(Details);
                                   InventoryReport.SETTABLEVIEW(PlantInventory);
                                   InventoryReport.RUN;
                                 END ELSE BEGIN
                                   PlantInventory.COPYFILTERS(Rec);
                                   PlantInventory.SETRANGE("Sold Filter");
                                   DocumentPrint.PrintPlantInventory(PlantInventory);
                                 END;
                               END;
                                }
      { 1100528400;1 ;Action    ;
                      CaptionML=[DEU=Lagerbestand von Lagerorten mit demselben Projekt zeigen;
                                 ENU=Show Inventory of Locations of Same Project;
                                 NLD=Voorraad van locaties van hetzelfde project tonen];
                      Visible=FALSE;
                      Enabled=ShowInventoryLocationsSameProjectEnabled;
                      Image=Filter;
                      OnAction=BEGIN
                                 SetLocationFilterToLocationsOfSameProject;
                               END;
                                }
      { 1100529500;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 1100529501;1 ;Action    ;
                      CaptionML=[DEU=Werkzeugnummernkarte;
                                 ENU=Plant Number Card;
                                 NLD=Materieelnummerkaart];
                      Promoted=Yes;
                      Enabled=PlantNumberCardEnabled;
                      Image=Card;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 RunPlantNumberCard;
                               END;
                                }
      { 1100527350;1 ;Action    ;
                      CaptionML=[DEU=Erwartete RÅcksendung;
                                 ENU=Expected Return;
                                 NLD=Verwachte retour];
                      Promoted=Yes;
                      Image=EditLines;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 RunExpectedReturn();
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100525001;1;Group  ;
                GroupType=Group }

    { 1100525003;2;Field  ;
                CaptionML=[DEU=Details;
                           ENU=Details;
                           NLD=Detaillering;
                           NOR=Detaljer;
                           SVE=Detaljer];
                OptionCaptionML=[DEU=LagerÑnderung,Werkzeugtyp,Werkzeugnr.,Tarifcode;
                                 ENU=Inventory Entry,Plant Type,Plant No.,Rate Code;
                                 NLD=Voorraadmutatie,Materieelsoort,Materieelnummer,Tariefcode];
                SourceExpr=Details;
                Editable=DetailsEditable;
                OnValidate=BEGIN
                             SetVisibleOnDetails();
                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 1   ;1   ;Group     ;
                Editable=FALSE;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                SourceExpr=Location }

    { 4   ;2   ;Field     ;
                DrillDown=No;
                SourceExpr="Location Description" }

    { 1100485021;2;Field  ;
                SourceExpr="Employee Location";
                Visible=FALSE }

    { 6   ;2   ;Field     ;
                SourceExpr="Plant Type" }

    { 1100529702;2;Field  ;
                SourceExpr="Plant Group";
                Visible=FALSE }

    { 1100525004;2;Field  ;
                SourceExpr="Rental Type" }

    { 8   ;2   ;Field     ;
                SourceExpr="Plant No.";
                Visible=PlantNoLevelVisible;
                HideValue=PlantNoLevelHideValue }

    { 10  ;2   ;Field     ;
                CaptionML=[DEU=Werkzeugbeschreibung;
                           ENU=Plant Description;
                           NLD=Materieel omschrijving;
                           NOR=Maskinbeskrivelse;
                           SVE=Maskinbeskrivning];
                SourceExpr=GetPlantDescription }

    { 1210190000;2;Field  ;
                DrillDown=No;
                SourceExpr="Plant Type Description";
                Visible=FALSE }

    { 1210190002;2;Field  ;
                DrillDown=No;
                SourceExpr="Plant No. Description";
                Visible=FALSE;
                HideValue=PlantNoLevelHideValue }

    { 1100485013;2;Field  ;
                DrillDown=No;
                SourceExpr="Inspection No.";
                Visible=FALSE;
                HideValue=PlantNoLevelHideValue }

    { 1100485002;2;Field  ;
                SourceExpr="Rate Code";
                Visible=RateCodeVisible;
                HideValue=RateCodeHideValue }

    { 1100485011;2;Field  ;
                CaptionML=[DEU=Extern;
                           ENU=External;
                           NLD=Extern;
                           NOR=Eksternt;
                           SVE=Externt];
                SourceExpr=GetExternal() }

    { 1100529901;2;Field  ;
                DrillDown=No;
                SourceExpr="Plant Type External";
                Visible=FALSE }

    { 1100529900;2;Field  ;
                DrillDown=No;
                SourceExpr="Plant No. External";
                Visible=FALSE }

    { 12  ;2   ;Field     ;
                CaptionML=[DEU=Menge;
                           ENU=Quantity;
                           NLD=Aantal;
                           NOR=Antall;
                           SVE=Antal];
                DecimalPlaces=0:5;
                BlankZero=Yes;
                SourceExpr=Qty }

    { 1100409003;2;Field  ;
                CaptionML=[DEU=Menge inkl. neg. Invent.;
                           ENU=Quantity Incl. Neg. Invent.;
                           NLD=Aantal incl. neg. vrd.];
                DecimalPlaces=0:5;
                BlankZero=Yes;
                SourceExpr=NegativeQty }

    { 1100529700;2;Field  ;
                Name=Quantity Alternative Unit of Measure;
                CaptionML=[DEU=Menge alternative Ma·einheit;
                           ENU=Quantity Alternative Unit of Measure;
                           NLD=Aantal alternatieve eenheid];
                SourceExpr=AlternativeQty;
                Visible=FALSE;
                Editable=FALSE }

    { 1100529701;2;Field  ;
                Name=Alternative Unit of Measure;
                CaptionML=[DEU=Alternative Ma·einheit;
                           ENU=Alternative Unit of Measure;
                           NLD=Alternatieve eenheid];
                SourceExpr=AlternativeUnit;
                Visible=FALSE;
                Editable=FALSE }

    { 14  ;2   ;Field     ;
                CaptionML=[DEU=Verkaufte Anzahl;
                           ENU=Number Sold;
                           NLD=Verkocht aantal;
                           NOR=Antall solgte;
                           SVE=Antal sÜlda];
                DecimalPlaces=0:5;
                BlankZero=Yes;
                SourceExpr=SoldQty }

    { 1100485009;2;Field  ;
                DrillDown=No;
                SourceExpr="Vendor No.";
                Visible=PlantNoLevelVisible;
                HideValue=PlantNoLevelHideValue;
                OnLookup=VAR
                           Vend@1100485000 : Record 23;
                         BEGIN
                           IF (gCompany <> '') THEN
                             EXIT(FALSE);

                           Vend."No." := "Vendor No.";
                           PAGE.RUNMODAL(0, Vend);
                         END;
                          }

    { 1100525000;2;Field  ;
                Name=VendorName;
                DrillDown=No;
                CaptionML=[DEU=Kreditorenname;
                           ENU=Vendor Name;
                           NLD=Leveranciersnaam;
                           NOR=Leverandõrnavn;
                           SVE=Leverantîrsnamn];
                SourceExpr=GetVendorName();
                Visible=PlantNoLevelVisible;
                HideValue=PlantNoLevelHideValue }

    { 1100528500;2;Field  ;
                DrillDown=No;
                SourceExpr="Purchase Order No.";
                Visible=FALSE }

    { 16  ;2   ;Field     ;
                SourceExpr="Present from";
                Visible=InventEntryLevelVisible;
                HideValue=InventEntryLevelHideValue }

    { 1100485005;2;Field  ;
                SourceExpr="Rental Start Date";
                Visible=InventEntryLevelVisible;
                HideValue=InventEntryLevelHideValue }

    { 1100485000;2;Field  ;
                SourceExpr="Expected return on";
                Visible=ExpectedReturnOnVisible;
                Editable=FALSE;
                HideValue=ExpectedReturnOnHideValue }

    { 18  ;2   ;Field     ;
                SourceExpr="Arrival Order Type";
                Visible=InventEntryLevelVisible;
                HideValue=InventEntryLevelHideValue }

    { 20  ;2   ;Field     ;
                SourceExpr="Arrival Order";
                Visible=InventEntryLevelVisible;
                HideValue=InventEntryLevelHideValue }

    { 22  ;2   ;Field     ;
                Lookup=No;
                DrillDown=No;
                BlankZero=Yes;
                SourceExpr="Arrival Order Line";
                Visible=InventEntryLevelVisible;
                HideValue=InventEntryLevelHideValue }

    { 1100409001;2;Field  ;
                DrillDown=No;
                SourceExpr="Arrival Order Line Descr. 1";
                Visible=FALSE;
                HideValue=InventEntryLevelHideValue }

    { 1100409002;2;Field  ;
                DrillDown=No;
                SourceExpr="Arrival Order Line Descr. 2";
                Visible=FALSE;
                HideValue=InventEntryLevelHideValue }

    { 1100485007;2;Field  ;
                DrillDown=No;
                SourceExpr="Arrival Transport Order";
                Visible=TransportVisible;
                HideValue=TransportHideValue;
                OnLookup=BEGIN
                           IF (gCompany <> '') THEN
                             EXIT;
                           LookupTransportOrder(TRUE);
                         END;
                          }

    { 1210190004;2;Field  ;
                Name=DateInspectBefore;
                CaptionML=[DEU=Inspizieren vor dem;
                           ENU=Inspect before;
                           NLD=Keuren voor;
                           NOR=Inspiser fõr;
                           SVE=Inspektera fîre];
                SourceExpr=DateInspectBefore;
                Visible=PlantNoLevelVisible;
                HideValue=PlantNoLevelHideValue }

    { 1100529001;2;Field  ;
                CaptionML=[DEU=ZÑhlerstand;
                           ENU=Meter Reading;
                           NLD=Meterstand;
                           NOR=Meteravlesning;
                           SVE=MÑtaravlÑsning];
                DecimalPlaces=0:0;
                BlankZero=Yes;
                SourceExpr=ActMeterReading;
                Visible=MeterReadingVisible;
                HideValue=MeterReadingHideValue;
                OnDrillDown=BEGIN
                              //DP00614
                              DrillDownMeterReading();
                            END;
                             }

    { 1100529000;2;Field  ;
                CaptionML=[DEU=Einheit ZÑhlerstand;
                           ENU=Unit Meter Reading;
                           NLD=Eenheid meterstand];
                SourceExpr=ActualMeterReadingUnit();
                Visible=MeterReadingVisible;
                HideValue=MeterReadingHideValue }

    { 1100529002;2;Field  ;
                CaptionML=[DEU=VerfÅgbar (Meter) bis nÑchste Inspektion;
                           ENU=Available (Meter) until Next Inspection;
                           NLD=Beschikbaar (meter) tot volgende keuring];
                DecimalPlaces=0:0;
                BlankZero=Yes;
                SourceExpr=AvailableMR;
                Visible=MeterReadingVisible;
                HideValue=AvailableMRHideValue;
                Style=Attention;
                StyleExpr=AvailableNegative }

    { 1100525002;2;Field  ;
                DrillDown=No;
                SourceExpr=Element;
                Visible=InventEntryLevelVisible;
                HideValue=InventEntryLevelHideValue }

    { 1100485015;2;Field  ;
                SourceExpr="Employee Company";
                Visible=PlantNoLevelVisible;
                HideValue=EmployeeHideValue }

    { 1100485017;2;Field  ;
                SourceExpr="Employee No.";
                Visible=PlantNoLevelVisible;
                Editable=PlantNoLevelHideValue;
                HideValue=EmployeeHideValue }

    { 1100485019;2;Field  ;
                Name=EmplName;
                CaptionML=[DEU=Name Mitarbeiter;
                           ENU=Employee Name;
                           NLD=Naam werknemer;
                           NOR=Navn pÜ ansatt;
                           SVE=Namn pÜ anstÑlld];
                SourceExpr=GetEmployeeName();
                Visible=PlantNoLevelVisible;
                HideValue=EmployeeHideValue }

    { 1100409000;2;Field  ;
                SourceExpr=Bulk;
                Visible=FALSE;
                Editable=FALSE }

    { 1100529600;2;Field  ;
                SourceExpr="Plant Category";
                Visible=FALSE }

    { 1100527000;2;Field  ;
                SourceExpr="Department Code";
                Visible=FALSE }

    { 1100529003;2;Field  ;
                DrillDown=No;
                SourceExpr="Weight / Unit";
                Visible=FALSE }

    { 1100529004;2;Field  ;
                DrillDown=No;
                SourceExpr="Current Price / Unit";
                Visible=FALSE }

    { 1100525005;2;Field  ;
                CaptionML=[DEU=Mietvertrag (Aktuell);
                           ENU=Rental Agreement (Current);
                           NLD=Huurafspraak (huidige)];
                SourceExpr=FORMAT(HasCurrentRentalAgreement);
                HideValue=RentalAgreementHideValue }

    { 1100529005;2;Field  ;
                CaptionML=[DEU=Mietvertrag Lagerort;
                           ENU=Location Rental Agreement;
                           NLD=Locatie huurafspraak];
                SourceExpr=RentalAgreementLocaction;
                HideValue=RentalAgreementHideValue;
                Style=Strong;
                StyleExpr=RentalAgreementLocStyleExpr }

    { 1100528100;2;Field  ;
                CaptionML=[DEU=Seriennr.;
                           ENU=Serial No.;
                           NLD=Serienr.];
                SourceExpr=PlantNumber2."Serial No.";
                Editable=FALSE;
                HideValue=PlantNoLevelHideValue }

    { 1100528101;2;Field  ;
                CaptionML=[DEU=Steuernummer;
                           ENU=Registration No.;
                           NLD=Registratienr.];
                SourceExpr=PlantNumber2."Registration No.";
                Editable=FALSE;
                HideValue=PlantNoLevelHideValue }

  }
  CODE
  {
    VAR
      PlantSetup@1100485000 : Record 11012550;
      Rec2@1100525016 : Record 11012555;
      gCompPlantType@1100525001 : Record 11012551;
      gCompPlantNumber@1100525002 : Record 11012552;
      gCompPlantLoc@1100525003 : Record 11012554;
      DateInspectBefore@1210190000 : Date;
      gDetails@1100485001 : Option;
      Details@11012001 : 'InventEntry,PlantType,PlantNo,RateCode';
      gSetDetail@1100485002 : Boolean;
      gSetMultiSelection@1100525005 : Boolean;
      DetailsEditable@1100525014 : Boolean INDATASET;
      PrintBtnVisible@19070540 : Boolean INDATASET;
      PlantNoLevelVisible@1100525000 : Boolean INDATASET;
      InventEntryLevelVisible@1100525006 : Boolean INDATASET;
      RateCodeVisible@1100525008 : Boolean INDATASET;
      TransportVisible@1100525011 : Boolean INDATASET;
      MeterReadingVisible@1100529002 : Boolean INDATASET;
      ExpectedReturnOnVisible@1100525004 : Boolean INDATASET;
      PlantNoLevelHideValue@1100525007 : Boolean INDATASET;
      InventEntryLevelHideValue@1100525010 : Boolean INDATASET;
      RateCodeHideValue@1100525009 : Boolean INDATASET;
      TransportHideValue@1100525012 : Boolean INDATASET;
      EmployeeHideValue@1100525015 : Boolean INDATASET;
      MeterReadingHideValue@1100529003 : Boolean INDATASET;
      AvailableMRHideValue@1100529007 : Boolean INDATASET;
      AvailableNegative@1100529008 : Boolean INDATASET;
      ExpectedReturnOnHideValue@1100525017 : Boolean INDATASET;
      RentalAgreementHideValue@1100529004 : Boolean INDATASET;
      PlantNumberCardEnabled@1100529500 : Boolean INDATASET;
      RentalAgreementLocStyleExpr@1100529005 : Boolean INDATASET;
      ShowInventoryLocationsSameProjectEnabled@1100529009 : Boolean INDATASET;
      ActMeterReading@1100529000 : Decimal;
      AvailableMR@1100529001 : Decimal;
      RentalAgreementLocaction@1100529006 : Code[20];
      gCompany@1100525013 : Text[30];
      AlternativeQty@1100529701 : Decimal;
      AlternativeUnit@1100529700 : Code[10];
      Qty@1100529702 : Decimal;
      SoldQty@1100528500 : Decimal;
      NegativeQty@1100529703 : Decimal;
      PlantNumber2@1100528100 : Record 11012552;

    PROCEDURE SameRec@1(PlantInventory1@1100528500 : Record 11012555;PlantInventory2@1100528501 : Record 11012555) : Boolean;
    BEGIN
      IF (Details = Details::InventEntry) OR
         (PlantInventory2.Location <> PlantInventory1.Location) OR (PlantInventory2."Plant Type" <> PlantInventory1."Plant Type") OR (PlantInventory2."Plant Group" <> PlantInventory1."Plant Group")
      THEN
        EXIT(FALSE);

      IF (Details = Details::PlantNo) THEN
        EXIT(PlantInventory2."Plant No." = PlantInventory1."Plant No.");

      IF Details = Details::RateCode THEN
        EXIT((PlantInventory2."Plant No." = PlantInventory1."Plant No.") AND (PlantInventory2."Rate Code" = PlantInventory1."Rate Code"));

      EXIT(TRUE);
    END;

    PROCEDURE SetVisibleOnDetails@16();
    BEGIN
      PlantNoLevelVisible := (Details <> Details::PlantType);
      PlantNoLevelHideValue := (NOT PlantNoLevelVisible);
      InventEntryLevelVisible := (Details = Details::InventEntry);
      InventEntryLevelHideValue := (NOT InventEntryLevelVisible);
      RateCodeVisible := (Details = Details::RateCode) OR (PlantSetup."Rate Codes" AND (Details = Details::InventEntry));
      RateCodeHideValue := ((Details = Details::PlantType) OR (Details = Details::PlantNo));
      TransportVisible := (Details = Details::InventEntry) AND PlantSetup."Transport Orders" AND (gCompany = '');
      TransportHideValue := (NOT TransportVisible);
      ExpectedReturnOnVisible := (Details <> Details::PlantType);  //C039437
    END;

    PROCEDURE GetInspectionDate@1210190000();
    VAR
      Inspection@1210190000 : Record 11012564;
    BEGIN
      DateInspectBefore := 0D;
      IF Details = Details::PlantType THEN
        EXIT;

      IF gCompany <> '' THEN
        Inspection.CHANGECOMPANY(gCompany);
      Inspection.SETCURRENTKEY("Plant Type", "Plant No.", "Inspect before");
      Inspection.SETRANGE("Plant Type", "Plant Type");
      Inspection.SETRANGE("Plant No.", "Plant No.");
      Inspection.SETRANGE("Inspection Result", Inspection."Inspection Result"::" ");
      IF Inspection.FINDFIRST THEN
        DateInspectBefore := Inspection."Inspect before";
    END;

    LOCAL PROCEDURE MeterReadingInspection@1100529000();
    VAR
      PlantMeterType@1100529000 : Record 11229921;
      PlantMeter@1100529001 : Code[10];
      MeterType@1100529002 : Code[10];
    BEGIN
      //DP00614
      AvailableMRHideValue := TRUE;
      IF gCompany <> '' THEN BEGIN
        MeterReadingVisible := FALSE;
        MeterReadingHideValue := TRUE;
      END ELSE BEGIN
        IF PlantNoLevelVisible THEN BEGIN
          ActMeterReading := ActualMeterReading();
          PlantMeterType.DetermineMeterAndMeterType_PlantNo("Plant Type", "Plant No.", PlantMeter, MeterType);
          AvailableMR := PlantMeterType.AvailableMeterReadingUntilNextInspection("Plant Type", "Plant No.", PlantMeter, MeterType, AvailableMRHideValue);
          AvailableMRHideValue := (NOT AvailableMRHideValue);
          AvailableNegative := (AvailableMR < 0);
        END;
        MeterReadingVisible := PlantNoLevelVisible;
        MeterReadingHideValue := PlantNoLevelHideValue;
      END;
    END;

    PROCEDURE GetExternal@1100485001() : Boolean;
    VAR
      PlantType@1100485000 : Record 11012551;
      PlantNumber@1100485001 : Record 11012552;
    BEGIN
      IF (Details = Details::PlantType) OR ("Plant No." = '') OR ("Plant No." = '0') THEN BEGIN
        IF gCompany <> '' THEN
          PlantType.CHANGECOMPANY(gCompany);
        IF PlantType.GET("Plant Type") THEN
          EXIT(PlantType.External);
      END ELSE BEGIN
        IF gCompany <> '' THEN
          PlantNumber.CHANGECOMPANY(gCompany);
        IF PlantNumber.GET("Plant Type", "Plant No.") THEN
          EXIT(PlantNumber.External);
      END;

      EXIT(FALSE);
    END;

    PROCEDURE GetPlantDescription@1100525011() : Text[50];
    BEGIN
      IF gCompany = '' THEN
        EXIT(PlantDescr(Details))
      ELSE BEGIN
        IF (Details <> Details::PlantType) AND
           ("Plant No." <> '') AND ("Plant No." <> '0') AND (gCompPlantNumber.Description <> '')
        THEN
          EXIT(gCompPlantNumber.Description);
        EXIT(gCompPlantType.Description);
      END;
    END;

    PROCEDURE SetDetailSelection@1100485002(IDetails@1100485000 : Option);
    BEGIN
      gSetDetail := TRUE;
      gDetails := IDetails;
    END;

    PROCEDURE GetDetailSelection@1100485000(VAR ODetails@1100485000 : Option);
    BEGIN
      ODetails := Details;
    END;

    PROCEDURE GetEmployeeName@1100485003() : Text[200];
    VAR
      Empl@1100485000 : Record 5200;
    BEGIN
      IF CheckShowEmployee() THEN BEGIN
        IF "Employee Company" <> '' THEN
          Empl.CHANGECOMPANY("Employee Company")
        ELSE BEGIN
          IF (gCompany <> '') THEN
            Empl.CHANGECOMPANY(gCompany);
        END;
        IF Empl.GET("Employee No.") THEN
          EXIT(Empl."Full Name");
      END;

      EXIT('');
    END;

    PROCEDURE CheckShowEmployee@1100485005() : Boolean;
    VAR
      PlantInvent@1100485000 : Record 11012555;
    BEGIN
      IF Details = Details::PlantType THEN
        EXIT(FALSE);

      IF ("Employee No." <> '') AND (Details <> Details::InventEntry) THEN BEGIN
        IF (gCompany <> '') THEN
          PlantInvent.CHANGECOMPANY(gCompany);
        PlantInvent.SETRANGE(Removed, FALSE);
        PlantInvent.SETRANGE(Location, Location);
        PlantInvent.SETRANGE("Plant Type", "Plant Type");
        PlantInvent.SETRANGE("Plant No.", "Plant No.");
        IF Details = Details::RateCode THEN
          PlantInvent.SETRANGE("Rate Code", "Rate Code");
        PlantInvent.SETFILTER("Employee No.", '<>%1', "Employee No.");
        IF NOT PlantInvent.ISEMPTY THEN
          EXIT(FALSE);
      END;

      EXIT(TRUE);
    END;

    PROCEDURE SetCompany@1100525000(ICompany@1100485000 : Text[30]);
    BEGIN
      IF (ICompany = '') OR (ICompany = COMPANYNAME) THEN
        gCompany := ''
      ELSE BEGIN
        gCompany := ICompany;
        Rec.CHANGECOMPANY(gCompany);
        Rec2.CHANGECOMPANY(gCompany);
        PlantSetup.CHANGECOMPANY(gCompany);
        gCompPlantType.CHANGECOMPANY(gCompany);
        gCompPlantNumber.CHANGECOMPANY(gCompany);
        gCompPlantLoc.CHANGECOMPANY(gCompany);
        PlantNumber2.CHANGECOMPANY(gCompany);
      END;
    END;

    PROCEDURE ReadDataOtherCompany@1100525002();
    BEGIN
      IF (gCompany = '') THEN
        EXIT;

      IF NOT gCompPlantType.GET("Plant Type") THEN
        gCompPlantType.INIT;
      IF NOT gCompPlantNumber.GET("Plant Type", "Plant No.") THEN
        gCompPlantNumber.INIT;
      IF NOT gCompPlantLoc.GET(Location) THEN
        gCompPlantLoc.INIT;
    END;

    PROCEDURE SetMultiSelection@1100485011();
    BEGIN
      //* Called from Plant Order and Plant Request: Function 'Insert Plant Present (Selection)'
      gSetMultiSelection := TRUE;
    END;

    PROCEDURE GetMarkedRecords@1100485015(VAR TmpPlantInvent@1100485000 : TEMPORARY Record 11012555) : Boolean;
    VAR
      MarkedPlantInvent@1100525000 : Record 11012555;
      PlantInventoryPrev@1100528500 : Record 11012555;
    BEGIN
      //* NOTE: In the calling process 'TmpPlantInventRec' must be 'Temporary' (and empty before calling)!
      IF (gCompany <> '') THEN
        MarkedPlantInvent.CHANGECOMPANY(gCompany);
      CurrPage.SETSELECTIONFILTER(MarkedPlantInvent);
      MarkedPlantInvent.SETCURRENTKEY(Removed,Location,"Plant Type","Plant No.","Rate Code","Line No.","Sales Order Line No.");

      IF MarkedPlantInvent.FINDSET(FALSE,FALSE) THEN BEGIN
        REPEAT
          IF NOT SameRec(MarkedPlantInvent, PlantInventoryPrev) THEN BEGIN
            MarkedPlantInvent.SETRANGE("Plant Group Filter",MarkedPlantInvent."Plant Group");
            IF NOT MarkedPlantInvent.Sold THEN
              MarkedPlantInvent.Quantity := MarkedPlantInvent.CompressedQuantity(Details,FALSE)
            ELSE
              MarkedPlantInvent.Quantity := MarkedPlantInvent.SoldQuantity(Details);
            MarkedPlantInvent.SETRANGE("Plant Group Filter");
            TmpPlantInvent := MarkedPlantInvent;
            TmpPlantInvent.INSERT;
            PlantInventoryPrev := MarkedPlantInvent;
          END;
        UNTIL MarkedPlantInvent.NEXT = 0;
        EXIT(TRUE);
      END;
      EXIT(FALSE);
    END;

    LOCAL PROCEDURE RunPlantNumberCard@1100529500();
    VAR
      PlantNumber@1100529500 : Record 11012552;
    BEGIN
      IF (Details <> Details::PlantType) AND ("Plant No." <> '0') THEN BEGIN
        PlantNumber.GET("Plant Type", "Plant No.");
        PlantNumber.SETRANGE("Plant Type", "Plant Type");
        PAGE.RUNMODAL(PAGE::"Plant Number", PlantNumber);
      END;
    END;

    PROCEDURE SetInventoryLocationsSameProjectDisabled@1100529001();
    BEGIN
      ShowInventoryLocationsSameProjectEnabled := FALSE;
    END;

    BEGIN
    END.
  }
}

