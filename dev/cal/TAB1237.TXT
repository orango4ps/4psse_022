OBJECT Table 1237 Transformation Rule
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Code,Description;
    OnInsert=BEGIN
               CheckMandatoryFields;
             END;

    OnModify=BEGIN
               CheckMandatoryFields;
             END;

    CaptionML=[ENU=Transformation Rule;
               NOR=Transformasjonsregel;
               SVE=Omvandlingsregel];
    LookupPageID=Page1237;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 3   ;   ;Transformation Type ;Option        ;OnValidate=BEGIN
                                                                IF NOT ("Transformation Type" IN ["Transformation Type"::Replace,
                                                                                                  "Transformation Type"::"Regular Expression - Replace",
                                                                                                  "Transformation Type"::"Regular Expression - Match"])
                                                                THEN BEGIN
                                                                  "Find Value" := '';
                                                                  "Replace Value" := '';
                                                                END;
                                                                IF NOT ("Transformation Type" IN ["Transformation Type"::Substring]) THEN BEGIN
                                                                  "Start Position" := 0;
                                                                  Length := 0;
                                                                  "Ending Text" := '';
                                                                  "Starting Text" := '';
                                                                END;
                                                                IF NOT ("Transformation Type" IN ["Transformation Type"::"Date Formatting",
                                                                                                  "Transformation Type"::"Date and Time Formatting",
                                                                                                  "Transformation Type"::"Decimal Formatting"])
                                                                THEN BEGIN
                                                                  "Data Format" := '';
                                                                  "Data Formatting Culture" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Transformation Type;
                                                              NOR=Transformasjonstype;
                                                              SVE=Omvandlingstyp];
                                                   OptionCaptionML=[ENU=Uppercase,Lowercase,Title Case,Trim,Substring,Replace,Regular Expression - Replace,Remove Non-Alphanumeric Characters,Date Formatting,Decimal Formatting,Regular Expression - Match,Custom,Date and Time Formatting;
                                                                    NOR=Store bokstaver,SmÜ bokstaver,Store forbokstaver,Trimming,Delstreng,Erstatt,Regulërt uttrykk - erstatt,Fjern ikke-alfanumeriske tegn,Datoformatering,Desimalformatering,Regulërt uttrykk - samsvar,Egendefinert,Dato- og klokkeslettformatering;
                                                                    SVE=Versaler,Gemener,Inledande versal i ord,Trimma,DelstrÑng,ErsÑtt,ReguljÑrt uttryck - ErsÑtt,Ta bort icke-alfanumeriska tecken,Datumformatering,Decimalformatering,ReguljÑrt uttryck - Matcha,Anpassat];
                                                   OptionString=Uppercase,Lowercase,Title Case,Trim,Substring,Replace,Regular Expression - Replace,Remove Non-Alphanumeric Characters,Date Formatting,Decimal Formatting,Regular Expression - Match,Custom,Date and Time Formatting }
    { 10  ;   ;Find Value          ;Text250       ;OnValidate=BEGIN
                                                                IF NOT ("Transformation Type" IN ["Transformation Type"::Replace,
                                                                                                  "Transformation Type"::"Regular Expression - Replace",
                                                                                                  "Transformation Type"::"Regular Expression - Match"])
                                                                THEN
                                                                  TESTFIELD("Find Value",'');
                                                              END;

                                                   CaptionML=[ENU=Find Value;
                                                              NOR=Sõk etter verdi;
                                                              SVE=Hitta vÑrde] }
    { 11  ;   ;Replace Value       ;Text250       ;OnValidate=BEGIN
                                                                IF NOT ("Transformation Type" IN ["Transformation Type"::Replace,"Transformation Type"::"Regular Expression - Replace"]) THEN
                                                                  TESTFIELD("Replace Value",'');
                                                              END;

                                                   CaptionML=[ENU=Replace Value;
                                                              NOR=Erstatt verdi;
                                                              SVE=ErsÑtt vÑrde] }
    { 12  ;   ;Starting Text       ;Text250       ;OnValidate=BEGIN
                                                                IF "Starting Text" <> '' THEN BEGIN
                                                                  TESTFIELD("Transformation Type","Transformation Type"::Substring);
                                                                  VALIDATE("Start Position",0);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Starting Text;
                                                              NOR=Starttekst;
                                                              SVE=Starttext] }
    { 13  ;   ;Ending Text         ;Text250       ;OnValidate=BEGIN
                                                                IF "Ending Text" <> '' THEN BEGIN
                                                                  TESTFIELD("Transformation Type","Transformation Type"::Substring);
                                                                  VALIDATE(Length,0);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Ending Text;
                                                              NOR=Sluttekst;
                                                              SVE=Avslutande text] }
    { 15  ;   ;Start Position      ;Integer       ;OnValidate=BEGIN
                                                                IF "Transformation Type" = "Transformation Type"::Substring THEN
                                                                  IF "Start Position" < 0 THEN
                                                                    ERROR(MustBeGreaterThanZeroErr);

                                                                IF "Start Position" <> 0 THEN BEGIN
                                                                  TESTFIELD("Transformation Type","Transformation Type"::Substring);
                                                                  VALIDATE("Starting Text",'');
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Start Position;
                                                              NOR=Startposisjon;
                                                              SVE=Startposition];
                                                   BlankZero=Yes }
    { 16  ;   ;Length              ;Integer       ;OnValidate=BEGIN
                                                                IF "Transformation Type" = "Transformation Type"::Substring THEN
                                                                  IF Length < 0 THEN
                                                                    ERROR(MustBeGreaterThanZeroErr);

                                                                IF Length <> 0 THEN BEGIN
                                                                  TESTFIELD("Transformation Type","Transformation Type"::Substring);
                                                                  VALIDATE("Ending Text",'');
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Length;
                                                              NOR=Lengde;
                                                              SVE=LÑngd];
                                                   BlankZero=Yes }
    { 18  ;   ;Data Format         ;Text100       ;OnValidate=BEGIN
                                                                IF NOT ("Transformation Type" IN ["Transformation Type"::"Date Formatting",
                                                                                                  "Transformation Type"::"Date and Time Formatting",
                                                                                                  "Transformation Type"::"Decimal Formatting"])
                                                                THEN
                                                                  TESTFIELD("Data Format",'');
                                                              END;

                                                   CaptionML=[ENU=Data Format;
                                                              NOR=Dataformat;
                                                              SVE=Dataformat] }
    { 20  ;   ;Data Formatting Culture;Text10     ;OnValidate=BEGIN
                                                                IF NOT ("Transformation Type" IN ["Transformation Type"::"Date Formatting",
                                                                                                  "Transformation Type"::"Date and Time Formatting",
                                                                                                  "Transformation Type"::"Decimal Formatting"])
                                                                THEN
                                                                  TESTFIELD("Data Formatting Culture",'');
                                                              END;

                                                   CaptionML=[ENU=Data Formatting Culture;
                                                              NOR=Dataformateringskultur;
                                                              SVE=Dataformateringskultur] }
    { 30  ;   ;Next Transformation Rule;Code20    ;TableRelation="Transformation Rule".Code;
                                                   CaptionML=[ENU=Next Transformation Rule;
                                                              NOR=Neste transformasjonsregel;
                                                              SVE=NÑsta omvandlingsregel] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      MustBeGreaterThanZeroErr@1000 : TextConst 'ENU=The Value entered must be greater than zero.;NOR=Den angitte verdien mÜ vëre stõrre enn null.;SVE=Det angivna vÑrdet mÜste vara stîrre Ñn noll.';
      UPPERCASETxt@1001 : TextConst '@@@=Assigned to Transformation.Code field for Upper case;ENU=UPPERCASE;NOR=STORE BOKSTAVER;SVE=VERSALER';
      UpperCaseDescTxt@1007 : TextConst 'ENU=Upper Case Text;NOR=Tekst med store bokstaver;SVE=Text i versaler';
      LOWERCASETxt@1002 : TextConst '@@@=Assigned to Transformation.Code field for Lower case;ENU=LOWERCASE;NOR=SMè BOKSTAVER;SVE=GEMENER';
      LowerCaseDescTxt@1008 : TextConst 'ENU=Lower Case Text;NOR=Tekst med smÜ bokstaver;SVE=Text i gemener';
      TITLECASETxt@1003 : TextConst '@@@=Assigned to Transformation.Code field for Title case;ENU=TITLECASE;NOR=SAKSTITTEL;SVE=INLEDANDE VERSAL';
      TitleCaseDescTxt@1009 : TextConst 'ENU=Title Case Text;NOR=Tekst for sakstittel;SVE=Text med inledande versal';
      TRIMTxt@1004 : TextConst '@@@=Assigned to Transformation.Code field for Trim;ENU=TRIM;NOR=TRIM;SVE=TRIMMA';
      TrimDescTxt@1010 : TextConst 'ENU=Trim Text;NOR=Trim tekst;SVE=Trimma text';
      FOURTH_TO_SIXTH_CHARTxt@1005 : TextConst '@@@=Assigned to Transformation.Code field for getting the 4th to 6th characters in a string;ENU=FOURTH_TO_SIXTH_CHAR;NOR=4DE_TIL_6TE_TEGN;SVE=FJéRDE_TILL_SJéTTE_T';
      FourthToSixthCharactersDescTxt@1011 : TextConst 'ENU=Fourth to Sixth Characters Text;NOR=Tekst for fjerde til sjette tegn;SVE=Text i fjÑrde till sjÑtte tecknet';
      YYYYMMDDDateTxt@1014 : TextConst '@@@=Assigned to Transformation.Code field for converting dates from yyyyMMdd format;ENU=YYYYMMDD_DATE;NOR=èèèèMMDD_DATO;SVE=èèèèMMDD_DATUM';
      YYYYMMDDDateDescTxt@1013 : TextConst 'ENU=yyyyMMdd Date Text;NOR=Tekst for ÜÜÜÜMMdd-dato;SVE=ÜÜÜÜMMDD-datumtext';
      YYYYMMDDHHMMSSTxt@1022 : TextConst '@@@=Assigned to Transformation.Code field for converting dates from yyyyMMdd format;ENU=YYYYMMDDHHMMSS_FMT;NOR=YYYYMMDDHHMMSS_FMT';
      YYYYMMDDHHMMSSDescTxt@1021 : TextConst 'ENU=yyyyMMddHHmmss Date/Time Format;NOR=ÜÜÜÜddMMTTmmss Dato-/klokkeslettformat';
      ALPHANUMERIC_ONLYTxt@1006 : TextConst '@@@=Assigned to Transformation.Code field for getting only the Alphanumeric characters in a string;ENU=ALPHANUMERIC_ONLY;NOR=BARE_ALFANUMERISK;SVE=ENDAST_ALFANUMERISKT';
      AlphaNumericDescTxt@1012 : TextConst 'ENU=Alphanumeric Text Only;NOR=Bare alfanumerisk tekst;SVE=Endast alfanumerisk text';
      DKNUMBERFORMATTxt@1015 : TextConst '@@@=Assigned to Transformation.Code field for getting decimal formatting rule for Danish numbers;ENU=DK_DECIMAL_FORMAT;NOR=DK_DESIMALFORMAT;SVE=DK_DECIMAL_FORMAT';
      DKNUMBERFORMATDescTxt@1016 : TextConst 'ENU=Danish Decimal Format;NOR=Dansk desimalformat;SVE=Danskt decimalformat';
      USDATEFORMATTxt@1017 : TextConst '@@@=Assigned to Transformation.Code field for getting date formatting rule from U.S. dates;ENU=US_DATE_FORMAT;NOR=US_DATE_FORMAT;SVE=US_DATE_FORMAT';
      USDATEFORMATDescTxt@1018 : TextConst 'ENU=U.S. Date Format;NOR=Amerikansk datoformat;SVE=Datumformat (USA)';
      USDATETIMEFORMATTxt@1024 : TextConst '@@@=Assigned to Transformation.Code field for getting date formatting rule from U.S. dates;ENU=US_DATETIME_FORMAT;NOR=US_DATETIME_FORMAT';
      USDATETIMEFORMATDescTxt@1023 : TextConst 'ENU=U.S. Date/Time Format;NOR=Amerikansk dato-/klokkeslettformat';
      DeleteNOTPROVIDEDTxt@1019 : TextConst '@@@=NOTPROVIDED should stay in english because it is a constant value. DELETE should be translated.;ENU=DELETE_NOTPROVIDED;NOR=SLETT_NOTPROVIDED;SVE=DELETE_NOTPROVIDED';
      DeleteNOTPROVIDEDDescriptionTxt@1020 : TextConst '@@@=NOTPROVIDED should stay in english because it is a constant value. ''Delete'' and ''value'' should be translated.;ENU=Delete NOTPROVIDED value;NOR=Slett NOTPROVIDED-verdi;SVE=Ta bort vÑrdet NOTPROVIDED';

    [External]
    PROCEDURE CreateDefaultTransformations@3();
    BEGIN
      InsertRec(UPPERCASETxt,UpperCaseDescTxt,"Transformation Type"::Uppercase,0,0,'','');
      InsertRec(LOWERCASETxt,LowerCaseDescTxt,"Transformation Type"::Lowercase,0,0,'','');
      InsertRec(TITLECASETxt,TitleCaseDescTxt,"Transformation Type"::"Title Case",0,0,'','');
      InsertRec(TRIMTxt,TrimDescTxt,"Transformation Type"::Trim,0,0,'','');
      InsertRec(FOURTH_TO_SIXTH_CHARTxt,FourthToSixthCharactersDescTxt,"Transformation Type"::Substring,4,3,'','');
      InsertRec(YYYYMMDDDateTxt,YYYYMMDDDateDescTxt,"Transformation Type"::"Date Formatting",0,0,'yyyyMMdd','');
      InsertRec(YYYYMMDDHHMMSSTxt,YYYYMMDDHHMMSSDescTxt,"Transformation Type"::"Date and Time Formatting",0,0,'yyyyMMddHHmmss','');
      InsertRec(ALPHANUMERIC_ONLYTxt,AlphaNumericDescTxt,"Transformation Type"::"Remove Non-Alphanumeric Characters",0,0,'','');
      InsertRec(DKNUMBERFORMATTxt,DKNUMBERFORMATDescTxt,"Transformation Type"::"Decimal Formatting",0,0,'','da-DK');
      InsertRec(USDATEFORMATTxt,USDATEFORMATDescTxt,"Transformation Type"::"Date Formatting",0,0,'','en-US');
      InsertRec(USDATETIMEFORMATTxt,USDATETIMEFORMATDescTxt,"Transformation Type"::"Date and Time Formatting",0,0,'','en-US');
      OnCreateTransformationRules;
      InsertFindAndReplaceRule(
        DeleteNOTPROVIDEDTxt,DeleteNOTPROVIDEDDescriptionTxt,"Transformation Type"::"Regular Expression - Replace",'NOTPROVIDED','');
    END;

    [External]
    PROCEDURE InsertRec@8(NewCode@1000 : Code[20];NewDescription@1001 : Text[100];NewTransformationType@1002 : Option;NewStartPosition@1003 : Integer;NewLength@1004 : Integer;NewDataFormat@1006 : Text[100];NewDataFormattingCulture@1007 : Text[10]);
    VAR
      TransformationRule@1005 : Record 1237;
    BEGIN
      WITH TransformationRule DO BEGIN
        INIT;
        VALIDATE(Code,NewCode);
        VALIDATE(Description,NewDescription);
        VALIDATE("Transformation Type",NewTransformationType);
        VALIDATE("Start Position",NewStartPosition);
        VALIDATE(Length,NewLength);
        VALIDATE("Data Format",NewDataFormat);
        VALIDATE("Data Formatting Culture",NewDataFormattingCulture);
        IF INSERT(TRUE) THEN;
      END;
    END;

    LOCAL PROCEDURE InsertFindAndReplaceRule@27(NewCode@1000 : Code[20];NewDescription@1001 : Text[100];NewTransformationType@1002 : Option;NewFindValue@1003 : Text[250];NewReplaceValue@1004 : Text[250]);
    VAR
      TransformationRule@1005 : Record 1237;
    BEGIN
      WITH TransformationRule DO BEGIN
        INIT;
        VALIDATE(Code,NewCode);
        VALIDATE(Description,NewDescription);
        VALIDATE("Transformation Type",NewTransformationType);
        VALIDATE("Find Value",NewFindValue);
        VALIDATE("Replace Value",NewReplaceValue);
        IF INSERT(TRUE) THEN;
      END;
    END;

    [External]
    PROCEDURE TransformText@5(OldValue@1001 : Text) : Text;
    VAR
      TransformationRule@1000 : Record 1237;
      NewValue@1002 : Text;
    BEGIN
      NewValue := OldValue;

      CASE "Transformation Type" OF
        "Transformation Type"::Uppercase:
          NewValue := UPPERCASE(OldValue);
        "Transformation Type"::Lowercase:
          NewValue := LOWERCASE(OldValue);
        "Transformation Type"::"Title Case":
          NewValue := TextToTitleCase(OldValue);
        "Transformation Type"::Trim:
          NewValue := DELCHR(OldValue,'<>');
        "Transformation Type"::Substring:
          NewValue := Substring(OldValue);
        "Transformation Type"::Replace:
          NewValue := StringReplace(OldValue,"Find Value","Replace Value");
        "Transformation Type"::"Regular Expression - Replace":
          NewValue := RegularExpressionReplace(OldValue,"Find Value","Replace Value");
        "Transformation Type"::"Regular Expression - Match":
          NewValue := RegularExpressionMatch(OldValue,"Find Value");
        "Transformation Type"::"Remove Non-Alphanumeric Characters":
          NewValue := RemoveNonAlphaNumericCharacters(OldValue);
        "Transformation Type"::"Date Formatting":
          NewValue := DateFormatting(OldValue);
        "Transformation Type"::"Date and Time Formatting":
          NewValue := DateTimeFormatting(OldValue);
        "Transformation Type"::"Decimal Formatting":
          NewValue := DecimalFormatting(OldValue);
        "Transformation Type"::Custom:
          OnTransformation(Code,OldValue,NewValue);
      END;

      IF "Next Transformation Rule" <> '' THEN
        IF TransformationRule.GET("Next Transformation Rule") THEN
          EXIT(TransformationRule.TransformText(NewValue));

      EXIT(NewValue);
    END;

    LOCAL PROCEDURE TextToTitleCase@17(OldValue@1001 : Text) : Text;
    VAR
      CultureInfo@1002 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Globalization.CultureInfo";
    BEGIN
      CultureInfo := CultureInfo.GetCultureInfo("Data Formatting Culture");
      EXIT(CultureInfo.TextInfo.ToTitleCase(OldValue));
    END;

    LOCAL PROCEDURE StringReplace@4(StringToReplace@1000 : Text;OldValue@1001 : Text;NewValue@1002 : Text) : Text;
    VAR
      TextManagement@1003 : Codeunit 41;
    BEGIN
      EXIT(TextManagement.Replace(StringToReplace,OldValue,NewValue));
    END;

    LOCAL PROCEDURE RegularExpressionReplace@2(StringToReplace@1000 : Text;Pattern@1001 : Text;NewValue@1002 : Text) : Text;
    VAR
      TypeHelper@1003 : Codeunit 10;
    BEGIN
      EXIT(TypeHelper.RegexReplaceIgnoreCase(StringToReplace,Pattern,NewValue));
    END;

    LOCAL PROCEDURE RegularExpressionMatch@21(StringToMatch@1000 : Text;Pattern@1001 : Text) : Text;
    VAR
      Regex@1003 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.RegularExpressions.Regex";
      RegexOptions@1005 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.RegularExpressions.RegexOptions";
      MatchCollection@1006 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.RegularExpressions.MatchCollection";
      Match@1007 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.RegularExpressions.Match";
      Group@1008 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.RegularExpressions.Group";
      Capture@1011 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.RegularExpressions.Capture";
      NewString@1004 : Text;
      WholeExpressionGroup@1009 : Boolean;
    BEGIN
      NewString := '';

      Regex := Regex.Regex(Pattern,RegexOptions.IgnoreCase);
      MatchCollection := Regex.Matches(StringToMatch);
      IF ISNULL(MatchCollection) THEN
        EXIT(NewString);

      IF MatchCollection.Count = 0 THEN
        EXIT(NewString);

      WholeExpressionGroup := TRUE;
      FOREACH Match IN MatchCollection DO
        FOREACH Group IN Match.Groups DO BEGIN
          IF WholeExpressionGroup THEN
            WholeExpressionGroup := FALSE
          ELSE
            FOREACH Capture IN Group.Captures DO
              NewString += Capture.Value;
        END;

      EXIT(NewString);
    END;

    LOCAL PROCEDURE RemoveNonAlphaNumericCharacters@19(OldValue@1003 : Text) : Text;
    VAR
      StringConversionManagement@1000 : Codeunit 47;
    BEGIN
      EXIT(StringConversionManagement.RemoveNonAlphaNumericCharacters(OldValue));
    END;

    LOCAL PROCEDURE GetDateTime@20(TextValue@1001 : Text;UTC@1003 : Boolean) : DateTime;
    VAR
      DotNetDateTime@1004 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.DateTime";
      CultureInfo@1000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Globalization.CultureInfo";
      DotNetDateTimeStyles@1005 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Globalization.DateTimeStyles";
      DateTimeValue@1002 : DateTime;
    BEGIN
      DateTimeValue := 0DT;
      DotNetDateTime := DotNetDateTime.DateTime(1);

      IF UTC THEN
        DotNetDateTimeStyles := DotNetDateTimeStyles.AssumeLocal
      ELSE
        DotNetDateTimeStyles := DotNetDateTimeStyles.None;

      IF "Data Formatting Culture" = '' THEN BEGIN
        CultureInfo := CultureInfo.InvariantCulture;
        IF NOT DotNetDateTime.TryParseExact(
             TextValue,
             "Data Format",
             CultureInfo,
             DotNetDateTimeStyles,
             DotNetDateTime)
        THEN
          EXIT(DateTimeValue);
      END ELSE BEGIN
        CultureInfo := CultureInfo.GetCultureInfo("Data Formatting Culture");
        IF NOT DotNetDateTime.TryParse(
             TextValue,
             CultureInfo,
             DotNetDateTimeStyles,
             DotNetDateTime)
        THEN
          EXIT(DateTimeValue);
      END;
      DateTimeValue := DotNetDateTime;
      EXIT(DateTimeValue);
    END;

    LOCAL PROCEDURE DateTimeFormatting@36(OldValue@1001 : Text) : Text;
    VAR
      DateTimeValue@1002 : DateTime;
      NewValue@1003 : Text;
    BEGIN
      DateTimeValue := GetDateTime(OldValue,FALSE);
      IF DateTimeValue <> 0DT THEN
        NewValue := FORMAT(DateTimeValue,0,XmlFormat)
      ELSE
        NewValue := OldValue;
      EXIT(NewValue);
    END;

    LOCAL PROCEDURE DateFormatting@28(OldValue@1001 : Text) : Text;
    VAR
      DateTimeValue@1002 : DateTime;
      DateValue@1006 : Date;
      NewValue@1003 : Text;
    BEGIN
      DateTimeValue := GetDateTime(OldValue,TRUE);
      DateValue := DT2DATE(DateTimeValue);
      IF DateValue <> 0D THEN
        NewValue := FORMAT(DateValue,0,XmlFormat)
      ELSE
        NewValue := OldValue;
      EXIT(NewValue);
    END;

    LOCAL PROCEDURE DecimalFormatting@14(OldValue@1001 : Text) : Text;
    VAR
      TypeHelper@1006 : Codeunit 10;
      NewDecimalVariant@1000 : Variant;
      NewValue@1003 : Text;
      DummyDecimal@1007 : Decimal;
    BEGIN
      NewValue := OldValue;
      NewDecimalVariant := DummyDecimal;
      TypeHelper.Evaluate(NewDecimalVariant,OldValue,'',"Data Formatting Culture");

      NewValue := FORMAT(NewDecimalVariant,0,XmlFormat);
      EXIT(NewValue);
    END;

    LOCAL PROCEDURE Substring@7(OldValue@1000 : Text) : Text;
    VAR
      StartPosition@1002 : Integer;
      NewLength@1003 : Integer;
    BEGIN
      StartPosition := SubstringGetStartPosition(OldValue);
      IF StartPosition <= 0 THEN
        EXIT('');

      NewLength := SubstringGetLength(OldValue,StartPosition);

      IF NewLength <= 0 THEN
        EXIT('');

      EXIT(COPYSTR(OldValue,StartPosition,NewLength));
    END;

    [External]
    PROCEDURE GetFourthToSixthSubstringCode@6() : Code[20];
    BEGIN
      EXIT(FOURTH_TO_SIXTH_CHARTxt);
    END;

    [External]
    PROCEDURE GetUSDateFormatCode@1() : Code[20];
    BEGIN
      EXIT(USDATEFORMATTxt);
    END;

    [External]
    PROCEDURE GetUSDateTimeFormatCode@44() : Code[20];
    BEGIN
      EXIT(USDATETIMEFORMATTxt);
    END;

    [External]
    PROCEDURE GetUppercaseCode@10() : Code[20];
    BEGIN
      EXIT(UPPERCASETxt);
    END;

    [External]
    PROCEDURE GetLowercaseCode@9() : Code[20];
    BEGIN
      EXIT(LOWERCASETxt);
    END;

    [External]
    PROCEDURE GetTitlecaseCode@12() : Code[20];
    BEGIN
      EXIT(TITLECASETxt);
    END;

    [External]
    PROCEDURE GetTrimCode@11() : Code[20];
    BEGIN
      EXIT(TRIMTxt);
    END;

    [External]
    PROCEDURE GetAlphanumericCode@13() : Code[20];
    BEGIN
      EXIT(ALPHANUMERIC_ONLYTxt);
    END;

    [External]
    PROCEDURE GetDanishDecimalFormatCode@15() : Code[20];
    BEGIN
      EXIT(DKNUMBERFORMATTxt);
    END;

    [External]
    PROCEDURE GetYYYYMMDDCode@16() : Code[20];
    BEGIN
      EXIT(YYYYMMDDDateTxt);
    END;

    [External]
    PROCEDURE GetYYYYMMDDHHMMSSCode@41() : Code[20];
    BEGIN
      EXIT(YYYYMMDDHHMMSSTxt);
    END;

    [External]
    PROCEDURE GetDeleteNOTPROVIDEDCode@23() : Code[20];
    BEGIN
      EXIT(DeleteNOTPROVIDEDTxt);
    END;

    LOCAL PROCEDURE RemoveLeadingAndEndingQuotes@18(InputText@1000 : Text) : Text;
    VAR
      QuotedText@1002 : Boolean;
      InputTextLength@1001 : Integer;
    BEGIN
      InputTextLength := STRLEN(InputText);
      IF InputTextLength < 2 THEN
        EXIT(InputText);

      QuotedText := (InputText[1] = '''') AND (InputText[InputTextLength] = '''');
      IF NOT QuotedText THEN
        QuotedText := (InputText[1] = '"') AND (InputText[InputTextLength] = '"');

      IF QuotedText THEN
        EXIT(COPYSTR(InputText,2,InputTextLength - 2));

      EXIT(InputText);
    END;

    LOCAL PROCEDURE SubstringGetLength@25(OldValue@1000 : Text;StartPosition@1001 : Integer) : Integer;
    VAR
      SearchableText@1002 : Text;
    BEGIN
      IF (Length <= 0) AND ("Ending Text" = '') THEN
        EXIT(STRLEN(OldValue) - StartPosition + 1);

      IF Length > 0 THEN
        EXIT(Length);

      IF "Ending Text" <> '' THEN BEGIN
        SearchableText := COPYSTR(OldValue,StartPosition,STRLEN(OldValue) - StartPosition + 1);
        EXIT(STRPOS(SearchableText,RemoveLeadingAndEndingQuotes("Ending Text")) - 1);
      END;

      EXIT(-1);
    END;

    LOCAL PROCEDURE SubstringGetStartPosition@30(OldValue@1000 : Text) : Integer;
    VAR
      StartingText@1001 : Text;
      StartIndex@1002 : Integer;
    BEGIN
      IF ("Start Position" <= 0) AND ("Starting Text" = '') THEN
        EXIT(1);

      IF "Start Position" > 0 THEN
        EXIT("Start Position");

      StartingText := RemoveLeadingAndEndingQuotes("Starting Text");
      IF StartingText <> '' THEN BEGIN
        StartIndex := STRPOS(OldValue,StartingText);
        IF StartIndex > 0 THEN
          EXIT(StartIndex + STRLEN(StartingText));
      END;

      EXIT(-1);
    END;

    [External]
    PROCEDURE EditNextTransformationRule@22();
    VAR
      TransformationRule@1000 : Record 1237;
    BEGIN
      IF "Next Transformation Rule" = '' THEN
        EXIT;

      TransformationRule.GET("Next Transformation Rule");
      PAGE.RUN(PAGE::"Transformation Rule Card",TransformationRule);
    END;

    LOCAL PROCEDURE CheckMandatoryFields@29();
    BEGIN
      IF "Transformation Type" IN ["Transformation Type"::Replace,
                                   "Transformation Type"::"Regular Expression - Replace",
                                   "Transformation Type"::"Regular Expression - Match"]
      THEN
        TESTFIELD("Find Value");
    END;

    [Integration]
    [External]
    PROCEDURE OnTransformation@26(TransformationCode@1000 : Code[20];InputText@1001 : Text;VAR OutputText@1002 : Text);
    BEGIN
    END;

    [Integration]
    [External]
    PROCEDURE OnCreateTransformationRules@24();
    BEGIN
    END;

    LOCAL PROCEDURE XmlFormat@32() : Integer;
    BEGIN
      EXIT(9);
    END;

    BEGIN
    END.
  }
}

