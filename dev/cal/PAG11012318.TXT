OBJECT Page 11012318 Item Relations
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Item Relations;
               NOR=Artikkelrelasjoner;
               SVE=Artikelrelationer];
    SourceTable=Table11012319;
    DelayedInsert=Yes;
    PageType=List;
    UsageCategory=Tasks;
    AutoSplitKey=No;
    OnOpenPage=VAR
                 FPSLicenseManagement@1100528500 : Codeunit 11229289;
               BEGIN
                 IF FPSLicenseManagement.LicenseAndReadPermissionForTable(CompRel) THEN
                   CentrManagementCU.ItemFormEditable(NavBln,DiscBln,DiscGrpBln,RcpBln);
               END;

    OnAfterGetRecord=BEGIN
                       IF "Source Type" = "Source Type"::"Basic Item" THEN BEGIN
                         CALCFIELDS("Cost Object (Basic Item)", "Item Brand");
                         CostObject := "Cost Object (Basic Item)";
                         "Item Brand" := "Item Brand";
                       END;
                       IF "Source Type" = "Source Type"::"Trade Item" THEN BEGIN
                         CALCFIELDS("Cost Object (Trade Item)");
                         CostObject := "Cost Object (Trade Item)";
                       END;
                     END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1210190004;1 ;ActionGroup;
                      Name=FuncButton;
                      CaptionML=[ENU=F&unctions;
                                 NOR=Fu&nksjon;
                                 SVE=F&unktioner] }
      { 1210190006;2 ;Action    ;
                      CaptionML=[ENU=&Import Navision Items;
                                 NOR=&Importer Navision-artikler;
                                 SVE=&Importera Navision-artiklar];
                      Promoted=Yes;
                      Image=Import;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 IF NOT NavBln THEN BEGIN
                                   REPORT.RUNMODAL(REPORT::"Import Navision Items");
                                   CurrPage.UPDATE(TRUE);
                                 END;
                               END;
                                }
      { 1210190007;2 ;Action    ;
                      CaptionML=[ENU=&Update Navision Items;
                                 NOR=&Oppdater Navision-artikler;
                                 SVE=&Uppdatera Navision-artiklar];
                      Promoted=Yes;
                      Image=Refresh;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 ItemRelation@1100525001 : Record 11012319;
                               BEGIN
                                 IF NOT NavBln THEN BEGIN
                                   ItemRelation.COPY(Rec);
                                   CurrPage.SETSELECTIONFILTER(ItemRelation);
                                   UpdateItemRelation(ItemRelation, FALSE);  //db, 19-12-11: M31760
                                   CurrPage.UPDATE(TRUE);
                                 END;
                               END;
                                }
      { 1100485000;2 ;Action    ;
                      CaptionML=[ENU=Modify Vendor Collectively;
                                 NOR=Endre leverand›r kollektivt;
                                 SVE=Žndra leverant”r kollektivt];
                      Image=Interaction;
                      OnAction=BEGIN
                                 UpdateVendor;
                                 CurrPage.UPDATE(TRUE);
                               END;
                                }
      { 1100528600;2 ;Action    ;
                      CaptionML=ENU=Pick other vendor;
                      Image=Change;
                      OnAction=VAR
                                 TradeItem@1100528600 : Record 11012317;
                                 WebshopItemMgt@1100528603 : Codeunit 11012306;
                                 VendorNo@1100528601 : Code[15];
                                 TradeItemNo@1100528602 : Code[20];
                               BEGIN
                                 TradeItem.GET(Vendor, "Trade Item");
                                 TradeItem.TESTFIELD("Maintained by 2ba");
                                 TradeItem.TESTFIELD(TradeItem."Product ID Guid (2ba)");
                                 VendorNo := Vendor;
                                 TradeItemNo := "Trade Item";
                                 WebshopItemMgt.RunModalDialogForOneItem(VendorNo, TradeItemNo);
                                 IF Vendor <> VendorNo THEN BEGIN
                                   VALIDATE("Trade Item", '');
                                   VALIDATE(Vendor, VendorNo);
                                 END;
                                 IF "Trade Item" <> TradeItemNo THEN
                                   VALIDATE("Trade Item", TradeItemNo);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                SourceExpr="Navision Item" }

    { 4   ;2   ;Field     ;
                DrillDown=No;
                SourceExpr="Description Navision-item" }

    { 14  ;2   ;Field     ;
                SourceExpr="Source Type" }

    { 6   ;2   ;Field     ;
                SourceExpr=Manufacturer }

    { 8   ;2   ;Field     ;
                SourceExpr="Basic Item";
                OnLookup=BEGIN
                           IF "Source Type" = "Source Type"::"Basic Item" THEN BEGIN
                             BasicItemRec.Manufacturer := Manufacturer;
                             BasicItemRec."Product Code" := "Basic Item";
                             IF PAGE.RUNMODAL(PAGE::"Basic Item List", BasicItemRec) = ACTION::LookupOK THEN BEGIN
                               Manufacturer := BasicItemRec.Manufacturer;
                               "Basic Item" := BasicItemRec."Product Code";
                               UpdateGTINCode;
                             END;
                           END;
                           CurrPage.UPDATE(TRUE);
                         END;
                          }

    { 10  ;2   ;Field     ;
                SourceExpr=Vendor }

    { 12  ;2   ;Field     ;
                SourceExpr="Trade Item";
                OnLookup=BEGIN
                           IF "Source Type" = "Source Type"::"Trade Item" THEN BEGIN
                             TradeItemRec.Vendor := Vendor;
                             TradeItemRec."Item Code" := "Trade Item";
                             IF PAGE.RUNMODAL(PAGE::"Trade Item List", TradeItemRec) = ACTION::LookupOK THEN BEGIN
                               Manufacturer := TradeItemRec.Manufacturer;
                               "Basic Item" := TradeItemRec."Product Code";
                               Vendor := TradeItemRec.Vendor;
                               VALIDATE("Trade Item", TradeItemRec."Item Code");
                               UpdateGTINCode;  //db, 20-12-19
                             END;
                           END;
                           CurrPage.UPDATE(TRUE);
                         END;
                          }

    { 1210190000;2;Field  ;
                SourceExpr="GTIN Code";
                OnLookup=VAR
                           TradeItemRec@1210190002 : Record 11012317;
                           TradeItemFrm@1210190001 : Page 11012334;
                         BEGIN
                           IF "Source Type" = "Source Type"::"Basic Item" THEN BEGIN
                             CLEAR(TradeItemFrm);
                             TradeItemFrm.LOOKUPMODE(TRUE);
                             IF "GTIN Code" <> '' THEN BEGIN
                               TradeItemRec.SETCURRENTKEY("GTIN Code (Item)");
                               TradeItemRec.SETRANGE("GTIN Code (Item)","GTIN Code");
                             END ELSE BEGIN
                               TradeItemRec.SETCURRENTKEY("Product Code", Manufacturer, "Item Code", Vendor);
                               TradeItemRec.SETRANGE("Product Code","Basic Item");
                               TradeItemRec.SETRANGE(Manufacturer,Manufacturer);
                             END;
                             TradeItemFrm.SETTABLEVIEW(TradeItemRec);
                             IF TradeItemFrm.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               TradeItemFrm.GETRECORD(TradeItemRec);
                               VALIDATE("GTIN Code", TradeItemRec."GTIN Code (Item)");
                             END;
                           END;
                           CurrPage.UPDATE(TRUE);
                         END;
                          }

    { 16  ;2   ;Field     ;
                DrillDown=No;
                SourceExpr="Product Class" }

    { 18  ;2   ;Field     ;
                DrillDown=No;
                SourceExpr=Series }

    { 20  ;2   ;Field     ;
                DrillDown=No;
                SourceExpr="Item Type" }

    { 1210190002;2;Field  ;
                SourceExpr="Item Brand" }

    { 1100409000;2;Field  ;
                SourceExpr="Expired (Basic Item)";
                Visible=FALSE }

    { 1100409001;2;Field  ;
                SourceExpr="Expired (Trade Item)" }

    { 1210190011;2;Field  ;
                SourceExpr="Cost Object (Basic Item)";
                Visible=FALSE }

    { 1210190013;2;Field  ;
                SourceExpr="Cost Object (Trade Item)";
                Visible=FALSE }

    { 1210190005;2;Field  ;
                CaptionML=[ENU=Cost Object;
                           NOR=Kostobjekt;
                           SVE=Kostnadsobjekt];
                SourceExpr=CostObject;
                Editable=FALSE }

  }
  CODE
  {
    VAR
      TradeItemRec@11012001 : Record 11012317;
      BasicItemRec@11012002 : Record 11012316;
      CompRel@1210190010 : Record 11012059;
      CentrManagementCU@1210190009 : Codeunit 11012010;
      NavBln@1210190008 : Boolean;
      DiscBln@1100485000 : Boolean;
      DiscGrpBln@1100528300 : Boolean;
      RcpBln@1210190002 : Boolean;
      CostObject@1210190001 : Code[20];

    PROCEDURE UpdateVendor@1100485004();
    VAR
      ItemRelation@1100525000 : Record 11012319;
      ModifyItemVendor@1100485000 : Page 11020420;
    BEGIN
      ItemRelation.COPY(Rec);
      CurrPage.SETSELECTIONFILTER(ItemRelation);
      ModifyItemVendor.SetItemRelationRec(ItemRelation);
      ModifyItemVendor.RUNMODAL;
      CurrPage.UPDATE(TRUE);
    END;

    BEGIN
    END.
  }
}

