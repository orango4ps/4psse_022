OBJECT Table 11125656 Package Header
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               JobsSetup.GET;

               IF "No." = '' THEN BEGIN
                 TestNoSeries;
                 NoSeriesMgt.InitSeries(GetNoSeriesCode,xRec."No. Series","Receive Date","No.","No. Series");
               END;
             END;

    OnDelete=VAR
               PackageLine@1100525000 : Record 11125657;
             BEGIN
               PackageLine.SETRANGE("Package No.","No.");
               PackageLine.DELETEALL;
             END;

    CaptionML=[ENU=Package Header;
               NOR=Pakkehode;
               SVE=Paketrubrik];
    LookupPageID=Page11125662;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  JobsSetup.GET;
                                                                  NoSeriesMgt.TestManual(GetNoSeriesCode);
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              NOR=Nr.;
                                                              SVE=Nr] }
    { 10  ;   ;Description         ;Text50        ;CaptionML=[ENU=Your Reference;
                                                              NOR=Deres referanse;
                                                              SVE=Er referens] }
    { 11  ;   ;Receive-from Contact No.;Code20    ;TableRelation=Contact;
                                                   CaptionML=[ENU=Ship-to Contact No.;
                                                              NOR=Leveranse, Kontaktnr.;
                                                              SVE=Leverans, kontaktnr] }
    { 20  ;   ;Receive Date        ;Date          ;CaptionML=[ENU=Posting Date;
                                                              NOR=Bokf›ringsdato;
                                                              SVE=Bokf”ringsdatum] }
    { 30  ;   ;Employee No.        ;Code20        ;TableRelation=Employee;
                                                   CaptionML=[ENU=Employee No.;
                                                              NOR=Ansattnr;
                                                              SVE=Anst„llningsnr] }
    { 35  ;   ;Number of packages  ;Integer       ;OnValidate=BEGIN
                                                                "Number of labels" := "Number of packages";
                                                              END;

                                                   CaptionML=[ENU=Number of packages;
                                                              NOR=Antall pakker;
                                                              SVE=Antal paket] }
    { 40  ;   ;Location            ;Text50        ;CaptionML=[ENU=Location;
                                                              NOR=Plassering;
                                                              SVE=Lagerst„lle] }
    { 45  ;   ;Number of labels    ;Integer       ;CaptionML=[ENU=Number of labels;
                                                              NOR=Antall etiketter;
                                                              SVE=Antal etiketter] }
    { 50  ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              NOR=Nr-serie;
                                                              SVE=Nr-serie];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      JobsSetup@1033 : Record 315;
      PackageReceipt@1100525001 : Record 11125656;
      NoSeriesMgt@1100525000 : Codeunit 396;

    PROCEDURE AssistEdit@1(OldPackageReceipt@1000 : Record 11125656) : Boolean;
    VAR
      PackageReceipt2@1001 : Record 11125656;
      Text001@1100525000 : TextConst 'ENU=The Project Delivery Note %1 already exists.;NOR=Prosjektleveransebeskjed %1 finnes allerede.;SVE=Projektleveransbesked %1 finns redan.';
    BEGIN
      WITH PackageReceipt DO BEGIN
        COPY(Rec);
        JobsSetup.GET;
        TestNoSeries;
        IF NoSeriesMgt.SelectSeries(GetNoSeriesCode,OldPackageReceipt."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("No.");
          IF PackageReceipt2.GET("No.") THEN
            ERROR(Text001,"No.");
          Rec := PackageReceipt;
          EXIT(TRUE);
        END;
      END;
    END;

    LOCAL PROCEDURE TestNoSeries@6() : Boolean;
    BEGIN
      JobsSetup.TESTFIELD("Package Receipt Nos.");
    END;

    LOCAL PROCEDURE GetNoSeriesCode@9() : Code[10];
    BEGIN
      EXIT(JobsSetup."Package Receipt Nos.");
    END;

    PROCEDURE Register@1100525000();
    VAR
      PackageLine@1100525001 : Record 11125657;
      RegisteredPackageHeader@1100525000 : Record 11125658;
      RegisteredPackageLine@1100525002 : Record 11125659;
      Text001@1100525003 : TextConst 'ENU=Do you want to register %1?;NOR=Vil du registrere %1?;SVE=Vill du registrera %1?';
    BEGIN
      IF CONFIRM(STRSUBSTNO(Text001,"No.")) THEN BEGIN
        RegisteredPackageHeader.INIT;
        RegisteredPackageHeader.TRANSFERFIELDS(Rec);
        RegisteredPackageHeader.INSERT;

        PackageLine.SETRANGE("Package No.","No.");
        IF PackageLine.FINDSET THEN
          REPEAT
            RegisteredPackageLine.INIT;
            RegisteredPackageLine.TRANSFERFIELDS(PackageLine);
            PackageLine.CALCFIELDS("Buy-from Vendor No.","Buy-from Vendor Name");
            RegisteredPackageLine."Buy-from Vendor No." := PackageLine."Buy-from Vendor No.";
            RegisteredPackageLine."Buy-from Vendor Name" := PackageLine."Buy-from Vendor Name";
            RegisteredPackageLine.INSERT;
          UNTIL PackageLine.NEXT = 0;

        DELETE(TRUE);
      END;
    END;

    BEGIN
    END.
  }
}

