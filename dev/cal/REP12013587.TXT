OBJECT Report 12013587 Batch Create Purchase Invoices
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF510000;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Batch Create Purchase Documents;
               SVE=Batch Skapa Ink”ps Dokument];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 4372;    ;DataItem;                    ;
               DataItemTable=Table12013587;
               DataItemTableView=SORTING(Journal Batch Name,Inbound Document No.);
               ReqFilterHeadingML=[ENU=Exflow Document;
                                   SVE=Exflowdokument];
               OnPreDataItem=BEGIN
                               IF ReplacePostingDate AND (PostingDateReq = 0D) THEN
                                 ERROR(Text000);

                               CounterOK := 0;
                               CounterTotal := COUNT;
                               IF NOT HideDialog THEN
                                 IF GUIALLOWED THEN BEGIN
                                   Counter := 0;
                                   Window.OPEN(Text001);
                                 END;
                             END;

               OnAfterGetRecord=BEGIN
                                  CLEARLASTERROR;
                                  Counter += 1;
                                  IF NOT HideDialog THEN
                                    IF GUIALLOWED THEN
                                      Window.UPDATE(1,ROUND(Counter / CounterTotal * 10000,1));

                                  ProcessDoc := TRUE;
                                  IF RunInAutomaticMode THEN BEGIN
                                    // CALCFIELDS("Potential UOM Mismatch");
                                    // IF "Potential UOM Mismatch" THEN
                                    //  ProcessDoc := FALSE;
                                  END;

                                  IF ProcessDoc THEN BEGIN
                                    ExFPurchDocHeader.GET("Inbound Document No.");
                                    ExFlowToInvoicePreCreate.VerifyDocument(ExFPurchDocHeader);
                                    COMMIT;

                                    ExFPurchDocHeader.GET("Inbound Document No.");
                                    IF NOT ExFPurchDocHeader."Validation Error" THEN BEGIN
                                      CLEAR(ExflowToInvoice);
                                      ExflowToInvoice.SetRealCreate(TRUE);
                                      ExflowToInvoice.SetPostingDate(ReplacePostingDate,ReplaceDocumentDate,PostingDateReq,UpdateDeferalsReq);
                                      ExflowToInvoice.SilentMode(HideDialog);
                                      IF ExflowToInvoice.RUN(ExFPurchDocHeader) THEN BEGIN
                                        CounterOK := CounterOK + 1;
                                      END
                                      ELSE BEGIN
                                        ExFPurchDocHeader.GET("Inbound Document No.");
                                        ExFPurchDocHeader."Error Message" := COPYSTR(GETLASTERRORTEXT,1,MAXSTRLEN("Error Message"));
                                        ExFPurchDocHeader.MODIFY;
                                      END;

                                      COMMIT;
                                    END;
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                IF NOT HideDialog THEN
                                  IF GUIALLOWED THEN
                                    Window.CLOSE;
                              END;

               ReqFilterFields=Inbound Document No.,Buy-from Vendor No.,Document Date }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100285005;0;Container;
                  ContainerType=ContentArea }

      { 1100285004;1;Group  ;
                  CaptionML=[ENU=Options;
                             SVE=Alternativ] }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Posting Date;
                             SVE=Bokf”ringsdatum];
                  SourceExpr=PostingDateReq }

      { 2   ;2   ;Field     ;
                  CaptionML=[ENU=Replace Posting Date;
                             SVE=Ers„tt bokf”ringsdatum];
                  SourceExpr=ReplacePostingDate }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Replace Document Date;
                             SVE=Ers„tt dokumentdatum];
                  SourceExpr=ReplaceDocumentDate }

      { 1100285001;2;Field  ;
                  CaptionML=[ENU=Update Deferal schedules;
                             SVE=Uppdatera periodiseringsscheman];
                  SourceExpr=UpdateDeferalsReq;
                  OnValidate=BEGIN
                               IF NOT ReplacePostingDate THEN
                                 ERROR(Text004);
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Please enter the posting date.;SVE=Skriv in bokf”ringsdatum.';
      Text001@1100285009 : TextConst 'ENU="Creating invoices  @1@@@@@@@@@@@@@ ";SVE=Skapar fakturor  @1@@@@@@@@@@@@@';
      ExFPurchDocHeader@1100285011 : Record 12013587;
      ExFlowToInvoicePreCreate@1100285010 : Codeunit 12013632;
      ExflowToInvoice@1100285000 : Codeunit 12013592;
      Window@1100285007 : Dialog;
      CounterTotal@1100285004 : Integer;
      Counter@1100285001 : Integer;
      CounterOK@1100285002 : Integer;
      PostingDateReq@1008 : Date;
      ReplacePostingDate@1012 : Boolean;
      ReplaceDocumentDate@1013 : Boolean;
      RunInAutomaticMode@1100285005 : Boolean;
      ProcessDoc@1100285006 : Boolean;
      UpdateDeferalsReq@1100285003 : Boolean;
      Text004@1100285008 : TextConst 'ENU=This option requires Replace Posting Date;SVE=Detta val kr„ver att Erst„tt bokf”ringsdatum „r valt';
      HideDialog@1100285012 : Boolean;

    PROCEDURE SetAutomaticMode@1100285003(_RunInAutomaticMode@1100285000 : Boolean);
    BEGIN
      RunInAutomaticMode := _RunInAutomaticMode;
    END;

    PROCEDURE GetCounters@1100285002(VAR _CounterTotal@1100285002 : Integer;VAR _CounterOK@1100285000 : Integer);
    BEGIN
      _CounterTotal := CounterTotal;
      _CounterOK := CounterOK;
    END;

    PROCEDURE SilentMode@1100285000();
    BEGIN
      HideDialog := TRUE;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

