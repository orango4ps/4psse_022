OBJECT Codeunit 6086307 Expense Inbox-Process
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.3.00.05;
  }
  PROPERTIES
  {
    TableNo=6086323;
    OnRun=BEGIN
            ExpenseInbox.COPY(Rec);
            Code;
            Rec := ExpenseInbox;
          END;

  }
  CODE
  {
    VAR
      ExpenseInbox@1160040004 : Record 6086323;
      ExpInboxCheck@1160040001 : Codeunit 6086308;

    LOCAL PROCEDURE Code@1160040000();
    VAR
      Expense@1160040004 : Record 6086320;
      ExpValidate@1160040000 : Codeunit 6086321;
    BEGIN
      WITH ExpenseInbox DO BEGIN
        IF Status = Status::Accepted THEN
          EXIT;

        CleanupPreviousInboxEntries(ExpenseInbox);
        COMMIT;

        IF NOT ExpInboxCheck.RUN(ExpenseInbox) THEN BEGIN
          "Error Text" := COPYSTR(GETLASTERRORTEXT,1,MAXSTRLEN("Error Text"));
          Status := Status::Error;

          IF "Expense Entry No." = 0 THEN BEGIN
            Expense.SETCURRENTKEY("Expense GUID");
            Expense.SETRANGE("Expense GUID","Expense GUID");
            IF Expense.FINDFIRST THEN
              "Expense Entry No." := Expense."Entry No.";
          END;
        END ELSE BEGIN
          "Expense Entry No." := ExpInboxCheck.GetExpenseEntryNo;
          "Error Text" := '';
          Status := Status::Accepted;
        END;

        "Processed Date/Time" := CURRENTDATETIME;
        "Processed by User ID" := USERID;
        MODIFY;

        IF "Expense Entry No." <> 0 THEN BEGIN
          Expense.GET("Expense Entry No.");
          ExpValidate.RUN(Expense);
        END;

      END;
    END;

    PROCEDURE CleanupPreviousInboxEntries@1160040006(CurrExpInbox@1160040000 : Record 6086323);
    VAR
      ExpInbox@1160040001 : Record 6086323;
    BEGIN
      //FORMER ENTRIES MIGHT BE IN ERRORS WHICH WERE ALREADY CORRECTED.
      ExpInbox.SETCURRENTKEY("Expense GUID");
      ExpInbox.SETRANGE("Expense GUID",CurrExpInbox."Expense GUID");
      ExpInbox.SETFILTER("Entry No.",'<%1',CurrExpInbox."Entry No.");
      ExpInbox.SETFILTER(Status,'%1|%2',ExpInbox.Status::Pending,ExpInbox.Status::Error);
      ExpInbox.SETRANGE("Expense Completed",FALSE);
      ExpInbox.MODIFYALL(Status,ExpInbox.Status::Accepted);
    END;

    BEGIN
    END.
  }
}

