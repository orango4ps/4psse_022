OBJECT Table 11012316 Basic Item
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnInsert=BEGIN
               TESTFIELD(Manufacturer);
               TESTFIELD("Product Code");
             END;

    OnDelete=VAR
               PrefVendor@1210190000 : Record 11012318;
               ItemtoReplace@1100525000 : Record 11012322;
               ETIMProductSpecification@1100525001 : Record 11012314;
             BEGIN
               ItemtoReplace.ReplaceBasicItem(Rec);

               ETIMProductSpecification.SETRANGE("Product Code", "Product Code");
               ETIMProductSpecification.SETRANGE(Manufacturer, Manufacturer);
               ETIMProductSpecification.DELETEALL(TRUE);

               PrefVendor.SETCURRENTKEY(Manufacturer, "Basic Item");
               PrefVendor.SETRANGE(Type, PrefVendor.Type::"Basic Item");
               PrefVendor.SETRANGE(Manufacturer, Manufacturer);
               PrefVendor.SETRANGE("Basic Item", "Product Code");
               IF PrefVendor.FINDFIRST THEN
                 PrefVendor.DELETE;
             END;

    CaptionML=[ENU=Basic Item;
               NOR=Fabrikantartikkel;
               SVE=Basartikel];
    LookupPageID=Page11012333;
  }
  FIELDS
  {
    { 10  ;   ;Product Code        ;Code20        ;CaptionML=[ENU=Product Code;
                                                              NOR=Produktkode;
                                                              SVE=Produktkod];
                                                   NotBlank=Yes }
    { 20  ;   ;Manufacturer        ;Code15        ;TableRelation="ETIM Vendor - Central Mgt";
                                                   CaptionML=[ENU=Manufacturer;
                                                              NOR=Produsent;
                                                              SVE=Tillverkare];
                                                   NotBlank=Yes }
    { 21  ;   ;Manufacturer Name   ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ETIM Vendor - Central Mgt".Name WHERE (Code=FIELD(Manufacturer)));
                                                   CaptionML=[ENU=Manufacturer Name;
                                                              NOR=Fabrikantens navn;
                                                              SVE=Tillverkarens namn];
                                                   Description=NAVISION;
                                                   Editable=No }
    { 25  ;   ;Product Description ;Text50        ;CaptionML=[ENU=Product Description;
                                                              NOR=Produktbeskrivelse;
                                                              SVE=Produktbeskrivning] }
    { 30  ;   ;GTIN Code           ;Code20        ;CaptionML=[ENU=GTIN Code;
                                                              NOR=GTIN-kode;
                                                              SVE=GTIN] }
    { 35  ;   ;Product Group       ;Code10        ;TableRelation="ETIM Product Group";
                                                   CaptionML=[ENU=Product Group;
                                                              NOR=Produktgruppe;
                                                              SVE=Produktgrupp] }
    { 36  ;   ;Product Group Line No.;Code10      ;CaptionML=[ENU=Product Group Line No.;
                                                              NOR=Produktgruppens Radnr.;
                                                              SVE=Produktgruppens radnr] }
    { 40  ;   ;Product Class       ;Text80        ;TableRelation="ETIM Product Class"."Product Class" WHERE (Product Group=FIELD(Product Group),
                                                                                                             Product Group Line No.=FIELD(Product Group Line No.));
                                                   CaptionML=[ENU=Product Class;
                                                              NOR=Produktklasse;
                                                              SVE=Produktklass];
                                                   Description=NAVISION }
    { 50  ;   ;Item Series         ;Text40        ;CaptionML=[ENU=Item Series;
                                                              NOR=Artikkelserier;
                                                              SVE=Artikelserier] }
    { 55  ;   ;Item Type           ;Text40        ;CaptionML=[ENU=Item Type;
                                                              NOR=Artikkeltype;
                                                              SVE=Artikeltyp] }
    { 70  ;   ;Item Brand          ;Text40        ;CaptionML=[ENU=Item Brand;
                                                              NOR=Artikkelens Varemerke;
                                                              SVE=Artikelns varum„rke] }
    { 75  ;   ;Status Code         ;Code10        ;CaptionML=[ENU=Status Code;
                                                              NOR=Statuskode;
                                                              SVE=Statuskod] }
    { 76  ;   ;Starting Date       ;Date          ;CaptionML=[ENU=Starting Date;
                                                              NOR=Startdato;
                                                              SVE=Startdatum] }
    { 110 ;   ;Net Weight          ;Decimal       ;CaptionML=[ENU=Net Weight;
                                                              NOR=Nettovekt;
                                                              SVE=Nettovikt] }
    { 115 ;   ;Weight ID           ;Code10        ;CaptionML=[ENU=Weight ID;
                                                              NOR=Vekt-ID;
                                                              SVE=Vikt-ID] }
    { 120 ;   ;Weight Unit         ;Text10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("ETIM Unit"."Unit of Measure");
                                                   CaptionML=[ENU=Weight Unit;
                                                              NOR=Vektenhet;
                                                              SVE=Viktenhet];
                                                   Description=NAVISION;
                                                   Editable=No }
    { 130 ;   ;Product Code (Successor);Code20    ;CaptionML=[ENU=Product Code (Successor);
                                                              NOR=Produktkode (Etterf›lger);
                                                              SVE=Produktkod (efterf”ljande)] }
    { 135 ;   ;Product Code (Predecessor);Code20  ;CaptionML=[ENU=Product Code (Predecessor);
                                                              NOR=Produktkode (Forgjenger);
                                                              SVE=Produktkod (f”reg†ende)] }
    { 150 ;   ;GTIN Code (Successor);Code20       ;CaptionML=[ENU=GTIN Code (Successor);
                                                              NOR=GTIN-kode (Etterf›lger);
                                                              SVE=GTIN-kod (efterf”ljande)] }
    { 155 ;   ;GTIN Code (Predecessor);Code20     ;CaptionML=[ENU=GTIN Code (Predecessor);
                                                              NOR=GTIN-kode (Forgjenger);
                                                              SVE=GTIN-kod (f”reg†ende)] }
    { 160 ;   ;Gross Price         ;Decimal       ;CaptionML=[ENU=Gross Price;
                                                              NOR=Bruttopris;
                                                              SVE=Bruttopris];
                                                   Description=NAVISION ( Price/Discount from History);
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 161 ;   ;Net Price           ;Decimal       ;CaptionML=[ENU=Net Price;
                                                              NOR=Nettopris;
                                                              SVE=Nettopris];
                                                   Description=NAVISION;
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 162 ;   ;Discount Percentage ;Decimal       ;CaptionML=[ENU=Discount Percentage;
                                                              NOR=Rabattprosent;
                                                              SVE=Rabattprocent];
                                                   DecimalPlaces=0:5;
                                                   Description=NAVISION;
                                                   Editable=No }
    { 163 ;   ;Price Reference Date;Date          ;CaptionML=[ENU=Price Reference Date;
                                                              NOR=Referansedato for pris;
                                                              SVE=Priss„ttningsdatum];
                                                   Description=NAVISION;
                                                   Editable=No }
    { 164 ;   ;Application Unit    ;Text10        ;OnValidate=BEGIN
                                                                ValidateUnitOfMeasure(FIELDNO("Application Unit"));
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupUnitOfMeasure(FIELDNO("Application Unit"));
                                                            END;

                                                   CaptionML=[ENU=Application Unit;
                                                              NOR=Programenhet;
                                                              SVE=Programenhet];
                                                   Description=NAVISION (see Item Class) }
    { 165 ;   ;Packaging Unit      ;Text10        ;OnValidate=BEGIN
                                                                ValidateUnitOfMeasure(FIELDNO("Packaging Unit"));
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupUnitOfMeasure(FIELDNO("Packaging Unit"));
                                                            END;

                                                   CaptionML=[ENU=Packaging Unit;
                                                              NOR=Pakkeenhet;
                                                              SVE=F”rpackningsenhet];
                                                   Description=NAVISION }
    { 166 ;   ;Qty. per Unit of Measure;Decimal   ;InitValue=1;
                                                   CaptionML=[ENU=Qty. per Unit of Measure;
                                                              NOR=Antall per M†leenhet;
                                                              SVE=Antal per enhet];
                                                   DecimalPlaces=0:5;
                                                   Description=NAVISION }
    { 180 ;   ;Expired             ;Boolean       ;OnValidate=BEGIN
                                                                IF Expired THEN
                                                                  "Expiry Date" := TODAY
                                                                ELSE
                                                                  "Expiry Date" := 0D;
                                                              END;

                                                   CaptionML=[ENU=Expired;
                                                              NOR=Utg†tt;
                                                              SVE=Utg†ngna];
                                                   Description=NAVISION }
    { 181 ;   ;Expiry Date         ;Date          ;CaptionML=[ENU=Expiry Date;
                                                              NOR=Utl›psdato;
                                                              SVE=Utg†ngsdatum];
                                                   Description=NAVISION }
    { 190 ;   ;Cost Object         ;Code20        ;TableRelation="Dimension Value".Code WHERE (Cost Type=FILTER(Material),
                                                                                               Global Dimension No.=CONST(2));
                                                   CaptionML=[ENU=Global Dimension 2 Code;
                                                              NOR=Kostnadsobjekt;
                                                              SVE=Kostnadsobjekt];
                                                   Description=NAVISION;
                                                   CaptionClass='1,1,2' }
    { 200 ;   ;Sub Operation Code  ;Code20        ;TableRelation="Sub Operation".Code;
                                                   CaptionML=[ENU=Sub Operation Code;
                                                              NOR=Underinnsatskode;
                                                              SVE=Underinsatskod];
                                                   Description=NAVISION }
    { 205 ;   ;Search Argument     ;Code20        ;CaptionML=[ENU=Search Argument;
                                                              NOR=S›keargument;
                                                              SVE=S”kargument];
                                                   Description=NAVISION }
    { 210 ;   ;Removal Contribution;Decimal       ;CaptionML=[ENU=Removal Contribution;
                                                              NOR=Fjerningsbidrag;
                                                              SVE=Borttagningsbidrag];
                                                   Description=NAVISION;
                                                   AutoFormatType=2 }
    { 220 ;   ;Cost Object (Removal);Code20       ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   CaptionML=[ENU=Cost Object (Removal Contribution);
                                                              NOR=Kostnadsobjekt (Fjerningsbidrag);
                                                              SVE=Kostnadsobjekt (borttagningsbidrag)];
                                                   Description=NAVISION }
    { 230 ;   ;Product Description 2;Text50       ;CaptionML=[ENU=Product Description 2;
                                                              NOR=Produktbeskrivelse 2;
                                                              SVE=Produktbeskrivning 2];
                                                   Description=NAVISION }
    { 240 ;   ;Trade Item Exists   ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Trade Item" WHERE (Manufacturer=FIELD(Manufacturer),
                                                                                         Product Code=FIELD(Product Code)));
                                                   CaptionML=[ENU=Trade Item Exists;
                                                              NOR=Katalogartikkel finnes;
                                                              SVE=Anskaffningsartikeln finns];
                                                   Description=NAVISION;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Manufacturer,Product Code               ;Clustered=Yes }
    {    ;Product Code                             }
    {    ;Product Class                            }
    {    ;Item Type                                }
    {    ;Item Brand                               }
    {    ;GTIN Code                                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE InitLookup@1000000000(VAR BasicItem@1000000000 : Record 11012316;ProductCode@1000000001 : Code[20];ManufacturerCode@1000000002 : Code[20]);
    BEGIN
      IF NOT BasicItem.GET(ManufacturerCode,ProductCode) THEN BEGIN
        CLEAR(BasicItem);
        IF ManufacturerCode <> '' THEN BEGIN
          BasicItem.RESET;
          BasicItem.SETCURRENTKEY(Manufacturer, "Product Code");
          BasicItem.SETRANGE(Manufacturer,ManufacturerCode);
          IF NOT BasicItem.FINDFIRST THEN
            BasicItem.Manufacturer := ManufacturerCode;
        END;
      END;
    END;

    PROCEDURE LookupProductClass@1100525000(EditBln@1100525001 : Boolean);
    VAR
      ProdClassRec@1100525000 : Record 11012303;
    BEGIN
      ProdClassRec.SETRANGE("Product Group", "Product Group");
      ProdClassRec.SETRANGE("Product Group Line No.", "Product Group Line No.");
      IF NOT EditBln THEN BEGIN
        PAGE.RUNMODAL(PAGE::"ETIM Product Class List", ProdClassRec);
      END ELSE BEGIN
        IF PAGE.RUNMODAL(PAGE::"ETIM Product Class List", ProdClassRec) = ACTION::LookupOK THEN
          "Product Class" := ProdClassRec."Product Class";
      END;
    END;

    PROCEDURE GetItemCode@1100528300() : Code[20];
    VAR
      ItemRelation@1100528300 : Record 11012319;
    BEGIN
      ItemRelation.SETCURRENTKEY(Manufacturer, "Basic Item");
      ItemRelation.SETRANGE(Manufacturer, Manufacturer);
      ItemRelation.SETRANGE("Basic Item", "Product Code");
      IF ItemRelation.FINDFIRST THEN
        EXIT(ItemRelation."Navision Item")
      ELSE
      EXIT('');
    END;

    PROCEDURE DrillDownNavisionItem@1100528301();
    VAR
      ItemRelation@1100525000 : Record 11012319;
      Item@1100528300 : Record 27;
    BEGIN
      ItemRelation.SETCURRENTKEY(Manufacturer, "Basic Item");
      ItemRelation.SETRANGE(Manufacturer, Manufacturer);
      ItemRelation.SETRANGE("Basic Item", "Product Code");
      IF ItemRelation.FINDSET THEN
        REPEAT
          IF Item.GET(ItemRelation."Navision Item") THEN
            Item.MARK(TRUE);
        UNTIL ItemRelation.NEXT = 0;
      Item.MARKEDONLY(TRUE);
      IF NOT Item.ISEMPTY THEN
        PAGE.RUNMODAL(0, Item);
    END;

    PROCEDURE ShowTradeItems@1100525001(ReferenceDate@1100525002 : Date;DiscTermGroupCust@1100525003 : Code[20];DiscTermGroupJob@1100525004 : Code[20]);
    VAR
      TradeItem@1100525000 : Record 11012317;
      TradeItemList@1100525001 : Page 11012334;
    BEGIN
      TradeItem.SETCURRENTKEY("Product Code", Manufacturer, "Item Code", Vendor);
      TradeItem.SETRANGE("Product Code", "Product Code");
      TradeItem.SETRANGE(Manufacturer , Manufacturer);
      TradeItemList.SETTABLEVIEW(TradeItem);
      TradeItemList.SetReference(ReferenceDate,DiscTermGroupCust,DiscTermGroupJob);
      TradeItemList.RUNMODAL;
    END;

    PROCEDURE CopyBasicItemToItem@1100525002();
    VAR
      BasicItem@1100525000 : Record 11012316;
    BEGIN
      BasicItem.SETRANGE("Product Code","Product Code");
      BasicItem.SETRANGE(Manufacturer,Manufacturer);
      REPORT.RUN(REPORT::"Copy Item from Basic Item",TRUE,FALSE,BasicItem);
    END;

    PROCEDURE CopyBasicItemToEPlanItem@1100525003();
    VAR
      BasicItem@1100525000 : Record 11012316;
    BEGIN
      BasicItem.SETRANGE("Product Code","Product Code");
      BasicItem.SETRANGE(Manufacturer,Manufacturer);
      REPORT.RUN(REPORT::"Copy E-Plan from Basic Item",TRUE,FALSE,BasicItem);
    END;

    PROCEDURE UpdateItemData@1100525005();
    VAR
      BasicItem@1100525000 : Record 11012316;
      TradeItem@1100525001 : Record 11012317;
      UpdateItemData@1100525002 : Report 11012304;
    BEGIN
      TradeItem.SETCURRENTKEY("Product Code", Manufacturer);
      TradeItem."Product Code" := "Product Code";
      TradeItem.Manufacturer := Manufacturer;
      TradeItem.SETRANGE("Product Code","Product Code");
      TradeItem.SETRANGE(Manufacturer,Manufacturer);
      UpdateItemData.SETTABLEVIEW(TradeItem);
      BasicItem."Product Code" := "Product Code";
      BasicItem.Manufacturer := Manufacturer;
      BasicItem.SETRANGE("Product Code","Product Code");
      BasicItem.SETRANGE(Manufacturer,Manufacturer);
      UpdateItemData.SETTABLEVIEW(BasicItem);
      UpdateItemData.RUNMODAL;
    END;

    PROCEDURE LookupProductDiscount@1100528900(RefDate@1100525003 : Date;DiscRef1@1100525002 : Code[20];DiscRef2@1100525001 : Code[20]);
    VAR
      ProductDiscount@1100525000 : Record 11012743;
      ProductDiscountList@1100525004 : Page 11071929;
    BEGIN
      //DP01727
      IF RefDate = 0D THEN RefDate := TODAY;
      ProductDiscount.SETRANGE("Discount Type", ProductDiscount."Discount Type"::BasicItem);
      ProductDiscount.SETRANGE(Manufacturer, Manufacturer);
      ProductDiscount.SETRANGE(Code, "Product Code");
      ProductDiscount.SETRANGE("Starting Date", 0D, RefDate);
      ProductDiscount.SETFILTER("Ending Date", '%1|>=%2', 0D, RefDate);
      IF NOT ProductDiscount.FINDLAST THEN BEGIN
        ProductDiscount.SETRANGE("Discount Type", ProductDiscount."Discount Type"::ProductGroup);
        ProductDiscount.SETRANGE(Manufacturer, Manufacturer);
        ProductDiscount.SETRANGE(Code, "Product Group");
        IF NOT ProductDiscount.FINDLAST THEN BEGIN
          ProductDiscount.SETRANGE("Discount Type", ProductDiscount."Discount Type"::Manufacturer);
          ProductDiscount.SETRANGE(Manufacturer, Manufacturer);
          ProductDiscount.SETFILTER(Code, '%1', '');
        END;
      END;

      ProductDiscountList.SETTABLEVIEW(ProductDiscount);
      ProductDiscountList.RUNMODAL;
    END;

    LOCAL PROCEDURE LookupUnitOfMeasure@1100528901(CalledbyField@1100528901 : Integer);
    VAR
      ETIMUnit@1100528900 : Record 11012307;
      UOM@1100528902 : Text;
    BEGIN
      //C056703
      CASE CalledbyField OF
        FIELDNO("Application Unit"): UOM := "Application Unit";
        FIELDNO("Packaging Unit"): UOM := "Packaging Unit";
      END;
      IF UOM = '' THEN
        ETIMUnit.SETFILTER("Unit (NAV)", '<>%1', '')
      ELSE
        ETIMUnit.SETRANGE("Unit (NAV)", UOM);
      IF PAGE.RUNMODAL(0, ETIMUnit) = ACTION::LookupOK THEN BEGIN
        CASE CalledbyField OF
          FIELDNO("Application Unit"): "Application Unit" := ETIMUnit."Unit (NAV)";
          FIELDNO("Packaging Unit"): "Packaging Unit" := ETIMUnit."Unit (NAV)";
        END;
      END;
    END;

    LOCAL PROCEDURE ValidateUnitOfMeasure@1100528902(CalledbyField@1100528902 : Integer);
    VAR
      ETIMUnit@1100528900 : Record 11012307;
      UOM@1100528901 : Text;
    BEGIN
      //C056703
      CASE CalledbyField OF
        FIELDNO("Application Unit"): UOM := UPPERCASE("Application Unit");
        FIELDNO("Packaging Unit"): UOM := UPPERCASE("Packaging Unit");
      END;
      ETIMUnit.SETRANGE("Unit (NAV)", UOM);
      ETIMUnit.FINDSET;

      CASE CalledbyField OF
        FIELDNO("Application Unit"): "Application Unit" := ETIMUnit."Unit (NAV)";
        FIELDNO("Packaging Unit"): "Packaging Unit" := ETIMUnit."Unit (NAV)";
      END;
    END;

    BEGIN
    {
      4PS, C009570, 06-03-14, Performance issue solved: Clustered index changed from Manufacturer,Product Code to Product Code
    }
    END.
  }
}

