OBJECT Page 11229876 Plant Number Picture
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Werkzeugnummer Bild;
               ENU=Plant Number Picture;
               NLD=Afbeelding materieelnummer];
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table11012552;
    PageType=CardPart;
    OnOpenPage=BEGIN
                 CameraAvailable := CameraProvider.IsAvailable;
                 IF CameraAvailable THEN
                   CameraProvider := CameraProvider.Create;
               END;

    OnAfterGetCurrRecord=BEGIN
                           SetEditableOnPictureActions;
                         END;

    ActionList=ACTIONS
    {
      { 1100525006;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100525005;1 ;Action    ;
                      Name=TakePicture;
                      CaptionML=[DEU=Aufnehmen;
                                 ENU=Take;
                                 NLD=Nemen;
                                 NOR=Ta;
                                 SVE=Ta bild];
                      ToolTipML=[DEU=Aktiviert die Kamera auf dem Ger„t.;
                                 ENU=Activate the camera on the device.;
                                 NLD=De camera op het apparaat activeren.;
                                 NOR=Aktiver kameraet p† enheten.;
                                 SVE=Aktivera enhetens kamera.];
                      ApplicationArea=#All;
                      Promoted=Yes;
                      Visible=CameraAvailable;
                      PromotedIsBig=Yes;
                      Image=Camera;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 CameraOptions@1000 : DotNet "'Microsoft.Dynamics.Nav.ClientExtensions, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.Capabilities.CameraOptions";
                               BEGIN
                                 IF NOT CameraAvailable THEN
                                   EXIT;
                                 CameraOptions := CameraOptions.CameraOptions;
                                 CameraOptions.Quality := 100;
                                 CameraProvider.RequestPictureAsync(CameraOptions);
                               END;
                                }
      { 1100525004;1 ;Action    ;
                      Name=ImportPicture;
                      CaptionML=[DEU=Importieren;
                                 ENU=Import;
                                 NLD=Importeren;
                                 NOR=Importer;
                                 SVE=Importera];
                      ToolTipML=[DEU=Importiert eine Bilddatei.;
                                 ENU=Import a picture file.;
                                 NLD=Een afbeeldingsbestand importeren.;
                                 NOR=Importer en bildefil.;
                                 SVE=Importera en bildfil.];
                      ApplicationArea=#All;
                      Image=Import;
                      OnAction=VAR
                                 FileManagement@1001 : Codeunit 419;
                                 FileName@1000 : Text;
                                 ClientFileName@1002 : Text;
                               BEGIN
                                 IF Image.HASVALUE THEN
                                   IF NOT CONFIRM(OverrideImageQst) THEN
                                     EXIT;

                                 FileName := FileManagement.UploadFile(SelectPictureTxt,ClientFileName);
                                 IF FileName = '' THEN
                                   EXIT;

                                 CLEAR(Image);
                                 Image.IMPORTFILE(FileName,ClientFileName);
                                 MODIFY(TRUE);
                                 IF FileManagement.DeleteServerFile(FileName) THEN;
                               END;
                                }
      { 1100525003;1 ;Action    ;
                      Name=ExportFile;
                      CaptionML=[DEU=Exportieren;
                                 ENU=Export;
                                 NLD=Exporteren;
                                 NOR=Eksport;
                                 SVE=Exportera];
                      ToolTipML=[DEU=Exportiert das Bild in eine Datei.;
                                 ENU=Export the picture to a file.;
                                 NLD=De afbeelding exporteren naar een bestand.;
                                 NOR=Eksporter bildet til en fil.;
                                 SVE=Exportera bilden till en fil.];
                      ApplicationArea=#All;
                      Enabled=DeleteExportEnabled;
                      Image=Export;
                      OnAction=VAR
                                 NameValueBuffer@1002 : Record 823;
                                 TempNameValueBuffer@1004 : TEMPORARY Record 823;
                                 FileManagement@1001 : Codeunit 419;
                                 ToFile@1003 : Text;
                                 ExportPath@1005 : Text;
                               BEGIN
                                 NameValueBuffer.DELETEALL;
                                 ExportPath := TEMPORARYPATH + "No." + FORMAT(Image.MEDIAID);
                                 Image.EXPORTFILE(ExportPath);
                                 FileManagement.GetServerDirectoryFilesList(TempNameValueBuffer,TEMPORARYPATH);
                                 TempNameValueBuffer.SETFILTER(Name,STRSUBSTNO('%1*',ExportPath));
                                 TempNameValueBuffer.FINDFIRST;
                                 ToFile := STRSUBSTNO('%1 %3 %2.jpg',"Plant Type","No.",Description);
                                 DOWNLOAD(TempNameValueBuffer.Name,DownloadImageTxt,'','',ToFile);
                                 IF FileManagement.DeleteServerFile(TempNameValueBuffer.Name) THEN;
                               END;
                                }
      { 1100525002;1 ;Action    ;
                      Name=DeletePicture;
                      CaptionML=[DEU=L”schen;
                                 ENU=Delete;
                                 NLD=Verwijderen;
                                 NOR=Fjern;
                                 SVE=Ta bort];
                      ToolTipML=[DEU=L”scht den Datensatz.;
                                 ENU=Delete the record.;
                                 NLD=De record verwijderen.;
                                 NOR=Slett posten.;
                                 SVE=Ta bort transaktionen.];
                      ApplicationArea=#All;
                      Enabled=DeleteExportEnabled;
                      Image=Delete;
                      OnAction=BEGIN
                                 IF NOT CONFIRM(DeleteImageQst) THEN
                                   EXIT;

                                 CLEAR(Image);
                                 MODIFY(TRUE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100525001;0;Container;
                ContainerType=ContentArea }

    { 1100525000;1;Field  ;
                ToolTipML=[DEU=Definiert das Bild des Werkzeugtyps, z. B. ein Logo;
                           ENU=Specifies the picture of the plant type, for example, a logo.;
                           NLD=Hiermee wordt de afbeelding van het materieel opgegeven, bijvoorbeeld een logo.];
                ApplicationArea=#All;
                SourceExpr=Image;
                ShowCaption=No }

  }
  CODE
  {
    VAR
      CameraProvider@1100525002 : DotNet "'Microsoft.Dynamics.Nav.ClientExtensions, Version=14.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.Capabilities.CameraProvider" WITHEVENTS RUNONCLIENT;
      CameraAvailable@1100525001 : Boolean;
      DeleteExportEnabled@1100525000 : Boolean;
      OverrideImageQst@1100525006 : TextConst 'DEU=Das vorhandene Bild wird ersetzt. M”chten Sie fortfahren?;ENU=The existing picture will be replaced. Do you want to continue?;NLD=De bestaande afbeelding wordt vervangen. Wilt u doorgaan?;NOR=Det eksisterende bildet vil bli erstattet. Vil du fortsette?;SVE=Den befintliga bilden ers„tts. Vill du forts„tta?';
      DeleteImageQst@1100525005 : TextConst 'DEU=M”chten Sie das Bild wirklich l”schen?;ENU=Are you sure you want to delete the picture?;NLD=Weet u zeker dat u de afbeelding wilt verwijderen?;NOR=Are you sure you want to delete the picture?;SVE=Vill du ta bort bilden?';
      SelectPictureTxt@1100525004 : TextConst 'DEU=Ein Bild zum Hochladen ausw„hlen;ENU=Select a picture to upload;NLD=Een afbeelding selecteren om te uploaden;NOR=Velg et bilde for opplasting;SVE=V„lj en bild att ladda upp';
      DownloadImageTxt@1100525003 : TextConst 'DEU=Bild herunterladen;ENU=Download image;NLD=Afbeelding downloaden;NOR=Download image;SVE=Ladda ned bild';

    LOCAL PROCEDURE SetEditableOnPictureActions@1();
    BEGIN
      DeleteExportEnabled := Image.HASVALUE;
    END;

    EVENT CameraProvider@1100525002::PictureAvailable@10(PictureName@1100525001 : Text;PictureFilePath@1100525000 : Text);
    VAR
      File@1100525003 : File;
      Instream@1100525002 : InStream;
    BEGIN
      IF (PictureName = '') OR (PictureFilePath = '') THEN
        EXIT;

      IF Image.HASVALUE THEN
        IF NOT CONFIRM(OverrideImageQst) THEN BEGIN
          IF ERASE(PictureFilePath) THEN;
          EXIT;
        END;

      File.OPEN(PictureFilePath);
      File.CREATEINSTREAM(Instream);

      CLEAR(Image);
      Image.IMPORTSTREAM(Instream,PictureName);
      MODIFY(TRUE);

      File.CLOSE;
      IF ERASE(PictureFilePath) THEN;
    END;

    BEGIN
    END.
  }
}

