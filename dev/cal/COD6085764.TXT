OBJECT Codeunit 6085764 Purch. Doc. - Get Status
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.4.00;
  }
  PROPERTIES
  {
    TableNo=6085590;
    OnRun=BEGIN
            "Document Status Text" := GetStatus(Rec);
          END;

  }
  CODE
  {
    VAR
      Text001@1160040000 : TextConst 'DEU=Gebucht;ENU=Posted;NLD=Geboekt;NOR=Bokf›rt;SVE=Bokf”rd';
      Text002@1160040001 : TextConst 'DEU=Ausgeglichen;ENU=Applied;NLD=Vereffend;NOR=Utlignet;SVE=Kopplat';
      Text003@1160040002 : TextConst 'DEU=Nicht angewendet;ENU=Un-applied;NLD=Niet vereffend';

    PROCEDURE GetStatus@1160040000(VAR Document@1160040000 : Record 6085590) : Text[250];
    VAR
      PurchHeader@1160040001 : Record 38;
      PurchInvHeader@1160040002 : Record 122;
      PurchCrMemoHeader@1160040004 : Record 124;
      VendLedgEntry@1160040005 : Record 25;
      PurchDocMgt@1160040003 : Codeunit 6085709;
    BEGIN
      WITH Document DO BEGIN
        IF Status = Status::Open THEN
          EXIT;

        IF Status = Status::Rejected THEN
          EXIT(FORMAT(Status));

        IF Document."Created Doc. Type" <> DATABASE::"Purchase Header" THEN
          EXIT(FORMAT(Status));

        CASE "Created Doc. Subtype" OF
          PurchHeader."Document Type"::Order:
            BEGIN
              PurchInvHeader.SETCURRENTKEY("Order No.");
              PurchInvHeader.SETRANGE("Order No.","Created Doc. ID");
              PurchInvHeader.SETRANGE("Vendor Invoice No.",PurchDocMgt.GetDocumentNo(Document));
              IF PurchInvHeader.FINDFIRST THEN BEGIN
                EXIT(GetLedgEntryStatus(
                  PurchInvHeader."Pay-to Vendor No.",PurchInvHeader."Posting Date",VendLedgEntry."Document Type"::Invoice,
                    PurchInvHeader."No."));
              END ELSE IF PurchHeader.GET("Created Doc. Subtype","Created Doc. ID") THEN
                EXIT(STRSUBSTNO('%1, %2',PurchHeader."Document Type",PurchHeader.Status));
            END;

          PurchHeader."Document Type"::Invoice:
            BEGIN
              IF PurchHeader.GET("Created Doc. Subtype","Created Doc. ID") THEN
                EXIT(STRSUBSTNO('%1, %2',PurchHeader."Document Type",PurchHeader.Status))
              ELSE BEGIN
                PurchInvHeader.SETCURRENTKEY("Pre-Assigned No.");
                PurchInvHeader.SETRANGE("Pre-Assigned No.","Created Doc. ID");
                IF PurchInvHeader.FINDFIRST THEN
                  EXIT(GetLedgEntryStatus(
                    PurchInvHeader."Pay-to Vendor No.",PurchInvHeader."Posting Date",VendLedgEntry."Document Type"::Invoice,
                      PurchInvHeader."No."));
              END;
            END;

          PurchHeader."Document Type"::"Return Order":
            BEGIN
              PurchCrMemoHeader.SETCURRENTKEY("Return Order No.");
              PurchCrMemoHeader.SETRANGE("Return Order No.","Created Doc. ID");
              PurchCrMemoHeader.SETRANGE("Vendor Cr. Memo No.",PurchDocMgt.GetDocumentNo(Document));
              IF PurchCrMemoHeader.FINDFIRST THEN BEGIN
                EXIT(GetLedgEntryStatus(
                  PurchCrMemoHeader."Pay-to Vendor No.",PurchCrMemoHeader."Posting Date",VendLedgEntry."Document Type"::"Credit Memo",
                    PurchCrMemoHeader."No."));
              END ELSE IF PurchHeader.GET("Created Doc. Subtype","Created Doc. ID") THEN
                EXIT(STRSUBSTNO('%1, %2',PurchHeader."Document Type",PurchHeader.Status));
            END;

          PurchHeader."Document Type"::"Credit Memo":
            BEGIN
              IF PurchHeader.GET("Created Doc. Subtype","Created Doc. ID") THEN
                EXIT(STRSUBSTNO('%1, %2',PurchHeader."Document Type",PurchHeader.Status))
              ELSE BEGIN
                PurchCrMemoHeader.SETCURRENTKEY("Pre-Assigned No.");
                PurchCrMemoHeader.SETRANGE("Pre-Assigned No.","Created Doc. ID");
                IF PurchCrMemoHeader.FINDFIRST THEN
                  EXIT(GetLedgEntryStatus(
                    PurchCrMemoHeader."Pay-to Vendor No.",PurchCrMemoHeader."Posting Date",VendLedgEntry."Document Type"::"Credit Memo",
                      PurchCrMemoHeader."No."));
              END;
            END;
        END;
      END;
    END;

    PROCEDURE GetLedgEntryStatus@1160040005(VendorNo@1160040001 : Code[20];PostingDate@1160040003 : Date;LedgType@1160040000 : Integer;DocNo@1160040002 : Code[20]) : Text[250];
    VAR
      VendLedgEntry@1160040004 : Record 25;
    BEGIN
      VendLedgEntry.SETCURRENTKEY("Vendor No.","Posting Date","Currency Code");
      VendLedgEntry.SETRANGE("Vendor No.",VendorNo);
      VendLedgEntry.SETRANGE("Posting Date",PostingDate);
      VendLedgEntry.SETRANGE("Document Type",LedgType);
      VendLedgEntry.SETRANGE("Document No.",DocNo);
      IF VendLedgEntry.FINDFIRST THEN BEGIN
        IF VendLedgEntry.Open THEN
          EXIT(STRSUBSTNO('%1, %2',Text001,Text003))
        ELSE
          EXIT(STRSUBSTNO('%1, %2',Text001,Text002));
      END;
    END;

    BEGIN
    END.
  }
}

