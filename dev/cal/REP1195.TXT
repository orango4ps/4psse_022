OBJECT Report 1195 Adjust Resource Costs/Prices
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Ressourcenpreise justieren;
               ENU=Adjust Resource Costs/Prices;
               NLD=Resourceprijzen herwaarderen;
               NOR=Juster ressurskost/priser;
               SVE=Justera resurskost./-priser...];
    ApplicationArea=#Basic,#Suite;
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  RoundingMethod.SETRANGE(Code,RoundingMethod.Code);
                END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 5508;    ;DataItem;                    ;
               DataItemTable=Table156;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=BEGIN
                               Window.OPEN(Text000);
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(1,"No.");

                                  CASE Selection OF
                                    Selection::"Direct Unit Cost":
                                      OldFieldNo := "Direct Unit Cost";
                                    Selection::"Indirect Cost %":
                                      OldFieldNo := "Indirect Cost %";
                                    Selection::"Unit Cost":
                                      OldFieldNo := "Unit Cost";
                                    Selection::"Profit %":
                                      OldFieldNo := "Profit %";
                                    Selection::"Unit Price":
                                      OldFieldNo := "Unit Price";
                                  END;

                                  NewFieldNo := OldFieldNo * AdjFactor;

                                  IF RoundingMethod.Code <> '' THEN BEGIN
                                    RoundingMethod."Minimum Amount" := NewFieldNo;
                                    IF RoundingMethod.FIND('=<') THEN BEGIN
                                      NewFieldNo := NewFieldNo + RoundingMethod."Amount Added Before";
                                      IF RoundingMethod.Precision > 0 THEN
                                        NewFieldNo := ROUND(NewFieldNo,RoundingMethod.Precision,COPYSTR('=><',RoundingMethod.Type + 1,1));
                                      NewFieldNo := NewFieldNo + RoundingMethod."Amount Added After";
                                    END;
                                  END;

                                  CASE Selection OF
                                    Selection::"Direct Unit Cost":
                                      VALIDATE("Direct Unit Cost",NewFieldNo);
                                    Selection::"Indirect Cost %":
                                      VALIDATE("Indirect Cost %",NewFieldNo);
                                    Selection::"Unit Cost":
                                      VALIDATE("Unit Cost",NewFieldNo);
                                    Selection::"Profit %":
                                      VALIDATE("Profit %",NewFieldNo);
                                    Selection::"Unit Price":
                                      VALIDATE("Unit Price",NewFieldNo);
                                  END;

                                  MODIFY;
                                END;

               ReqFilterFields=No.,Resource Group No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   IF AdjFactor = 0 THEN
                     AdjFactor := 1;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 10  ;2   ;Field     ;
                  CaptionML=[DEU=Feld korrigieren;
                             ENU=Adjust Field;
                             NLD=Veld aanpassen;
                             NOR=Juster felt];
                  ToolTipML=[DEU=Legt die Art der Kosten oder der Preise fest, die reguliert werden sollen.;
                             ENU=Specifies the type of cost or price to be adjusted.;
                             NLD=Hiermee wordt het soort van de te herwaarderen kosten of prijs opgegeven.;
                             NOR=Angir typen kostnad eller pris som skal justeres.];
                  OptionCaptionML=[DEU=EK-Preis,Indirekte Kosten %,Einstandspreis,Gewinn %,VK-Preis;
                                   ENU=Direct Unit Cost,Indirect Cost %,Unit Cost,Profit %,Unit Price;
                                   NLD=Directe kostprijs,Indirecte kosten %,Kostprijs,Winst %,Eenheidsprijs;
                                   NOR=Direkte enhetskost,Indirekte kost-%,Enhetskost,Fortjeneste-%,Enhetspris];
                  ApplicationArea=#Jobs;
                  SourceExpr=Selection }

      { 3   ;2   ;Field     ;
                  CaptionML=[DEU=Korrekturfaktor;
                             ENU=Adjustment Factor;
                             NLD=Herwaarderingsfactor;
                             NOR=Justeringsfaktor];
                  ToolTipML=[DEU=Gibt einen Korrekturfaktor an, mit dem die Betr„ge, die Sie vorschlagen m”chten, multipliziert werden. Durch die Eingabe eines Korrekturfaktors k”nnen Sie die Betr„ge erh”hen oder verringern, die vorgeschlagen werden.;
                             ENU=Specifies an adjustment factor to multiply the amounts that you want suggested. By entering an adjustment factor, you can increase or decrease the amounts that are suggested.;
                             NLD=Hiermee wordt een herwaarderingsfactor opgegeven om de bedragen te vermenigvuldigen die u wilt voorstellen. Door een herwaarderingsfactor in te voeren kunt u de bedragen die worden voorgesteld, verhogen of verlagen.;
                             NOR=Angir en justeringsfaktor for † multiplisere bel›pene du vil foresl†. Ved † angi en justeringsfaktor, kan du ›ke eller minske bel›pene som foresl†s.];
                  ApplicationArea=#Jobs;
                  DecimalPlaces=0:5;
                  SourceExpr=AdjFactor }

      { 1   ;2   ;Field     ;
                  CaptionML=[DEU=Rundungsmethode;
                             ENU=Rounding Method;
                             NLD=Afrondingsmethode;
                             NOR=Avrundingsmetode;
                             SVE=Avrundningsmetod];
                  ToolTipML=[DEU=Legt einen Code fr die Rundungsmethode fest, die Sie auf Kosten oder Preise anwenden m”chten, die reguliert werden sollen.;
                             ENU=Specifies a code for the rounding method that you want to apply to costs or prices that you adjust.;
                             NLD=Hiermee wordt een code opgegeven voor de afrondingsmethode die u wilt toepassen op kosten of prijzen die u herwaardeert.;
                             NOR=Angir en kode for avrundingsmetoden du vil bruke p† kostnader eller priser du justerer.];
                  ApplicationArea=#Jobs;
                  SourceExpr=RoundingMethod.Code;
                  TableRelation="Rounding Method" }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Ressourcen werden durchgelesen   #1##########;ENU=Processing resources  #1##########;NLD=Verwerken resources   #1##########;NOR=Behandler ressurser   #1##########';
      RoundingMethod@1003 : Record 42;
      Window@1006 : Dialog;
      NewFieldNo@1001 : Decimal;
      OldFieldNo@1002 : Decimal;
      AdjFactor@1004 : Decimal;
      Selection@1005 : 'Direct Unit Cost,Indirect Cost %,Unit Cost,Profit %,Unit Price';

    [External]
    PROCEDURE InitializeRequest@1(SelectionFrom@1000 : Option;AdjFactorFrom@1001 : Decimal;RoundingMethodCode@1002 : Code[10]);
    BEGIN
      Selection := SelectionFrom;
      AdjFactor := AdjFactorFrom;
      RoundingMethod.Code := RoundingMethodCode;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

