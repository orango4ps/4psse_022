OBJECT Table 11012552 Plant Number
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 5601=ri,
                TableData 11012551=rm;
    DataCaptionFields=Plant Type,No.,Description;
    OnInsert=VAR
               InstallationManagement@1100526000 : Codeunit 11012268;
               PlantID@1100526001 : Record 11124922;
             BEGIN
               PlantSetup.GET;

               IF "No." = '' THEN
                 "No." := FirstFreeNo;

               IF "No." = '0' THEN
                 ERROR(Text007);

               CALCFIELDS(Bulk);
               IF NOT Bulk THEN
                 Quantity := 1;

               "Fixed Asset Inactivate" := PlantSetup."Default Fixed Asset Inactivate";
               //"Ship To Location" := PlantSetup."Ship To Location";  //*23594.o
               "Ship To Location" := PlantSetup.GetShipToLocation;     //*23594.n
               "Receipt Date" := WORKDATE;
               Posted := FALSE;
               "Posting Date" := 0D;
               "Fixed Asset Created" := FALSE;

               FillInventoryNo;
               FillInspectionNo();  //DP00241.n (16161)
               IF "Department Code" <> '' THEN
                 ValidateShortcutDimCode(1,"Department Code");

               IF PlantSetup.NumberingPerPlantTypeYesOrYesInclCompNo(PlantSetup."Numbering per Plant Type") THEN BEGIN
                 PlantRec.GET("Plant Type");
                 PlantRec."Last Used Plant Number" := "No.";
                 PlantRec.MODIFY;
               END;

               CopyTechSpecsFromPlantType; //mg, 12-04-11: M25005
               CopyPlantParts("Plant Type", '', "Plant Type", "No.");
               CopyPlantMeterTypes("Plant Type", '', "No.");
               CopyPlantMaintenanceSchemeFromPlantType;

               IF ("Plant Type" <> '') THEN BEGIN
                 PlantRec.GET("Plant Type");
                 "Blocked (Plant Type)" := PlantRec.Blocked;
               END;

               IF InstallationManagement.LicensePermissionForTableData(DATABASE::"Plant ID") THEN BEGIN
                 PlantID.VALIDATE("Plant Type", "Plant Type");
                 IF Bulk THEN
                   PlantID."Plant No." := ''
                 ELSE
                   PlantID."Plant No." := "No.";
                 PlantID.ID := STRSUBSTNO('%1%2', PlantID."Plant Type", PlantID."Plant No.");
                 IF PlantID.INSERT THEN;
               END;
             END;

    OnModify=BEGIN
               IF NOT Posted THEN
                 FillInventoryNo;

               UpdDepartmentCodeInspections();  //DP00241.n (26914)
               UpdateResource(1, xRec, Rec);  // 1=Modify
               UpdatePlantMaintSchemeStartingDate;
             END;

    OnDelete=VAR
               lvRentalRateRec@1100485000 : Record 11012567;
               lvPlantProfileAnswerRec@1210190000 : Record 11012582;
               lvInspectRec@1100485001 : Record 11012564;
               lvPlantRevRec@1100485002 : Record 11012573;
               lvPlantCostLineRec@1100485004 : Record 11012578;
               lvLocRentalRateRec@1100485005 : Record 11012583;
               lvCustRentalRateRec@1100485006 : Record 11012541;
               lvSetLineRec@1100485007 : Record 11012588;
               lvExtRentEntryRec@1100485003 : Record 11012535;
               lvPlantTransRec@1100485008 : Record 11012542;
               lvRentDiscRec@1100485009 : Record 11012544;
               RentalQuantityDiscount@1100528800 : Record 11072669;
               lvFARec@1100525000 : Record 5600;
               lvFALedgEntryRec@1100525001 : Record 5601;
               Vehicle@1100529000 : Record 11072667;
               PlantMaintenanceArrangement@1100529008 : Record 11126146;
               PlantMaintProposalLine@1100529009 : Record 11126148;
               PlantMaintenanceScheme@1100528801 : Record 11072677;
               PlantMeterType@1100529400 : Record 11229921;
               PlantMeterReading@1100529006 : Record 11072671;
               PlantMRDeviatingExpectQty@1100529007 : Record 11229938;
               PlantWarranty@1100529001 : Record 11229924;
               PlantMaterialInventory@1100529002 : Record 11229926;
               PlantPartPlantLevel@1100529003 : Record 11229932;
               PlantPartProblem@1100529004 : Record 11229931;
               PlantID@1100528000 : Record 11124922;
               InstallationManagement@1100528001 : Codeunit 11012268;
               PlantRentalAgreement@1100529005 : Record 11229935;
             BEGIN
               IF "No." = '' THEN
                 EXIT;

               CheckDeleteAllowed();

               IF Posted THEN BEGIN
                 //* Filters exact zoals in de functie 'CheckDeleteAllowed'.
                 PlantInventRec.RESET;
                 PlantInventRec.SETRANGE(Removed, FALSE);
                 PlantInventRec.SETRANGE(Location, "Ship To Location");
                 PlantInventRec.SETRANGE("Plant Type", "Plant Type");
                 PlantInventRec.SETRANGE("Plant No.", "Stock No.");
                 PlantInventRec.SETRANGE(Quantity, Quantity);
                 PlantInventRec.SETRANGE("Present from", "Receipt Date");
                 PlantInventRec.SETRANGE("Arrival Order Type", PlantInventRec."Arrival Order Type"::Receipt);
                 IF PlantInventRec.FINDFIRST THEN
                   PlantInventRec.DELETE;
               END;

               lvRentalRateRec.SETRANGE("Plant Type","Plant Type");
               lvRentalRateRec.SETRANGE("Plant No.", "No.");
               lvRentalRateRec.DELETEALL(TRUE);

               lvPlantProfileAnswerRec.SETRANGE(Level,lvPlantProfileAnswerRec.Level::PlantNumber);
               lvPlantProfileAnswerRec.SETRANGE("Plant Type","Plant Type");
               lvPlantProfileAnswerRec.SETRANGE("Plant No.","No.");
               lvPlantProfileAnswerRec.DELETEALL;

               lvInspectRec.SETRANGE("Plant Type","Plant Type");
               lvInspectRec.SETRANGE("Plant No.","No.");
               lvInspectRec.DELETEALL(TRUE);

               lvPlantRevRec.SETRANGE("Plant Type","Plant Type");
               lvPlantRevRec.SETRANGE("Plant No.", "No.");
               lvPlantRevRec.DELETEALL(TRUE);

               lvPlantCostLineRec.SETRANGE("Plant Type","Plant Type");
               lvPlantCostLineRec.SETRANGE("Plant No.","No.");
               lvPlantCostLineRec.DELETEALL;

               lvLocRentalRateRec.SETRANGE("Plant Type","Plant Type");
               lvLocRentalRateRec.SETRANGE("Plant No.","No.");
               lvLocRentalRateRec.DELETEALL;

               lvCustRentalRateRec.SETRANGE("Plant Type","Plant Type");
               lvCustRentalRateRec.SETRANGE("Plant No.","No.");
               lvCustRentalRateRec.DELETEALL;

               lvRentDiscRec.SETRANGE("Plant Type", "Plant Type");
               lvRentDiscRec.SETRANGE("Plant No.", "No.");
               lvRentDiscRec.DELETEALL;

               RentalQuantityDiscount.SETRANGE("Plant Type", "Plant Type");
               RentalQuantityDiscount.SETRANGE("Plant No.", "No.");
               RentalQuantityDiscount.DELETEALL;

               lvSetLineRec.SETCURRENTKEY("Plant Type", "Plant No.");
               lvSetLineRec.SETRANGE("Plant Type","Plant Type");
               lvSetLineRec.SETRANGE("Plant No.","No.");
               lvSetLineRec.SETRANGE(Type, lvSetLineRec.Type::Plant);
               lvSetLineRec.DELETEALL;

               lvExtRentEntryRec.SETRANGE("Plant Type","Plant Type");
               lvExtRentEntryRec.SETRANGE("Plant No.","No.");
               lvExtRentEntryRec.DELETEALL;

               lvPlantTransRec.SETRANGE(Type,lvPlantTransRec.Type::PlantNo);  //DP00241.n (27089)
               lvPlantTransRec.SETRANGE(Code,"Plant Type");
               lvPlantTransRec.SETRANGE("No.","No.");
               lvPlantTransRec.DELETEALL;

               Vehicle.SETRANGE("Plant Type","Plant Type");  //DP00331
               Vehicle.SETRANGE("Plant No.","No.");
               Vehicle.DELETEALL(TRUE);

               PlantMaintenanceArrangement.SETRANGE("Plant Type", "Plant Type");
               PlantMaintenanceArrangement.SETRANGE("Plant No.", "No.");
               PlantMaintenanceArrangement.DELETEALL(FALSE);

               PlantMaintProposalLine.SETRANGE("Plant Type", "Plant Type");
               PlantMaintProposalLine.SETRANGE("Plant No.", "No.");
               PlantMaintProposalLine.DELETEALL;

               PlantMaintenanceScheme.SETRANGE("Plant Type", "Plant Type");
               PlantMaintenanceScheme.SETRANGE("Plant No.", "No.");
               PlantMaintenanceScheme.DELETEALL(TRUE);

               PlantWarranty.SETRANGE("Plant Type", "Plant Type");
               PlantWarranty.SETRANGE("Plant No.", "No.");
               PlantWarranty.DELETEALL(TRUE);

               PlantMaterialInventory.SETRANGE("Plant Type", "Plant Type");
               PlantMaterialInventory.SETRANGE("Plant No.", "No.");
               PlantMaterialInventory.DELETEALL(TRUE);

               PlantPartProblem.SETRANGE(Level, PlantPartPlantLevel.Level::PlantNumber);
               PlantPartProblem.SETRANGE("Plant Type", "Plant Type");
               PlantPartProblem.SETRANGE("Plant No.", "No.");
               PlantPartProblem.DELETEALL;

               PlantPartPlantLevel.SETRANGE(Level, PlantPartPlantLevel.Level::PlantNumber);
               PlantPartPlantLevel.SETRANGE("Plant Type", "Plant Type");
               PlantPartPlantLevel.SETRANGE("Plant No.", "No.");
               PlantPartPlantLevel.DELETEALL;

               IF InstallationManagement.LicensePermissionForTableData(DATABASE::"Plant ID") THEN BEGIN
                 PlantID.SETRANGE("Plant Type", "Plant Type");
                 PlantID.SETRANGE("Plant No.", "No.");
                 PlantID.DELETEALL;
               END;

               PlantRentalAgreement.SETRANGE("Plant Type", "Plant Type");
               PlantRentalAgreement.SETRANGE("Plant No.", "No.");
               PlantRentalAgreement.DELETEALL(TRUE);

               IF "Fixed Asset" <> '' THEN BEGIN  //* Melding 12907
                 IF lvFARec.GET("Fixed Asset") THEN BEGIN
                   //* If there are no FA-Ledger-Entries then delete the Fixed Asset.
                   lvFALedgEntryRec.SETCURRENTKEY("FA No.");
                   lvFALedgEntryRec.SETRANGE("FA No.", "Fixed Asset");
                   IF NOT lvFALedgEntryRec.FINDFIRST THEN
                     lvFARec.DELETE(TRUE);  //* With 'TRUE' trigger !
                 END;
               END;

               DimMgt.DeleteDefaultDim(DATABASE::"Plant Number", DimMgt.MakeCombinedPlantNo("Plant Type","No."));
               DeleteDocumentLinks(); //DP00183.n
               DeleteCommentLines();  //DP00183.n
               UpdateResource(2, xRec, Rec);  // 2=Delete

               { Merge MBS4.00->MBS2009
               IF WebSite.FIND('-') AND (NOT Bulk) THEN
                 SynchMgt.DeletePlantNumber(Rec);
               }

               PlantMeterReading.SETRANGE("Plant Type", "Plant Type");
               PlantMeterReading.SETRANGE("Plant No.", "No.");
               PlantMeterReading.DELETEALL;

               PlantMRDeviatingExpectQty.SETRANGE("Plant Type", "Plant Type");
               PlantMRDeviatingExpectQty.SETRANGE("Plant No.", "No.");
               PlantMRDeviatingExpectQty.DELETEALL;

               //**4PS.sn BI049 KD 10-08-16
               PlantMeterType.SETRANGE("Plant Type","Plant Type");
               PlantMeterType.SETRANGE("Plant No.","No.");
               PlantMeterType.DELETEALL;
               //**4PS.en BI049 KD 10-08-16
             END;

    OnRename=VAR
               Vehicle@1100529000 : Record 11072667;
             BEGIN
               TESTFIELD(Posted,FALSE);
               IF "No." = '0' THEN
                 ERROR(Text007);
               IF Vehicle.GET("Plant Type", "No.") THEN  //DP00331
                 ERROR(TABLECAPTION + '-' + Vehicle.TABLECAPTION);

               DimMgt.CopyDefaultDim(
                 DATABASE::"Plant Number", DimMgt.MakeCombinedPlantNo(xRec."Plant Type",xRec."No."),
                 DATABASE::"Plant Number", DimMgt.MakeCombinedPlantNo("Plant Type","No."));
               DimMgt.DeleteDefaultDim(DATABASE::"Plant Number", DimMgt.MakeCombinedPlantNo(xRec."Plant Type",xRec."No."));
               MoveDocumentLinks(xRec, Rec, FALSE); //DP00183.n
               UpdateResource(3, xRec, Rec);  // 3=Rename
             END;

    CaptionML=[ENU=Plant Number;
               NOR=Maskinnr.;
               SVE=Maskinnummer];
    LookupPageID=Page11012555;
    DrillDownPageID=Page11012555;
  }
  FIELDS
  {
    { 10  ;   ;Plant Type          ;Code20        ;TableRelation="Plant Type";
                                                   OnValidate=BEGIN
                                                                IF ("Plant Type" <> '') THEN BEGIN
                                                                  PlantRec.GET("Plant Type");
                                                                  VALIDATE(Description, PlantRec.Description);
                                                                  //"Purchase Price" := PlantRec."Current Price"; Niet vullen, gebeurt bij boeken inkoopfacturen (wordt er dan bij opgeteld)
                                                                  "Blocked (Plant Type)" := PlantRec.Blocked;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Plant Type;
                                                              NOR=Maskintype;
                                                              SVE=Maskintyp] }
    { 20  ;   ;No.                 ;Code10        ;OnValidate=BEGIN
                                                                IF "No." = '0' THEN
                                                                  ERROR(Text007);

                                                                FillInventoryNo;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              NOR=Nr;
                                                              SVE=Nr];
                                                   SQL Data Type=Integer;
                                                   Numeric=Yes }
    { 30  ;   ;Description         ;Text50        ;OnValidate=BEGIN
                                                                UpdateFADescription();
                                                                IF ("Search Name" = UPPERCASE(COPYSTR(xRec.Description, 1, MAXSTRLEN("Search Name")))) OR ("Search Name" = '') THEN
                                                                  "Search Name" := COPYSTR(Description, 1, MAXSTRLEN("Search Name"));
                                                              END;

                                                   CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 35  ;   ;Search Name         ;Code30        ;DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Search Name;
                                                              SVE=Sîknamn] }
    { 40  ;   ;Quantity            ;Decimal       ;CaptionML=[ENU=Quantity;
                                                              NOR=Antall;
                                                              SVE=Antal];
                                                   DecimalPlaces=0:5 }
    { 50  ;   ;Quantity in Stock   ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Inventory".Quantity WHERE (Removed=CONST(No),
                                                                                                     Plant Type=FIELD(Plant Type),
                                                                                                     Plant No.=FIELD(Stock No.),
                                                                                                     Sold=CONST(No)));
                                                   CaptionML=[ENU=Quantity in Stock;
                                                              NOR=Antall i Lager;
                                                              SVE=Antal i lager];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 55  ;   ;Quantity External Rented;Decimal   ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Rent Entry".Quantity WHERE (Removed=CONST(No),
                                                                                                      Plant Type=FIELD(Plant Type),
                                                                                                      Plant No.=FIELD(No.),
                                                                                                      Present from=FIELD(Date Filter)));
                                                   CaptionML=[ENU=Quantity External Rented;
                                                              NOR=Antall Eksternt Utleid;
                                                              SVE=Antal externt uthyrt];
                                                   DecimalPlaces=0:5;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 60  ;   ;Ship To Location    ;Code20        ;TableRelation="Plant Location";
                                                   CaptionML=[ENU=Ship To Location;
                                                              NOR=Lever til lokasjon;
                                                              SVE=Leverera till maskinplats] }
    { 70  ;   ;Receipt Date        ;Date          ;CaptionML=[ENU=Receipt Date;
                                                              NOR=Kvitteringsdato;
                                                              SVE=Inleveransdatum] }
    { 80  ;   ;Unit of Measure     ;Code10        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Type"."Unit of Measure" WHERE (Code=FIELD(Plant Type)));
                                                   CaptionML=[ENU=Unit of Measure;
                                                              NOR=MÜleenhet;
                                                              SVE=Enhet];
                                                   Editable=No }
    { 90  ;   ;Vendor              ;Code20        ;TableRelation=Vendor;
                                                   OnValidate=VAR
                                                                PurchHeadRec@1100525000 : Record 38;
                                                              BEGIN
                                                                CALCFIELDS("Vendor Name");

                                                                IF (CurrFieldNo = FIELDNO(Vendor)) AND (Vendor <> xRec.Vendor) AND ("Purchase Order No." <> '') THEN BEGIN
                                                                  IF PurchHeadRec.GET(PurchHeadRec."Document Type"::Order, "Purchase Order No.") THEN BEGIN
                                                                    IF Vendor <> PurchHeadRec."Buy-from Vendor No." THEN
                                                                      MESSAGE(Text015, "Plant Type", "No.", "Purchase Order No.", PurchHeadRec."Buy-from Vendor No.");
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Vendor;
                                                              NOR=Leverandõr;
                                                              SVE=Leverantîr] }
    { 91  ;   ;Vendor Name         ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor.Name WHERE (No.=FIELD(Vendor)));
                                                   CaptionML=[ENU=Vendor Name;
                                                              NOR=Leverandõrsnavn;
                                                              SVE=Leverantîrsnamn];
                                                   Editable=No }
    { 100 ;   ;Bulk                ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Plant Type".Bulk WHERE (Code=FIELD(Plant Type)));
                                                   CaptionML=[ENU=Bulk;
                                                              NOR=Bulk;
                                                              SVE=Bulk];
                                                   Editable=No }
    { 101 ;   ;Blocked (Plant Type);Boolean       ;CaptionML=[ENU=Blocked (Plant Type);
                                                              SVE=SpÑrrad (Maskintyp)];
                                                   Editable=No }
    { 110 ;   ;Posted              ;Boolean       ;CaptionML=[ENU=Posted;
                                                              NOR=Bokfõrt;
                                                              SVE=Bokfîrd];
                                                   Editable=No }
    { 120 ;   ;Posting Date        ;Date          ;CaptionML=[ENU=Posting Date;
                                                              NOR=Bokfõringsdato;
                                                              SVE=Bokfîringsdatum];
                                                   Editable=No }
    { 130 ;   ;Purchase Price      ;Decimal       ;CaptionML=[ENU=Purchase Price;
                                                              NOR=Innkjõpspris;
                                                              SVE=Inkîpspris];
                                                   MinValue=0;
                                                   BlankZero=Yes;
                                                   AutoFormatType=2 }
    { 140 ;   ;Technical Specifications;Text250   ;CaptionML=[ENU=Technical Specifications;
                                                              NOR=Teknisk Spesifikasjoner;
                                                              SVE=Tekniska specifikationer] }
    { 150 ;   ;Exploded on No.     ;Code10        ;CaptionML=[ENU=Exploded on No.;
                                                              NOR=Utfoldet pÜ nr.;
                                                              SVE=Expanderad pÜ nr];
                                                   Editable=No }
    { 160 ;   ;External            ;Boolean       ;OnValidate=BEGIN
                                                                CALCFIELDS(Bulk);
                                                                PlantRec.GET("Plant Type");
                                                                DoValidateExternal(Bulk, PlantRec);
                                                                GetDefaultExternalRateComponent;
                                                              END;

                                                   CaptionML=[ENU=External;
                                                              NOR=Eksternt;
                                                              SVE=Externt] }
    { 170 ;   ;Purchase Order No.  ;Code20        ;TableRelation="Purchase Header".No. WHERE (Document Type=CONST(Order));
                                                   OnLookup=VAR
                                                              PurchaseHeader@1100525000 : Record 38;
                                                            BEGIN
                                                              IF "Purchase Order No." <> '' THEN BEGIN
                                                                PurchaseHeader."Document Type" := PurchaseHeader."Document Type"::Order;
                                                                PurchaseHeader."No." := "Purchase Order No.";
                                                                PAGE.RUNMODAL(PAGE::"Purchase Order List", PurchaseHeader);
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Purchase Order No.;
                                                              NOR=Innkjõpsordrenr.;
                                                              SVE=Inkîpsordernr];
                                                   Editable=No }
    { 180 ;   ;External Post no Surcharge;Boolean ;OnValidate=BEGIN
                                                                IF "External Post no Surcharge" THEN
                                                                  TESTFIELD(External, TRUE);
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=External Post no Surcharge;
                                                              SVE=Extern bokfîring utan tillÑggsavgift] }
    { 200 ;   ;Stock No.           ;Code10        ;CaptionML=[ENU=Stock No.;
                                                              NOR=Lagernr.;
                                                              SVE=Lagernr];
                                                   Editable=No }
    { 205 ;   ;Fixed Asset Inactivate;Boolean     ;CaptionML=[ENU=Fixed Asset Inactive;
                                                              NOR=Driftsmidler inaktiv;
                                                              SVE=AnlÑggningstillgÜng inaktiv] }
    { 210 ;   ;Fixed Asset         ;Code20        ;TableRelation="Fixed Asset".No.;
                                                   CaptionML=[ENU=Fixed Asset;
                                                              NOR=Driftsmiddel;
                                                              SVE=AnlÑggningstillgÜngar];
                                                   Editable=No }
    { 211 ;   ;Fixed Asset Created ;Boolean       ;CaptionML=[ENU=Fixed Asset Created;
                                                              SVE=AnlÑggningstillgÜng skapad];
                                                   Editable=No }
    { 220 ;   ;Plant Calendar      ;Code10        ;TableRelation="Plant Calendar";
                                                   OnValidate=BEGIN
                                                                //DP00241 (29768)
                                                                IF "Plant Calendar" = '' THEN
                                                                  TESTFIELD("Take into Account Rental Cal.", FALSE);
                                                              END;

                                                   CaptionML=[ENU=Plant Calendar;
                                                              NOR=Maskinkalender;
                                                              SVE=Maskinkalender] }
    { 221 ;   ;Take into Account Rental Cal.;Boolean;
                                                   OnValidate=BEGIN
                                                                //DP00241 (29768)
                                                                IF "Take into Account Rental Cal." THEN
                                                                  TESTFIELD("Plant Calendar");
                                                              END;

                                                   CaptionML=[ENU=Take into Account Rental Calendar;
                                                              NOR=Ta med Leiekalender i Beregningen] }
    { 230 ;   ;External Rent Calendar;Code10      ;TableRelation="Plant Calendar";
                                                   CaptionML=[ENU=External Rent Calendar;
                                                              NOR=Ekstern Leiekalender;
                                                              SVE=Extern hyreskalender] }
    { 240 ;   ;Serial No.          ;Code50        ;CaptionML=[ENU=Serial No.;
                                                              NOR=Serienr;
                                                              SVE=Serienr] }
    { 245 ;   ;Registration No.    ;Code20        ;CaptionML=[ENU=Registration No.;
                                                              NOR=Registreringsnr.;
                                                              SVE=Organisationsnr] }
    { 250 ;   ;Insurance Policy No.;Text20        ;CaptionML=[ENU=Insurance Policy No.;
                                                              NOR=Forsikringspolisenr.;
                                                              SVE=FîrsÑkringsbrev, nr] }
    { 260 ;   ;Licence Number      ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Vehicle 4PS"."Licence Number" WHERE (Plant Type=FIELD(Plant Type),
                                                                                                            Plant No.=FIELD(No.)));
                                                   CaptionML=[ENU=License Number;
                                                              NOR=Lisensnummer;
                                                              SVE=Licensnummer];
                                                   NotBlank=Yes;
                                                   Description=Vehicle;
                                                   Editable=No }
    { 300 ;   ;No Rate Component Posting;Boolean  ;CaptionML=ENU=No Rate Component Posting;
                                                   Description=For Rental and/Hours }
    { 310 ;   ;Values on Plant No. Level;Boolean  ;OnValidate=VAR
                                                                PlantType@1100529000 : Record 11012551;
                                                              BEGIN
                                                                IF "Values on Plant No. Level" THEN BEGIN
                                                                  IF PlantType.GET("Plant Type") THEN BEGIN
                                                                    IF "Current Price" = 0 THEN
                                                                      "Current Price" := PlantType."Current Price";
                                                                    IF "No. of Depreciation Years" = 0 THEN
                                                                      "No. of Depreciation Years" := PlantType."No. of Depreciation Years";
                                                                    IF "Scrap Value" = 0 THEN
                                                                      "Scrap Value" := PlantType."Scrap Value";
                                                                    IF "Expected Occupation Perc." = 0 THEN
                                                                      "Expected Occupation Perc." := PlantType."Expected Occupation Perc.";
                                                                  END;
                                                                END ELSE BEGIN
                                                                  TESTFIELD("Current Price", 0);
                                                                  TESTFIELD("No. of Depreciation Years", 0);
                                                                  TESTFIELD("Scrap Value", 0);
                                                                  TESTFIELD("Expected Occupation Perc.", 0);
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Values on Plant No. Level }
    { 311 ;   ;Current Price       ;Decimal       ;CaptionML=[ENU=Current Price;
                                                              NOR=Aktuell pris;
                                                              SVE=Aktuellt pris];
                                                   MinValue=0;
                                                   BlankZero=Yes;
                                                   AutoFormatType=2 }
    { 312 ;   ;No. of Depreciation Years;Decimal  ;CaptionML=[ENU=No. of Depreciation Years;
                                                              NOR=Antall AvskrivningsÜr;
                                                              SVE=Antal avskrivningsÜr];
                                                   DecimalPlaces=2:8;
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 313 ;   ;Scrap Value         ;Decimal       ;CaptionML=[ENU=Scrap Value;
                                                              NOR=Utrangeringsverdi;
                                                              SVE=SkrotvÑrde];
                                                   MinValue=0 }
    { 320 ;   ;Expected Occupation Perc.;Decimal  ;CaptionML=[ENU=Expected Occupation Perc.;
                                                              NOR=Forventet beleggprosent;
                                                              SVE=FîrvÑntad belÑggningsprocent];
                                                   MinValue=0 }
    { 330 ;   ;Posting Group Internal;Code10      ;TableRelation="Plant Posting Group";
                                                   CaptionML=[ENU=Posting Group Internal;
                                                              NOR=Intern Bokfõringsgruppe;
                                                              SVE=Intern bokfîringsmall] }
    { 340 ;   ;Posting Group External;Code10      ;TableRelation="Plant Posting Group";
                                                   CaptionML=[ENU=Posting Group External;
                                                              NOR=Ekstern Bokfõringsgruppe;
                                                              SVE=Extern bokfîringsmall] }
    { 390 ;   ;Blocked             ;Boolean       ;CaptionML=[ENU=Blocked;
                                                              NOR=Blokkert;
                                                              SVE=SpÑrrad] }
    { 400 ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Date Filter;
                                                              NOR=Dato filter;
                                                              SVE=Datumfilter] }
    { 410 ;   ;Revenues            ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Ledger Entry"."Total Price" WHERE (Plant Type=FIELD(Plant Type),
                                                                                                             Plant No.=FIELD(No.),
                                                                                                             Posting Date=FIELD(Date Filter),
                                                                                                             Relate to=FIELD(Revenue Filter),
                                                                                                             Gen. Bus. Posting Group=FIELD(Gen. Bus. Posting Group Filter),
                                                                                                             Plant Posting Group=FIELD(Posting Group Filter)));
                                                   CaptionML=[ENU=Revenues;
                                                              NOR=Inntekter;
                                                              SVE=IntÑkter];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 420 ;   ;Costs               ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Plant Ledger Entry"."Total Cost" WHERE (Plant Type=FIELD(Plant Type),
                                                                                                            Plant No.=FIELD(No.),
                                                                                                            Posting Date=FIELD(Date Filter),
                                                                                                            Cost Component=FIELD(Cost Component Filter),
                                                                                                            Plant Posting Group=FIELD(Posting Group Filter)));
                                                   CaptionML=[ENU=Costs;
                                                              NOR=Kostnader;
                                                              SVE=Kostnader];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 430 ;   ;Cost Component Filter;Code10       ;FieldClass=FlowFilter;
                                                   TableRelation="Cost Component Plant";
                                                   CaptionML=[ENU=Cost Component Filter;
                                                              NOR=Kostdel, filter;
                                                              SVE=Kostnadsdel, filter] }
    { 440 ;   ;Revenue Filter      ;Option        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Revenue Filter;
                                                              NOR=Inntektsfilter;
                                                              SVE=IntÑkter, filter];
                                                   OptionCaptionML=[ENU=,Rental,Sales,Missing,Scrap,Buy Back,Hours,Transport,Sundry Costs,Plant Hours,Damage,Job Order,Load/Unload,Item,Item Buy-Back;
                                                                    NOR=,Leie,Salg,Mangler,Utrangeres,Tilbakekjõp,Timer,Transport,Diversekoster,Maskintimer,Skade,Jobbordre,Lasting/Avlasting,Artikkel,Artikkel Tilbakekjõp;
                                                                    SVE=,Uthyrning,FîrsÑljning,Saknas,Skrot,èterkîp,Timmar,Transport,Diversekostnader,Maskintimmar,Skada,Arbetsorder,Lastning/avlastning,Artikel,ArtikelÜterkîp];
                                                   OptionString=,Rental,Sales,Missing,Scrap,Buy Back,Hours,Transport,Sundry Costs,Plant Hours,Damage,Job Order,Load/Unload,Item,ItemBuyBack;
                                                   BlankZero=Yes }
    { 450 ;   ;Gen. Bus. Posting Group Filter;Code20;
                                                   FieldClass=FlowFilter;
                                                   TableRelation="Gen. Business Posting Group";
                                                   CaptionML=[ENU=Gen. Bus. Posting Group Filter;
                                                              NOR=Generell bevegelse, Bokfõringsgruppefilter;
                                                              SVE=Allm. rîrelse, bokfîringsmallfilter] }
    { 460 ;   ;Posting Group Filter;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation="Plant Posting Group";
                                                   CaptionML=[ENU=Posting Group Filter;
                                                              NOR=Bokfõringsgruppefilter;
                                                              SVE=Bokfîringsmallfilter] }
    { 470 ;   ;Department Code     ;Code20        ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=VAR
                                                                Inspection@1100525000 : Record 11012564;
                                                              BEGIN
                                                                ValidateShortcutDimCode(1,"Department Code");

                                                                //**4PS.dbeekman.sn: 5-10-2010, M22760
                                                                Inspection.SETRANGE("Plant Type", "Plant Type");
                                                                Inspection.SETRANGE("Plant No.", "No.");
                                                                Inspection.MODIFYALL("Department Code", "Department Code");
                                                                //**4PS.dbeekman.en
                                                              END;

                                                   CaptionML=[ENU=Global Dimension 1 Code;
                                                              NOR=Avdelingskode;
                                                              SVE=Avdelningskod];
                                                   CaptionClass='1,1,1' }
    { 480 ;   ;Inspection No.      ;Code30        ;OnValidate=BEGIN
                                                                //DP00241.so (16161)   Move to a function
                                                                //IF "Inspection No." <> '' THEN BEGIN
                                                                //  PlantNoRec.RESET;
                                                                //  PlantNoRec.SETCURRENTKEY("Inspection No.");
                                                                //  PlantNoRec.SETRANGE("Inspection No.", "Inspection No.");
                                                                //  IF PlantNoRec.FIND('-') THEN BEGIN
                                                                //    IF (PlantNoRec."Plant Type" = "Plant Type") AND (PlantNoRec."No." = "No.") THEN
                                                                //      InspectionNoAlreadyUsed := (PlantNoRec.NEXT <> 0)
                                                                //    ELSE
                                                                //      InspectionNoAlreadyUsed := TRUE;
                                                                //    IF InspectionNoAlreadyUsed THEN
                                                                //      ERROR(Text004,FIELDCAPTION("Inspection No."),"Inspection No.",PlantNoRec."Plant Type", PlantNoRec."No.");
                                                                //  END;
                                                                //END;
                                                                //DP00241.eo
                                                                ValidateInspectionNo(TRUE);  //DP00241.n (16161)
                                                              END;

                                                   CaptionML=[ENU=Inspection No.;
                                                              NOR=Inspeksjonnr.;
                                                              SVE=Inspektionsnr] }
    { 490 ;   ;Ext. Rented Invoiced to;Date       ;CaptionML=[ENU=Ext. Rented Invoiced to;
                                                              NOR=ETA Utleid er fakturert til;
                                                              SVE=éTA Uthyrd Ñr fakturerad till] }
    { 500 ;   ;CP Plant Number     ;Code30        ;CaptionML=[ENU=CP Plant No.;
                                                              NOR=CP-Maskinnr.;
                                                              SVE=CP-Maskinnr];
                                                   Description=BNBM }
    { 510 ;   ;Coverage Date       ;Date          ;CaptionML=ENU=Coverage Date }
    { 520 ;   ;Image               ;Media         ;CaptionML=[ENU=Image;
                                                              NOR=Image;
                                                              SVE=Bild] }
    { 530 ;   ;Location            ;Code10        ;TableRelation=Location;
                                                   CaptionML=[ENU=Location;
                                                              NOR=Plassering;
                                                              SVE=LagerstÑlle] }
  }
  KEYS
  {
    {    ;Plant Type,No.                          ;Clustered=Yes }
    {    ;Plant Type,Receipt Date,No.              }
    {    ;Inspection No.                           }
    {    ;Vendor,Plant Type,No.                    }
    {    ;Fixed Asset,Fixed Asset Created          }
    {    ;Plant Type,Search Name                   }
    {    ;Search Name                              }
    {    ;Blocked (Plant Type)                     }
  }
  FIELDGROUPS
  {
    { 1   ;Brick               ;No.,Description,Purchase Price,Image     }
    { 2   ;DropDown            ;Plant Type,No.,Description,Search Name   }
  }
  CODE
  {
    VAR
      Text000@11012005 : TextConst 'ENU=Explode?;NOR=Utfolde?;SVE=Expandera?';
      PlantSetup@11012000 : Record 11012550;
      FASetup@1210190003 : Record 5603;
      PlantRec@11012001 : Record 11012551;
      PlantNoRec@11012002 : Record 11012552;
      PlantInspTypeRec@11012003 : Record 11012563;
      InspectionRec@11012004 : Record 11012564;
      Text001@1210190000 : TextConst 'ENU=Plant No. ''%1'' is Exploded. Do you want to post all exploded numbers ?;SVE=Maskinnr %1 har expanderats. Vill du bokfîra alla expanderade nummer??';
      Text002@1210190001 : TextConst 'ENU=Create Fixed Asset?;NOR=Opprette anleggsmiddel?;SVE=Skapa anlÑggningstillgÜng?';
      PlantProfileAnswerRec@1210190004 : Record 11012582;
      PlantProfileAnswerRec2@1210190005 : Record 11012582;
      Text003@1100485000 : TextConst 'ENU=Plant No. ''%1-%2'' is already posted. Delete, are you sure?;NOR=Maskinnr. ''%1-%2'' er allerede bokfõrt Fjerne, er du sikker?;SVE=Maskinnr ''%1-%2'' Ñr redan bokfîrd. Ta bort, Ñr du sÑker?';
      Text004@1100485010 : TextConst 'ENU=%1 %2 is already used for %3 %4.;NOR=%1 %2 er allerede i bruk  for %3 %4.;SVE=%1 %2 anvÑnds redan fîr %3 %4.';
      Text006@1100485003 : TextConst 'ENU=Plant No. is linked to Fixd Asset ''%1'' which Book Value is not zero.;NOR=Maskinnr. er linket til anleggsmiddel ''%1'' hvis bokfõringsverdi ikke er null.;SVE=Maskinnr Ñr lÑnkat till MaskintillgÜng ''%1'' vars bokfîringsvÑrde inte Ñr noll.';
      PlantInventRec@1100485005 : Record 11012555;
      Text007@1100485006 : TextConst 'ENU=Plant No. ''0'' is not allowed.;NOR=Maskinnr. ''0'' er ikke tillatt.;SVE=Maskinnr ''0'' Ñr inte tillÜtet.';
      Text008@1100485008 : TextConst 'ENU=Delete Plant No. ''%1-%2'' not allowed.;NOR=Fjerning av maskinnr. ''%1-%2'' er ikke tillatt.;SVE=Att ta bort Maskinnr ''%1-%2'' Ñr inte tillÜtet.';
      Text009@1100485009 : TextConst 'ENU=Plant is still present on location ''%1''.;NOR=Maskin finnes fortsatt pÜ plassering ''%1''.';
      Text010@1100485011 : TextConst 'ENU=Plant is not completely invoiced, %1 ''%2''.;NOR=Maskin er ike fullstendig fakturert, %1 ''%2''.';
      Text011@1100485014 : TextConst 'ENU=Invoice Proposal ''%1'' of the plant not posted yet.;NOR=Fakturaforslag ''%1'' for maskinen er ikke bokfõrt enda.';
      Text012@1100485012 : TextConst 'ENU=Sales Invoice ''%1'' of the plant not posted yet.;NOR=Salgsfaktura ''%1'' for maskinen er ikke bokfõrt enda.';
      Text013@1100485013 : TextConst 'ENU=Concerns the last Bulk Lot of the Plant Type, but is still there is plant present on a location and/or depot.;NOR=Gjelder det siste Bulkpartiet av maskintypen, men maskinen finnes fortsatt pÜ en lokasjon og/eller en Depot.;SVE=GÑller det senaste bulkpartiet av Maskintypen, men Maskinen finns fortfarande pÜ ett lagerstÑlle och/eller en depÜ.';
      Text014@1100485001 : TextConst 'ENU=Total quantity on location and/or depot is greater than the total quantity of de remaining bulk lots.;NOR=Totalt antall pÜ lokasjon og/eller depot er stõrre enn det totale antallet for de resterende bulkpartiene.;SVE=Totalt antal pÜ lagerstÑlle och/eller depÜ Ñr stîrre Ñn det totala antalet fîr de resterande bulkpartierna.';
      DimMgt@1100525001 : Codeunit 408;
      gNoConfirmOnDeleteAllowed@1100525000 : Boolean;
      DPA_Management@1100000999 : Codeunit 11012783;
      Text015@1100525002 : TextConst 'ENU=Note: Plant No. ''%1-%2'' is created by purchase orderr'' %3'' with vendor ''%4''.;NOR=Obs! maskinnr. ''%1-%2'' er opprettet gjennom kjõpsordre'' %3'' med leverandõr ''%4''.;SVE=Obs! Maskinnr ''%1-%2'' Ñr skapad genom inkîpsorder'' %3'' med leverantîr ''%4''.';
      Text016@1210190002 : TextConst 'ENU="Note: \%1 ''%2'' of %3 ''%4-%5'' is unequal to %1 ''%6'' of linked %7 ''%8''.\ \If desired, you should also adjust this on  the %7. ";NOR=Obs! \%1 ''%2'' av %3 ''%4-%5'' er ikke like med %1 ''%6'' av linket %7 ''%8''.\ \Om õnsket skal du ogsÜ justere dette pÜ %7.;SVE="Obs! \%1 ''%2'' av %3 ''%4-%5'' Ñr inte lika med %1 ''%6'' av lÑnkad %7 ''%8''.\ \Om înskvÑrt ska du Ñven justera detta pÜ %7. "';
      Text017@1210190006 : TextConst 'ENU=Not allowed, already %1 present.;NOR=Ikke tillatt, %1 finnes allerede.;SVE=ér inte tillÜtet, %1 finns redan.';
      Text018@1210190007 : TextConst 'ENU=Unlink %1 ''%2'' from Plant No. ''%3-%4''. Link to this %1 is not possible anymore, are you sure?;NOR=Fjern link %1 ''%2'' fra maskinnr. ''%3-%4''. Linke til denne %1 er ikke mulig lenger, er du sikker?;SVE=AvlÑnka %1 ''%2'' frÜn Maskinnr ''%3-%4''. Att lÑnka till detta %1 gÜr inte lÑngre, Ñr du sÑker att du vill fortsÑtta?';
      Text019@1100525003 : TextConst 'ENU="Note: %1 ''%2'' is already disposal, Are you sure? ";NOR=Obs: %1 ''%2'' er allerede salg, er du sikker?';
      Text020@1100529000 : TextConst 'ENU=Create %1 for %2 ''%3-%4''?';
      Text021@1100529001 : TextConst 'ENU="%1 ''%2'' is already present, %3 ''%4'' (%5 ''%6'') ";SVE="%1 ''%2'' finns redan, %3 ''%4'' (%5 ''%6'') "';
      Text022@1100525004 : TextConst 'ENU=%1 ''%2-%3'', %4 is zero. Continue?';
      Text023@1100525005 : TextConst 'ENU=Search purchase orders of plant no. in all purchaseorder lines. This may take some time.';
      FPSLicenseManagement@1100527400 : Codeunit 11229289;
      Text025@1100527352 : TextConst 'ENU=Do you want to create a Plant Location with %1 ''%2'' and %3 ''%4''?';
      Text026@1100527351 : TextConst 'ENU=Plant Location ''%1'' is created.';

    PROCEDURE FirstFreeNo@2() : Code[10];
    VAR
      lvPlantNoRec@1100485000 : Record 11012552;
      CompanyData@1210190001 : Record 11020674;
      CentralPlantNumberSeries@1100529000 : Record 11229922;
      lvFreeNo@1100485001 : Code[10];
      LastUsedNo@1210190002 : Code[10];
      CompanyNo@1210190000 : Code[5];
    BEGIN
      PlantSetup.GET;
      CASE PlantSetup."Numbering per Plant Type" OF
        PlantSetup."Numbering per Plant Type"::Yes:
          BEGIN
            PlantRec.GET("Plant Type");
            LastUsedNo := PlantRec."Last Used Plant Number";
            IF LastUsedNo = '' THEN
              lvFreeNo := PlantSetup."Start Number Plant Number"
            ELSE
              lvFreeNo := INCSTR(LastUsedNo);
            WHILE PlantNoAlreadyExist("Plant Type", lvFreeNo) DO
              lvFreeNo := INCSTR(lvFreeNo);
            WHILE PlantNoAlreadyUsedBefore("Plant Type", lvFreeNo) DO
              lvFreeNo := INCSTR(lvFreeNo);
          END;
        PlantSetup."Numbering per Plant Type"::"Yes including Company Number":
          BEGIN
            IF NOT CompanyData.GET(COMPANYNAME) THEN
              CompanyData."Company Name" := COMPANYNAME;
            CompanyData.TESTFIELD("Company No.");
            CompanyNo := CompanyData."Company No.";
            LastUsedNo := PlantRec."Last Used Plant Number";
            IF LastUsedNo = '' THEN
              lvFreeNo := STRSUBSTNO('%1%2',CompanyNo,PlantSetup."Start Number Plant Number")
            ELSE BEGIN
              lvFreeNo := LastUsedNo;
              DetermineFreeNoInCompanyRange(lvFreeNo,CompanyNo);
            END;
            WHILE PlantNoAlreadyExist("Plant Type", lvFreeNo) DO
              DetermineFreeNoInCompanyRange(lvFreeNo,CompanyNo);
            WHILE PlantNoAlreadyUsedBefore("Plant Type", lvFreeNo) DO
              DetermineFreeNoInCompanyRange(lvFreeNo,CompanyNo);
          END;
        PlantSetup."Numbering per Plant Type"::No:
          BEGIN
            lvPlantNoRec.SETRANGE("Plant Type","Plant Type");
            IF lvPlantNoRec.FINDLAST THEN
              lvFreeNo := INCSTR(lvPlantNoRec."No.")
            ELSE
              lvFreeNo := '1';
            WHILE PlantNoAlreadyUsedBefore("Plant Type", lvFreeNo) DO
              lvFreeNo := INCSTR(lvFreeNo);
          END;
        PlantSetup."Numbering per Plant Type"::"Central No. Series":
          BEGIN
            PlantRec.GET("Plant Type");
            PlantRec.TESTFIELD("Plant Number Series");
            lvFreeNo := CentralPlantNumberSeries.GetNextPlantNo(PlantRec."Plant Number Series");
            IF lvPlantNoRec.GET("Plant Type", lvFreeNo) THEN
              ERROR(Text021,
                lvPlantNoRec.TABLECAPTION, lvFreeNo, lvPlantNoRec.FIELDCAPTION("Plant Type"), lvPlantNoRec."Plant Type",
                PlantRec.FIELDCAPTION("Plant Number Series"), PlantRec."Plant Number Series");
          END;
        PlantSetup."Numbering per Plant Type"::"Central No. Series All Plant":
          BEGIN
            PlantSetup.TESTFIELD("Plant Number Series");
            lvFreeNo := CentralPlantNumberSeries.GetNextPlantNo(PlantSetup."Plant Number Series");
            IF lvPlantNoRec.GET("Plant Type", lvFreeNo) THEN
              ERROR(Text021,
                lvPlantNoRec.TABLECAPTION, lvFreeNo, lvPlantNoRec.FIELDCAPTION("Plant Type"), lvPlantNoRec."Plant Type",
                PlantSetup.FIELDCAPTION("Plant Number Series"), PlantSetup."Plant Number Series");
          END;
      END;

      EXIT(lvFreeNo);
    END;

    PROCEDURE PlantNoAlreadyExist@1210190002(IPlantType@1100485001 : Code[20];IPlantNo@1100485002 : Code[10]) : Boolean;
    VAR
      lvPlantNumberRec@1210190000 : Record 11012552;
    BEGIN
      lvPlantNumberRec.SETRANGE("Plant Type", IPlantType);
      lvPlantNumberRec.SETRANGE("No.", IPlantNo);
      IF lvPlantNumberRec.FINDFIRST THEN
        EXIT(TRUE);

      EXIT(FALSE);
    END;

    PROCEDURE PlantNoAlreadyUsedBefore@1100485001(IPlantType@1100485001 : Code[20];IPlantNo@1100485002 : Code[10]) : Boolean;
    VAR
      lvPlantLedgEntryRec@1100485000 : Record 11012572;
    BEGIN
      lvPlantLedgEntryRec.SETCURRENTKEY("Plant Type", "Plant No.");
      lvPlantLedgEntryRec.SETRANGE("Plant Type", IPlantType);
      lvPlantLedgEntryRec.SETRANGE("Plant No.", IPlantNo);
      IF lvPlantLedgEntryRec.FINDFIRST THEN
        EXIT(TRUE);

      EXIT(FALSE);
    END;

    PROCEDURE DetermineFreeNoInCompanyRange@1210190011(VAR FreeNo@1100485002 : Code[10];CompanyNo@1210190000 : Code[5]) : Boolean;
    VAR
      LenComp@1210190001 : Integer;
      ShortFreeNo@1210190002 : Code[10];
    BEGIN
      LenComp := STRLEN(CompanyNo);
      ShortFreeNo := COPYSTR(FreeNo,1+LenComp);
      ShortFreeNo := INCSTR(ShortFreeNo);
      FreeNo := STRSUBSTNO('%1%2',CompanyNo,ShortFreeNo);
    END;

    PROCEDURE FillInventoryNo@1();
    BEGIN
      CALCFIELDS(Bulk);
      IF Bulk THEN
        "Stock No." := '0'
      ELSE
        "Stock No." := "No.";

      IF Bulk AND External THEN BEGIN
        PlantSetup.GET;
        IF PlantSetup."Ext Bulk Plant with No. in Inv" THEN
          "Stock No." := "No."
      END;

      FillCPPlantNumber();
    END;

    PROCEDURE GetPlantDescription@4(PlntType@11012000 : Code[20];PlntNo@11012001 : Code[10];VAR PlantDescr@11012002 : Text[100]);
    BEGIN
      IF NOT PlantRec.GET(PlntType) THEN
        EXIT;

      IF (PlntNo > '0') THEN BEGIN
        IF PlantNoRec.GET(PlntType,PlntNo) THEN
          PlantDescr := PlantNoRec.Description;
      END ELSE
        IF (PlntType <> '') THEN
          PlantDescr := PlantRec.Description;
    END;

    PROCEDURE Explode@3();
    VAR
      lvPlantNoRec@1100485000 : Record 11012552;
    BEGIN
      //Checking
      TESTFIELD(Posted,FALSE);
      CALCFIELDS(Bulk);
      TESTFIELD(Bulk,FALSE);
      IF Quantity <= 1 THEN
        FIELDERROR(Quantity);
      IF Quantity <> ROUND(Quantity,1) THEN
        FIELDERROR(Quantity);

      IF NOT CONFIRM(Text000) THEN
        EXIT;

      REPEAT
        lvPlantNoRec."Plant Type" := "Plant Type";
        lvPlantNoRec."No." := lvPlantNoRec.FirstFreeNo;
        lvPlantNoRec.TRANSFERFIELDS(Rec,FALSE);
        lvPlantNoRec.Quantity := 1;
        lvPlantNoRec."Stock No." := lvPlantNoRec."No.";
        lvPlantNoRec."Exploded on No." := "No.";
        lvPlantNoRec."Fixed Asset" := '';
        lvPlantNoRec."Fixed Asset Created" := FALSE;
        lvPlantNoRec.FillInspectionNo();  //DP00241.n (16161)
        lvPlantNoRec.INSERT;
        PlantProfileAnswerRec.SETRANGE(Level, PlantProfileAnswerRec.Level::PlantNumber);
        PlantProfileAnswerRec.SETRANGE("Plant Type", "Plant Type");
        PlantProfileAnswerRec.SETRANGE("Plant No.", "No.");
        IF PlantProfileAnswerRec.FINDSET THEN
          REPEAT
            PlantProfileAnswerRec2 := PlantProfileAnswerRec;
            PlantProfileAnswerRec2."Plant No." := lvPlantNoRec."No.";
            PlantProfileAnswerRec2.UpdateModifiedBy; //C037249.n
            PlantProfileAnswerRec2.INSERT;
          UNTIL PlantProfileAnswerRec.NEXT = 0;
        DimMgt.CopyDefaultDim(
          DATABASE::"Plant Number", DimMgt.MakeCombinedPlantNo("Plant Type","No."),
          DATABASE::"Plant Number", DimMgt.MakeCombinedPlantNo(lvPlantNoRec."Plant Type",lvPlantNoRec."No."));

        CopyPlantNoRates("Plant Type", "No.", lvPlantNoRec."Plant Type", lvPlantNoRec."No.", 0);
        CopyPlantNoRates("Plant Type", "No.", lvPlantNoRec."Plant Type", lvPlantNoRec."No.", 1);
        CopyPlantNoRates("Plant Type", "No.", lvPlantNoRec."Plant Type", lvPlantNoRec."No.", 2);
        CopyPlantNoRentalDiscount("Plant Type", "No.", lvPlantNoRec."Plant Type", lvPlantNoRec."No.");
        CopyPlantParts("Plant Type", "No.", lvPlantNoRec."Plant Type", lvPlantNoRec."No.");
        CopyPlantMeterTypes("Plant Type", "No.", lvPlantNoRec."No.");
        CopyPlantMaintenanceScheme("Plant Type", "No.", lvPlantNoRec."Plant Type", lvPlantNoRec."No."); //DP00649.n
        Quantity := Quantity - 1
      UNTIL Quantity = 1;

      "Exploded on No." := "No.";
      MODIFY;

      PlantSetup.GET;
      IF PlantSetup.NumberingPerPlantTypeYesOrYesInclCompNo(PlantSetup."Numbering per Plant Type") THEN BEGIN
        PlantRec.GET("Plant Type");
        PlantRec."Last Used Plant Number" := lvPlantNoRec."No.";
        PlantRec.MODIFY;
      END;
    END;

    PROCEDURE CopyPlantNoRates@1100525002(FromPlantType@1100525000 : Code[20];FromPlantNo@1100525001 : Code[10];ToPlantType@1100525002 : Code[20];ToPlantNo@1100525003 : Code[10];RentalType@1100525006 : Integer);
    VAR
      RentalRateRec@1100525004 : Record 11012567;
      RentalRateRec2@1100525005 : Record 11012567;
    BEGIN
      //* RentalType: 0=Rental, 1=Sales/BayBack, 2=Hour
      CASE RentalType OF
        0: RentalRateRec.SETRANGE("Rental Type", RentalRateRec."Rental Type"::Rental);
        1: RentalRateRec.SETRANGE("Rental Type", RentalRateRec."Rental Type"::"Sales / Buy Back");
        2: RentalRateRec.SETRANGE("Rental Type", RentalRateRec."Rental Type"::"Plant Hours");
        ELSE
          EXIT;
      END;

      RentalRateRec.SETRANGE("Plant Type", FromPlantType);
      RentalRateRec.SETRANGE("Plant No.", FromPlantNo);
      IF RentalRateRec.FINDSET THEN BEGIN
        REPEAT
          RentalRateRec2 := RentalRateRec;
          RentalRateRec2."Plant Type" := ToPlantType;
          RentalRateRec2."Plant No." := ToPlantNo;
          RentalRateRec2.INSERT;
        UNTIL RentalRateRec.NEXT = 0;
      END;
    END;

    PROCEDURE CopyPlantNoRentalDiscount@1100525003(FromPlantType@1100525000 : Code[20];FromPlantNo@1100525001 : Code[10];ToPlantType@1100525002 : Code[20];ToPlantNo@1100525003 : Code[10]);
    VAR
      RentalDiscRec@1100525004 : Record 11012544;
      RentalDiscRec2@1100525005 : Record 11012544;
    BEGIN
      RentalDiscRec.SETRANGE("Customer Discount Group", '');
      RentalDiscRec.SETRANGE(Location, '');
      RentalDiscRec.SETRANGE("Group Level", '');
      RentalDiscRec.SETRANGE("Plant Type", FromPlantType);
      RentalDiscRec.SETRANGE("Plant No.", FromPlantNo);
      IF RentalDiscRec.FINDSET THEN BEGIN
        REPEAT
          RentalDiscRec2 := RentalDiscRec;
          RentalDiscRec2."Plant Type" := ToPlantType;
          RentalDiscRec2."Plant No." := ToPlantNo;
          RentalDiscRec2.INSERT;
        UNTIL RentalDiscRec.NEXT = 0;
      END;
    END;

    PROCEDURE CreateInspections@5();
    VAR
      InspecTypeRec@1100525000 : Record 11012562;
    BEGIN
      CALCFIELDS(Bulk);
      IF Bulk THEN
         EXIT;
      PlantSetup.GET;
      IF External AND (NOT PlantSetup."Create Inspec. External Plant") THEN
        EXIT;

      PlantInspTypeRec.RESET;
      PlantInspTypeRec.SETRANGE("Plant Type","Plant Type");
      PlantInspTypeRec.SETFILTER("Inspection Type", '<>%1', '');
      IF PlantInspTypeRec.FINDSET THEN
        REPEAT
          IF InspecTypeRec.GET(PlantInspTypeRec."Inspection Type") THEN BEGIN
            InspectionRec.RESET;
            InspectionRec.SETRANGE("Plant Type","Plant Type");
            InspectionRec.SETRANGE("Plant No.","No.");
            InspectionRec.SETRANGE("Inspection Type",PlantInspTypeRec."Inspection Type");
            IF NOT InspectionRec.FIND('-') THEN BEGIN
              InspectionRec.INIT;
              InspectionRec."Plant Type" := "Plant Type";
              InspectionRec."Plant No." := "No.";
              InspectionRec."Line No." := 0;
              InspectionRec."Inspection Type" := PlantInspTypeRec."Inspection Type";
              InspectionRec."Inspected on" := "Receipt Date";
              InspectionRec."Inspect before" := InspectionRec.NewCallDate;
              InspectionRec."Inspected on" := 0D;
              InspectionRec."Inspection Vendor" := InspecTypeRec."Inspection Vendor";
              IF InspecTypeRec."Meter Reading Type" <> '' THEN
                InspectionRec.FillMeterReadingFromInspectionType
              ELSE
                InspectionRec.FillMeterReading;
              InspectionRec.FillDefaultMaintenanceScheme;
              InspectionRec.INSERT(TRUE);
            END;
          END;
        UNTIL PlantInspTypeRec.NEXT = 0;
    END;

    PROCEDURE CreateFA@1210190000(IConfirmed@1100485000 : Boolean;ICostCentre@1100485001 : Code[20]);
    VAR
      lRecFixedAsset@1210190000 : Record 5600;
      lRecPlantNumber@1210190005 : Record 11012552;
      CarLeaseContractRec@1100525001 : Record 11012540;
      Vehicle@1100529001 : Record 11072667;
      lBlnConfirmed@1210190006 : Boolean;
      LeaseContractFound@1100529000 : Boolean;
    BEGIN
      PlantSetup.GET;
      PlantSetup.TESTFIELD("Integration Fixed Assets");
      FASetup.GET;
      //FASetup.TESTFIELD(FASetup."Default Depr. Book");
      //lRecDepreciationBook.GET(FASetup."Default Depr. Book");
      //lRecDepreciationBook.TESTFIELD("G/L Integration - Acq. Cost");
      //lRecDepreciationBook.TESTFIELD("G/L Integration - Disposal");

      TESTFIELD(External,FALSE);
      CALCFIELDS(Bulk);
      IF (NOT Bulk) THEN
        TESTFIELD(Quantity, 1.0)
      ELSE
        IF (Quantity = 0.0) THEN
          FIELDERROR(Quantity);

      lBlnConfirmed := IConfirmed;
      lRecPlantNumber := Rec;
      lRecPlantNumber.SETRANGE("Plant Type", "Plant Type");
      lRecPlantNumber.SETFILTER("Fixed Asset", '%1', '');
      IF ("Exploded on No." <> '') THEN BEGIN
        lRecPlantNumber.SETRANGE("Exploded on No.", "Exploded on No.");
        IF (lBlnConfirmed = FALSE) THEN BEGIN
          IF (lRecPlantNumber.COUNT > 0) THEN
            lBlnConfirmed := CONFIRM(Text001, FALSE, "Plant Type" + '-' + "No.");
        END;
      END ELSE BEGIN
        lRecPlantNumber.SETRANGE("No.",lRecPlantNumber."No.");
        TESTFIELD("Fixed Asset", '');
      END;

      IF (lBlnConfirmed = FALSE) THEN BEGIN
        lRecPlantNumber.SETRECFILTER;
        IF (NOT CONFIRM(Text002)) THEN
          EXIT;
      END;

      // Determine FA-posting group from plant posting group
      PlantRec.GET("Plant Type");
      PlantRec.TESTFIELD("Integration Fixed Assets");
      PlantRec.TESTFIELD("No. of Depreciation Years");
      PlantRec.TESTFIELD("Posting Group Internal");
      PlantRec.TESTFIELD("Rental Type", PlantRec."Rental Type"::Rental);
      //*FA.so
      //IF Vendor <> '' THEN BEGIN
      //  lvVendorRec.GET(Vendor);
      //  IF NOT lRecPlantPostingGroup.GET(PlantRec."Posting Group Internal", lvVendorRec."Vendor Posting Group") THEN
      //    lRecPlantPostingGroup.GET(PlantRec."Posting Group Internal", '');
      //END ELSE BEGIN
      //  lRecPlantPostingGroup.GET(PlantRec."Posting Group Internal", '');
      //END;
      //lRecPlantPostingGroup.TESTFIELD("FA Posting Group");  //*FA.o
      //*FA.eo

      IF NOT lRecPlantNumber.FIND('-') THEN
        EXIT;

      REPEAT
        TESTFIELD("Receipt Date");
        IF lRecPlantNumber."Values on Plant No. Level" THEN
          lRecPlantNumber.TESTFIELD("No. of Depreciation Years");

        //C002776.sn
        CarLeaseContractRec.RESET;
        CarLeaseContractRec.SETRANGE("Plant Type", lRecPlantNumber."Plant Type");
        CarLeaseContractRec.SETRANGE("Plant No.", lRecPlantNumber."No.");
        //DP00331.so
        //CarLeaseContractRec.SETRANGE("Lease Type", CarLeaseContractRec."Lease Type"::"Operational lease");
        //IF CarLeaseContractRec.FINDLAST THEN
        //  CarLeaseContractRec.FIELDERROR("Lease Type");
        //DP00331.eo
        //DP00331.sn
        LeaseContractFound := FALSE;
        IF CarLeaseContractRec.FINDLAST THEN BEGIN
          IF CarLeaseContractRec."Lease Type" = CarLeaseContractRec."Lease Type"::"Operational lease" THEN
            CarLeaseContractRec.FIELDERROR("Lease Type");
          LeaseContractFound := TRUE;
        END;
        //DP00331.en
        //C002776.en

        lRecFixedAsset.INIT;
        lRecFixedAsset."No." := '';
        IF (PlantSetup."Fixed FA Nos.") THEN
          lRecFixedAsset."No." := lRecFixedAsset.GetFA_Number(lRecPlantNumber."Plant Type", lRecPlantNumber."No.");

        IF PlantSetup."Connect Grouping to FA" THEN BEGIN
          lRecFixedAsset."FA Location Code" := PlantRec."Group Level 1";
          lRecFixedAsset."FA Class Code" := PlantRec."Group Level 2";
          lRecFixedAsset."FA Subclass Code" := PlantRec."Group Level 3";
        END ELSE BEGIN
          lRecFixedAsset."FA Location Code" := PlantRec."Default FA Location Code";
          lRecFixedAsset."FA Class Code" := PlantRec."Default FA Class Code";
          lRecFixedAsset."FA Subclass Code" := PlantRec."Default FA Subclass Code";
        END;

        lRecFixedAsset.SetSkipCreateDefFADeprBooks(TRUE);  //* Must not be done in OnInsert, not all FA-fields are filled yet
        lRecFixedAsset.INSERT(TRUE);
        lRecFixedAsset.VALIDATE(Description, COPYSTR(lRecPlantNumber.Description, 1, MAXSTRLEN(lRecFixedAsset.Description)));
        //lRecFixedAsset."FA Posting Group" := lRecPlantPostingGroup."FA Posting Group";  //*FA.o Not needed, field only for prints
        // (group level), but then is filled for FA's before printing depending on the selected Depr. Book (see R5600/05/0607/10).
        lRecFixedAsset."Vendor No." := Vendor;
        IF (ICostCentre <> '') THEN
          lRecFixedAsset.VALIDATE("Global Dimension 1 Code", ICostCentre)
        ELSE BEGIN
          IF lRecPlantNumber."Department Code" <> '' THEN
            lRecFixedAsset.VALIDATE("Global Dimension 1 Code", lRecPlantNumber."Department Code")
          ELSE
            lRecFixedAsset.VALIDATE("Global Dimension 1 Code", PlantRec."Department Code");
        END;
        lRecFixedAsset.VALIDATE("Global Dimension 2 Code", PlantRec."Cost Object");
        lRecFixedAsset.Inactive := lRecPlantNumber."Fixed Asset Inactivate";

        //*FA.so  Is done in function now
        //lRecFADepreciationBook."FA No." := lRecFixedAsset."No.";
        //lRecFADepreciationBook."Depreciation Book Code" := FASetup."Default Depr. Book";
        //lRecFADepreciationBook.INSERT(TRUE);
        //lRecFADepreciationBook."FA Posting Group" := lRecFixedAsset."FA Posting Group";
        //lRecFADepreciationBook."Depreciation Method" := lRecFADepreciationBook."Depreciation Method"::"Straight-Line";
        //lRecFADepreciationBook."Depreciation Starting Date" := "Receipt Date";
        //lRecFADepreciationBook.VALIDATE("No. of Depreciation Years", PlantRec."No. of Depreciation Years");
        //lRecFADepreciationBook.MODIFY;
        //*FA.eo

        lRecPlantNumber."Fixed Asset" := lRecFixedAsset."No.";
        lRecPlantNumber."Fixed Asset Created" := TRUE;
        lRecPlantNumber.MODIFY;
        //DP00331.sn  //* After field Fixed Asset is filled in Plant Number record
        IF Vehicle.GET(lRecPlantNumber."Plant Type", lRecPlantNumber."No.") THEN BEGIN
          Vehicle.UpdateRelatedFAFields();
          IF LeaseContractFound THEN
            CarLeaseContractRec.UpdateRelatedFAFields();
        END;
        //DP00331.en
        lRecFixedAsset.CreateDefaultFADeprBooks();  //* After field Fixed Asset is filled in Plant Number record
        CreateFAEntryOnlyQuantity(lRecPlantNumber);  //DP00485 (C010851)
      UNTIL (lRecPlantNumber.NEXT = 0);
    END;

    PROCEDURE CreateFAEntryOnlyQuantity@1100525005(PlantNumber@1100525000 : Record 11012552);
    VAR
      FA@1100525004 : Record 5600;
      FAEntry@1100525001 : Record 5601;
      FADeprBook@1100525003 : Record 5612;
      NextEntryNo@1100525002 : Integer;
    BEGIN
      //DP00485 (C010851)
      IF PlantNumber."Fixed Asset" = '' THEN
        EXIT;
      IF PlantNumber."Purchase Order No." <> '' THEN  // If via purch.order then quantities via purch. invoice
        EXIT;
      IF NOT FA.GET(PlantNumber."Fixed Asset") THEN
        EXIT;
      FAEntry.SETCURRENTKEY("FA No.");
      FAEntry.SETRANGE("FA No.", PlantNumber."Fixed Asset");
      IF NOT FAEntry.ISEMPTY THEN  // Only if no entries present yet
        EXIT;

      FADeprBook.SETRANGE("FA No.", PlantNumber."Fixed Asset");
      IF FADeprBook.FINDSET(TRUE,FALSE) THEN BEGIN
        REPEAT
          FAEntry.RESET;
          NextEntryNo := 1;
          IF FAEntry.FINDLAST THEN
            NextEntryNo := FAEntry."Entry No." + 1;
          FAEntry.INIT;
          FAEntry."Entry No." := NextEntryNo;
          FAEntry."FA No." := PlantNumber."Fixed Asset";
          FAEntry."FA Posting Date" := TODAY;
          FAEntry."Posting Date" := TODAY;
          FAEntry."Document Date" := TODAY;
          FAEntry.Description := FAEntry.FIELDCAPTION(Quantity);
          FAEntry."Depreciation Book Code" := FADeprBook."Depreciation Book Code";
          FAEntry.Quantity := PlantNumber.Quantity;  //Only Qutity posting, no amounts!
          FAEntry."Part of Book Value"  := TRUE;
          FAEntry."FA Class Code" := FA."FA Class Code";
          FAEntry."FA Subclass Code" := FA."FA Subclass Code";
          FAEntry."FA Location Code" := FA."FA Location Code";
          FAEntry."FA Posting Group" := FADeprBook."FA Posting Group";
          FAEntry."Global Dimension 1 Code" := FA."Global Dimension 1 Code";
          FAEntry."Global Dimension 2 Code" := FA."Global Dimension 2 Code";
          FAEntry."Location Code" := FA."Location Code";
          FAEntry."User ID" := USERID;
          FAEntry."Depreciation Method" := FADeprBook."Depreciation Method";
          FAEntry.INSERT;
          //
          FADeprBook."Acquisition Date" := FAEntry."FA Posting Date";
          FADeprBook.MODIFY;
        UNTIL FADeprBook.NEXT = 0;
      END;
    END;

    PROCEDURE UnlinkFA@1210190005();
    VAR
      FAEntry@1210190000 : Record 5601;
    BEGIN
      //*31411.n
      TESTFIELD("Fixed Asset");
      FAEntry.SETCURRENTKEY("FA No.");
      FAEntry.SETRANGE("FA No.", "Fixed Asset");
      //IF NOT FAEntry.ISEMPTY THEN              //C007980.o
      //  ERROR(Text017, FAEntry.TABLECAPTION);  //C007980.o
      //C007980.sn
      IF NOT FAEntry.ISEMPTY THEN BEGIN
        IF NOT DisposalFA() THEN
          ERROR(Text017, FAEntry.TABLECAPTION)
        ELSE BEGIN
          IF NOT CONFIRM(Text019, FALSE, FIELDCAPTION("Fixed Asset"), "Fixed Asset") THEN
            EXIT;
        END;
      END;
      //C007980.en

      IF NOT CONFIRM(Text018, FALSE, FIELDCAPTION("Fixed Asset"), "Fixed Asset", "Plant Type", "No.") THEN
        EXIT;
      "Fixed Asset" := '';
      "Fixed Asset Created" := FALSE;
      MODIFY;
    END;

    LOCAL PROCEDURE DisposalFA@1100525004() : Boolean;
    VAR
      FixedAsset@1100525000 : Record 5600;
      FA_DeprBook@1100525001 : Record 5612;
      DeprBookCode@1100525002 : Code[10];
    BEGIN
      //C007980.n
      IF "Fixed Asset" <> '' THEN BEGIN
        DeprBookCode := FixedAsset.GetFA_MainDeprBook("Fixed Asset", FALSE);
        IF DeprBookCode <> '' THEN BEGIN
          IF FA_DeprBook.GET("Fixed Asset", DeprBookCode) THEN
            EXIT(FA_DeprBook."Disposal Date" > 0D);
        END;
      END;
      EXIT(FALSE);
    END;

    PROCEDURE UpdateFADescription@1210190001();
    VAR
      FixedAsset@1210190000 : Record 5600;
    BEGIN
      PlantSetup.GET;
      IF (NOT PlantSetup."Integration Fixed Assets") OR ("Fixed Asset" = '') THEN
        EXIT;

      IF FixedAsset.GET("Fixed Asset") THEN BEGIN
        IF (FixedAsset.Description = '') OR (FixedAsset.Description = COPYSTR(xRec.Description, 1, MAXSTRLEN(FixedAsset.Description))) THEN BEGIN
          FixedAsset.VALIDATE(Description, COPYSTR(Description, 1, MAXSTRLEN(FixedAsset.Description)));
          FixedAsset.MODIFY(FALSE);
        END;
      END;
    END;

    PROCEDURE SetNoConfirmOnDeleteAllowed@1100525000(iNoConfirmOnDeleteAllowed@1100525000 : Boolean);
    BEGIN
      gNoConfirmOnDeleteAllowed := iNoConfirmOnDeleteAllowed;
    END;

    PROCEDURE CheckDeleteAllowed@1100485000();
    VAR
      lvPlantNoRec@1100485000 : Record 11012552;
      lvFixedAssetRec@1100485001 : Record 5600;
      lvFaDeprBookRec@1100485002 : Record 5612;
      lvDisposed@1100485004 : Boolean;
      lvQuan@1100485003 : Integer;
    BEGIN
      //* Uitgangspunt verwijderen:
      //* - Als men een verkeerd materieelnummer heeft toegevoegd (nog niet geboekt, of wel al wel geboekt maar nog niets mee gedaan)
      //* - Oude materieelnummers die men (al tijden) niet meer gebruikt (niet meer in bezit, exit of verkocht v/t materieel)

      IF (NOT Posted) THEN
        EXIT;

      IF NOT gNoConfirmOnDeleteAllowed THEN BEGIN
        IF NOT CONFIRM(Text003, FALSE, "Plant Type", "No.") THEN
          ERROR('');
      END;

      PlantSetup.GET;
      CALCFIELDS(Bulk);

      //* Check on Fixed Asset: If the plant no. is linked to a Fixed Asset the book value must be zero.
      lvDisposed := FALSE;
      IF PlantSetup."Integration Fixed Assets" AND ("Fixed Asset" <> '') THEN BEGIN
        IF lvFixedAssetRec.GET("Fixed Asset") THEN BEGIN
          lvFaDeprBookRec.SETRANGE("FA No.", "Fixed Asset");
          IF lvFaDeprBookRec.FIND('-') THEN BEGIN
            lvDisposed := (lvFaDeprBookRec."Disposal Date" <> 0D);
            lvFaDeprBookRec.CALCFIELDS("Book Value");
            IF (NOT lvDisposed) AND (lvFaDeprBookRec."Book Value" <> 0) THEN
              ERROR(STRSUBSTNO(Text008, "Plant Type", "No.") + ' ' + STRSUBSTNO(Text006,"Fixed Asset"));
          END;
        END;
      END;

      PlantInventRec.RESET;
      PlantInventRec.SETRANGE(Removed, FALSE);
      PlantInventRec.SETRANGE(Location, "Ship To Location");
      PlantInventRec.SETRANGE("Plant Type", "Plant Type");
      PlantInventRec.SETRANGE("Plant No.", "Stock No.");
      PlantInventRec.SETRANGE(Quantity, Quantity);
      PlantInventRec.SETRANGE("Present from", "Receipt Date");
      PlantInventRec.SETRANGE("Arrival Order Type", PlantInventRec."Arrival Order Type"::Receipt);
      IF NOT PlantInventRec.FINDFIRST THEN BEGIN
        //* If all of plant no. is still on the receipt location then no check on de plant inventory (present/invoiced) needed.
        //* Then plant inventory will be deleted in the 'OnDelete' trigger. So only check if the plant is already moved.
        IF (NOT Bulk) OR ("Stock No." <> '0') THEN BEGIN  //* Unique or bulk with No. in inventory
          PlantInventRec.RESET;
          PlantInventRec.SETCURRENTKEY(Removed, "Plant Type", "Plant No.");
          PlantInventRec.SETRANGE(Removed, FALSE);
          PlantInventRec.SETRANGE("Plant Type", "Plant Type");
          PlantInventRec.SETRANGE("Plant No.", "Stock No.");
          PlantInventRec.SETRANGE(Sold, FALSE);
          IF PlantInventRec.FINDFIRST THEN
            ERROR(STRSUBSTNO(Text008, "Plant Type", "No.") + ' ' + STRSUBSTNO(Text009, PlantInventRec.Location))
          ELSE BEGIN
            PlantInventRec.RESET;
            PlantInventRec.SETCURRENTKEY(Removed, "Plant Type", "Plant No.");
            PlantInventRec.SETRANGE(Removed, TRUE);
            PlantInventRec.SETRANGE("Plant Type", "Plant Type");
            PlantInventRec.SETRANGE("Plant No.", "Stock No.");
            PlantInventRec.SETRANGE(Sold, FALSE);
            PlantInventRec.SETRANGE(Depot, FALSE);
            PlantInventRec.SETRANGE("Employee Location", FALSE);
            //
            PlantInventRec.SETRANGE("Completely Invoiced",TRUE);
            PlantInventRec.SETRANGE("Invoice Status",PlantInventRec."Invoice Status"::Proposal);
            IF PlantInventRec.FINDFIRST THEN
              ERROR(STRSUBSTNO(Text008, "Plant Type", "No.") + ' ' + STRSUBSTNO(Text011, PlantInventRec."Last Invoice No."));
            PlantInventRec.SETRANGE("Invoice Status",PlantInventRec."Invoice Status"::"Temporary");
            IF PlantInventRec.FINDFIRST THEN
              ERROR(STRSUBSTNO(Text008, "Plant Type", "No.") + ' ' + STRSUBSTNO(Text012, PlantInventRec."Last Invoice No."));
            //
            PlantInventRec.SETRANGE("Invoice Status");  //Reset filter
            PlantInventRec.SETRANGE("Completely Invoiced", FALSE);
            IF PlantInventRec.FINDFIRST THEN
              ERROR(STRSUBSTNO(Text008, "Plant Type", "No.") + ' ' +
                STRSUBSTNO(Text010, PlantInventRec.FIELDCAPTION(Location), PlantInventRec.Location));
          END;
        END ELSE BEGIN  //* Bulk with Stock No. is '0' in inventory
          //* Check last one of Plant Type with "Stock No." is '0' in the inventory.
          PlantInventRec.RESET;
          PlantInventRec.SETCURRENTKEY(Removed, "Plant Type", "Plant No.");
          PlantInventRec.SETRANGE(Removed, FALSE);
          PlantInventRec.SETRANGE("Plant Type", "Plant Type");
          PlantInventRec.SETRANGE("Plant No.", '0');
          PlantInventRec.SETRANGE(Sold, FALSE);
          IF PlantInventRec.FINDFIRST THEN BEGIN
            lvPlantNoRec.RESET;
            lvPlantNoRec.SETRANGE("Plant Type", "Plant Type");
            lvPlantNoRec.SETFILTER("No.", '<>%1', "No.");
            lvPlantNoRec.SETRANGE("Stock No.", '0');
            lvPlantNoRec.SETRANGE(Posted, TRUE);
            IF NOT lvPlantNoRec.FINDSET(FALSE,FALSE) THEN
              ERROR(STRSUBSTNO(Text008, "Plant Type", "No.") + ' ' + Text013)
            ELSE BEGIN
              CALCFIELDS("Quantity in Stock");
              IF "Quantity in Stock" > 0 THEN BEGIN
                lvQuan := 0;
                REPEAT
                  lvQuan := lvQuan + lvPlantNoRec.Quantity;
                UNTIL lvPlantNoRec.NEXT = 0;
              END;
              IF "Quantity in Stock" > lvQuan THEN
                ERROR(STRSUBSTNO(Text008, "Plant Type", "No.") + ' ' + Text014);
            END;
          END;
        END;
      END;

      //* Verder maar niet testen of materieel nog op huurcorrectie, exitorder, etc. die nog niet (volledig) gefactureerd zijn. In
      //* principe zal dit toch niet het geval zijn (zie uitgangspunt in kop van deze functie).
      //* Men kan altijd materieelnummer nog even weer toevoegen (zonder te boeken!) en dan direct na het verwerken weer verwijderen.

      IF External THEN BEGIN
        CALCFIELDS("Quantity External Rented");
        TESTFIELD("Quantity External Rented", 0);
      END;
    END;

    PROCEDURE GetPlantWithInspectionNo@1100485002(InspectionNo@1100485000 : Code[30];VAR PlantType@1100485001 : Code[20];VAR PlantNo@1100485002 : Code[10]);
    BEGIN
      //* Input       : InspectionNo
      //* Input/Output: PlantType + PlantNo
      //*               Deze worden alleen aangepast als 'InspectionNo' gevonden, anders blijven ze ongewijzigd

      IF InspectionNo <> '' THEN BEGIN
        PlantNoRec.RESET;
        PlantNoRec.SETCURRENTKEY("Inspection No.");
        PlantNoRec.SETRANGE("Inspection No.", InspectionNo);
        IF PlantNoRec.FIND('-') THEN BEGIN
          PlantType := PlantNoRec."Plant Type";
          PlantNo := PlantNoRec."No.";
        END;
      END;
    END;

    PROCEDURE DoValidateExternal@1100485003(IBulk@1100485000 : Boolean;PlantType@1100485001 : Record 11012551);
    BEGIN
      TESTFIELD(Posted, FALSE);
      IF External AND ("Fixed Asset" <> '') THEN
        FIELDERROR(External);
      PlantSetup.GET;
      IF IBulk THEN BEGIN
        IF (NOT External) THEN BEGIN
          IF PlantType.External THEN
            FIELDERROR(External);
        END ELSE BEGIN
          IF (NOT PlantType.External) AND (NOT PlantSetup."Ext Bulk Plant with No. in Inv") THEN  //??
            FIELDERROR(External);
        END;
      END;
      IF (NOT External) AND PlantType."Ext. Plant Rental Invoicing" AND PlantSetup."Ext. Plant Rental Invoicing" THEN
        FIELDERROR(External);
      IF NOT External THEN
        "External Post no Surcharge" := FALSE;
    END;

    PROCEDURE ValidateShortcutDimCode@8(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateDimValueCode(FieldNumber,ShortcutDimCode);
      DimMgt.SaveDefaultDim(DATABASE::"Plant Number", DimMgt.MakeCombinedPlantNo("Plant Type","No."),FieldNumber,ShortcutDimCode);
    END;

    PROCEDURE FillCPPlantNumber@1100485004();
    BEGIN
      //**BNBM.n
      //* Vullen van dit veld is wel erg BNBM-specifiek, kan ook nergers op materieelkaart ingegeven worden.
      //* Maar zolang alleen BNBM dit veld gebruikt is dit geen probleem.
      //* Als ook andere klanten dit willen gaan gebruiken dan zou in de CP instellingen aangegeven moeten
      //* kunnen worden hoe het CP materieelnummer opgebouwd moet worden.

      { Merge MBS4.00->MBS2009
      IF NOT FPSLicenseManagement.LicenseAndReadPermissionForTable(CPSetupRec) THEN
        EXIT;

      IF NOT CPSetupRec.GET THEN
        EXIT;

      IF NOT CPSetupRec."Composed CP Plant Number" THEN
        EXIT;
      }

      CALCFIELDS(Bulk);
      IF Bulk AND (("Stock No." <> '') OR ("Stock No." <> '0')) THEN
        "CP Plant Number" := "Plant Type"
      ELSE
        "CP Plant Number" := "Plant Type" + "Stock No.";
    END;

    PROCEDURE DrillDownRevenuesDetails@1100525012();
    VAR
      PlantLedgerEntry@1210190000 : Record 11012572;
    BEGIN
      PlantLedgerEntry.SETCURRENTKEY("Plant Type", "Plant No.", "Relate to", "Gen. Bus. Posting Group", "Plant Posting Group", "Posting Date");
      PlantLedgerEntry.SETRANGE("Plant Type", "Plant Type");
      PlantLedgerEntry.SETRANGE("Plant No.", "No.");
      COPYFILTER("Date Filter", PlantLedgerEntry."Posting Date");
      COPYFILTER("Revenue Filter", PlantLedgerEntry."Relate to");
      COPYFILTER("Posting Group Filter", PlantLedgerEntry."Plant Posting Group");
      COPYFILTER("Gen. Bus. Posting Group Filter", PlantLedgerEntry."Gen. Bus. Posting Group");
      FILTERGROUP(0);
      PlantLedgerEntry.SETFILTER("Total Price", '<>%1', 0);  //* Only Revenues (15512)
      PlantLedgerEntry.FILTERGROUP(7);
      PlantLedgerEntry.SETFILTER("Relate to", '<>%1', 0);
      PlantLedgerEntry.FILTERGROUP(0);
      PAGE.RUNMODAL(0, PlantLedgerEntry);
    END;

    PROCEDURE DrillDownCostsDetails@1100525006();
    VAR
      PlantLedgerEntry@1210190000 : Record 11012572;
    BEGIN
      PlantLedgerEntry.SETCURRENTKEY("Plant Type", "Plant No.", "Plant Posting Group", "Cost Component", "Posting Date");
      PlantLedgerEntry.SETRANGE("Plant Type", "Plant Type");
      PlantLedgerEntry.SETRANGE("Plant No.", "No.");
      COPYFILTER("Date Filter", PlantLedgerEntry."Posting Date");
      COPYFILTER("Cost Component Filter", PlantLedgerEntry."Cost Component");
      COPYFILTER("Posting Group Filter", PlantLedgerEntry."Plant Posting Group");
      PlantLedgerEntry.SETFILTER("Total Cost", '<>%1', 0);  //* Only Revenues (15512)
      //PlantLedgerEntry.FILTERGROUP(7);
      //PlantLedgerEntry.SETRANGE("Relate to", 0);
      //PlantLedgerEntry.FILTERGROUP(0);
      PAGE.RUNMODAL(0, PlantLedgerEntry);
    END;

    PROCEDURE DPA_GetFieldNoDepartmentFilter@1100000993() : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      EXIT(0);
    END;

    PROCEDURE DPA_GetFiltersFltrGrp@1100000997(FilterGroupNo@1100525000 : Integer) FilterText : Text[250];
    BEGIN
      //**4PS Generated Code - Do Not Change
      FILTERGROUP(FilterGroupNo);
      FilterText := COPYSTR(GETFILTERS, 1, MAXSTRLEN(FilterText));
      FILTERGROUP(0);
    END;

    PROCEDURE DPA_SetFilters@1100000998(Context@1100525000 : Text[30]) : Boolean;
    VAR
      RecRef@1100525003 : RecordRef;
      i@1100525004 : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN
        EXIT;

      RecRef.GETTABLE(Rec);
      DPA_Management.SetDPAFilterFromContext(
        RecRef,
        Context,
        DPA_GetFieldNoDepartmentCode,
        DPA_GetFieldNoDepartmentFilter );

      FOR i := 10 DOWNTO 0 DO BEGIN
        RecRef.FILTERGROUP(i);
        FILTERGROUP(i);
        SETVIEW(RecRef.GETVIEW(FALSE));
      END;

      EXIT(DPA_Management.IsHardFilter);
    END;

    LOCAL PROCEDURE DPA_IsActive@1100000999() : Boolean;
    VAR
      DepartmentAuthorizObject@1100526000 : Record 11012466;
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT FPSLicenseManagement.LicenseAndReadPermissionForTable(DepartmentAuthorizObject) THEN
        EXIT(FALSE);

      IF NOT DPA_Management.AuthorizationActive THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    PROCEDURE RecFoundWithDPAFilterApplied@1100525001() : Boolean;
    BEGIN
      //**4PS Derived From Generated Code in DPA_FindRecWithDPAFilterAppld
      IF NOT DPA_IsActive THEN
        EXIT(FIND);

      IF NOT DPA_Management.HasTableHardFilter(11012552) THEN
        EXIT(FIND);

      DPA_SetFilters('Table 11012552');
        EXIT(FIND);
    END;

    PROCEDURE DPA_FindRecWithDPAFilterAppld@1100000989();
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN BEGIN
        FIND;
        EXIT;
      END;

      IF NOT DPA_Management.HasTableHardFilter(11012552) THEN BEGIN
        FIND;
        EXIT;
      END;

      DPA_SetFilters('Table 11012552');
      IF NOT FIND THEN
        DPA_Management.RaiseDPAErrorForTable(TABLECAPTION);
    END;

    PROCEDURE DPA_ChkDepartmentCodeAllowed@1100000990(IsHardFilter@1100526000 : Boolean);
    VAR
      DimensionManagement@1100526001 : Codeunit 408;
    BEGIN
      //**4PS Generated Code - Do Not Change
      DimensionManagement.CheckDepartmAuthWithFilterType(
        DATABASE::"Plant Number", "Department Code", IsHardFilter);
    END;

    PROCEDURE DPA_GetFieldNoDepartmentCode@1100000994() : Integer;
    BEGIN
      //**4PS Generated Code - Do Not Change
      EXIT(FIELDNO("Department Code"));
    END;

    PROCEDURE DPA_SetDefaultDepartment@1100000996();
    BEGIN
      //**4PS Generated Code - Do Not Change
      IF NOT DPA_IsActive THEN
        EXIT;

      DPA_Management.SetDepartmentCodeUser(TABLENAME, "Department Code");
    END;

    LOCAL PROCEDURE CopyTechSpecsFromPlantType@1210190003();
    VAR
      PlantProfileAnswer@1210190003 : Record 11012582;
      PlantProfileAnswer2@1210190002 : Record 11012582;
    BEGIN
      //mg, 12-04-11: M25005
      PlantProfileAnswer.SETRANGE(Level, PlantProfileAnswer.Level::PlantType);
      PlantProfileAnswer.SETRANGE("Plant Type", Rec."Plant Type");
      PlantProfileAnswer.SETRANGE("Copy To Plant No.", TRUE);
      IF PlantProfileAnswer.FINDSET THEN
        REPEAT
          PlantProfileAnswer2.TRANSFERFIELDS(PlantProfileAnswer);
          PlantProfileAnswer2.Level := PlantProfileAnswer.Level::PlantNumber;
          PlantProfileAnswer2."Plant No." := Rec."No.";
          PlantProfileAnswer2."Copy To Plant No." := FALSE;
          PlantProfileAnswer2.INSERT(TRUE);
        UNTIL PlantProfileAnswer.NEXT = 0;
    END;

    LOCAL PROCEDURE CopyPlantMaintenanceSchemeFromPlantType@1100528800();
    VAR
      PlantMaintenanceScheme@1100528800 : Record 11072677;
      PlantMaintenanceScheme2@1100528801 : Record 11072677;
    BEGIN
      PlantMaintenanceScheme.SETRANGE("Plant Type", "Plant Type");
      PlantMaintenanceScheme.SETRANGE("Plant No.", '');
      IF PlantMaintenanceScheme.FINDSET THEN
        REPEAT
          IF NOT PlantMaintenanceScheme2.GET("Plant Type","No.",PlantMaintenanceScheme."Maintenance Scheme") THEN
            PlantMaintenanceScheme2.INIT;
            PlantMaintenanceScheme2."Plant Type" := "Plant Type";
            PlantMaintenanceScheme2."Plant No." := "No.";
            PlantMaintenanceScheme2.VALIDATE("Maintenance Scheme", PlantMaintenanceScheme."Maintenance Scheme");
            PlantMaintenanceScheme2."Starting Date Maintenance" := "Receipt Date";
            PlantMaintenanceScheme2.INSERT;
            PlantMaintenanceScheme2.CopyMaintSchemePartsToPlantNumber("Plant Type", PlantMaintenanceScheme."Maintenance Scheme", "No.", 0, '');
        UNTIL PlantMaintenanceScheme.NEXT = 0;
    END;

    LOCAL PROCEDURE CopyPlantMaintenanceScheme@1100528801(FromPlantType@1100528803 : Code[20];FromPlantNo@1100528802 : Code[10];ToPlantType@1100528801 : Code[20];ToPlantNo@1100528800 : Code[10]);
    VAR
      PlantMaintenanceScheme@1100528804 : Record 11072677;
      PlantMaintenanceScheme2@1100528805 : Record 11072677;
    BEGIN
      PlantMaintenanceScheme.SETRANGE("Plant Type", FromPlantType);
      PlantMaintenanceScheme.SETRANGE("Plant No.", FromPlantNo);
      IF PlantMaintenanceScheme.FINDSET THEN
        REPEAT
          PlantMaintenanceScheme2 := PlantMaintenanceScheme;
          PlantMaintenanceScheme2."Plant Type" := ToPlantType;
          PlantMaintenanceScheme2."Plant No." := ToPlantNo;
          PlantMaintenanceScheme2.INSERT;
          PlantMaintenanceScheme2.CopyMaintSchemePartsToPlantNumber(FromPlantType,PlantMaintenanceScheme."Maintenance Scheme",ToPlantNo,1,FromPlantNo);
        UNTIL PlantMaintenanceScheme.NEXT = 0;
    END;

    PROCEDURE WarningDepartmentLinkedFA@1210190004();
    VAR
      FA@1210190000 : Record 5600;
    BEGIN
      //*19584
      IF "Fixed Asset" = '' THEN
        EXIT;
      IF NOT FA.GET("Fixed Asset") THEN
        EXIT;
      IF "Department Code" <> FA."Global Dimension 1 Code" THEN
        MESSAGE(Text016,
          FIELDCAPTION("Department Code"), "Department Code",
          FA.FIELDCAPTION("Plant No."), "Plant Type", "No.",
          FA."Global Dimension 1 Code",
          FIELDCAPTION("Fixed Asset"), "Fixed Asset");
    END;

    PROCEDURE ShowDocuments@1100529007();
    VAR
      RecRef@1100525000 : RecordRef;
      DocumentFilters@1100525001 : Codeunit 11012432;
    BEGIN
      //DP00183
      TESTFIELD("No.");
      RecRef.GETTABLE(Rec);
      IF Rec.CURRENTCOMPANY <> COMPANYNAME THEN
        DocumentFilters.SetDocumentCompany(Rec.CURRENTCOMPANY);
      DocumentFilters.CreateTempFilterAndExecute1(RecRef, "Department Code", FALSE, TRUE);
    END;

    PROCEDURE HasDocumentLinks@1100529015() : Boolean;
    VAR
      DocumentLinkMgt@1100525000 : Codeunit 11012401;
      RecRef@1100525001 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      IF Rec.CURRENTCOMPANY <> COMPANYNAME THEN
        DocumentLinkMgt.SetDocumentCompany(Rec.CURRENTCOMPANY);
      EXIT(DocumentLinkMgt.EntityHasDocumentLinks(RecRef));
    END;

    PROCEDURE DeleteDocumentLinks@1100529006();
    VAR
      DocumentLinkMgt@1100525000 : Codeunit 11012401;
      RecRef@1100525001 : RecordRef;
    BEGIN
      //DP00183
      RecRef.GETTABLE(Rec);
      DocumentLinkMgt.DeleteDocumentLinksFromObject(RecRef);
    END;

    PROCEDURE MoveDocumentLinks@1100529005(SourceRec@1100529001 : Record 11012552;TargetRec@1100529000 : Record 11012552;CopyMode@1100529003 : Boolean);
    VAR
      DocumentLinkMgt@1100525000 : Codeunit 11012401;
      SourceRecRef@1100525001 : RecordRef;
      TargetRecRef@1100529002 : RecordRef;
    BEGIN
      //DP00183
      SourceRecRef.GETTABLE(SourceRec);
      TargetRecRef.GETTABLE(TargetRec);
      IF NOT CopyMode THEN
        DocumentLinkMgt.ReplaceDocLink(SourceRecRef, TargetRecRef)
      ELSE
        DocumentLinkMgt.CopyDocLinks(SourceRecRef, TargetRecRef);
    END;

    PROCEDURE CommentSheet@1100529004(EditableMode@1100529002 : Boolean);
    VAR
      PlantCommentLine@1100529000 : Record 11072666;
    BEGIN
      //DP00183
      TESTFIELD("No.");
      PlantCommentLine.EditCommentLines(DATABASE::"Plant Number", "Plant Type", "No.", 0, EditableMode);  //C005461.c
    END;

    PROCEDURE HasCommentLines@1100529002() : Boolean;
    VAR
      PlantCommentLine@1100529000 : Record 11072666;
    BEGIN
      //DP00183
      EXIT(PlantCommentLine.HasCommentLines(DATABASE::"Plant Number", "Plant Type", "No.", 0));  //C005461.c
    END;

    PROCEDURE DeleteCommentLines@1100529003();
    VAR
      PlantCommentLine@1100529000 : Record 11072666;
    BEGIN
      //DP00183
      PlantCommentLine.DeleteComments(DATABASE::"Plant Number", "Plant Type", "No.", 0);  //C005461.c
    END;

    PROCEDURE FillInspectionNo@1100529000();
    VAR
      PlantNoWithPrefixZero@1100529000 : Code[30];
    BEGIN
      //DP00241 (16161)
      IF ("Plant Type" = '') OR ("No." = '') THEN
        EXIT;
      CALCFIELDS(Bulk);
      IF Bulk THEN
        EXIT;
      PlantSetup.GET;
      IF (PlantSetup."Length Plant No. in Inspec.No." = 0) OR
         (STRLEN("No.") > PlantSetup."Length Plant No. in Inspec.No.") OR
         ((STRLEN("Plant Type") + PlantSetup."Length Plant No. in Inspec.No.") > MAXSTRLEN("Inspection No."))
      THEN
        EXIT;

      PlantNoWithPrefixZero := "No.";
      WHILE STRLEN(PlantNoWithPrefixZero) < PlantSetup."Length Plant No. in Inspec.No." DO
        PlantNoWithPrefixZero := '0' + PlantNoWithPrefixZero;
      "Inspection No." := "Plant Type" + PlantNoWithPrefixZero;
      ValidateInspectionNo(FALSE);
    END;

    PROCEDURE ValidateInspectionNo@1100529011(ErrorMode@1100529002 : Boolean);
    VAR
      PlantNumber@1100529000 : Record 11012552;
      InspectionNoAlreadyUsed@1100529001 : Boolean;
    BEGIN
      //DP00241 (16161)
      IF "Inspection No." <> '' THEN BEGIN
        PlantNumber.RESET;
        PlantNumber.SETCURRENTKEY("Inspection No.");
        PlantNumber.SETRANGE("Inspection No.", "Inspection No.");
        IF PlantNumber.FINDFIRST THEN BEGIN
          IF (PlantNumber."Plant Type" = "Plant Type") AND (PlantNumber."No." = "No.") THEN
            InspectionNoAlreadyUsed := (PlantNumber.NEXT <> 0)
          ELSE
            InspectionNoAlreadyUsed := TRUE;
          IF NOT ErrorMode THEN
            "Inspection No." := ''
          ELSE BEGIN
            IF InspectionNoAlreadyUsed THEN
              ERROR(Text004,FIELDCAPTION("Inspection No."),"Inspection No.",PlantNumber."Plant Type", PlantNumber."No.");
          END;
        END;
      END;
    END;

    PROCEDURE UpdDepartmentCodeInspections@1100529008();
    VAR
      Inspection@1100529000 : Record 11012564;
      PlantType@1100529001 : Record 11012551;
      Department@1100529002 : Code[20];
    BEGIN
      //DP00241 (26914)
      IF ("Plant Type" = '') OR ("No." = '') THEN
        EXIT;
      Department := "Department Code";
      IF Department = '' THEN BEGIN
        IF PlantType.GET("Plant Type") THEN
          Department := PlantType."Department Code";
      END;
      IF Department = '' THEN
        EXIT;

      Inspection.SETRANGE("Plant Type", "Plant Type");
      Inspection.SETRANGE("Plant No.", "No.");
      Inspection.SETFILTER("Department Code", '<>%1', Department);
      IF NOT Inspection.ISEMPTY THEN
        Inspection.MODIFYALL("Department Code", Department, FALSE);
    END;

    PROCEDURE VehicleCard@1100529001();
    VAR
      Vehicle@1100529000 : Record 11072667;
      PlantType@1100529001 : Record 11012551;
    BEGIN
      //DP00331
      TESTFIELD("Plant Type");
      TESTFIELD("No.");
      PlantType.GET("Plant Type");
      PlantType.TESTFIELD(Bulk, FALSE);
      IF NOT Vehicle.GET("Plant Type", "No.") THEN BEGIN
        IF NOT CONFIRM(Text020, FALSE, Vehicle.TABLECAPTION, TABLECAPTION, "Plant Type", "No.") THEN
          EXIT;
        Vehicle.INIT;
        Vehicle.VALIDATE("Plant Type", "Plant Type");
        Vehicle.VALIDATE("Plant No.", "No.");
        Vehicle.INSERT;
        COMMIT;
      END;
      Vehicle.SETRANGE("Plant Type", "Plant Type");
      Vehicle.SETRANGE("Plant No.", "No.");
      PAGE.RUNMODAL(PAGE::Vehicle, Vehicle);
    END;

    PROCEDURE MeterReadings@1100529009();
    VAR
      PlantMeterType@1100529400 : Record 11229921;
    BEGIN
      //DP00614
      PlantMeterType.ShowMeterReadings_General("Plant Type", "No.", '', '', 0, '');
    END;

    PROCEDURE CreateServiceOrder@1100409000();
    VAR
      PlantType@1100525000 : Record 11012551;
      CreatePlantServiceOrder@1100529000 : Page 11229373;
    BEGIN
      TESTFIELD(Blocked, FALSE);
      PlantType.GET("Plant Type");
      PlantType.TESTFIELD(Blocked, FALSE);
      IF (NOT PlantType.Bulk) AND GUIALLOWED THEN BEGIN
        CALCFIELDS("Quantity in Stock");
        IF "Quantity in Stock" = 0 THEN BEGIN
          IF NOT CONFIRM(Text022, FALSE, TABLECAPTION, "Plant Type", "No.", FIELDCAPTION("Quantity in Stock")) THEN
            EXIT;
        END;
      END;
      CreatePlantServiceOrder.SetSelectionPlantNumber(Rec);
      CreatePlantServiceOrder.RUNMODAL;
    END;

    PROCEDURE CreateServiceCall@1100529600(VAR ServiceCallNo@1100409000 : Code[20]);
    VAR
      ServiceCall@1100528801 : Record 11012822;
      PlantType@1100525000 : Record 11012551;
    BEGIN
      TESTFIELD(Blocked, FALSE);
      PlantType.GET("Plant Type");
      PlantType.TESTFIELD(Blocked, FALSE);
      IF (NOT PlantType.Bulk) AND GUIALLOWED THEN BEGIN
        CALCFIELDS("Quantity in Stock");
        IF "Quantity in Stock" = 0 THEN BEGIN
          IF NOT CONFIRM(Text022, FALSE, TABLECAPTION, "Plant Type", "No.", FIELDCAPTION("Quantity in Stock")) THEN
            EXIT;
        END;
      END;

      ServiceCall.INIT;
      ServiceCall.INSERT(TRUE);

      ServiceCall."Plant Type" := "Plant Type";
      ServiceCall.VALIDATE("Plant Number", "No.");
      ServiceCall.MODIFY;

      ServiceCallNo := ServiceCall."No.";
    END;

    PROCEDURE CheckSeries@1100485007(lvSeries@1100485000 : Code[20]) : Boolean;
    VAR
      SerieRel@1210190000 : Record 310;
    BEGIN
      SerieRel.SETRANGE(Code, lvSeries);

      EXIT(SerieRel.FINDFIRST);
    END;

    PROCEDURE GetLastInspectionDate@1100525007() : Date;
    VAR
      Inspection@1100525000 : Record 11012564;
    BEGIN
      //C000333
      Inspection.SETCURRENTKEY("Plant Type","Plant No.","Inspected on");
      Inspection.SETRANGE("Plant Type", "Plant Type");
      Inspection.SETRANGE("Plant No.", "No.");
      Inspection.SETFILTER("Inspection Result", '>=%1', Inspection."Inspection Result"::Approved);
      IF Inspection.FINDLAST THEN
        EXIT(Inspection."Inspected on");
    END;

    PROCEDURE GetNextInspectionDate@1100525008() : Date;
    VAR
      Inspection@1100525000 : Record 11012564;
    BEGIN
      //C000333
      Inspection.SETCURRENTKEY("Plant Type","Plant No.","Inspect before");
      Inspection.SETRANGE("Plant Type", "Plant Type");
      Inspection.SETRANGE("Plant No.", "No.");
      Inspection.SETRANGE("Inspection Result", Inspection."Inspection Result"::" ");
      Inspection.SETFILTER("Inspect before", '<>%1', 0D);
      IF Inspection.FINDFIRST THEN
        EXIT(Inspection."Inspect before");
    END;

    PROCEDURE ShowInspectionList@1100525009(FieldNumber@1100525000 : Integer);
    VAR
      Inspection@1100525001 : Record 11012564;
    BEGIN
      //C000333
      Inspection.SETRANGE("Plant Type", "Plant Type");
      Inspection.SETRANGE("Plant No.", "No.");
      CASE FieldNumber OF
        Inspection.FIELDNO("Inspected on"):
          BEGIN
            Inspection.SETCURRENTKEY("Plant Type","Plant No.","Inspected on");
            Inspection.SETFILTER("Inspection Result", '>=%1', Inspection."Inspection Result"::Approved);
            IF Inspection.FINDLAST THEN;
          END;
        Inspection.FIELDNO("Inspect before"):
          BEGIN
            Inspection.SETCURRENTKEY("Plant Type","Plant No.","Inspect before");
            Inspection.SETRANGE("Inspection Result", Inspection."Inspection Result"::" ");
            Inspection.SETFILTER("Inspect before", '<>%1', 0D);
            IF Inspection.FINDFIRST THEN;
          END;
      END;
      PAGE.RUNMODAL(0, Inspection);
    END;

    [External]
    PROCEDURE GetResourceNo@1100529013() : Code[20];
    BEGIN
      EXIT(GetResourceNoWithCompany(COMPANYNAME));
    END;

    [External]
    PROCEDURE GetResourceNoWithCompany@1100528600(ICompanyName@1100528601 : Text[30]) : Code[20];
    VAR
      ResourceWOP@1100528600 : Record 11229278;
    BEGIN
      IF "No." <> '' THEN BEGIN
        ResourceWOP.SETCURRENTKEY(Type, Company, "Source No.", "Source No. 2");
        ResourceWOP.SETRANGE(Type, ResourceWOP.Type::Plant);
        ResourceWOP.SETRANGE(Company, ICompanyName);
        ResourceWOP.SETRANGE("Source No.", "Plant Type");
        ResourceWOP.SETRANGE("Source No. 2", "No.");
        IF ResourceWOP.FINDFIRST THEN
          EXIT(ResourceWOP."No.");
      END;
      EXIT('');
    END;

    PROCEDURE ShowResourcePlant@1100529012();
    VAR
      Resource@1100525001 : Record 11229278;
    BEGIN
      Resource."No." := GetResourceNo();
      IF Resource."No." <> '' THEN
        Resource.GET(Resource."No.");
      Resource.SETRANGE(Company, COMPANYNAME);
      Resource.SETRANGE(Type, Resource.Type::Plant);
      PAGE.RUNMODAL(PAGE::"Resource List (WOP)", Resource);
    END;

    PROCEDURE UpdateResource@1100529010(UpdateMode@1100525002 : ',Modify,Delete,Rename';xPlantNumber@1100525003 : Record 11012552;PlantNumber@1100525004 : Record 11012552);
    VAR
      WorkOrderPlanningSetup@1100525000 : Record 11229277;
      ResourceMgt@1100525001 : Codeunit 11229277;
    BEGIN
      IF NOT WorkOrderPlanningSetup.ISEMPTY THEN
        ResourceMgt.UpdateResourcePlant(UpdateMode, xPlantNumber, PlantNumber);
    END;

    PROCEDURE ShowPlantMeterReadingTypes@1100529401();
    VAR
      PlantType@1100529401 : Record 11012551;
      PlantMeterType@1100529400 : Record 11229921;
    BEGIN
      //**4PS.sn BI049 KD 10-08-16
      TESTFIELD("No.");
      PlantType.GET("Plant Type");
      PlantType.TESTFIELD("Meter Reading");
      PlantMeterType.FILTERGROUP(2);
      PlantMeterType.SETRANGE("Plant Type","Plant Type");
      PlantMeterType.SETRANGE("Plant No.","No.");
      PlantMeterType.FILTERGROUP(0);
      PAGE.RUNMODAL(0,PlantMeterType);
      //**4PS.en BI049 KD 10-08-16
    END;

    PROCEDURE ShowPlantNoMeterReadingPerMeterAndType@1100529026();
    VAR
      PlantType@1100529001 : Record 11012551;
      PlantMeterType@1100529000 : Record 11229921;
    BEGIN
      TESTFIELD("No.");
      PlantType.GET("Plant Type");
      PlantType.TESTFIELD("Meter Reading");
      PlantMeterType.ShowPlantNoMeterReadingsPerMeterAndType("Plant Type", "No.");
    END;

    PROCEDURE GetNumberOfActivePlantWarranties@1100529014() : Integer;
    VAR
      PlantWarranty@1100529000 : Record 11229924;
    BEGIN
      IF "No." <> '' THEN BEGIN
        IF MarkActivePlantWarranties(PlantWarranty) THEN BEGIN
          PlantWarranty.MARKEDONLY(TRUE);
          EXIT(PlantWarranty.COUNT);
        END;
      END;
      EXIT(0);
    END;

    PROCEDURE ShowActivePlantWarranties@1100529016();
    VAR
      PlantWarranty@1100529000 : Record 11229924;
      PlantWarrantyPage@1100529001 : Page 11229924;
    BEGIN
      IF "No." <> '' THEN BEGIN
        MarkActivePlantWarranties(PlantWarranty);
        PlantWarranty.MARKEDONLY(TRUE);
        PlantWarrantyPage.SETTABLEVIEW(PlantWarranty);
        PlantWarrantyPage.EDITABLE(FALSE);
        PlantWarrantyPage.RUNMODAL;
      END;
    END;

    LOCAL PROCEDURE MarkActivePlantWarranties@1100529017(VAR PlantWarranty@1100529000 : Record 11229924) ActiveFound : Boolean;
    BEGIN
      PlantWarranty.FILTERGROUP(2);
      PlantWarranty.SETRANGE("Plant Type", "Plant Type");
      PlantWarranty.SETRANGE("Plant No.", "No.");
      PlantWarranty.SETFILTER("Warranty Type", '<>%1', '');
      PlantWarranty.FILTERGROUP(0);
      IF PlantWarranty.FINDSET THEN BEGIN
        REPEAT
          IF PlantWarranty.IsActiveWarranty THEN BEGIN
            ActiveFound := TRUE;
            PlantWarranty.MARK(TRUE);
          END;
        UNTIL PlantWarranty.NEXT = 0;
      END;
      EXIT(ActiveFound);
    END;

    PROCEDURE GetNumberOfActiveMaterialWarranties@1100529019() : Integer;
    VAR
      PlantMaterialInventory@1100529000 : Record 11229926;
    BEGIN
      IF "No." <> '' THEN BEGIN
        IF MarkActiveMaterialWarranties(PlantMaterialInventory) THEN BEGIN
          PlantMaterialInventory.MARKEDONLY(TRUE);
          EXIT(PlantMaterialInventory.COUNT);
        END;
      END;
      EXIT(0);
    END;

    PROCEDURE ShowActiveMaterialWarranties@1100529020();
    VAR
      PlantMaterialInventory@1100529000 : Record 11229926;
      PlantMaterialInventoryPage@1100529001 : Page 11229926;
    BEGIN
      IF "No." <> '' THEN BEGIN
        MarkActiveMaterialWarranties(PlantMaterialInventory);
        PlantMaterialInventory.MARKEDONLY(TRUE);
        PlantMaterialInventoryPage.SETTABLEVIEW(PlantMaterialInventory);
        PlantMaterialInventoryPage.EDITABLE(FALSE);
        PlantMaterialInventoryPage.RUNMODAL;
      END;
    END;

    LOCAL PROCEDURE MarkActiveMaterialWarranties@1100529021(VAR PlantMaterialInventory@1100529000 : Record 11229926) ActiveFound : Boolean;
    BEGIN
      PlantMaterialInventory.FILTERGROUP(2);
      PlantMaterialInventory.SETRANGE("Plant Type", "Plant Type");
      PlantMaterialInventory.SETRANGE("Plant No.", "No.");
      PlantMaterialInventory.SETFILTER("Warranty Type", '<>%1', '');
      PlantMaterialInventory.FILTERGROUP(0);
      IF PlantMaterialInventory.FINDSET THEN BEGIN
        REPEAT
          IF PlantMaterialInventory.IsActiveWarranty THEN BEGIN
            ActiveFound := TRUE;
            PlantMaterialInventory.MARK(TRUE);
          END;
        UNTIL PlantMaterialInventory.NEXT = 0;
      END;
      EXIT(ActiveFound);
    END;

    PROCEDURE ShowPlantNumberParts@1100529018();
    VAR
      PlantPartPlantLevel@1100529000 : Record 11229932;
    BEGIN
      TESTFIELD("Plant Type");
      TESTFIELD("No.");
      PlantPartPlantLevel.FILTERGROUP(2);
      PlantPartPlantLevel.SETRANGE("Plant Type", "Plant Type");
      PlantPartPlantLevel.SETRANGE("Plant No.", "No.");
      PlantPartPlantLevel.FILTERGROUP(0);
      PAGE.RUNMODAL(PAGE::"Plant Number Parts", PlantPartPlantLevel);
    END;

    LOCAL PROCEDURE CopyPlantParts@1100529022(PlantTypeFrom@1100529002 : Code[20];PlantNoFrom@1100529003 : Code[10];PlantTypeTo@1100529004 : Code[20];PlantNoTo@1100529005 : Code[10]);
    VAR
      PlantPartPlantLevel1@1100529001 : Record 11229932;
      PlantPartPlantLevel2@1100529000 : Record 11229932;
      PlantPartProblem1@1100529006 : Record 11229931;
      PlantPartProblem2@1100529007 : Record 11229931;
    BEGIN
      IF (PlantTypeFrom = '') OR (PlantTypeTo = '') OR (PlantNoTo = '') THEN
        EXIT;
      IF PlantNoFrom = '' THEN
        PlantPartPlantLevel1.SETRANGE(Level, PlantPartPlantLevel1.Level::PlantType)
      ELSE BEGIN
        PlantPartPlantLevel1.SETRANGE(Level, PlantPartPlantLevel1.Level::PlantNumber);
        PlantPartPlantLevel1.SETRANGE("Plant No.", PlantNoFrom);
      END;
      PlantPartPlantLevel1.SETRANGE("Plant Type", PlantTypeFrom);
      IF PlantPartPlantLevel1.FINDSET THEN BEGIN
        REPEAT
          PlantPartPlantLevel2.INIT;
          PlantPartPlantLevel2.Level := PlantPartPlantLevel1.Level::PlantNumber;
          PlantPartPlantLevel2."Plant Type" := PlantTypeTo;
          PlantPartPlantLevel2."Plant No." := PlantNoTo;
          PlantPartPlantLevel2."Plant Part" := PlantPartPlantLevel1."Plant Part";
          IF PlantPartPlantLevel1.Level = PlantPartPlantLevel1.Level::PlantType THEN
            PlantPartPlantLevel2.INSERT(TRUE)
          ELSE BEGIN
            PlantPartPlantLevel2.INSERT(FALSE);
            PlantPartProblem1.SETRANGE(Level, PlantPartProblem1.Level::PlantNumber);
            PlantPartProblem1.SETRANGE("Plant Type", PlantPartPlantLevel1."Plant Type");
            PlantPartProblem1.SETRANGE("Plant No.", PlantPartPlantLevel1."Plant No.");
            PlantPartProblem1.SETRANGE("Plant Part", PlantPartPlantLevel1."Plant Part");
            IF PlantPartProblem1.FINDSET THEN BEGIN
              REPEAT
                PlantPartProblem2.INIT;
                PlantPartProblem2.Level := PlantPartProblem1.Level::PlantNumber;
                PlantPartProblem2."Plant Type" := PlantPartPlantLevel2."Plant Type";
                PlantPartProblem2."Plant No." := PlantPartPlantLevel2."Plant No.";
                PlantPartProblem2."Plant Part" := PlantPartPlantLevel2."Plant Part";
                PlantPartProblem2.Problem := PlantPartProblem1.Problem;
                PlantPartProblem2.INSERT;
              UNTIL PlantPartProblem1.NEXT = 0;
            END;
          END;
        UNTIL PlantPartPlantLevel1.NEXT = 0;
      END;
    END;

    PROCEDURE ShowPurchaseOrderCard@1100525011();
    VAR
      PurchaseHeader@1100525000 : Record 38;
    BEGIN
      IF "Purchase Order No." <> '' THEN BEGIN
        PurchaseHeader."Document Type" := PurchaseHeader."Document Type"::Order;
        PurchaseHeader."No." := "Purchase Order No.";
        PAGE.RUNMODAL(PAGE::"Purchase Order", PurchaseHeader);
      END;
    END;

    PROCEDURE ShowPlantNoPurchaseOrders@1100525010();
    VAR
      PurchaseHeader@1100525000 : Record 38;
      PurchaseLine@1100525001 : Record 39;
      TmpPurchaseHeader@1100525002 : TEMPORARY Record 38;
      Window@1100525003 : Dialog;
    BEGIN
      //* All purchase orders of/on plant number, so not only acquisition
      TESTFIELD("Plant Type");
      TESTFIELD("No.");
      PurchaseLine.SETRANGE("Document Type", PurchaseLine."Document Type"::Order);
      PurchaseLine.SETRANGE("Plant Type", "Plant Type");
      PurchaseLine.SETRANGE("Plant No.", "No.");
      IF PurchaseLine.FINDSET THEN BEGIN
        Window.OPEN(Text023);
        REPEAT
          IF PurchaseHeader.GET(PurchaseHeader."Document Type"::Order, PurchaseLine."Document No.") THEN BEGIN
            IF NOT TmpPurchaseHeader.GET(PurchaseHeader."Document Type", PurchaseHeader."No.") THEN BEGIN
              TmpPurchaseHeader := PurchaseHeader;
              TmpPurchaseHeader.INSERT(FALSE);
            END;
          END;
        UNTIL PurchaseLine.NEXT = 0;
        Window.CLOSE;
      END;
      IF "Purchase Order No." <> '' THEN BEGIN  //* Acquisition can be on plant type level, so order not found yet.
        IF PurchaseHeader.GET(PurchaseHeader."Document Type"::Order, "Purchase Order No.") THEN BEGIN
          IF NOT TmpPurchaseHeader.GET(PurchaseHeader."Document Type", PurchaseHeader."No.") THEN BEGIN
            TmpPurchaseHeader := PurchaseHeader;
            TmpPurchaseHeader.INSERT(FALSE);
          END;
        END;
      END;
      PAGE.RUNMODAL(PAGE::"Purchase Order List", TmpPurchaseHeader);
    END;

    PROCEDURE GetNoOfRentalAgreements@1100529023() : Integer;
    VAR
      PlantRentalAgreement@1100525000 : Record 11229935;
    BEGIN
      IF "No." = '' THEN
        EXIT(0);
      PlantRentalAgreement.SETRANGE("Plant Type", "Plant Type");
      PlantRentalAgreement.SETRANGE("Plant No.", "No.");
      EXIT(PlantRentalAgreement.COUNT);
    END;

    PROCEDURE HasCurrentRentalAgreement@1100529024() : Boolean;
    VAR
      PlantRentalAgreement@1100525000 : Record 11229935;
    BEGIN
      IF "No." = '' THEN
        EXIT(FALSE);
      PlantRentalAgreement.SETRANGE("Plant Type", "Plant Type");
      PlantRentalAgreement.SETRANGE("Plant No.", "No.");
      PlantRentalAgreement.SETFILTER("Starting Date", '<=%1', WORKDATE);
      PlantRentalAgreement.SETFILTER("Ending Date", '>=%1', WORKDATE);
      EXIT(NOT PlantRentalAgreement.ISEMPTY);
    END;

    PROCEDURE ShowRentalAgreements@1100529025();
    VAR
      PlantRentalAgreement@1100525000 : Record 11229935;
      PlantRentalAgreements@1100525001 : Page 11229941;
    BEGIN
      TESTFIELD("No.");
      CALCFIELDS(Bulk);
      TESTFIELD(Bulk, FALSE);
      PlantRentalAgreement.FILTERGROUP(2);
      PlantRentalAgreement.SETRANGE("Plant Type", "Plant Type");
      PlantRentalAgreement.SETRANGE("Plant No.", "No.");
      PlantRentalAgreement.FILTERGROUP(0);
      PlantRentalAgreements.SETTABLEVIEW(PlantRentalAgreement);
      IF PlantRentalAgreement.FINDLAST THEN BEGIN
        PlantRentalAgreement.SETFILTER("Starting Date", '<=%1', WORKDATE);
        PlantRentalAgreement.SETFILTER("Ending Date", '>=%1', WORKDATE);
        IF PlantRentalAgreement.FINDFIRST THEN;
        PlantRentalAgreements.SETRECORD(PlantRentalAgreement);
      END;
      PlantRentalAgreements.SetCalledFromPlantNumber;
      PlantRentalAgreements.RUNMODAL;
    END;

    PROCEDURE GetDefaultExternalRateComponent@1100529601();
    VAR
      RentalRate@1100529600 : Record 11012567;
      RateComponent@1100529601 : Record 11012585;
    BEGIN
      IF "No Rate Component Posting" OR (NOT External) THEN
        EXIT;

      PlantSetup.GET;
      IF (NOT PlantSetup."Post Rent by Rate Component") OR (PlantSetup."Rate Comp. Extern. Plant No." = '') THEN
        EXIT;

      RentalRate.SETRANGE("Rental Type", RentalRate."Rental Type"::Rental);
      RentalRate.SETRANGE("Plant Type", "Plant Type");
      RentalRate.SETRANGE("Plant No.", "No.");
      IF NOT RentalRate.FINDLAST THEN
        EXIT;

      RateComponent.SETRANGE("Rental Type", RateComponent."Rental Type"::Rental);
      RateComponent.SETRANGE("Plant Type", "Plant Type");
      RateComponent.SETRANGE("Plant No.", "No.");
      RateComponent.SETRANGE("Starting Date", RentalRate."Starting Date");
      IF RateComponent.ISEMPTY THEN BEGIN
        RateComponent.INIT;
        RateComponent."Rental Type" := RateComponent."Rental Type"::Rental;
        RateComponent.VALIDATE("Plant Type", "Plant Type");
        RateComponent."Plant No." := "No.";
        RateComponent."Starting Date" := RentalRate."Starting Date";
        RateComponent.VALIDATE("Rental Rate Code", PlantSetup."Rate Comp. Extern. Plant No.");
        RateComponent.INSERT;
      END;
    END;

    LOCAL PROCEDURE CopyPlantMeterTypes@1100528500(PlantTypeCode@1100528503 : Code[20];FromPlantNo@1100528504 : Code[10];ToPlantNo@1100528506 : Code[10]);
    VAR
      PlantType@1100528502 : Record 11012551;
      PlantMeterType@1100528500 : Record 11229921;
      PlantMeterType2@1100528501 : Record 11229921;
    BEGIN
      IF (PlantTypeCode = '') OR (ToPlantNo = '') THEN
        EXIT;
      IF NOT PlantType.GET(PlantTypeCode) THEN
        EXIT;
      IF PlantType.Bulk OR (NOT PlantType."Meter Reading") THEN
        EXIT;
      PlantMeterType.SETRANGE("Plant Type", PlantTypeCode);
      PlantMeterType.SETRANGE("Plant No.", FromPlantNo);
      IF PlantMeterType.FINDSET THEN BEGIN
        REPEAT
          PlantMeterType2 := PlantMeterType;
          PlantMeterType2."Plant Type" := PlantTypeCode;
          PlantMeterType2."Plant No." := ToPlantNo;
          PlantMeterType2.INSERT(TRUE);
        UNTIL PlantMeterType.NEXT = 0;
      END;
    END;

    PROCEDURE CheckExternalPostNoSurcharge@1100529602(PlantTypeCode@1100529600 : Code[20];PlantNo@1100529601 : Code[10];ShowError@1100529602 : Boolean) : Boolean;
    VAR
      LocPlantType@1100529603 : Record 11012551;
      LocPlantNumber@1100529604 : Record 11012552;
    BEGIN
      IF (PlantTypeCode = '') AND (PlantNo = '') THEN
        EXIT(TRUE);

      IF PlantNo <> '' THEN
        IF LocPlantNumber.GET(PlantTypeCode, PlantNo) THEN BEGIN
          IF ShowError THEN
            LocPlantNumber.TESTFIELD("External Post no Surcharge", FALSE);
          EXIT(NOT LocPlantNumber."External Post no Surcharge");
        END;

      IF NOT LocPlantType.GET(PlantTypeCode) THEN
        EXIT(TRUE);
      IF ShowError THEN
        LocPlantType.TESTFIELD("External Post no Surcharge", FALSE);
      EXIT(NOT LocPlantType."External Post no Surcharge");
    END;

    PROCEDURE CreatePlantLocation@1100527350(IPlantNumber@1100528500 : Record 11012552);
    VAR
      PlantLocation@1100528501 : Record 11012554;
      PlantSetup@1100527350 : Record 11012550;
    BEGIN
      WITH IPlantNumber DO BEGIN
        TESTFIELD("No.");
        TESTFIELD("Plant Type");
        IF NOT CONFIRM(Text025, TRUE, IPlantNumber.FIELDCAPTION("Plant Type"), IPlantNumber."Plant Type", IPlantNumber.FIELDCAPTION("No."),IPlantNumber."No.") THEN
          EXIT;
        PlantSetup.GET;

        PlantLocation.INIT;
        CASE PlantSetup."Build Pl. Loc. Pl. Num. from" OF
          PlantSetup."Build Pl. Loc. Pl. Num. from"::PlantTypePlantNo:
            PlantLocation.Code := "Plant Type" + "No.";
          PlantSetup."Build Pl. Loc. Pl. Num. from"::InspectionNo:
            BEGIN
              TESTFIELD("Inspection No.");
              PlantLocation.Code := "Inspection No.";
            END;
          PlantSetup."Build Pl. Loc. Pl. Num. from"::NoSeries:
            BEGIN
              PlantSetup.TESTFIELD("Plant Location Nos.");
              PlantLocation.Code := '';
            END;
        ELSE
          PlantSetup.FIELDERROR("Build Pl. Loc. Pl. Num. from");
        END;
        PlantLocation.InitRecord;
        PlantLocation."Plant Type" := "Plant Type";
        PlantLocation."Plant No." := "No.";
        PlantLocation.Description := Description;
        PlantLocation."Search Name" := "Search Name";
        PlantLocation.INSERT(TRUE);
        PlantLocation.TESTFIELD(Code);
        MESSAGE(Text026, PlantLocation.Code);
      END;
    END;

    PROCEDURE ShowPlantMaintenanceScheme@1100529027();
    VAR
      PlantMaintenanceScheme@1100529000 : Record 11072677;
      PlantMaintenanceSchemes@1100529001 : Page 11125954;
    BEGIN
      TESTFIELD("No.");
      PlantMaintenanceScheme.FILTERGROUP(2);
      PlantMaintenanceScheme.SETRANGE("Plant Type", "Plant Type");
      PlantMaintenanceScheme.FILTERGROUP(0);
      PlantMaintenanceScheme.SETRANGE("Plant No.", "No.");
      PlantMaintenanceSchemes.SetSelectionLevel(TRUE);
      PlantMaintenanceSchemes.SETTABLEVIEW(PlantMaintenanceScheme);
      PlantMaintenanceSchemes.RUNMODAL;
    END;

    PROCEDURE ExpectedOccupationPercentage@1100529028(PlantType@1100529002 : Code[20];PlantNo@1100529003 : Code[10]) : Decimal;
    VAR
      PlantTypes@1100529000 : Record 11012551;
      PlantNumber@1100529001 : Record 11012552;
    BEGIN
      IF PlantType <> '' THEN BEGIN
        IF PlantNo <> '' THEN BEGIN
          IF PlantNumber.GET(PlantType, PlantNo) THEN BEGIN
            IF PlantNumber."Values on Plant No. Level" THEN
              EXIT(PlantNumber."Expected Occupation Perc.");
          END;
        END;
        IF PlantTypes.GET(PlantType) THEN
          EXIT(PlantTypes."Expected Occupation Perc.");
      END;
      EXIT(0);
    END;

    LOCAL PROCEDURE UpdatePlantMaintSchemeStartingDate@1100529029();
    VAR
      PlantMaintenanceScheme@1100529000 : Record 11072677;
    BEGIN
      IF ("No." = '') OR ("Receipt Date" = xRec."Receipt Date") OR Posted THEN
        EXIT;
      PlantMaintenanceScheme.SETRANGE("Plant Type", "Plant Type");
      PlantMaintenanceScheme.SETRANGE("Plant No.", "No.");
      PlantMaintenanceScheme.SETFILTER("Starting Date Maintenance", '%1|%2', xRec."Receipt Date", 0D);
      PlantMaintenanceScheme.MODIFYALL("Starting Date Maintenance", "Receipt Date");
    END;

    BEGIN
    {
      4PS 10-08-16 UKR-C25593 KD: Multiple meter readings (BI049) - Trigger ShowPlantMeterReadingTypes added
                                                                    Triggers OnDelete, MeterReadings changed
    }
    END.
  }
}

