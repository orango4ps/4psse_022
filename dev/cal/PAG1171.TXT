OBJECT Page 1171 User Task Card
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=User Task;
               NOR=Brukeroppgave;
               SVE=Uppgift fîr anvÑndaren];
    SourceTable=Table1170;
    PageType=Card;
    OnOpenPage=BEGIN
                 RESET;
               END;

    OnAfterGetRecord=BEGIN
                       MultiLineTextControl := GetDescription;
                       IsMarkCompleteEnabled := NOT IsCompleted;
                     END;

    OnNewRecord=BEGIN
                  "Created By" := USERSECURITYID;
                  VALIDATE("Created DateTime",CURRENTDATETIME);
                  CALCFIELDS("Created By User Name");

                  CLEAR(MultiLineTextControl);
                END;

    ActionList=ACTIONS
    {
      { 18      ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 19      ;1   ;Action    ;
                      Name=Go To Task Item;
                      CaptionML=[ENU=Go To Task Item;
                                 NOR=GÜ til oppgaveelement;
                                 SVE=GÜ till uppgiftsobjekt];
                      ToolTipML=[ENU=Open the page or report that is associated with this task.;
                                 NOR=èpne siden eller rapporten som er knyttet til denne oppgaven.;
                                 SVE=ôppna sidan eller rapporten som Ñr associerad med den hÑr uppgiften.];
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      Image=Navigate;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 RunReportOrPageLink;
                               END;
                                }
      { 6       ;1   ;Action    ;
                      Name=Mark Completed;
                      CaptionML=[ENU=Mark Completed;
                                 NOR=Merk som fullfõrt;
                                 SVE=Markera som slutfîrd];
                      ToolTipML=[ENU=Mark the task as completed.;
                                 NOR=Merk oppgaven som fullfõrt.;
                                 SVE=Markera uppgiften som slutfîrd.];
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      Enabled=IsMarkCompleteEnabled;
                      Image=Completed;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 // Marks the current task as completed.
                                 SetCompleted;
                                 CurrPage.UPDATE(TRUE);
                               END;
                                }
      { 14      ;1   ;Action    ;
                      Name=Recurrence;
                      CaptionML=[ENU=Recurrence;
                                 NOR=Gjentakelse;
                                 SVE=èterkommande];
                      ToolTipML=[ENU=Make this a recurring task.;
                                 NOR=Gjõr dette til en gjentakende oppgave.;
                                 SVE=Gîr detta till en Üterkommande uppgift.];
                      ApplicationArea=#Basic,#Suite;
                      Promoted=Yes;
                      Image=Refresh;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 UserTaskRecurrence@1000 : Page 1172;
                               BEGIN
                                 UserTaskRecurrence.SetInitialData(Rec);
                                 UserTaskRecurrence.RUNMODAL;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=General;
                GroupType=Group }

    { 3   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the title of the task.;
                           NOR=Angir tittelen pÜ oppgaven.;
                           SVE=Anger uppgiftens rubrik.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Title;
                ShowMandatory=TRUE }

    { 8   ;2   ;Field     ;
                CaptionML=[ENU=Task Description;
                           NOR=Oppgavebeskrivelse;
                           SVE=Uppgiftsbeskrivning];
                ToolTipML=[ENU=Specifies what the task is about.;
                           NOR=Angir hva oppgaven dreier seg om.;
                           SVE=Anger vad uppgiften handlar om.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=MultiLineTextControl;
                MultiLine=Yes;
                OnValidate=BEGIN
                             SetDescription(MultiLineTextControl);
                           END;
                            }

    { 7   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies who created the task.;
                           NOR=Angir hvem som opprettet oppgaven.;
                           SVE=Anger vem som skapade uppgiften.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Created By User Name";
                Importance=Additional;
                Enabled=false;
                Editable=false }

    { 5   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies when the task was created.;
                           NOR=Angir nÜr oppgaven ble opprettet.;
                           SVE=Anger nÑr uppgiften skapades.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Created DateTime";
                Importance=Additional }

    { 16  ;1   ;Group     ;
                CaptionML=[ENU=Status;
                           NOR=Status;
                           SVE=Status];
                GroupType=Group }

    { 15  ;2   ;Field     ;
                DrillDown=No;
                ToolTipML=[ENU=Specifies who the task is assigned to.;
                           NOR=Angir hvem oppgaven er tilordnet til.;
                           SVE=Anger vem som Ñr tilldelad uppgiften.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Assigned To User Name";
                OnAssistEdit=VAR
                               User@1001 : Record 2000000120;
                               Users@1000 : Page 9800;
                             BEGIN
                               IF User.GET("Assigned To") THEN
                                 Users.SETRECORD(User);

                               Users.LOOKUPMODE := TRUE;
                               IF Users.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                 Users.GETRECORD(User);
                                 VALIDATE("Assigned To",User."User Security ID");
                                 CurrPage.UPDATE(TRUE);
                               END;
                             END;
                              }

    { 23  ;2   ;Field     ;
                CaptionML=[ENU=User Task Group;
                           NOR=Gruppe for brukeroppgave;
                           SVE=Grupp fîr anvÑndaruppgift];
                ToolTipML=[ENU=Specifies the group if the task has been assigned to a group of people.;
                           NOR=Angir gruppen hvis oppgaven er tilordnet til en gruppe personer.;
                           SVE=Anger gruppen om uppgiften har tilldelats en grupp anvÑndare.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="User Task Group Assigned To";
                OnValidate=BEGIN
                             CurrPage.UPDATE(TRUE);
                           END;
                            }

    { 10  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies when the task must be completed.;
                           NOR=Angir nÜr oppgaven mÜ vëre fullfõrt.;
                           SVE=Anger nÑr uppgiften mÜste vara slutfîrd.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Due DateTime" }

    { 11  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the progress of the task.;
                           NOR=Angir fremdriften for oppgaven.;
                           SVE=Anger uppgiftens fîrlopp.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Percent Complete";
                OnValidate=BEGIN
                             CurrPage.UPDATE(TRUE);
                           END;
                            }

    { 12  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies when the task must start.;
                           NOR=Angir nÜr oppgaven mÜ vëre startet.;
                           SVE=Anger nÑr uppgiften mÜste pÜbîrjas.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Start DateTime" }

    { 13  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the priority of the task.;
                           NOR=Angir prioriteten for oppgaven.;
                           SVE=Anger uppgiftens prioritet.];
                ApplicationArea=#Basic,#Suite;
                BlankZero=Yes;
                SourceExpr=Priority }

    { 9   ;2   ;Field     ;
                DrillDown=No;
                ToolTipML=[ENU=Specifies who completed the task.;
                           NOR=Angir hvem som fullfõrte oppgaven.;
                           SVE=Anger vem som slutfîrde uppgiften.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Completed By User Name";
                Importance=Additional;
                OnAssistEdit=VAR
                               User@1001 : Record 2000000120;
                               Users@1000 : Page 9800;
                             BEGIN
                               IF User.GET("Completed By") THEN
                                 Users.SETRECORD(User);

                               Users.LOOKUPMODE := TRUE;
                               IF Users.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                 Users.GETRECORD(User);
                                 VALIDATE("Completed By",User."User Security ID");
                                 CurrPage.UPDATE(TRUE);
                               END;
                             END;
                              }

    { 4   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies when the task was completed.;
                           NOR=Angir nÜr oppgaven ble fullfõrt.;
                           SVE=Anger nÑr uppgiften slutfîrdes.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Completed DateTime";
                Importance=Additional;
                OnValidate=BEGIN
                             CurrPage.UPDATE(TRUE);
                           END;
                            }

    { 17  ;1   ;Group     ;
                CaptionML=[ENU=Task Item;
                           NOR=Oppgaveelement;
                           SVE=Uppgiftsobjekt];
                GroupType=Group }

    { 20  ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the type of window that the task opens.;
                           NOR=Angir vindustypen som oppgaven Üpner.;
                           SVE=Anger vilken typ av fînster som uppgiften îppnar.];
                OptionCaptionML=[ENU=,,,Report,,,,,Page;
                                 NOR=,,,Rapport,,,,,Side;
                                 SVE=,,,Rapport,,,,,Sida];
                ApplicationArea=#Basic,#Suite;
                BlankZero=Yes;
                SourceExpr="Object Type";
                OnValidate=BEGIN
                             // Clear out the values for object id if it exists.
                             IF "Object ID" <> 0 THEN
                               "Object ID" := 0;
                           END;
                            }

    { 21  ;2   ;Field     ;
                Lookup=Yes;
                ToolTipML=[ENU=Specifies the window that the task opens.;
                           NOR=Angir vinduet som oppgaven Üpner.;
                           SVE=Anger fînstret som uppgiften îppnar.];
                ApplicationArea=#Basic,#Suite;
                BlankZero=Yes;
                SourceExpr="Object ID";
                CaptionClass=GetObjectTypeCaption;
                OnValidate=VAR
                             AllObjWithCaption@1000 : Record 2000000058;
                           BEGIN
                             IF "Object Type" = AllObjWithCaption."Object Type"::Page THEN BEGIN
                               AllObjWithCaption.SETRANGE("Object Type",AllObjWithCaption."Object Type"::Page);
                               AllObjWithCaption.SETRANGE("Object ID","Object ID");
                               IF AllObjWithCaption.FINDFIRST THEN
                                 IF AllObjWithCaption."Object Subtype" <> 'List' THEN
                                   ERROR(InvalidPageTypeErr);
                             END;
                           END;

                OnLookup=VAR
                           AllObjWithCaption@1007 : Record 2000000058;
                           AllObjectsWithCaption@1006 : Page 9174;
                         BEGIN
                           // If object type is empty then show both pages / reports in lookup
                           AllObjWithCaption.FILTERGROUP(2);
                           IF "Object Type" = 0 THEN BEGIN
                             AllObjWithCaption.SETFILTER("Object Type",'Page|Report');
                             AllObjWithCaption.SETFILTER("Object Subtype",'%1|%2','','List');
                           END ELSE BEGIN
                             IF "Object Type" = AllObjWithCaption."Object Type"::Page THEN BEGIN
                               AllObjWithCaption.SETRANGE("Object Type",AllObjWithCaption."Object Type"::Page);
                               AllObjWithCaption.SETRANGE("Object Subtype",'List');
                             END ELSE
                               AllObjWithCaption.SETRANGE("Object Type",AllObjWithCaption."Object Type"::Report);
                           END;
                           AllObjWithCaption.FILTERGROUP(0);

                           AllObjectsWithCaption.IsObjectTypeVisible(FALSE);
                           AllObjectsWithCaption.SETTABLEVIEW(AllObjWithCaption);

                           AllObjectsWithCaption.LOOKUPMODE := TRUE;
                           IF AllObjectsWithCaption.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             AllObjectsWithCaption.GETRECORD(AllObjWithCaption);
                             "Object ID" := AllObjWithCaption."Object ID";
                             "Object Type" := AllObjWithCaption."Object Type";
                           END;
                         END;
                          }

    { 22  ;2   ;Field     ;
                Name=ObjectName;
                CaptionML=[ENU=Resource Name;
                           NOR=Ressursnavn;
                           SVE=Resursnamn];
                ToolTipML=[ENU=Specifies the name of the resource that is assigned to the task.;
                           NOR=Angir navnet pÜ ressursen som er knyttet til oppgaven.;
                           SVE=Anger namnet pÜ den resurs som Ñr tilldelad uppgiften.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=DisplayObjectName;
                Enabled=false }

  }
  CODE
  {
    VAR
      MultiLineTextControl@1000 : Text;
      InvalidPageTypeErr@1001 : TextConst 'ENU=You must specify a list page.;NOR=Du mÜ angi en listeside.;SVE=Du mÜste ange en listsida.';
      IsMarkCompleteEnabled@1002 : Boolean;
      PageTok@1003 : TextConst 'ENU=Page;NOR=Side;SVE=Sida';
      ReportTok@1004 : TextConst 'ENU=Report;NOR=Rapport;SVE=Rapport';

    LOCAL PROCEDURE DisplayObjectName@6() : Text;
    VAR
      AllObjWithCaption@1000 : Record 2000000058;
    BEGIN
      AllObjWithCaption.SETRANGE("Object Type","Object Type");
      AllObjWithCaption.SETRANGE("Object ID","Object ID");
      IF AllObjWithCaption.FINDFIRST THEN
        EXIT(AllObjWithCaption."Object Name");
    END;

    LOCAL PROCEDURE RunReportOrPageLink@2();
    VAR
      AllObjWithCaption@1000 : Record 2000000058;
    BEGIN
      IF ("Object Type" = 0) OR ("Object ID" = 0) THEN
        EXIT;
      IF "Object Type" = AllObjWithCaption."Object Type"::Page THEN
        PAGE.RUN("Object ID")
      ELSE
        REPORT.RUN("Object ID");
    END;

    LOCAL PROCEDURE GetObjectTypeCaption@1() : Text;
    BEGIN
      IF "Object Type" = "Object Type"::Page THEN
        EXIT(PageTok);

      EXIT(ReportTok);
    END;

    BEGIN
    END.
  }
}

