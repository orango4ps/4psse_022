OBJECT Table 11071967 Service Cluster Employee
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ServiceContractClusterSeq@1100528600 : Record 11071966;
             BEGIN
               IF ServiceContractClusterSeq.GET(Type, "Type Code", "Source Type", "Cluster Code", "Seq. No.") THEN
                 ServiceContractClusterSeq.TESTFIELD(Subcontractor, '');
               TESTFIELD("Resource No.");

               IF Type IN [Type::" ", Type::Contract] THEN
                 IF "Source Type" <> "Source Type"::" " THEN
                   "Source Type" := "Source Type"::" ";
             END;

    OnDelete=VAR
               EmployeeAbsence@1100528600 : Record 5207;
               ServiceSetup@1100528601 : Record 11012800;
               AvailableClusterSeqnBlock@1100525000 : Record 11071968;
               EmplNo@1100529000 : Code[20];
             BEGIN
               ServiceSetup.GET;
               ServiceSetup.TESTFIELD("Cause of Abs. for Reservations");
               IF (Type IN [Type::" ", Type::Contract, Type::Modification]) THEN BEGIN
                 EmplNo := ResourceNo2EmplNo("Resource No.");  //DP00465.n
                 IF EmplNo <> '' THEN BEGIN  //DP00465.n
                   EmployeeAbsence.SETRANGE("Employee No.", EmplNo);  //DP00465.c
                   EmployeeAbsence.SETRANGE("Cause of Absence Code", ServiceSetup."Cause of Abs. for Reservations");
                   EmployeeAbsence.SETRANGE("Service Contract No.", "Type Code");
                   EmployeeAbsence.SETRANGE("Contract Cluster", "Cluster Code");
                   EmployeeAbsence.SETRANGE("Contract Cluster Seqn.", "Seq. No.");
                   EmployeeAbsence.DELETEALL(TRUE);
                 END;
                 AvailableClusterSeqnBlock.SETRANGE(Code, "Type Code");
                 AvailableClusterSeqnBlock.SETRANGE("Source Type", "Source Type");
                 AvailableClusterSeqnBlock.SETRANGE("Cluster Code", "Cluster Code");
                 AvailableClusterSeqnBlock.SETRANGE("Cluster Seq. No.", "Seq. No.");
                 AvailableClusterSeqnBlock.SETRANGE("Resource No.", "Resource No.");
                 AvailableClusterSeqnBlock.DELETEALL(TRUE);
               END;
             END;

    CaptionML=ENU=Service Cluster Employee;
    LookupPageID=Page11126446;
    DrillDownPageID=Page11126446;
  }
  FIELDS
  {
    { 2   ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Typ];
                                                   OptionCaptionML=ENU=" ,Contract,Template,Source Type,Modification";
                                                   OptionString=[ ,Contract,Template,Source Type,Modification] }
    { 5   ;   ;Type Code           ;Code20        ;TableRelation=IF (Type=CONST(Contract)) Service-Contract
                                                                 ELSE IF (Type=CONST(Modification)) "Object Modification";
                                                   CaptionML=[ENU=Type Code;
                                                              NOR=Typekode;
                                                              SVE=Typkod];
                                                   Editable=No }
    { 7   ;   ;Source Type         ;Option        ;OnValidate=BEGIN
                                                                IF "Source Type" <> "Source Type"::" " THEN BEGIN
                                                                  TESTFIELD(Type, Type::"Source Type");
                                                                  TESTFIELD("Type Code", '');
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Source Type;
                                                              NOR=Kildetype;
                                                              SVE=Ursprungstyp];
                                                   OptionCaptionML=ENU="Call,Contract,Direct,Estimate,,,Modification,Replacement,,,,,,, ";
                                                   OptionString=[Call,Contract,Direct,Estimate,,,Modification,Replacement,,,,,,, ] }
    { 10  ;   ;Cluster Code        ;Code20        ;TableRelation="Service Cluster".Code WHERE (Type=FIELD(Type),
                                                                                               Type Code=FIELD(Type Code),
                                                                                               Source Type=FIELD(Source Type));
                                                   CaptionML=[ENU=Cluster Code;
                                                              NOR=Klyngekode];
                                                   Editable=No }
    { 20  ;   ;Cluster Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Cluster".Description WHERE (Type=FIELD(Type),
                                                                                                           Type Code=FIELD(Type Code),
                                                                                                           Source Type=FIELD(Source Type),
                                                                                                           Code=FIELD(Cluster Code)));
                                                   CaptionML=[ENU=Cluster Description;
                                                              NOR=Klyngebeskrivelse];
                                                   Editable=No }
    { 30  ;   ;Seq. No.            ;Integer       ;TableRelation="Service Cluster Seq."."Seq. No." WHERE (Type=FIELD(Type),
                                                                                                          Type Code=FIELD(Type Code),
                                                                                                          Source Type=FIELD(Source Type),
                                                                                                          Cluster Code=FIELD(Cluster Code),
                                                                                                          Seq. No.=FIELD(Seq. No.));
                                                   CaptionML=[ENU=Seq. No.;
                                                              NOR=Sekv. nr;
                                                              SVE=Sekv. nr];
                                                   Editable=No }
    { 40  ;   ;Resource No.        ;Code20        ;TableRelation="Resource (WOP)" WHERE (Type=CONST(Employee));
                                                   OnValidate=VAR
                                                                EmployeeAbsence@1100528600 : Record 5207;
                                                                ServiceContractClusterSeq@1100528601 : Record 11071966;
                                                                ServiceSetup@1100528602 : Record 11012800;
                                                                ResourceWOP@1100528603 : Record 11229278;
                                                                EmplNo@1100529000 : Code[20];
                                                              BEGIN
                                                                IF Type IN [Type::" ", Type::Contract, Type::Modification] THEN BEGIN
                                                                  ServiceSetup.GET;
                                                                  ServiceSetup.TESTFIELD("Cause of Abs. for Reservations");
                                                                  ServiceContractClusterSeq.GET(Type, "Type Code", "Source Type", "Cluster Code", "Seq. No.");
                                                                  CALCFIELDS("Employee Name");
                                                                  IF xRec."Resource No." <> '' THEN BEGIN
                                                                    EmplNo := ResourceNo2EmplNo(xRec."Resource No.");  //DP00465.n
                                                                    IF EmplNo <> '' THEN BEGIN  //DP00465.n
                                                                      EmployeeAbsence.SETRANGE("Employee No.", EmplNo);  //DP00465.c
                                                                      EmployeeAbsence.SETRANGE("Cause of Absence Code", ServiceSetup."Cause of Abs. for Reservations");
                                                                      EmployeeAbsence.SETRANGE("Service Contract No.", "Type Code");
                                                                      EmployeeAbsence.SETRANGE("Contract Cluster", "Cluster Code");
                                                                      EmployeeAbsence.SETRANGE("Contract Cluster Seqn.", "Seq. No.");
                                                                      EmployeeAbsence.DELETEALL(TRUE);
                                                                    END;
                                                                  END;

                                                                  IF "Resource No." <> '' THEN BEGIN
                                                                    EmplNo := ResourceNo2EmplNo("Resource No.");  //DP00465.n
                                                                    IF EmplNo <> '' THEN BEGIN  //DP00465.n
                                                                      EmployeeAbsence."Entry No." := 0;
                                                                      EmployeeAbsence."Employee No." := EmplNo;  //DP00465.c
                                                                      EmployeeAbsence."From Date" := ServiceContractClusterSeq."Execution Period Start Date";
                                                                      EmployeeAbsence."To Date" := ServiceContractClusterSeq."Execution Period Ending Date";
                                                                      EmployeeAbsence.VALIDATE("Cause of Absence Code", ServiceSetup."Cause of Abs. for Reservations");
                                                                      CALCFIELDS("Cluster Description");
                                                                      EmployeeAbsence.VALIDATE(Description, "Cluster Description");
                                                                      EmployeeAbsence."Service Contract No." := "Type Code";
                                                                      EmployeeAbsence."Contract Cluster" := "Cluster Code";
                                                                      EmployeeAbsence."Contract Cluster Seqn." := "Seq. No.";
                                                                      EmployeeAbsence.INSERT(TRUE);
                                                                    END;
                                                                  END;
                                                                END;
                                                                IF "Resource No." <> '' THEN
                                                                  IF ResourceWOP.GET("Resource No.") THEN
                                                                    ResourceWOP.TestResourceWithinCompanyPlanGroup;
                                                              END;

                                                   CaptionML=[ENU=Resource No.;
                                                              NOR=Ressursnr.;
                                                              SVE=Resursnr];
                                                   Description=DP00465, based on Resource }
    { 50  ;   ;Employee Name       ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Resource (WOP)".Name WHERE (No.=FIELD(Resource No.)));
                                                   CaptionML=[ENU=Employee Name;
                                                              NOR=Navn p† ansatt;
                                                              SVE=Namn p† anst„lld];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Type,Type Code,Source Type,Cluster Code,Seq. No.,Resource No.;
                                                   Clustered=Yes }
    {    ;Resource No.                             }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE ResourceNo2EmplNo@1100529000(ResourceNo@1100529000 : Code[20]) : Code[20];
    VAR
      Resource@1100529001 : Record 11229278;
    BEGIN
      IF Resource.GET(ResourceNo) THEN BEGIN
        IF Resource.Company = COMPANYNAME THEN
          EXIT(Resource."Source No.");
      END;
      EXIT('');
    END;

    BEGIN
    END.
  }
}

