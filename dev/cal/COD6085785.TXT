OBJECT Codeunit 6085785 DC Workflow Event Handling
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW19.00.00.4.50;
  }
  PROPERTIES
  {
    EventSubscriberInstance=Static-Automatic;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      WorkflowManagement@1010 : Codeunit 1501;
      ApprReqForceApprovedEventDescTxt@1001 : TextConst 'DEU=Genehmigung eines Kaufbelegs wurde erzwungen.;ENU=A purchase document is force approved. (Continia);NLD=Een inkoopdocument is geforceerd goedgekeurd. (Continia)';
      ApprApproveAndDelegateEventTxt@1002 : TextConst 'DEU=Eine Genehmigungsanfrage wurde genehmigt und delegiert. (Continia);ENU=An approval request is approved and delegated. (Continia);NLD=Een goedkeuringsverzoek is goedgekeurd en gedelegeerd. (Continia)';
      ApprAdvApprovalRequest@1006 : TextConst 'DEU=Ein erweiterter Genehmigungsanfrage wurde genehmigt. (Continia);ENU=An advanced approval request is approved. (Continia);NLD=Een geavanceerd goedkeuringsverzoek is goedgekeurd. (Continia)';
      ApprDelegateWithoutApprovalEventTxt@1003 : TextConst 'DEU=Eine Genehmigungsanfrage wurde delegiert. (Continia);ENU=An approval request is delegated. (Continia);NLD=Een goedkeuringsverzoek is gedelegeerd. (Continia)';
      ApprDelegateAndSendBackTxt@1004 : TextConst 'DEU=Eine Genehmigungsanfrage wurde mit der Aufforderung zur RÅcksendung delegiert. (Continia);ENU=An approval request is delegated with a request to be sent back. (Continia);NLD=Een goedkeuringsverzoek is gedelegeerd met het verzoek terug te sturen. (Continia)';
      ApprSendBackApprovalRequestTxt@1005 : TextConst 'DEU=Eine Genehmigungsanfrage wurde zurÅckgesendet. (Continia);ENU=An approval request was sent back. (Continia);NLD=Een goedkeuringsverzoek is teruggestuurd. (Continia)';
      EventAlreadyExistErr@1000 : TextConst 'DEU=Es besteht bereits ein Ereignis mit Beschreibung %1. (Continia);ENU=An event with description %1 already exists. (Continia);NLD=Er bestaat al een gebeurtenis met beschrijving %1. (Continia)';

    [EventSubscriber(Codeunit,1520,OnAddWorkflowEventsToLibrary,"",Skip)]
    PROCEDURE CreateEventsLibrary@13();
    BEGIN
      AddEventToLibrary(
        RunWorkflowOnPurchDocForceApproveApprovalRequestCode,DATABASE::"Purchase Header",ApprReqForceApprovedEventDescTxt,0,FALSE);
      AddEventToLibrary(
        RunWorkflowOnApproveAndDelegateRequestCode,DATABASE::"Approval Entry",ApprApproveAndDelegateEventTxt,0,FALSE);
      AddEventToLibrary(
        RunWorkflowOnAfterApproveAdvancedAppRequestCode,DATABASE::"Approval Entry",ApprAdvApprovalRequest,0,FALSE);
      AddEventToLibrary(
        RunWorkflowOnDelegateAndSendBackRequestCode,DATABASE::"Approval Entry",ApprDelegateAndSendBackTxt,0,FALSE);
      AddEventToLibrary(
        RunWorkflowOnDelegateWithoutApprovalRequestCode,DATABASE::"Approval Entry",ApprDelegateWithoutApprovalEventTxt,0,FALSE);
      AddEventToLibrary(
        RunWorkflowOnSendBackApprovalRequestCode,DATABASE::"Approval Entry",ApprSendBackApprovalRequestTxt,0,FALSE);
    END;

    LOCAL PROCEDURE AddEventPredecessors@67(EventFunctionName@1001 : Code[128]);
    VAR
      WorkflowEventHandling@1000 : Codeunit 1520;
    BEGIN
      CASE EventFunctionName OF
        RunWorkflowOnPurchDocForceApproveApprovalRequestCode: // No predecessors. It is a starting event.
          ;
        RunWorkflowOnApproveAndDelegateRequestCode:
          AddEventPredecessor(RunWorkflowOnApproveAndDelegateRequestCode,WorkflowEventHandling.RunWorkflowOnSendPurchaseDocForApprovalCode);
        RunWorkflowOnDelegateAndSendBackRequestCode:
          AddEventPredecessor(RunWorkflowOnDelegateAndSendBackRequestCode,WorkflowEventHandling.RunWorkflowOnSendPurchaseDocForApprovalCode);
        RunWorkflowOnDelegateWithoutApprovalRequestCode:
          AddEventPredecessor(RunWorkflowOnDelegateWithoutApprovalRequestCode,WorkflowEventHandling.RunWorkflowOnSendPurchaseDocForApprovalCode);
        RunWorkflowOnSendBackApprovalRequestCode:
          AddEventPredecessor(RunWorkflowOnApproveAndDelegateRequestCode,WorkflowEventHandling.RunWorkflowOnSendPurchaseDocForApprovalCode);
        RunWorkflowOnAfterApproveAdvancedAppRequestCode:
          AddEventPredecessor(RunWorkflowOnAfterApproveAdvancedAppRequestCode,WorkflowEventHandling.RunWorkflowOnApproveApprovalRequestCode);
      END;
    END;

    PROCEDURE AddEventToLibrary@14(FunctionName@1001 : Code[128];TableID@1002 : Integer;Description@1003 : Text[250];RequestPageID@1004 : Integer;UsedForRecordChange@1005 : Boolean);
    VAR
      WorkflowEvent@1000 : Record 1520;
    BEGIN
      IF WorkflowEvent.GET(FunctionName) THEN
        EXIT;

      WorkflowEvent.SETRANGE(Description,Description);
      IF WorkflowEvent.FINDFIRST THEN
        ERROR(EventAlreadyExistErr,Description);

      WorkflowEvent.INIT;
      WorkflowEvent."Function Name" := FunctionName;
      WorkflowEvent."Table ID" := TableID;
      WorkflowEvent.Description := Description;
      WorkflowEvent."Request Page ID" := RequestPageID;
      WorkflowEvent."Used for Record Change" := UsedForRecordChange;
      WorkflowEvent.INSERT;

      AddEventPredecessors(WorkflowEvent."Function Name");
    END;

    PROCEDURE AddEventPredecessor@66(FunctionName@1001 : Code[128];PredecessorFunctionName@1002 : Code[128]);
    VAR
      WFEventResponseCombination@1000 : Record 1509;
    BEGIN
      WFEventResponseCombination.INIT;
      WFEventResponseCombination.Type := WFEventResponseCombination.Type::"Event";
      WFEventResponseCombination."Function Name" := FunctionName;
      WFEventResponseCombination."Predecessor Type" := WFEventResponseCombination."Predecessor Type"::"Event";
      WFEventResponseCombination."Predecessor Function Name" := PredecessorFunctionName;
      IF WFEventResponseCombination.INSERT THEN;
    END;

    PROCEDURE RunWorkflowOnPurchDocForceApproveApprovalRequestCode@21() : Code[128];
    BEGIN
      EXIT(UPPERCASE('RunWorkflowOnForceApproveApprovalRequest'));
    END;

    [EventSubscriber(Codeunit,6085790,OnSendPurchDocForForceApproval)]
    PROCEDURE RunWorkflowOnPurchDocForceApproveApprovalRequest@1(VAR PurchaseHeader@1000 : Record 38);
    BEGIN
      WorkflowManagement.HandleEvent(RunWorkflowOnPurchDocForceApproveApprovalRequestCode,PurchaseHeader);
    END;

    PROCEDURE RunWorkflowOnSendBackApprovalRequestCode@8() : Code[128];
    BEGIN
      EXIT(UPPERCASE('RunWorkflowOnSendBackApprovalRequest'));
    END;

    [EventSubscriber(Codeunit,6085790,OnSendBackApprovalRequested)]
    PROCEDURE RunWorkflowOnSendBackApprovalRequest@7(VAR ApprovalEntry@1000 : Record 454);
    BEGIN
      WorkflowManagement.HandleEvent(RunWorkflowOnSendBackApprovalRequestCode,ApprovalEntry);
    END;

    PROCEDURE RunWorkflowOnAfterApproveAdvancedAppRequestCode@11() : Code[128];
    BEGIN
      EXIT(UPPERCASE('RunWorkflowOnAfterApproveAdvancedAppRequest'));
    END;

    [EventSubscriber(Codeunit,6085790,OnAfterApproveAdvancedApprovalRequested)]
    PROCEDURE RunWorkflowOnAfterApproveAdvancedAppRequest@9(VAR ApprovalEntry@6085573 : Record 454);
    BEGIN
      WorkflowManagement.HandleEvent(RunWorkflowOnAfterApproveAdvancedAppRequestCode,ApprovalEntry);
    END;

    PROCEDURE RunWorkflowOnApproveAndDelegateRequestCode@10() : Text;
    BEGIN
      EXIT(UPPERCASE('RunWorkflowOnApproveAndDelegateRequest'));
    END;

    [EventSubscriber(Codeunit,6085790,OnApproveAndDelegateRequested)]
    LOCAL PROCEDURE RunWorkflowOnApproveAndDelegateRequest@6(VAR ApprovalEntry@1000 : Record 454);
    VAR
      RecRef@1002 : RecordRef;
    BEGIN
      WorkflowManagement.HandleEventOnKnownWorkflowInstance(RunWorkflowOnApproveAndDelegateRequestCode,
        ApprovalEntry,ApprovalEntry."Workflow Step Instance ID");
    END;

    PROCEDURE RunWorkflowOnDelegateWithoutApprovalRequestCode@5() : Text;
    BEGIN
      EXIT(UPPERCASE('RunWorkflowOnDelegateWithoutApprovalRequest'));
    END;

    [EventSubscriber(Codeunit,6085790,OnDelegateWithoutApprovalRequested)]
    LOCAL PROCEDURE RunWorkflowOnDelegateWithoutApprovalRequest@3(VAR ApprovalEntry@1000 : Record 454);
    VAR
      RecRef@1002 : RecordRef;
    BEGIN
      WorkflowManagement.HandleEventOnKnownWorkflowInstance(RunWorkflowOnDelegateWithoutApprovalRequestCode,
        ApprovalEntry,ApprovalEntry."Workflow Step Instance ID");
    END;

    PROCEDURE RunWorkflowOnDelegateAndSendBackRequestCode@2() : Text;
    BEGIN
      EXIT(UPPERCASE('RunWorkflowOnDelegateAndSendBackRequest'));
    END;

    [EventSubscriber(Codeunit,6085790,OnDelegateAndSendBackRequested)]
    LOCAL PROCEDURE RunWorkflowOnDelegateAndSendBackRequest@4(VAR ApprovalEntry@1000 : Record 454);
    VAR
      RecRef@1002 : RecordRef;
    BEGIN
      WorkflowManagement.HandleEventOnKnownWorkflowInstance(RunWorkflowOnDelegateAndSendBackRequestCode,
        ApprovalEntry,ApprovalEntry."Workflow Step Instance ID");
    END;

    BEGIN
    END.
  }
}

