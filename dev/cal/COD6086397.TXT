OBJECT Codeunit 6086397 EM Document File System
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.3.00.07;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      EMSetup@1000 : Record 6086300;
      ContCompSetup@6085574 : Record 6192773;
      FileSysMgt@1001 : Codeunit 6085584;
      FileInfo@1004 : Codeunit 6085666;
      GotCompSetup@6085575 : Boolean;
      GotEMSetup@1002 : Boolean;
      FilenameTooLong@1160040000 : TextConst 'ENU=File path too long. The full filename and path can be %1 characters.\Please change %2 %3.';

    PROCEDURE GetAttachmentFile@10(VAR EMAttachment@1000 : Record 6086359;VAR TempFile@1001 : TEMPORARY Record 6085608) : Boolean;
    BEGIN
      IF NOT HasAttachment(EMAttachment) THEN
        EXIT;

      EXIT(FileSysMgt.GetFile(GetAttachServerFilePath(EMAttachment),TempFile));
    END;

    PROCEDURE SetAttachment@3(VAR EMAttachment@1001 : Record 6086359;VAR TempFile@1000 : TEMPORARY Record 6085608) : Boolean;
    VAR
      LocalFile@1004 : File;
      ReadStream@1002 : InStream;
      WriteStream@1003 : OutStream;
    BEGIN
      TempFile.GetDataStream(ReadStream);

      IF FileSysMgt.FileExists(GetAttachServerFilePath(EMAttachment)) THEN
        FileSysMgt.DeleteFile(GetAttachServerFilePath(EMAttachment),FALSE);

      LocalFile.CREATE(GetAttachServerFilePath(EMAttachment));
      LocalFile.CREATEOUTSTREAM(WriteStream);
      COPYSTREAM(WriteStream,ReadStream);
      LocalFile.CLOSE;
      EXIT(TRUE);
    END;

    PROCEDURE HasAttachment@16(VAR EMAttachment@1000 : Record 6086359) : Boolean;
    BEGIN
      EXIT(FileSysMgt.FileExists(GetAttachServerFilePath(EMAttachment)));
    END;

    PROCEDURE ClearAttachment@14(VAR EMAttachment@1001 : Record 6086359;SupressError@1160040000 : Boolean) : Boolean;
    BEGIN
      IF HasAttachment(EMAttachment) THEN
        EXIT(FileSysMgt.DeleteFile(GetAttachServerFilePath(EMAttachment),SupressError));
    END;

    PROCEDURE GetInboxAttachmentFile@1160040009(VAR EMAttachmentInbox@1000 : Record 6086363;VAR TempFile@1001 : TEMPORARY Record 6085608) : Boolean;
    BEGIN
      //IF THE FILE IS ALREADY MOVED.
      IF EMAttachmentInbox.InboxAccepted THEN
        EXIT;

      IF NOT HasInboxAttachment(EMAttachmentInbox) THEN
        EXIT;

      EXIT(FileSysMgt.GetFile(GetInboxServerFilePath(EMAttachmentInbox),TempFile));
    END;

    PROCEDURE SetInboxAttachment@1160040008(VAR EMAttachmentInbox@1001 : Record 6086363;VAR TempFile@1000 : TEMPORARY Record 6085608) : Boolean;
    VAR
      LocalFile@1004 : File;
      ReadStream@1002 : InStream;
      WriteStream@1003 : OutStream;
    BEGIN
      TempFile.GetDataStream(ReadStream);

      IF FileSysMgt.FileExists(GetInboxServerFilePath(EMAttachmentInbox)) THEN
        FileSysMgt.DeleteFile(GetInboxServerFilePath(EMAttachmentInbox),FALSE);

      LocalFile.CREATE(GetInboxServerFilePath(EMAttachmentInbox));
      LocalFile.CREATEOUTSTREAM(WriteStream);
      COPYSTREAM(WriteStream,ReadStream);
      LocalFile.CLOSE;
      EXIT(TRUE);
    END;

    PROCEDURE HasInboxAttachment@1160040006(VAR EMAttachmentInbox@1000 : Record 6086363) : Boolean;
    BEGIN
      EXIT(FileSysMgt.FileExists(GetInboxServerFilePath(EMAttachmentInbox)));
    END;

    PROCEDURE ClearInboxAttachment@1160040005(VAR EMAttachmentInbox@1001 : Record 6086363) : Boolean;
    BEGIN
      IF HasInboxAttachment(EMAttachmentInbox) THEN
        EXIT(FileSysMgt.DeleteFile(GetInboxServerFilePath(EMAttachmentInbox),FALSE));
    END;

    PROCEDURE GetStorageLocation@6085574() : Text[1024];
    VAR
      EMAttachment@6085573 : Record 6086359;
    BEGIN
      IF NOT EMAttachment.FINDSET THEN;

      GetSetup;
      EXIT(GetArchiveDirFromDate(TODAY));
    END;

    LOCAL PROCEDURE GetAttachServerFilePath@6085573(VAR EMAttachment@6085575 : Record 6086359) FullFilename : Text[1024];
    VAR
      DateCreated@1160040000 : Date;
    BEGIN
      GetSetup;

      DateCreated := EMAttachment.GetCreatedDate;
      IF DateCreated = 0D THEN
        EXIT;

      FullFilename :=
        STRSUBSTNO('%1%2 - %3',GetArchiveDirFromDate(DateCreated),EMAttachment."Attachment GUID",EMAttachment."File Name");
      FileSysMgt.CreateDirectory(FileInfo.GetFilePath(FullFilename));
    END;

    LOCAL PROCEDURE GetInboxServerFilePath@1160040011(VAR EMAttachmentInbox@6085575 : Record 6086363) FullFilename : Text[1024];
    VAR
      DateCreated@1160040000 : Date;
    BEGIN
      GetSetup;

      DateCreated := EMAttachmentInbox.GetCreatedDate;
      IF DateCreated = 0D THEN
        EXIT;

      FullFilename :=
        STRSUBSTNO('%1%2 - %3',
          GetArchiveDirFromDate(DateCreated),EMAttachmentInbox."Attachment GUID",EMAttachmentInbox."File Name");
      FileSysMgt.CreateDirectory(FileInfo.GetFilePath(FullFilename));
    END;

    LOCAL PROCEDURE GetSetup@11();
    BEGIN
      IF NOT GotEMSetup THEN
        GotEMSetup := EMSetup.GET;

      IF NOT GotCompSetup THEN
        GotCompSetup := ContCompSetup.GET;
    END;

    PROCEDURE SetSetup@4(VAR NewEMSetup@1000 : Record 6086300;VAR NewContCompSetup@1160040000 : Record 6192773);
    BEGIN
      EMSetup := NewEMSetup;
      GotEMSetup := TRUE;

      ContCompSetup := NewContCompSetup;
      GotCompSetup := TRUE;
    END;

    LOCAL PROCEDURE GetArchiveDirFromDate@1160040002(DateCreated@1160040000 : Date) Path : Text[1024];
    BEGIN
      GetSetup;
      Path := EMSetup."Archive Path" +
        GetSubDir(EMSetup."Archive Directory Structure",DateCreated,EMSetup."Company Code in Archive",ContCompSetup."Company Code");
    END;

    LOCAL PROCEDURE GetSubDir@1160040001(Structure@1160040000 : Integer;Date@1160040001 : Date;CompCodeInArchive@1160040002 : Boolean;CompanyCode@1160040003 : Code[20]) : Text[1024];
    VAR
      Path@1160040006 : Text[1024];
      Day@1160040005 : Code[2];
      Month@1160040004 : Code[2];
      DayInteger@1160040008 : Integer;
      MonthInteger@1160040007 : Integer;
    BEGIN
      MonthInteger := DATE2DMY(Date,2);
      DayInteger := DATE2DMY(Date,1);

      IF MonthInteger < 10 THEN
        Month := '0' + FORMAT(MonthInteger)
      ELSE
        Month := FORMAT(MonthInteger);

      IF DayInteger < 10 THEN
        Day := '0' + FORMAT(DayInteger)
      ELSE
        Day := FORMAT(DayInteger);

      IF CompCodeInArchive AND (CompanyCode <> '') THEN
        Path := '\' + CompanyCode + '\'
      ELSE
        Path := '\';

      CASE Structure OF
        EMSetup."Archive Directory Structure"::"One Directory":
          EXIT(Path);
        EMSetup."Archive Directory Structure"::"Year\Month":
          EXIT(Path + STRSUBSTNO('%1\%2\',DATE2DMY(Date,3),Month));
        EMSetup."Archive Directory Structure"::"Year\Month\Day":
          EXIT(Path + STRSUBSTNO('%1\%2\%3\',DATE2DMY(Date,3),Month,Day));
      END;
    END;

    PROCEDURE CorrectAndShortenFilename@1160040000(Filename@1160040000 : Text[1024]) NewFileName : Text[1024];
    VAR
      TempFile@1160040001 : TEMPORARY Record 6085608;
      EMAttachmentInbox@1160040003 : Record 6086363;
      FileInformation@1160040005 : Codeunit 6085666;
      FileNameMaxLen@1160040002 : Integer;
      FileExtension@1160040004 : Text[10];
    BEGIN
      Filename := DELCHR(Filename,'=','\/:?"<>|');

      FileNameMaxLen := MAXSTRLEN(TempFile.Name) - STRLEN(GetInboxServerFilePath(EMAttachmentInbox));
      IF FileNameMaxLen < 0 THEN
        ERROR(FilenameTooLong,MAXSTRLEN(TempFile.Name),EMSetup.TABLECAPTION,(EMSetup.FIELDCAPTION("Archive Directory Structure")));

      IF FileNameMaxLen > STRLEN(Filename) THEN BEGIN
        NewFileName := Filename;
        EXIT;
      END;

      //SHORTEN STRING WITHOUT LOSING EXTENSION
      FileExtension := FileInformation.GetFileExtension(Filename);
      IF FileExtension <> '' THEN
        NewFileName := COPYSTR(Filename,1,FileNameMaxLen - STRLEN(FileExtension) - 1) + '.' + FileExtension
      ELSE
        NewFileName := COPYSTR(Filename,1,FileNameMaxLen);
    END;

    BEGIN
    END.
  }
}

