OBJECT Report 5683 Create Sum of Digits Table
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Dezimalstellen-Summentabelle erstellen;
               ENU=Create Sum of Digits Table;
               NLD=Cijfersomtabel maken;
               NOR=Opprett sum av sifre - tabell;
               SVE=Skapa tabell fîr siffersummering];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF NoOfYears < 1 THEN
                    ERROR(Text000);
                  IF NoOfYears >= 200 THEN
                    ERROR(Text001);

                  CreateDeprTableLines(DeprTableCode,NoOfYears);
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1   ;2   ;Field     ;
                  Name=NoOfYears;
                  CaptionML=[DEU=Anzahl Jahre;
                             ENU=No. of Years;
                             NLD=Aantal jaren;
                             NOR=Antall Ür];
                  ToolTipML=[DEU=Gibt die Anzahl der Jahre an, Åber die die Anlage abgeschrieben wird.;
                             ENU=Specifies the number of years over which the fixed asset will be depreciated.;
                             NLD=Hiermee wordt opgegeven over hoeveel jaar het vaste activum wordt afgeschreven.;
                             NOR=Angir hvor mange Ür aktivaet skal avskrives over.];
                  ApplicationArea=#FixedAssets;
                  BlankZero=Yes;
                  SourceExpr=NoOfYears;
                  MinValue=0 }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      NoOfYears@1000 : Integer;
      DeprTableCode@1001 : Code[20];
      Text000@1002 : TextConst 'DEU=Sie mÅssen die Anzahl der Jahre angeben.;ENU=You must specify No. of Years.;NLD=U moet Aantal jaren opgeven.;NOR=Du mÜ angi antall Ür.';
      Text001@1003 : TextConst 'DEU=Die Anzahl der Jahre muss weniger als 200 sein.;ENU=No. of Years must be less than 200.;NLD=Aantal jaren moet minder zijn dan 200.;NOR=Antall Ür mÜ vëre mindre enn 200.';

    [External]
    PROCEDURE SetTableCode@1(DeprTableCode2@1000 : Code[20]);
    BEGIN
      DeprTableCode := DeprTableCode2;
    END;

    LOCAL PROCEDURE CreateDeprTableLines@2(DeprTableCode@1000 : Code[10];NoOfYears@1001 : Integer);
    VAR
      DeprTableHeader@1002 : Record 5642;
      DeprTableLine@1003 : Record 5643;
      TotalDigitSum@1004 : Integer;
      TotalPercentSum@1007 : Decimal;
      CurrentDigit@1005 : Integer;
      CurrentDigitSum@1006 : Integer;
    BEGIN
      DeprTableHeader.GET(DeprTableCode);
      DeprTableHeader."Period Length" := DeprTableHeader."Period Length"::Year;
      DeprTableHeader."Total No. of Units" := 0;
      DeprTableLine.SETRANGE("Depreciation Table Code",DeprTableCode);
      DeprTableLine.DELETEALL;
      DeprTableHeader.MODIFY;

      CLEAR(DeprTableLine);
      DeprTableLine."Depreciation Table Code" := DeprTableCode;
      DeprTableLine."Period No." := 0;
      TotalDigitSum := 0;
      CurrentDigitSum := 0;
      TotalPercentSum := 0;
      FOR CurrentDigit := 1 TO NoOfYears DO
        TotalDigitSum := TotalDigitSum + CurrentDigit;
      FOR CurrentDigit := NoOfYears DOWNTO 1 DO BEGIN
        DeprTableLine."Period Depreciation %" := ROUND((CurrentDigit + CurrentDigitSum) / TotalDigitSum * 100,0.00000001) -
          TotalPercentSum;
        CurrentDigitSum := CurrentDigitSum + CurrentDigit;
        TotalPercentSum := TotalPercentSum + DeprTableLine."Period Depreciation %";
        DeprTableLine."Period No." := DeprTableLine."Period No." + 1;
        DeprTableLine.INSERT;
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

