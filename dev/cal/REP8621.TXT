OBJECT Report 8621 Config. Package - Process
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    TransactionType=UpdateNoLocks;
    CaptionML=[DEU=Paket konfigurieren - verarbeiten;
               ENU=Config. Package - Process;
               NLD=Configuratiepakket - verwerken;
               NOR=Konfigurer pakke - prosess];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 1   ;    ;DataItem;                    ;
               DataItemTable=Table8613;
               DataItemTableView=SORTING(Package Code,Table ID)
                                 ORDER(Ascending);
               OnAfterGetRecord=VAR
                                  TempTransformationRule@1000 : TEMPORARY Record 1237;
                                  TempField@1001 : TEMPORARY Record 2000000041;
                                BEGIN
                                  OnBeforeTextTransformation("Config. Package Table",TempField,TempTransformationRule);
                                  TempField.SETRANGE(TableNo,"Table ID");
                                  IF TempField.FINDSET THEN
                                    REPEAT
                                      TempTransformationRule.GET(FORMAT(TempField."No."));
                                      ApplyTextTransformation("Config. Package Table",TempField."No.",TempTransformationRule);
                                    UNTIL TempField.NEXT = 0
                                  ELSE
                                    MESSAGE(STRSUBSTNO(ImplementProcessingLogicMsg,"Table ID"))
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      SourceTable=Table8613;
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ImplementProcessingLogicMsg@1001 : TextConst '@@@=%1 - a table Id.;DEU=Implementieren Sie die Verarbeitungslogik fÅr Tabelle %1 in Bericht 8621 - Paket konfigurieren - Verarbeiten.;ENU=Implement processing logic for Table %1 in Report 8621 - Config. Package - Process.;NLD=Verwerkingslogica voor tabel %1 implementeren in rapport 8621 - Configuratiepakket - Verwerken.;NOR=Implementer behandlingslogikk for tabellen %1 i rapport 8621 - Konfigurer pakke - prosess.';

    [External]
    PROCEDURE AddRuleForField@2(TableNo@1002 : Integer;FieldNo@1003 : Integer;TransformationType@1004 : Option;VAR TempField@1000 : TEMPORARY Record 2000000041;VAR TempTransformationRule@1001 : TEMPORARY Record 1237);
    BEGIN
      TempField.INIT;
      TempField.TableNo := TableNo;
      TempField."No." := FieldNo;
      TempField.INSERT;
      TempTransformationRule.INIT;
      TempTransformationRule.Code := FORMAT(TempField."No.");
      TempTransformationRule."Transformation Type" := TransformationType;
      TempTransformationRule.INSERT;
    END;

    LOCAL PROCEDURE ApplyTextTransformation@25(ConfigPackageTable@1001 : Record 8613;FieldNo@1002 : Integer;TransformationRule@1000 : Record 1237);
    VAR
      ConfigPackageData@1003 : Record 8615;
    BEGIN
      IF GetConfigPackageData(ConfigPackageData,ConfigPackageTable,FieldNo) THEN
        REPEAT
          ConfigPackageData.Value := COPYSTR(TransformationRule.TransformText(ConfigPackageData.Value),1,250);
          ConfigPackageData.MODIFY;
        UNTIL ConfigPackageData.NEXT = 0;
    END;

    LOCAL PROCEDURE GetConfigPackageData@4(VAR ConfigPackageData@1004 : Record 8615;ConfigPackageTable@1000 : Record 8613;FieldId@1001 : Integer) : Boolean;
    BEGIN
      ConfigPackageData.SETRANGE("Package Code",ConfigPackageTable."Package Code");
      ConfigPackageData.SETRANGE("Table ID",ConfigPackageTable."Table ID");
      ConfigPackageData.SETRANGE("Field ID",FieldId);
      EXIT(ConfigPackageData.FINDSET(TRUE,FALSE));
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeTextTransformation@1(ConfigPackageTable@1000 : Record 8613;VAR TempField@1002 : TEMPORARY Record 2000000041;VAR TempTransformationRule@1001 : TEMPORARY Record 1237);
    BEGIN
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

