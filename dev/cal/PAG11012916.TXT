OBJECT Page 11012916 Investment Proposal Matrix
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Investment Proposal;
               NOR=Investeringsforslag;
               SVE=Investeringsfîrslag];
    SaveValues=Yes;
    SourceTable=Table11012912;
    PageType=ListPlus;
    RefreshOnActivate=Yes;
    OnInit=BEGIN
             PeriodType := PeriodType::Year;
             RoundFactor := RoundFactor::"1";

             ProjectNoEditable := TRUE;
             VersionEditable := TRUE;
             DescriptionEditable := TRUE;
             VersionDateEditable := TRUE;
             WACCPercentageEditable := TRUE;
             PhaseEditable := TRUE;
             FixedEditable := TRUE;
             ReferenceDateEditable := TRUE;
             StartingDateSalesEditable := TRUE;
             StartingDateConstrEditable := TRUE;
             DeliveryDateEditable := TRUE;
           END;

    OnOpenPage=BEGIN
                 MATRIX_MaximumSetLength := 12;
                 MATRIX_GenerateColumnCaptions(MATRIX_Step::Initial);
                 UpdateMatrixSubForm;
                 CurrPage.MATRIX.PAGE.SetEPVCalculation(TRUE);
               END;

    OnAfterGetRecord=VAR
                       RecRef@1100528700 : RecordRef;
                     BEGIN
                       SetEditable;

                       RecRef.GETTABLE(Rec);
                       CurrPage.RegisterExternalDocumentFB.PAGE.SetRecordReference(RecRef);
                       CurrPage.RegisterExternalDocumentFB.PAGE.SetByCompany(TRUE);
                       CurrPage.RegisterExternalDocumentFB.PAGE.SetDepartmentCode('');
                     END;

    OnNewRecord=BEGIN
                  "Version Date" := TODAY;
                  "Reference Date" := TODAY;
                END;

    ActionList=ACTIONS
    {
      { 1100528702;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 1100528730;1 ;ActionGroup;
                      CaptionML=[ENU=&Investment Proposal;
                                 SVE=&Investeringsfîrslag] }
      { 1100528731;2 ;Action    ;
                      CaptionML=[ENU=&Documents;
                                 NOR=&Dokumenter;
                                 SVE=&Dokument];
                      Image=Documents;
                      OnAction=VAR
                                 RecRef@1100528701 : RecordRef;
                                 DocumentFilters@1100528700 : Codeunit 11012432;
                               BEGIN
                                 RecRef.GETTABLE(Rec);
                                 DocumentFilters.CreateTempFilterAndExecute1(RecRef, '', FALSE, TRUE);
                               END;
                                }
      { 1100525000;2 ;Action    ;
                      CaptionML=ENU=Project Dev. Rubric Relations;
                      RunObject=Page 11127125;
                      RunPageLink=Project No.=FIELD(Project No.);
                      Image=Relationship }
      { 1100528710;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100528732;1 ;ActionGroup;
                      CaptionML=[ENU=F&unctions;
                                 NOR=Fu&nksjon;
                                 SVE=Fu&nktion] }
      { 1100528733;2 ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=ENU=Calculate ROS/EPV/PRI on Header;
                      Image=Calculate;
                      OnAction=VAR
                                 InvestmentProposal@1100528701 : Record 11012912;
                                 UpdateROS_EPV_PRI_Report@1100528700 : Report 11012908;
                               BEGIN
                                 InvestmentProposal := Rec;
                                 InvestmentProposal.SETRECFILTER;
                                 UpdateROS_EPV_PRI_Report.SETTABLEVIEW(InvestmentProposal);
                                 UpdateROS_EPV_PRI_Report.SetDefaults(PeriodType, TODAY);
                                 UpdateROS_EPV_PRI_Report.RUNMODAL;
                               END;
                                }
      { 1100528738;2 ;Action    ;
                      CaptionML=[ENU=Copy &Template;
                                 SVE=Kopiera &mall];
                      Image=Copy;
                      OnAction=VAR
                                 CopyInvestProposalTemplate@1100528700 : Report 11012907;
                               BEGIN
                                 TESTFIELD(Fixed, FALSE);
                                 CopyInvestProposalTemplate.SetSelections("Project No.", Version);
                                 CopyInvestProposalTemplate.RUNMODAL;
                                 CurrPage.MATRIX.PAGE.SetRecalculateEPV;
                                 CurrPage.UPDATE;
                               END;
                                }
      { 1100528734;2 ;Action    ;
                      CaptionML=ENU=Create &New Version;
                      Image=NewDocument;
                      OnAction=BEGIN
                                 CreateNewVersion;
                               END;
                                }
      { 1100528735;2 ;Action    ;
                      CaptionML=[ENU=Import Investment Proposal;
                                 SVE=Importera investeringsfîrslag];
                      Image=Import;
                      OnAction=VAR
                                 ImportInvestmentProposal@1100528700 : Page 11127312;
                               BEGIN
                                 TESTFIELD("Project No.");
                                 TESTFIELD(Version);

                                 ImportInvestmentProposal.InitXMLport("Project No.", Version);
                                 ImportInvestmentProposal.RUN;
                               END;
                                }
      { 1100527700;2 ;Action    ;
                      CaptionML=ENU=Import Investment Proposal Matrix;
                      Image=Import;
                      OnAction=VAR
                                 ImportInvestmPropMatrix@1100528700 : XMLport 11012903;
                               BEGIN
                                 TESTFIELD("Project No.");
                                 TESTFIELD(Version);

                                 ImportInvestmPropMatrix.InitSelections("Project No.", Version);
                                 ImportInvestmPropMatrix.RUN;
                               END;
                                }
      { 1100528736;2 ;Action    ;
                      CaptionML=[ENU=Distribute Budget;
                                 SVE=Distribuera budget];
                      Image=LedgerBudget;
                      OnAction=VAR
                                 RubricBudgetToDistribute@1100528700 : Record 11012909;
                               BEGIN
                                 TESTFIELD(Fixed, FALSE);
                                 RubricBudgetToDistribute.SETRANGE("Project No.", "Project No.");
                                 RubricBudgetToDistribute.SETRANGE(Version, Version);
                                 REPORT.RUNMODAL(REPORT::"Proj. Dev. Budget Distribution", TRUE, TRUE, RubricBudgetToDistribute);
                                 CurrPage.MATRIX.PAGE.SetRecalculateEPV;
                               END;
                                }
      { 1100528737;2 ;Action    ;
                      CaptionML=[ENU=Reopen (Reset 'Fixed');
                                 SVE=ôppna igen (ÜterstÑll 'Fast')];
                      Image=ReOpen;
                      OnAction=BEGIN
                                 TESTFIELD(Fixed, TRUE);
                                 Rec.ResetFixed;
                               END;
                                }
      { 1100525001;2 ;Action    ;
                      CaptionML=ENU=Copy To Project Budget;
                      Image=CopyCostBudget;
                      OnAction=BEGIN
                                 CopyToProjectBudget;
                               END;
                                }
      { 1100528707;1 ;Action    ;
                      CaptionML=[ENU=Previous Set;
                                 NOR=Forrige sett;
                                 SVE=FîregÜende uppsÑttning];
                      ToolTipML=[ENU=Previous Set;
                                 NOR=Forrige sett;
                                 SVE=FîregÜende uppsÑttning];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=PreviousSet;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 MATRIX_Step@1001 : 'Initial,Previous,Same,Next';
                               BEGIN
                                 MATRIX_GenerateColumnCaptions(MATRIX_Step::Previous);
                                 UpdateMatrixSubForm;
                               END;
                                }
      { 1100528706;1 ;Action    ;
                      CaptionML=[ENU=Previous Column;
                                 NOR=Forrige kolonne;
                                 SVE=FîregÜende kolumn];
                      ToolTipML=[ENU=Previous Column;
                                 NOR=Forrige kolonne;
                                 SVE=FîregÜende kolumn];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=PreviousRecord;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 MATRIX_GenerateColumnCaptions(MATRIX_Step::PreviousColumn);
                                 UpdateMatrixSubForm;
                               END;
                                }
      { 1100528705;1 ;Action    ;
                      CaptionML=[ENU=Next Column;
                                 NOR=Neste kolonne;
                                 SVE=NÑsta kolumn];
                      ToolTipML=[ENU=Next Column;
                                 NOR=Neste kolonne;
                                 SVE=NÑsta kolumn];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=NextRecord;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 MATRIX_GenerateColumnCaptions(MATRIX_Step::NextColumn);
                                 UpdateMatrixSubForm;
                               END;
                                }
      { 1100528704;1 ;Action    ;
                      CaptionML=[ENU=Next Set;
                                 NOR=Neste Sett;
                                 SVE=NÑsta uppsÑttning];
                      ToolTipML=[ENU=Next Set;
                                 NOR=Neste Sett;
                                 SVE=NÑsta uppsÑttning];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=NextSet;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 MATRIX_Step@1001 : 'Initial,Previous,Same,Next';
                               BEGIN
                                 MATRIX_GenerateColumnCaptions(MATRIX_Step::Next);
                                 UpdateMatrixSubForm;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100528703;;Container;
                ContainerType=ContentArea }

    { 1100528708;1;Group  ;
                CaptionML=[ENU=General;
                           NOR=Generell;
                           SVE=AllmÑnt];
                GroupType=Group }

    { 1100528711;2;Field  ;
                SourceExpr="Project No.";
                Editable=ProjectNoEditable;
                OnValidate=BEGIN
                             IF ("Project No." <> xRec."Project No.") AND ("Project No." <> '') THEN
                               FillDefaultWithProjectData;
                           END;
                            }

    { 1100528712;2;Field  ;
                SourceExpr="Project Description" }

    { 1100528713;2;Field  ;
                SourceExpr=Version;
                Editable=VersionEditable }

    { 1100528714;2;Field  ;
                SourceExpr=Description;
                Editable=DescriptionEditable }

    { 1100528715;2;Field  ;
                SourceExpr="Version Date";
                Editable=VersionDateEditable }

    { 1100528716;2;Field  ;
                SourceExpr="WACC Percentage";
                Editable=WACCPercentageEditable;
                OnValidate=BEGIN
                             CurrPage.MATRIX.PAGE.SetRecalculateEPV;
                             CurrPage.UPDATE;
                           END;
                            }

    { 1100528717;2;Field  ;
                SourceExpr=Phase;
                Editable=PhaseEditable;
                LookupPageID=Project Development Phase }

    { 1100528718;2;Field  ;
                SourceExpr="Phase Description" }

    { 1100528719;2;Field  ;
                SourceExpr="Score Percentage" }

    { 1100528720;2;Field  ;
                SourceExpr=Fixed;
                Editable=FixedEditable;
                OnValidate=VAR
                             InvestmentProposal@1100528700 : Record 11012912;
                           BEGIN
                             IF (NOT Fixed) THEN BEGIN
                               IF InvestmentProposal.GET("Project No.", Version) THEN BEGIN
                                 IF InvestmentProposal.Fixed THEN
                                   Fixed := TRUE;
                               END;
                             END ELSE BEGIN
                               IF InvestmentProposal.GET("Project No.", Version) THEN BEGIN
                                 IF NOT InvestmentProposal.Fixed THEN
                                   MODIFY;
                               END;
                             END;

                             CurrPage.UPDATE(FALSE);
                             SetEditable;
                           END;
                            }

    { 1100528709;1;Group  ;
                CaptionML=[ENU=Data;
                           NOR=Data;
                           SVE=Data];
                GroupType=Group }

    { 1100528721;2;Field  ;
                SourceExpr="Starting Date Sales";
                Editable=StartingDateSalesEditable }

    { 1100528722;2;Field  ;
                SourceExpr="Starting Date Construction";
                Editable=StartingDateConstrEditable }

    { 1100528723;2;Field  ;
                SourceExpr="Delivery Date";
                Editable=DeliveryDateEditable }

    { 1100528724;2;Field  ;
                SourceExpr="ROS % (Budget)" }

    { 1100528725;2;Field  ;
                SourceExpr="EPV (Budget)" }

    { 1100528726;2;Field  ;
                SourceExpr="PRI % (Budget)" }

    { 1100528729;1;Group  ;
                CaptionML=[ENU=Matrix Options;
                           NOR=Matrisealternativer;
                           SVE=Matrisalternativ];
                GroupType=Group }

    { 1100528727;2;Field  ;
                CaptionML=[ENU=View by;
                           NOR=Vis per;
                           SVE=Visa per];
                OptionCaptionML=[ENU=Day,Week,Month,Quarter,Year;
                                 NOR=Dag,Uke,MÜned,Kvartal,èr;
                                 SVE=Dag,Vecka,MÜnad,Kvartal,èr];
                SourceExpr=PeriodType;
                OnValidate=BEGIN
                             MATRIX_GenerateColumnCaptions(MATRIX_Step::Initial);
                             UpdateMatrixSubForm;
                           END;
                            }

    { 1100528701;2;Field  ;
                CaptionML=[ENU=Round Factor;
                           SVE=Avrundad faktor];
                SourceExpr=RoundFactor;
                OnValidate=BEGIN
                             UpdateMatrixSubForm;
                           END;
                            }

    { 1100528744;2;Field  ;
                SourceExpr="Reference Date";
                OnValidate=BEGIN
                             CurrPage.MATRIX.PAGE.SetRecalculateEPV;
                             CurrPage.UPDATE;
                           END;
                            }

    { 1100528700;1;Part   ;
                Name=MATRIX;
                SubPageLink=Project No.=FIELD(Project No.),
                            Version=FIELD(Version);
                PagePartID=Page11012917;
                PartType=Page }

    { 1100528743;0;Container;
                ContainerType=FactBoxArea }

    { 1100528728;1;Part   ;
                Name=RegisterExternalDocumentFB;
                PagePartID=Page11125053;
                PartType=Page }

  }
  CODE
  {
    VAR
      MATRIX_MatrixRecords@1100528707 : ARRAY [32] OF Record 2000000007;
      MATRIX_MaximumSetLength@1100528706 : Integer;
      MATRIX_PrimKeyRecInCurrSet@1100528705 : Text[1024];
      MATRIX_CaptionSet@1100528704 : ARRAY [32] OF Text[80];
      MATRIX_CaptionRange@1100528703 : Text[250];
      MATRIX_CurrSetLength@1100528702 : Integer;
      MATRIX_Step@1100528701 : 'Initial,Previous,Same,Next,PreviousColumn,NextColumn,,,,,FirstRec,LastRec';
      PeriodType@1100528709 : 'Day,Week,Month,Quarter,Year';
      RoundFactor@1100528710 : 'None,1,1000,1000000';
      Text000@1100528715 : TextConst 'ENU=Copy Investment Proposal (Project ''%1'', Version ''%2'') to a new version?;SVE=Kopiera investeringsfîrslag (projekt ''%1'', version ''%2'') till en ny version?';
      Text001@1100528714 : TextConst 'ENU=No rubric lines present.;SVE=Det finns inga rubrikrader.';
      Text002@1100528713 : TextConst 'ENU=New version can not be determined.;SVE=Det gÜr inte att faststÑlla den nya versionen.';
      Text003@1100528712 : TextConst 'ENU=Set up Version %2'' (Project ''%1'' as the actual version?;SVE=Vill du ange version %2'' (projekt ''%1'') som den verkliga versionen?';
      Text004@1100528711 : TextConst 'ENU=Set up Version %2'' (Project ''%1'') as the definite version?;SVE=Vill du ange version %2'' (projekt ''%1'') som den definitiva versionen?';
      ProjectNoEditable@1100528716 : Boolean INDATASET;
      VersionEditable@1100528717 : Boolean INDATASET;
      DescriptionEditable@1100528718 : Boolean INDATASET;
      VersionDateEditable@1100528719 : Boolean INDATASET;
      WACCPercentageEditable@1100528720 : Boolean INDATASET;
      PhaseEditable@1100528721 : Boolean INDATASET;
      FixedEditable@1100528722 : Boolean INDATASET;
      ReferenceDateEditable@1100528723 : Boolean INDATASET;
      StartingDateSalesEditable@1100528724 : Boolean INDATASET;
      StartingDateConstrEditable@1100528725 : Boolean INDATASET;
      DeliveryDateEditable@1100528726 : Boolean INDATASET;
      Text005@1100525000 : TextConst 'ENU=Do you want to create a Budget?';
      Text006@1100525001 : TextConst 'ENU=A Budget is already present. Continue and delete present Budget?';
      Text007@1100525002 : TextConst 'ENU=Budget created.';

    LOCAL PROCEDURE MATRIX_GenerateColumnCaptions@1152(MATRIX_SetWanted@1000 : 'Initial,Previous,Same,Next,PreviousColumn,NextColumn');
    VAR
      MatrixManagement@1002 : Codeunit 9200;
    BEGIN
      CLEAR(MATRIX_CaptionSet);
      CLEAR(MATRIX_MatrixRecords);

      MatrixManagement.GeneratePeriodMatrixData(
        MATRIX_SetWanted,
        MATRIX_MaximumSetLength,
        FALSE, // UseNameForCaption
        PeriodType,
        '', // DateFilter
        MATRIX_PrimKeyRecInCurrSet,
        MATRIX_CaptionSet,
        MATRIX_CaptionRange,
        MATRIX_CurrSetLength,
        MATRIX_MatrixRecords);
    END;

    LOCAL PROCEDURE UpdateMatrixSubForm@7();
    BEGIN
      CurrPage.MATRIX.PAGE.Load(
        MATRIX_CaptionSet,
        MATRIX_MatrixRecords,
        MATRIX_CurrSetLength,
        PeriodType,
        RoundFactor);
    END;

    PROCEDURE FillDefaultWithProjectData@1100485001();
    VAR
      ProjectDevelopment@1100485000 : Record 11012915;
    BEGIN
      ProjectDevelopment.GET("Project No.");

      "Starting Date Sales" := ProjectDevelopment."Starting Date Sales";
      "Starting Date Construction" := ProjectDevelopment."Starting Date Construction";
      "Delivery Date" := ProjectDevelopment."Delivery Date";
    END;

    PROCEDURE SetEditable@1100485000();
    BEGIN
      ProjectNoEditable := (NOT Fixed);
      VersionEditable := (NOT Fixed);
      DescriptionEditable := (NOT Fixed);
      VersionDateEditable := (NOT Fixed);
      WACCPercentageEditable := (NOT Fixed);
      PhaseEditable := (NOT Fixed);
      FixedEditable := (NOT Fixed);
      ReferenceDateEditable := (NOT Fixed);
      StartingDateSalesEditable := (NOT Fixed);
      StartingDateConstrEditable := (NOT Fixed);
      DeliveryDateEditable := (NOT Fixed);
    END;

    PROCEDURE CreateNewVersion@1100528705();
    VAR
      InvestmentProposal@1100528708 : Record 11012912;
      InvestmentProposal2@1100528707 : Record 11012912;
      InvestmProposalRubricLine@1100528706 : Record 11012913;
      InvestmProposalRubricLine2@1100528705 : Record 11012913;
      InvestmentProposalBudget@1100528704 : Record 11012914;
      InvestmentProposalBudget2@1100528703 : Record 11012914;
      RubricBudgetToDistribute@1100528702 : Record 11012909;
      RubricBudgetToDistribute2@1100528701 : Record 11012909;
      ProjectDevelopment@1100528700 : Record 11012915;
    BEGIN
      InvestmProposalRubricLine.SETRANGE("Project No.", "Project No.");
      InvestmProposalRubricLine.SETRANGE(Version, Version);
      IF NOT InvestmProposalRubricLine.FIND('-') THEN
        ERROR(Text001);

      IF NOT CONFIRM(Text000, FALSE, "Project No.", Version) THEN
        EXIT;

      InvestmentProposalBudget.SETCURRENTKEY("Project No.",Version, "Rubric Code", Date);

      InvestmentProposal.RESET;
      InvestmentProposal.SETRANGE("Project No.", "Project No.");
      InvestmentProposal.FIND('+');

      InvestmentProposal2 := Rec;
      InvestmentProposal2.Version := INCSTR(InvestmentProposal.Version);
      IF (InvestmentProposal2.Version = '') THEN
        ERROR(Text002);
      InvestmentProposal2."Version Date" := TODAY;
      InvestmentProposal2.Fixed := FALSE;
      InvestmentProposal2.INSERT(TRUE);

      InvestmProposalRubricLine.RESET;
      InvestmProposalRubricLine.SETRANGE("Project No.", "Project No.");
      InvestmProposalRubricLine.SETRANGE(Version, Version);
      InvestmProposalRubricLine.FIND('-');
      REPEAT
        InvestmProposalRubricLine2 := InvestmProposalRubricLine;
        InvestmProposalRubricLine2.Version := InvestmentProposal2.Version;
        InvestmProposalRubricLine2.INSERT;
        IF (InvestmProposalRubricLine2.Type IN
          [InvestmProposalRubricLine2.Type::Costs, InvestmProposalRubricLine2.Type::Revenues,
           InvestmProposalRubricLine2.Type::"Costs as Perc. Revenues", InvestmProposalRubricLine2.Type::"Revenues as Perc. Costs",
           InvestmProposalRubricLine2.Type::"Costs to Distr. as Perc. Rev."])
        THEN BEGIN
          InvestmentProposalBudget.SETRANGE("Project No.", "Project No.");
          InvestmentProposalBudget.SETRANGE(Version, Version);
          InvestmentProposalBudget.SETRANGE("Rubric Code", InvestmProposalRubricLine2."Rubric Code");
          IF InvestmentProposalBudget.FIND('-') THEN BEGIN
            REPEAT
              InvestmentProposalBudget2 := InvestmentProposalBudget;
              InvestmentProposalBudget2.Version := InvestmentProposal2.Version;
              InvestmentProposalBudget2."Line No." := 0;
              InvestmentProposalBudget2.INSERT(TRUE);
            UNTIL InvestmentProposalBudget.NEXT = 0;
          END;
          RubricBudgetToDistribute.SETRANGE("Project No.", "Project No.");
          RubricBudgetToDistribute.SETRANGE(Version, Version);
          RubricBudgetToDistribute.SETRANGE("Rubric Code", InvestmProposalRubricLine2."Rubric Code");
          IF RubricBudgetToDistribute.FIND('-') THEN BEGIN
            REPEAT
              RubricBudgetToDistribute2 := RubricBudgetToDistribute;
              RubricBudgetToDistribute2.Version := InvestmentProposal2.Version;
              RubricBudgetToDistribute2.INSERT(TRUE);
            UNTIL RubricBudgetToDistribute.NEXT = 0;
          END;
        END;
      UNTIL InvestmProposalRubricLine.NEXT = 0;

      InvestmProposalRubricLine2.UpdateSurcharges(InvestmProposalRubricLine2, FALSE);

      COMMIT;
      IF ProjectDevelopment.GET(InvestmentProposal2."Project No.") THEN BEGIN
        IF CONFIRM(Text003, TRUE, InvestmentProposal2."Project No.", InvestmentProposal2.Version) THEN BEGIN
          IF ProjectDevelopment."Definite Exploitation" = '' THEN
            ProjectDevelopment."Definite Exploitation" := InvestmentProposal2.Version;
          ProjectDevelopment."Actual Exploitation" := InvestmentProposal2.Version;
          ProjectDevelopment.MODIFY;
          InvestmentProposal.GET("Project No.", Version);
          IF NOT InvestmentProposal.Fixed THEN BEGIN
            InvestmentProposal.Fixed := TRUE;
            InvestmentProposal.MODIFY;
          END;
        END ELSE BEGIN
          IF ProjectDevelopment."Definite Exploitation" = '' THEN BEGIN
            IF CONFIRM(Text004, TRUE, InvestmentProposal2."Project No.", InvestmentProposal2.Version) THEN BEGIN
              ProjectDevelopment."Definite Exploitation" := InvestmentProposal2.Version;
              ProjectDevelopment.MODIFY;
              InvestmentProposal.GET("Project No.", Version);
              IF NOT InvestmentProposal.Fixed THEN BEGIN
                InvestmentProposal.Fixed := TRUE;
                InvestmentProposal.MODIFY;
              END;
            END;
          END;
        END;
      END;

      GET(InvestmentProposal2."Project No.", InvestmentProposal2.Version);
    END;

    LOCAL PROCEDURE CopyToProjectBudget@1100525000();
    VAR
      Project@1100525006 : Record 11072003;
      BudgetLine@1100525000 : Record 11012001;
      InvestmProposalRubricLine@1100525001 : Record 11012913;
      ProjectDevRubricRelation@1100525002 : Record 11012902;
      ProjectDevelopmentFunctions@1100525007 : Codeunit 11012903;
      DimVal@1100525005 : Record 349;
      DimMgt@1100525004 : Codeunit 408;
      NewBudgetLineNo@1100525003 : Integer;
    BEGIN
      TESTFIELD("Project No.");

      IF NOT CONFIRM(Text005, TRUE) THEN
        EXIT;

      Project.GET("Project No.");

      BudgetLine.SETRANGE("Project No.", "Project No.");
      BudgetLine.SETRANGE(Adjustment, '');
      BudgetLine.SETRANGE("Extension Contract", '');
      BudgetLine.SETRANGE(Option, '');
      IF NOT BudgetLine.ISEMPTY THEN
        IF CONFIRM(Text006) THEN
          BudgetLine.DELETEALL(TRUE)
        ELSE
          EXIT;

      InvestmProposalRubricLine.RESET;
      InvestmProposalRubricLine.SETRANGE("Project No.", "Project No.");
      InvestmProposalRubricLine.SETRANGE(Version, Version);
      InvestmProposalRubricLine.SETRANGE(Type, InvestmProposalRubricLine.Type::Costs);
      InvestmProposalRubricLine.FINDSET;
      REPEAT
        ProjectDevRubricRelation.GET(InvestmProposalRubricLine."Project No.", InvestmProposalRubricLine."Rubric Code");
        ProjectDevRubricRelation.TESTFIELD("Cost Object");

        DimMgt.GetDimValueRec(2,ProjectDevRubricRelation."Cost Object",DimVal,FALSE,InvestmProposalRubricLine."Project No.");
        DimVal.TESTFIELD(Blocked, FALSE);

        NewBudgetLineNo := NewBudgetLineNo + 10000;

        BudgetLine.INIT;
        BudgetLine."Project No." := InvestmProposalRubricLine."Project No.";
        BudgetLine.FillMainProject;
        BudgetLine."Line No." := NewBudgetLineNo;
        BudgetLine.VALIDATE("Cost Type",DimVal."Cost Type");
        BudgetLine.VALIDATE("Cost Object",ProjectDevRubricRelation."Cost Object");
        BudgetLine.Description := COPYSTR(InvestmProposalRubricLine.Description, 1, MAXSTRLEN(BudgetLine.Description));
        BudgetLine."Time Quantity" := 1;
        IF BudgetLine."Cost Type" = BudgetLine."Cost Type"::Labor THEN BEGIN
          IF BudgetLine."Rate (LCY)" = 0 THEN BEGIN
            BudgetLine."Rate (LCY)" := ProjectDevelopmentFunctions.CalculateBudgetByRubric(InvestmProposalRubricLine, 0D, 0D, 0D);
            BudgetLine.Norm := 1;
            BudgetLine.Quantity := 1;
          END ELSE
            BudgetLine.Quantity := ProjectDevelopmentFunctions.CalculateBudgetByRubric(InvestmProposalRubricLine, 0D, 0D, 0D) / BudgetLine."Rate (LCY)";
          BudgetLine."Rate (FCY)" := BudgetLine."Rate (LCY)";
        END ELSE BEGIN
          BudgetLine.Quantity := 1;
          BudgetLine."Gross Price (LCY)" := ProjectDevelopmentFunctions.CalculateBudgetByRubric(InvestmProposalRubricLine, 0D, 0D, 0D);
          BudgetLine."Gross Price (FCY)" := BudgetLine."Gross Price (LCY)";
          BudgetLine."Price (LCY)" := BudgetLine."Gross Price (LCY)";
          BudgetLine."Price (FCY)" := BudgetLine."Gross Price (FCY)";
        END;
        BudgetLine.CalcTotals;
        BudgetLine."Department Code" := Project."Global Dimension 1 Code";
        BudgetLine.FillDefaultFscType;
        BudgetLine.GetVersionDate;
        BudgetLine.UpdateExtContractStatus;
        BudgetLine.GetDefaultPurchAction;
        BudgetLine.UpdateCostControlStatus(0);
        BudgetLine.INSERT;

      UNTIL InvestmProposalRubricLine.NEXT = 0;

      MESSAGE(Text007);
    END;

    BEGIN
    END.
  }
}

