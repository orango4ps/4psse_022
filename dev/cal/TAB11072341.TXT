OBJECT Table 11072341 DPT Def Resp Pers Perm ServLoc
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnModify=BEGIN
               CALCFIELDS("Subsite Structure");
               IF ("Subsite Structure" < "Subsite Structure"::PermissionPerDocType) AND (Type = Type::Access) THEN
                 "Document Type" := '';
               CheckPermissions;
             END;

    CaptionML=ENU=DPT Def Responsible Person Permission (Service);
  }
  FIELDS
  {
    { 10  ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Typ];
                                                   OptionCaptionML=ENU=,Access;
                                                   OptionString=,Access;
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 20  ;   ;Responsibility      ;Code10        ;TableRelation=Responsibility;
                                                   CaptionML=[ENU=Responsibility;
                                                              NOR=Ansvar;
                                                              SVE=Ansvar];
                                                   NotBlank=Yes }
    { 21  ;   ;Responsibility Description;Text50  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Responsibility.Description WHERE (Code=FIELD(Responsibility)));
                                                   CaptionML=[ENU=Responsibility Description;
                                                              NOR=Ansvar, Beskrivelse;
                                                              SVE=Ansvar, beskrivning];
                                                   Editable=No }
    { 30  ;   ;Subsite Code        ;Code20        ;TableRelation="DPT Default Subsite".Code WHERE (Source Type=CONST(ServiceLocation),
                                                                                                   Quote Request Subsite=CONST(No));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Subsite Structure");
                                                              END;

                                                   CaptionML=ENU=Document Portal Subsite }
    { 31  ;   ;Subsite Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Default Subsite".Description WHERE (Source Type=CONST(Project),
                                                                                                               Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Subsite Description;
                                                   Editable=No }
    { 32  ;   ;Subsite Structure   ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("DPT Default Subsite"."Subsite Structure" WHERE (Source Type=CONST(ServiceLocation),
                                                                                                                       Code=FIELD(Subsite Code)));
                                                   CaptionML=ENU=Subsite Structure;
                                                   OptionCaptionML=ENU=1 Document Library,Document Library per Document Type,Permission per Document Type;
                                                   OptionString=OneDocLibrary,DocLibraryPerDocType,PermissionPerDocType;
                                                   Editable=No }
    { 40  ;   ;Document Type       ;Code20        ;TableRelation=IF (Type=CONST(Access),
                                                                     Subsite Structure=CONST(PermissionPerDocType)) "DPT Default Subsite Doc Type"."Document Type" WHERE (Source Type=CONST(ServiceLocation),
                                                                                                                                                                          Subsite Code=FIELD(Subsite Code));
                                                   CaptionML=[ENU=Document Type;
                                                              NOR=Dokumenttype;
                                                              SVE=Dokumenttyp] }
    { 41  ;   ;Document Type Description;Text50   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Document Type".Description WHERE (Type=FIELD(Document Type)));
                                                   CaptionML=[ENU=Document Type Description;
                                                              NOR=Dokumenttype, Beskrivelse;
                                                              SVE=Dokumenttyp, beskrivning];
                                                   Editable=No }
    { 50  ;   ;Permission Level    ;Option        ;OnValidate=BEGIN
                                                                CheckPermissions;
                                                                IF ("Permission Level" < "Private Permission Level") THEN
                                                                  "Private Permission Level" := "Permission Level";
                                                                IF ("Permission Level" = "Permission Level"::Owner) AND ("Subsite Code" <> '') AND ("Document Type" = '') THEN
                                                                  "Private Permission Level" := "Permission Level";
                                                              END;

                                                   CaptionML=ENU=Permission Level;
                                                   OptionCaptionML=ENU=None,Visitor,Member,Owner;
                                                   OptionString=None,Visitor,Member,Owner }
    { 60  ;   ;Show in View OLD    ;Boolean       ;CaptionML=;
                                                   Editable=No }
    { 70  ;   ;Private Permission Level;Option    ;OnValidate=BEGIN
                                                                TESTFIELD("Subsite Code");
                                                                TESTFIELD("Document Type", '');
                                                                IF ("Permission Level" = "Permission Level"::Owner) THEN
                                                                  "Private Permission Level" := "Permission Level";
                                                                IF ("Permission Level" < "Private Permission Level") THEN
                                                                  FIELDERROR("Private Permission Level");
                                                              END;

                                                   CaptionML=ENU=Private Permission Level;
                                                   OptionCaptionML=ENU=None,Visitor,Member,Owner;
                                                   OptionString=None,Visitor,Member,Owner }
  }
  KEYS
  {
    {    ;Type,Responsibility,Subsite Code,Document Type;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE AddPortalPermissions@1100528302(CodeFilter@1100528303 : Code[20]);
    VAR
      Responsibility@1100528300 : Record 11012007;
      DPTDefaultSubsite@1100528301 : Record 11229941;
      DPTDefaultSubsiteDocType@1100528302 : Record 11229942;
    BEGIN
      IF (NOT Responsibility.FINDSET) THEN
        EXIT;
      REPEAT
        InsertRecord(1, Responsibility.Code, '', '');

        DPTDefaultSubsite.SETRANGE("Source Type", DPTDefaultSubsite."Source Type"::ServiceLocation);
        IF (CodeFilter <> '') THEN
          DPTDefaultSubsite.SETRANGE(Code, CodeFilter);
        DPTDefaultSubsite.SETRANGE("Quote Request Subsite", FALSE);
        DPTDefaultSubsite.SETRANGE("Subsite Structure", DPTDefaultSubsite."Subsite Structure"::PermissionPerDocType);
        IF (DPTDefaultSubsite.FINDSET) THEN BEGIN
          REPEAT
            DPTDefaultSubsiteDocType.SETRANGE("Source Type", DPTDefaultSubsite."Source Type");
            DPTDefaultSubsiteDocType.SETRANGE("Subsite Code", DPTDefaultSubsite.Code);
            IF (DPTDefaultSubsiteDocType.FINDSET) THEN BEGIN
              InsertRecord(1, Responsibility.Code, DPTDefaultSubsite.Code, '');
              REPEAT
                InsertRecord(1, Responsibility.Code, DPTDefaultSubsite.Code, DPTDefaultSubsiteDocType."Document Type");
              UNTIL (DPTDefaultSubsiteDocType.NEXT = 0);
            END;
          UNTIL (DPTDefaultSubsite.NEXT = 0);
        END;

        DPTDefaultSubsite.SETFILTER("Subsite Structure", '<%1', DPTDefaultSubsite."Subsite Structure"::PermissionPerDocType);
        IF (DPTDefaultSubsite.FINDSET) THEN BEGIN
          REPEAT
            InsertRecord(1, Responsibility.Code, DPTDefaultSubsite.Code, '');
          UNTIL (DPTDefaultSubsite.NEXT = 0);
        END;

        DPTDefaultSubsite.SETRANGE("Quote Request Subsite", TRUE);
        IF (DPTDefaultSubsite.FINDSET) THEN BEGIN
          REPEAT
            InsertRecord(1, Responsibility.Code, DPTDefaultSubsite.Code, '');
          UNTIL (DPTDefaultSubsite.NEXT = 0);
        END;
      UNTIL (Responsibility.NEXT = 0);
    END;

    LOCAL PROCEDURE InsertRecord@1100528301(Type@1100528301 : Option;Responsibility@1100528302 : Code[20];SubsiteCode@1100528303 : Code[20];DocumentType@1100528304 : Code[20]);
    VAR
      ServiceSetup@1100409000 : Record 11012800;
      DPTDefRespPersPermServLoc@1100528300 : Record 11072341;
    BEGIN
      IF (DPTDefRespPersPermServLoc.GET(Type, Responsibility, SubsiteCode, DocumentType)) THEN
        EXIT;
      DPTDefRespPersPermServLoc.INIT;
      DPTDefRespPersPermServLoc.Type := Type;
      DPTDefRespPersPermServLoc.Responsibility := Responsibility;
      DPTDefRespPersPermServLoc."Subsite Code" := SubsiteCode;
      DPTDefRespPersPermServLoc."Document Type" := DocumentType;
      CASE Type OF
        1: BEGIN
          ServiceSetup.GET;
          IF (Responsibility = ServiceSetup."Responsibility Location Man") THEN
            DPTDefRespPersPermServLoc."Permission Level" := DPTDefRespPersPermServLoc."Permission Level"::Owner
          ELSE
            DPTDefRespPersPermServLoc."Permission Level" := DPTDefRespPersPermServLoc."Permission Level"::Visitor;
        END;
      END;
      DPTDefRespPersPermServLoc.INSERT;
    END;

    PROCEDURE CheckPermissions@1100409000();
    BEGIN
      CheckPermissionLevel;
    END;

    LOCAL PROCEDURE CheckPermissionLevel@1100528303();
    VAR
      DPTDefRespPersPermServLoc@1100528300 : Record 11072341;
    BEGIN
      IF (Type <> Type::Access) THEN
        EXIT;
      IF (("Subsite Code" = '') AND ("Document Type" = '') AND ("Permission Level" = "Permission Level"::None)) THEN BEGIN
        DPTDefRespPersPermServLoc.SETRANGE(Type , DPTDefRespPersPermServLoc.Type::Access);
        DPTDefRespPersPermServLoc.SETRANGE(Responsibility, Responsibility);
        DPTDefRespPersPermServLoc.SETFILTER("Subsite Code", '<>%1', "Subsite Code");
        DPTDefRespPersPermServLoc.MODIFYALL("Permission Level", "Permission Level"::None);
        EXIT;
      END;
      CALCFIELDS("Subsite Structure");
      IF ("Subsite Structure" < "Subsite Structure"::PermissionPerDocType) THEN
        EXIT;

      IF ("Document Type" = '') THEN BEGIN
        DPTDefRespPersPermServLoc.SETRANGE(Type, Type);
        DPTDefRespPersPermServLoc.SETRANGE(Responsibility, Responsibility);
        DPTDefRespPersPermServLoc.SETRANGE("Subsite Code", "Subsite Code");
        DPTDefRespPersPermServLoc.SETFILTER("Document Type", '<>%1', "Document Type");
        DPTDefRespPersPermServLoc.MODIFYALL("Permission Level", "Permission Level");
      END ELSE BEGIN
        DPTDefRespPersPermServLoc.GET(Type, Responsibility, "Subsite Code", '');
        IF (DPTDefRespPersPermServLoc."Permission Level" = DPTDefRespPersPermServLoc."Permission Level"::None) THEN BEGIN
          "Permission Level" := "Permission Level"::Visitor;
        END;
        IF (DPTDefRespPersPermServLoc."Permission Level" < "Permission Level") THEN BEGIN
          DPTDefRespPersPermServLoc."Permission Level" := "Permission Level";
          DPTDefRespPersPermServLoc.MODIFY;
        END;
      END;
    END;

    PROCEDURE MakePrivatePermEqualToPerm@1100529900();
    VAR
      DPTDefRespPersPermServLoc@1100529900 : Record 11072341;
    BEGIN
      DPTDefRespPersPermServLoc.SETRANGE(Type, Type::Access);
      DPTDefRespPersPermServLoc.SETFILTER("Subsite Code", '%1', '');
      DPTDefRespPersPermServLoc.MODIFYALL("Private Permission Level", "Private Permission Level"::None, FALSE);

      DPTDefRespPersPermServLoc.SETFILTER("Subsite Code", '<>%1', '');
      DPTDefRespPersPermServLoc.SETFILTER("Document Type", '<>%1', '');
      DPTDefRespPersPermServLoc.MODIFYALL("Private Permission Level", "Private Permission Level"::None, FALSE);

      DPTDefRespPersPermServLoc.SETFILTER("Document Type", '%1', '');
      IF (NOT DPTDefRespPersPermServLoc.FINDSET) THEN
        EXIT;
      REPEAT
        DPTDefRespPersPermServLoc."Private Permission Level" := DPTDefRespPersPermServLoc."Permission Level";
        DPTDefRespPersPermServLoc.MODIFY;
      UNTIL (DPTDefRespPersPermServLoc.NEXT = 0);
    END;

    BEGIN
    END.
  }
}

