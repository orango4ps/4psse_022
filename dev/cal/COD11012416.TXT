OBJECT Codeunit 11012416 Create Provisional Work Doc.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    TableNo=11012779;
    OnRun=BEGIN
            IF CreateDocLinkOnly THEN
              CreateDocumentLink(Rec)
            ELSE
              CreateDocument(Rec);

            COMMIT;
          END;

  }
  CODE
  {
    VAR
      DocumentProperties@1210190006 : Record 11012746;
      ProvisionalWorkDocument@1210190007 : Record 11012783;
      DocumentTemplate@1210190001 : Record 11012401;
      ProvisionalWorkDocType@1210190003 : Record 11012770;
      OverwriteExistingDocs@1210190000 : Boolean;
      CreateDocLinkOnly@1210190004 : Boolean;
      NoteCode@1210190002 : Code[20];
      NewDocumentNo@1210190005 : Code[20];

    LOCAL PROCEDURE CreateDocument@1100485000(ProvisionalWorkContact@1100485000 : Record 11012779);
    VAR
      ProvisionalWork@1100525000 : Record 11012774;
      Job@1100525003 : Record 11072003;
      DocumentManagement@1100485011 : Codeunit 11012406;
      RecRef@1100525002 : RecordRef;
      DocLinkRID@1100525001 : ARRAY [5] OF RecordID;
    BEGIN
      RecRef.GETTABLE(ProvisionalWorkContact);
      DocLinkRID[1] := RecRef.RECORDID;

      // Get source Document Link
      RecRef.GETTABLE(ProvisionalWorkDocType);
      DocLinkRID[2] := RecRef.RECORDID;

      // Get other Document Links
      IF ProvisionalWork.GET(ProvisionalWorkContact."Provisional Work No.") THEN BEGIN
        RecRef.GETTABLE(ProvisionalWork);
        DocLinkRID[3] := RecRef.RECORDID;
      END;

      IF ProvisionalWork."Job No." <> '' THEN
        IF Job.GET(ProvisionalWork."Job No.") THEN BEGIN
          RecRef.GETTABLE(Job);
          DocLinkRID[4] := RecRef.RECORDID;
        END;

      DocumentManagement.GenerateDocFromTemplate2(
        DocumentTemplate,
        DocLinkRID,
        '',
        '',
        ProvisionalWorkContact."Contact No.",
        ProvisionalWorkContact."Contact Person No.",
        '',
        FALSE,
        FALSE,
        FALSE);

      ProvisionalWorkDocument.INIT;
      ProvisionalWorkDocument."Provisional Work No." := ProvisionalWorkContact."Provisional Work No.";
      ProvisionalWorkDocument."Feature Group" := ProvisionalWorkContact."Feature Group";
      ProvisionalWorkDocument."Feature Code" := ProvisionalWorkContact.Feature;
      ProvisionalWorkDocument."Contact No." := ProvisionalWorkContact."Contact No.";
      ProvisionalWorkDocument."Contact Person No." := ProvisionalWorkContact."Contact Person No.";
      ProvisionalWorkDocument."Document Group" := ProvisionalWorkDocType.Code;
      ProvisionalWorkDocument."Note Code" := NoteCode;
      ProvisionalWorkDocument.INSERT(TRUE);

      NewDocumentNo := ProvisionalWorkDocument.GetDocumentNo;
    END;

    LOCAL PROCEDURE CreateDocumentLink@1210190001(ProvisionalWorkContact@1210190004 : Record 11012779);
    VAR
      RecRef@1210190002 : RecordRef;
      DocumentLinkManagement@1210190003 : Codeunit 11012401;
      DocLinkRID@1210190001 : ARRAY [5] OF RecordID;
    BEGIN
      DocumentProperties.GET(NewDocumentNo);
      RecRef.GETTABLE(ProvisionalWorkContact);
      DocLinkRID[2] := RecRef.RECORDID();
      DocumentLinkManagement.CreateDocumentLink(DocumentProperties,DocLinkRID);
    END;

    PROCEDURE SetGlobalValues@1210190000(DocTemplate@1210190003 : Record 11012401;ProvWorkDocType@1210190002 : Record 11012770;OverwriteExistDocs@1210190001 : Boolean;CreateLinkOnly@1210190005 : Boolean;NewDocNo@1210190004 : Code[20];NoteCde@1210190000 : Code[20]);
    BEGIN
      DocumentTemplate := DocTemplate;
      ProvisionalWorkDocType := ProvWorkDocType;
      OverwriteExistingDocs := OverwriteExistDocs;
      CreateDocLinkOnly := CreateLinkOnly;
      NewDocumentNo := NewDocNo;
      NoteCode := NoteCde;
    END;

    PROCEDURE GetNewDocumentNo@1210190002() : Code[20];
    BEGIN
      EXIT(NewDocumentNo);
    END;

    BEGIN
    END.
  }
}

