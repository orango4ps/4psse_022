OBJECT Report 790 Calculate Inventory
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04,4PS14.00,4PSSE;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Lagerbestand berechnen;
               ENU=Calculate Inventory;
               NLD=Voorraad berekenen;
               NOR=Beregn beholdning];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF SkipDim THEN
                    ColumnDim := ''
                  ELSE
                    DimSelectionBuf.CompareDimText(3,REPORT::"Calculate Inventory",'',ColumnDim,Text003);
                  ZeroQtySave := ZeroQty;

                  //**4PS.sn
                  //*04-03-2008 If dimension Cost Center and/or Cost Object is not selected, then use those of the item (EK, call 11052)
                  GLSetup.GET;
                  UsedItemDim1 := NOT SelectedDim.GET(USERID,3,REPORT::"Calculate Inventory",'',GLSetup."Shortcut Dimension 1 Code");
                  UsedItemDim2 := NOT SelectedDim.GET(USERID,3,REPORT::"Calculate Inventory",'',GLSetup."Shortcut Dimension 2 Code");
                  //**4PS.en
                END;

  }
  DATASET
  {
    { 8129;    ;DataItem;                    ;
               DataItemTable=Table27;
               DataItemTableView=SORTING(No.)
                                 WHERE(Type=CONST(Inventory),
                                       Blocked=CONST(No));
               OnPreDataItem=VAR
                               ItemJnlTemplate@1000 : Record 82;
                               ItemJnlBatch@1001 : Record 233;
                             BEGIN
                               IF PostingDate = 0D THEN
                                 ERROR(Text000);

                               ItemJnlTemplate.GET(ItemJnlLine."Journal Template Name");
                               ItemJnlBatch.GET(ItemJnlLine."Journal Template Name",ItemJnlLine."Journal Batch Name");
                               IF NextDocNo = '' THEN BEGIN
                                 IF ItemJnlBatch."No. Series" <> '' THEN BEGIN
                                   ItemJnlLine.SETRANGE("Journal Template Name",ItemJnlLine."Journal Template Name");
                                   ItemJnlLine.SETRANGE("Journal Batch Name",ItemJnlLine."Journal Batch Name");
                                   IF NOT ItemJnlLine.FINDFIRST THEN
                                     NextDocNo := NoSeriesMgt.GetNextNo(ItemJnlBatch."No. Series",PostingDate,FALSE);
                                   ItemJnlLine.INIT;
                                 END;
                                 IF NextDocNo = '' THEN
                                   ERROR(Text001);
                               END;

                               NextLineNo := 0;

                               IF NOT HideValidationDialog THEN
                                 Window.OPEN(Text002,"No.");

                               IF NOT SkipDim THEN
                                 SelectedDim.GetSelectedDim(USERID,3,REPORT::"Calculate Inventory",'',TempSelectedDim);

                               QuantityOnHandBuffer.RESET;
                               QuantityOnHandBuffer.DELETEALL;

                               OnAfterItemOnPreDataItem(Item,ZeroQty,IncludeItemWithNoTransaction);
                             END;

               OnAfterGetRecord=BEGIN
                                  IF NOT HideValidationDialog THEN
                                    Window.UPDATE;
                                  TempSKU.DELETEALL;
                                END;

               OnPostDataItem=BEGIN
                                CalcPhysInvQtyAndInsertItemJnlLine;
                              END;

               ReqFilterFields=No.,Location Filter,Bin Filter }

    { 7209;1   ;DataItem;                    ;
               DataItemTable=Table32;
               DataItemTableView=SORTING(Item No.,Entry Type,Variant Code,Drop Shipment,Location Code,Posting Date);
               OnPreDataItem=BEGIN
                               WhseEntry.SETCURRENTKEY("Item No.","Bin Code","Location Code","Variant Code");
                               Item.COPYFILTER("Bin Filter",WhseEntry."Bin Code");

                               IF ColumnDim = '' THEN
                                 TempDimBufIn.SETRANGE("Table ID",DATABASE::Item)
                               ELSE
                                 TempDimBufIn.SETRANGE("Table ID",DATABASE::"Item Ledger Entry");
                               TempDimBufIn.SETRANGE("Entry No.");
                               TempDimBufIn.DELETEALL;
                             END;

               OnAfterGetRecord=VAR
                                  ItemVariant@1003 : Record 5401;
                                  ByBin@1000 : Boolean;
                                  ExecuteLoop@1002 : Boolean;
                                  InsertTempSKU@1001 : Boolean;
                                BEGIN
                                  IF NOT GetLocation("Location Code") THEN
                                    CurrReport.SKIP;

                                  IF ColumnDim <> '' THEN
                                    TransferDim("Dimension Set ID");
                                  IF NOT "Drop Shipment" THEN
                                    ByBin := Location."Bin Mandatory" AND NOT Location."Directed Put-away and Pick";
                                  IF NOT SkipCycleSKU("Location Code","Item No.","Variant Code") THEN
                                    IF ByBin THEN BEGIN
                                      IF NOT TempSKU.GET("Location Code","Item No.","Variant Code") THEN BEGIN
                                        InsertTempSKU := FALSE;
                                        IF "Variant Code" = '' THEN
                                          InsertTempSKU := TRUE
                                        ELSE
                                          IF ItemVariant.GET("Item No.","Variant Code") THEN
                                            InsertTempSKU := TRUE;
                                        IF InsertTempSKU THEN BEGIN
                                          TempSKU."Item No." := "Item No.";
                                          TempSKU."Variant Code" := "Variant Code";
                                          TempSKU."Location Code" := "Location Code";
                                          TempSKU.INSERT;
                                          ExecuteLoop := TRUE;
                                        END;
                                      END;
                                      IF ExecuteLoop THEN BEGIN
                                        WhseEntry.SETRANGE("Item No.","Item No.");
                                        WhseEntry.SETRANGE("Location Code","Location Code");
                                        WhseEntry.SETRANGE("Variant Code","Variant Code");
                                        IF WhseEntry.FIND('-') THEN
                                          IF WhseEntry."Entry No." <> OldWhseEntry."Entry No." THEN BEGIN
                                            OldWhseEntry := WhseEntry;
                                            REPEAT
                                              WhseEntry.SETRANGE("Bin Code",WhseEntry."Bin Code");
                                              IF NOT ItemBinLocationIsCalculated(WhseEntry."Bin Code") THEN BEGIN
                                                WhseEntry.CALCSUMS("Qty. (Base)");
                                                UpdateBuffer(WhseEntry."Bin Code",WhseEntry."Qty. (Base)");
                                              END;
                                              WhseEntry.FIND('+');
                                              Item.COPYFILTER("Bin Filter",WhseEntry."Bin Code");
                                            UNTIL WhseEntry.NEXT = 0;
                                          END;
                                      END;
                                    END ELSE
                                      UpdateBuffer('',Quantity);
                                END;

               DataItemLink=Item No.=FIELD(No.),
                            Variant Code=FIELD(Variant Filter),
                            Location Code=FIELD(Location Filter),
                            Global Dimension 1 Code=FIELD(Global Dimension 1 Filter),
                            Global Dimension 2 Code=FIELD(Global Dimension 2 Filter) }

    { 1   ;1   ;DataItem;                    ;
               DataItemTable=Table7312;
               OnAfterGetRecord=BEGIN
                                  IF NOT "Item Ledger Entry".ISEMPTY THEN
                                    CurrReport.SKIP;   // Skip if item has any record in Item Ledger Entry.
                                  CLEAR(QuantityOnHandBuffer);
                                  QuantityOnHandBuffer."Item No." := "Item No.";
                                  QuantityOnHandBuffer."Location Code" := "Location Code";
                                  QuantityOnHandBuffer."Variant Code" := "Variant Code";

                                  GetLocation("Location Code");
                                  IF Location."Bin Mandatory" AND NOT Location."Directed Put-away and Pick" THEN
                                    QuantityOnHandBuffer."Bin Code" := "Bin Code";
                                  IF NOT QuantityOnHandBuffer.FIND THEN
                                    QuantityOnHandBuffer.INSERT;   // Insert a zero quantity line.
                                END;

               DataItemLink=Item No.=FIELD(No.),
                            Variant Code=FIELD(Variant Filter),
                            Location Code=FIELD(Location Filter) }

    { 2   ;1   ;DataItem;ItemWithNoTransaction;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number)
                                 WHERE(Number=CONST(1));
               OnAfterGetRecord=BEGIN
                                  IF IncludeItemWithNoTransaction THEN
                                    UpdateQuantityOnHandBuffer(Item."No.");
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF PostingDate = 0D THEN
                     PostingDate := WORKDATE;
                   ValidatePostingDate;
                   ColumnDim := DimSelectionBuf.GetDimSelectionText(3,REPORT::"Calculate Inventory",'');
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1   ;2   ;Field     ;
                  Name=PostingDate;
                  CaptionML=[DEU=Buchungsdatum;
                             ENU=Posting Date;
                             NLD=Boekingsdatum;
                             NOR=Bokfõringsdato;
                             SVE=Bokfîringsdatum];
                  ToolTipML=[DEU=Legt das Datum fÅr die Buchung dieser Stapelverarbeitung fest. StandardmÑ·ig wird das Arbeitsdatum eingegeben, Sie kînnen es allerdings Ñndern.;
                             ENU=Specifies the date for the posting of this batch job. By default, the working date is entered, but you can change it.;
                             NLD=Hiermee wordt de datum opgegeven voor de boeking van deze batchverwerking. Standaard wordt de werkdatum ingevuld in dit veld, maar u kunt deze wijzigen.;
                             NOR=Angir datoen for bokfõring av denne kjõrselen. Som standard oppgis arbeidsdatoen, men du kan endre den.;
                             SVE=Anger datumet fîr bokfîringen av detta batch-jobb. Arbetsdatumet anges standard, men du kan Ñndra det.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=PostingDate;
                  OnValidate=BEGIN
                               ValidatePostingDate;
                             END;
                              }

      { 9   ;2   ;Field     ;
                  Name=DocumentNo;
                  CaptionML=[DEU=Belegnr.;
                             ENU=Document No.;
                             NLD=Documentnr.;
                             NOR=Bilagsnr.;
                             SVE=Dokumentnr];
                  ToolTipML=[DEU=Gibt die Nummer des Dokuments an, das vom Bericht oder der Stapelverarbeitung verarbeitet wird.;
                             ENU=Specifies the number of the document that is processed by the report or batch job.;
                             NLD=Hiermee wordt het nummer opgegeven van het document dat wordt verwerkt door het rapport of de batchverwerking.;
                             NOR=Angir nummeret for dokumentet som behandles av rapporten eller kjõrselen.;
                             SVE=Anger antalet dokument som rapporten eller buntjobbet bearbetar.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=NextDocNo }

      { 11  ;2   ;Field     ;
                  Name=ItemsNotOnInventory;
                  CaptionML=[DEU=Artikel nicht auf Lager.;
                             ENU=Items Not on Inventory.;
                             NLD=Artikelen niet in voorraad.;
                             NOR=Varer ikke pÜ lager.];
                  ToolTipML=[DEU=Gibt an, ob Buch.-Blattzeilen fÅr Artikel erstellt werden sollen, die nicht am Lager sind, also fÅr Artikel, deren Wert im Feld "Menge (berechnet)" 0 ist.;
                             ENU=Specifies if journal lines should be created for items that are not on inventory, that is, items where the value in the Qty. (Calculated) field is 0.;
                             NLD=Hiermee wordt opgegeven of u wilt dat er dagboekregels worden gemaakt voor artikelen die niet in voorraad zijn. Dat zijn artikelen waarvoor de waarde in het veld Aantal (Berekend) gelijk is aan 0.;
                             NOR=Angir om kladdelinjer skal opprettes for varer som ikke er pÜ lager, det vil si varer der verdien i feltet Antall (beregnet) er 0.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=ZeroQty;
                  OnValidate=BEGIN
                               IF NOT ZeroQty THEN
                                 IncludeItemWithNoTransaction := FALSE;
                             END;
                              }

      { 2   ;2   ;Field     ;
                  Name=IncludeItemWithNoTransaction;
                  CaptionML=[DEU=Artikel ohne Transaktionen einbeziehen;
                             ENU=Include Item without Transactions;
                             NLD=Artikel zonder transacties opnemen;
                             NOR=Inkluder vare uten transaksjoner];
                  ToolTipML=[DEU=Legt fest, ob Buch.-Blattzeilen fÅr Artikel erstellt werden sollen, die nicht am Lager sind und nicht in Transaktionen verwendet werden.;
                             ENU=Specifies if journal lines should be created for items that are not on inventory and are not used in any transactions.;
                             NLD=Hiermee wordt opgegeven of u wilt dat er dagboekregels worden gemaakt voor artikelen die niet in voorraad zijn en die niet worden gebruikt in transacties.;
                             NOR=Angir om kladdelinjer skal opprettes for varer som ikke er pÜ lager og som ikke brukes i noen transaksjoner.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=IncludeItemWithNoTransaction;
                  OnValidate=BEGIN
                               IF NOT IncludeItemWithNoTransaction THEN
                                 EXIT;
                               IF NOT ZeroQty THEN
                                 ERROR(ItemNotOnInventoryErr);
                             END;
                              }

      { 15  ;2   ;Field     ;
                  Name=ByDimensions;
                  CaptionML=[DEU=Nach Dimensionen;
                             ENU=By Dimensions;
                             NLD=Per dimensies;
                             NOR=Per dimensjon];
                  ToolTipML=[DEU=Legt die Dimensionen fest, die die Stapelverarbeitung berÅcksichtigen soll.;
                             ENU=Specifies the dimensions that you want the batch job to consider.;
                             NLD=Hiermee worden de dimensies opgegeven die door de batchverwerking moeten worden meegenomen.;
                             NOR=Angir dimensjonene du vil at kjõrselen skal vurdere.;
                             SVE=Anger de dimensioner som du vill att batch-jobbet ska îvervÑga.];
                  ApplicationArea=#Dimensions;
                  SourceExpr=ColumnDim;
                  Editable=FALSE;
                  OnAssistEdit=BEGIN
                                 DimSelectionBuf.SetDimSelectionMultiple(3,REPORT::"Calculate Inventory",ColumnDim);
                               END;
                                }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Geben Sie das Buchungsdatum ein.;ENU=Enter the posting date.;NLD=Voer de boekingdatum opnieuw in.;NOR=Angi bokfõringsdatoen.;SVE=Ange bokfîringsdatum.';
      Text001@1001 : TextConst 'DEU=Geben Sie die Belegnummer ein.;ENU=Enter the document no.;NLD=Voer een documentnummer in.;NOR=Angi bilagsnummer.;SVE=Ange dokumentnr';
      Text002@1002 : TextConst 'DEU=Artikel werden durchgelesen  #1##########;ENU=Processing items    #1##########;NLD=Verwerken art.      #1##########;NOR=Behandler varer     #1##########;SVE=Behandlar artiklar  #1##########';
      Text003@1003 : TextConst 'DEU=Dimensionen behalten;ENU=Retain Dimensions;NLD=Te bewaren dimensies;NOR=Behold dimensjoner;SVE=BibehÜll dimensioner';
      ItemJnlBatch@1004 : Record 233;
      ItemJnlLine@1005 : Record 83;
      WhseEntry@1024 : Record 7312;
      QuantityOnHandBuffer@1007 : TEMPORARY Record 307;
      SourceCodeSetup@1008 : Record 242;
      DimSetEntry@1010 : Record 480;
      OldWhseEntry@1038 : Record 7312;
      TempSKU@1035 : TEMPORARY Record 5700;
      TempDimSetEntry@1015 : TEMPORARY Record 480;
      SelectedDim@1011 : Record 369;
      TempSelectedDim@1012 : TEMPORARY Record 369;
      TempDimBufIn@1013 : TEMPORARY Record 360;
      TempDimBufOut@1014 : TEMPORARY Record 360;
      DimSelectionBuf@1016 : Record 368;
      Location@1009 : Record 14;
      NoSeriesMgt@1017 : Codeunit 396;
      DimBufMgt@1018 : Codeunit 411;
      Window@1019 : Dialog;
      PostingDate@1020 : Date;
      CycleSourceType@1030 : ' ,Item,SKU';
      PhysInvtCountCode@1029 : Code[10];
      NextDocNo@1021 : Code[20];
      NextLineNo@1023 : Integer;
      ZeroQty@1026 : Boolean;
      ZeroQtySave@1033 : Boolean;
      IncludeItemWithNoTransaction@1022 : Boolean;
      HideValidationDialog@1028 : Boolean;
      AdjustPosQty@1036 : Boolean;
      ItemTrackingSplit@1006 : Boolean;
      SkipDim@1037 : Boolean;
      ColumnDim@1027 : Text[250];
      PosQty@1031 : Decimal;
      NegQty@1032 : Decimal;
      ItemNotOnInventoryErr@1034 : TextConst 'DEU=Artikel nicht auf Lager.;ENU=Items Not on Inventory.;NLD=Artikelen niet in voorraad.;NOR=Varer ikke pÜ lager.';
      GLSetup@1100485002 : Record 98;
      UsedItemDim1@1100485000 : Boolean;
      UsedItemDim2@1100485001 : Boolean;

    [External]
    PROCEDURE SetItemJnlLine@1(VAR NewItemJnlLine@1000 : Record 83);
    BEGIN
      ItemJnlLine := NewItemJnlLine;
    END;

    LOCAL PROCEDURE ValidatePostingDate@4();
    BEGIN
      ItemJnlBatch.GET(ItemJnlLine."Journal Template Name",ItemJnlLine."Journal Batch Name");
      IF ItemJnlBatch."No. Series" = '' THEN
        NextDocNo := ''
      ELSE BEGIN
        NextDocNo := NoSeriesMgt.GetNextNo(ItemJnlBatch."No. Series",PostingDate,FALSE);
        CLEAR(NoSeriesMgt);
      END;
    END;

    LOCAL PROCEDURE InsertItemJnlLine@6(ItemNo@1000 : Code[20];VariantCode2@1001 : Code[10];DimEntryNo2@1002 : Integer;BinCode2@1004 : Code[20];Quantity2@1005 : Decimal;PhysInvQuantity@1014 : Decimal);
    VAR
      ItemLedgEntry@1007 : Record 32;
      ReservEntry@1012 : Record 337;
      WhseEntry@1009 : Record 7312;
      WhseEntry2@1015 : Record 7312;
      Bin@1008 : Record 7354;
      DimValue@1013 : Record 349;
      CreateReservEntry@1010 : Codeunit 99000830;
      DimMgt@1016 : Codeunit 408;
      EntryType@1003 : 'Negative Adjmt.,Positive Adjmt.';
      NoBinExist@1011 : Boolean;
      OrderLineNo@1017 : Integer;
    BEGIN
      OnBeforeFunctionInsertItemJnlLine(ItemNo,VariantCode2,DimEntryNo2,BinCode2,Quantity2,PhysInvQuantity);

      WITH ItemJnlLine DO BEGIN
        IF NextLineNo = 0 THEN BEGIN
          LOCKTABLE;
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          IF FINDLAST THEN
            NextLineNo := "Line No.";

          SourceCodeSetup.GET;
        END;
        NextLineNo := NextLineNo + 10000;

        IF (Quantity2 <> 0) OR ZeroQty THEN BEGIN
          IF (Quantity2 = 0) AND Location."Bin Mandatory" AND NOT Location."Directed Put-away and Pick"
          THEN
            IF NOT Bin.GET(Location.Code,BinCode2) THEN
              NoBinExist := TRUE;

          INIT;
          "Line No." := NextLineNo;
          VALIDATE("Posting Date",PostingDate);
          IF PhysInvQuantity >= Quantity2 THEN
            VALIDATE("Entry Type","Entry Type"::"Positive Adjmt.")
          ELSE
            VALIDATE("Entry Type","Entry Type"::"Negative Adjmt.");
          VALIDATE("Document No.",NextDocNo);
          VALIDATE("Item No.",ItemNo);
          VALIDATE("Variant Code",VariantCode2);
          VALIDATE("Location Code",Location.Code);
          IF NOT NoBinExist THEN
            VALIDATE("Bin Code",BinCode2)
          ELSE
            VALIDATE("Bin Code",'');
          VALIDATE("Source Code",SourceCodeSetup."Phys. Inventory Journal");
          "Qty. (Phys. Inventory)" := PhysInvQuantity;
          "Phys. Inventory" := TRUE;
          VALIDATE("Qty. (Calculated)",Quantity2);
          VALIDATE("Unit Amount"); //**4PS.n (db, 24-11-04: no difference with manual input)
          "Posting No. Series" := ItemJnlBatch."Posting No. Series";
          "Reason Code" := ItemJnlBatch."Reason Code";

          "Phys Invt Counting Period Code" := PhysInvtCountCode;
          "Phys Invt Counting Period Type" := CycleSourceType;

          IF Location."Bin Mandatory" THEN
            "Dimension Set ID" := 0;
          IF NOT UsedItemDim1 THEN  //**4PS.n
            "Shortcut Dimension 1 Code" := '';
          IF NOT UsedItemDim2 THEN  //**4PS.n
            "Shortcut Dimension 2 Code" := '';

          ItemLedgEntry.RESET;
          ItemLedgEntry.SETCURRENTKEY("Item No.");
          ItemLedgEntry.SETRANGE("Item No.",ItemNo);
          IF ItemLedgEntry.FINDLAST THEN
            "Last Item Ledger Entry No." := ItemLedgEntry."Entry No."
          ELSE
            "Last Item Ledger Entry No." := 0;

          INSERT(TRUE);
          OnAfterInsertItemJnlLine(ItemJnlLine);

          IF Location.Code <> '' THEN
            IF Location."Directed Put-away and Pick" THEN BEGIN
              WhseEntry.SETCURRENTKEY(
                "Item No.","Bin Code","Location Code","Variant Code","Unit of Measure Code",
                "Lot No.","Serial No.","Entry Type");
              WhseEntry.SETRANGE("Item No.","Item No.");
              WhseEntry.SETRANGE("Bin Code",Location."Adjustment Bin Code");
              WhseEntry.SETRANGE("Location Code","Location Code");
              WhseEntry.SETRANGE("Variant Code","Variant Code");
              IF "Entry Type" = "Entry Type"::"Positive Adjmt." THEN
                EntryType := EntryType::"Negative Adjmt.";
              IF "Entry Type" = "Entry Type"::"Negative Adjmt." THEN
                EntryType := EntryType::"Positive Adjmt.";
              WhseEntry.SETRANGE("Entry Type",EntryType);
              IF WhseEntry.FIND('-') THEN
                REPEAT
                  WhseEntry.SETRANGE("Lot No.",WhseEntry."Lot No.");
                  WhseEntry.SETRANGE("Serial No.",WhseEntry."Serial No.");
                  WhseEntry.CALCSUMS("Qty. (Base)");

                  WhseEntry2.SETCURRENTKEY(
                    "Item No.","Bin Code","Location Code","Variant Code","Unit of Measure Code",
                    "Lot No.","Serial No.","Entry Type");
                  WhseEntry2.COPYFILTERS(WhseEntry);
                  CASE EntryType OF
                    EntryType::"Positive Adjmt.":
                      WhseEntry2.SETRANGE("Entry Type",WhseEntry2."Entry Type"::"Negative Adjmt.");
                    EntryType::"Negative Adjmt.":
                      WhseEntry2.SETRANGE("Entry Type",WhseEntry2."Entry Type"::"Positive Adjmt.");
                  END;
                  WhseEntry2.CALCSUMS("Qty. (Base)");
                  IF ABS(WhseEntry2."Qty. (Base)") > ABS(WhseEntry."Qty. (Base)") THEN
                    WhseEntry."Qty. (Base)" := 0
                  ELSE
                    WhseEntry."Qty. (Base)" := WhseEntry."Qty. (Base)" + WhseEntry2."Qty. (Base)";

                  IF WhseEntry."Qty. (Base)" <> 0 THEN BEGIN
                    IF "Order Type" = "Order Type"::Production THEN
                      OrderLineNo := "Order Line No.";
                    CreateReservEntry.CreateReservEntryFor(
                      DATABASE::"Item Journal Line",
                      "Entry Type",
                      "Journal Template Name",
                      "Journal Batch Name",
                      OrderLineNo,
                      "Line No.",
                      "Qty. per Unit of Measure",
                      ABS(WhseEntry.Quantity),
                      ABS(WhseEntry."Qty. (Base)"),
                      WhseEntry."Serial No.",
                      WhseEntry."Lot No.");
                    IF WhseEntry."Qty. (Base)" < 0 THEN             // only Date on positive adjustments
                      CreateReservEntry.SetDates(WhseEntry."Warranty Date",WhseEntry."Expiration Date");
                    CreateReservEntry.CreateEntry(
                      "Item No.",
                      "Variant Code",
                      "Location Code",
                      Description,
                      0D,
                      0D,
                      0,
                      ReservEntry."Reservation Status"::Prospect);
                  END;
                  WhseEntry.FIND('+');
                  WhseEntry.SETRANGE("Lot No.");
                  WhseEntry.SETRANGE("Serial No.");
                UNTIL WhseEntry.NEXT = 0;
            END;

          IF ColumnDim = '' THEN
            DimEntryNo2 := CreateDimFromItemDefault;

          IF DimBufMgt.GetDimensions(DimEntryNo2,TempDimBufOut) THEN BEGIN
            TempDimSetEntry.RESET;
            TempDimSetEntry.DELETEALL;
            IF TempDimBufOut.FIND('-') THEN BEGIN
              REPEAT
                DimValue.GET(TempDimBufOut."Dimension Code",TempDimBufOut."Dimension Value Code");
                TempDimSetEntry."Dimension Code" := TempDimBufOut."Dimension Code";
                TempDimSetEntry."Dimension Value Code" := TempDimBufOut."Dimension Value Code";
                TempDimSetEntry."Dimension Value ID" := DimValue."Dimension Value ID";
                IF TempDimSetEntry.INSERT THEN;
                "Dimension Set ID" := DimMgt.GetDimensionSetID(TempDimSetEntry);
                DimMgt.UpdateGlobalDimFromDimSetID("Dimension Set ID",
                  "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
                MODIFY;
              UNTIL TempDimBufOut.NEXT = 0;
              TempDimBufOut.DELETEALL;
            END;
          END;
        END;
      END;
      OnAfterFunctionInsertItemJnlLine(ItemNo,VariantCode2,DimEntryNo2,BinCode2,Quantity2,PhysInvQuantity,ItemJnlLine);
    END;

    LOCAL PROCEDURE InsertQuantityOnHandBuffer@10(ItemNo@1000 : Code[20];LocationCode@1001 : Code[10];VariantCode@1002 : Code[10]);
    BEGIN
      WITH QuantityOnHandBuffer DO BEGIN
        RESET;
        SETRANGE("Item No.",ItemNo);
        SETRANGE("Location Code",LocationCode);
        SETRANGE("Variant Code",VariantCode);
        IF NOT FINDFIRST THEN BEGIN
          RESET;
          INIT;
          "Item No." := ItemNo;
          "Location Code" := LocationCode;
          "Variant Code" := VariantCode;
          "Bin Code" := '';
          "Dimension Entry No." := 0;
          INSERT(TRUE);
        END;
      END;
    END;

    [External]
    PROCEDURE InitializeRequest@2(NewPostingDate@1000 : Date;DocNo@1001 : Code[20];ItemsNotOnInvt@1002 : Boolean;InclItemWithNoTrans@1003 : Boolean);
    BEGIN
      PostingDate := NewPostingDate;
      NextDocNo := DocNo;
      ZeroQty := ItemsNotOnInvt;
      IncludeItemWithNoTransaction := InclItemWithNoTrans AND ZeroQty;
      IF NOT SkipDim THEN
        ColumnDim := DimSelectionBuf.GetDimSelectionText(3,REPORT::"Calculate Inventory",'');
    END;

    LOCAL PROCEDURE TransferDim@3(DimSetID@1000 : Integer);
    BEGIN
      DimSetEntry.SETRANGE("Dimension Set ID",DimSetID);
      IF DimSetEntry.FIND('-') THEN BEGIN
        REPEAT
          IF TempSelectedDim.GET(
               USERID,3,REPORT::"Calculate Inventory",'',DimSetEntry."Dimension Code")
          THEN
            InsertDim(DATABASE::"Item Ledger Entry",DimSetID,DimSetEntry."Dimension Code",DimSetEntry."Dimension Value Code");
        UNTIL DimSetEntry.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE CalcWhseQty@5(AdjmtBin@1002 : Code[20];VAR PosQuantity@1003 : Decimal;VAR NegQuantity@1004 : Decimal);
    VAR
      WhseEntry@1000 : Record 7312;
      WhseEntry2@1008 : Record 7312;
      ItemTrackingMgt@1005 : Codeunit 6500;
      WhseQuantity@1001 : Decimal;
      WhseSNRequired@1006 : Boolean;
      WhseLNRequired@1007 : Boolean;
      NoWhseEntry@1010 : Boolean;
      NoWhseEntry2@1009 : Boolean;
    BEGIN
      AdjustPosQty := FALSE;
      WITH QuantityOnHandBuffer DO BEGIN
        ItemTrackingMgt.CheckWhseItemTrkgSetup("Item No.",WhseSNRequired,WhseLNRequired,FALSE);
        ItemTrackingSplit := WhseSNRequired OR WhseLNRequired;
        WhseEntry.SETCURRENTKEY(
          "Item No.","Bin Code","Location Code","Variant Code","Unit of Measure Code",
          "Lot No.","Serial No.","Entry Type");

        WhseEntry.SETRANGE("Item No.","Item No.");
        WhseEntry.SETRANGE("Location Code","Location Code");
        WhseEntry.SETRANGE("Variant Code","Variant Code");
        WhseEntry.CALCSUMS("Qty. (Base)");
        WhseQuantity := WhseEntry."Qty. (Base)";
        WhseEntry.SETRANGE("Bin Code",AdjmtBin);

        IF WhseSNRequired THEN BEGIN
          WhseEntry.SETRANGE("Entry Type",WhseEntry."Entry Type"::"Positive Adjmt.");
          WhseEntry.CALCSUMS("Qty. (Base)");
          PosQuantity := WhseQuantity - WhseEntry."Qty. (Base)";
          WhseEntry.SETRANGE("Entry Type",WhseEntry."Entry Type"::"Negative Adjmt.");
          WhseEntry.CALCSUMS("Qty. (Base)");
          NegQuantity := WhseQuantity - WhseEntry."Qty. (Base)";
          WhseEntry.SETRANGE("Entry Type",WhseEntry."Entry Type"::Movement);
          WhseEntry.CALCSUMS("Qty. (Base)");
          IF WhseEntry."Qty. (Base)" <> 0 THEN BEGIN
            IF WhseEntry."Qty. (Base)" > 0 THEN
              PosQuantity := PosQuantity + WhseQuantity - WhseEntry."Qty. (Base)"
            ELSE
              NegQuantity := NegQuantity - WhseQuantity - WhseEntry."Qty. (Base)";
          END;

          WhseEntry.SETRANGE("Entry Type",WhseEntry."Entry Type"::"Positive Adjmt.");
          IF WhseEntry.FIND('-') THEN BEGIN
            REPEAT
              WhseEntry.SETRANGE("Serial No.",WhseEntry."Serial No.");

              WhseEntry2.RESET;
              WhseEntry2.SETCURRENTKEY(
                "Item No.","Bin Code","Location Code","Variant Code",
                "Unit of Measure Code","Lot No.","Serial No.","Entry Type");

              WhseEntry2.COPYFILTERS(WhseEntry);
              WhseEntry2.SETRANGE("Entry Type",WhseEntry2."Entry Type"::"Negative Adjmt.");
              WhseEntry2.SETRANGE("Serial No.",WhseEntry."Serial No.");
              IF WhseEntry2.FIND('-') THEN
                REPEAT
                  PosQuantity := PosQuantity + 1;
                  NegQuantity := NegQuantity - 1;
                  NoWhseEntry := WhseEntry.NEXT = 0;
                  NoWhseEntry2 := WhseEntry2.NEXT = 0;
                UNTIL NoWhseEntry2 OR NoWhseEntry
              ELSE
                AdjustPosQty := TRUE;

              IF NOT NoWhseEntry AND NoWhseEntry2 THEN
                AdjustPosQty := TRUE;

              WhseEntry.FIND('+');
              WhseEntry.SETRANGE("Serial No.");
            UNTIL WhseEntry.NEXT = 0;
          END;
        END ELSE BEGIN
          IF WhseEntry.FIND('-') THEN
            REPEAT
              WhseEntry.SETRANGE("Lot No.",WhseEntry."Lot No.");
              WhseEntry.CALCSUMS("Qty. (Base)");
              IF WhseEntry."Qty. (Base)" <> 0 THEN BEGIN
                IF WhseEntry."Qty. (Base)" > 0 THEN
                  NegQuantity := NegQuantity - WhseEntry."Qty. (Base)"
                ELSE
                  PosQuantity := PosQuantity + WhseEntry."Qty. (Base)";
              END;
              WhseEntry.FIND('+');
              WhseEntry.SETRANGE("Lot No.");
            UNTIL WhseEntry.NEXT = 0;
          IF PosQuantity <> WhseQuantity THEN
            PosQuantity := WhseQuantity - PosQuantity;
          IF NegQuantity <> -WhseQuantity THEN
            NegQuantity := WhseQuantity + NegQuantity;
        END;
      END;
    END;

    [External]
    PROCEDURE SetHideValidationDialog@7380(NewHideValidationDialog@1000 : Boolean);
    BEGIN
      HideValidationDialog := NewHideValidationDialog;
    END;

    [External]
    PROCEDURE InitializePhysInvtCount@7381(PhysInvtCountCode2@1000 : Code[10];CountSourceType2@1001 : ' ,Item,SKU');
    BEGIN
      PhysInvtCountCode := PhysInvtCountCode2;
      CycleSourceType := CountSourceType2;
    END;

    LOCAL PROCEDURE SkipCycleSKU@7382(LocationCode@1000 : Code[10];ItemNo@1002 : Code[20];VariantCode@1003 : Code[10]) : Boolean;
    VAR
      SKU@1001 : Record 5700;
    BEGIN
      IF CycleSourceType = CycleSourceType::Item THEN
        IF SKU.READPERMISSION THEN
          IF SKU.GET(LocationCode,ItemNo,VariantCode) THEN
            EXIT(TRUE);
      EXIT(FALSE);
    END;

    LOCAL PROCEDURE GetLocation@11(LocationCode@1000 : Code[10]) : Boolean;
    BEGIN
      IF LocationCode = '' THEN BEGIN
        CLEAR(Location);
        EXIT(TRUE);
      END;

      IF Location.Code <> LocationCode THEN
        IF NOT Location.GET(LocationCode) THEN
          EXIT(FALSE);

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE UpdateBuffer@12(BinCode@1002 : Code[20];NewQuantity@1001 : Decimal);
    VAR
      DimEntryNo@1000 : Integer;
    BEGIN
      WITH QuantityOnHandBuffer DO BEGIN
        IF NOT HasNewQuantity(NewQuantity) THEN
          EXIT;
        IF BinCode = '' THEN BEGIN
          IF ColumnDim <> '' THEN
            TempDimBufIn.SETRANGE("Entry No.","Item Ledger Entry"."Dimension Set ID");
          DimEntryNo := DimBufMgt.FindDimensions(TempDimBufIn);
          IF DimEntryNo = 0 THEN
            DimEntryNo := DimBufMgt.InsertDimensions(TempDimBufIn);
        END;
        IF RetrieveBuffer(BinCode,DimEntryNo) THEN BEGIN
          Quantity := Quantity + NewQuantity;
          MODIFY;
        END ELSE BEGIN
          Quantity := NewQuantity;
          INSERT;
        END;
      END;
    END;

    LOCAL PROCEDURE RetrieveBuffer@7(BinCode@1000 : Code[20];DimEntryNo@1001 : Integer) : Boolean;
    BEGIN
      WITH QuantityOnHandBuffer DO BEGIN
        RESET;
        "Item No." := "Item Ledger Entry"."Item No.";
        "Variant Code" := "Item Ledger Entry"."Variant Code";
        "Location Code" := "Item Ledger Entry"."Location Code";
        "Dimension Entry No." := DimEntryNo;
        "Bin Code" := BinCode;
        EXIT(FIND);
      END;
    END;

    LOCAL PROCEDURE HasNewQuantity@22(NewQuantity@1000 : Decimal) : Boolean;
    BEGIN
      EXIT((NewQuantity <> 0) OR ZeroQty);
    END;

    LOCAL PROCEDURE ItemBinLocationIsCalculated@18(BinCode@1000 : Code[20]) : Boolean;
    BEGIN
      WITH QuantityOnHandBuffer DO BEGIN
        RESET;
        SETRANGE("Item No.","Item Ledger Entry"."Item No.");
        SETRANGE("Variant Code","Item Ledger Entry"."Variant Code");
        SETRANGE("Location Code","Item Ledger Entry"."Location Code");
        SETRANGE("Bin Code",BinCode);
        EXIT(FIND('-'));
      END;
    END;

    [External]
    PROCEDURE SetSkipDim@8(NewSkipDim@1000 : Boolean);
    BEGIN
      SkipDim := NewSkipDim;
    END;

    LOCAL PROCEDURE UpdateQuantityOnHandBuffer@13(ItemNo@1000 : Code[20]);
    VAR
      Location@1001 : Record 14;
      ItemVariant@1002 : Record 5401;
    BEGIN
      ItemVariant.SETRANGE("Item No.",Item."No.");
      Item.COPYFILTER("Variant Filter",ItemVariant.Code);
      Item.COPYFILTER("Location Filter",Location.Code);
      Location.SETRANGE("Use As In-Transit",FALSE);
      IF (Item.GETFILTER("Location Filter") <> '') AND Location.FINDSET THEN
        REPEAT
          IF (Item.GETFILTER("Variant Filter") <> '') AND ItemVariant.FINDSET THEN
            REPEAT
              InsertQuantityOnHandBuffer(ItemNo,Location.Code,ItemVariant.Code);
            UNTIL ItemVariant.NEXT = 0
          ELSE
            InsertQuantityOnHandBuffer(ItemNo,Location.Code,'');
        UNTIL Location.NEXT = 0
      ELSE
        IF (Item.GETFILTER("Variant Filter") <> '') AND ItemVariant.FINDSET THEN
          REPEAT
            InsertQuantityOnHandBuffer(ItemNo,'',ItemVariant.Code);
          UNTIL ItemVariant.NEXT = 0
        ELSE
          InsertQuantityOnHandBuffer(ItemNo,'','');
    END;

    LOCAL PROCEDURE CalcPhysInvQtyAndInsertItemJnlLine@9();
    BEGIN
      WITH QuantityOnHandBuffer DO BEGIN
        RESET;
        IF FINDSET THEN BEGIN
          REPEAT
            PosQty := 0;
            NegQty := 0;

            GetLocation("Location Code");
            IF Location."Directed Put-away and Pick" THEN
              CalcWhseQty(Location."Adjustment Bin Code",PosQty,NegQty);

            IF (NegQty - Quantity <> Quantity - PosQty) OR ItemTrackingSplit THEN BEGIN
              IF PosQty = Quantity THEN
                PosQty := 0;
              IF (PosQty <> 0) OR AdjustPosQty THEN
                InsertItemJnlLine(
                  "Item No.","Variant Code","Dimension Entry No.",
                  "Bin Code",Quantity,PosQty);

              IF NegQty = Quantity THEN
                NegQty := 0;
              IF NegQty <> 0 THEN BEGIN
                IF ((PosQty <> 0) OR AdjustPosQty) AND NOT ItemTrackingSplit THEN BEGIN
                  NegQty := NegQty - Quantity;
                  Quantity := 0;
                  ZeroQty := TRUE;
                END;
                IF NegQty = -Quantity THEN BEGIN
                  NegQty := 0;
                  AdjustPosQty := TRUE;
                END;
                InsertItemJnlLine(
                  "Item No.","Variant Code","Dimension Entry No.",
                  "Bin Code",Quantity,NegQty);

                ZeroQty := ZeroQtySave;
              END;
            END ELSE BEGIN
              PosQty := 0;
              NegQty := 0;
            END;

            IF (PosQty = 0) AND (NegQty = 0) AND NOT AdjustPosQty THEN
              InsertItemJnlLine(
                "Item No.","Variant Code","Dimension Entry No.",
                "Bin Code",Quantity,Quantity);
          UNTIL NEXT = 0;
          DELETEALL;
        END;
      END;
    END;

    LOCAL PROCEDURE CreateDimFromItemDefault@17() DimEntryNo : Integer;
    VAR
      DefaultDimension@1000 : Record 352;
    BEGIN
      WITH DefaultDimension DO BEGIN
        SETRANGE("No.",QuantityOnHandBuffer."Item No.");
        SETRANGE("Table ID",DATABASE::Item);
        SETFILTER("Dimension Value Code",'<>%1','');
        IF FINDSET THEN
          REPEAT
            InsertDim(DATABASE::Item,0,"Dimension Code","Dimension Value Code");
          UNTIL NEXT = 0;
      END;

      DimEntryNo := DimBufMgt.InsertDimensions(TempDimBufIn);
      TempDimBufIn.SETRANGE("Table ID",DATABASE::Item);
      TempDimBufIn.DELETEALL;
    END;

    LOCAL PROCEDURE InsertDim@56(TableID@1000 : Integer;EntryNo@1001 : Integer;DimCode@1002 : Code[20];DimValueCode@1003 : Code[20]);
    BEGIN
      WITH TempDimBufIn DO BEGIN
        INIT;
        "Table ID" := TableID;
        "Entry No." := EntryNo;
        "Dimension Code" := DimCode;
        "Dimension Value Code" := DimValueCode;
        IF INSERT THEN;
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInsertItemJnlLine@1001(VAR ItemJournalLine@1000 : Record 83);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterItemOnPreDataItem@16(VAR Item@1000 : Record 27;ZeroQty@1001 : Boolean;IncludeItemWithNoTransaction@1002 : Boolean);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeFunctionInsertItemJnlLine@14(ItemNo@1006 : Code[20];VariantCode2@1005 : Code[10];DimEntryNo2@1004 : Integer;BinCode2@1003 : Code[20];Quantity2@1002 : Decimal;PhysInvQuantity@1001 : Decimal);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterFunctionInsertItemJnlLine@15(ItemNo@1006 : Code[20];VariantCode2@1005 : Code[10];DimEntryNo2@1004 : Integer;BinCode2@1003 : Code[20];Quantity2@1002 : Decimal;PhysInvQuantity@1001 : Decimal;VAR ItemJournalLine@1000 : Record 83);
    BEGIN
    END;

    PROCEDURE InitializeRequest2@1100528000(NewPostingDate@1100528003 : Date;DocNo@1100528002 : Code[20];ItemsNotOnInvt@1100528001 : Boolean;NewIncludeItemWithNoTransaction@1100528000 : Boolean);
    BEGIN
      //**4PS
      IncludeItemWithNoTransaction := NewIncludeItemWithNoTransaction;
      InitializeRequest(NewPostingDate, DocNo, ItemsNotOnInvt, IncludeItemWithNoTransaction);
    END;

    PROCEDURE SetPostingDate@1000000000(VAR NewPostingDateIn@1000000000 : Date);
    BEGIN
      //191021 ORANGO.EE
      PostingDate := NewPostingDateIn;
      ValidatePostingDate;
    END;

    BEGIN
    {
      4PS
      14-06-2001, D.Borkent - extra filter on date because standard version always takes current inventory.
                            - error by selecting Cost Object/Cost Center because the right index(fields) are missing.

      191021 ORANGO.EE - New function
    }
    END.
  }
  RDLDATA
  {
  }
}

