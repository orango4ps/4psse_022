OBJECT Report 11020213 Batch Approve Purch. Invoices
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Stapel-Genehm. Einkaufsrechn.;
               ENU=Batch Approve Purchase Invoices;
               NLD=Batch-goedkeuren inkoopfacturen;
               NOR=Batchgodkjenn innkjõpsfakturaer;
               SVE=BatchgodkÑnn inkîpsfakturor];
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  FilterTxt@1100485000 : Text[250];
                  SelMinCde@1100485001 : Code[20];
                  SelMaxCde@1100485002 : Code[20];
                BEGIN
                  FilterTxt := "Purch. Inv. Header".GETFILTER("Journal Template");
                  IF FilterTxt <> '' THEN BEGIN
                    SelMinCde := "Purch. Inv. Header".GETRANGEMIN("Journal Template");
                    SelMaxCde := "Purch. Inv. Header".GETRANGEMAX("Journal Template");
                    IF SelMinCde <>  SelMaxCde THEN
                      ERROR(Text001);
                  END ELSE BEGIN
                    ERROR(Text001);
                  END;

                  TmpReportRec.RESET;
                  TmpReportRec.DELETEALL;
                  TmpGLRegRec.RESET;
                  TmpGLRegRec.DELETEALL;
                END;

  }
  DATASET
  {
    { 3733;    ;DataItem;                    ;
               DataItemTable=Table122;
               DataItemTableView=SORTING(No.)
                                 WHERE(Invoice Approved=CONST(No));
               OnPreDataItem=BEGIN
                               CounterTotal := COUNT;
                               Window.OPEN(Text002);
                             END;

               OnAfterGetRecord=VAR
                                  lCrMHdrRecDummy@1100485000 : Record 124;
                                BEGIN
                                  Counter := Counter + 1;
                                  Window.UPDATE(1,"No.");
                                  Window.UPDATE(2,ROUND(Counter / CounterTotal * 10000,1));

                                  PurchApprovalLineRec.SETRANGE("Journal Template Name","Journal Template");
                                  PurchApprovalLineRec.SETRANGE("Document Type",PurchApprovalLineRec."Document Type"::"Purchase Invoice");
                                  PurchApprovalLineRec.SETRANGE("Document No.", "No.");
                                  IF PurchApprovalLineRec.FINDFIRST THEN BEGIN
                                    CounterToPost := CounterToPost + 1;
                                    CLEAR(PostInvApproval);
                                    PostInvApproval.SetBatch(PurchApprovalLineRec."Document Type", gPrint, "Purch. Inv. Header", lCrMHdrRecDummy);
                                    IF PostInvApproval.RUN THEN BEGIN
                                      CounterPosted := CounterPosted + PostInvApproval.GetInvoiceCounterOk;
                                      IF gPrint THEN
                                        FillTmpTablesBatchrunPrint();
                                      IF MARKEDONLY THEN
                                        MARK(FALSE);
                                    END;
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;
                                IF gPrint THEN
                                  PrintApprovalBatch();
                                MESSAGE(Text003,CounterTotal,CounterToPost,CounterPosted);
                              END;

               ReqFilterFields=No.,Journal Template }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'DEU=WÑhlen Sie eine RechnungsÅberprÅfungs-Buch.-Blattvorlage aus;ENU=Select one Invoice Approval Journal Template;NLD=Selecteer ÇÇn Factuur Controle Dagboeksjabloon;NOR=Velg en journalmal for fakturagodkjenning;SVE=VÑlj en journalmall fîr fakturagodkÑnnande';
      PurchApprovalLineRec@1003 : Record 11012032;
      TmpGLRegRec@1100525001 : TEMPORARY Record 45;
      TmpReportRec@1100525002 : TEMPORARY Record 78;
      PostInvApproval@1006 : Codeunit 11012027;
      Window@1100485006 : Dialog;
      gPrint@1100485000 : Boolean;
      CounterTotal@1100485003 : Integer;
      Counter@1100485002 : Integer;
      CounterToPost@1100525000 : Integer;
      CounterPosted@1100485001 : Integer;
      Text002@1100485004 : TextConst 'DEU=Rechnungen buchen  #1########## @2@@@@@@@@@@@@@;ENU=Posting invoices   #1########## @2@@@@@@@@@@@@@;NLD=Boeken facturen    #1########## @2@@@@@@@@@@@@@;NOR=Bokfõrer fakturaer #1########## @2@@@@@@@@@@@@@;SVE=Bokfîr fakturor    #1########## @2@@@@@@@@@@@@@';
      Text003@1100485005 : TextConst 'DEU=Von den %1 Rechnungen gibt es %2 mit eingegebenen Zeilen und davon sind %3 gebucht worden.;ENU=Of %1 invoices %2 are provided with Lines. %3 have now been posted.;NLD=Van de %1 facturen zijn er %2 met regels gevuld en daarvan zijn er %3 geboekt.;NOR=Av %1 fakturaer er %2 gitt rader. %3 er nÜ bokfõrt.;SVE=Av %1 fakturor tillhandahÜlls %2 med rader. %3 har nu bokfîrts.';

    PROCEDURE SetPrint@1100485000(iPrint@1100485000 : Boolean);
    BEGIN
      gPrint := iPrint;
    END;

    PROCEDURE FillTmpTablesBatchrunPrint@1100525002();
    VAR
      GLRegNo@1100525001 : Integer;
      PostingReportID@1100525000 : Integer;
    BEGIN
      IF PostInvApproval.GetGLRegNoAndReportID(GLRegNo, PostingReportID) THEN BEGIN
        IF NOT TmpGLRegRec.GET(GLRegNo) THEN BEGIN
          TmpGLRegRec.INIT;
          TmpGLRegRec."No." := GLRegNo;
          TmpGLRegRec."From VAT Entry No." := PostingReportID;
          TmpGLRegRec.INSERT;
          IF NOT TmpReportRec.GET('', PostingReportID) THEN BEGIN
            TmpReportRec.INIT;
            TmpReportRec."User ID" := '';
            TmpReportRec."Report ID" := PostingReportID;
            TmpReportRec.INSERT;
          END;
        END;
      END;
    END;

    PROCEDURE PrintApprovalBatch@1100525000();
    VAR
      GLRegRec@1100525000 : Record 45;
      StartNo@1100525001 : Integer;
      EndNo@1100525002 : Integer;
    BEGIN
      TmpReportRec.RESET;
      TmpReportRec.SETRANGE("User ID", '');
      IF TmpReportRec.FINDSET THEN BEGIN
        REPEAT
          StartNo := 0;
          EndNo := 0;
          GLRegRec.RESET;
          TmpGLRegRec.RESET;
          TmpGLRegRec.SETRANGE("From VAT Entry No.", TmpReportRec."Report ID");
          IF TmpGLRegRec.FINDSET THEN BEGIN
            REPEAT
              IF GLRegRec.GET(TmpGLRegRec."No.") THEN BEGIN
                GLRegRec.MARK(TRUE);
                IF StartNo = 0 THEN
                  StartNo := GLRegRec."No.";
                EndNo := GLRegRec."No.";
              END;
            UNTIL TmpGLRegRec.NEXT = 0;
            GLRegRec.MARKEDONLY(TRUE);
            GLRegRec.SETRANGE("No.", StartNo, EndNo);
            IF GLRegRec.FINDFIRST THEN
              REPORT.RUN(TmpReportRec."Report ID", FALSE, FALSE, GLRegRec);
            TmpGLRegRec.DELETEALL;
          END;
        UNTIL TmpReportRec.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

