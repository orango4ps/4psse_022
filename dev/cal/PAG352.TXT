OBJECT Page 352 Vendor Purchase Lines
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    Editable=No;
    CaptionML=[ENU=Lines;
               NOR=Linjer;
               SVE=Rader];
    LinksAllowed=No;
    SourceTable=Table2000000007;
    PageType=ListPart;
    OnOpenPage=BEGIN
                 RESET;
               END;

    OnFindRecord=BEGIN
                   EXIT(PeriodFormMgt.FindDate(Which,Rec,PeriodType));
                 END;

    OnNextRecord=BEGIN
                   EXIT(PeriodFormMgt.NextDate(Steps,Rec,PeriodType));
                 END;

    OnAfterGetRecord=BEGIN
                       SetDateFilter;
                       Vend.CALCFIELDS("Balance Due (LCY)","Purchases (LCY)");
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                Editable=FALSE;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                CaptionML=[ENU=Period Start;
                           NOR=Periodens start;
                           SVE=Periodstart];
                ToolTipML=[ENU=Specifies purchase statistics for each vendor for a period of time, starting on the date that you specify.;
                           NOR=Angir kj›psstatistikk for hver leverand›r i en tidsperiode, som starter p† datoen du angir.;
                           SVE=Anger ink”psstatistik f”r varje leverant”r f”r en tidsperiod, som b”rjar vid det datum du anger.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Period Start" }

    { 6   ;2   ;Field     ;
                CaptionML=[ENU=Period Name;
                           NOR=Periodenavn;
                           SVE=Periodnamn];
                ToolTipML=[ENU=Specifies the name of the period that you want to view.;
                           NOR=Angir navnet p† perioden du vil vise.;
                           SVE=Anger namnet p† tidsperioden som du vill visa.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Period Name" }

    { 4   ;2   ;Field     ;
                Name=BalanceDueLCY;
                DrillDown=Yes;
                CaptionML=[ENU=Balance Due (LCY);
                           NOR=Forfalt saldo (NOK);
                           SVE=F”rfallet saldo (BVA)];
                ToolTipML=[ENU=Specifies the balance due to the vendor, in local currency.;
                           NOR=Angir forfalt saldo til leverand›ren i lokal valuta.;
                           SVE=Anger saldot som „r f”rfallet till leverant”ren, i lokal valuta.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Vend."Balance Due (LCY)";
                AutoFormatType=1;
                OnDrillDown=BEGIN
                              ShowVendEntriesDue;
                            END;
                             }

    { 8   ;2   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=Purchases (LCY);
                           NOR=Kj›p (NOK);
                           SVE=Ink”p (BVA)];
                ToolTipML=[ENU=Specifies the purchases, in local currency.;
                           NOR=Angir kj›pene i lokal valuta.;
                           SVE=Anger ink”pen, i lokal valuta.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Vend."Purchases (LCY)";
                AutoFormatType=1;
                OnDrillDown=BEGIN
                              ShowVendEntries;
                            END;
                             }

  }
  CODE
  {
    VAR
      Vend@1000 : Record 23;
      VendLedgEntry@1001 : Record 25;
      PeriodFormMgt@1002 : Codeunit 359;
      PeriodType@1003 : 'Day,Week,Month,Quarter,Year,Accounting Period';
      AmountType@1004 : 'Net Change,Balance at Date';

    [External]
    PROCEDURE Set@1(VAR NewVend@1000 : Record 23;NewPeriodType@1001 : Integer;NewAmountType@1002 : 'Net Change,Balance at Date');
    BEGIN
      Vend.COPY(NewVend);
      PeriodType := NewPeriodType;
      AmountType := NewAmountType;
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE ShowVendEntries@2();
    BEGIN
      SetDateFilter;
      VendLedgEntry.RESET;
      VendLedgEntry.SETCURRENTKEY("Vendor No.","Posting Date");
      VendLedgEntry.SETRANGE("Vendor No.",Vend."No.");
      VendLedgEntry.SETFILTER("Posting Date",Vend.GETFILTER("Date Filter"));
      VendLedgEntry.SETFILTER("Global Dimension 1 Code",Vend.GETFILTER("Global Dimension 1 Filter"));
      VendLedgEntry.SETFILTER("Global Dimension 2 Code",Vend.GETFILTER("Global Dimension 2 Filter"));
      PAGE.RUN(0,VendLedgEntry);
    END;

    LOCAL PROCEDURE ShowVendEntriesDue@3();
    VAR
      DtldVendLedgEntry@1000 : Record 380;
    BEGIN
      SetDateFilter;
      DtldVendLedgEntry.RESET;
      DtldVendLedgEntry.SETCURRENTKEY("Vendor No.","Initial Entry Due Date","Posting Date","Currency Code");
      DtldVendLedgEntry.SETRANGE("Vendor No.",Vend."No.");
      DtldVendLedgEntry.SETFILTER("Initial Entry Due Date",Vend.GETFILTER("Date Filter"));
      DtldVendLedgEntry.SETFILTER("Posting Date",'..%1',Vend.GETRANGEMAX("Date Filter"));
      DtldVendLedgEntry.SETFILTER("Initial Entry Global Dim. 1",Vend.GETFILTER("Global Dimension 1 Filter"));
      DtldVendLedgEntry.SETFILTER("Initial Entry Global Dim. 2",Vend.GETFILTER("Global Dimension 2 Filter"));
      PAGE.RUN(0,DtldVendLedgEntry)
    END;

    LOCAL PROCEDURE SetDateFilter@4();
    BEGIN
      IF AmountType = AmountType::"Net Change" THEN
        Vend.SETRANGE("Date Filter","Period Start","Period End")
      ELSE
        Vend.SETRANGE("Date Filter",0D,"Period End");
    END;

    BEGIN
    END.
  }
}

