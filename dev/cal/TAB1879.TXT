OBJECT Table 1879 VAT Assisted Setup Bus. Grp.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.05;
  }
  PROPERTIES
  {
    CaptionML=[ENU=VAT Assisted Setup Bus. Grp.;
               NOR=Forr.grp. for mva-assistert oppsett];
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 3   ;   ;Selected            ;Boolean       ;CaptionML=[ENU=Selected;
                                                              NOR=Valgt;
                                                              SVE=Vald] }
    { 4   ;   ;Default             ;Boolean       ;CaptionML=[ENU=Default;
                                                              NOR=Standard;
                                                              SVE=STANDARD] }
  }
  KEYS
  {
    {    ;Code,Default                            ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Code001Tok@1601 : TextConst '@@@=the same as values in Bus. posting group;ENU=DOMESTIC;NOR=INNENLANDS';
      Code002Tok@1602 : TextConst '@@@=the same as values in Bus. posting group;ENU=EU;NOR=EU';
      Code003Tok@1603 : TextConst '@@@=the same as values in Bus. posting group;ENU=EXPORT;NOR=EKSPORT;SVE=Exportera';
      Text001Txt@1607 : TextConst 'ENU=Domestic customers and vendors;NOR=Innenlandske kunder og leverand›rer';
      Text002Txt@1608 : TextConst 'ENU=Customers and vendors in EU;NOR=Kunder og leverand›rer i EU';
      Text003Txt@1609 : TextConst 'ENU=Other customers and vendors (not EU);NOR=Andre kunder og lev. (ikke EU)';

    [External]
    PROCEDURE PopulateVATBusGrp@1();
    BEGIN
      SETRANGE(Default,FALSE);
      DELETEALL;

      SETRANGE(Default,TRUE);
      IF NOT FINDSET THEN BEGIN
        InitWithStandardValues;
        FINDSET;
      END;

      REPEAT
        InsertBusPostingGrp(Code,Description,FALSE);
      UNTIL NEXT = 0;
    END;

    [External]
    PROCEDURE InsertBusPostingGrp@10(GrpCode@1002 : Code[20];GrpDesc@1003 : Text[100];IsDefault@1000 : Boolean);
    VAR
      VATAssistedSetupBusGrp@1001 : Record 1879;
    BEGIN
      VATAssistedSetupBusGrp.INIT;
      VATAssistedSetupBusGrp.Code := GrpCode;
      VATAssistedSetupBusGrp.Description := GrpDesc;
      VATAssistedSetupBusGrp.Selected := TRUE;
      VATAssistedSetupBusGrp.Default := IsDefault;
      VATAssistedSetupBusGrp.INSERT;
    END;

    [External]
    PROCEDURE ValidateVATBusGrp@2() : Boolean;
    BEGIN
      SETRANGE(Selected,TRUE);
      SETRANGE(Default,FALSE);
      EXIT(NOT ISEMPTY);
    END;

    [External]
    PROCEDURE CheckExistingCustomersAndVendorsWithVAT@3(VATBusPostingGroupCode@1002 : Code[20]) : Boolean;
    VAR
      Customer@1000 : Record 18;
      Vendor@1001 : Record 23;
    BEGIN
      Customer.SETRANGE("VAT Bus. Posting Group",VATBusPostingGroupCode);
      Vendor.SETRANGE("VAT Bus. Posting Group",VATBusPostingGroupCode);
      EXIT((NOT Vendor.ISEMPTY) OR (NOT Customer.ISEMPTY));
    END;

    LOCAL PROCEDURE InitWithStandardValues@4();
    BEGIN
      InsertBusPostingGrp(Code001Tok,Text001Txt,TRUE);
      InsertBusPostingGrp(Code002Tok,Text002Txt,TRUE);
      InsertBusPostingGrp(Code003Tok,Text003Txt,TRUE);
    END;

    BEGIN
    END.
  }
}

