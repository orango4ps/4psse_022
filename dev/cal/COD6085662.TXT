OBJECT Codeunit 6085662 Web Client Management
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW113.00.00.5.00.04;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            DownloadAndImportWebClientAddins;
          END;

  }
  CODE
  {

    PROCEDURE IsWebClient@1() : Boolean;
    VAR
      ActiveSession@6085573 : Record 2000000110;
    BEGIN
      IF ActiveSession.GET(SERVICEINSTANCEID,SESSIONID) THEN
        EXIT(ActiveSession."Client Type" = ActiveSession."Client Type"::"Web Client");
    END;

    LOCAL PROCEDURE DownloadAndImportWebClientAddins@6();
    VAR
      Addin@1000 : Record 2000000069;
      TempFile@1004 : TEMPORARY Record 6085608;
      COMgt@1001 : Codeunit 6192772;
      ReadStream@1005 : InStream;
      WriteStream@1006 : OutStream;
    BEGIN
      IF NOT Addin.WRITEPERMISSION THEN
        EXIT;
      Addin.SETFILTER("Add-in Name",'Continia.DocumentCapture.Dynamics.Nav.Web.*');
      Addin.SETRANGE(Category,Addin.Category::"JavaScript Control Add-in");
      IF Addin.FINDSET(TRUE,FALSE) THEN
        REPEAT
          TempFile.CreateEmpty('Addins.zip');
          COMgt.DownloadFile(GetAddInResourceUrl(Addin),TempFile.Name,TempFile,FALSE);
          TempFile.Data.CREATEINSTREAM(ReadStream);
          Addin.Resource.CREATEOUTSTREAM(WriteStream);
          COPYSTREAM(WriteStream,ReadStream);
          Addin.MODIFY;
        UNTIL Addin.NEXT = 0;
    END;

    PROCEDURE DownloadPDFDocument@6085573(Document@6085573 : Record 6085590);
    VAR
      FileExtension@6085577 : Text[10];
      ToFileName@6085576 : Text[1024];
      FromFileName@6085575 : Text[1024];
      FileFilter@6085574 : Text[1024];
    BEGIN
      WITH Document DO BEGIN
        IF "File Type" = "File Type"::OCR THEN BEGIN
          FileExtension := '.pdf';
          ToFileName := "No." + FileExtension;
        END ELSE BEGIN
          FileExtension := "File Extension";
          ToFileName := "No." + '.' + FileExtension;
        END;
        FileFilter := STRSUBSTNO('File (*.%1)|*.%1',FileExtension);
        DOWNLOAD(FromFileName,'Download','C:\',FileFilter,ToFileName);
      END;
    END;

    LOCAL PROCEDURE GetAddInResourceUrl@2(Addin@1002 : Record 2000000069) : Text;
    VAR
      CODC@1000 : Codeunit 6085617;
      AboutDC@1001 : Codeunit 6085589;
      ContiniaVersionMgt@6085573 : Codeunit 6192775;
    BEGIN
      EXIT(STRSUBSTNO('%1/addins/%2/%3/%4.zip',CODC.GetResourceUrl,AboutDC.ControlAddInVersion,ContiniaVersionMgt.NAVVersion,LOWERCASE(Addin."Add-in Name")));
    END;

    BEGIN
    END.
  }
}

