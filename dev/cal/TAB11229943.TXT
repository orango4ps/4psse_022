OBJECT Table 11229943 DPT Header Text
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF ("Creation Date" = 0D) THEN
                 "Creation Date" := TODAY;
               IF ("Created By" = '') THEN
                 "Created By" := USERID;
             END;

    OnModify=VAR
               DPTSetup@1100529400 : Record 11229823;
             BEGIN
               DPTSetup.GET;
               IF ("Language Code" = '') THEN
                 "Language Code" := DPTSetup."Language Code"
             END;

    CaptionML=ENU=DPT Header Text;
  }
  FIELDS
  {
    { 10  ;   ;Table No.           ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   CaptionML=[ENU=Table No.;
                                                              NOR=Tabellnr;
                                                              SVE=Tabellnr];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 20  ;   ;Field No.           ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table No.));
                                                   CaptionML=[ENU=Field No.;
                                                              NOR=Feltnr.;
                                                              SVE=F„ltnr];
                                                   Editable=No }
    { 30  ;   ;Source No.          ;Code20        ;CaptionML=[ENU=Source No.;
                                                              NOR=Kildenr.;
                                                              SVE=Ursprungsnr] }
    { 40  ;   ;Subsite Code        ;Code20        ;CaptionML=ENU=Subsite Code }
    { 80  ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              NOR=Linjenr.;
                                                              SVE=Radnr];
                                                   Editable=No }
    { 90  ;   ;Text Line           ;Text250       ;CaptionML=ENU=Text Line }
    { 100 ;   ;Line Break          ;Boolean       ;CaptionML=[ENU=Line Break;
                                                              NOR=Radbrytning;
                                                              SVE=Radbrytning] }
    { 110 ;   ;Creation Date       ;Date          ;CaptionML=[ENU=Creation Date;
                                                              NOR=Opprettet den;
                                                              SVE=Uppl„ggningsdatum];
                                                   Editable=No }
    { 120 ;   ;Created By          ;Text50        ;CaptionML=[ENU=Created by;
                                                              NOR=Opprettet av;
                                                              SVE=Skapades av];
                                                   Editable=No }
    { 130 ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   CaptionML=[ENU=Language Code;
                                                              NOR=Spr†kkode;
                                                              SVE=Spr†kkod] }
  }
  KEYS
  {
    {    ;Table No.,Field No.,Source No.,Subsite Code,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      NOTIMPLEMENTED@1100528300 : TextConst 'ENU=DPT Header Text not implemented.';

    PROCEDURE StartTextEditor@1100528300();
    VAR
      TextEditor@1100528300 : Codeunit 11012001;
    BEGIN
      TextEditor.DPTHeaderText(Rec);
    END;

    PROCEDURE GetLastLineNo@1100528304() : Integer;
    VAR
      DPTHeaderText@1100528300 : Record 11229943;
    BEGIN
      DPTHeaderText.SETRANGE("Table No.", "Table No.");
      DPTHeaderText.SETRANGE("Field No.", "Field No.");
      DPTHeaderText.SETRANGE("Source No.", "Source No.");
      DPTHeaderText.SETRANGE("Subsite Code", "Subsite Code");
      IF (DPTHeaderText.FINDLAST) THEN
        EXIT(DPTHeaderText."Line No.");
      EXIT(0);
    END;

    PROCEDURE CopySourceText@1100528301(TableNo@1100528300 : Integer;FieldNo@1100528301 : Integer;SourceNo@1100528302 : Code[20]);
    VAR
      DPTSetup@1100528306 : Record 11229823;
      DPTHeaderTextFrom@1100528304 : Record 11229943;
      DPTHeaderTextTo@1100528305 : Record 11229943;
      Job@1100528307 : Record 11072003;
    BEGIN
      DPTHeaderTextFrom.SETRANGE("Table No.", DATABASE::"DPT Setup");
      CASE TableNo OF
        DATABASE::Job: DPTHeaderTextFrom.SETRANGE("Field No.", DPTSetup.FIELDNO("Use Project Portal"));
        DATABASE::"Service Location": DPTHeaderTextFrom.SETRANGE("Field No.", DPTSetup.FIELDNO("Use Location Portal"));
        ELSE ERROR(NOTIMPLEMENTED);
      END;
      DPTHeaderTextFrom.SETRANGE("Source No.", '');
      DPTHeaderTextFrom.SETRANGE("Subsite Code", '');
      IF (NOT DPTHeaderTextFrom.FINDSET) THEN
        EXIT;
      DPTHeaderTextTo.SETRANGE("Table No.", TableNo);
      DPTHeaderTextTo.SETRANGE("Field No.", FieldNo);
      DPTHeaderTextTo.SETRANGE("Source No.", SourceNo);
      DPTHeaderTextTo.SETRANGE("Subsite Code", '');
      IF (DPTHeaderTextTo.FINDSET) THEN
        EXIT;

      REPEAT
        DPTHeaderTextTo.COPY(DPTHeaderTextFrom);
        DPTHeaderTextTo."Table No." := TableNo;
        DPTHeaderTextTo."Field No." := FieldNo;
        DPTHeaderTextTo."Source No." := SourceNo;
        DPTHeaderTextTo."Line No." := DPTHeaderTextTo.GetLastLineNo + 10000;
        DPTHeaderTextTo.INSERT;
      UNTIL (DPTHeaderTextFrom.NEXT = 0);
    END;

    PROCEDURE CopySubsiteText@1100528302(TableNo@1100528300 : Integer;FieldNo@1100528301 : Integer;SourceNo@1100528302 : Code[20];SubsiteCode@1100528303 : Code[20]);
    VAR
      DPTHeaderTextFrom@1100528305 : Record 11229943;
      DPTHeaderTextTo@1100528304 : Record 11229943;
      DPTDefaultSubsite@1100528306 : Record 11229941;
    BEGIN
      DPTHeaderTextFrom.SETRANGE("Table No.", DATABASE::"DPT Default Subsite");
      DPTHeaderTextFrom.SETRANGE("Field No.", DPTDefaultSubsite.FIELDNO(Code));
      CASE TableNo OF
        DATABASE::"DPT Subsite Project": DPTHeaderTextFrom.SETRANGE("Source No.", 'Type::Project');
        DATABASE::"DPT Subsite ServLoc": DPTHeaderTextFrom.SETRANGE("Source No.", 'Type::ServLoc');
        ELSE ERROR(NOTIMPLEMENTED);
      END;
      DPTHeaderTextFrom.SETRANGE("Subsite Code", SubsiteCode);
      IF (NOT DPTHeaderTextFrom.FINDSET) THEN
        EXIT;

      REPEAT
        DPTHeaderTextTo.COPY(DPTHeaderTextFrom);
        DPTHeaderTextTo."Table No." := TableNo;
        DPTHeaderTextTo."Field No." := FieldNo;
        DPTHeaderTextTo."Source No." := SourceNo;
        DPTHeaderTextTo."Subsite Code" := SubsiteCode;
        DPTHeaderTextTo."Line No." := DPTHeaderTextTo.GetLastLineNo + 10000;
        DPTHeaderTextTo.INSERT;
      UNTIL (DPTHeaderTextFrom.NEXT = 0);
    END;

    BEGIN
    END.
  }
}

