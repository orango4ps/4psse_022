OBJECT XMLport 11012382 Import GL Entries ADP
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Import GL Entries ADP;
               SVE=Importera redovisningstransaktioner ADP];
    Direction=Import;
    DefaultFieldsValidation=No;
    TextEncoding=WINDOWS;
    OnInitXMLport=BEGIN
                    HumanResourcesSetup.GET;
                    HumanResourcesSetup.TESTFIELD("Salary Application",HumanResourcesSetup."Salary Application"::Multipay);
                    HumanResourcesSetup.TESTFIELD("GL Entry Journal Template");
                    HumanResourcesSetup.TESTFIELD("GL Entry Journal Batch");

                    currXMLport.FILENAME := HumanResourcesSetup."Pathname Files from Salary";
                  END;

    OnPreXMLport=BEGIN
                   GenJnlLine.LOCKTABLE;
                   GenJnlLine.SETRANGE("Journal Template Name",HumanResourcesSetup."GL Entry Journal Template");
                   GenJnlLine.SETRANGE("Journal Batch Name",HumanResourcesSetup."GL Entry Journal Batch");
                   IF GenJnlLine.FIND('+') THEN
                     LineCounter := GenJnlLine."Line No."
                   ELSE
                     LineCounter := 0;
                 END;

    OnPostXMLport=BEGIN
                    OldFile := currXMLport.FILENAME;
                    NewFile := HumanResourcesSetup.DetermineBackupFile(OldFile);
                    IF NewFile <> OldFile THEN
                      FileMgt.RenameClientFile(OldFile, NewFile, TRUE);
                  END;

    Format=Fixed Text;
  }
  ELEMENTS
  {
    { [{E6921924-A08B-41B8-ACA7-DB5A30390A03}];  ;Root                ;Element ;Text     }

    { [{036B8060-15E1-44A8-9E09-24E70275442F}];1 ;GenJournalLine      ;Element ;Table   ;
                                                  SourceTable=Table81;
                                                  SourceTableView=SORTING(Field1,Field51,Field2);
                                                  Import::OnAfterInitRecord=BEGIN
                                                                              "Gen. Journal Line".INIT;
                                                                              CLEAR(CustomerNo);
                                                                              CLEAR(Date);
                                                                              CLEAR(Department);
                                                                              CLEAR(WageFactor);
                                                                              CLEAR(ValueTxt);
                                                                              CLEAR(Sign);
                                                                              CLEAR(Info);
                                                                              CLEAR(GLaccount);
                                                                              CLEAR(DCCode);
                                                                            END;

                                                  Import::OnBeforeInsertRecord=BEGIN
                                                                                 WITH "Gen. Journal Line" DO BEGIN
                                                                                   IF CustomerNo <> HumanResourcesSetup."Customer No. (ADP/Raet)" THEN
                                                                                     ERROR(Text001, CustomerNo);
                                                                                   IF Date <> '' THEN BEGIN
                                                                                     EVALUATE(BookYear,COPYSTR(Date,5,2));
                                                                                     EVALUATE(BookMonth,COPYSTR(Date,3,2));
                                                                                     EVALUATE(BookDay,COPYSTR(Date,1,2));
                                                                                     PostingDate := DMY2DATE(BookDay,BookMonth,BookYear+2000);
                                                                                   END ELSE BEGIN
                                                                                     PostingDate := TODAY;
                                                                                   END;
                                                                                   Value := Value / 100.0;
                                                                                   IF Sign = '-' THEN
                                                                                     Value := -Value;
                                                                                   IF DCCode = Text002 THEN
                                                                                     Value := -Value;
                                                                                   "Journal Template Name" := HumanResourcesSetup."GL Entry Journal Template";
                                                                                   "Journal Batch Name"    := HumanResourcesSetup."GL Entry Journal Batch";
                                                                                   "Line No."     := LineCounter + 10000;
                                                                                   LineCounter    := "Line No.";
                                                                                   "Account Type" := "Account Type"::"G/L Account";
                                                                                   IF NOT JnlTemplRec.GET(HumanResourcesSetup."GL Entry Journal Template") THEN
                                                                                     JnlTemplRec.INIT;
                                                                                   IF NOT JnlBatchRec.GET(HumanResourcesSetup."GL Entry Journal Template", HumanResourcesSetup."GL Entry Journal Batch") THEN
                                                                                     JnlBatchRec.INIT;
                                                                                   IF DocNo = '' THEN BEGIN
                                                                                     IF JnlBatchRec."No. Series" <> '' THEN
                                                                                       DocNo := NoSeriesMgt.GetNextNo(JnlBatchRec."No. Series",0D,FALSE)
                                                                                     ELSE
                                                                                       IF JnlTemplRec."No. Series" <> '' THEN
                                                                                         DocNo := NoSeriesMgt.GetNextNo(JnlTemplRec."No. Series",0D,FALSE);
                                                                                   END;
                                                                                   "Document No." := DocNo;
                                                                                   StripZero(GLaccount);
                                                                                   StripZero(Department);
                                                                                   GlAccountRec.GET(GLaccount);
                                                                                   IF NOT GlAccountRec."Direct Posting" THEN BEGIN
                                                                                     "Account No." := GLaccount;
                                                                                     "Gen. Posting Type" := GlAccountRec."Gen. Posting Type";
                                                                                     "Gen. Bus. Posting Group" := GlAccountRec."Gen. Bus. Posting Group";
                                                                                     "Gen. Prod. Posting Group" := GlAccountRec."Gen. Prod. Posting Group";
                                                                                     "VAT Bus. Posting Group" := GlAccountRec."VAT Bus. Posting Group";
                                                                                     "VAT Prod. Posting Group" := GlAccountRec."VAT Prod. Posting Group";
                                                                                   END ELSE BEGIN
                                                                                     "Shortcut Dimension 1 Code" := Department;
                                                                                     VALIDATE("Account No.", GLaccount);
                                                                                   END;
                                                                                   "Shortcut Dimension 1 Code" := Department;
                                                                                   VALIDATE(Amount, Value);
                                                                                   IF EntryDesc <> '' THEN
                                                                                     Description := EntryDesc
                                                                                   ELSE
                                                                                     Description := Info;
                                                                                   "Posting Date"  := PostingDate;
                                                                                   "Document Date" := PostingDate;
                                                                                   "Source Code"   := JnlTemplRec."Source Code";
                                                                                   IF JnlBatchRec."Reason Code" <> '' THEN
                                                                                     "Reason Code" := JnlBatchRec."Reason Code"
                                                                                   ELSE
                                                                                     "Reason Code" := JnlTemplRec."Reason Code";
                                                                                   "Origin Salary Application" := TRUE;
                                                                                   ShowStatus("Journal Template Name", "Journal Batch Name", "Line No.");
                                                                                   INSERT;
                                                                                   currXMLport.SKIP;
                                                                                 END;
                                                                               END;
                                                                                }

    { [{4752292C-4746-43FC-AE0E-AC3E50A2711D}];2 ;CustomerNo          ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=5 }

    { [{44B3E6C3-A323-450B-AC2B-8A20AE0BA95D}];2 ;Date                ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=6 }

    { [{00BCB043-5949-42A3-A5A7-E71FEDDC45E6}];2 ;Department          ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=6 }

    { [{5A12EEB9-DF8A-4C77-A052-574C8B1CB820}];2 ;WageFactor          ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=3 }

    { [{61F840E3-8C92-45D7-9CC7-AA581433AAAF}];2 ;ValueTxt            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  FillEmptyTextWithZero(ValueTxt);
                                                                                  EVALUATE(Value, ValueTxt);
                                                                                END;

                                                  Width=9 }

    { [{24FCF88C-9720-443C-9622-EC073419F0A5}];2 ;Sign                ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=1 }

    { [{1CA6FF33-21DB-42ED-BF16-9222AB0F1B61}];2 ;Info                ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=10 }

    { [{A5A849FB-1EF1-427F-AD3C-67DF0D5F61B0}];2 ;GLaccount           ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=11 }

    { [{952798AA-33ED-4CB8-9A23-900E623309E1}];2 ;DCCode              ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Width=1 }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1100528401;;Container;
                  ContainerType=ContentArea }

      { 1100528400;1;Group  ;
                  CaptionML=[ENU=Options;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100528402;2;Field  ;
                  CaptionML=[ENU=Entry Description;
                             SVE=Transaktionsbeskrivning];
                  SourceExpr=EntryDesc }

    }
  }
  CODE
  {
    VAR
      HumanResourcesSetup@1100528423 : Record 5218;
      GenJnlLine@1100528422 : Record 81;
      JnlTemplRec@1100528421 : Record 80;
      JnlBatchRec@1100528420 : Record 232;
      GlAccountRec@1100528419 : Record 15;
      NoSeriesMgt@1100528418 : Codeunit 396;
      FileMgt@1100528404 : Codeunit 419;
      XMLportStatusDialogMgt@1100528405 : Codeunit 11130006;
      LineCounter@1100528417 : Integer;
      PostingDate@1100528416 : Date;
      BookYear@1100528415 : Integer;
      BookMonth@1100528414 : Integer;
      BookDay@1100528413 : Integer;
      Value@1100528408 : Decimal;
      DocNo@1100528403 : Code[20];
      EntryDesc@1100528402 : Text[30];
      OldFile@1100528401 : Text[250];
      NewFile@1100528400 : Text[250];
      Text001@1100528407 : TextConst 'ENU=Entries for Customer Number %1 could not be imported in this Navision Company.;SVE=Transaktioner f”r kundnummer %1 kunde inte importeras till detta Navision-f”retag.';
      Text002@1100528406 : TextConst 'ENU=C;SVE=Text005';

    PROCEDURE StripZero@1(VAR convtext@11012000 : Text[30]);
    VAR
      Position@11012001 : Integer;
    BEGIN
      Position := STRPOS(convtext,'0');
      WHILE Position = 1 DO
        BEGIN
          convtext := DELSTR(convtext,Position,1);
          Position  := STRPOS(convtext,'0');
        END;
    END;

    PROCEDURE FillEmptyTextWithZero@1100525013(VAR Text@1100525000 : Text[1024]);
    BEGIN
      IF DELCHR(Text,'<>=',' ') = '' THEN
        Text := '0';
    END;

    LOCAL PROCEDURE ShowStatus@1100409000(JournalTemplateName@1100409000 : Code[10];JournalBatchName@1100409001 : Code[10];LineNo@1100409002 : Integer);
    VAR
      GenJournalLine@1100409004 : Record 81;
      RecRef@1100409003 : RecordRef;
    BEGIN
      GenJournalLine."Journal Template Name" := JournalTemplateName;
      GenJournalLine."Journal Batch Name" := JournalBatchName;
      GenJournalLine."Line No." := LineNo;
      RecRef.GETTABLE(GenJournalLine);
      XMLportStatusDialogMgt.ShowImportStatus(RecRef);
    END;

    PROCEDURE InitXMLport@1100529500(iEntryDesc@1100529500 : Text[30]);
    BEGIN
      EntryDesc := iEntryDesc;
    END;

    BEGIN
    END.
  }
}

