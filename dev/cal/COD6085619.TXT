OBJECT Codeunit 6085619 Export Continia Users
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.5.00.07;
  }
  PROPERTIES
  {
    OnRun=VAR
            ContiniaCompanySetup@6085573 : Record 6192773;
            ContiniaWebPortal@1160040000 : Record 6192774;
            ContiniaUser@1160040002 : Record 6086001;
            ContiniaUser2@1160040001 : Record 6086001;
            CEM@1160040004 : Codeunit 6085782;
          BEGIN
            Code;

            ContiniaCompanySetup.GET;
            IF NOT CEM.IsCEMEnabled(FALSE) THEN
              ContiniaCompanySetup.TESTFIELD("Web Portal Code");

            IF ContiniaCompanySetup."Web Portal Code" <> '' THEN
              IF ContiniaWebPortal.GET(ContiniaCompanySetup."Web Portal Code") AND
                (ContiniaWebPortal."Welcome E-Mails" = ContiniaWebPortal."Welcome E-Mails"::"Send automatically")
              THEN
                SendWelcomeEmail
              ELSE BEGIN
                ContiniaUser.SETCURRENTKEY("Send Welcome E-mail");
                ContiniaUser.SETRANGE("Send Welcome E-mail",TRUE);
                IF NOT ContiniaUser.ISEMPTY THEN
                  MESSAGE(Text025,ContiniaWebPortal.TABLECAPTION);
              END;

            CEM.SendWelcomeEmail2;
          END;

  }
  CODE
  {
    VAR
      Text000@1160040020 : TextConst 'DEU=Willkommen bei Continia Online;ENU=Welcome to Continia Online;NLD=Welkom om Continia Online';
      Text001@1160040012 : TextConst 'DEU=Sehr geehrte/r %1;ENU=Dear %1;NLD=Geachte %1';
      Text002@1160040013 : TextConst 'DEU=Sie haben ein Benutzerkonto fr Continia Document Capture erstellt. Bevor Sie beginnen, aktivieren Sie Ihr Benutzerkonto bitte ber untenstehenden Link.;ENU=You have now been created as a user for Continia Document Capture. Before you get started please use the link below to activate your user account.;NLD=U bent nu aangemaakt  als gebruiker van Continia Document Capture. Gebruik, alvorens u van start gaat, onderstaande link om uw gebruikersaccount te avtiveren.';
      Text003@1160040014 : TextConst 'DEU=Kontodetails;ENU=Account Details;NLD=Accountdetails;NOR=Kontodetaljer;SVE=Kontodetaljer';
      Text004@1160040015 : TextConst 'DEU=Benutzername: %1;ENU=Username: %1;NLD=Gebruikersnaam: %1';
      Text005@1160040016 : TextConst 'DEU=Passwort:;ENU=Password:;NLD=Wachtwoord:';
      Text006@1160040018 : TextConst 'DEU=Klicken Sie hier, um Ihr Konto zu erg„nzen und zu aktivieren;ENU=Click here to specify and activate your account;NLD=Klik hier om uw account te specificeren en activeren';
      Text007@1160040017 : TextConst 'DEU=Wenn Sie Ihr Konto aktiviert haben, k”nnen Sie hier auf Continia Online zugreifen: %1;ENU=When your account has been activated you can access Continia Online here: %1;NLD=Zodra uw account geactiveerd is heeft u via %1 toegang tot het Continia Online.';
      Text008@1160040019 : TextConst 'DEU=Mit freundlichen Gráen;ENU=Best regards,;NLD=Met vriendelijke groet,;NOR=Med vennlig hilsen,;SVE=Med v„nlig h„lsning';
      Text009@1160040010 : TextConst 'DEU=%1 Benutzer exportiert.;ENU=%1 users exported.;NLD=%1 gebruikers ge‰xporteerd.';
      Text015@1160040002 : TextConst 'DEU=Es wurden keine Benutzer exportiert;ENU=No users were exported.;NLD=Er zijn geen gebruikers geexporteerd.';
      COMgt@1160040007 : Codeunit 6192772;
      XMLLib@1160040011 : Codeunit 6085573;
      HttpActions@1160040009 : 'PUT,DELETE,GET';
      Protocol@1160040008 : 'IMAP,ContiniaMail';
      Text017@1160040021 : TextConst 'DEU=Sie haben ein Benutzerkonto fr Continia Online Capture erstellt.;ENU=You have now been created as a user for Continia Online.;NLD=U bent nu aangemaakt  als gebruiker van Continia Online.';
      Text018@1160040022 : TextConst 'DEU=Ihr Windows-Passwort;ENU=Your Windows password;NLD=Uw Windows wachtwoord';
      Text019@1160040023 : TextConst 'DEU=Ihr Konto wurde bereits aktiviert, Sie erhalten hier Zugriff auf Continia Online: %1;ENU=Your account has already been activated and you can access Continia Online here: %1;NLD=Uw account is reeds geactiveerd en u heeft toegang tot Continia Online: %1';
      Text020@1160040024 : TextConst 'DEU=Begráungs-E-Mails werden gesendet ...\@1@@@@@@@@@@@@@@@@@@@@@@@@@@;ENU=Sending Welcome E-mails...\@1@@@@@@@@@@@@@@@@@@@@@@@@@@;NLD=Herinneringsmails versturen...\@1@@@@@@@@@@@@@@@@@@@@@@@@@@';
      Text021@1160040027 : TextConst 'DEU=Exportieren von Continia-Benutzern...;ENU=Exporting Continia users...;NLD=Exporteren van Continia gebruikers...';
      Text022@1160040026 : TextConst 'DEU=Exportieren Unternehmen;ENU=Exporting Companies;NLD=Exporteren bedrijven';
      Text023@1160040033 : TextConst 'DEU=Exportiere Benutzer;ENU=Exporting Users;NLD=Exporteer Gebruikers';
      Text024@1160040028 : TextConst 'DEU=Benutzer hochladen;ENU=Uploading Users;NLD=Gebruikers uploaden';
      Text025@1160040001 : TextConst 'DEU=Benutzer werden exportiert...\Schritt #1#######################\Status @2@@@@@@@@@@@@@@@@@@@@@@@;ENU=One or more users have not received a welcome e-mail, because welcome e-mails has been set up to be send manually in %1.;NLD=Een of meer gebruikers hebben geen welkomst-email ontvangen, omdat het versturen van welkomst-emails als handmatig ingesteld staat in %1.';
      LicenseExpiredErr@116004004 : TextConst 'DEU=%1 ist fr Unternehmen %2 nicht richtig aktiviert. Aktivieren oder deaktivieren Sie das Unternehmen aus der %1 Einrichtungskarte.;ENU=%1 has not been activated correctly for company %2. Please activate or deactivate the company from the %1 Setup card.;NLD=%1 is niet correct geactiveerd voor bedrijf %2. Activeer of deactiveer het bedrijf vanuit de %1 instellingenkaart.';
      CompanyNotActivatedErr@6085574 : TextConst 'DEU=Es stimmt etwas nicht mit der Continia-Einrichtung.\\Aktivieiren Sie Ihr Continia-Produkt nochmals, bevor Sie fortfahren.;ENU=There is something wrong with the Continia setup.\\Please reactivate your Continia product before you can continue.;NLD=Er is iets mis met de Continia instellingen.\\Heractiveer uw Continia-product voordat u verder kunt gaan.';
      ConflictingWebportalSetupUrlEr@6085575 : TextConst 'DEU=%1 muss fr jeden %2 eindeutig sein. \\Aktualisieren Sie die Continia Webportal-Einrichtung und exportieren Sie nochmals die Continia-Benutzer.;ENU=%1 must be unique for each %2.\\Please update the Continia Web Portal setup and export Continia Users again.;NLD=%1 moet uniek zijn voor elke %2. \\Werk de Continia Webportaalinstellingen bij en exporteer de Continia gebruikers opnieuw.';
      ConflictingCompCodeOnPortalErr@6085573 : TextConst 'DEU=Unternehmen %1 und %2 haben den gleichen %3 (%4) in %5.\\%3 muss eindeutig fr alle Unternhemen sein die dieselbe Website benutzen (%6 in %5).;ENU=Company %1 and %2 have been assigned the same %3 (%4) in %5.\\%3 must be unique for all companies using the same web site (%6 in %5).;NLD=Bedrijf %1 en %2 hebben dezelfde %3 (%4) in %5.\\%3 moet uniek zijn voor alle bedrijven die dezelfde website gebruiken (%6 in %5).';

    LOCAL PROCEDURE Code@1160040037() : Boolean;
    VAR
      Company@1160040012 : Record 2000000006;
      CompanyNeedReactivation@1160040031 : TEMPORARY Record 2000000006;
      ContiniaUser@1160040011 : Record 6086001;
      ContiniaUserSetup@1160040014 : Record 6086002;
      ContiniaCompanySetup@6085573 : Record 6192773;
      ContiniaWebPortal@1160040017 : Record 6192774;
      ContiniaWebPortal2@6085574 : Record 6192774;
      AboutDC@1160040015 : Codeunit 6085589;
      CODC@1160040000 : Codeunit 6085617;
      CEM@1160040010 : Codeunit 6085782;
      COLMgt@1160040027 : Codeunit 6192773;
      ContiniaWebPortalList@6085575 : Page 6192774;
      CloudXmlDoc@1160040021 : Codeunit 6192800;
      OnPremXmlDoc@1160040022 : Codeunit 6192800;
      CloudUsersNode@1160040019 : Codeunit 6192801;
      OnPremUsersNode@1160040008 : Codeunit 6192801;
      CloudDatabaseNode@1160040020 : Codeunit 6192801;
      OnPremDatabaseNode@1160040007 : Codeunit 6192801;
      CloudWindowsNode@1160040023 : Codeunit 6192801;
      OnPremWindowsNode@1160040006 : Codeunit 6192801;
      CloudUserNode@1160040024 : Codeunit 6192801;
      OnPremUserNode@1160040005 : Codeunit 6192801;
      CloudCompaniesNode@1160040025 : Codeunit 6192801;
      OnPremCompaniesNode@1160040016 : Codeunit 6192801;
      Window@1160040013 : Dialog;
      TotalCount@1160040028 : Integer;
      RecCount@1160040001 : Integer;
      ExportCount@1160040029 : Integer;
      UseCloudApproval@1160040004 : Boolean;
      ExportOnPrem@1160040002 : Boolean;
      ExportCloud@1160040003 : Boolean;
      DCObjectVersion@1160040009 : Text[30];
      CEMObjectVersion@1160040018 : Text[30];
      XMLDocsToUpload@1160040026 : Integer;
      ActivationState@1160040030 : 'NeverActivated,NeedReactivation,Activated,OfflineStarted';
    BEGIN
      ContiniaCompanySetup.GET;
      ContiniaCompanySetup.TESTFIELD("Company Code");

      IF NOT CEM.IsCEMEnabled(FALSE) THEN
        ContiniaCompanySetup.TESTFIELD("Web Portal Code");

      IF GUIALLOWED THEN BEGIN
        Window.OPEN(CreateDialogText);
        TotalCount := Company.COUNT;
      END;

      IF ContiniaWebPortal.GET(ContiniaCompanySetup."Web Portal Code") AND
        NOT ContiniaWebPortal."Use Continia Online Web Portal"
      THEN BEGIN
        ContiniaWebPortal.TESTFIELD("Web Site Url");

        ContiniaWebPortal2.SETRANGE("Use Continia Online Web Portal",FALSE);
        ContiniaWebPortal2.SETRANGE("Web Site Url",ContiniaWebPortal."Web Site Url");
        IF ContiniaWebPortal2.COUNT > 1 THEN
          ERROR(ConflictingWebportalSetupUrlEr,ContiniaWebPortal2.FIELDCAPTION("Web Site Url"),ContiniaWebPortal2.TABLECAPTION);
      END;

      IF Company.FINDSET THEN
        REPEAT
          IF GUIALLOWED THEN BEGIN
            RecCount += 1;
            Window.UPDATE(1,CalcProgress(TotalCount,RecCount));
          END;

          CheckCompanySetup(Company.Name);

          IF SameWebPortal(Company.Name) THEN BEGIN
            IF (COLMgt.GetProductActivationState(AboutDC.ProductCode,Company.Name) <> ActivationState::NeedReactivation) AND
               (COLMgt.GetProductActivationState('CEM',Company.Name) <> ActivationState::NeedReactivation)
            THEN BEGIN
              IF COLMgt.GetProductActivationState(AboutDC.ProductCode,Company.Name) = ActivationState::Activated THEN
                Company.MARK(TRUE);

              IF COLMgt.GetProductActivationState('CEM',Company.Name) = ActivationState::Activated THEN
                Company.MARK(TRUE);
            END;
          END;
        UNTIL Company.NEXT = 0;

      Company.MARKEDONLY(TRUE);
      IF Company.COUNT > 0 THEN BEGIN
        DCObjectVersion := AboutDC.ObjectVersion;
        CEMObjectVersion := CEM.CEMBuildVersion;

        ContiniaUser.SETFILTER("E-Mail",'<>%1','');
        IF ContiniaUser.FINDSET THEN BEGIN
          IF GUIALLOWED THEN BEGIN
            RecCount := 0;
            TotalCount := ContiniaUser.COUNT;
          END;

          REPEAT
            IF GUIALLOWED THEN BEGIN
              RecCount += 1;
              Window.UPDATE(2,CalcProgress(TotalCount,RecCount));
            END;

            CLEAR(CloudUserNode);
            CLEAR(OnPremUserNode);
            IF Company.FINDSET THEN
              REPEAT
                ContiniaUserSetup.CHANGECOMPANY(Company.Name);
                IF ContiniaUserSetup.GET(ContiniaUser."User ID") THEN BEGIN
                  ContiniaCompanySetup.CHANGECOMPANY(Company.Name);
                  IF ContiniaCompanySetup.GET THEN BEGIN
                    UseCloudApproval := (ContiniaWebPortal.GET(ContiniaCompanySetup."Web Portal Code") AND
                      ContiniaWebPortal."Use Continia Online Web Portal");
                    ExportOnPrem := (ContiniaWebPortal.GET(ContiniaCompanySetup."Web Portal Code") AND
                      NOT ContiniaWebPortal."Use Continia Online Web Portal");
                    ExportCloud := UseCloudApproval OR CEM.IsCEMEnabled(FALSE);
                  END ELSE BEGIN
                    UseCloudApproval := FALSE;
                    ExportOnPrem := FALSE;
                    ExportCloud := FALSE;
                  END;

                  IF ContiniaUserSetup."Expense Management User" OR
                    ((ContiniaUserSetup."Approval Client" = ContiniaUserSetup."Approval Client"::"Continia Web Portal") AND
                    ContiniaWebPortal.GET(ContiniaCompanySetup."Web Portal Code") AND
                    ContiniaWebPortal."Use Continia Online Web Portal")
                  THEN BEGIN
                    IF ExportCloud THEN BEGIN
                      IF CloudXmlDoc.IsEmpty THEN
                        CreateUsersXml(ContiniaWebPortal,DCObjectVersion,CEMObjectVersion,
                          CloudXmlDoc,CloudUsersNode,CloudWindowsNode,CloudDatabaseNode);

                      IF CloudUserNode.IsClear THEN
                        IF IsWindowsUser(ContiniaUser) THEN
                          CreateUserXml(CloudWindowsNode,ContiniaWebPortal,ContiniaUser,CloudUserNode,CloudCompaniesNode)
                        ELSE
                          CreateUserXml(CloudDatabaseNode,ContiniaWebPortal,ContiniaUser,CloudUserNode,CloudCompaniesNode);

                      CreateCompanyXml(CloudCompaniesNode,Company,
                        ContiniaUserSetup."Expense Management User",
                        (ContiniaUserSetup."Approval Client" = ContiniaUserSetup."Approval Client"::"Continia Web Portal") AND
                        UseCloudApproval,ContiniaUserSetup."Expense User Group");
                    END;
                  END;

                  // Export users to OnPrem Web Approval if needed
                  IF ExportOnPrem AND
                    (ContiniaUserSetup."Approval Client" = ContiniaUserSetup."Approval Client"::"Continia Web Portal")
                  THEN BEGIN
                    ContiniaCompanySetup.TESTFIELD("Web Portal Code");
                    ContiniaWebPortal.GET(ContiniaCompanySetup."Web Portal Code");
                    IF OnPremXmlDoc.IsEmpty THEN
                      CreateUsersXml(ContiniaWebPortal,DCObjectVersion,CEMObjectVersion,
                        OnPremXmlDoc,OnPremUsersNode,OnPremWindowsNode,OnPremDatabaseNode);

                    IF OnPremUserNode.IsClear THEN
                      IF IsWindowsUser(ContiniaUser) THEN
                        CreateUserXml(OnPremWindowsNode,ContiniaWebPortal,ContiniaUser,OnPremUserNode,OnPremCompaniesNode)
                      ELSE
                        CreateUserXml(OnPremDatabaseNode,ContiniaWebPortal,ContiniaUser,OnPremUserNode,OnPremCompaniesNode);

                    CreateCompanyXml(OnPremCompaniesNode,Company,FALSE,
                      (ContiniaUserSetup."Approval Client" = ContiniaUserSetup."Approval Client"::"Continia Web Portal"),'');
                  END;
                END;
              UNTIL Company.NEXT = 0;

            IF (NOT OnPremUserNode.IsClear) OR (NOT CloudUserNode.IsClear) THEN
              ExportCount += 1;
          UNTIL ContiniaUser.NEXT = 0;

          IF ExportCount <> 0 THEN BEGIN
            IF GUIALLOWED THEN BEGIN
              IF NOT CloudXmlDoc.IsEmpty AND NOT OnPremXmlDoc.IsEmpty THEN
                XMLDocsToUpload := 2
              ELSE
                XMLDocsToUpload := 1;

              IF XMLDocsToUpload = 1 THEN
                Window.UPDATE(3,5000)
              ELSE
                Window.UPDATE(3,3333);
            END;

            IF NOT CloudXmlDoc.IsEmpty THEN BEGIN
              COMgt.ExecuteRequest(CODC.GetCOUserUrl,HttpActions::PUT,TRUE,CloudXmlDoc);
              SaveActivateLinks(CloudXmlDoc);
              COMMIT;

              IF GUIALLOWED THEN
                IF XMLDocsToUpload = 1 THEN
                  Window.UPDATE(3,10000)
                ELSE
                  Window.UPDATE(3,6667);
            END;

            IF NOT OnPremXmlDoc.IsEmpty THEN BEGIN
              COMgt.ExecuteRequestWithoutToken(CODC.GetUserUrl,HttpActions::PUT,TRUE,OnPremXmlDoc);
              SaveActivateLinks(OnPremXmlDoc);
              COMMIT;

              IF GUIALLOWED THEN
                Window.UPDATE(3,10000);
            END;
          END;
        END;
      END;

      IF GUIALLOWED THEN
        Window.CLOSE;

      IF ExportCount = 0 THEN
        MESSAGE(Text015)
      ELSE
        MESSAGE(Text009,ExportCount);
    END;

    LOCAL PROCEDURE CheckCompanySetup@1160040057(_CompanyName@1160040000 : Text[30]);
    VAR
      ContiniaCompanySetup@6085573 : Record 6192773;
      CCSetup2@6085575 : Record 6192773;
      ContiniaWebPortal@1160040003 : Record 6192774;
      ContiniaUser@1160040005 : Record 6086001;
      ContiniaUserSetup@1160040002 : Record 6086002;
      Comp@6085574 : Record 2000000006;
      EmptyGUID@1160040004 : GUID;
      Stop@1160040006 : Boolean;
    BEGIN
      ContiniaUserSetup.CHANGECOMPANY(_CompanyName);
      ContiniaUserSetup.SETRANGE("Approval Client",ContiniaUserSetup."Approval Client"::"Continia Web Portal");
      IF ContiniaUserSetup.ISEMPTY THEN
        EXIT;

      ContiniaCompanySetup.CHANGECOMPANY(_CompanyName);
      IF NOT ContiniaCompanySetup.GET THEN
        EXIT;

      IF ContiniaCompanySetup."Company GUID" = EmptyGUID THEN
        ERROR(CompanyNotActivatedErr);

      IF ContiniaUserSetup.FINDSET THEN
        REPEAT
          ContiniaUser.GET(ContiniaUserSetup."Continia User ID");
          IF IsWindowsUser(ContiniaUser) AND ContiniaWebPortal.GET(ContiniaCompanySetup."Web Portal Code") THEN BEGIN
            ContiniaWebPortal.TESTFIELD("Windows Web Service URL");
            ContiniaWebPortal.TESTFIELD("Local Domain Name");
            Stop := TRUE;
          END;
        UNTIL (ContiniaUserSetup.NEXT = 0) OR Stop;

      Stop := FALSE;
      IF ContiniaUserSetup.FINDSET THEN
        REPEAT
          ContiniaUser.GET(ContiniaUserSetup."Continia User ID");
          IF ContiniaUser."NAV Login Type" = ContiniaUser."NAV Login Type"::Database THEN BEGIN
            IF ContiniaWebPortal.GET(ContiniaCompanySetup."Web Portal Code") THEN
              ContiniaWebPortal.TESTFIELD("Database Web Service URL");
            Stop := TRUE;
          END;
        UNTIL (ContiniaUserSetup.NEXT = 0) OR Stop;

      Comp.SETFILTER(Name,'<>%1',_CompanyName);
      IF Comp.FINDSET THEN
        REPEAT
          CCSetup2.CHANGECOMPANY(Comp.Name);
          IF CCSetup2.GET AND
            ((CCSetup2."Web Portal Code" = ContiniaCompanySetup."Web Portal Code") AND
            (ContiniaCompanySetup."Web Portal Code" <> '')) AND
            (CCSetup2."Company Code" = ContiniaCompanySetup."Company Code")
          THEN
            ERROR(ConflictingCompCodeOnPortalErr,_CompanyName,Comp.Name,ContiniaCompanySetup.FIELDCAPTION("Company Code"),
              ContiniaCompanySetup."Company Code",ContiniaCompanySetup.TABLECAPTION,
              ContiniaCompanySetup.FIELDCAPTION("Web Portal Code"));
        UNTIL Comp.NEXT = 0;
    END;

    LOCAL PROCEDURE CreateUsersXml@1160040042(ContiniaWebPortal@1160040002 : Record 6192774;DCBuildVersion@1160040003 : Text[30];CEMBuildVersion@1160040006 : Text[30];VAR XmlDoc@1160040001 : Codeunit 6192800;VAR UsersNode@1160040000 : Codeunit 6192801;VAR WindowsNode@1160040004 : Codeunit 6192801;VAR DatabaseNode@1160040005 : Codeunit 6192801);
    BEGIN
      XmlDoc.CreateNode(UsersNode,'element','Users','');
      XMLLib.SetXmlNodeAttr(UsersNode,'Version','2');
      XmlDoc.AppendChild(UsersNode);

      XMLLib.CreateXmlNode(UsersNode,'DatabaseLogins','',DatabaseNode);
      XMLLib.SetXmlNodeAttr(DatabaseNode,'WebServiceUrl',ContiniaWebPortal."Database Web Service URL");
      XMLLib.SetXmlNodeAttr(DatabaseNode,'Tenant',ContiniaWebPortal."Web Service Tenant");
      XMLLib.SetXmlNodeAttr(DatabaseNode,'DCVersion',DCBuildVersion);
      XMLLib.SetXmlNodeAttr(DatabaseNode,'EMVersion',CEMBuildVersion);

      XMLLib.CreateXmlNode(UsersNode,'WindowsLogins','',WindowsNode);
      XMLLib.SetXmlNodeAttr(WindowsNode,'WebServiceUrl',ContiniaWebPortal."Windows Web Service URL");
      XMLLib.SetXmlNodeAttr(WindowsNode,'Tenant',ContiniaWebPortal."Web Service Tenant");
      XMLLib.SetXmlNodeAttr(WindowsNode,'DCVersion',DCBuildVersion);
      XMLLib.SetXmlNodeAttr(WindowsNode,'EMVersion',CEMBuildVersion);
    END;

    LOCAL PROCEDURE CreateUserXml@1160040047(VAR ParentNode@1160040001 : Codeunit 6192801;ContiniaWebPortal@1160040004 : Record 6192774;ContiniaUser@1160040003 : Record 6086001;VAR UserNode@1160040000 : Codeunit 6192801;VAR CompaniesNode@1160040002 : Codeunit 6192801) : Boolean;
    VAR
      WebServiceKey@1160040005 : Text[250];
    BEGIN
      XMLLib.CreateXmlNode(ParentNode,'User','',UserNode);
      XMLLib.CreateXmlNode(UserNode,'Companies','',CompaniesNode);

      IF IsWindowsUser(ContiniaUser) THEN BEGIN
        IF STRPOS(ContiniaUser."User ID",'\') > 0 THEN
          XMLLib.SetXmlNodeAttr(UserNode,'WindowsUsername',ContiniaUser."User ID")
        ELSE
          XMLLib.SetXmlNodeAttr(UserNode,'WindowsUsername',ContiniaWebPortal."Local Domain Name" + '\' + ContiniaUser."User ID");
      END ELSE BEGIN
        WebServiceKey := COMgt.GetUserWebServiceKey(ContiniaUser."User ID");
        XMLLib.SetXmlNodeAttr(UserNode,'WebServiceKey',WebServiceKey);
      END;

      XMLLib.SetXmlNodeAttr(UserNode,'Email',ContiniaUser."E-Mail");
      XMLLib.SetXmlNodeAttr(UserNode,'Username',ContiniaUser."User ID");
      XMLLib.SetXmlNodeAttr(UserNode,'FullName',ContiniaUser.Name);
    END;

    LOCAL PROCEDURE CreateCompanyXml@1160040048(VAR CompaniesNode@1160040000 : Codeunit 6192801;Company@1160040002 : Record 2000000006;IsEMUser@1160040004 : Boolean;IsWAUser@1160040003 : Boolean;EMUserGroup@1160040006 : Code[20]) : Boolean;
    VAR
      CompanyNode@1160040001 : Codeunit 6192801;
      ContiniaCompanySetup@6085573 : Record 6192773;
    BEGIN
      ContiniaCompanySetup.CHANGECOMPANY(Company.Name);
      ContiniaCompanySetup.GET;

      XMLLib.CreateXmlNode(CompaniesNode,'Company','',CompanyNode);
      XMLLib.SetXmlNodeAttr(CompanyNode,'Name',Company.Name);
      XMLLib.SetXmlNodeAttr(CompanyNode,'Code',ContiniaCompanySetup."Company Code");
      XMLLib.SetXmlNodeAttr(CompanyNode,'Guid',ContiniaCompanySetup."Company GUID");
      IF IsWAUser THEN
        XMLLib.SetXmlNodeAttr(CompanyNode,'WAUser',XMLLib.Bool2Text(IsWAUser));
      IF IsEMUser THEN
        XMLLib.SetXmlNodeAttr(CompanyNode,'EMUser',XMLLib.Bool2Text(IsEMUser));
      IF EMUserGroup <> '' THEN
        XMLLib.SetXmlNodeAttr(CompanyNode,'EMUserGroup',EMUserGroup);
    END;

    PROCEDURE SaveActivateLinks@1160040000(VAR XmlDoc@1160040001 : Codeunit 6192800);
    VAR
      ContiniaUser@1160040000 : Record 6086001;
      XMLNodeList@1160040004 : Codeunit 6192802;
      DocumentElement@1000 : Codeunit 6192801;
      ActivationLink@1160040006 : Text[1024];
      EMActivationLink@1160040002 : Text[1024];
      Username@1160040007 : Text[1024];
      i@1160040005 : Integer;
    BEGIN
      IF NOT XmlDoc.HasChildnodes THEN
        EXIT;

      XmlDoc.GetDocumentElement(DocumentElement);
      DocumentElement.SelectNodes(XMLNodeList,'User');

      FOR i := 0 TO XMLNodeList.Count - 1 DO BEGIN
        COMgt.GetAttribAsText(XMLNodeList,'Username',i,Username);
        COMgt.GetAttribAsText(XMLNodeList,'Link',i,ActivationLink);
        COMgt.GetAttribAsText(XMLNodeList,'EMLink',i,EMActivationLink);

        IF ContiniaUser.GET(Username) THEN BEGIN
          IF EMActivationLink <> '' THEN
            ContiniaUser."Activation Link (EM)" := EMActivationLink;
          IF ActivationLink <> '' THEN
            ContiniaUser."Activation Link" := ActivationLink;
          IF (EMActivationLink <> '') OR (ActivationLink <> '') THEN
            ContiniaUser.MODIFY(TRUE);
        END;
      END;
    END;

    PROCEDURE SendWelcomeEmail@1160040002();
    VAR
      ContiniaCompanySetup@1160040002 : Record 6192773;
      ContiniaWebPortal@6086300 : Record 6192774;
      ContiniaUser@1160040001 : Record 6086001;
      ContiniaUser2@1160040004 : Record 6086001;
      SmtpMailMgt@1160040000 : Codeunit 6085598;
      ContiniaUserMgt@1160040007 : Codeunit 6085781;
      Window@1160040005 : Dialog;
      TotalCount@1160040010 : Integer;
      RecCount@1160040009 : Integer;
    BEGIN
      ContiniaUser.SETCURRENTKEY("Send Welcome E-mail");
      ContiniaUser.SETRANGE("Send Welcome E-mail",TRUE);
      IF ContiniaUser.FINDSET(TRUE,TRUE) THEN BEGIN
        ContiniaCompanySetup.GET;
        ContiniaCompanySetup.TESTFIELD("Web Portal Code");
        ContiniaWebPortal.GET(ContiniaCompanySetup."Web Portal Code");

        IF GUIALLOWED THEN BEGIN
          Window.OPEN(Text020);
          TotalCount := ContiniaUser.COUNT;
        END;

        REPEAT
          IF ContiniaUser.IsWebApprovalUser(TRUE) THEN BEGIN
            IF GUIALLOWED THEN BEGIN
              RecCount += 1;
              Window.UPDATE(1,CalcProgress(TotalCount,RecCount));
            END;

            SmtpMailMgt.AddTo(ContiniaUser.Name,ContiniaUser."E-Mail");
            SmtpMailMgt.SetSubject(Text000);

            SmtpMailMgt.AppendToBody(STRSUBSTNO('<b>%1</b>',STRSUBSTNO(Text001,ContiniaUser.Name)),TRUE);
            SmtpMailMgt.AppendLineBreakToBody;

            IF IsWindowsUser(ContiniaUser) THEN BEGIN
              SmtpMailMgt.AppendToBody(Text017,TRUE);
              SmtpMailMgt.AppendLineBreakToBody;
              SmtpMailMgt.AppendToBody(STRSUBSTNO('<b>%1</b>',Text003),TRUE);
              IF ContiniaWebPortal."Use Continia Online Web Portal" THEN
                SmtpMailMgt.AppendToBody(STRSUBSTNO(Text004,ContiniaUser."E-Mail"),TRUE)
              ELSE
                SmtpMailMgt.AppendToBody(STRSUBSTNO(Text004,ContiniaUserMgt.GetNAVUserID(ContiniaUser."User ID")),TRUE);
              SmtpMailMgt.AppendToBody(STRSUBSTNO('%1 %2',Text005,Text018),TRUE);
              SmtpMailMgt.AppendLineBreakToBody;
              SmtpMailMgt.AppendToBody(STRSUBSTNO(Text019,ContiniaWebPortal."Web Site Url"),TRUE);
            END ELSE BEGIN
              ContiniaUser.TESTFIELD("Activation Link");
              SmtpMailMgt.AppendToBody(Text002,TRUE);
              SmtpMailMgt.AppendLineBreakToBody;
              SmtpMailMgt.AppendToBody(STRSUBSTNO('<b>%1</b>',Text003),TRUE);
              IF ContiniaWebPortal."Use Continia Online Web Portal" THEN
                SmtpMailMgt.AppendToBody(STRSUBSTNO(Text004,ContiniaUser."E-Mail"),TRUE)
              ELSE
                SmtpMailMgt.AppendToBody(STRSUBSTNO(Text004,ContiniaUserMgt.GetNAVUserID(ContiniaUser."User ID")),TRUE);
              SmtpMailMgt.AppendToBody(STRSUBSTNO('%1<a href=%3>%2</a>',Text005,Text006,ContiniaUser."Activation Link"),TRUE);
              SmtpMailMgt.AppendLineBreakToBody;
              SmtpMailMgt.AppendToBody(STRSUBSTNO(Text007,ContiniaWebPortal."Web Site Url"),TRUE);
            END;

            SmtpMailMgt.AppendLineBreakToBody;
            SmtpMailMgt.AppendToBody(Text008,TRUE);
            SmtpMailMgt.AppendToBody('Continia Software A/S',TRUE);

            IF NOT SmtpMailMgt.Send THEN
              SmtpMailMgt.DisplayLastError;

            CLEAR(SmtpMailMgt);

            ContiniaUser2.GET(ContiniaUser."User ID");
            ContiniaUser2."Send Welcome E-mail" := FALSE;
            ContiniaUser2.MODIFY(TRUE);
            COMMIT;
          END;
        UNTIL ContiniaUser.NEXT = 0;

        IF GUIALLOWED THEN
          Window.CLOSE;
      END;
    END;

    PROCEDURE CalcProgress@161024017(TotalCount@161024012 : Integer;Index@161024013 : Integer) : Integer;
    BEGIN
      EXIT(ROUND(Index / TotalCount * 10000,1,'>'));
    END;

    PROCEDURE CreateDialogText@1160040003() : Text[1024];
    VAR
      SubText@1160040004 : ARRAY [8] OF Text[100];
    BEGIN
      SubText[1] := Text022;
      SubText[2] := Text023;
      SubText[3] := Text024;
      EXIT(CreateDialogText2(
        '%1\' +
        '%2 @1@@@@@@@@@@@@@\' +
        '%3 @2@@@@@@@@@@@@@\' +
        '%4 @3@@@@@@@@@@@@@',
        Text021,
        SubText));
    END;

    PROCEDURE CreateDialogText2@1160040005(MainText@1160040000 : Text[250];CaptionText@1160040005 : Text[100];SubText@1160040001 : ARRAY [8] OF Text[100]) DialogText : Text[1024];
    VAR
      NewSubText@1160040004 : ARRAY [8] OF Text[100];
      i@1160040003 : Integer;
      MaxSubTextLenght@1160040002 : Integer;
    BEGIN
      FOR i := 1 TO 8 DO
        IF STRLEN(SubText[i]) > MaxSubTextLenght THEN
          MaxSubTextLenght := STRLEN(SubText[i]);

      FOR i := 1 TO 8 DO
        NewSubText[i] := SubText[i] + PADSTR('',MaxSubTextLenght - STRLEN(SubText[i]),' ');

      DialogText := STRSUBSTNO(MainText,CaptionText,NewSubText[1],NewSubText[2],NewSubText[3],NewSubText[3],NewSubText[5],
        NewSubText[6],NewSubText[7],NewSubText[8]);
    END;

    LOCAL PROCEDURE SameWebPortal@6085574(CompanyName@6085573 : Text[30]) : Boolean;
    VAR
      ContiniaCompanySetup@6085574 : Record 6192773;
      ContiniaCompanySetup2@6085575 : Record 6192773;
    BEGIN
      ContiniaCompanySetup.GET;
      ContiniaCompanySetup2.CHANGECOMPANY(CompanyName);
      IF NOT ContiniaCompanySetup2.GET THEN
        EXIT(FALSE);

      IF ContiniaCompanySetup."Client ID" <> ContiniaCompanySetup2."Client ID" THEN
        EXIT(FALSE);

      IF ContiniaCompanySetup."Web Portal Code" = ContiniaCompanySetup2."Web Portal Code" THEN BEGIN
        ContiniaCompanySetup2.TESTFIELDCompanyCode(CompanyName);
        EXIT(TRUE);
      END ELSE
        EXIT(FALSE);
    END;

    PROCEDURE IsWindowsUser@1160040001(ContiniaUser@1160040000 : Record 6086001) : Boolean;
    BEGIN
      IF (ContiniaUser."NAV Login Type" = ContiniaUser."NAV Login Type"::Windows) AND
        (COMgt.GetUserWebServiceKey(ContiniaUser."User ID") = '')
      THEN
        EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

