OBJECT Table 5054 Contact Business Relation
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04,4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ContBusRel@1000 : Record 5054;
               Cont@1001 : Record 5050;
             BEGIN
               IF "No." <> '' THEN BEGIN
                 IF ContBusRel.FindByContact("Link to Table","Contact No.") THEN
                   ERROR(
                     Text000,
                     Cont.TABLECAPTION,"Contact No.",TABLECAPTION,"Link to Table",ContBusRel."No.");

                 IF ContBusRel.FindByRelation("Link to Table","No.") THEN
                   IF GetContactBusinessRelation(ContBusRel) THEN
                     ERROR(
                       Text000,
                       "Link to Table","No.",TABLECAPTION,Cont.TABLECAPTION,ContBusRel."Contact No.");

                 ContBusRel.RESET;
                 ContBusRel.SETRANGE("Contact No.","Contact No.");
                 ContBusRel.SETRANGE("Business Relation Code","Business Relation Code");
                 ContBusRel.SETRANGE("No.",'');
                 ContBusRel.DELETEALL;
               END;

               //**4PS.sn
               VendorCategory.UpdateFromContBusRelation(0, Rec, Rec);
               CentralDataMgt.UpdateBusinessRelation(Rec,1,xRec);
               //**4PS.en
             END;

    OnModify=BEGIN
               //**4PS.sn
               VendorCategory.UpdateFromContBusRelation(1, xRec, Rec);
               CentralDataMgt.UpdateBusinessRelation(Rec,2,xRec);
               //**4PS.en
             END;

    OnDelete=BEGIN
               //**4PS.sn
               VendorCategory.UpdateFromContBusRelation(2, xRec, Rec);
               CentralDataMgt.UpdateBusinessRelation(Rec,4,xRec);
               //**4PS.en
             END;

    OnRename=BEGIN
               //**4PS.sn
               VendorCategory.UpdateFromContBusRelation(1, xRec, Rec);
               //**4PS.en
             END;

    CaptionML=[ENU=Contact Business Relation;
               NOR=Kontaktens forr.forbindelse];
    LookupPageID=Page5062;
    DrillDownPageID=Page5061;
  }
  FIELDS
  {
    { 1   ;   ;Contact No.         ;Code20        ;TableRelation=Contact;
                                                   OnValidate=BEGIN
                                                                IF "Contact No." <> '' THEN
                                                                  VALIDATE("Business Relation Code");
                                                              END;

                                                   CaptionML=[ENU=Contact No.;
                                                              NOR=Kontaktnr.;
                                                              SVE=Kontaktnr];
                                                   NotBlank=Yes }
    { 2   ;   ;Business Relation Code;Code10      ;TableRelation="Business Relation";
                                                   OnValidate=VAR
                                                                RMSetup@1000 : Record 5079;
                                                                Cust@1001 : Record 18;
                                                                Vend@1002 : Record 23;
                                                                BankAcc@1003 : Record 270;
                                                              BEGIN
                                                                IF ("No." = '') AND
                                                                   ("Contact No." <> '') AND
                                                                   ("Business Relation Code" <> '') AND
                                                                   (CurrFieldNo <> 0)
                                                                THEN BEGIN
                                                                  RMSetup.GET;
                                                                  IF "Business Relation Code" = RMSetup."Bus. Rel. Code for Customers" THEN
                                                                    ERROR(Text001,
                                                                      FIELDCAPTION("Business Relation Code"),"Business Relation Code",
                                                                      Cont.TABLECAPTION,Cust.TABLECAPTION);
                                                                  IF "Business Relation Code" = RMSetup."Bus. Rel. Code for Vendors" THEN
                                                                    ERROR(Text001,
                                                                      FIELDCAPTION("Business Relation Code"),"Business Relation Code",
                                                                      Cont.TABLECAPTION,Vend.TABLECAPTION);
                                                                  IF "Business Relation Code" = RMSetup."Bus. Rel. Code for Bank Accs." THEN
                                                                    ERROR(Text001,
                                                                      FIELDCAPTION("Business Relation Code"),"Business Relation Code",
                                                                      Cont.TABLECAPTION,BankAcc.TABLECAPTION);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Business Relation Code;
                                                              NOR=Forretn.forbindelseskode;
                                                              SVE=Aff„rsrelationskod];
                                                   NotBlank=Yes }
    { 3   ;   ;Link to Table       ;Option        ;CaptionML=[ENU=Link to Table;
                                                              NOR=Knyttet til tabell;
                                                              SVE=L„nka till tabell];
                                                   OptionCaptionML=[ENU=" ,Customer,Vendor,Bank Account,Employee,Employer";
                                                                    NOR=" ,Kunde,Leverand›r,Bankkonto"];
                                                   OptionString=[ ,Customer,Vendor,Bank Account,Employee,Employer] }
    { 4   ;   ;No.                 ;Code20        ;TableRelation=IF (Link to Table=CONST(Customer)) Customer
                                                                 ELSE IF (Link to Table=CONST(Vendor)) Vendor
                                                                 ELSE IF (Link to Table=CONST(Bank Account)) "Bank Account"
                                                                 ELSE IF (Link to Table=CONST(Employer)) Employer
                                                                 ELSE IF (Link to Table=CONST(Employee)) Employee;
                                                   CaptionML=[ENU=No.;
                                                              NOR=Nr.;
                                                              SVE=Nr] }
    { 5   ;   ;Business Relation Description;Text100;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Business Relation".Description WHERE (Code=FIELD(Business Relation Code)));
                                                   CaptionML=[ENU=Business Relation Description;
                                                              NOR=Beskr. av forretn.forbindelse;
                                                              SVE=Aff„rsrelationsbeskrivning];
                                                   Editable=No }
    { 6   ;   ;Contact Name        ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=[ENU=Contact Name;
                                                              NOR=Kontaktnavn;
                                                              SVE=Kontaktnamn];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Contact No.,Business Relation Code      ;Clustered=Yes }
    {    ;Link to Table,No.                        }
    {    ;Link to Table,Contact No.                }
    {    ;Business Relation Code                   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 %2 already has a %3 with %4 %5.;NOR=%1 %2 har allerede en %3 med %4 %5.';
      Text001@1001 : TextConst 'ENU=%1 %2 is used when a %3 is linked with a %4.;NOR=%1 %2 brukes n†r en %3 er knyttet til en %4.';
      Cont@1002 : Record 5050;
      VendorCategory@1100529600 : Record 11071892;
      CentralDataMgt@1210190003 : Codeunit 11012010;

    LOCAL PROCEDURE GetContactBusinessRelation@2(ContactBusinessRelation@1000 : Record 5054) : Boolean;
    VAR
      Customer@1001 : Record 18;
      Vendor@1002 : Record 23;
      BankAccount@1003 : Record 270;
    BEGIN
      CASE ContactBusinessRelation."Link to Table" OF
        ContactBusinessRelation."Link to Table"::"Bank Account":
          EXIT(BankAccount.GET(ContactBusinessRelation."No."));
        ContactBusinessRelation."Link to Table"::Customer:
          EXIT(Customer.GET(ContactBusinessRelation."No."));
        ContactBusinessRelation."Link to Table"::Vendor:
          EXIT(Vendor.GET(ContactBusinessRelation."No."));
      END;
    END;

    [External]
    PROCEDURE FindByContact@3(LinkType@1000 : Option;ContactNo@1001 : Code[20]) : Boolean;
    BEGIN
      RESET;
      SETCURRENTKEY("Link to Table","Contact No.");
      SETRANGE("Link to Table",LinkType);
      SETRANGE("Contact No.",ContactNo);
      EXIT(FINDFIRST);
    END;

    [External]
    PROCEDURE FindByRelation@4(LinkType@1000 : Option;LinkNo@1001 : Code[20]) : Boolean;
    BEGIN
      RESET;
      SETCURRENTKEY("Link to Table","No.");
      SETRANGE("Link to Table",LinkType);
      SETRANGE("No.",LinkNo);
      EXIT(FINDFIRST);
    END;

    [External]
    PROCEDURE GetContactNo@1(LinkType@1000 : Option;LinkNo@1001 : Code[20]) : Code[20];
    BEGIN
      IF FindByRelation(LinkType,LinkNo) THEN
        EXIT("Contact No.");
      EXIT('');
    END;

    [External]
    PROCEDURE CreateRelation@5(ContactNo@1002 : Code[20];LinkNo@1004 : Code[20];LinkToTable@1003 : Option);
    BEGIN
      INIT;
      "Contact No." := ContactNo;
      "Business Relation Code" := GetBusinessRelationCodeFromSetup(LinkToTable);
      "Link to Table" := LinkToTable;
      "No." := LinkNo;
      INSERT(TRUE);
    END;

    [External]
    PROCEDURE FindOrRestoreContactBusinessRelation@7(VAR Cont@1000 : Record 5050;RecVar@1006 : Variant;LinkToTable@1002 : Option);
    VAR
      ContCompany@1004 : Record 5050;
      CustContUpdate@1008 : Codeunit 5056;
      VendContUpdate@1005 : Codeunit 5057;
      RecRef@1007 : RecordRef;
      FieldRef@1010 : FieldRef;
    BEGIN
      RecRef.GETTABLE(RecVar);
      FieldRef := RecRef.FIELD(1);

      IF NOT FindByRelation(LinkToTable,FORMAT(FieldRef.VALUE)) THEN
        IF Cont.Type = Cont.Type::Person THEN
          IF ContCompany.GET(Cont."Company No.") THEN BEGIN
            ContCompany.CheckForExistingRelationships(LinkToTable);
            CreateRelation(ContCompany."No.",FORMAT(FieldRef.VALUE),LinkToTable);
          END ELSE BEGIN
            CASE RecRef.NUMBER OF
              DATABASE::Customer:
                CustContUpdate.OnInsert(RecVar);
              DATABASE::Vendor:
                VendContUpdate.OnInsert(RecVar);
            END;
            FINDFIRST;
            Cont.VALIDATE("Company No.","Contact No.");
            Cont.MODIFY(TRUE);
          END;
    END;

    LOCAL PROCEDURE GetBusinessRelationCodeFromSetup@6(LinkToTable@1002 : Option) : Code[10];
    VAR
      MarketingSetup@1000 : Record 5079;
      ContactBusinessRelation@1001 : Record 5054;
    BEGIN
      MarketingSetup.GET;
      CASE LinkToTable OF
        ContactBusinessRelation."Link to Table"::Customer:
          BEGIN
            MarketingSetup.TESTFIELD("Bus. Rel. Code for Customers");
            EXIT(MarketingSetup."Bus. Rel. Code for Customers");
          END;
        ContactBusinessRelation."Link to Table"::Vendor:
          BEGIN
            MarketingSetup.TESTFIELD("Bus. Rel. Code for Vendors");
            EXIT(MarketingSetup."Bus. Rel. Code for Vendors");
          END;
      END;
    END;

    PROCEDURE UpdateEmptyNoForContact@8(EntityNo@1000 : Code[20];PrimaryContactNo@1001 : Code[20];LinkToTableOption@1002 : Option) : Boolean;
    VAR
      PersonContact@1005 : Record 5050;
      CompanyContact@1004 : Record 5050;
      ContactBusinessRelation@1003 : Record 5054;
    BEGIN
      IF (EntityNo = '') OR (PrimaryContactNo = '') THEN
        EXIT(FALSE);

      IF NOT PersonContact.GET(PrimaryContactNo) THEN
        EXIT(FALSE);

      IF PersonContact.Type <> PersonContact.Type::Person THEN
        EXIT(FALSE);

      IF CompanyContact.GET(PersonContact."Company No.") THEN
        IF ContactBusinessRelation.FindByContact(LinkToTableOption,CompanyContact."No.") THEN
          IF ContactBusinessRelation."No." = '' THEN BEGIN
            ContactBusinessRelation."No." := EntityNo;
            ContactBusinessRelation.MODIFY;
            EXIT(TRUE);
          END;

      EXIT(FALSE);
    END;

    BEGIN
    {
      4PS, 11-01-11, call C-026486, Field Contact No.: tablerelation changed:  WHERE (Type=CONST(Company)) removed
    }
    END.
  }
}

