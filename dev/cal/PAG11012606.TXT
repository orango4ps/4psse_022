OBJECT Page 11012606 Plant Need List
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 11020521=r;
    CaptionML=[DEU=Werkzeugbedarf šbersicht;
               ENU=Plant Need List;
               NLD=Materieelbehoefte overzicht;
               NOR=Maskinbehovsliste;
               SVE=Maskinbehovslista];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table11012579;
    PageType=List;
    OnOpenPage=BEGIN
                 IF CopyAllowed THEN
                   CurrPage.CAPTION := Text001;
                 IF MaintainAllowed THEN
                   CurrPage.CAPTION := Text002;
               END;

    OnAfterGetRecord=BEGIN
                       SetEditable;
                     END;

    OnQueryClosePage=BEGIN
                       IF CloseAction = ACTION::LookupOK THEN
                           LookupOKOnPush;
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1210190000;1;Group  ;
                GroupType=Repeater }

    { 1210190001;2;Field  ;
                SourceExpr=Location;
                Editable=FALSE }

    { 1100529000;2;Field  ;
                SourceExpr=Type;
                Editable=FALSE }

    { 1210190003;2;Field  ;
                SourceExpr="Plant Type";
                Editable=FALSE }

    { 1100529002;2;Field  ;
                SourceExpr="Item No.";
                Editable=FALSE }

    { 1100529001;2;Field  ;
                CaptionML=[DEU=Beschreibung;
                           ENU=Description;
                           NLD=Omschrijving;
                           NOR=Beskrivelse;
                           SVE=Beskrivning];
                SourceExpr=PlantOrItemDescription() }

    { 1210190005;2;Field  ;
                SourceExpr="Starting Date";
                Editable=FALSE }

    { 1210190007;2;Field  ;
                SourceExpr="Ending Date";
                Editable=FALSE }

    { 1210190009;2;Field  ;
                SourceExpr=Quantity;
                Editable=FALSE }

    { 1210190017;2;Field  ;
                CaptionML=[DEU=Einheit;
                           ENU=Unit of Measure;
                           NLD=Eenheid;
                           NOR=Enhet;
                           SVE=M†ttenhet];
                SourceExpr=UnitOfMeasureCode }

    { 1100525000;2;Field  ;
                CaptionML=[DEU=Kopierte Anzahl (Anfrage);
                           ENU=Copied Quantity (Request);
                           NLD=Gekopieerd aantal (aanvraag);
                           NOR=Kopiert antall (foresp›rsel);
                           SVE=Kopierat antal (beg„ran)];
                DecimalPlaces=0:5;
                SourceExpr=QuantityCopiedOnPlantRequests('') }

    { 1210190021;2;Field  ;
                CaptionML=[DEU=Kopierte Menge (Werkz.-Auftrag);
                           ENU=Copied Quantity (PO);
                           NLD=Gekopieerd aantal (MO);
                           NOR=Kopiert antall (PO);
                           SVE=Kopierat antal (PO)];
                SourceExpr=QuantityCopiedOnPlantOrders('') }

    { 1100529004;2;Field  ;
                CaptionML=[DEU=Gebuchte Anzahl (PO);
                           ENU=Quantity Posted (PO);
                           NLD=Aantal geboekt (MO)];
                SourceExpr=QuantityCopiedOnPostedPlantOrders('') }

    { 1210190016;2;Field  ;
                SourceExpr="Quantity To Copy";
                Editable=QuantityToCopyEDITABLE;
                OnValidate=VAR
                             CopiedQuantity@1100529000 : Decimal;
                           BEGIN
                             IF "Quantity To Copy" = 0 THEN BEGIN
                                 Finished := TRUE;
                                 "Delete Remainder" := FALSE;
                             END ELSE BEGIN
                               CopiedQuantity := QuantityCopiedLineTotal('');
                               IF "Quantity To Copy" < (Quantity - CopiedQuantity) THEN BEGIN
                                 IF CONFIRM(Text003,FALSE) THEN
                                   "Delete Remainder" := TRUE
                                 ELSE
                                   "Delete Remainder" := FALSE;
                               END;
                               IF "Quantity To Copy" > (Quantity - CopiedQuantity) THEN BEGIN
                                 "Delete Remainder" := FALSE;
                                 MESSAGE(Text004);
                               END;
                             END;
                           END;
                            }

    { 1210190015;2;Field  ;
                SourceExpr="Delete Remainder";
                Editable=DeleteRemainderEditable }

    { 1210190024;2;Field  ;
                SourceExpr=Finished;
                Editable=FinishedEDITABLE;
                OnValidate=BEGIN
                             IF Finished THEN BEGIN
                               "Quantity To Copy" := 0;
                               "Delete Remainder" := FALSE;
                             END ELSE BEGIN
                               "Quantity To Copy" := Quantity - QuantityCopiedLineTotal('');
                               IF "Quantity To Copy" < 0 THEN
                                 "Quantity To Copy" := 0;
                               "Delete Remainder" := FALSE;
                             END;
                             SetEditable;
                           END;
                            }

    { 1210190019;2;Field  ;
                SourceExpr="Plant Type Description";
                Visible=FALSE }

    { 1100529003;2;Field  ;
                SourceExpr="Item Description";
                Visible=FALSE }

    { 1100529005;2;Field  ;
                SourceExpr="From Depot";
                Visible=FALSE;
                Editable=FALSE }

  }
  CODE
  {
    VAR
      PlantOrderRec@1210190000 : Record 11012556;
      PlantRequestRec@1210190001 : Record 11020520;
      PlantOrderNo@1210190002 : Code[20];
      PlantToLocation@1100525000 : Code[20];
      PlantRequestNo@1100525001 : Code[20];
      CopyAllowed@1210190003 : Boolean;
      Text001@1210190010 : TextConst 'DEU=Werkzeugbedarf kopieren;ENU=Copy Plant Need;NLD=Materieelbehoefte kopi‰ren;NOR=Kopier maskinbehov;SVE=Kopiera Maskinsbehov';
      Text002@1210190011 : TextConst 'DEU=Werzeugbedarf abwickeln;ENU=Finish Plant Need;NLD=Materieelbehoefte afhandelen;NOR=Slutff›r maskinbehov;SVE=Slutf”r Maskinsbehov';
      Text003@1210190005 : TextConst 'DEU=Restliche Reservierung l”schen?;ENU=Delete the remaining reservation?;NLD=Resterende reservering verwijderen?;NOR=Fjerne gjenst†ende reservasjon?;SVE=Ta bort †terst†ende reservation?';
      Text004@1210190006 : TextConst 'DEU=Achtung: es wird mehr kopiert als dass reserviert worden ist.;ENU=Note, there will be copied more than is reserved;NLD="Let op, er wordt meer gekopieerd dan er gereserveerd is ";NOR=Obs: Det vil kopieres mer enn det som er reservert;SVE=Observera att mer „n vad som „r reserverat kommer att kopieras';
      MaintainAllowed@1210190007 : Boolean;
      FinishedEditable@1210190004 : Boolean INDATASET;
      QuantityToCopyEditable@1210190008 : Boolean INDATASET;
      DeleteRemainderEditable@1210190009 : Boolean INDATASET;

    LOCAL PROCEDURE SetEditable@1100529007();
    BEGIN
      FinishedEditable := (CopyAllowed OR MaintainAllowed) AND ("Document No." = '');
      QuantityToCopyEditable := (NOT Finished) AND (CopyAllowed OR MaintainAllowed) AND ("Document No." = '');
      DeleteRemainderEditable :=
        (Finished) OR ((NOT CopyAllowed) AND (NOT MaintainAllowed)) OR ("Document No." <> '') OR
        ("Quantity To Copy" = 0) OR ("Quantity To Copy" >= (Quantity - QuantityCopiedLineTotal('')));
    END;

    PROCEDURE InitCopyProcess@1210190001(IOrderNo@1210190002 : Code[20]);
    VAR
      PlantLocation@1100529400 : Record 11012554;
    BEGIN
      IF PlantOrderRec.GET(IOrderNo) THEN BEGIN
        IF (NOT PlantOrderRec.Posted) AND (PlantOrderRec."To Location" <> '') AND
           (PlantOrderRec.Type IN [PlantOrderRec.Type::Arrival, PlantOrderRec.Type::"Other Transfers"])
        THEN BEGIN
          IF PlantLocation.GET(PlantOrderRec."To Location") THEN
            CopyAllowed := (NOT PlantLocation.Depot);
        END;
        PlantOrderNo := IOrderNo;
        PlantToLocation := PlantOrderRec."To Location";
      END;
    END;

    PROCEDURE InitCopyProcessFromRequest@1100525000(IRequestNo@1210190002 : Code[20]);
    VAR
      PlantLocation@1100529400 : Record 11012554;
    BEGIN
      IF PlantRequestRec.GET(IRequestNo) THEN BEGIN
        IF (NOT PlantRequestRec.Posted) AND (PlantRequestRec.Status <= PlantRequestRec.Status::Requested) AND
           (PlantRequestRec."To Location" <> '') AND
           (PlantRequestRec.Type IN [PlantRequestRec.Type::Arrival, PlantRequestRec.Type::"Other Transfers"])
        THEN BEGIN
          IF PlantLocation.GET(PlantRequestRec."To Location") THEN
            CopyAllowed := (NOT PlantLocation.Depot);
        END;
        PlantRequestNo := IRequestNo;
        PlantToLocation := '';  //* Also another location is allowed (see call 17635)
      END;
    END;

    PROCEDURE InitMaintainProcess@1210190000();
    BEGIN
      MaintainAllowed := TRUE;
    END;

    PROCEDURE CopyLines@1210190002();
    VAR
      lvSelectedRec@1210190004 : Record 11012579;
    BEGIN
      // Copy the selected lines to the Plant Order
      CurrPage.SETSELECTIONFILTER(lvSelectedRec);

      lvSelectedRec.FILTERGROUP(10);
      IF PlantToLocation <> '' THEN
        lvSelectedRec.SETRANGE(Location, PlantToLocation);
      lvSelectedRec.SETRANGE(Finished, FALSE);
      lvSelectedRec.SETRANGE("Document No.", '');  //* Als vanuit verkoopofferte/order (verhuurcontract) dan overslaan
      lvSelectedRec.FILTERGROUP(0);
      IF NOT lvSelectedRec.FINDFIRST THEN
        EXIT;

      IF PlantOrderNo <> '' THEN
        CopyLinesToPlantOrder(lvSelectedRec, PlantOrderNo)
      ELSE BEGIN
        IF PlantRequestNo <> '' THEN
          CopyLinesToPlantRequest(lvSelectedRec, PlantRequestNo);
      END;
    END;

    LOCAL PROCEDURE LookupOKOnPush@19031339();
    BEGIN
      IF CopyAllowed THEN
        CopyLines();
    END;

    BEGIN
    END.
  }
}

