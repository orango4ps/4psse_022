OBJECT Codeunit 2135 O365 Setup Email
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.01;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            SetupEmail(FALSE);
          END;

  }
  CODE
  {
    VAR
      MailNotConfiguredErr@1000 : TextConst 'ENU=An email account must be configured to send emails.;NOR=En e-postkonto m† konfigureres for † sende e-poster.;SVE=Ett e-postkonto m†ste konfigureras f”r att du ska kunna skicka e-post.';
      ClientTypeManagement@1001 : Codeunit 4030;

    [External]
    PROCEDURE SilentSetup@5();
    VAR
      GraphMail@1001 : Codeunit 405;
    BEGIN
      IF GraphMail.IsEnabled OR SMTPEmailIsSetUp THEN
        EXIT;

      IF NOT GraphMail.HasConfiguration THEN
        ERROR(MailNotConfiguredErr);

      GraphMail.SetupGraph(FALSE);

      IF NOT GraphMail.IsEnabled THEN
        ERROR(MailNotConfiguredErr);
    END;

    [External]
    PROCEDURE SetupEmail@15(ForceSetup@1000 : Boolean);
    VAR
      GraphMail@1001 : Codeunit 405;
    BEGIN
      IF (NOT GraphMail.IsEnabled) AND SMTPEmailIsSetUp THEN BEGIN
        SetupSmtpEmail(ForceSetup);
        EXIT;
      END;

      IF NOT GraphMail.HasConfiguration THEN BEGIN
        SetupSmtpEmail(ForceSetup);
        EXIT;
      END;

      IF NOT GraphMail.IsEnabled THEN
        IF NOT GraphMail.UserHasLicense THEN BEGIN
          SetupSmtpEmail(ForceSetup);
          EXIT;
        END;

      SetupGraphEmail(ForceSetup);
    END;

    LOCAL PROCEDURE SetupGraphEmail@2(ForceSetup@1000 : Boolean);
    VAR
      GraphMail@1001 : Codeunit 405;
      RunSetup@1002 : Boolean;
    BEGIN
      RunSetup := ForceSetup;
      IF NOT RunSetup THEN BEGIN
        IF NOT GraphMail.IsEnabled THEN
          IF NOT SMTPEmailIsSetUp THEN
            RunSetup := TRUE;
      END;

      IF RunSetup THEN
        IF NOT GraphMail.SetupGraph(ForceSetup) THEN // Returns if graph mail was setup
          IF NOT SMTPEmailIsSetUp THEN BEGIN // If it wasn't maybe smtp was configured instead
            IF ClientTypeManagement.GetCurrentClientType <> CLIENTTYPE::Phone THEN
              ERROR('');
            ERROR(MailNotConfiguredErr);
          END;
    END;

    LOCAL PROCEDURE SetupSmtpEmail@3(ForceSetup@1000 : Boolean);
    BEGIN
      IF ForceSetup OR (NOT SMTPEmailIsSetUp) THEN BEGIN
        IF NOT GUIALLOWED THEN
          ERROR(MailNotConfiguredErr);

        IF PAGE.RUNMODAL(PAGE::"BC O365 Email Setup Wizard") <> ACTION::LookupOK THEN
          ERROR('');

        IF NOT SMTPEmailIsSetUp THEN
          ERROR(MailNotConfiguredErr);
      END;
    END;

    [Internal]
    PROCEDURE SMTPEmailIsSetUp@1() : Boolean;
    VAR
      SMTPMailSetup@1001 : Record 409;
      MailManagement@1002 : Codeunit 9520;
    BEGIN
      IF NOT MailManagement.IsSMTPEnabled THEN
        EXIT(FALSE);

      IF SMTPMailSetup.GetSetup THEN
        EXIT((SMTPMailSetup."User ID" <> '') AND (SMTPMailSetup.GetPassword <> ''));
    END;

    BEGIN
    END.
  }
}

