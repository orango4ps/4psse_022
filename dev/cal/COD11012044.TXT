OBJECT Codeunit 11012044 Convert Amount To Text
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    [External]
    PROCEDURE Zegge@1210190005(g@1210190000 : Decimal) : Text[1000];
    VAR
      m@1210190001 : Integer;
      r@1210190003 : Decimal;
      s@1210190002 : Text[1000];
      c@1210190004 : Text[1000];
      v1@1210190005 : Text[30];
      v2@1210190006 : Text[30];
    BEGIN
      s := '';
      v1 := ' euro';
      v2 := ' cent';
      c := '';

      IF g < 0 THEN BEGIN
        g := g * -1;
        s := 'min ';
      END;

      r := g MOD 1;
      IF r <> 0 THEN BEGIN
        g := g - r;
        r := r * 100;
        c := ' en ' + Translate100(r) + v2;
      END;

      IF g = 0 THEN
        s := 'nul';

      IF g >= 1000000000 THEN BEGIN
        m := g DIV 1000000000;
        g := g - (m * 1000000000);
        s := s + Translate1000(m) + 'miljard';
      END;

      IF g >= 1000000 THEN BEGIN
        m := g DIV 1000000;
        g := g - (m * 1000000);
        s := s + Translate1000(m) + 'miljoen';
      END;

      IF g >= 1000 THEN BEGIN
        m := g DIV 1000;
        g := g - (m * 1000);
        s := s + Translate1000(m) + 'duizend';
      END;

      EXIT ( s + Translate1000(g) + v1 + c );
    END;

    [External]
    PROCEDURE Translate1@1210190001(g@1210190000 : Decimal) : Text[1000];
    BEGIN
      CASE g OF
        1:  EXIT('een');
        2:  EXIT('twee');
        3:  EXIT('drie');
        4:  EXIT('vier');
        5:  EXIT('vijf');
        6:  EXIT('zes');
        7:  EXIT('zeven');
        8:  EXIT('acht');
        9:  EXIT('negen');
      END;
    END;

    [External]
    PROCEDURE Translate10@1210190002(g@1210190000 : Decimal) : Text[1000];
    BEGIN
      CASE g OF
        1:  EXIT('tien');
        2:  EXIT('twintig');
        3:  EXIT('dertig');
        4:  EXIT('veertig');
        5:  EXIT('vijftig');
        6:  EXIT('zestig');
        7:  EXIT('zeventig');
        8:  EXIT('tachtig');
        9:  EXIT('negentig');
      END;
    END;

    [External]
    PROCEDURE Translate100@1210190003(g@1210190000 : Decimal) : Text[1000];
    VAR
      t@1210190001 : Integer;
      e@1210190002 : Integer;
    BEGIN
      t := g DIV 10;
      e := g - (t * 10);

      IF e = 0 THEN
        EXIT( Translate10(t) );

      IF g < 10 THEN
        EXIT( Translate1(g) );

      IF g < 20 THEN BEGIN
        CASE g OF
          10:  EXIT( Translate10(1) );
          11:  EXIT('elf');
          12:  EXIT('twaalf');
          13:  EXIT('dertien');
          14:  EXIT('veertien');
          15,16,17,18,19:  EXIT( Translate1(e) + Translate10(1) );
        END;
      END;

      EXIT( Translate1(e) + 'en' + Translate10(t) );
    END;

    [External]
    PROCEDURE Translate1000@1210190004(g@1210190000 : Decimal) : Text[1000];
    VAR
      m@1210190001 : Integer;
      s@1210190002 : Text[1000];
    BEGIN
      s := '';
      IF g >= 100 THEN BEGIN
        m := g DIV 100;
        g := g - (m * 100);
        s := Translate1(m) + 'honderd';
      END;
      EXIT( s + Translate100(g) );
    END;

    BEGIN
    END.
  }
}

