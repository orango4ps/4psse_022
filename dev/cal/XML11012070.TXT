OBJECT XMLport 11012070 Import GL Entries Microloon
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Import GL Entries Microloon;
               SVE=Importera redovisningstransaktioner Microloon];
    Direction=Import;
    DefaultFieldsValidation=No;
    PreserveWhiteSpace=Yes;
    TextEncoding=WINDOWS;
    OnInitXMLport=BEGIN
                    HumanResourcesSetup.GET;

                    HumanResourcesSetup.TESTFIELD("GL Entry Journal Template");
                    HumanResourcesSetup.TESTFIELD("GL Entry Journal Batch");
                    HumanResourcesSetup.TESTFIELD("Salary Application",HumanResourcesSetup."Salary Application"::Microloon);

                    currXMLport.FILENAME(HumanResourcesSetup."Pathname Files from Salary");
                  END;

    OnPreXMLport=BEGIN
                   GenJnlLine.LOCKTABLE;
                   GenJnlLine.SETRANGE("Journal Template Name",HumanResourcesSetup."GL Entry Journal Template");
                   GenJnlLine.SETRANGE("Journal Batch Name",HumanResourcesSetup."GL Entry Journal Batch");
                   IF GenJnlLine.FIND('+') THEN
                     LineCounter := GenJnlLine."Line No."
                   ELSE
                     LineCounter := 0;

                   JnlTemplRec.GET(HumanResourcesSetup."GL Entry Journal Template");
                   JnlBatchRec.GET(HumanResourcesSetup."GL Entry Journal Template", HumanResourcesSetup."GL Entry Journal Batch");

                   IF JnlBatchRec."No. Series" <> '' THEN
                     DocNo := NoSeriesMgt.GetNextNo(JnlBatchRec."No. Series",0D,FALSE)
                   ELSE
                     IF JnlTemplRec."No. Series" <> '' THEN
                       DocNo := NoSeriesMgt.GetNextNo(JnlTemplRec."No. Series",0D,FALSE);
                 END;

    OnPostXMLport=BEGIN
                    OldFile := currXMLport.FILENAME;
                    NewFile := HumanResourcesSetup.DetermineBackupFile(OldFile);
                    IF NewFile <> OldFile THEN
                      FileMgt.RenameClientFile(OldFile, NewFile, TRUE);
                  END;

    Format=Variable Text;
    UseRequestPage=No;
  }
  ELEMENTS
  {
    { [{7DEC0337-97FE-418F-A127-CAB0756A54C5}];  ;Root                ;Element ;Text     }

    { [{CB2B77F9-060A-4A34-A784-D40F1974A43B}];1 ;GenJournalLine      ;Element ;Table   ;
                                                  SourceTable=Table81;
                                                  SourceTableView=SORTING(Field1,Field51,Field2);
                                                  Import::OnAfterInitRecord=BEGIN
                                                                              "Gen. Journal Line".INIT;
                                                                              CLEAR(Date);
                                                                              CLEAR(DescTxt);
                                                                              CLEAR(ValueTxt);
                                                                              CLEAR(Sign);
                                                                            END;

                                                  Import::OnBeforeInsertRecord=VAR
                                                                                 RecRef@1100528400 : RecordRef;
                                                                               BEGIN
                                                                                 WITH "Gen. Journal Line" DO BEGIN //With statement added because this code has been copied from dataport.

                                                                                 IF "Account No." = '1' THEN
                                                                                   currXMLport.SKIP;

                                                                                 IF "Bal. Account No." = '1' THEN
                                                                                   "Bal. Account No." := '';

                                                                                 "Journal Template Name" := HumanResourcesSetup."GL Entry Journal Template";
                                                                                 "Journal Batch Name" := HumanResourcesSetup."GL Entry Journal Batch";
                                                                                 "Line No." := LineCounter + 10000;
                                                                                 LineCounter := "Line No.";
                                                                                 "Account Type" := "Account Type"::"G/L Account";
                                                                                 "Document No." := DocNo;

                                                                                 GlAccountRec.GET("Account No.");
                                                                                 IF NOT GlAccountRec."Direct Posting" THEN BEGIN
                                                                                   "Gen. Posting Type" := GlAccountRec."Gen. Posting Type";
                                                                                   "Gen. Bus. Posting Group" := GlAccountRec."Gen. Bus. Posting Group";
                                                                                   "Gen. Prod. Posting Group" := GlAccountRec."Gen. Prod. Posting Group";
                                                                                   "VAT Bus. Posting Group" := GlAccountRec."VAT Bus. Posting Group";
                                                                                   "VAT Prod. Posting Group" := GlAccountRec."VAT Prod. Posting Group";
                                                                                 END ELSE
                                                                                   VALIDATE("Account No.");

                                                                                 IF "Bal. Account No." <> '' THEN BEGIN
                                                                                   GlAccountRec.GET("Bal. Account No.");
                                                                                   IF NOT GlAccountRec."Direct Posting" THEN BEGIN
                                                                                     "Bal. Gen. Posting Type" := GlAccountRec."Gen. Posting Type";
                                                                                     "Bal. Gen. Bus. Posting Group" := GlAccountRec."Gen. Bus. Posting Group";
                                                                                     "Bal. Gen. Prod. Posting Group" := GlAccountRec."Gen. Prod. Posting Group";
                                                                                     "Bal. VAT Bus. Posting Group" := GlAccountRec."VAT Bus. Posting Group";
                                                                                     "Bal. VAT Prod. Posting Group" := GlAccountRec."VAT Prod. Posting Group";
                                                                                   END ELSE
                                                                                     VALIDATE("Bal. Account No.");
                                                                                 END;

                                                                                 IF Sign = '-' THEN Value := -Value;
                                                                                 VALIDATE(Amount, Value);

                                                                                 Description := DescTxt;

                                                                                 IF Date <> '' THEN BEGIN
                                                                                   EVALUATE(BookYear,COPYSTR(Date,5,2));
                                                                                   EVALUATE(BookMonth,COPYSTR(Date,3,2));
                                                                                   EVALUATE(BookDay,COPYSTR(Date,1,2));
                                                                                   "Posting Date" := DMY2DATE(BookDay,BookMonth,BookYear+2000);
                                                                                 END ELSE
                                                                                   "Posting Date" := TODAY;
                                                                                 "Document Date" := "Posting Date";

                                                                                 "Source Code"   := JnlTemplRec."Source Code";
                                                                                 IF JnlBatchRec."Reason Code" <> '' THEN
                                                                                   "Reason Code" := JnlBatchRec."Reason Code"
                                                                                 ELSE
                                                                                   "Reason Code" := JnlTemplRec."Reason Code";
                                                                                 "Origin Salary Application" := TRUE;

                                                                                 VALIDATE("Shortcut Dimension 1 Code");

                                                                                 RecRef.GETTABLE("Gen. Journal Line");
                                                                                 XMLportStatusDialogMgt.ShowImportStatus(RecRef);

                                                                                 INSERT;

                                                                                 currXMLport.SKIP;

                                                                                 END; //Belongs to With statement.
                                                                               END;
                                                                                }

    { [{A6EEBF2A-DF05-4B29-ABC7-C38196875724}];2 ;Filler1             ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{16BB48E0-9473-40A4-8178-F2BCF8340A5C}];2 ;Filler2             ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{0C667FED-71F3-4A9F-9233-624DD768B5FF}];2 ;Filler3             ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{8C1A4342-3EDE-49E2-932E-D1673F2394EC}];2 ;Filler4             ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{12ACD1AD-70A5-4C00-8089-FA4207F5E984}];2 ;Date                ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{ADAB1A7F-319B-4B8F-B222-BC90E5083355}];2 ;AccountNo           ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=Gen. Journal Line::Account No.;
                                                  MinOccurs=Zero }

    { [{EB86E3FD-BDEC-4395-AEBB-4869AC7C3202}];2 ;BalAccountNo        ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=Gen. Journal Line::Bal. Account No.;
                                                  MinOccurs=Zero }

    { [{2AB35A66-09E3-4912-BF7C-747D90C0CF2D}];2 ;ShortcutDimension1Code;Element;Field  ;
                                                  DataType=Code;
                                                  SourceField=Gen. Journal Line::Shortcut Dimension 1 Code;
                                                  MinOccurs=Zero }

    { [{8CD68919-D7AB-49BC-B141-6661EB4DA34C}];2 ;Filler5             ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{9A78494F-007B-4AFA-8BCD-019E3E6A34D9}];2 ;DescTxt             ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{BC82E581-1E2D-4791-884B-D77B7196EC43}];2 ;Filler6             ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{1157B5DB-0965-40F4-8874-2795C35A4655}];2 ;Filler7             ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{A81DCBF8-DD85-48BB-AED4-331D652ADFE3}];2 ;Filler8             ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{9E63CD50-92DC-4D2C-BBB3-F789AF47CA28}];2 ;ValueTxt            ;Element ;Text    ;
                                                  MinOccurs=Zero;
                                                  Import::OnAfterAssignVariable=BEGIN
                                                                                  FillEmptyTextWithZero(ValueTxt);
                                                                                  EVALUATE(Value, ValueTxt);
                                                                                END;
                                                                                 }

    { [{98F736D3-E74E-491F-B5B5-E0A81C7F21C8}];2 ;Sign                ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{619DFBA3-27F9-4D5D-8011-2F8E9E1430C6}];2 ;Filler9             ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{ACA26A84-6F44-4D51-9A12-3B138BC91AED}];2 ;Filler10            ;Element ;Text    ;
                                                  MinOccurs=Zero }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      HumanResourcesSetup@1100528417 : Record 5218;
      GenJnlLine@1100528416 : Record 81;
      JnlTemplRec@1100528415 : Record 80;
      JnlBatchRec@1100528414 : Record 232;
      GlAccountRec@1100528413 : Record 15;
      NoSeriesMgt@1100528412 : Codeunit 396;
      FileMgt@1100528401 : Codeunit 419;
      XMLportStatusDialogMgt@1100528404 : Codeunit 11130006;
      LineCounter@1100528411 : Integer;
      BookYear@1100528410 : Integer;
      BookMonth@1100528409 : Integer;
      BookDay@1100528408 : Integer;
      Value@1100528406 : Decimal;
      OldFile@1100528403 : Text[250];
      NewFile@1100528402 : Text[250];
      DocNo@1100528400 : Text[30];

    PROCEDURE StripZero@1(VAR convtext@11012000 : Text[30]);
    VAR
      Position@11012001 : Integer;
    BEGIN
      Position := STRPOS(convtext,'0');
      WHILE Position = 1 DO
        BEGIN
          convtext := DELSTR(convtext,Position,1);
          Position  := STRPOS(convtext,'0');
        END;
    END;

    PROCEDURE FillEmptyTextWithZero@1100525013(VAR Text@1100525000 : Text[1024]);
    BEGIN
      IF DELCHR(Text,'<>=',' ') = '' THEN
        Text := '0';
    END;

    BEGIN
    {
      CC: Dataport 11012333
    }
    END.
  }
}

