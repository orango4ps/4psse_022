OBJECT Table 11072214 Change Request Task
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               ChangeRequestTaskHourAcc@1100528300 : Record 11072222;
             BEGIN
               IF (Status > Status::Received) THEN
                 FIELDERROR(Status);
               ChangeRequestTaskHourAcc.SETRANGE("Change Request No.", "Change Request No.");
               ChangeRequestTaskHourAcc.SETRANGE("Task Seq. No.", "Seq. No.");
               IF (ChangeRequestTaskHourAcc.FINDFIRST) THEN
                 ERROR(Text000, ChangeRequestTaskHourAcc.TABLECAPTION);
             END;

    CaptionML=ENU=Change Request Task;
    LookupPageID=Page11126267;
    DrillDownPageID=Page11126267;
  }
  FIELDS
  {
    { 10  ;   ;Change Request No.  ;Code20        ;TableRelation="Change Request";
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Change Request Description");
                                                              END;

                                                   CaptionML=[ENU=Change Request No.;
                                                              SVE=Žndringsbeg„ransnr];
                                                   NotBlank=Yes }
    { 11  ;   ;Change Request Description;Text50  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Change Request".Description WHERE (No.=FIELD(Change Request No.)));
                                                   CaptionML=[ENU=Change Request Description;
                                                              SVE=Žndringsbeg„ran, beskrivning];
                                                   Editable=No }
    { 20  ;   ;Seq. No.            ;Integer       ;CaptionML=[ENU=Seq. No.;
                                                              NOR=Sekv. nr;
                                                              SVE=Sekv. nr] }
    { 30  ;   ;Task                ;Code20        ;TableRelation="Default Change Request Task";
                                                   OnValidate=BEGIN
                                                                FillFieldsFromDefault;
                                                              END;

                                                   OnLookup=VAR
                                                              DefaultChangeRequestTask@1100528301 : Record 11072224;
                                                              DefaultChangeRequestTasks@1100528300 : Page 11130546;
                                                            BEGIN
                                                              DefaultChangeRequestTasks.SetCopyTaskBtnVisible("Change Request No.", TRUE);
                                                              DefaultChangeRequestTask."No." := Task;
                                                              DefaultChangeRequestTasks.SETRECORD(DefaultChangeRequestTask);
                                                              DefaultChangeRequestTasks.LOOKUPMODE := TRUE;
                                                              IF (DefaultChangeRequestTasks.RUNMODAL = ACTION::LookupOK) THEN BEGIN
                                                              //message('RetVal: %1', DefaultChangeRequestTasks.RUNMODAL);
                                                                DefaultChangeRequestTasks.GETRECORD(DefaultChangeRequestTask);
                                                                Task := DefaultChangeRequestTask."No.";
                                                              END;
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Task;
                                                              NOR=Oppgave;
                                                              SVE=Uppgift];
                                                   NotBlank=Yes }
    { 40  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 50  ;   ;Responsible Employee;Code20        ;TableRelation=Employee;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Responsible Employee Name");
                                                              END;

                                                   CaptionML=[ENU=Responsible Employee;
                                                              NOR=Ansvarlig Ansatt;
                                                              SVE=Ansvarig] }
    { 51  ;   ;Responsible Employee Name;Text100  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Employee."Full Name" WHERE (No.=FIELD(Responsible Employee)));
                                                   CaptionML=[ENU=Responsible Employee Name;
                                                              NOR=Navn p† Ansvarlig Ansatt;
                                                              SVE=Namn p† ansvarig anst„lld];
                                                   Editable=No }
    { 60  ;   ;Due Date            ;Date          ;CaptionML=[ENU=Due Date;
                                                              NOR=Forfallsdato;
                                                              SVE=F”rfallodatum] }
    { 70  ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              NOR=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=ENU=Received,In Process,Finished;
                                                   OptionString=Received,In Process,Finished }
    { 80  ;   ;Object No.          ;Code20        ;TableRelation=Service-Object;
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Object Description");
                                                              END;

                                                   CaptionML=[ENU=Object No.;
                                                              NOR=Objektnr.;
                                                              SVE=Objektnr] }
    { 81  ;   ;Object Description  ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object.Description WHERE (No.=FIELD(Object No.)));
                                                   CaptionML=[ENU=Object Description;
                                                              NOR=Objektbeskrivelse;
                                                              SVE=Objektbeskrivning];
                                                   Editable=No }
    { 90  ;   ;On Hold Code        ;Code20        ;TableRelation="On Hold Code";
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("On Hold Description");
                                                              END;

                                                   CaptionML=[ENU=On Hold Code;
                                                              NOR=P† vent-kode;
                                                              SVE=Stoppkod] }
    { 91  ;   ;On Hold Description ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("On Hold Code".Description WHERE (Code=FIELD(On Hold Code)));
                                                   CaptionML=ENU=On Hold Description;
                                                   Editable=No }
    { 100 ;   ;Lead Time (Hrs)     ;Decimal       ;OnValidate=BEGIN
                                                                "Lead Time (Hrs)" := ROUND("Lead Time (Hrs)", 0.25);
                                                              END;

                                                   CaptionML=ENU=Lead Time (Hrs);
                                                   DecimalPlaces=2:2 }
    { 200 ;   ;Total Hours         ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Change Request Task Hour Acc".Hours WHERE (Change Request No.=FIELD(Change Request No.),
                                                                                                               Task Seq. No.=FIELD(Seq. No.)));
                                                   CaptionML=[ENU=Total Hours;
                                                              NOR=Timer totalt;
                                                              SVE=Timmar totalt];
                                                   Editable=No }
    { 201 ;   ;Customer No.        ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Change Request"."Customer No." WHERE (No.=FIELD(Change Request No.)));
                                                   TableRelation=Customer.No.;
                                                   CaptionML=[ENU=Customer No.;
                                                              NOR=Kundenr.;
                                                              SVE=Kundnr];
                                                   Editable=No }
    { 202 ;   ;Customer Name       ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Customer.Name WHERE (No.=FIELD(Customer No.)));
                                                   CaptionML=[ENU=Customer Name;
                                                              NOR=Kundnavn;
                                                              SVE=Kundens namn];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Change Request No.,Seq. No.             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528301 : TextConst 'ENU=There are already %1 record(s) present.';

    PROCEDURE FillFieldsFromDefault@1100528300();
    VAR
      DefaultChangeRequestTask@1100528300 : Record 11072224;
    BEGIN
      IF (DefaultChangeRequestTask.GET(Task)) THEN BEGIN
        Description := DefaultChangeRequestTask.Description;
        "Responsible Employee" := DefaultChangeRequestTask."Responsible Employee";
        "Lead Time (Hrs)" := DefaultChangeRequestTask."Lead Time (Hrs)";
      END;
    END;

    PROCEDURE AddHourLine@1100528301();
    VAR
      ChangeRequestTaskHourAcc@1100528300 : Record 11072222;
      UserSetup@1100528302 : Record 91;
      Employee@1100528303 : Record 5200;
      InputHourAccChangeRequest@1100528301 : Page 11130544;
    BEGIN
      UserSetup.GET(USERID);
      UserSetup.TESTFIELD("Employee No.");
      Employee.GET(UserSetup."Employee No.");
      IF (NOT ChangeRequestTaskHourAcc.GET("Change Request No.", "Seq. No.", UserSetup."Employee No.",
          DATE2DWY(TODAY, 3), DATE2DWY(TODAY, 2), DATE2DWY(TODAY, 1)))
      THEN BEGIN
        ChangeRequestTaskHourAcc.INIT;
        ChangeRequestTaskHourAcc."Change Request No." := "Change Request No.";
        ChangeRequestTaskHourAcc."Task Seq. No." := "Seq. No.";
        ChangeRequestTaskHourAcc."Employee No." := UserSetup."Employee No.";
        ChangeRequestTaskHourAcc.Year := DATE2DWY(TODAY, 3);
        ChangeRequestTaskHourAcc.Week := DATE2DWY(TODAY, 2);
        ChangeRequestTaskHourAcc.Day := DATE2DWY(TODAY, 1);
        ChangeRequestTaskHourAcc.INSERT(TRUE);
      END;
      ChangeRequestTaskHourAcc.TESTFIELD(Processed, FALSE);
      ChangeRequestTaskHourAcc.CALCFIELDS(Task, "Employee Name");
      InputHourAccChangeRequest.SetVars(ChangeRequestTaskHourAcc.Task, ChangeRequestTaskHourAcc."Employee Name",
        ChangeRequestTaskHourAcc.Year, ChangeRequestTaskHourAcc.Week, ChangeRequestTaskHourAcc.Day, ChangeRequestTaskHourAcc.Hours);
      COMMIT;
      IF (InputHourAccChangeRequest.RUNMODAL = ACTION::OK) THEN BEGIN
        ChangeRequestTaskHourAcc.VALIDATE(Hours, InputHourAccChangeRequest.GetHourQty);
        IF (ChangeRequestTaskHourAcc.Hours = 0) THEN
          ChangeRequestTaskHourAcc.DELETE
        ELSE
          ChangeRequestTaskHourAcc.MODIFY(TRUE);
      END;
    END;

    PROCEDURE GetLastSeqNo@1100528302() : Integer;
    VAR
      ChangeRequestTask@1100528300 : Record 11072214;
    BEGIN
      ChangeRequestTask.SETRANGE("Change Request No.", "Change Request No.");
      IF (ChangeRequestTask.FINDLAST) THEN
        EXIT(ChangeRequestTask."Seq. No.");
      EXIT(10000);
    END;

    PROCEDURE StartTasksChart@1100409000();
    VAR
      ChangeRequestTasksChart@1100409000 : Page 11229224;
    BEGIN
      ChangeRequestTasksChart.SetChangeRequestFilter("Change Request No.");
      ChangeRequestTasksChart.RUNMODAL;
    END;

    PROCEDURE StartHoursChart@1100409001();
    VAR
      ChangeRequestHoursChart@1100409000 : Page 11229225;
    BEGIN
      ChangeRequestHoursChart.SetChangeRequestFilter("Change Request No.");
      ChangeRequestHoursChart.RUNMODAL;
    END;

    BEGIN
    END.
  }
}

