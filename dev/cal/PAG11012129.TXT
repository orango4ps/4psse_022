OBJECT Page 11012129 Vendors by Purchase Action
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Kreditoren pro Einkaufsmarkierung;
               ENU=Vendors by Purchase Action;
               NLD=Leveranciers per inkoopactie;
               NOR=Leverandõrer etter Innkjõpshandling;
               SVE=Leverantîrer efter inkîpsÜtgÑrd];
    SourceTable=Table11012068;
    DataCaptionFields=Purchase Action;
    PageType=List;
    OnOpenPage=VAR
                 Job@1100527800 : Record 11072003;
               BEGIN
                 IF (NOT Job.GET("Project No.")) THEN
                   Job.INIT;
                 QuoteSubsitePresent := Job."Quote Subsite Generated";
               END;

    OnAfterGetRecord=BEGIN
                       CalcFieldsVendor();
                       OnAfterGetCurrRecord;
                     END;

    OnNewRecord=BEGIN
                  OnAfterGetCurrRecord;
                END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1210190008;1 ;Action    ;
                      CaptionML=[DEU=&Filter;
                                 ENU=F&ilter;
                                 NLD=&Filter;
                                 NOR=F&ilter;
                                 SVE=F&ilter];
                      Promoted=Yes;
                      Image=UseFilters;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 lvPurchActFeatRec@1100485001 : Record 11012069;
                               BEGIN
                                 FilterFeaturesRec.RESET;
                                 FilterFeaturesRec.SETRANGE(User,USERID);
                                 FilterFeaturesRec.DELETEALL;
                                 ContFeatGrpRec.SETRANGE("Purchase Actions",TRUE);
                                 IF ContFeatGrpRec.FIND('-') THEN BEGIN
                                   REPEAT
                                     ContFeatRec.SETRANGE("Feature Group",ContFeatGrpRec.Description);
                                     IF ContFeatRec.FIND('-') THEN BEGIN
                                       REPEAT
                                         FilterFeaturesRec.INIT;
                                         FilterFeaturesRec.User := USERID;
                                         FilterFeaturesRec."Contact Feature Group" := ContFeatRec."Feature Group";
                                         FilterFeaturesRec."Contact Feature" := ContFeatRec.Code;

                                         IF "Estimate No." <> '' THEN
                                           lvPurchActFeatRec.Source := PurchActFeatRec.Source::Estimate
                                         ELSE
                                           lvPurchActFeatRec.Source := PurchActFeatRec.Source::Project;

                                         IF PurchActFeatRec.GET(lvPurchActFeatRec.Source,
                                                                "Estimate No.",
                                                                "Project No.",
                                                                "Purchase Action",
                                                                ContFeatRec."Feature Group",
                                                                ContFeatRec.Code) THEN
                                           FilterFeaturesRec.Filter := TRUE;
                                         FilterFeaturesRec.INSERT;
                                       UNTIL ContFeatRec.NEXT = 0;
                                     END;
                                   UNTIL ContFeatGrpRec.NEXT = 0;
                                 END;

                                 COMMIT;
                                 IF "Estimate No." <> '' THEN
                                   FilterFeatFrm.SetEstNo("Estimate No." ,"Purchase Action")
                                 ELSE
                                   FilterFeatFrm.SetProjectNo("Project No.","Purchase Action");

                                 FilterFeatFrm.LOOKUPMODE(FALSE);
                                 FilterFeatFrm.RUNMODAL;
                                 CLEAR(FilterFeatFrm);

                                 FilterFeaturesRec.DELETEALL;
                               END;
                                }
      { 1100528300;1 ;ActionGroup;
                      CaptionML=[DEU=Dokument-Portal;
                                 ENU=Document Portal;
                                 NLD=Documentenportaal;
                                 SVE=Dokumentportal];
                      Visible=QuoteSubsitePresent;
                      Image=Web }
      { 1100528301;2 ;Action    ;
                      CaptionML=[DEU=Angebotsanfrage-E-Mail erneut senden;
                                 ENU=Resend Quote Request E-mail;
                                 NLD=Offerteaanvraag email opnieuw versturen;
                                 SVE=ètersÑnd E-post med offertfîrfrÜgan];
                      Image=Web;
                      OnAction=VAR
                                 DocumentPortalProject@1100528300 : Codeunit 11012948;
                               BEGIN
                                 DocumentPortalProject.ResendQuoteRequestMail(Rec);
                               END;
                                }
      { 1100527802;1 ;ActionGroup;
                      CaptionML=[DEU=4PS Control;
                                 ENU=4PS Control;
                                 NLD=4PS Control] }
      { 1100527803;2 ;Action    ;
                      AccessByPermission=Codeunit 11012498=X;
                      CaptionML=[DEU=Belege synchronisieren;
                                 ENU=Sync Document;
                                 NLD=Documenten synchroniseren];
                      Image=Web;
                      OnAction=VAR
                                 ProjectPurchaseAction@1100527801 : Record 11012026;
                                 CTRLManagement@1100527800 : Codeunit 11012498;
                               BEGIN
                                 ProjectPurchaseAction.GET("Project No.", "Purchase Action");
                                 IF (ProjectPurchaseAction."Publish in Control") THEN BEGIN
                                   CTRLManagement.UpdateDocLinksForVendorPurchaseAction(Rec, 1); //Add
                                   CTRLManagement.UpdateDocPropsPublishInCtrlForVendorPurchaseAction(Rec, ProjectPurchaseAction."Publish in Control");
                                 END ELSE BEGIN
                                   CTRLManagement.UpdateDocLinksForVendorPurchaseAction(Rec, 2); //Delete
                                   CTRLManagement.UpdateDocPropsPublishInCtrlForVendorPurchaseAction(Rec, ProjectPurchaseAction."Publish in Control");
                                 END;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1210190000;1;Group  ;
                GroupType=Repeater }

    { 1100485000;2;Field  ;
                SourceExpr=Type }

    { 1210190001;2;Field  ;
                SourceExpr="Vendor No.";
                OnValidate=BEGIN
                             VendorNoOnAfterValidate;
                           END;

                OnLookup=VAR
                           VendorRec@1100485002 : Record 23;
                           lvContRec@1100485003 : Record 5050;
                           LookupFrm@1210190000 : Page 27;
                         BEGIN
                           IF Type <> Type::Contact THEN BEGIN
                             CLEAR(LookupFrm);
                             LookupFrm.LOOKUPMODE(TRUE);
                             LookupFrm.InitSelectionCopyPurchAct("Project No.", "Estimate No.", "Purchase Action");
                             VendorRec."No." := "Vendor No.";
                             LookupFrm.SETRECORD(VendorRec);
                             IF LookupFrm.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               LookupFrm.GETRECORD(VendorRec);
                               VALIDATE("Vendor No.",VendorRec."No.");
                             END;
                           END ELSE BEGIN
                             IF NOT lvContRec.GET("Vendor No.") THEN
                               lvContRec.INIT;
                             IF PAGE.RUNMODAL(0, lvContRec) = ACTION::LookupOK THEN
                               VALIDATE("Vendor No.",lvContRec."No.");
                           END;
                           CurrPage.UPDATE(TRUE);
                         END;
                          }

    { 1210190003;2;Field  ;
                DrillDown=No;
                SourceExpr="Vendor Name" }

    { 1210190009;2;Field  ;
                DrillDown=No;
                SourceExpr=Address }

    { 1210190015;2;Field  ;
                Lookup=No;
                DrillDown=No;
                SourceExpr="Post Code" }

    { 1210190011;2;Field  ;
                DrillDown=No;
                SourceExpr=City }

    { 1210190013;2;Field  ;
                DrillDown=No;
                SourceExpr="Phone No." }

    { 1100485002;2;Field  ;
                DrillDown=No;
                SourceExpr="E-mail" }

    { 1210190017;2;Field  ;
                SourceExpr=Contact;
                OnLookup=BEGIN
                           LookupContact;

                           CurrPage.UPDATE(TRUE);
                         END;
                          }

    { 1100525000;2;Field  ;
                SourceExpr="Contact Name" }

    { 1100525005;2;Field  ;
                SourceExpr="Preferred Vendor" }

    { 1210190023;2;Field  ;
                DrillDown=No;
                SourceExpr=Blocked;
                Visible=FALSE }

    { 1210190025;2;Field  ;
                SourceExpr="Block Purchase Order";
                Visible=FALSE }

    { 1210190021;2;Field  ;
                DrillDown=No;
                SourceExpr="Contract Type" }

    { 1210190019;2;Field  ;
                SourceExpr="Document Template" }

    { 1100528601;2;Field  ;
                CaptionML=[DEU=Durchschnittliches Bewertungsergebnis;
                           ENU=Average Assessment Score;
                           NLD=Gemiddelde score leveranciersbeoordeling];
                BlankZero=Yes;
                SourceExpr=AverageAssessmentScore;
                Visible=FALSE;
                Editable=FALSE }

    { 1100528600;2;Field  ;
                CaptionML=[DEU=Beschreibung des Bewertungsergebnisses;
                           ENU=Assessment Score Description;
                           NLD=Scoreomschrijving leveranciersbeoordeling];
                SourceExpr=AssessmentScoreDescription;
                Visible=FALSE;
                Editable=FALSE }

    { 1100525001;2;Field  ;
                SourceExpr="Budget Amount (LCY)" }

    { 1100525002;2;Field  ;
                SourceExpr="Budget Amount (FCY)" }

    { 1100525003;2;Field  ;
                SourceExpr="Currency Code" }

    { 1100525004;2;Field  ;
                CaptionML=[DEU=Angebotsbetrag (LCY);
                           ENU=Quote Amount (LCY);
                           NLD=Offertebedrag (LV);
                           SVE=Offertbelopp (BVA)];
                SourceExpr=QuoteAmnt;
                OnDrillDown=VAR
                              PurchHeader@1100525001 : Record 38;
                            BEGIN
                              //DP00885
                              PurchHeader.SETCURRENTKEY("Document Type","Job No.");
                              PurchHeader.SETRANGE("Document Type", PurchHeader."Document Type"::Quote);
                              PurchHeader.SETRANGE("Job No.", "Project No.");
                              PurchHeader.SETRANGE("Purchase Action", "Purchase Action");
                              PurchHeader.SETRANGE("Buy-from Vendor No.", "Vendor No.");
                              IF PurchHeader.COUNT = 1 THEN
                                PAGE.RUNMODAL(PAGE::"Purchase Quote", PurchHeader)
                              ELSE
                                 PAGE.RUNMODAL(PAGE::"Purchase Quotes", PurchHeader);
                            END;
                             }

    { 1100527800;2;Field  ;
                SourceExpr="Downloaded in Control";
                Visible=false }

    { 1100527801;2;Field  ;
                SourceExpr="Quote Received in Control";
                Visible=false }

  }
  CODE
  {
    VAR
      FilterFeaturesRec@1210190000 : Record 11012070;
      ContFeatGrpRec@1210190001 : Record 11012751;
      ContFeatRec@1210190002 : Record 11012752;
      PurchActFeatRec@1210190003 : Record 11012069;
      FilterFeatFrm@1210190004 : Page 11012131;
      AverageAssessmentScore@1100528601 : Decimal;
      AssessmentScoreDescription@1100528600 : Text[30];
      QuoteAmnt@1100525000 : Decimal;
      QuoteSubsitePresent@1100527800 : Boolean;

    LOCAL PROCEDURE VendorNoOnAfterValidate@19009338();
    BEGIN
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE OnAfterGetCurrRecord@19077479();
    BEGIN
      xRec := Rec;
      CalcFieldsVendor();
      DetermineAverageAssessmentScore();
      QuoteAmnt := CalcSumQuoteComparison(2, '', '', '');  //DP00885
    END;

    LOCAL PROCEDURE DetermineAverageAssessmentScore@1100528600();
    VAR
      VendorAssessment@1100528600 : Record 11012292;
      FPSLicenseManagement@1100527400 : Codeunit 11229289;
    BEGIN
      IF FPSLicenseManagement.LicenseAndReadPermissionForTable(VendorAssessment) THEN
        AverageAssessmentScore := VendorAssessment.GetAverageAssessmentScore("Vendor No.", AssessmentScoreDescription);
    END;

    BEGIN
    END.
  }
}

