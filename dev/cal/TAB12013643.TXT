OBJECT Table 12013643 EX Attached File
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF510000;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               "Created By" := USERID;
               "Created Date" := CREATEDATETIME(TODAY,TIME);
               IF GUIALLOWED THEN
                 Source := Source::NAV;
             END;

    CaptionML=[ENU=ExFlow Attached File;
               SVE=ExFlow Bifogad fil];
  }
  FIELDS
  {
    { 2   ;   ;Entry No.           ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[ENU=Entry No.;
                                                              SVE=Lîpnr] }
    { 10  ;   ;Attached File       ;Text200       ;CaptionML=[ENU=Attached file;
                                                              SVE=Bilaga] }
    { 11  ;   ;Document Type       ;Option        ;CaptionML=[ENU=Document Type;
                                                              SVE=Dokumenttyp];
                                                   OptionCaptionML=[ENU=Quote,Order,Invoice,Credit Memo,,,Posted Invoice,Posted Credit Memo,Requisition;
                                                                    SVE=Offert,Order,Faktura,Kreditnota,,,Bokfîrd faktura,Bokfîrd kreditnota,Rekvisition];
                                                   OptionString=Quote,Order,Invoice,Credit Memo,,,Posted Invoice,Posted Credit Memo,Requisition }
    { 12  ;   ;Document No.        ;Code20        ;CaptionML=[ENU=Document No.;
                                                              SVE=Dokumentnr] }
    { 13  ;   ;Attached            ;Boolean       ;CaptionML=[ENU=Attached;
                                                              SVE=Kopplad] }
    { 14  ;   ;Connect to Document ;Code20        ;OnValidate=VAR
                                                                PurchHeader@1100285002 : Record 38;
                                                                PostedPurchHead@1100285001 : Record 122;
                                                                PostedCN@1100285000 : Record 124;
                                                                DocHead@1100285003 : Record 12013608;
                                                              BEGIN
                                                                IF "Connect to Document" <> '' THEN
                                                                  CASE "Document Type" OF
                                                                    "Document Type":: "Posted Invoice":
                                                                      IF NOT PostedPurchHead.GET("Connect to Document") THEN
                                                                        ERROR(EXF02);

                                                                    "Document Type":: "Posted Credit Memo":
                                                                      IF NOT PostedCN.GET("Connect to Document") THEN
                                                                        ERROR(EXF02);

                                                                    "Document Type":: Quote,"Document Type":: Invoice,"Document Type":: "Credit Memo","Document Type":: Order:
                                                                      IF NOT PurchHeader.GET("Document Type","Connect to Document") THEN
                                                                        ERROR(EXF02);

                                                                    "Document Type":: Requisition:
                                                                      IF NOT DocHead.RetrieveRecord(DocHead, "Document Type","Connect to Document") THEN
                                                                        ERROR(EXF02);
                                                                  END;
                                                              END;

                                                   OnLookup=VAR
                                                              PurchHeader@1000000000 : Record 38;
                                                              PostedPurchHead@1000000001 : Record 122;
                                                              PostedCN@1000000002 : Record 124;
                                                              EXDocument@1100285000 : Record 12013608;
                                                            BEGIN
                                                              CASE "Document Type" OF
                                                                "Document Type":: "Posted Invoice":
                                                                  BEGIN
                                                                    PostedPurchHead.RESET;
                                                                    IF PAGE.RUNMODAL(PAGE::"Posted Purchase Invoices",PostedPurchHead) = ACTION::LookupOK THEN
                                                                      "Connect to Document" := PostedPurchHead."No.";
                                                                  END;
                                                                "Document Type":: Requisition:
                                                                  BEGIN
                                                                    EXDocument.RESET;
                                                                    IF PAGE.RUNMODAL(PAGE::"Requisition List",EXDocument) = ACTION::LookupOK THEN
                                                                      "Connect to Document" := EXDocument."Document No.";
                                                                  END;
                                                                "Document Type":: "Posted Credit Memo":
                                                                  BEGIN
                                                                    PostedCN.RESET;
                                                                    IF PAGE.RUNMODAL(PAGE::"Posted Purchase Credit Memos",PostedCN) = ACTION::LookupOK THEN
                                                                      "Connect to Document" := PostedCN."No.";
                                                                  END ELSE BEGIN
                                                                    PurchHeader.RESET;

                                                                    CASE "Document Type" OF
                                                                      "Document Type":: Quote:
                                                                        PurchHeader.SETRANGE("Document Type","Document Type"::Quote);
                                                                      "Document Type":: Invoice:
                                                                        PurchHeader.SETRANGE("Document Type","Document Type"::Invoice);
                                                                      "Document Type":: "Credit Memo":
                                                                        PurchHeader.SETRANGE("Document Type","Document Type"::"Credit Memo");
                                                                      "Document Type":: Order:
                                                                        PurchHeader.SETRANGE("Document Type","Document Type"::Order)
                                                                      ELSE
                                                                          EXIT;
                                                                    END;
                                                                    IF PAGE.RUNMODAL(PAGE::"Purchase List",PurchHeader) = ACTION::LookupOK THEN
                                                                      "Connect to Document" := PurchHeader."No.";
                                                                  END;
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Connected to Document;
                                                              SVE=Koppla till dokument] }
    { 15  ;   ;Path                ;Text250       ;CaptionML=[ENU=Path;
                                                              SVE=SîkvÑg] }
    { 16  ;   ;Document ID         ;BigInteger    ;CaptionML=[ENU=Document ID;
                                                              SVE=Dokument ID] }
    { 17  ;   ;File Extension      ;Text250       ;CaptionML=[ENU=File Extension;
                                                              SVE=Filtyp];
                                                   Editable=No }
    { 81  ;   ;Web ID              ;BigInteger    ;CaptionML=[ENU=Web ID;
                                                              SVE=Webb ID];
                                                   Description=Doc ID+Web Company ID }
    { 650 ;   ;Created By          ;Code50        ;CaptionML=[ENU=Created By;
                                                              SVE=Skapad av];
                                                   Editable=No }
    { 651 ;   ;Created Date        ;DateTime      ;CaptionML=[ENU=Created Date;
                                                              SVE=Skapad datum];
                                                   Editable=No }
    { 652 ;   ;Source              ;Option        ;CaptionML=[ENU=Created in;
                                                              SVE=Skapad i];
                                                   OptionCaptionML=[ENU=NAV,Web;
                                                                    SVE=NAV,Web];
                                                   OptionString=NAV,Web;
                                                   Editable=No }
    { 700 ;   ;Attachment Blob     ;BLOB          ;CaptionML=SVE=Bilags BLOB }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Document ID                              }
    {    ;Document Type,Document No.,Attached      }
    {    ;Attached File                            }
    {    ;Document Type,Document No.,Entry No.     }
    {    ;Document No.                             }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      AppSetup@1100285008 : Record 12013601;
      ExFlow@1100285000 : Codeunit 12013601;
      EXF01@1100285009 : TextConst 'ENU=Get invoice    @1@@@@@@@@@@@@@;SVE=HÑmtar faktura      @1@@@@@@@@@@@@@';
      EXF02@1100285010 : TextConst 'ENU=Document do not exist!;SVE=Dokumentet finns inte!';
      EXF03@1100285011 : TextConst 'ENU=Document %1 do not exist!;SVE=Dokumentet %1 finns inte!';
      EXF04@1100285014 : TextConst 'ENU=Browse File;SVE=Sîk dokumnet';
      ExFlowFileMgt@1100285013 : Codeunit 12013602;
      EXF50@1100285002 : TextConst 'ENU=Failed to create folder %1 while moving attached file %2.;SVE=Kunde inte skapa katalog %1 i flyttningen fîr fil %2.';
      EXF51@1100285001 : TextConst 'ENU=Failed to create folder %1.;SVE=Kunde inte skapa katalog %1.';
      EXF55@1100285016 : TextConst 'ENU=%1;SVE=%1';
      EXF101@1100285006 : TextConst 'ENU=Import Attachment;SVE=Importera bilaga';
      EXF102@1100285005 : TextConst 'ENU=All Files (*.*)|*.*;SVE=Alla filer (*.*)|*.*';
      EXF103@1100285004 : TextConst 'ENU=Warning the existing attachment will be deleted, proceed anyway?;SVE=Varning den existerande billagan kommer att tas bort. FortsÑtta ÑndÜ?';
      EXF104@1100285003 : TextConst 'ENU=Aborted;SVE=ètgÑrd avbryten';

    PROCEDURE Connect@1000000001(VAR ExAttachment@1100285003 : Record 12013643);
    VAR
      PurchHeader@1000000002 : Record 38;
      PostedPurchHead@1000000001 : Record 122;
      PostedCN@1000000000 : Record 124;
      DocHead@1100285000 : Record 12013608;
      PostedDocHead@1100285001 : Record 12013626;
      DocumentID@1100285002 : BigInteger;
      WebID@1100285004 : BigInteger;
    BEGIN
      TESTFIELD(Attached,FALSE);
      TESTFIELD("Connect to Document");

      DocumentID := 0;

      CASE "Document Type" OF
        "Document Type"::"Posted Invoice":
            BEGIN
              PostedPurchHead.GET("Connect to Document");
              IF PostedDocHead.RetrieveRecord(PostedDocHead,"Document Type"::"Posted Invoice","Connect to Document") THEN BEGIN
                DocumentID := PostedDocHead.ID;
                WebID := PostedDocHead."Web ID";
              END;
            END;
        "Document Type"::"Posted Credit Memo":
          BEGIN
            PostedCN.GET("Connect to Document");
            IF PostedDocHead.RetrieveRecord(PostedDocHead,"Document Type"::"Posted Credit Memo","Connect to Document") THEN BEGIN
              DocumentID := PostedDocHead.ID;
              WebID := PostedDocHead."Web ID";
            END
          END;
        ELSE
          BEGIN
            IF NOT PurchHeader.GET("Document Type","Connect to Document") THEN
              EXIT;
            IF DocHead.RetrieveRecord(DocHead,"Document Type","Connect to Document") THEN BEGIN
              DocumentID := DocHead.ID;
              WebID := DocHead."Web ID";
            END;
          END;
      END;

      IF DocumentID = 0 THEN
        ExAttachment."Document ID" := DATE2DWY(WORKDATE, 3) * 100 + DATE2DWY(WORKDATE, 2)
      ELSE
        ExAttachment."Document ID" := DocumentID;

      ExAttachment."Document No." := ExAttachment."Connect to Document";
      ExAttachment."Web ID" := WebID;
      ExAttachment."Connect to Document" := '';
      ExAttachment.Attached := TRUE;
      ExAttachment.MODIFY;
    END;

    PROCEDURE ImportDocument@1100285002(VAR ExAttachedFile@1100285000 : Record 12013643) : Boolean;
    VAR
      ExFlowFileMgt@1100285002 : Codeunit 12013602;
      TempFileName@1100285001 : Text[250];
      FileManagement@1100285003 : Codeunit 419;
      TempBlob@1100285004 : Record 99008535;
    BEGIN
      IF ExAttachedFile."Entry No." = 0 THEN
        ExAttachedFile.INSERT(TRUE);

      ExAttachedFile.CALCFIELDS("Attachment Blob");
      IF ExAttachedFile."Attachment Blob".HASVALUE THEN
        ConfirmDelete;

      ExFlowFileMgt.FileNameFromDialog(TempFileName,EXF101,4,EXF102,0);

      IF TempFileName <> '' THEN BEGIN
        IF FileManagement.ServerFileExists(TempFileName) THEN
          FileManagement.BLOBImportFromServerFile(TempBlob,TempFileName)
        ELSE
          FileManagement.BLOBImportFromServerFile(TempBlob,FileManagement.UploadFileSilent(TempFileName));
        ExAttachedFile."Attachment Blob" := TempBlob.Blob;
        TempFileName := DELCHR(TempFileName,'=','@%&<>/?');
        ExAttachedFile."Attached File" := COPYSTR(FileManagement.GetFileName(TempFileName),1,MAXSTRLEN(TempFileName));
        ExAttachedFile."File Extension":= LOWERCASE(COPYSTR(FileManagement.GetExtension(TempFileName),1,MAXSTRLEN("File Extension")));
        IF MODIFY THEN;
      END;

      IF "Connect to Document" <> '' THEN
        Connect(ExAttachedFile);
    END;

    PROCEDURE Export@3(ShowFileDialog@1002 : Boolean) : Text;
    VAR
      TempBlob@1001 : Record 99008535;
      FileMgt@1000 : Codeunit 419;
      DefaultFileName@1100285000 : Text;
    BEGIN
      CALCFIELDS("Attachment Blob");
      IF NOT "Attachment Blob".HASVALUE THEN
        EXIT;

      TempBlob.Blob := "Attachment Blob";
      EXIT(FileMgt.BLOBExport(TempBlob,"Attached File",ShowFileDialog));
    END;

    LOCAL PROCEDURE ConfirmDelete@1100285001();
    BEGIN
      IF NOT CONFIRM(EXF103,FALSE) THEN
        ERROR(EXF104);
    END;

    BEGIN
    END.
  }
}

