OBJECT XMLport 1231 Export Generic Fixed Width
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW111.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Export Generic Fixed Width;
               NOR=Eksporter generisk fast bredde;
               SVE=Exportera generisk fast bredd];
    Direction=Export;
    TextEncoding=WINDOWS;
    OnInitXMLport=BEGIN
                    Window.OPEN(ProgressMsg);
                    currXMLport.FIELDDELIMITER('');
                    currXMLport.FIELDSEPARATOR('');
                  END;

    OnPreXMLport=BEGIN
                   InitializeGlobals;
                 END;

    OnPostXMLport=BEGIN
                    IF ErrorText <> '' THEN
                      ERROR(ErrorText);

                    Window.CLOSE;
                  END;

    Format=Variable Text;
    UseRequestPage=No;
  }
  ELEMENTS
  {
    { [{C19D763B-C1F1-473D-8D3E-3640AD129D95}];  ;root                ;Element ;Text    ;
                                                  MinOccurs=Zero }

    { [{125F3B85-5B53-4F67-9434-589A04D6DCA1}];1 ;DataExchField       ;Element ;Table   ;
                                                  SourceTable=Table1221 }

    { [{A64C65BE-4E70-436F-9680-A7C77EC03315}];2 ;ColumnX             ;Element ;Text    ;
                                                  DataType=Text;
                                                  MinOccurs=Zero;
                                                  Unbound=Yes;
                                                  Export::OnBeforePassVariable=BEGIN
                                                                                 IF QuitLoop THEN
                                                                                   currXMLport.BREAKUNBOUND;

                                                                                 IF "Data Exch. Field"."Line No." <> LastLineNo THEN BEGIN
                                                                                   IF "Data Exch. Field"."Line No." <> LastLineNo + 1 THEN
                                                                                     ErrorText += LinesNotSequentialErr
                                                                                   ELSE BEGIN
                                                                                     LastLineNo := "Data Exch. Field"."Line No.";
                                                                                     PrevColumnNo := 0;
                                                                                     "Data Exch. Field".NEXT(-1);
                                                                                     Window.UPDATE(1,LastLineNo);
                                                                                   END;
                                                                                   currXMLport.BREAKUNBOUND;
                                                                                 END;

                                                                                 CheckColumnSequence;
                                                                                 ColumnX := "Data Exch. Field".Value;

                                                                                 IF "Data Exch. Field".NEXT = 0 THEN
                                                                                   QuitLoop := TRUE;
                                                                               END;
                                                                                }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      DataExchDef@1064 : Record 1222;
      DataExch@1063 : Record 1220;
      Window@1000 : Dialog;
      ErrorText@1005 : Text;
      DataExchEntryNo@1062 : Integer;
      LastLineNo@1065 : Integer;
      PrevColumnNo@1003 : Integer;
      QuitLoop@1001 : Boolean;
      ColumnsNotSequentialErr@1002 : TextConst 'ENU=The data to be exported is not structured correctly. The columns in the dataset must be sequential.;NOR=Dataene som skal eksporteres, er ikke riktig strukturert. Kolonnene i datasettet m† v‘re sekvensielle.;SVE=De data som ska exporteras har en felaktig struktur. Kolumnerna i dataupps„ttningen m†ste vara sekventiella.';
      LinesNotSequentialErr@1004 : TextConst 'ENU=The data to be exported is not structured correctly. The lines in the dataset must be sequential.;NOR=Dataene som skal eksporteres, er ikke riktig strukturert. Linjene i datasettet m† v‘re sekvensielle.;SVE=De data som ska exporteras har en felaktig struktur. Raderna i dataupps„ttningen m†ste vara sekventiella.';
      ProgressMsg@1060 : TextConst 'ENU=Exporting line no. #1######;NOR=Eksporterer linje nr. #1######;SVE=Exporterar rad nr #1######';

    LOCAL PROCEDURE InitializeGlobals@11();
    BEGIN
      DataExchEntryNo := "Data Exch. Field".GETRANGEMIN("Data Exch. No.");
      LastLineNo := 1;
      PrevColumnNo := 0;
      QuitLoop := FALSE;

      IF DataExch.GET(DataExchEntryNo) AND DataExchDef.GET(DataExch."Data Exch. Def Code") THEN BEGIN
        currXMLport.FILENAME := DataExchDef.Name + '.txt';
        IF DataExchDef."File Type" = DataExchDef."File Type"::"Variable Text" THEN
          currXMLport.FIELDSEPARATOR(DataExchDef.ColumnSeparatorChar)
      END;
    END;

    [External]
    PROCEDURE CheckColumnSequence@3();
    BEGIN
      IF "Data Exch. Field"."Column No." <> PrevColumnNo + 1 THEN BEGIN
        ErrorText += ColumnsNotSequentialErr;
        currXMLport.BREAKUNBOUND;
      END;

      PrevColumnNo := "Data Exch. Field"."Column No.";
    END;

    BEGIN
    END.
  }
}

