OBJECT Report 511 Complete IC Inbox Action
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.04;
  }
  PROPERTIES
  {
    CaptionML=[DEU=IC-Eingangsvorgang abschl.;
               ENU=Complete IC Inbox Action;
               NLD=Bewerking IC-inbox voltooien;
               NOR=Fullfõr KI-innbokshandling];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 1153;    ;DataItem;                    ;
               DataItemTable=Table418;
               DataItemTableView=SORTING(Transaction No.,IC Partner Code,Transaction Source,Document Type);
               OnPreDataItem=BEGIN
                               IF TempGenJnlLine."Journal Template Name" <> '' THEN BEGIN
                                 GenJnlTemplate.GET(TempGenJnlLine."Journal Template Name");
                                 GenJnlTemplate.TESTFIELD(Type,GenJnlTemplate.Type::Intercompany);
                                 TempGenJnlLine.SETRANGE("Journal Template Name",GenJnlTemplate.Name);
                                 TempGenJnlLine.SETRANGE("Journal Batch Name",GenJnlBatch.Name);
                               END;
                               GetGLSetup;
                             END;

               OnAfterGetRecord=VAR
                                  InboxTransaction2@1000 : Record 418;
                                  HandledInboxTransaction2@1001 : Record 420;
                                  ICCommentLine@1002 : Record 424;
                                  ICPartner@1003 : Record 413;
                                BEGIN
                                  IF "Line Action" = "Line Action"::"No Action" THEN
                                    CurrReport.SKIP;
                                  InboxTransaction2 := "IC Inbox Transaction";
                                  IF ("Source Type" = "Source Type"::Journal) AND
                                     (InboxTransaction2."Line Action" <> InboxTransaction2."Line Action"::Cancel) AND
                                     (InboxTransaction2."Line Action" <> InboxTransaction2."Line Action"::"Return to IC Partner")
                                  THEN BEGIN
                                    TempGenJnlLine.TESTFIELD("Journal Template Name");
                                    TempGenJnlLine.TESTFIELD("Journal Batch Name");
                                  END;
                                  IF (InboxTransaction2."Line Action" <> InboxTransaction2."Line Action"::Cancel) AND
                                     ICPartner.GET(InboxTransaction2."IC Partner Code")
                                  THEN
                                    ICPartner.TESTFIELD(Blocked,FALSE);
                                  HandledInboxTransaction2.TRANSFERFIELDS(InboxTransaction2);
                                  CASE InboxTransaction2."Line Action" OF
                                    InboxTransaction2."Line Action"::Accept:
                                      HandledInboxTransaction2.Status := HandledInboxTransaction2.Status::Accepted;
                                    InboxTransaction2."Line Action"::"Return to IC Partner":
                                      HandledInboxTransaction2.Status := HandledInboxTransaction2.Status::"Returned to IC Partner";
                                    InboxTransaction2."Line Action"::Cancel:
                                      HandledInboxTransaction2.Status := HandledInboxTransaction2.Status::Cancelled;
                                  END;
                                  OnBeforeHandledInboxTransactionInsert(HandledInboxTransaction2);
                                  IF NOT HandledInboxTransaction2.INSERT THEN
                                    ERROR(
                                      Text001,InboxTransaction2.FIELDCAPTION("Transaction No."),
                                      InboxTransaction2."Transaction No.",InboxTransaction2."IC Partner Code",
                                      HandledInboxTransaction2.TABLECAPTION);
                                  InboxTransaction2.DELETE;

                                  ICIOMgt.HandleICComments(ICCommentLine."Table Name"::"IC Inbox Transaction",
                                    ICCommentLine."Table Name"::"Handled IC Inbox Transaction","Transaction No.",
                                    "IC Partner Code","Transaction Source");

                                  Forward := FALSE;
                                END;

               ReqFilterFields=IC Partner Code,Transaction Source,Line Action }

    { 1687;1   ;DataItem;                    ;
               DataItemTable=Table419;
               DataItemTableView=SORTING(Transaction No.,IC Partner Code,Transaction Source,Line No.);
               OnAfterGetRecord=VAR
                                  InboxJnlLine2@1001 : Record 419;
                                  HandledInboxJnlLine@1000 : Record 421;
                                BEGIN
                                  InboxJnlLine2 := "IC Inbox Jnl. Line";
                                  CASE "IC Inbox Transaction"."Line Action" OF
                                    "IC Inbox Transaction"."Line Action"::Accept:
                                      ICIOMgt.CreateJournalLines("IC Inbox Transaction","IC Inbox Jnl. Line",TempGenJnlLine,GenJnlTemplate);
                                    "IC Inbox Transaction"."Line Action"::"Return to IC Partner":
                                      IF NOT Forward THEN BEGIN
                                        ICIOMgt.ForwardToOutBox("IC Inbox Transaction");
                                        Forward := TRUE;
                                      END;
                                    "IC Inbox Transaction"."Line Action"::Cancel:
                                      BEGIN
                                        HandledInboxJnlLine.TRANSFERFIELDS(InboxJnlLine2);
                                        HandledInboxJnlLine.INSERT;
                                      END;
                                  END;

                                  ICIOMgt.MoveICJnlDimToHandled(DATABASE::"IC Inbox Jnl. Line",DATABASE::"Handled IC Inbox Jnl. Line",
                                    "IC Inbox Transaction"."Transaction No.","IC Inbox Transaction"."IC Partner Code",
                                    TRUE,InboxJnlLine2."Line No.");
                                  InboxJnlLine2.DELETE(TRUE);
                                END;

               OnPostDataItem=BEGIN
                                TempGenJnlLine."Document No." := INCSTR(TempGenJnlLine."Document No.");
                              END;

               DataItemLink=Transaction No.=FIELD(Transaction No.),
                            IC Partner Code=FIELD(IC Partner Code),
                            Transaction Source=FIELD(Transaction Source) }

    { 2880;1   ;DataItem;                    ;
               DataItemTable=Table434;
               DataItemTableView=SORTING(IC Transaction No.,IC Partner Code,Transaction Source);
               OnAfterGetRecord=VAR
                                  InboxSalesHeader2@1000 : Record 434;
                                  HandledInboxSalesHeader@1005 : Record 438;
                                  InboxSalesLine@1004 : Record 435;
                                  HandledInboxSalesLine@1003 : Record 439;
                                  ICDocDim@1002 : Record 442;
                                  ICDocDim2@1001 : Record 442;
                                BEGIN
                                  InboxSalesHeader2 := "IC Inbox Sales Header";
                                  CASE "IC Inbox Transaction"."Line Action" OF
                                    "IC Inbox Transaction"."Line Action"::Accept:
                                      ICIOMgt.CreateSalesDocument("IC Inbox Sales Header",
                                        ReplaceDocPostingDate,DocPostingDate);
                                    "IC Inbox Transaction"."Line Action"::"Return to IC Partner":
                                      ICIOMgt.ForwardToOutBox("IC Inbox Transaction");
                                    "IC Inbox Transaction"."Line Action"::Cancel:
                                      BEGIN
                                        HandledInboxSalesHeader.TRANSFERFIELDS(InboxSalesHeader2);
                                        HandledInboxSalesHeader.INSERT;
                                        DimMgt.SetICDocDimFilters(
                                          ICDocDim,DATABASE::"IC Inbox Sales Header","IC Transaction No.","IC Partner Code","Transaction Source",0);
                                        IF ICDocDim.FINDFIRST THEN
                                          DimMgt.MoveICDocDimtoICDocDim(ICDocDim,ICDocDim2,DATABASE::"Handled IC Inbox Sales Header","Transaction Source");
                                        WITH InboxSalesLine DO  BEGIN
                                          SETRANGE("IC Transaction No.",InboxSalesHeader2."IC Transaction No.");
                                          SETRANGE("IC Partner Code",InboxSalesHeader2."IC Partner Code");
                                          SETRANGE("Transaction Source",InboxSalesHeader2."Transaction Source");
                                          IF FIND('-') THEN
                                            REPEAT
                                              HandledInboxSalesLine.TRANSFERFIELDS(InboxSalesLine);
                                              HandledInboxSalesLine.INSERT;
                                              DimMgt.SetICDocDimFilters(
                                                ICDocDim,DATABASE::"IC Inbox Sales Line","IC Transaction No.","IC Partner Code","Transaction Source","Line No.");
                                              IF ICDocDim.FINDFIRST THEN
                                                DimMgt.MoveICDocDimtoICDocDim(ICDocDim,ICDocDim2,DATABASE::"Handled IC Inbox Sales Line","Transaction Source");
                                            UNTIL NEXT = 0;
                                        END;
                                        OnAfterMoveICInboxSalesHeaderToHandled(InboxSalesHeader2,HandledInboxSalesHeader);
                                      END;
                                  END;
                                  InboxSalesHeader2.DELETE(TRUE);
                                END;

               DataItemLink=IC Transaction No.=FIELD(Transaction No.),
                            IC Partner Code=FIELD(IC Partner Code),
                            Transaction Source=FIELD(Transaction Source) }

    { 3959;1   ;DataItem;                    ;
               DataItemTable=Table436;
               DataItemTableView=SORTING(IC Transaction No.,IC Partner Code,Transaction Source);
               OnAfterGetRecord=VAR
                                  InboxPurchHeader2@1001 : Record 436;
                                  HandledInboxPurchHeader@1005 : Record 440;
                                  InboxPurchLine@1004 : Record 437;
                                  HandledInboxPurchLine@1003 : Record 441;
                                  ICDocDim@1002 : Record 442;
                                  ICDocDim2@1000 : Record 442;
                                BEGIN
                                  InboxPurchHeader2 := "IC Inbox Purchase Header";
                                  CASE "IC Inbox Transaction"."Line Action" OF
                                    "IC Inbox Transaction"."Line Action"::Accept:
                                      ICIOMgt.CreatePurchDocument("IC Inbox Purchase Header",
                                        ReplaceDocPostingDate,DocPostingDate);
                                    "IC Inbox Transaction"."Line Action"::"Return to IC Partner":
                                      ICIOMgt.ForwardToOutBox("IC Inbox Transaction");
                                    "IC Inbox Transaction"."Line Action"::Cancel:
                                      BEGIN
                                        HandledInboxPurchHeader.TRANSFERFIELDS(InboxPurchHeader2);
                                        HandledInboxPurchHeader.INSERT;
                                        DimMgt.SetICDocDimFilters(
                                          ICDocDim,DATABASE::"IC Inbox Purchase Header","IC Transaction No.","IC Partner Code","Transaction Source",0);
                                        IF ICDocDim.FINDFIRST THEN
                                          DimMgt.MoveICDocDimtoICDocDim(ICDocDim,ICDocDim2,DATABASE::"Handled IC Inbox Purch. Header","Transaction Source");
                                        WITH InboxPurchLine DO  BEGIN
                                          SETRANGE("IC Transaction No.",InboxPurchHeader2."IC Transaction No.");
                                          SETRANGE("IC Partner Code",InboxPurchHeader2."IC Partner Code");
                                          SETRANGE("Transaction Source",InboxPurchHeader2."Transaction Source");
                                          IF FIND('-') THEN
                                            REPEAT
                                              HandledInboxPurchLine.TRANSFERFIELDS(InboxPurchLine);
                                              HandledInboxPurchLine.INSERT;
                                              DimMgt.SetICDocDimFilters(
                                                ICDocDim,DATABASE::"IC Inbox Purchase Line","IC Transaction No.","IC Partner Code",
                                                "Transaction Source","Line No.");
                                              IF ICDocDim.FINDFIRST THEN
                                                DimMgt.MoveICDocDimtoICDocDim(ICDocDim,ICDocDim2,DATABASE::"Handled IC Inbox Purch. Line","Transaction Source");
                                            UNTIL NEXT = 0;
                                        END;
                                        OnAfterMoveICInboxPurchHeaderToHandled(InboxPurchHeader2,HandledInboxPurchHeader);
                                      END;
                                  END;
                                  InboxPurchHeader2.DELETE(TRUE);
                                END;

               DataItemLink=IC Transaction No.=FIELD(Transaction No.),
                            IC Partner Code=FIELD(IC Partner Code),
                            Transaction Source=FIELD(Transaction Source) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnInit=BEGIN
               PostingDateEditable := TRUE;
               DocPostingDateEditable := TRUE;
             END;

      OnOpenPage=BEGIN
                   ValidateJnl;
                   IF ReplaceDocPostingDate THEN
                     DocPostingDateEditable := TRUE
                   ELSE
                     DocPostingDateEditable := FALSE;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 17  ;2   ;Group     ;
                  CaptionML=[DEU=Buch.-BlÑtter;
                             ENU=Journals;
                             NLD=Dagboeken;
                             NOR=Kladder;
                             SVE=Journaler] }

      { 4   ;3   ;Field     ;
                  CaptionML=[DEU=IC-Fibu Buch.-Blattvorlage;
                             ENU=IC Gen. Journal Template;
                             NLD=IC-Fin. dagboeksjabloon;
                             NOR=KI-finanskladdmal];
                  ToolTipML=[DEU=Gibt die Buch.-Blattvorlage an, in der die Buch.-Blattzeilen erstellt werden sollen.;
                             ENU=Specifies the journal template in which you want to create the journal lines.;
                             NLD=Hiermee wordt de dagboeksjabloon opgegeven waarin u de dagboekregels wilt maken.;
                             NOR=Angir kladdemalen du vil opprette kladdelinjene i.];
                  ApplicationArea=#Intercompany;
                  SourceExpr=TempGenJnlLine."Journal Template Name";
                  TableRelation="Gen. Journal Template";
                  OnValidate=BEGIN
                               TempGenJnlLine."Journal Batch Name" := '';
                               ValidateJnl;
                             END;

                  OnLookup=BEGIN
                             IF PAGE.RUNMODAL(0,GenJnlTemplate) = ACTION::LookupOK THEN BEGIN
                               TempGenJnlLine."Journal Template Name" := GenJnlTemplate.Name;
                               ValidateJnl;
                             END;
                           END;
                            }

      { 6   ;3   ;Field     ;
                  Lookup=Yes;
                  CaptionML=[DEU=Fibu Buch.-Blattname;
                             ENU=Gen. Journal Batch;
                             NLD=Fin. dagboekbatch;
                             NOR=Finanskladd;
                             SVE=Redovisningsjournal];
                  ToolTipML=[DEU=Gibt den Buch.-Blattnamen an, in dem die Buch.-Blattzeilen erstellt werden sollen.;
                             ENU=Specifies the journal batch in which you want to create the journal lines.;
                             NLD=Hiermee wordt de dagboekbatch opgegeven waarin u de dagboekregels wilt maken.;
                             NOR=Angir finanskladden du vil opprette kladdelinjene i.];
                  ApplicationArea=#Intercompany;
                  SourceExpr=TempGenJnlLine."Journal Batch Name";
                  OnValidate=BEGIN
                               IF TempGenJnlLine."Journal Batch Name" <> '' THEN BEGIN
                                 TempGenJnlLine.TESTFIELD("Journal Template Name");
                                 GenJnlBatch.GET(TempGenJnlLine."Journal Template Name",TempGenJnlLine."Journal Batch Name");
                               END;
                               ValidateJnl;
                             END;

                  OnLookup=BEGIN
                             TempGenJnlLine.TESTFIELD("Journal Template Name");
                             GenJnlTemplate.GET(TempGenJnlLine."Journal Template Name");
                             GenJnlBatch.FILTERGROUP(2);
                             GenJnlBatch.SETRANGE("Journal Template Name",TempGenJnlLine."Journal Template Name");
                             GenJnlBatch.FILTERGROUP(0);
                             GenJnlBatch."Journal Template Name" := TempGenJnlLine."Journal Template Name";
                             GenJnlBatch.Name := TempGenJnlLine."Journal Batch Name";
                             IF PAGE.RUNMODAL(0,GenJnlBatch) = ACTION::LookupOK THEN BEGIN
                               Text := GenJnlBatch.Name;
                               EXIT(TRUE);
                             END;
                           END;
                            }

      { 1   ;3   ;Field     ;
                  CaptionML=[DEU=Start von Belegnr.;
                             ENU=Starting Document No.;
                             NLD=Eerste documentnr.;
                             NOR=Startbilagsnr.;
                             SVE=Fr.o.m. dokumentnr];
                  ToolTipML=[DEU="Gibt die nÑchste verfÅgbare Nummer in der Nummernserie fÅr das Buch.-Blatt an, das mit dem Zahlungsausgangs-Buch.-Blatt verknÅpft ist. Wenn Sie die Stapelverarbeitung ausfÅhren, ist dies die Belegnummer, die in der ersten Buchungsblattzeile fÅr den Zahlungsausgang erscheint. Durch die Stapelverarbeitung wird dieses Feld automatisch ausgefÅllt. Sie kînnen dieses Feld aber auch manuell ausfÅllen. ";
                             ENU="Specifies the next available number in the number series for the journal batch that is linked to the payment journal. When you run the batch job, this is the document number that appears on the first payment journal line. The batch job automatically fills in this field, but you can also fill in this field manually. ";
                             NLD="Hiermee wordt het volgende beschikbare nummer opgegeven uit de nummerreeks voor de dagboekbatch die is gekoppeld aan het betalingsdagboek. Wanneer u de batchverwerking uitvoert, wordt dit documentnummer weergegeven op de eerste regel van het betalingsdagboek. Bij de batchverwerking wordt dit veld automatisch ingevuld, maar u kunt dit veld ook handmatig invullen. ";
                             NOR="Angir det neste tilgjengelige nummeret i nummerserien for kladden som er knyttet til utbetalingskladden. NÜr du kjõrer kjõrselen, er det dette dokumentnummeret som vises pÜ den fõrste utbetalingskladdelinjen. Kjõrselen fyller automatisk ut dette feltet, men du kan ogsÜ fylle ut dette feltet manuelt. "];
                  ApplicationArea=#Intercompany;
                  SourceExpr=TempGenJnlLine."Document No." }

      { 9   ;3   ;Field     ;
                  Name=Replace Posting Date;
                  CaptionML=[DEU=Buchungsdatum ersetzen;
                             ENU=Replace Posting Date;
                             NLD=Boekingsdatum vervangen;
                             NOR=Erstatt bokfõringsdato;
                             SVE=ErsÑtt bokfîringsdatum];
                  ToolTipML=[DEU=Gibt an, ob das Buchungsdatum der Buch.-BlÑtter durch das im Feld "Buchungsdatum" eingegebene Datum ersetzt werden soll.;
                             ENU=Specifies if you want to replace the journals' posting date with the date that is entered in the Posting Date field.;
                             NLD=Hiermee wordt opgegeven of de boekingsdatum van de dagboeken wordt vervangen door de datum die is ingevoerd in het veld Boekingsdatum.;
                             NOR=Angir om du vil erstatte kladdens bokfõringsdato for med datoen som er angitt i feltet Bokfõringsdato.];
                  ApplicationArea=#Intercompany;
                  SourceExpr=ReplacePostingDate;
                  OnValidate=BEGIN
                               IF ReplacePostingDate THEN
                                 PostingDateEditable := TRUE
                               ELSE BEGIN
                                 TempGenJnlLine."Posting Date" := 0D;
                                 PostingDateEditable := FALSE;
                               END;
                             END;
                              }

      { 3   ;3   ;Field     ;
                  Name=Posting Date;
                  CaptionML=[DEU=Buchungsdatum;
                             ENU=Posting Date;
                             NLD=Boekingsdatum;
                             NOR=Bokfõringsdato;
                             SVE=Bokfîringsdatum];
                  ToolTipML=[DEU="Gibt das Datum an, das verwendet wird, wenn Sie buchen, sofern Sie das Feld ""Buchungsdatum ersetzen"" ausgewÑhlt haben. Wenn das Buchungsdatum auf einem Beleg leer ist, wird das Datum im Feld ""Buchungsdatum"" verwendet, selbst wenn Sie es nicht ausgewÑhlt haben. ";
                             ENU="Specifies the date that will be used when you post, if you have selected the Replace Posting Date field. If the posting date on a document is blank, the date in the Posting Date field is used even if you have not selected it. ";
                             NLD="Hiermee wordt de datum opgegeven die bij het boeken wordt gebruikt als u de optie Boekingsdatum vervangen hebt geselecteerd. Als de boekingsdatum voor een document leeg is, wordt de datum in het veld Boekingsdatum gebruikt, zelfs als deze optie niet is ingeschakeld. ";
                             NOR="Angir datoen som skal brukes nÜr du bokfõrer, hvis du har valgt feltet Erstatt bokfõringsdato. Hvis bokfõringsdatoen i et dokument er tom, brukes datoen i feltet Bokfõringsdato selv om du ikke har valgt den. "];
                  ApplicationArea=#Intercompany;
                  SourceExpr=TempGenJnlLine."Posting Date";
                  Editable=PostingDateEditable;
                  OnValidate=BEGIN
                               ValidateJnl;
                             END;
                              }

      { 15  ;2   ;Group     ;
                  CaptionML=[DEU=Belege;
                             ENU=Documents;
                             NLD=Documenten;
                             NOR=Bilag;
                             SVE=Dokument] }

      { 11  ;3   ;Field     ;
                  Name=ReplaceDocPostingDate;
                  CaptionML=[DEU=Buchungsdatum ersetzen;
                             ENU=Replace Posting Date;
                             NLD=Boekingsdatum vervangen;
                             NOR=Erstatt bokfõringsdato;
                             SVE=ErsÑtt bokfîringsdatum];
                  ToolTipML=[DEU=Gibt an, ob das Buchungsdatum der Buch.-BlÑtter durch das im Feld "Buchungsdatum" eingegebene Datum ersetzt werden soll.;
                             ENU=Specifies if you want to replace the journals' posting date with the date that is entered in the Posting Date field.;
                             NLD=Hiermee wordt opgegeven of de boekingsdatum van de dagboeken wordt vervangen door de datum die is ingevoerd in het veld Boekingsdatum.;
                             NOR=Angir om du vil erstatte kladdens bokfõringsdato for med datoen som er angitt i feltet Bokfõringsdato.];
                  ApplicationArea=#Intercompany;
                  SourceExpr=ReplaceDocPostingDate;
                  OnValidate=BEGIN
                               IF ReplaceDocPostingDate THEN BEGIN
                                 DocPostingDateEditable := TRUE
                               END ELSE BEGIN
                                 DocPostingDate := 0D;
                                 DocPostingDateEditable := FALSE;
                               END;
                             END;
                              }

      { 14  ;3   ;Field     ;
                  Name=Doc Posting Date;
                  CaptionML=[DEU=Buchungsdatum;
                             ENU=Posting Date;
                             NLD=Boekingsdatum;
                             NOR=Bokfõringsdato;
                             SVE=Bokfîringsdatum];
                  ToolTipML=[DEU=Gibt das Buchungsdatum an.;
                             ENU=Specifies Posting Date.;
                             NLD=Hiermee wordt de boekingsdatum opgegeven.;
                             NOR=Angir bokfõringsdato.];
                  ApplicationArea=#Intercompany;
                  SourceExpr=DocPostingDate;
                  Editable=DocPostingDateEditable }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TempGenJnlLine@1003 : TEMPORARY Record 81;
      GenJnlTemplate@1001 : Record 80;
      GenJnlBatch@1002 : Record 232;
      GLSetup@1007 : Record 98;
      NoSeriesMgt@1006 : Codeunit 396;
      ICIOMgt@1009 : Codeunit 427;
      DimMgt@1000 : Codeunit 408;
      GLSetupFound@1005 : Boolean;
      ReplacePostingDate@1014 : Boolean;
      ReplaceDocPostingDate@1015 : Boolean;
      DocPostingDate@1016 : Date;
      Forward@1017 : Boolean;
      Text001@1004 : TextConst 'DEU=%1 %2 von IC-Partner %3 ist bereits im Fenster %4 vorhanden. Lîschen Sie %1 %2 im Fenster %4, bevor Sie die Zeilenaktion abschlie·en.;ENU=%1 %2 from IC Partner %3 already exists in the %4 window. You have to delete %1 %2 in the %4 window before you complete the line action.;NLD=%1 %2 van IC-partner %3 bestaat al in het venster %4. U moet %1 %2 verwijderen in het venster %4 voordat u de regelactie kunt voltooien.;NOR=%1 %2 fra KI-partneren %3 finnes allerede i vinduet %4. Du mÜ slette %1 %2 i vinduet %4 fõr du fullfõrer linjehandlingen.';
      DocPostingDateEditable@19025509 : Boolean INDATASET;
      PostingDateEditable@19008133 : Boolean INDATASET;

    LOCAL PROCEDURE ValidateJnl@3();
    BEGIN
      PageValidateJnl;
    END;

    LOCAL PROCEDURE GetGLSetup@1();
    BEGIN
      IF NOT GLSetupFound THEN
        GLSetup.GET;
      GLSetupFound := TRUE;
    END;

    LOCAL PROCEDURE PageValidateJnl@19046080();
    VAR
      GenJnlLine@1000 : Record 81;
    BEGIN
      TempGenJnlLine."Document No." := '';
      GenJnlLine.SETRANGE("Journal Template Name",TempGenJnlLine."Journal Template Name");
      GenJnlLine.SETRANGE("Journal Batch Name",TempGenJnlLine."Journal Batch Name");
      IF GenJnlLine.FINDLAST THEN BEGIN
        TempGenJnlLine."Document No." := INCSTR(GenJnlLine."Document No.");
        TempGenJnlLine."Line No." := GenJnlLine."Line No.";
      END ELSE
        IF GenJnlBatch.GET(TempGenJnlLine."Journal Template Name",TempGenJnlLine."Journal Batch Name") THEN
          IF GenJnlBatch."No. Series" = '' THEN
            TempGenJnlLine."Document No." := ''
          ELSE BEGIN
            TempGenJnlLine."Document No." := NoSeriesMgt.GetNextNo(GenJnlBatch."No. Series",TempGenJnlLine."Posting Date",FALSE);
            CLEAR(NoSeriesMgt);
          END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterMoveICInboxSalesHeaderToHandled@2(VAR ICInboxSalesHeader@1000 : Record 434;VAR HandledICInboxSalesHeader@1001 : Record 438);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterMoveICInboxPurchHeaderToHandled@5(VAR ICInboxPurchaseHeader@1000 : Record 436;VAR HandledICInboxPurchHeader@1001 : Record 440);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeHandledInboxTransactionInsert@4(VAR HandledICInboxTrans@1000 : Record 420);
    BEGIN
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

