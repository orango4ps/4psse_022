OBJECT Report 750 Save as Standard Gen. Journal
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Als Standard Fibu Buch.-Blatt speichern;
               ENU=Save as Standard Gen. Journal;
               NLD=Opslaan als standaarddagboek;
               NOR=Lagre som standard finanskladd];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF Code = '' THEN
                    ERROR(Text000);

                  StdJournalCreated := FALSE;
                  SaveGenJnlAsStandardJnl;
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 2   ;2   ;Field     ;
                  Name=Code;
                  Lookup=Yes;
                  CaptionML=[DEU=Code;
                             ENU=Code;
                             NLD=Code;
                             NOR=Kode;
                             SVE=Kod];
                  ToolTipML=[DEU=Gibt den Code fÅr das Buch.-Blatt an.;
                             ENU=Specifies the code for the journal.;
                             NLD=Hiermee wordt de code voor het dagboek opgegeven.;
                             NOR=Angir koden for kladden.];
                  ApplicationArea=#Suite;
                  NotBlank=Yes;
                  SourceExpr=Code;
                  OnLookup=VAR
                             StdGenJnl@1001 : Record 750;
                             StdGenJnls@1000 : Page 750;
                           BEGIN
                             StdGenJnl.SETRANGE("Journal Template Name",GenJnlBatch."Journal Template Name");
                             StdGenJnls.SETTABLEVIEW(StdGenJnl);

                             StdGenJnls.LOOKUPMODE := TRUE;
                             StdGenJnls.EDITABLE := FALSE;
                             IF StdGenJnls.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               StdGenJnls.GETRECORD(StdGenJnl);
                               Code := StdGenJnl.Code;
                               Description := StdGenJnl.Description;
                             END;
                           END;
                            }

      { 4   ;2   ;Field     ;
                  CaptionML=[DEU=Beschreibung;
                             ENU=Description;
                             NLD=Omschrijving;
                             NOR=Beskrivelse;
                             SVE=Beskrivning];
                  ToolTipML=[DEU=Gibt eine Beschreibung des Buch.-Blatts an.;
                             ENU=Specifies a description of the journal.;
                             NLD=Hiermee wordt een omschrijving van het dagboek opgegeven.;
                             NOR=Angir en beskrivelse av kladden.];
                  ApplicationArea=#Suite;
                  SourceExpr=Description }

      { 8   ;2   ;Field     ;
                  CaptionML=[DEU=Betrag speichern;
                             ENU=Save Amount;
                             NLD=Bedrag opslaan;
                             NOR=Lagre belõp];
                  ToolTipML=[DEU=Legt fest, ob die Werte im Feld "Betrag" des Standardbuch.-Blatts gespeichert werden sollen.;
                             ENU=Specifies if you want to save the values in the Amount field of the standard journal.;
                             NLD=Hiermee wordt opgegeven of u de waarden in het veld Bedrag van het standaarddagboek wilt opslaan.;
                             NOR=Angir om du vil lagre verdiene i feltet Belõp i standardkladden.];
                  ApplicationArea=#Suite;
                  SourceExpr=SaveAmount }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      GenJnlLine@1008 : Record 81;
      GenJnlBatch@1002 : Record 232;
      StdGenJnl@1007 : Record 750;
      Code@1000 : Code[10];
      Description@1001 : Text[100];
      Text000@1004 : TextConst 'DEU=Geben Sie einen Code fÅr das Standard Fibu Buch.-Blatt ein.;ENU=Enter a code for Standard General Journal.;NLD=Geef een code op voor het standaarddagboek.;NOR=Angi en kode for standard finanskladd.';
      Text001@1005 : TextConst 'DEU=Das Standard Fibu Buch.-Blatt ''%1'' ist bereits vorhanden. Mîchten Sie es Åberschreiben?;ENU=Standard General Journal %1 already exists. Do you want to overwrite?;NLD=Standaarddagboek %1 bestaat reeds. Wilt u dit overschrijven?;NOR=Standard finanskladd %1 finnes allerede. Vil du overskrive den?';
      SaveAmount@1006 : Boolean;
      StdJournalCreated@1003 : Boolean;
      NextLineNo@1010 : Integer;

    [External]
    PROCEDURE Initialise@2(VAR SelectedGenJnlLines@1000 : Record 81;SelectedGenJnlBatch@1001 : Record 232);
    BEGIN
      GenJnlLine.COPY(SelectedGenJnlLines);
      GenJnlBatch := SelectedGenJnlBatch;
    END;

    [External]
    PROCEDURE InitializeRequest@7(NewCode@1002 : Code[10];NewDescription@1001 : Text[50];NewSaveAmount@1000 : Boolean);
    BEGIN
      Code := NewCode;
      Description := NewDescription;
      SaveAmount := NewSaveAmount;
    END;

    LOCAL PROCEDURE SaveGenJnlAsStandardJnl@6();
    VAR
      StdGenJnlLine@1000 : Record 751;
      ConfirmManagement@1001 : Codeunit 27;
    BEGIN
      StdGenJnl.INIT;
      StdGenJnl."Journal Template Name" := GenJnlBatch."Journal Template Name";
      StdGenJnl.Code := Code;
      StdGenJnl.Description := Description;

      IF StdGenJnlExists THEN
        IF NOT ConfirmManagement.ConfirmProcess(STRSUBSTNO(Text001,StdGenJnl.Code),TRUE) THEN
          EXIT;

      StdGenJnlLine.LOCKTABLE;
      StdGenJnl.LOCKTABLE;

      IF StdGenJnlExists THEN BEGIN
        StdGenJnl.MODIFY(TRUE);
        StdGenJnlLine.SETRANGE("Journal Template Name",StdGenJnl."Journal Template Name");
        StdGenJnlLine.SETRANGE("Standard Journal Code",StdGenJnl.Code);
        StdGenJnlLine.DELETEALL(TRUE);
      END ELSE
        StdGenJnl.INSERT(TRUE);

      NextLineNo := 10000;
      IF GenJnlLine.FIND('-') THEN
        REPEAT
          StdGenJnlLine."Line No." := NextLineNo;
          NextLineNo := NextLineNo + 10000;
          StdGenJnlLine.INIT;
          StdGenJnlLine."Journal Template Name" := StdGenJnl."Journal Template Name";
          StdGenJnlLine."Standard Journal Code" := StdGenJnl.Code;
          StdGenJnlLine.TRANSFERFIELDS(GenJnlLine,FALSE);
          StdGenJnlLine."Shortcut Dimension 1 Code" := '';
          StdGenJnlLine."Shortcut Dimension 2 Code" := '';
          IF NOT SaveAmount THEN BEGIN
            StdGenJnlLine.Amount := 0;
            StdGenJnlLine."Debit Amount" := 0;
            StdGenJnlLine."Credit Amount" := 0;
            StdGenJnlLine."Amount (LCY)" := 0;
            StdGenJnlLine."VAT Amount" := 0;
            StdGenJnlLine."VAT Base Amount" := 0;
            StdGenJnlLine."VAT Difference" := 0;
            StdGenJnlLine."Bal. VAT Amount" := 0;
            StdGenJnlLine."Bal. VAT Base Amount" := 0;
            StdGenJnlLine."Bal. VAT Difference" := 0;
            StdGenJnlLine."Balance (LCY)" := 0;
          END;
          StdGenJnlLine.INSERT(TRUE);
          CopyGenJnlLineDims(GenJnlLine,StdGenJnlLine);
        UNTIL GenJnlLine.NEXT = 0;

      StdJournalCreated := TRUE;
    END;

    LOCAL PROCEDURE CopyGenJnlLineDims@3(GenJnlLine@1000 : Record 81;StdGenJnlLine@1001 : Record 751);
    VAR
      DimMgt@1004 : Codeunit 408;
    BEGIN
      DimMgt.UpdateGlobalDimFromDimSetID(GenJnlLine."Dimension Set ID",StdGenJnlLine."Shortcut Dimension 1 Code",
        StdGenJnlLine."Shortcut Dimension 2 Code");
      StdGenJnlLine."Dimension Set ID" := GenJnlLine."Dimension Set ID";

      StdGenJnlLine.MODIFY;
    END;

    LOCAL PROCEDURE StdGenJnlExists@10() : Boolean;
    VAR
      StdGenJnl@1000 : Record 750;
    BEGIN
      StdGenJnl.SETRANGE("Journal Template Name",GenJnlBatch."Journal Template Name");
      StdGenJnl.SETRANGE(Code,Code);

      EXIT(StdGenJnl.FINDFIRST);
    END;

    [External]
    PROCEDURE GetStdGeneralJournal@1(VAR StdGenJnl1@1000 : Record 750) : Boolean;
    BEGIN
      IF StdJournalCreated THEN
        StdGenJnl1.COPY(StdGenJnl);

      EXIT(StdJournalCreated);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

