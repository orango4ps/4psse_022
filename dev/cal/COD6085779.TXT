OBJECT Codeunit 6085779 Sales Doc. - Get Status
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.4.00;
  }
  PROPERTIES
  {
    TableNo=6085590;
    OnRun=BEGIN
            "Document Status Text" := GetStatus(Rec);
          END;

  }
  CODE
  {
    VAR
      Text001@1160040000 : TextConst 'DEU=Fakturiert;ENU=Invoiced;NLD=Gefactureerd;NOR=Fakturert;SVE=Fakturerat';
      Text002@1160040001 : TextConst 'DEU=Ausgeglichen;ENU=Applied;NLD=Vereffend;NOR=Utlignet;SVE=Kopplat';
      Text003@1160040002 : TextConst 'DEU=Nicht angewendet;ENU=Un-applied;NLD=Niet vereffend';
      Text004@1160040003 : TextConst 'DEU=Teilweise versendet und fakturiert;ENU=Partically Shipped and Invoiced;NLD=Gedeeltelijk verzonden en gefactureerd.';
      Text005@1160040004 : TextConst 'DEU=Versendet, nicht fakturiert;ENU=Shipped, not Invoiced;NLD=Verzonden, niet gefactureerd';

    PROCEDURE GetStatus@1160040000(VAR Document@1160040000 : Record 6085590) : Text[250];
    VAR
      SalesHeader@1160040001 : Record 36;
      SalesInvHeader@1160040002 : Record 112;
      SalesShptHeader@1160040004 : Record 110;
      CustLedgEntry@1160040005 : Record 21;
      PurchDocMgt@1160040003 : Codeunit 6085709;
      OrderExists@1160040008 : Boolean;
      Invoiced@1160040006 : Boolean;
      Shipped@1160040007 : Boolean;
    BEGIN
      WITH Document DO BEGIN
        IF Status = Status::Open THEN
          EXIT;

        IF Status = Status::Rejected THEN
          EXIT(FORMAT(Status));

        IF Document."Created Doc. Type" <> DATABASE::"Sales Header" THEN
          EXIT(FORMAT(Status));

        SalesInvHeader.SETCURRENTKEY("Order No.");
        SalesInvHeader.SETRANGE("Order No.","Created Doc. ID");
        Invoiced := SalesInvHeader.FINDFIRST;

        SalesShptHeader.SETCURRENTKEY("Order No.");
        SalesShptHeader.SETRANGE("Order No.","Created Doc. ID");
        Shipped := SalesShptHeader.FINDFIRST;

        OrderExists := SalesHeader.GET("Created Doc. Subtype","Created Doc. ID");

        CASE TRUE OF
          Shipped AND Invoiced AND OrderExists:
            EXIT(Text004);

          Shipped AND OrderExists:
            EXIT(Text005);

          OrderExists:
            EXIT(STRSUBSTNO('%1, %2',SalesHeader."Document Type",SalesHeader.Status));

          Invoiced:
            EXIT(GetLedgEntryStatus(
              SalesInvHeader."Sell-to Customer No.",SalesInvHeader."Posting Date",CustLedgEntry."Document Type"::Invoice,
                SalesInvHeader."No."));
        END;
      END;
    END;

    PROCEDURE GetLedgEntryStatus@1160040005(CustNo@1160040001 : Code[20];PostingDate@1160040003 : Date;LedgType@1160040000 : Integer;DocNo@1160040002 : Code[20]) : Text[250];
    VAR
      CustLedgEntry@1160040004 : Record 21;
    BEGIN
      CustLedgEntry.SETCURRENTKEY("Customer No.","Posting Date","Currency Code");
      CustLedgEntry.SETRANGE("Customer No.",CustNo);
      CustLedgEntry.SETRANGE("Posting Date",PostingDate);
      CustLedgEntry.SETRANGE("Document Type",LedgType);
      CustLedgEntry.SETRANGE("Document No.",DocNo);
      IF CustLedgEntry.FINDFIRST THEN BEGIN
        IF CustLedgEntry.Open THEN
          EXIT(STRSUBSTNO('%1, %2',Text001,Text003))
        ELSE
          EXIT(STRSUBSTNO('%1, %2',Text001,Text002));
      END;
    END;

    BEGIN
    END.
  }
}

