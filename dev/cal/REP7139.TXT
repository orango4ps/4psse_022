OBJECT Report 7139 Date Comp. Item Budget Entries
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW113.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Datumskompr. Art.-Budgetposten;
               ENU=Date Compr. Item Budget Entries;
               NLD=Datumcompressie artikelbudgetposten;
               NOR=Datokomprimer varebudsjettposter];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  DimSelectionBuf.CompareDimText(
                    3,REPORT::"Date Comp. Item Budget Entries",'',RetainDimText,Text009);
                  ItemBudgetEntryFilter := "Item Budget Entry".GETFILTERS;
                END;

  }
  DATASET
  {
    { 4172;    ;DataItem;                    ;
               DataItemTable=Table7134;
               DataItemTableView=SORTING(Analysis Area,Budget Name,Item No.,Date);
               OnPreDataItem=VAR
                               GLSetup@1000 : Record 98;
                             BEGIN
                               IF NOT
                                  CONFIRM(Text000,FALSE)
                               THEN
                                 CurrReport.BREAK;

                               IF EntrdDateComprReg."Ending Date" = 0D THEN
                                 ERROR(
                                   Text002,
                                   EntrdDateComprReg.FIELDCAPTION("Ending Date"));

                               DateComprReg.INIT;
                               DateComprReg."Starting Date" := EntrdDateComprReg."Starting Date";
                               DateComprReg."Ending Date" := EntrdDateComprReg."Ending Date";
                               DateComprReg."Period Length" := EntrdDateComprReg."Period Length";

                               IF AnalysisView.FINDFIRST THEN BEGIN
                                 AnalysisView.CheckDimensionsAreRetained(3,REPORT::"Date Comp. Item Budget Entries",TRUE);
                                 AnalysisView.CheckViewsAreUpdated;
                                 COMMIT;
                               END;

                               SelectedDim.GetSelectedDim(
                                 USERID,3,REPORT::"Date Comp. Item Budget Entries",'',TempSelectedDim);
                               GLSetup.GET;
                               Retain[1] :=
                                 TempSelectedDim.GET(
                                   USERID,3,REPORT::"Date Comp. Item Budget Entries",'',GLSetup."Global Dimension 1 Code");
                               Retain[2] :=
                                 TempSelectedDim.GET(
                                   USERID,3,REPORT::"Date Comp. Item Budget Entries",'',GLSetup."Global Dimension 2 Code");

                               SourceCodeSetup.GET;
                               SourceCodeSetup.TESTFIELD("Compress Item Budget");

                               ItemBudgetEntry2.LOCKTABLE;
                               IF ItemBudgetEntry2.FIND('+') THEN;
                               LastEntryNo := ItemBudgetEntry2."Entry No.";
                               LowestEntryNo := 2147483647;

                               Window.OPEN(
                                 Text003 +
                                 Text004 +
                                 Text005 +
                                 Text006 +
                                 Text007 +
                                 Text008);

                               SETRANGE("Entry No.",0,LastEntryNo);
                               SETRANGE(Date,EntrdDateComprReg."Starting Date",EntrdDateComprReg."Ending Date");
                               SETRANGE("Analysis Area",AnalysisAreaSelection);

                               InitRegisters;
                             END;

               OnAfterGetRecord=VAR
                                  ItemBudgetName@1000 : Record 7132;
                                BEGIN
                                  ItemBudgetName.GET("Analysis Area","Budget Name");
                                  Retain[3] :=
                                    TempSelectedDim.GET(
                                      USERID,3,REPORT::"Date Comp. Item Budget Entries",'',ItemBudgetName."Budget Dimension 1 Code");
                                  Retain[4] :=
                                    TempSelectedDim.GET(
                                      USERID,3,REPORT::"Date Comp. Item Budget Entries",'',ItemBudgetName."Budget Dimension 2 Code");
                                  Retain[5] :=
                                    TempSelectedDim.GET(
                                      USERID,3,REPORT::"Date Comp. Item Budget Entries",'',ItemBudgetName."Budget Dimension 3 Code");
                                  ItemBudgetEntry2 := "Item Budget Entry";
                                  WITH ItemBudgetEntry2 DO BEGIN
                                    SETCURRENTKEY("Analysis Area","Budget Name","Item No.",Date);
                                    COPYFILTERS("Item Budget Entry");
                                    SETFILTER(Date,DateComprMgt.GetDateFilter(Date,EntrdDateComprReg,FALSE));
                                    SETRANGE("Analysis Area","Analysis Area");
                                    SETRANGE("Budget Name","Budget Name");
                                    SETRANGE("Item No.","Item No.");

                                    LastEntryNo := LastEntryNo + 1;

                                    IF RetainNo(FIELDNO("Global Dimension 1 Code")) THEN
                                      SETRANGE("Global Dimension 1 Code","Global Dimension 1 Code");
                                    IF RetainNo(FIELDNO("Global Dimension 2 Code")) THEN
                                      SETRANGE("Global Dimension 2 Code","Global Dimension 2 Code");
                                    IF Quantity >= 0 THEN
                                      SETFILTER(Quantity,'>=0')
                                    ELSE
                                      SETFILTER(Quantity,'<0');
                                    IF "Cost Amount" >= 0 THEN
                                      SETFILTER("Cost Amount",'>=0')
                                    ELSE
                                      SETFILTER("Cost Amount",'<0');
                                    IF "Sales Amount" >= 0 THEN
                                      SETFILTER("Sales Amount",'>=0')
                                    ELSE
                                      SETFILTER("Sales Amount",'<0');

                                    InitNewEntry(NewItemBudgetEntry);

                                    DimBufMgt.CollectDimEntryNo(
                                      TempSelectedDim,"Dimension Set ID","Entry No.",0,FALSE,DimEntryNo);
                                    ComprDimEntryNo := DimEntryNo;
                                    SummarizeEntry(NewItemBudgetEntry,ItemBudgetEntry2);
                                    WHILE NEXT <> 0 DO BEGIN
                                      DimBufMgt.CollectDimEntryNo(
                                        TempSelectedDim,"Dimension Set ID","Entry No.",ComprDimEntryNo,TRUE,DimEntryNo);
                                      IF DimEntryNo = ComprDimEntryNo THEN
                                        SummarizeEntry(NewItemBudgetEntry,ItemBudgetEntry2);
                                    END;

                                    InsertNewEntry(NewItemBudgetEntry,ComprDimEntryNo);

                                    ComprCollectedEntries;
                                  END;

                                  IF DateComprReg."No. Records Deleted" >= NoOfDeleted + 10 THEN BEGIN
                                    NoOfDeleted := DateComprReg."No. Records Deleted";
                                    InsertRegisters(DateComprReg);
                                    COMMIT;
                                    ItemBudgetEntry2.LOCKTABLE;
                                    ItemBudgetEntry2.RESET;
                                    IF ItemBudgetEntry2.FIND('+') THEN;
                                    LastEntryNo := ItemBudgetEntry2."Entry No.";
                                  END;
                                END;

               OnPostDataItem=VAR
                                UpdateAnalysisView@1000 : Codeunit 410;
                              BEGIN
                                IF DateComprReg."No. Records Deleted" > NoOfDeleted THEN
                                  InsertRegisters(DateComprReg);

                                IF AnalysisView.FINDFIRST THEN
                                  IF LowestEntryNo < 2147483647 THEN
                                    UpdateAnalysisView.SetLastBudgetEntryNo(LowestEntryNo - 1);
                              END;

               ReqFilterFields=Budget Name,Item No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   InitializeVariables;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 5   ;2   ;Field     ;
                  Name=AnalysisArea;
                  CaptionML=[DEU=Analysebereich;
                             ENU=Analysis Area;
                             NLD=Analysegebied;
                             NOR=Analyseomr†de;
                             SVE=Analysomr†de];
                  ToolTipML=[DEU=Gibt den Analysebereich der Datumkomponente des Artikelbudgetpostens an.;
                             ENU=Specifies the analysis area of the date component item budget entry.;
                             NLD=Hiermee wordt het analysegebied van de artikelbudgetpost van het datumonderdeel opgegeven.;
                             NOR=Angir analyseomr†det for varebudsjettposten for datokomponenten.];
                  OptionCaptionML=[DEU=Verkauf,Einkauf;
                                   ENU=Sales,Purchase;
                                   NLD=Verkoop,Inkoop;
                                   NOR=Salg,Kj›p;
                                   SVE=F”rs„ljning,Ink”p];
                  ApplicationArea=#ItemBudget;
                  SourceExpr=AnalysisAreaSelection }

      { 1   ;2   ;Field     ;
                  Name=StartingDate;
                  CaptionML=[DEU=Startdatum;
                             ENU=Starting Date;
                             NLD=Begindatum;
                             NOR=Startdato;
                             SVE=Startdatum];
                  ToolTipML=[DEU=Gibt das erste Datum an, das in der Datumskomprimierung enthalten sein soll.;
                             ENU=Specifies the first date to be included in the date compression.;
                             NLD=Hiermee wordt de eerste datum opgegeven die in de datumcompressie moet worden opgenomen.;
                             NOR=Angir f›rste dato som skal inkluderes i datokomprimeringen.];
                  ApplicationArea=#ItemBudget;
                  ClosingDates=Yes;
                  SourceExpr=EntrdDateComprReg."Starting Date" }

      { 2   ;2   ;Field     ;
                  Name=EndingDate;
                  CaptionML=[DEU=Enddatum;
                             ENU=Ending Date;
                             NLD=Einddatum;
                             NOR=Sluttdato;
                             SVE=Slutdatum];
                  ToolTipML=[DEU=Gibt das Enddatum an.;
                             ENU=Specifies the end date.;
                             NLD=Hiermee wordt de einddatum opgegeven.;
                             NOR=Angir sluttdatoen.;
                             SVE=Anger slutdatumet.];
                  ApplicationArea=#ItemBudget;
                  ClosingDates=Yes;
                  SourceExpr=EntrdDateComprReg."Ending Date" }

      { 3   ;2   ;Field     ;
                  Name=PeriodLength;
                  CaptionML=[DEU=Periodenl„nge;
                             ENU=Period Length;
                             NLD=Periodelengte;
                             NOR=Periodelengde;
                             SVE=Period];
                  ToolTipML=[DEU=Gibt die L„nge der Periode an, innerhalb der Posten zusammengefasst werden. W„hlen Sie das Feld, um die Optionen anzuzeigen.;
                             ENU=Specifies the length of the period whose entries will be combined. Choose the field to see the options.;
                             NLD=Hiermee wordt de lengte opgegeven van de periode waarvoor de posten worden gecombineerd. Kies het veld om de opties te bekijken.;
                             NOR=Angir lengden p† perioden som har poster som vil bli kombinert. Velg feltet for † vise alternativene.];
                  OptionCaptionML=[DEU=Tag,Woche,Monat,Quartal,Jahr,Periode;
                                   ENU=Day,Week,Month,Quarter,Year,Period;
                                   NLD=Dag,Week,Maand,Kwartaal,Jaar,Periode;
                                   NOR=Dag,Uke,M†ned,Kvartal,r,Periode;
                                   SVE=Dag,Vecka,M†nad,Kvartal,r,Period];
                  ApplicationArea=#ItemBudget;
                  SourceExpr=EntrdDateComprReg."Period Length" }

      { 4   ;2   ;Field     ;
                  Name=PostingDescription;
                  CaptionML=[DEU=Buchungsbeschreibung;
                             ENU=Posting Description;
                             NLD=Boekingsomschrijving;
                             NOR=Bokf›ringsbeskrivelse;
                             SVE=Bokf”ringsbeskrivning];
                  ToolTipML=[DEU=Gibt einen Text an, der die aus der Komprimierung resultierenden Posten begleitet. Die Standardbeschreibung lautet "Datumskomprimiert".;
                             ENU=Specifies a text that will accompany the entries resulting from the compression. The default description is "Date Compressed.";
                             NLD=Hiermee wordt een tekst opgegeven voor de posten die tijdens de compressie worden gevormd. De standaardomschrijving is Gecomprimeerde datum.;
                             NOR=Angir en tekst som f›lger med postene som er et resultat av komprimeringen. Standardbeskrivelsen er Datokomprimert.];
                  ApplicationArea=#ItemBudget;
                  SourceExpr=EntrdItemBudgetEntry.Description }

      { 6   ;2   ;Field     ;
                  Name=RetainDimensions;
                  CaptionML=[DEU=Dimensionen behalten;
                             ENU=Retain Dimensions;
                             NLD=Te bewaren dimensies;
                             NOR=Behold dimensjoner;
                             SVE=Bibeh†ll dimensioner];
                  ToolTipML=[DEU=Gibt die Felder an, deren Inhalt Sie behalten m”chten, obwohl die Posten komprimiert werden. Je mehr Felder Sie ausw„hlen, desto detaillierter werden die komprimierten Posten sein.;
                             ENU=Specifies the fields you want to retain the contents of even though the entries will be compressed. The more fields you select, the more detailed the compressed entries will be.;
                             NLD=Hiermee worden de velden opgegeven waarvan u de inhoud wilt behouden, zelfs als de posten worden gecomprimeerd. Hoe meer velden u selecteert, des te nauwkeuriger de gecomprimeerde posten zullen zijn.;
                             NOR=Angir feltene du vil beholde innholdet i, selv om postene komprimeres. Jo flere felt du velger, jo mer detaljerte blir de komprimerte postene.];
                  ApplicationArea=#Dimensions;
                  SourceExpr=RetainDimText;
                  Editable=FALSE;
                  OnAssistEdit=BEGIN
                                 DimSelectionBuf.SetDimSelectionMultiple(3,REPORT::"Date Comp. Item Budget Entries",RetainDimText);
                               END;
                                }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Durch diesen Batchauftrag werden Posten gel”scht.\\M”chten Sie die Buchungen datumskomprimieren?;ENU=This batch job deletes entries.\\Do you want to date compress the entries?;NLD=Met deze batchverwerking worden posten verwijderd.\\Wilt u de posten comprimeren?;NOR=Denne kj›rselen sletter poster.\\Vil du datokomprimere postene?';
      Text002@1002 : TextConst 'DEU=%1 muss angegeben werden.;ENU=%1 must be specified.;NLD=%1 moet opgegeven worden.;NOR=%1 m† angis.;SVE=%1 m†ste specificeras.';
      Text003@1003 : TextConst 'DEU=Artikelbudgetposten werden datumskomprimiert...\\;ENU=Date compressing Item budget entries...\\;NLD=Datumcompressie artikelbudgetposten...\\;NOR=Datokomprimerer varebudsjettposter...\\';
      Text004@1004 : TextConst 'DEU=Budgetname           #1##########\;ENU=Budget Name          #1##########\;NLD=Budget               #1##########\;NOR=Budsjettnavn         #1##########\';
      Text005@1005 : TextConst 'DEU=Artikelnr.           #2##########\;ENU=Item No.             #2##########\;NLD=Artikelnr.           #2##########\;NOR=Varenr.              #2##########\';
      Text006@1006 : TextConst 'DEU=Datum                #3######\\;ENU=Date                 #3######\\;NLD=Datum                #3######\\;NOR=Dato                 #3######\\';
      Text007@1007 : TextConst 'DEU=Anzahl neue Posten   #4######\;ENU=No. of new entries   #4######\;NLD=Nieuwe posten        #4######\;NOR=Antall nye poster    #4######\';
      Text008@1008 : TextConst 'DEU=Anzahl gel”scht      #5######;ENU=No. of entries del.  #5######;NLD=Verwijderde posten   #5######;NOR=Ant. slettede poster #5######';
      Text009@1009 : TextConst 'DEU=Dimensionen behalten;ENU=Retain Dimensions;NLD=Te bewaren dimensies;NOR=Behold dimensjoner;SVE=Bibeh†ll dimensioner';
      SourceCodeSetup@1035 : Record 242;
      DateComprReg@1010 : Record 87;
      EntrdDateComprReg@1011 : Record 87;
      EntrdItemBudgetEntry@1012 : Record 7134;
      NewItemBudgetEntry@1013 : Record 7134;
      ItemBudgetEntry2@1014 : Record 7134;
      SelectedDim@1015 : Record 369;
      TempSelectedDim@1016 : TEMPORARY Record 369;
      DimSelectionBuf@1017 : Record 368;
      AnalysisView@1018 : Record 7152;
      DateComprMgt@1019 : Codeunit 356;
      DimBufMgt@1020 : Codeunit 411;
      DimMgt@1021 : Codeunit 408;
      Window@1022 : Dialog;
      NoOfFields@1023 : Integer;
      Retain@1024 : ARRAY [10] OF Boolean;
      FieldNumber@1025 : ARRAY [10] OF Integer;
      FieldNameArray@1026 : ARRAY [10] OF Text[100];
      LastEntryNo@1027 : Integer;
      LowestEntryNo@1028 : Integer;
      NoOfDeleted@1029 : Integer;
      i@1030 : Integer;
      ComprDimEntryNo@1031 : Integer;
      DimEntryNo@1032 : Integer;
      RetainDimText@1033 : Text[250];
      AnalysisAreaSelection@1034 : 'Sales,Purchase';
      ItemBudgetEntryFilter@1036 : Text;

    LOCAL PROCEDURE InitRegisters@9();
    BEGIN
      IF DateComprReg.FINDLAST THEN;
      DateComprReg.INIT;
      DateComprReg."No." := DateComprReg."No." + 1;
      DateComprReg."Table ID" := DATABASE::"Item Budget Entry";
      DateComprReg."Creation Date" := TODAY;
      DateComprReg."Starting Date" := EntrdDateComprReg."Starting Date";
      DateComprReg."Ending Date" := EntrdDateComprReg."Ending Date";
      DateComprReg."Period Length" := EntrdDateComprReg."Period Length";
      FOR i := 1 TO NoOfFields DO
        IF Retain[i] THEN
          DateComprReg."Retain Field Contents" :=
            COPYSTR(
              DateComprReg."Retain Field Contents" + ',' + FieldNameArray[i],1,
              MAXSTRLEN(DateComprReg."Retain Field Contents"));
      DateComprReg."Retain Field Contents" := COPYSTR(DateComprReg."Retain Field Contents",2);
      DateComprReg.Filter := COPYSTR(ItemBudgetEntryFilter,1,MAXSTRLEN(DateComprReg.Filter));
      DateComprReg."Register No." := "Item Budget Entry"."Entry No.";
      DateComprReg."Source Code" := SourceCodeSetup."Compress Item Budget";
      DateComprReg."User ID" := USERID;

      NoOfDeleted := 0;
    END;

    LOCAL PROCEDURE InsertRegisters@10(VAR DateComprReg@1000 : Record 87);
    BEGIN
      DateComprReg.INSERT;

      NewItemBudgetEntry.LOCKTABLE;
      DateComprReg.LOCKTABLE;

      ItemBudgetEntry2.RESET;
      IF ItemBudgetEntry2.FIND('+') THEN;
      IF LastEntryNo <> ItemBudgetEntry2."Entry No." THEN BEGIN
        LastEntryNo := ItemBudgetEntry2."Entry No.";
        InitRegisters;
      END;
    END;

    LOCAL PROCEDURE InsertField@2(Number@1000 : Integer;Name@1001 : Text[100]);
    BEGIN
      NoOfFields := NoOfFields + 1;
      FieldNumber[NoOfFields] := Number;
      FieldNameArray[NoOfFields] := Name;
    END;

    LOCAL PROCEDURE RetainNo@3(Number@1000 : Integer) : Boolean;
    BEGIN
      EXIT(Retain[Index(Number)]);
    END;

    LOCAL PROCEDURE Index@6(Number@1000 : Integer) : Integer;
    BEGIN
      FOR i := 1 TO NoOfFields DO
        IF Number = FieldNumber[i] THEN
          EXIT(i);
    END;

    LOCAL PROCEDURE SummarizeEntry@5(VAR NewItemBudgetEntry@1000 : Record 7134;ItemBudgetEntry@1001 : Record 7134);
    BEGIN
      WITH ItemBudgetEntry DO BEGIN
        NewItemBudgetEntry.Quantity := NewItemBudgetEntry.Quantity + Quantity;
        NewItemBudgetEntry."Cost Amount" := NewItemBudgetEntry."Cost Amount" + "Cost Amount";
        NewItemBudgetEntry."Sales Amount" := NewItemBudgetEntry."Sales Amount" + "Sales Amount";
        DELETE;
        IF "Entry No." < LowestEntryNo THEN
          LowestEntryNo := "Entry No.";
        DateComprReg."No. Records Deleted" := DateComprReg."No. Records Deleted" + 1;
        Window.UPDATE(5,DateComprReg."No. Records Deleted");
      END;
    END;

    LOCAL PROCEDURE ComprCollectedEntries@12();
    VAR
      ItemBudgetEntry@1000 : Record 7134;
      OldDimEntryNo@1001 : Integer;
      Found@1002 : Boolean;
      ItemBudgetEntryNo@1003 : Integer;
    BEGIN
      OldDimEntryNo := 0;
      IF DimBufMgt.FindFirstDimEntryNo(DimEntryNo,ItemBudgetEntryNo) THEN BEGIN
        InitNewEntry(NewItemBudgetEntry);
        REPEAT
          ItemBudgetEntry.GET(ItemBudgetEntryNo);
          SummarizeEntry(ItemBudgetEntry,ItemBudgetEntry);
          OldDimEntryNo := DimEntryNo;
          Found := DimBufMgt.NextDimEntryNo(DimEntryNo,ItemBudgetEntryNo);
          IF (OldDimEntryNo <> DimEntryNo) OR NOT Found THEN BEGIN
            InsertNewEntry(NewItemBudgetEntry,OldDimEntryNo);
            IF Found THEN
              InitNewEntry(NewItemBudgetEntry);
          END;
          OldDimEntryNo := DimEntryNo;
        UNTIL NOT Found;
      END;
      DimBufMgt.DeleteAllDimEntryNo;
    END;

    [External]
    PROCEDURE InitNewEntry@7(VAR NewItemBudgetEntry@1000 : Record 7134);
    BEGIN
      LastEntryNo := LastEntryNo + 1;

      WITH ItemBudgetEntry2 DO BEGIN
        NewItemBudgetEntry.INIT;
        NewItemBudgetEntry."Entry No." := LastEntryNo;
        NewItemBudgetEntry."Analysis Area" := AnalysisAreaSelection;
        NewItemBudgetEntry."Budget Name" := "Budget Name";
        NewItemBudgetEntry."Item No." := "Item No.";
        NewItemBudgetEntry.Date := GETRANGEMIN(Date);
        NewItemBudgetEntry.Description := EntrdItemBudgetEntry.Description;
        NewItemBudgetEntry."User ID" := USERID;

        IF RetainNo(FIELDNO("Global Dimension 1 Code")) THEN
          NewItemBudgetEntry."Global Dimension 1 Code" := "Global Dimension 1 Code";
        IF RetainNo(FIELDNO("Global Dimension 2 Code")) THEN
          NewItemBudgetEntry."Global Dimension 2 Code" := "Global Dimension 2 Code";
        IF RetainNo(FIELDNO("Budget Dimension 1 Code")) THEN
          NewItemBudgetEntry."Budget Dimension 1 Code" := "Budget Dimension 1 Code";
        IF RetainNo(FIELDNO("Budget Dimension 2 Code")) THEN
          NewItemBudgetEntry."Budget Dimension 2 Code" := "Budget Dimension 2 Code";
        IF RetainNo(FIELDNO("Budget Dimension 3 Code")) THEN
          NewItemBudgetEntry."Budget Dimension 3 Code" := "Budget Dimension 3 Code";

        Window.UPDATE(1,NewItemBudgetEntry."Budget Name");
        Window.UPDATE(2,NewItemBudgetEntry."Item No.");
        Window.UPDATE(3,NewItemBudgetEntry.Date);
        DateComprReg."No. of New Records" := DateComprReg."No. of New Records" + 1;
        Window.UPDATE(4,DateComprReg."No. of New Records");
      END;
    END;

    LOCAL PROCEDURE InsertNewEntry@23(VAR NewItemBudgetEntry@1000 : Record 7134;DimEntryNo@1001 : Integer);
    VAR
      TempDimBuf@1002 : TEMPORARY Record 360;
      TempDimSetEntry@1005 : TEMPORARY Record 480;
    BEGIN
      TempDimBuf.DELETEALL;
      DimBufMgt.GetDimensions(DimEntryNo,TempDimBuf);
      DimMgt.CopyDimBufToDimSetEntry(TempDimBuf,TempDimSetEntry);
      NewItemBudgetEntry."Dimension Set ID" := DimMgt.GetDimensionSetID(TempDimSetEntry);
      NewItemBudgetEntry.INSERT;
    END;

    [External]
    PROCEDURE InitializeRequest@1(AnalAreaSelection@1000 : Option;StartDate@1001 : Date;EndDate@1002 : Date;PeriodLength@1003 : Option;Desc@1004 : Text[50]);
    BEGIN
      InitializeVariables;
      AnalysisAreaSelection := AnalAreaSelection;
      EntrdDateComprReg."Starting Date" := StartDate;
      EntrdDateComprReg."Ending Date" := EndDate;
      EntrdDateComprReg."Period Length" := PeriodLength;
      EntrdItemBudgetEntry.Description := Desc;
    END;

    LOCAL PROCEDURE InitializeVariables@4();
    BEGIN
      IF EntrdDateComprReg."Ending Date" = 0D THEN
        EntrdDateComprReg."Ending Date" := TODAY;

      WITH "Item Budget Entry" DO BEGIN
        InsertField(FIELDNO("Global Dimension 1 Code"),FIELDCAPTION("Global Dimension 1 Code"));
        InsertField(FIELDNO("Global Dimension 2 Code"),FIELDCAPTION("Global Dimension 2 Code"));
        InsertField(FIELDNO("Budget Dimension 1 Code"),FIELDCAPTION("Budget Dimension 1 Code"));
        InsertField(FIELDNO("Budget Dimension 2 Code"),FIELDCAPTION("Budget Dimension 2 Code"));
        InsertField(FIELDNO("Budget Dimension 3 Code"),FIELDCAPTION("Budget Dimension 3 Code"));
      END;

      RetainDimText := DimSelectionBuf.GetDimSelectionText(3,REPORT::"Date Comp. Item Budget Entries",'');
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

