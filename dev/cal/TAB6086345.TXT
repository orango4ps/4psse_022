OBJECT Table 6086345 Field Type
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.3.00;
  }
  PROPERTIES
  {
    Permissions=TableData 6086348=r,
                TableData 6086352=r;
    DataCaptionFields=Code,Description;
    OnInsert=BEGIN
               "System Field" := IsSystemField;
             END;

    OnModify=BEGIN
               "Last Update Date/Time" := CURRENTDATETIME;
               "System Field" := IsSystemField;
             END;

    OnDelete=VAR
               ConfiguredField@1160040003 : Record 6086346;
               LookupValue@1160040000 : Record 6086347;
               TableFilterField@1160040002 : Record 6086348;
               FieldTranslation@1160040001 : Record 6086352;
             BEGIN
               IF IsSystemField THEN
                 ERROR(NotAllowedToDeleteSysFieldErr);

               LookupValue.SETRANGE("Field Type Code",Code);
               LookupValue.DELETEALL;

               FieldTranslation.SETRANGE("Field Type Code",Code);
               FieldTranslation.DELETEALL;

               TableFilterField.SETRANGE("Table Filter GUID","Source Table Filter GUID");
               TableFilterField.DELETEALL;

               ConfiguredField.SETRANGE("Field Code",Code);
               ConfiguredField.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               IF CodeIsSystemField(xRec.Code) THEN
                 ERROR(NotAllowedToRenameSysFieldErr);

               "Last Update Date/Time" := CURRENTDATETIME;
               "System Field" := FALSE;
             END;

    CaptionML=[DEU=Field Type;
               ENU=Field Type;
               NLD=Veldtype;
               NOR=Felttype;
               SVE=Field Type];
    LookupPageID=Page6086336;
    DrillDownPageID=Page6086336;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;OnValidate=BEGIN
                                                                IF IsSystemField AND (CurrFieldNo <> 0) THEN
                                                                  ERROR(NotAllowedToInsertSysFieldErr);
                                                              END;

                                                   CaptionML=[DEU=Code;
                                                              ENU=Code;
                                                              NLD=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text50        ;CaptionML=[DEU=Beschreibung;
                                                              ENU=Description;
                                                              NLD=Omschrijving;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 10  ;   ;Type                ;Option        ;OnValidate=VAR
                                                                ExpAttendee@1160040000 : Record 6086308;
                                                              BEGIN
                                                                IF IsSystemField THEN
                                                                  ERROR(NotAllowedToChangeSysFieldErr,FIELDCAPTION(Type));

                                                                CASE Type OF
                                                                  Type::Code:
                                                                    Length := 20;
                                                                  Type::Text:
                                                                    Length := 30;
                                                                  Type::Attendees:
                                                                    Length := MAXSTRLEN(ExpAttendee.Name);
                                                                  ELSE
                                                                    Length := 0;
                                                                END;

                                                                CASE Type OF
                                                                  Type::Decimal:
                                                                    "Decimal Places" := '2:2';
                                                                  ELSE
                                                                    "Decimal Places" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Art;
                                                              ENU=Type;
                                                              NLD=Type;
                                                              NOR=Type;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=[DEU=Ganzzahl,Text,Code,Dezimalzahl,Option,Boolesch,Datum,Adresse,Teilnehmer;
                                                                    ENU=Integer,Text,Code,Decimal,Option,Boolean,Date,Address,Attendees;
                                                                    NLD=Integer,Tekst,Code,Decimal,Optie,Boolean,Datum,Adres,Deelnemer];
                                                   OptionString=Integer,Text,Code,Decimal,Option,Boolean,Date,Address,Attendees }
    { 11  ;   ;Length              ;Integer       ;OnValidate=BEGIN
                                                                IF IsSystemField THEN
                                                                  ERROR(NotAllowedToChangeSysFieldErr,FIELDCAPTION(Length));

                                                                IF NOT (Type IN [Type::Text,Type::Code,Type::Address]) THEN
                                                                  ERROR(FieldTypeMustBeTextOrCodeErr,FIELDCAPTION(Type),Type::Code,Type::Text);
                                                              END;

                                                   CaptionML=[DEU=LÑnge;
                                                              ENU=Length;
                                                              NLD=Lengte;
                                                              NOR=Lengde;
                                                              SVE=LÑngd];
                                                   MaxValue=250;
                                                   BlankZero=Yes }
    { 12  ;   ;Manual Lookup Values;Boolean       ;CaptionML=[DEU=Manuelle Lookup-Werte;
                                                              ENU=Manual Lookup Values;
                                                              NLD=Handmatige opzoekwaarden] }
    { 15  ;   ;Source Table No.    ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   OnValidate=VAR
                                                                TableFilterField@1160040000 : Record 6086348;
                                                              BEGIN
                                                                IF "Source Table No." = xRec."Source Table No." THEN
                                                                  EXIT;

                                                                "Source Field No." := 0;
                                                                "Source Description Field No." := 0;

                                                                TableFilterField.SETRANGE("Table Filter GUID","Source Table Filter GUID");
                                                                TableFilterField.DELETEALL;
                                                              END;

                                                   CaptionML=[DEU=Herkunftstabellennr.;
                                                              ENU=Source Table No.;
                                                              NLD=Brontabelnr.];
                                                   BlankZero=Yes }
    { 16  ;   ;Source Table Name   ;Text249       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Source Table No.)));
                                                   CaptionML=[DEU=Herkunftstabellenname;
                                                              ENU=Source Table Name;
                                                              NLD=Brontabelnaam];
                                                   Editable=No }
    { 18  ;   ;Source Field No.    ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Source Table No.));
                                                   OnValidate=VAR
                                                                RecIDMgt@1160040000 : Codeunit 6085604;
                                                              BEGIN
                                                                IF "Source Field No." = xRec."Source Field No." THEN
                                                                  EXIT;

                                                                IF "Source Field No." = 0 THEN
                                                                  EXIT;

                                                                TESTFIELD("Source Table No.");

                                                                IF NOT RecIDMgt.PartOfKey("Source Table No.","Source Field No.") THEN
                                                                  ERROR(Text005,FIELDCAPTION("Source Field No."),FIELDCAPTION("Source Table No."));

                                                                AddDefaultCaptions("Source Table No.","Source Field No.");
                                                              END;

                                                   CaptionML=[DEU=Quellfeldnr.;
                                                              ENU=Source Field No.;
                                                              NLD=Bronveldnr.;
                                                              NOR=Kildefeltnr.;
                                                              SVE=KÑllfÑltsnr];
                                                   BlankZero=Yes }
    { 19  ;   ;Source Description Field No.;Integer;
                                                   TableRelation=Field.No. WHERE (TableNo=FIELD(Source Table No.));
                                                   OnValidate=BEGIN
                                                                IF "Source Description Field No." = 0 THEN
                                                                  EXIT;

                                                                TESTFIELD("Source Table No.");
                                                              END;

                                                   CaptionML=[DEU=Herkunftsbeschreibungsfeldnr.;
                                                              ENU=Source Description Field No.;
                                                              NLD=Bronomschrijvingveldnr.];
                                                   BlankZero=Yes }
    { 20  ;   ;No. of Lookup Values;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Lookup Value" WHERE (Field Type Code=FIELD(Code)));
                                                   CaptionML=[DEU=Anz. der Lookup-Werte;
                                                              ENU=No. of Lookup Values;
                                                              NLD=Aantal opzoekwaarden];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 21  ;   ;Source Field Name   ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Source Table No.),
                                                                                                   No.=FIELD(Source Field No.)));
                                                   CaptionML=[DEU=Quellfeldname;
                                                              ENU=Source Field Name;
                                                              NLD=Bronveldnaam];
                                                   Editable=No }
    { 22  ;   ;Description Field Name;Text80      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Source Table No.),
                                                                                                   No.=FIELD(Source Description Field No.)));
                                                   CaptionML=[DEU=Beschreibungsfeldname;
                                                              ENU=Description Field Name;
                                                              NLD=Omschrijving veldnaam];
                                                   Editable=No }
    { 40  ;   ;Mandatory           ;Boolean       ;OnValidate=BEGIN
                                                                IF Mandatory THEN
                                                                  TESTFIELD(Editable,TRUE);
                                                              END;

                                                   CaptionML=[DEU=Notwendig;
                                                              ENU=Mandatory;
                                                              NLD=Verplicht;
                                                              NOR=Obligatorisk;
                                                              SVE=Obligatoriskt] }
    { 45  ;   ;Editable            ;Boolean       ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                IF NOT Editable THEN
                                                                  TESTFIELD(Mandatory,FALSE);
                                                              END;

                                                   CaptionML=[DEU=Bearbeitbar;
                                                              ENU=Editable;
                                                              NLD=Wijzigbaar;
                                                              SVE=Kan redigeras] }
    { 50  ;   ;Last Update Date/Time;DateTime     ;CaptionML=[DEU=Letzte Aktualisierung Datum/Uhrzeit;
                                                              ENU=Last Update Date Time;
                                                              NLD=Laatste wijziging datum/tijd];
                                                   Editable=No }
    { 55  ;   ;Source Table Filter GUID;GUID      ;CaptionML=[DEU=Herkunftstabellenfilter-GUID;
                                                              ENU=Source Table Filter GUID;
                                                              NLD=Brontabelfilter GUID] }
    { 58  ;   ;No. of Source Table Filters;Integer;FieldClass=FlowField;
                                                   CalcFormula=Count("Table Filter Field EM" WHERE (Table Filter GUID=FIELD(Source Table Filter GUID)));
                                                   CaptionML=[DEU=Anz. der Quelltabellenfilter;
                                                              ENU=No. of Source Table Filters;
                                                              NLD=Aantal brontabelfilters];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 60  ;   ;System Field        ;Boolean       ;CaptionML=[DEU=Standardfeld;
                                                              ENU=System Field;
                                                              NLD=Systeemveld] }
    { 110 ;   ;No. of Translations ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Field Translation" WHERE (Field Type Code=FIELD(Code)));
                                                   CaptionML=[DEU=Anz. der öbersetzungen;
                                                              ENU=No. of Translations;
                                                              NLD=Aantal vertalingen];
                                                   Editable=No }
    { 120 ;   ;Decimal Places      ;Text5         ;OnValidate=BEGIN
                                                                IF NOT CorrectFormat("Decimal Places") THEN
                                                                  ERROR(ErrWrongDecimalPlaces);
                                                              END;

                                                   CaptionML=[DEU=Dezimalstellen;
                                                              ENU=Decimal Places;
                                                              NLD=Decimalen];
                                                   Description=0:2 or similar 2:2 is default }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
    {    ;Source Table Filter GUID                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ErrWrongDecimalPlaces@1160040001 : TextConst 'DEU=Das Format muss n:N sein, wobei n und N Ganzzahlen von 0 bis 10 sind;ENU=The format must be n:N where n and M are integers from 0 to 10.;NLD=Het formaat moet zijn n:M, waarbij n en M gehele getallen van 0 t/m 10 zijn.';
      FieldTypeMustBeTextOrCodeErr@1160040007 : TextConst 'DEU=%1 muss %2 oder %3 sein.;ENU=%1 must be either text or code.;NLD=%1 moet tekst of code zijn.;SVE=%1 mÜste vara antingen %2 eller %3.';
      NotAllowedToChangeSysFieldErr@1160040000 : TextConst 'DEU=Sie dÅrfen %1 auf einem Systemfeld nicht Ñndern.;ENU=You are not allowed to change %1 on a system field.;NLD=U mag %1 op een systeemveld niet wijzigen.';
      NotAllowedToDeleteSysFieldErr@1160040004 : TextConst 'DEU=Sie dÅrfen ein Systemfeld nicht lîschen.;ENU=You are not allowed to delete a system field.;NLD=U mag een systeemveld niet verwijderen.';
      NotAllowedToInsertSysFieldErr@1160040005 : TextConst 'DEU=Sie dÅrfen ein Systemfeld nicht erstellen.;ENU=You are not allowed to create a system field.;NLD=U mag een systeemveld niet aanmaken.';
      NotAllowedToRenameSysFieldErr@1160040006 : TextConst 'DEU=Sie dÅrfen ein Systemfeld nicht umbenennen.;ENU=You are not allowed to rename a system field.;NLD=U mag een systeemveld niet hernoemen.';
      Text005@1160040003 : TextConst 'DEU=%1 muss Teil des PrimÑrschlÅssels von %2 sein.;ENU=The %1 must be part of the primary key of the %2.;NLD=De %1 moet onderdeel zijn van de primaire sleutel van de %2.';
      UpdatingLookupValTxt@1160040002 : TextConst 'DEU=Aktualisieren Suchwerte\\#1##############################\@2@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@;ENU=Updating Lookup Values\\ #1##############################\ @2@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@;NLD=Bijwerken zoekwaarden\\#1##############################\@2@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@';

    PROCEDURE CalcLookupValForAllFields@1160040003();
    VAR
      FieldType@1160040000 : Record 6086345;
    BEGIN
      IF FieldType.FINDSET THEN
        REPEAT
          FieldType.FillLookupValuesForOneField;
        UNTIL FieldType.NEXT = 0;
    END;

    PROCEDURE CalcLookupValForFieldAndParent@1160040017(IsManualUpdate@1160040003 : Boolean);
    VAR
      FieldType@1160040001 : Record 6086345;
      TableFilterField@1160040000 : Record 6086348;
    BEGIN
      //FINDS TOP LEVEL AND THEN GOES TO ALL THE CHILDREN
      TableFilterField.SETCURRENTKEY("Table Filter GUID","Filter Type","Field Type Code");
      TableFilterField.SETRANGE("Table Filter GUID","Source Table Filter GUID");
      TableFilterField.SETRANGE("Filter Type",TableFilterField."Filter Type"::"Field Type");
      TableFilterField.SETFILTER("Field Type Code",'<>%1','');
      IF TableFilterField.FINDSET THEN
        //THIS FIELD HAS A PARENT
        REPEAT
          FieldType.GET(TableFilterField."Field Type Code");
          FieldType.CalcLookupValForFieldAndParent(IsManualUpdate);
        UNTIL TableFilterField.NEXT = 0
      ELSE BEGIN
        // THIS FIELD DOES NOT HAVE A PARENT
        // IF THIS FIELD IS A PARENT FOR OTHERS,WE CALCULATE FOR ALL THE CHILDREN
        CLEAR(TableFilterField);
        TableFilterField.SETCURRENTKEY("Filter Type","Field Type Code");
        TableFilterField.SETRANGE("Filter Type",TableFilterField."Filter Type"::"Field Type");
        TableFilterField.SETRANGE("Field Type Code",Code);
        IF TableFilterField.FINDSET THEN
          REPEAT
            FieldType.SETCURRENTKEY("Source Table Filter GUID");
            FieldType.SETRANGE("Source Table Filter GUID",TableFilterField."Table Filter GUID");
            FieldType.FINDFIRST;

            FieldType.FillLookupValuesForOneField;
          UNTIL TableFilterField.NEXT = 0;

        FillLookupValuesForOneField;

        IF IsManualUpdate THEN BEGIN
          "Last Update Date/Time" := CURRENTDATETIME;
          MODIFY;
        END;
      END;
    END;

    PROCEDURE FillLookupValuesForOneField@1160040000();
    VAR
      LookupValue@1160040003 : Record 6086347;
      Window@1160040010 : Dialog;
      RecRef@1160040000 : RecordRef;
      ParentSourceFieldNoRef@1160040008 : FieldRef;
      SourceDescFieldRef@1160040002 : FieldRef;
      SourceNoFieldRef@1160040001 : FieldRef;
      PrimaryFieldValue@1160040005 : Code[50];
      ParentFieldNo@1160040006 : Integer;
      RecIndex@1160040012 : Integer;
      TotalCount@1160040011 : Integer;
    BEGIN
      LookupValue.SETRANGE("Field Type Code",Code);
      LookupValue.SETRANGE(Manual,FALSE);
      LookupValue.DELETEALL(TRUE);

      IF "Source Table No." <> 0 THEN BEGIN
        TESTFIELD("Source Field No.");
        TESTFIELD("Source Description Field No.");

        RecRef.OPEN("Source Table No.");
        SetFixedFiltersOnRecRef(RecRef,"Source Table Filter GUID");

        IF GUIALLOWED THEN BEGIN
          Window.OPEN(UpdatingLookupValTxt);
          TotalCount := RecRef.COUNT;
        END;

        IF RecRef.FINDSET THEN
          REPEAT
            CLEAR(PrimaryFieldValue);
            ParentFieldNo := GetParentPKFieldNo("Source Table Filter GUID");
            IF ParentFieldNo <> 0 THEN BEGIN
              ParentSourceFieldNoRef := RecRef.FIELD(ParentFieldNo);
              PrimaryFieldValue := ParentSourceFieldNoRef.VALUE;
            END;

            IF GUIALLOWED THEN BEGIN
              RecIndex += 1;
              Window.UPDATE(1,Code);
              Window.UPDATE(2,ROUND(RecIndex / TotalCount * 10000,1));
            END;

            SourceNoFieldRef := RecRef.FIELD("Source Field No.");
            SourceDescFieldRef := RecRef.FIELD("Source Description Field No.");

            IF NOT LookupValue.GET(Code,PrimaryFieldValue,SourceNoFieldRef.VALUE) THEN BEGIN
              LookupValue.INIT;
              LookupValue."Field Type Code" := Code;
              LookupValue."Parent Field Type Code" := PrimaryFieldValue;
              LookupValue.Code := SourceNoFieldRef.VALUE;
              LookupValue.Description := COPYSTR(FORMAT(SourceDescFieldRef.VALUE),1,MAXSTRLEN(LookupValue.Description));
              LookupValue.INSERT;
            END ELSE BEGIN
              LookupValue.Description := COPYSTR(FORMAT(SourceDescFieldRef.VALUE),1,MAXSTRLEN(LookupValue.Description));
              LookupValue.Manual := FALSE;
              LookupValue.MODIFY;
            END;
          UNTIL RecRef.NEXT = 0;
        IF GUIALLOWED THEN
          Window.CLOSE;
      END;

      AddLCY;
    END;

    PROCEDURE GetFieldCaptionForApp@1160040012() : Text[30];
    VAR
      EMSetup@1160040001 : Record 6086300;
      FieldTranslation@1160040000 : Record 6086352;
    BEGIN
      EMSetup.GET;
      FieldTranslation.SETRANGE("Field Type Code",Code);
      FieldTranslation.SETRANGE("Language Code",EMSetup."Default Web/App Language");
      IF FieldTranslation.FINDFIRST THEN
        EXIT(FieldTranslation.Translation)
      ELSE
        IF Description <> '' THEN
          EXIT(Description)
        ELSE BEGIN
          FieldTranslation.SETRANGE("Language Code");
          IF FieldTranslation.FINDFIRST THEN
            EXIT(FieldTranslation.Translation);
        END;
      EXIT(Code);
    END;

    LOCAL PROCEDURE AddDefaultCaptions@1160040013(TableNo@1160040000 : Integer;FieldNumber@1160040001 : Integer);
    VAR
      Language@1160040011 : Record 8;
      ExpenseMgtSetup@1160040012 : Record 6086300;
      FieldTranslation@1160040002 : Record 6086352;
      Field@1160040007 : Record 2000000041;
      RecRef@1160040003 : RecordRef;
      FldRef@1160040006 : FieldRef;
      PrimaryField@1160040008 : FieldRef;
      KeyRef@1160040009 : KeyRef;
      OldLanguageCode@1160040005 : Integer;
    BEGIN
      IF (TableNo = 0) OR (FieldNumber = 0) THEN
        EXIT;

      ExpenseMgtSetup.GET;
      IF ExpenseMgtSetup."Default Web/App Language" = '' THEN
        EXIT;

      OldLanguageCode := GLOBALLANGUAGE;

      FieldTranslation."Field Type Code" := Code;

      Language.GET(ExpenseMgtSetup."Default Web/App Language");
      GLOBALLANGUAGE := Language."Windows Language ID";

      RecRef.CLOSE;
      RecRef.OPEN(TableNo);
      KeyRef := RecRef.KEYINDEX(1);
      PrimaryField := KeyRef.FIELDINDEX(1);
      IF (KeyRef.FIELDCOUNT = 1) AND (FieldNumber = PrimaryField.NUMBER) THEN BEGIN
        FieldTranslation."Language Code" := Language.Code;
        FieldTranslation.Translation := RecRef.CAPTION;
        IF NOT FieldTranslation.INSERT THEN
          FieldTranslation.MODIFY;
      END ELSE BEGIN
        FldRef := RecRef.FIELD(FieldNumber);
        FieldTranslation."Language Code" := Language.Code;
        FieldTranslation.Translation := FldRef.CAPTION;
        IF Field.GET(TableNo,FieldNumber) THEN BEGIN
          FieldTranslation.Translation := Field."Field Caption";
          IF NOT FieldTranslation.INSERT THEN
            FieldTranslation.MODIFY;
        END;
      END;
      CLEAR(KeyRef);
      CLEAR(PrimaryField);

      GLOBALLANGUAGE := OldLanguageCode;
    END;

    LOCAL PROCEDURE AddLCY@1160040015();
    VAR
      GeneralLedgerSetup@1160040001 : Record 98;
      LookupValue@1160040000 : Record 6086347;
    BEGIN
      IF "Source Table No." = DATABASE::Currency THEN BEGIN
        GeneralLedgerSetup.GET;
        IF GeneralLedgerSetup."LCY Code" <> '' THEN BEGIN
          LookupValue.SETRANGE("Field Type Code",Code);
          LookupValue.SETRANGE(Code,GeneralLedgerSetup."LCY Code");
          IF NOT LookupValue.FINDFIRST THEN BEGIN
            LookupValue.INIT;
            LookupValue."Field Type Code" := Code;
            LookupValue.Code := GeneralLedgerSetup."LCY Code";
            LookupValue.Description := GeneralLedgerSetup."LCY Code";
            LookupValue.INSERT;
          END;
        END;
      END;
    END;

    PROCEDURE GetDimCode@1160040001() : Code[20];
    VAR
      Dimension@1160040000 : Record 348;
      TableFilterField@1160040001 : Record 6086348;
    BEGIN
      IF ("Source Table No." = DATABASE::"Dimension Value") AND TableFilterField.GET("Source Table Filter GUID",1) THEN
        IF Dimension.GET(TableFilterField."Value (Text)") THEN
          EXIT(Dimension.Code);
    END;

    PROCEDURE GetFieldFromDim@1160040002(Dim@1160040000 : Code[20]) : Code[20];
    VAR
      Dimension@1160040002 : Record 348;
      FieldType@1160040003 : Record 6086345;
      TableFilterField@1160040001 : Record 6086348;
    BEGIN
      IF NOT Dimension.GET(Dim) THEN
        EXIT(Dim);

      TableFilterField.SETRANGE("Table No.",DATABASE::"Dimension Value");
      TableFilterField.SETRANGE("Field No.",1);
      TableFilterField.SETRANGE("Value (Text)",Dim);
      IF TableFilterField.FINDFIRST THEN BEGIN
        FieldType.SETRANGE("Source Table Filter GUID",TableFilterField."Table Filter GUID");
        IF FieldType.FINDFIRST THEN
          EXIT(FieldType.Code);
      END;

      EXIT(Dim);
    END;

    PROCEDURE LookupTable@1160040008(VAR Text@1160040000 : Text[30]) : Boolean;
    VAR
      AllObjWithCap@1160040001 : Record 2000000058;
      ObjId@1160040002 : Integer;
    BEGIN
      IF EVALUATE(ObjId,Text) THEN;
      AllObjWithCap.SETRANGE("Object Type",AllObjWithCap."Object Type"::Table);

      IF AllObjWithCap.GET(AllObjWithCap."Object Type"::Table,ObjId) THEN;
      IF PAGE.RUNMODAL(PAGE::"Objects 2",AllObjWithCap) = ACTION::LookupOK THEN BEGIN
        Text := FORMAT(AllObjWithCap."Object ID");
        EXIT(TRUE);
      END;
    END;

    PROCEDURE LookupField@1160040005(VAR Text@1160040004 : Text[1024];TableNo@1160040002 : Integer;xFieldNo@1160040003 : Integer;OnlyKeys@1160040000 : Boolean) : Boolean;
    VAR
      RecIDMgt@1160040001 : Codeunit 6085604;
    BEGIN
      IF NOT EVALUATE(xFieldNo,Text) THEN
        Text := FORMAT(xFieldNo);

      EXIT(RecIDMgt.LookupField(Text,TableNo,OnlyKeys));
    END;

    PROCEDURE LookupOptionString@1160040020(TableID@1160040008 : Integer;FldNo@1160040009 : Integer;VAR NewValue@1160040004 : Text[250]) : Boolean;
    VAR
      LookupTableTemp@1160040002 : TEMPORARY Record 6086369;
      RecRef@1160040000 : RecordRef;
      SourceNoFieldRef@1160040001 : FieldRef;
    BEGIN
      RecRef.OPEN(TableID);
      SourceNoFieldRef := RecRef.FIELD(FldNo);
      ParseOptionString(SourceNoFieldRef.OPTIONCAPTION,LookupTableTemp);

      LookupTableTemp.SETRANGE(Value,NewValue);
      IF LookupTableTemp.FINDFIRST THEN;
      LookupTableTemp.SETRANGE(Value);
      IF PAGE.RUNMODAL(0,LookupTableTemp) = ACTION::LookupOK THEN BEGIN
        NewValue := LookupTableTemp.Value;
        EXIT(TRUE);
      END;
    END;

    LOCAL PROCEDURE ParseOptionString@1160040016(TextOptString@1160040001 : Text[250];VAR LookupTableTemp@1160040002 : TEMPORARY Record 6086369);
    VAR
      Pos@1160040000 : Integer;
    BEGIN
      REPEAT
        Pos := STRPOS(TextOptString,',');

        LookupTableTemp."Entry No." += 1;
        IF Pos <> 0 THEN
          LookupTableTemp.Value := COPYSTR(TextOptString,1,Pos - 1)
        ELSE
          LookupTableTemp.Value := COPYSTR(TextOptString,1);
        LookupTableTemp.INSERT;

        TextOptString := COPYSTR(TextOptString,Pos + 1);
      UNTIL Pos = 0;
    END;

    PROCEDURE LookupBoolean@1160040023(TableID@1160040008 : Integer;FldNo@1160040009 : Integer;VAR NewValue@1160040004 : Text[250]) : Boolean;
    VAR
      LookupTableTemp@1160040002 : TEMPORARY Record 6086369;
      RecRef@1160040000 : RecordRef;
      SourceNoFieldRef@1160040001 : FieldRef;
      Bool@1160040003 : Boolean;
    BEGIN
      RecRef.OPEN(TableID);
      SourceNoFieldRef := RecRef.FIELD(FldNo);

      ParseOptionString(FORMAT(Bool) + ',' + FORMAT(NOT Bool),LookupTableTemp);

      LookupTableTemp.SETRANGE(Value,NewValue);
      IF LookupTableTemp.FINDFIRST THEN;
      LookupTableTemp.SETRANGE(Value);
      IF PAGE.RUNMODAL(0,LookupTableTemp) = ACTION::LookupOK THEN BEGIN
        NewValue := LookupTableTemp.Value;
        EXIT(TRUE);
      END;
    END;

    LOCAL PROCEDURE SetFixedFiltersOnRecRef@1160040004(VAR RecRef@1160040000 : RecordRef;SourceGUID@1160040001 : GUID) : Boolean;
    VAR
      TableFilterField@1160040002 : Record 6086348;
      FieldRef@1160040003 : FieldRef;
      Values@1160040004 : Text[100];
      OldFilterGroup@161024012 : Integer;
    BEGIN
      OldFilterGroup := RecRef.FILTERGROUP;

      RecRef.FILTERGROUP(2);
      TableFilterField.SETCURRENTKEY("Table Filter GUID","Filter Type");
      TableFilterField.SETRANGE("Table Filter GUID",SourceGUID);
      TableFilterField.SETRANGE("Filter Type",TableFilterField."Filter Type"::"Fixed Filter");
      IF TableFilterField.FINDSET THEN
        REPEAT
          FieldRef := RecRef.FIELD(TableFilterField."Field No.");
          TableFilterField.GetValues(Values,TableFilterField."Filter Type");
          FieldRef.SETFILTER(Values);
        UNTIL TableFilterField.NEXT = 0;

      RecRef.FILTERGROUP(OldFilterGroup);

      EXIT(RecRef.FINDSET);
    END;

    LOCAL PROCEDURE GetParentPKFieldNo@1160040038(SourceGUID@1160040000 : GUID) : Integer;
    VAR
      TableFilterField@1160040001 : Record 6086348;
    BEGIN
      TableFilterField.SETCURRENTKEY("Table Filter GUID","Filter Type");
      TableFilterField.SETRANGE("Table Filter GUID",SourceGUID);
      TableFilterField.SETRANGE("Filter Type",TableFilterField."Filter Type"::"Field Type");
      IF TableFilterField.FINDSET THEN
        EXIT(TableFilterField."Field No.");
    END;

    PROCEDURE GetParentFieldTypeCode@1160040007() FieldTypeCode : Code[20];
    VAR
      TableFilterField@1160040002 : Record 6086348;
    BEGIN
      TableFilterField.SETRANGE("Table Filter GUID","Source Table Filter GUID");
      TableFilterField.SETRANGE("Filter Type",TableFilterField."Filter Type"::"Field Type");
      IF TableFilterField.FINDSET THEN
        EXIT(TableFilterField."Field Type Code");
    END;

    PROCEDURE IsSystemField@1160040009() : Boolean;
    BEGIN
      EXIT(
        (GetSystemFieldExpTableNo(Code) <> -1) OR
        (GetSystemFieldMilTableNo(Code) <> -1) OR
        (GetSystemFieldSettlemenTableNo(Code) <> -1));
    END;

    LOCAL PROCEDURE CodeIsSystemField@1160040031(FldCode@1160040000 : Code[20]) : Boolean;
    BEGIN
      EXIT(
        (GetSystemFieldExpTableNo(FldCode) <> -1) OR
        (GetSystemFieldMilTableNo(FldCode) <> -1) OR
        (GetSystemFieldSettlemenTableNo(FldCode) <> -1));
    END;

    PROCEDURE IsRequiredSystemField@1160040011(Type@1160040000 : Integer) : Boolean;
    VAR
      ConfiguredField@1160040001 : Record 6086346;
    BEGIN
      CASE Type OF
        ConfiguredField.Type::Expense:
          EXIT(GetReqSystemFieldExpTableNo(Code) <> -1);
        ConfiguredField.Type::Mileage:
          EXIT(GetReqSystemFieldMilTableNo(Code) <> -1);
        ConfiguredField.Type::Settlement:
          EXIT(GetReqSysFieldSettlemenTableNo(Code) <> -1);
      END;
    END;

    PROCEDURE GetSystemFieldExpTableNo@1160040006(FldCode@1160040000 : Code[20]) : Integer;
    VAR
      Expense@1160040001 : Record 6086320;
    BEGIN
      CASE FldCode OF
        'DESCRIPTION':
          EXIT(Expense.FIELDNO(Description));
        'DESCRIPTION 2':
          EXIT(Expense.FIELDNO("Description 2"));
        'AMOUNT':
          EXIT(Expense.FIELDNO(Amount));
        'DOCUMENT DATE':
          EXIT(Expense.FIELDNO("Document Date"));
        'COUNTRY/REGION':
          EXIT(Expense.FIELDNO("Country/Region Code"));
        'CURRENCY':
          EXIT(Expense.FIELDNO("Currency Code"));
        'EXPENSE TYPE':
          EXIT(Expense.FIELDNO("Expense Type"));
        'EMPLOYEE NO.':
          EXIT(Expense.FIELDNO("Continia User ID"));
        'JOBNO':
          EXIT(Expense.FIELDNO("Job No."));
        'TASK':
          EXIT(Expense.FIELDNO("Job Task No."));
        'APPROVAL STATUS':
          EXIT(Expense.FIELDNO(Status));
        'BILLABLE':
          EXIT(Expense.FIELDNO(Billable));
        'CASH/PRIVATE CARD':
          EXIT(Expense.FIELDNO("Cash/Private Card"));
        'ATTENDEES':
          EXIT(Expense.FIELDNO("No. of Attendees"));
        'ADMINCOMMENT':
          EXIT(Expense.FIELDNO("Admin Comment"));
        'EMPLOYEE NAME':
          EXIT(Expense.FIELDNO("Continia User Name"))
        ELSE
          EXIT(-1);
      END;
    END;

    PROCEDURE GetSystemFieldMilTableNo@1160040018(FldCode@1160040000 : Code[20]) : Integer;
    VAR
      Mileage@1160040001 : Record 6086338;
    BEGIN
      WITH Mileage DO
        CASE FldCode OF
          'DESCRIPTION':
            EXIT(FIELDNO(Description));
          'AMOUNT':
            EXIT(FIELDNO("Amount (LCY)"));
          'DOCUMENT DATE':
            EXIT(FIELDNO("Registration Date"));
          'EMPLOYEE NO.':
            EXIT(FIELDNO("Continia User ID"));
          'JOBNO':
            EXIT(FIELDNO("Job No."));
          'TASK':
            EXIT(FIELDNO("Job Task No."));
          'FROM' :
            EXIT(FIELDNO("From Address"));
          'TO' :
            EXIT(FIELDNO("To Address"));
          'APPROVAL STATUS':
            EXIT(FIELDNO(Status));
          'DISTANCE':
            EXIT(FIELDNO("Total Distance"));
          'CALCULATED DISTANCE':
            EXIT(FIELDNO("Calculated Distance"));
          'VEHICLE':
            EXIT(FIELDNO("Vehicle Code"));
          'FROM HOME':
            EXIT(FIELDNO("From Home"));
          'TO HOME':
            EXIT(FIELDNO("To Home"));
          'TRAVEL TIME':
            EXIT(FIELDNO("Travel Time"));
          'BILLABLE':
            EXIT(FIELDNO(Billable));
          'VEHICLE REG. NO.':
            EXIT(FIELDNO("Vehicle Registration No."));
          'ADMINCOMMENT':
            EXIT(FIELDNO("Admin Comment"));
          'EMPLOYEE NAME':
            EXIT(FIELDNO("Continia User Name"));
          ELSE
            EXIT(-1);
        END;
    END;

    PROCEDURE GetSystemFieldSettlemenTableNo@1160040026(FldCode@1160040000 : Code[20]) : Integer;
    VAR
      ExpHeader@1160040001 : Record 6086339;
    BEGIN
      CASE FldCode OF
        'DESCRIPTION':
          EXIT(ExpHeader.FIELDNO(Description));
        'EMPLOYEE NO.':
          EXIT(ExpHeader.FIELDNO("Continia User ID"));
        'JOBNO':
          EXIT(ExpHeader.FIELDNO("Job No."));
        'TASK':
          EXIT(ExpHeader.FIELDNO("Job Task No."));
        'APPROVAL STATUS':
          EXIT(ExpHeader.FIELDNO(Status));
        'BILLABLE':
          EXIT(ExpHeader.FIELDNO(Billable));
        'ADMINCOMMENT':
          EXIT(ExpHeader.FIELDNO("Admin Comment"));
        'EMPLOYEE NAME':
          EXIT(ExpHeader.FIELDNO("Continia User Name"));
        ELSE
          EXIT(-1);
      END;
    END;

    LOCAL PROCEDURE GetReqSystemFieldExpTableNo@1160040010(FldCode@1160040002 : Code[20]) : Integer;
    VAR
      Expense@1160040001 : Record 6086320;
    BEGIN
      CASE FldCode OF
        'DESCRIPTION':
          EXIT(Expense.FIELDNO(Description));
        'AMOUNT':
          EXIT(Expense.FIELDNO(Amount));
        'DOCUMENT DATE':
          EXIT(Expense.FIELDNO("Document Date"));
        'CURRENCY':
          EXIT(Expense.FIELDNO("Currency Code"));
        'EXPENSE TYPE':
          EXIT(Expense.FIELDNO("Expense Type"));
        ELSE
          EXIT(-1);
      END;
    END;

    LOCAL PROCEDURE GetReqSystemFieldMilTableNo@1160040014(FldCode@1160040002 : Code[20]) : Integer;
    VAR
      EmSetup@1160040000 : Record 6086300;
      Mileage@1160040001 : Record 6086338;
    BEGIN
      EmSetup.GET;
      IF NOT EmSetup."Enable Mileage" THEN
        EXIT(-1);

      IF EmSetup."Enable 60 day rule" THEN
        WITH Mileage DO
          CASE FldCode OF
            'FROM HOME':
              EXIT(FIELDNO("From Home"));
            'TO HOME':
              EXIT(FIELDNO("To Home"));
          END;

      WITH Mileage DO
        CASE FldCode OF
          'DESCRIPTION':
            EXIT(FIELDNO(Description));
          'AMOUNT':
            EXIT(FIELDNO("Amount (LCY)"));
          'DOCUMENT DATE':
            EXIT(FIELDNO("Registration Date"));
          'FROM' :
            EXIT(FIELDNO("From Address"));
          'TO' :
            EXIT(FIELDNO("To Address"));
          'DISTANCE' :
            EXIT(FIELDNO("Total Distance"));
          ELSE
            EXIT(-1);
        END;
    END;

    LOCAL PROCEDURE GetReqSysFieldSettlemenTableNo@1160040027(FldCode@1160040000 : Code[20]) : Integer;
    VAR
      ExpHeader@1160040001 : Record 6086339;
    BEGIN
      WITH ExpHeader DO
        CASE FldCode OF
          'DESCRIPTION':
            EXIT(FIELDNO(Description));
          ELSE
            EXIT(-1);
        END;
    END;

    LOCAL PROCEDURE CorrectFormat@1160040019(VAR FormatText@1160040000 : Text[10]) : Boolean;
    VAR
      IntVar@1160040002 : Integer;
      Pos@1160040001 : Integer;
    BEGIN
      Pos := STRPOS(FormatText,':');
      IF Pos < 1 THEN
        EXIT(FALSE);

      IF NOT EVALUATE(IntVar,COPYSTR(FormatText,1,Pos - 1)) THEN
        EXIT(FALSE);
      IF (IntVar < 0) OR (IntVar > 10) THEN
        EXIT(FALSE);

      IF NOT EVALUATE(IntVar,COPYSTR(FormatText,Pos + 1)) THEN
        EXIT(FALSE);
      IF (IntVar < 0) OR (IntVar > 10) THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    PROCEDURE FieldIsOptionstring@1160040021(TableID@1160040002 : Integer;FldNo@1160040001 : Integer) : Boolean;
    VAR
      Field@1160040000 : Record 2000000041;
    BEGIN
      IF NOT Field.GET(TableID,FldNo) THEN
        EXIT(FALSE);

      IF Field.Type = Field.Type::Option THEN
        EXIT(TRUE);
    END;

    PROCEDURE FieldIsBoolean@1160040022(TableID@1160040002 : Integer;FldNo@1160040001 : Integer) : Boolean;
    VAR
      Field@1160040000 : Record 2000000041;
    BEGIN
      IF NOT Field.GET(TableID,FldNo) THEN
        EXIT(FALSE);

      IF Field.Type = Field.Type::Boolean THEN
        EXIT(TRUE);
    END;

    PROCEDURE GetFieldTypeFromDefaultDim@1160040024(TableID@1160040000 : Integer) : Code[20];
    VAR
      FieldType@1160040001 : Record 6086345;
      RecIDMgt@1160040002 : Codeunit 6085604;
    BEGIN
      FieldType.SETRANGE("Source Table No.",TableID);
      FieldType.SETRANGE("Source Field No.",RecIDMgt.GetFirstKeyField(TableID));
      IF FieldType.FINDFIRST THEN
        EXIT(FieldType.Code);
    END;

    PROCEDURE FieldDependencyConfigured@1160040025(FieldTypeCode@1160040000 : Code[20];RefFieldTypeCode@1160040001 : Code[20]) : Boolean;
    VAR
      ConfiguredField@1160040002 : Record 6086346;
      RefConfField@1160040003 : Record 6086346;
    BEGIN
      IF (FieldTypeCode = '') OR (RefFieldTypeCode = '') THEN
        EXIT(FALSE);

      ConfiguredField.RESET;
      ConfiguredField.SETRANGE("Field Code",FieldTypeCode);
      IF ConfiguredField.FINDFIRST THEN
        REPEAT
          RefConfField.RESET;
          RefConfField.SETRANGE(Type,ConfiguredField.Type);
          RefConfField.SETRANGE("Field Code",RefFieldTypeCode);
          IF RefConfField.FINDFIRST THEN
            EXIT(TRUE);
        UNTIL ConfiguredField.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

