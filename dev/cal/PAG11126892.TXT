OBJECT Page 11126892 Resource Request Work Order SF
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Ressourcen (ArbeitsauftrÑge);
               ENU=Resources (Work Orders);
               NLD=Resources (werkorders)];
    SourceTable=Table11229279;
    SourceTableView=SORTING(Source No.,Source Company,No.)
                    WHERE(Source Type=CONST(Request));
    PageType=ListPart;
    RefreshOnActivate=Yes;
    OnOpenPage=BEGIN
                 FILTERGROUP(2);
                 SETRANGE("Source Company", COMPANYNAME);
                 FILTERGROUP(0);
                 ResourcePlanningSetup.GET;
               END;

    OnAfterGetRecord=BEGIN
                       ProjectElementEnabled := RequestIsLinkedToProject(DummyProjectNo, DummyCompany);
                       FillDateTimeFields;
                       FillFieldsFromAdditionalInfo;
                     END;

    OnNewRecord=VAR
                  ResourceRequest@1100529002 : Record 11124901;
                  SourceNo@1100529000 : Code[20];
                BEGIN
                  FILTERGROUP(4);
                  SourceNo := GETFILTER("Source No.");
                  FILTERGROUP(0);
                  IF (SourceNo <> '') AND (ResourceRequest.GET(SourceNo)) THEN
                    InitRecordOnNew("Source Type"::Request, SourceNo, "Resource Type"::Employee, ResourceRequest."Project Element");
                  FillDateTimeFields;
                END;

    OnInsertRecord=VAR
                     WorkOrderPlanningSetup@1100526202 : Record 11229277;
                     NoSeriesMgt@1100526201 : Codeunit 396;
                     ResourceRequest@1100528502 : Record 11124901;
                     SourceNo@1100528501 : Code[20];
                   BEGIN
                     FILTERGROUP(4);
                     SourceNo := GETFILTER("Source No.");
                     FILTERGROUP(0);
                     IF (SourceNo <> '') AND (ResourceRequest.GET(SourceNo)) THEN
                       InitRecordOnNew("Source Type"::Request, SourceNo, "Resource Type"::Employee, ResourceRequest."Project Element");
                     IF "No." = '' THEN BEGIN
                       WorkOrderPlanningSetup.GET;
                       WorkOrderPlanningSetup.TESTFIELD("Work Order Nos.");
                       NoSeriesMgt.InitSeries(WorkOrderPlanningSetup."Work Order Nos.", xRec."No. Series",0D,"No.","No. Series");
                     END;
                     TESTFIELD("Source No.");
                     CheckWorkOrder;
                   END;

    OnModifyRecord=BEGIN
                     CheckWorkOrder;
                   END;

    OnAfterGetCurrRecord=BEGIN
                           ProjectElementEnabled := RequestIsLinkedToProject(DummyProjectNo, DummyCompany);
                           FillFieldsFromAdditionalInfo;
                         END;

  }
  CONTROLS
  {
    { 1100529000;0;Container;
                ContainerType=ContentArea }

    { 1100529001;1;Group  ;
                GroupType=Repeater }

    { 1100529002;2;Field  ;
                SourceExpr="No.";
                OnAssistEdit=BEGIN
                               IF AssistEdit(xRec) THEN
                                 CurrPage.UPDATE;
                             END;
                              }

    { 1100529004;2;Field  ;
                SourceExpr=Description }

    { 1100529017;2;Field  ;
                SourceExpr="Resource Type";
                OnValidate=BEGIN
                             CheckUpdatePage(FIELDNO("Resource Type"));
                           END;
                            }

    { 1100529003;2;Field  ;
                SourceExpr="Resource Group No." }

    { 1100529007;2;Field  ;
                SourceExpr="Resource Group Name";
                Visible=FALSE }

    { 1100529014;2;Field  ;
                SourceExpr="Resource No.";
                OnValidate=BEGIN
                             CheckUpdatePage(FIELDNO("Resource No."));
                           END;

                OnLookup=VAR
                           ResourceNo@1100529001 : Code[20];
                         BEGIN
                           ResourceNo := "Resource No.";
                           LookupResourceOfResourceGroup(TRUE);
                           IF "Resource No." <> ResourceNo THEN
                             CheckUpdatePage(FIELDNO("Resource No."));
                         END;
                          }

    { 1100529015;2;Field  ;
                SourceExpr="Resource Name" }

    { 1100529005;2;Field  ;
                SourceExpr="Starting Date/Time";
                Visible=FALSE;
                OnValidate=BEGIN
                             ValidateDateTimeFields(ValidationField::StartingDateTime);
                             CheckUpdatePage(FIELDNO("Starting Date/Time"));
                           END;
                            }

    { 1100528304;2;Field  ;
                CaptionML=[DEU=Startdatum;
                           ENU=Starting Date;
                           NLD=Startdatum;
                           NOR=Startdato;
                           SVE=Startdatum];
                SourceExpr=StartingDate;
                Importance=Additional;
                OnValidate=BEGIN
                             ValidateDateTimeFields(ValidationField::StartingDate);
                             CheckUpdatePage(FIELDNO("Starting Date/Time"));
                           END;
                            }

    { 1100528303;2;Field  ;
                CaptionML=[DEU=Startzeit;
                           ENU=Starting Time;
                           NLD=Starttijd;
                           NOR=Starttid;
                           SVE=Starttid];
                SourceExpr=StartingTime;
                Importance=Additional;
                OnValidate=BEGIN
                             ValidateDateTimeFields(ValidationField::StartingTime);
                             CheckUpdatePage(FIELDNO("Starting Date/Time"));
                           END;
                            }

    { 1100529006;2;Field  ;
                SourceExpr="Ending Date/Time";
                Visible=FALSE;
                OnValidate=BEGIN
                             ValidateDateTimeFields(ValidationField::EndingDateTime);
                             CheckUpdatePage(FIELDNO("Ending Date/Time"));
                           END;
                            }

    { 1100528301;2;Field  ;
                CaptionML=[DEU=Enddatum;
                           ENU=Ending Date;
                           NLD=Einddatum;
                           NOR=Sluttdato;
                           SVE=Slutdatum];
                SourceExpr=EndingDate;
                Importance=Additional;
                OnValidate=BEGIN
                             ValidateDateTimeFields(ValidationField::EndingDate);
                             CheckUpdatePage(FIELDNO("Ending Date/Time"));
                           END;
                            }

    { 1100528300;2;Field  ;
                CaptionML=[DEU=Endzeit;
                           ENU=Ending Time;
                           NLD=Eindtijd;
                           NOR=Sluttid;
                           SVE=Sluttid];
                SourceExpr=EndingTime;
                Importance=Additional;
                OnValidate=BEGIN
                             ValidateDateTimeFields(ValidationField::EndingTime);
                             CheckUpdatePage(FIELDNO("Ending Date/Time"));
                           END;
                            }

    { 1100528302;2;Field  ;
                CaptionML=[DEU=Pause berechnen;
                           ENU=Calculate Pause;
                           NLD=Pauze verrekenen];
                SourceExpr=PauseAdditionalInfo;
                Editable=FieldsFromAdditionalInfoEditable;
                OnValidate=BEGIN
                             UpdateFieldInAdditionalInfo(AdditionalInfoRequest.FIELDNO("Calulate Pause"));
                           END;
                            }

    { 1100528501;2;Field  ;
                SourceExpr="Plan during Clock Time" }

    { 1100529170;2;Field  ;
                SourceExpr="Plan in Weekend" }

    { 1100528502;2;Field  ;
                SourceExpr="Firm Planned" }

    { 1100529011;2;Field  ;
                CaptionML=[DEU=Geplante Stunden;
                           ENU=Planned Hours;
                           NLD=Geplande uren;
                           NOR="Timer ";
                           SVE=Timmar];
                DecimalPlaces=2:2;
                SourceExpr=NumberOfPlannedHours }

    { 1100527202;2;Field  ;
                CaptionML=[DEU=Verwendete Stunden;
                           ENU=Actual Hours;
                           NLD=Werkelijke uren;
                           NOR=Reelle timer;
                           SVE=Verkliga timmar];
                DecimalPlaces=2:2;
                SourceExpr=ActualHoursAdditionalInfo;
                Editable=FieldsFromAdditionalInfoEditable;
                OnValidate=BEGIN
                             UpdateFieldInAdditionalInfo(AdditionalInfoRequest.FIELDNO("Actual Hours"));
                           END;
                            }

    { 1100528500;2;Field  ;
                SourceExpr="Duration Time";
                Editable=false }

    { 1100529010;2;Field  ;
                CaptionML=[DEU=Bedient Werkzeug (AA);
                           ENU=Operates Plant (WO);
                           NLD=Bedient materieel (WO)];
                SourceExpr="Operates Plant (WO)";
                OnValidate=BEGIN
                             CheckUpdatePage(FIELDNO("Operates Plant (WO)"));
                           END;

                OnLookup=VAR
                           OperatesPlantWO@1100529000 : Code[20];
                         BEGIN
                           OperatesPlantWO := "Operates Plant (WO)";
                           LookupOperatesPlantWO;
                           IF "Operates Plant (WO)" <> OperatesPlantWO THEN
                             CheckUpdatePage(FIELDNO("Operates Plant (WO)"));
                         END;
                          }

    { 1100529008;2;Field  ;
                SourceExpr="Related Work Order No." }

    { 1100529100;2;Field  ;
                SourceExpr="Schedule Relation" }

    { 1100527204;2;Field  ;
                SourceExpr="Project Element";
                Enabled=ProjectElementEnabled }

    { 1100529009;2;Field  ;
                SourceExpr=Finished }

    { 1100528100;2;Field  ;
                SourceExpr="Export to FSA" }

    { 1100527200;2;Field  ;
                SourceExpr="Actual Field Service Status" }

    { 1100527201;2;Field  ;
                SourceExpr="Actual Field Serv. Status Desc" }

    { 1100527205;2;Field  ;
                SourceExpr="Signature Mandatory" }

    { 1100527203;2;Field  ;
                CaptionML=[DEU=Unterschrift;
                           ENU=Signature;
                           NLD=Handtekening;
                           NOR=Signatur;
                           SVE=Signatur];
                SourceExpr=HasSignature;
                Enabled=FALSE;
                Editable=FALSE }

  }
  CODE
  {
    VAR
      ResourcePlanningSetup@1100528307 : Record 11124900;
      AdditionalInfoRequest@1100528306 : Record 11124915;
      StartingDate@1100528303 : Date;
      StartingTime@1100528302 : Time;
      EndingDate@1100528301 : Date;
      EndingTime@1100528300 : Time;
      ValidationField@1100528304 : 'FirstPossibleStartingDateTime,FirstPossibleStartingDate,FirstPossibleStartingTime,LastPossibleEndingDateTime,LastPossibleEndingDate,LastPossibleEndingTime,StartingDateTime,StartingDate,StartingTime,EndingDateTime,EndingDate,EndingTime';
      FieldsFromAdditionalInfoEditable@1100527201 : Boolean INDATASET;
      ProjectElementEnabled@1100527202 : Boolean INDATASET;
      PauseAdditionalInfo@1100528305 : Boolean;
      PauseAdditionalInfoPlant@1100525002 : Boolean;
      ActualHoursAdditionalInfo@1100527200 : Decimal;
      DummyProjectNo@1100527203 : Code[20];
      DummyCompany@1100529600 : Text[30];
      Notified1@1100529100 : Text;
      Text001@1100529101 : TextConst 'DEU=Ressource %1, %2 ist doppelt eingeplant.;ENU=Resource %1, %2 is double-planned.;NLD=Resource %1, %2 is dubbel gepland.';
      Notified2@1100525000 : Text;
      Text002@1100525001 : TextConst 'DEU=Ressource %1, %2 ist nicht verfÅgbar.;ENU=Resource %1, %2 is not available.;NLD=Resource %1, %2 is afwezig.';

    LOCAL PROCEDURE FillDateTimeFields@1100528601();
    VAR
      FirstPossibleStartingDate@1100528400 : Date;
      FirstPossibleStartingTime@1100528401 : Time;
      LastPossibleEndingDate@1100528402 : Date;
      LastPossibleEndingTime@1100528403 : Time;
    BEGIN
      FillSeparateDateAndTimeFields(
        FirstPossibleStartingDate, FirstPossibleStartingTime, LastPossibleEndingDate,
        LastPossibleEndingTime, StartingDate, StartingTime, EndingDate, EndingTime);
    END;

    LOCAL PROCEDURE ValidateDateTimeFields@1100528600(IValidationField@1100528600 : Option);
    VAR
      ResourceWOP@1100528500 : Record 11229278;
      FirstPossibleStartingDate@1100528403 : Date;
      FirstPossibleStartingTime@1100528402 : Time;
      LastPossibleEndingDate@1100528401 : Date;
      LastPossibleEndingTime@1100528400 : Time;
      EmptyDateTime@1100528501 : DateTime;
      CalcDuration@1100528502 : Duration;
    BEGIN
      ValidateSeparateDateAndTimeFields(
        IValidationField, FirstPossibleStartingDate, FirstPossibleStartingTime, LastPossibleEndingDate,
        LastPossibleEndingTime, StartingDate, StartingTime, EndingDate, EndingTime);

      IF ("Ending Date/Time" <> EmptyDateTime) AND ("Starting Date/Time" <> EmptyDateTime) THEN BEGIN
        IF "Plan during Clock Time" THEN BEGIN
          CalcDuration := "Ending Date/Time" - "Starting Date/Time";
          "Duration Time" := ROUND(CalcDuration/ 3600000, 0.01);
        END ELSE BEGIN
          IF NOT ResourceWOP.GET("Resource No.") THEN
            ResourceWOP.INIT;
          ResourceWOP.CalculateDurationTime("Starting Date/Time", "Ending Date/Time", "Duration Time", FALSE);
        END;
      END;
    END;

    LOCAL PROCEDURE FillFieldsFromAdditionalInfo@1100528300();
    BEGIN
      ActualHoursAdditionalInfo := 0;
      PauseAdditionalInfo := FALSE;
      FieldsFromAdditionalInfoEditable := FALSE;
      IF "No." <> '' THEN BEGIN
        IF AdditionalInfoRequest.GET("No.") THEN BEGIN
          PauseAdditionalInfo := AdditionalInfoRequest."Calulate Pause";
          ActualHoursAdditionalInfo := AdditionalInfoRequest."Actual Hours";
          FieldsFromAdditionalInfoEditable := TRUE;
        END;
      END ELSE BEGIN
        IF "Resource Type" = "Resource Type"::Plant THEN
          PauseAdditionalInfo := ResourcePlanningSetup."Calc. Pause Plant (Default)"
        ELSE
          PauseAdditionalInfo := ResourcePlanningSetup."Calulate Pause (Default)";
      END;
    END;

    LOCAL PROCEDURE UpdateFieldInAdditionalInfo@1100528304(FieldNumber@1100527200 : Integer);
    BEGIN
      AdditionalInfoRequest.GET("No.");
      CASE FieldNumber OF
        AdditionalInfoRequest.FIELDNO("Calulate Pause"):
          AdditionalInfoRequest.VALIDATE("Calulate Pause", PauseAdditionalInfo);
        AdditionalInfoRequest.FIELDNO("Actual Hours"):
          AdditionalInfoRequest.VALIDATE("Actual Hours", ActualHoursAdditionalInfo);
      END;
      AdditionalInfoRequest.MODIFY;
    END;

    LOCAL PROCEDURE NumberOfPlannedHours@1100529000() : Decimal;
    BEGIN
      IF "No." <> '' THEN BEGIN
        IF AdditionalInfoRequest.GET("No.") THEN
          EXIT(AdditionalInfoRequest."Planned Hours");
      END;
      EXIT(0);
    END;

    LOCAL PROCEDURE CheckUpdatePage@1100529001(CurrFieldNo@1100529000 : Integer);
    BEGIN
      IF ("Starting Date/Time" = 0DT) OR ("Ending Date/Time" = 0DT) THEN
        EXIT;
      IF "Ending Date/Time" <= "Starting Date/Time" THEN
        EXIT;
      CASE CurrFieldNo OF
        FIELDNO("Resource Type"),
        FIELDNO("Starting Date/Time"),
        FIELDNO("Ending Date/Time"):
          CurrPage.UPDATE;
        FIELDNO("Resource No."),
        FIELDNO("Operates Plant (WO)"):
          IF "Resource Type" = "Resource Type"::Employee THEN
            CurrPage.UPDATE;
      END;
    END;

    LOCAL PROCEDURE GetOverlap@1100529100();
    BEGIN
      IF NOT CheckNotified("No.", Notified1) THEN BEGIN
        IF GetOverlapWorkOrderNo <> '' THEN BEGIN
          MESSAGE(Text001, "Resource No.", "Resource Name");
          AddNotified("No.", Notified1);
        END;
      END;
    END;

    LOCAL PROCEDURE CheckAbsence@1100525000();
    BEGIN
      IF NOT CheckNotified("No.", Notified2) THEN BEGIN
        IF CheckEmployeeAbsence THEN BEGIN
          MESSAGE(Text002, "Resource No.", "Resource Name");
          AddNotified("No.", Notified2);
        END;
      END;
    END;

    LOCAL PROCEDURE CheckWorkOrder@1100529102();
    BEGIN
      IF NOT CheckNotified("No.", Notified1) THEN
        IF GetOverlapWorkOrderNo <> '' THEN BEGIN
          MESSAGE(Text001, "Resource No.", "Resource Name");
          AddNotified("No.", Notified1);
        END;
      IF NOT CheckNotified("No.", Notified2) THEN
        IF CheckEmployeeAbsence THEN BEGIN
          MESSAGE(Text002, "Resource No.", "Resource Name");
          AddNotified("No.", Notified2);
        END;
    END;

    LOCAL PROCEDURE AddNotified@1100529105(RecordToAdd@1100529100 : Text;VAR NotifiedString@1100529101 : Text);
    BEGIN
      IF STRPOS(NotifiedString, RecordToAdd) = 0 THEN
        NotifiedString += ' ' + RecordToAdd;
    END;

    LOCAL PROCEDURE CheckNotified@1100529106(RecordToCheck@1100529101 : Text;VAR NotifiedString@1100529100 : Text) : Boolean;
    BEGIN
      IF STRPOS(NotifiedString, RecordToCheck) > 0 THEN
        EXIT(TRUE)
      ELSE
        EXIT(FALSE);
    END;

    BEGIN
    END.
  }
}

