OBJECT Table 6086339 Expense Header
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.3.00.03,4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 311=r,
                TableData 6086002=r,
                TableData 6086300=r,
                TableData 6086341=r;
    DataCaptionFields=No.,Continia User ID,Description;
    OnInsert=VAR
               SalesSetup@1160040003 : Record 311;
               EMSetup@1160040000 : Record 6086300;
               NoSeriesMgt@1160040001 : Codeunit 396;
             BEGIN
               EMSetup.GET;
               EMSetup.TESTFIELD("Enable Settlement",TRUE);

               IF "No." = '' THEN BEGIN
                 EMSetup.TESTFIELD("Settlement Nos.");
                 NoSeriesMgt.InitSeries(EMSetup."Settlement Nos.",xRec."No. Series",WORKDATE,"No.","No. Series");
               END;

               AddDefaultDim(0);

               "Posting Date" := WORKDATE;
               "Date Created" := TODAY;
               "Created by User ID" := USERID;

               IF SalesSetup.GET THEN
                 IF SalesSetup."Default Posting Date" = SalesSetup."Default Posting Date"::"No Date" THEN
                   "Posting Date" := 0D;

               "Posting Description" := FORMAT("Document Type") + ' ' + "No.";
               ExpHeaderValidate.RUN(Rec);
             END;

    OnModify=BEGIN
               TESTFIELD(Posted,FALSE);
               TestStatusOpenOrPendingExpUser;
               CheckInboxAndThrowError;

               SendToExpenseUser;
             END;

    OnDelete=VAR
               Expense@1160040002 : Record 6086320;
               Expense2@1160040006 : Record 6086320;
               Mileage@1160040001 : Record 6086338;
               EMDimension@1160040003 : Record 6086360;
               EMComment@1160040004 : Record 6086361;
               EMReminder@1160040008 : Record 6086364;
               EMCommentLine@1160040007 : Record 6086371;
               EMOnlineMgt@1160040000 : Codeunit 6086305;
               EMApprovalsBridge@1160040005 : Codeunit 6086369;
             BEGIN
               TESTFIELD(Posted,FALSE);

               CheckInboxAndThrowError;

               Expense.SETCURRENTKEY("Settlement No.");
               Expense.SETRANGE("Settlement No.","No.");
               Expense.SETRANGE("Matched to Bank Transaction",FALSE);

               Mileage.SETCURRENTKEY("Settlement No.");
               Mileage.SETRANGE("Settlement No.","No.");

               IF NOT HideUI THEN
                 IF NOT (Expense.ISEMPTY AND Mileage.ISEMPTY) THEN
                   IF NOT CONFIRM(DeleteSettlementTxt,FALSE) THEN
                     ERROR('');

               Expense.SETRANGE("Matched to Bank Transaction",TRUE);
               IF Expense.FINDFIRST THEN
                 REPEAT
                   Expense2 := Expense;
                   Expense2.VALIDATE("Settlement No.",'');
                   Expense2.MODIFY(TRUE);
                 UNTIL Expense.NEXT = 0;

               Expense.SETRANGE("Matched to Bank Transaction",FALSE);
               Expense.DELETEALL(TRUE);
               Mileage.DELETEALL(TRUE);

               EMDimension.LOCKTABLE;
               EMDimension.SETCURRENTKEY("Table ID","Document Type","Document No.","Doc. Ref. No.");
               EMDimension.SETRANGE("Table ID",DATABASE::"Expense Header");
               EMDimension.SETRANGE("Document Type",EMDimension."Document Type"::Settlement);
               EMDimension.SETRANGE("Document No.","No.");
               EMDimension.SETRANGE("Doc. Ref. No.",0);
               EMDimension.DELETEALL;

               EMComment.SETCURRENTKEY("Table ID","Document Type","Document No.","Doc. Ref. No.");
               EMComment.SETRANGE("Table ID",DATABASE::"Expense Header");
               EMComment.SETRANGE("Document Type",EMDimension."Document Type"::Settlement);
               EMComment.SETRANGE("Document No.","No.");
               EMComment.SETRANGE("Doc. Ref. No.",0);
               EMComment.DELETEALL(TRUE);

               EMReminder.SETCURRENTKEY("Table ID","Document Type","Document No.","Doc. Ref. No.");
               EMReminder.SETRANGE("Table ID",DATABASE::"Expense Header");
               EMReminder.SETRANGE("Document Type",EMReminder."Document Type"::Settlement);
               EMReminder.SETRANGE("Document No.","No.");
               EMReminder.SETRANGE("Doc. Ref. No.",0);
               EMReminder.DELETEALL(TRUE);

               EMCommentLine.SETRANGE("Table Name",EMCommentLine."Table Name"::"Expense Header");
               EMCommentLine.SETRANGE("No.","No.");
               EMCommentLine.DELETEALL(TRUE);

               EMApprovalsBridge.DeleteApprovalEntries(DATABASE::"Expense Header","No.");

               IF Status = Status::"Pending Expense User" THEN
                 EMOnlineMgt.PhysicalDeleteDocFromCO(DATABASE::"Expense Header","Exp. Header GUID",TRUE);
             END;

    OnRename=BEGIN
               ERROR(CannotRename,TABLECAPTION);
             END;

    CaptionML=[DEU=Ausgabenkopf;
               ENU=Expense Header;
               NLD=Uitgavekop];
    LookupPageID=Page6086384;
    DrillDownPageID=Page6086384;
  }
  FIELDS
  {
    { 1   ;   ;Document Type       ;Option        ;CaptionML=[DEU=Belegart;
                                                              ENU=Document Type;
                                                              NLD=Documenttype;
                                                              SVE=Dokumenttyp];
                                                   OptionCaptionML=[DEU=Budget,Verrechnung;
                                                                    ENU=Budget,Settlement;
                                                                    NLD=Budget,Afboeking];
                                                   OptionString=Budget,Settlement }
    { 2   ;   ;No.                 ;Code20        ;OnValidate=VAR
                                                                EMSetup@1160040000 : Record 6086300;
                                                                NoSeriesMgt@1160040001 : Codeunit 396;
                                                              BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  EMSetup.GET;
                                                                  NoSeriesMgt.TestManual(EMSetup."Settlement Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Nr.;
                                                              ENU=No.;
                                                              NLD=Nr.;
                                                              SVE=Nr] }
    { 3   ;   ;Continia User ID    ;Code50        ;TableRelation="Continia User Setup";
                                                   OnValidate=VAR
                                                                ContiniaUser@1160040000 : Record 6086001;
                                                                UserResp@1160040001 : Record 6086379;
                                                              BEGIN
                                                                CheckLinesExist;

                                                                IF "Continia User ID" <> '' THEN
                                                                  ContiniaUser.GET("Continia User ID")
                                                                ELSE
                                                                  CLEAR(ContiniaUser);

                                                                "Continia User Name" := ContiniaUser.Name;
                                                                AddDefaultDim(CurrFieldNo);

                                                                UserResp.VerifyUser("Continia User ID");
                                                              END;

                                                   CaptionML=[DEU=Continia-Benutzer-ID;
                                                              ENU=Continia User ID;
                                                              NLD=Continia gebruikers-id;
                                                              SVE=Anst„llningsnr] }
    { 4   ;   ;Continia User Name  ;Text50        ;CaptionML=[DEU=Name;
                                                              ENU=Name;
                                                              NLD=Naam;
                                                              SVE=Namn] }
    { 5   ;   ;Description         ;Text50        ;CaptionML=[DEU=Beschreibung;
                                                              ENU=Description;
                                                              NLD=Omschrijving;
                                                              SVE=Beskrivning] }
    { 6   ;   ;Date Created        ;Date          ;CaptionML=[DEU=Erstellt am;
                                                              ENU=Date Created;
                                                              NLD=Datum aangemaakt;
                                                              SVE=Skapat datum];
                                                   Editable=No }
    { 7   ;   ;Description 2       ;Text50        ;CaptionML=[DEU=Beschreibung 2;
                                                              ENU=Description 2;
                                                              NLD=Omschrijving 2;
                                                              SVE=Beskrivning 2] }
    { 8   ;   ;Created by User ID  ;Code50        ;CaptionML=[DEU=Erstellt von Benutzer-ID;
                                                              ENU=Created by User ID;
                                                              NLD=Aangemaak door gebruikers-id;
                                                              SVE=Skapades av anv„ndar-ID];
                                                   Editable=No }
    { 9   ;   ;Global Dimension 1 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=VAR
                                                                EMDimMgt@1160040000 : Codeunit 6086318;
                                                              BEGIN
                                                                EMDimMgt.UpdateEMDimForGlobalDim(DATABASE::"Expense Header","Document Type","No.",0,1,"Global Dimension 1 Code");
                                                                ExpHeaderValidate.RUN(Rec);
                                                              END;

                                                   CaptionML=[DEU=Globaler Dimensionscode 1;
                                                              ENU=Global Dimension 1 Code;
                                                              NLD=Globale dimensiecode 1;
                                                              SVE=Global dimension 1 kod];
                                                   CaptionClass='1,1,1' }
    { 10  ;   ;Global Dimension 2 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=VAR
                                                                EMDimMgt@1160040000 : Codeunit 6086318;
                                                              BEGIN
                                                                EMDimMgt.UpdateEMDimForGlobalDim(DATABASE::"Expense Header","Document Type","No.",0,2,"Global Dimension 2 Code");
                                                                ExpHeaderValidate.RUN(Rec);
                                                              END;

                                                   CaptionML=[DEU=Globaler Dimensionscode 2;
                                                              ENU=Global Dimension 2 Code;
                                                              NLD=Globale dimensiecode 2;
                                                              SVE=Global dimension 2 kod];
                                                   CaptionClass='1,1,2' }
    { 11  ;   ;Job No.             ;Code20        ;TableRelation=Job;
                                                   OnValidate=BEGIN
                                                                AddDefaultDim(CurrFieldNo);
                                                                IF "Job No." = '' THEN
                                                                  VALIDATE("Job Task No.",'');
                                                                VALIDATE(Billable,"Job Task No." <> '');
                                                              END;

                                                   CaptionML=[DEU=Projektnr.;
                                                              ENU=Job No.;
                                                              NLD=Projectnr.;
                                                              SVE=Projektnr] }
    { 12  ;   ;Job Task No.        ;Code20        ;TableRelation="Job Task"."Job Task No." WHERE (Job No.=FIELD(Job No.));
                                                   OnValidate=BEGIN
                                                                VALIDATE(Billable,"Job Task No." <> '');
                                                              END;

                                                   CaptionML=[DEU=Projektaufgabennr.;
                                                              ENU=Job Task No.;
                                                              NLD=Projecttaaknr.;
                                                              SVE=Projektaktivitetsnr] }
    { 13  ;   ;Status              ;Option        ;CaptionML=[DEU=Status;
                                                              ENU=Status;
                                                              NLD=Status;
                                                              SVE=Status];
                                                   OptionCaptionML=[DEU=Offen,Ausgabenbenutzer ausstehend,Genehmigung ausstehend,Freigegeben;
                                                                    ENU=Open,Pending Expense User,Pending Approval,Released;
                                                                    NLD=Open,Afwachten uitgavegebruiker,Afwachting van goedkeuring,Vrijgegeven];
                                                   OptionString=Open,Pending Expense User,Pending Approval,Released;
                                                   Editable=No }
    { 14  ;   ;Posted              ;Boolean       ;OnValidate=BEGIN
                                                                "Posted Date/Time" := CURRENTDATETIME;
                                                                "Posted by User ID" := USERID;
                                                              END;

                                                   CaptionML=[DEU=Gebucht;
                                                              ENU=Posted;
                                                              NLD=Geboekt;
                                                              SVE=Bokf”rd];
                                                   Editable=No }
    { 15  ;   ;Posted Date/Time    ;DateTime      ;CaptionML=[DEU=Buchungsdatum/-uhrzeit;
                                                              ENU=Posted Date Time;
                                                              NLD=Boekingsdatum/tijd];
                                                   Editable=No }
    { 16  ;   ;Posted by User ID   ;Code50        ;CaptionML=[DEU=Gebucht von Benutzer-ID;
                                                              ENU=Posted by User ID;
                                                              NLD=Geboekt door gebruikers-id];
                                                   Editable=No }
    { 17  ;   ;Admin Comment       ;Text250       ;CaptionML=[DEU=Admin Bemerkung;
                                                              ENU=Admin Comment;
                                                              NLD=Admin Opmerking] }
    { 19  ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=[DEU=Nummernserie;
                                                              ENU=No. Series;
                                                              NLD=Nr.-reeksen;
                                                              SVE=Nr-serie];
                                                   Editable=No }
    { 20  ;   ;On Hold             ;Code3         ;CaptionML=[DEU=Abwarten;
                                                              ENU=On Hold;
                                                              NLD=Afwachten;
                                                              SVE=Stoppad] }
    { 21  ;   ;Exp. Header GUID    ;GUID          ;CaptionML=[DEU=Ausgabenkopf GUID;
                                                              ENU=Exp. Header GUID;
                                                              NLD=Uitgavekop GUID] }
    { 22  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("EM Comment Line" WHERE (Table Name=CONST(Expense Header),
                                                                                              No.=FIELD(No.)));
                                                   CaptionML=[DEU=Bemerkung;
                                                              ENU=Comment;
                                                              NLD=Opmerking;
                                                              SVE=Kommentar];
                                                   Editable=No }
    { 23  ;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   CaptionML=[DEU=L„nder-/Regionscode;
                                                              ENU=Country/Region Code;
                                                              NLD=Land-/regiocode;
                                                              SVE=Lands-/regionkod];
                                                   NotBlank=Yes }
    { 24  ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=[DEU=W„hrungscode;
                                                              ENU=Currency Code;
                                                              NLD=Valutacode;
                                                              SVE=Valutakod] }
    { 25  ;   ;Expense Header Completed;Boolean   ;CaptionML=[DEU=Ausgleich vollendet;
                                                              ENU=Settlement Completed;
                                                              NLD=Afrekening voltooid] }
    { 26  ;   ;Continia Online Version No.;Text100;CaptionML=[DEU=Continia-Online Versionsnr.;
                                                              ENU=Continia Online Version No.;
                                                              NLD=Continia Online versienr.] }
    { 30  ;   ;Posting Date        ;Date          ;CaptionML=[DEU=Buchungsdatum;
                                                              ENU=Posting Date;
                                                              NLD=Boekingsdatum;
                                                              SVE=Bokf”ringsdatum] }
    { 33  ;   ;Billable            ;Boolean       ;OnValidate=BEGIN
                                                                IF Billable AND ("Job No." <> '') AND ("Job Task No." <> '') THEN
                                                                  VALIDATE("Job Line Type","Job Line Type"::Contract)
                                                                ELSE
                                                                  VALIDATE("Job Line Type","Job Line Type"::" ");
                                                              END;

                                                   CaptionML=[DEU=Fakturierbar;
                                                              ENU=Billable;
                                                              NLD=Facturabel;
                                                              SVE=Fakturerbart] }
    { 34  ;   ;Job Line Type       ;Option        ;OnValidate=BEGIN
                                                                IF "Job Line Type" = "Job Line Type"::Contract THEN BEGIN
                                                                  TESTFIELD("Job No.");
                                                                  TESTFIELD("Job Task No.");
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Projektzeilenart;
                                                              ENU=Job Line Type;
                                                              NLD=Regelsoort project;
                                                              SVE=Projektradtyp];
                                                   OptionCaptionML=[DEU=" ,Budget,Fakturierbar,Budget und Fakturierbar";
                                                                    ENU=" ,Budget,Billable,Both Budget and Billable";
                                                                    NLD=" ,Budget,Factureerbaar,Budget en factureerbaar";
                                                                    SVE=" ,Budget,Fakturerbart,B†de budget och fakturerbart"];
                                                   OptionString=[ ,Schedule,Contract,Both Schedule and Contract] }
    { 40  ;   ;Posting Description ;Text30        ;CaptionML=[DEU=Buchungsbeschreibung;
                                                              ENU=Posting Description;
                                                              NLD=Boekingsomschrijving;
                                                              SVE=Bokf”ringsbeskrivning] }
    { 100 ;   ;Expense Total (LCY) ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum(Expense."Amount (LCY)" WHERE (Settlement No.=FIELD(No.)));
                                                   CaptionML=[DEU=Ausgaben Total (LCY);
                                                              ENU=Expense Total (LCY);
                                                              NLD=Uitgavetotaal (LV)];
                                                   Editable=No }
    { 101 ;   ;Mileage Total (LCY) ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum(Mileage."Amount (LCY)" WHERE (Settlement No.=FIELD(No.)));
                                                   CaptionML=[DEU=Kilometerleistung-Total (LCY);
                                                              ENU=Mileage Total (LCY);
                                                              NLD=Kilometragetotaal (LV)];
                                                   Editable=No }
    { 113 ;   ;Created Doc. Type   ;Integer       ;CaptionML=[DEU=Erstellte Belegart;
                                                              ENU=Created Doc. Type;
                                                              NLD=Aangemaakt documenttype];
                                                   Editable=No }
    { 114 ;   ;Created Doc. Subtype;Integer       ;CaptionML=[DEU=Erstellte Bel.-Unterart;
                                                              ENU=Created Doc. Subtype;
                                                              NLD=Aangemaakt documentsubtype];
                                                   Editable=No }
    { 115 ;   ;Created Doc. ID     ;Code20        ;CaptionML=[DEU=Erstellte Bel.-ID;
                                                              ENU=Created Doc. ID;
                                                              NLD=Aangemaakt document-id];
                                                   Editable=No }
    { 116 ;   ;Created Doc. Ref. No.;Integer      ;CaptionML=[DEU=Erst. Bel. Referenznr.;
                                                              ENU=Created Doc. Ref. No.;
                                                              NLD=Aangemaakt documentreferentienr.];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Document Type,No.                       ;Clustered=Yes }
    {    ;Exp. Header GUID                         }
    {    ;Continia User ID,Status                  }
    {    ;Posted,Continia User ID,Status           }
    {    ;Created Doc. Type,Created Doc. Subtype,Created Doc. ID,Created Doc. Ref. No. }
    {    ;Posted,Posted Date/Time,Document Type,No. }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ExpHeaderValidate@1160040006 : Codeunit 6086381;
      HideUI@1160040015 : Boolean;
      SkipSendToExpUser@1160040004 : Boolean;
      SuspendInboxCheck@1160040002 : Boolean;
      CannotModifyField@1160040001 : TextConst 'DEU=%1 kann nicht ge„ndert werden, weil es eine oder mehrere Zeilen gibt.;ENU=%1 cannot be modified because one or more lines exist.;NLD=%1 kan niet worden gewijzigd omdat ‚‚n of meer regels bestaan.';
      CannotRename@1160040003 : TextConst 'DEU=Sie k”nnen eine %1 nicht umbenennen.;ENU=You cannot rename a %1.;NLD=U kunt een %1 niet hernoemen.;SVE=Du kan inte byta namn p† %1.';
      DeleteSettlementTxt@1160040014 : TextConst 'DEU=Alle Zeilen des Ausgleichs werden gel”scht.\\M”chten Sie fortfahren?;ENU=This will delete all lines on the Settlement.\\Do you want to continue?;NLD=Alle regels van de afrekening worden verwijderd.\\Wilt u doorgaan?';
      EMInboxFoundErr@1160040010 : TextConst 'DEU=Die Ausgabe kann nicht ge„ndert werden, da das Ausgabeneingangsfach eine nicht verarbeitete Zeile enth„lt.\\Die Zeile im Ausgabeneingangsfach muss verarbeitet werden. bevor die Ausgabe aktualisiert werden kann.;ENU=%1 %2 cannot be updated as there are one or more unprocessed lines in the %3.\\Please process the related lines in the %3 before making changes to this %1.;NLD=%1 %2 kan niet bijgewerkt worden omdat er ‚‚n of meerdere onverwerkte regels in de %3 zijn.\\Verwerk de relevante regels in de %3 voordat u deze %1 wijzigt.';
      ExpensesTxt@1160040005 : TextConst 'DEU=Ausgaben;ENU=Expenses;NLD=Uitgaven;SVE=Kostnader';
      OneOrMoreInboxError@1160040009 : TextConst 'DEU=Es befinden sich ein oder mehrere nicht verarbeitete Posten im %1.;ENU=There are one or more unprocessed entries in the %1.;NLD=Er zijn ‚‚n of meerdere onverwerkte gegevens in %1.';
      ProcessInboxAsapTxt@1160040008 : TextConst 'DEU=\\Sie sollten diese so bald wie m”glich verarbeiten.;ENU=\\You should process these as soon as possible.;NLD=\\U dient deze zo spoedig mogelijk te verwerken.';
      SettlementPlural@1160040011 : TextConst 'DEU=Ausgleiche;ENU=Settlements;NLD=Afrekeningen';
      StatusNotAllowed@1160040007 : TextConst 'DEU=Status muss Offen oder Abwarten Benutzer in %1 %2 sein.;ENU=Status must be Open or Pending Expense User in %1 %2.;NLD=Status moet Open of Afwachten gebruiker zijn in %1 %2.';

    PROCEDURE AssistEditNoSeries@2(OldExpHeader@1000 : Record 6086339) : Boolean;
    VAR
      EMSetup@1160040000 : Record 6086300;
      NoSeriesMgt@1160040001 : Codeunit 396;
    BEGIN
      EMSetup.GET;
      EMSetup.TESTFIELD("Settlement Nos.");
      IF NoSeriesMgt.SelectSeries(EMSetup."Settlement Nos.",OldExpHeader."No. Series","No. Series") THEN BEGIN
        NoSeriesMgt.SetSeries("No.");
        EXIT(TRUE);
      END;
    END;

    LOCAL PROCEDURE LinesExist@1160040000() : Boolean;
    VAR
      Expense@1160040000 : Record 6086320;
      Mileage@1160040001 : Record 6086338;
    BEGIN
      IF "No." = '' THEN
        EXIT;

      Expense.SETCURRENTKEY("Settlement No.");
      Expense.SETRANGE("Settlement No.","No.");
      IF NOT Expense.ISEMPTY THEN
        EXIT(TRUE);

      Mileage.SETCURRENTKEY("Settlement No.");
      Mileage.SETRANGE("Settlement No.","No.");
      IF NOT Mileage.ISEMPTY THEN
        EXIT(TRUE);
    END;

    PROCEDURE SetSkipSendToExpUser@1160040002(NewSkipSendToExpUser@1160040000 : Boolean);
    BEGIN
      SkipSendToExpUser := NewSkipSendToExpUser;
    END;

    PROCEDURE SendToExpenseUser@1160040012();
    VAR
      SendToExpUser@1160040000 : Codeunit 6086382;
    BEGIN
      IF SkipSendToExpUser THEN
        EXIT;

      IF Status = Status::"Pending Expense User" THEN
        SendToExpUser.Update(Rec);
    END;

    PROCEDURE GetEmployeeEmail@1160040029() : Text[250];
    VAR
      ContiniaUser@1160040000 : Record 6086001;
    BEGIN
      IF ContiniaUser.GET("Continia User ID") THEN
        EXIT(ContiniaUser."E-Mail");
    END;

    PROCEDURE GetOverviewDetails@1160040033() AddInfo : Text[1024];
    VAR
      Expense@1160040000 : Record 6086320;
      Mileage@1160040001 : Record 6086338;
    BEGIN
      IF "No." <> '' THEN BEGIN
        Expense.SETCURRENTKEY("Settlement No.");
        Expense.SETRANGE("Settlement No.","No.");
        IF Expense.COUNT = 1 THEN
          AddTextTo(AddInfo,FORMAT(Expense.COUNT) + ' ' + Expense.TABLECAPTION)
        ELSE
          IF Expense.COUNT > 1 THEN
            AddTextTo(AddInfo,FORMAT(Expense.COUNT) + ' ' + ExpensesTxt);

        Mileage.SETCURRENTKEY("Settlement No.");
        Mileage.SETRANGE("Settlement No.","No.");
        IF Mileage.COUNT > 0 THEN
          AddTextTo(AddInfo,FORMAT(Mileage.COUNT) + ' ' + Mileage.TABLECAPTION);
      END;
    END;

    LOCAL PROCEDURE AddTextTo@1160040035(VAR ReturnTxt@1160040001 : Text[1024];TxtToAdd@1160040000 : Text[1024]);
    BEGIN
      IF TxtToAdd = '' THEN
        EXIT;

      IF (STRLEN(TxtToAdd) + STRLEN(ReturnTxt)) > MAXSTRLEN(ReturnTxt) THEN
        EXIT;

      IF ReturnTxt = '' THEN
        ReturnTxt := TxtToAdd
      ELSE
        ReturnTxt := ReturnTxt + ', ' + TxtToAdd;
    END;

    PROCEDURE LookupDimensions@1160040025();
    BEGIN
      DrillDownDimensions(PAGE::"Expense Dimensions");
    END;

    PROCEDURE LookupExtraFields@1160040028();
    BEGIN
      DrillDownDimensions(PAGE::"Expense Extra Fields");
    END;

    LOCAL PROCEDURE DrillDownDimensions@1160040014(FormID@1160040004 : Integer);
    VAR
      EMDim@1160040000 : Record 6086360;
      TempEMDim@1160040002 : TEMPORARY Record 6086360;
      ExpDim@1160040001 : Page 6086356;
      ExpExtraFields@1160040005 : Page 6086358;
    BEGIN
      EMDim.SETRANGE("Table ID",DATABASE::"Expense Header");
      EMDim.SETRANGE("Document Type","Document Type");
      EMDim.SETRANGE("Document No.","No.");
      EMDim.SETRANGE("Doc. Ref. No.",0);

      IF NOT Posted THEN BEGIN
        IF EMDim.FINDSET THEN
          REPEAT
            TempEMDim := EMDim;
            TempEMDim.INSERT;
          UNTIL EMDim.NEXT = 0;

        TempEMDim.SETRANGE("Table ID",DATABASE::"Expense Header");
        TempEMDim.SETRANGE("Document Type","Document Type");
        TempEMDim.SETRANGE("Document No.","No.");
        TempEMDim.SETRANGE("Doc. Ref. No.",0);
        PAGE.RUNMODAL(FormID,TempEMDim);

        IF EMDim.EMDimUpdated(TempEMDim,DATABASE::"Expense Header","Document Type","No.",0) THEN BEGIN
          EMDim.DELETEALL(TRUE);

          IF TempEMDim.FINDSET THEN
            REPEAT
              EMDim := TempEMDim;
              EMDim.INSERT(TRUE);
            UNTIL TempEMDim.NEXT = 0;

          GET("Document Type","No.");
          SendToExpenseUser;

          CODEUNIT.RUN(CODEUNIT::"Exp. Header - Validate",Rec);
        END;
      END ELSE
        CASE FormID OF
          PAGE::"Expense Dimensions":
            BEGIN
              ExpDim.SETTABLEVIEW(EMDim);
              ExpDim.SetReadOnly;
              ExpDim.RUNMODAL;
            END;

          PAGE::"Expense Extra Fields":
            BEGIN
              ExpExtraFields.SETTABLEVIEW(EMDim);
              ExpExtraFields.SetReadOnly;
              ExpExtraFields.RUNMODAL;
            END;
        END;
    END;

    PROCEDURE AddDefaultDim@1160040024(ValidatedFieldNo@1160040002 : Integer);
    VAR
      ContiniaUserSetup@1160040001 : Record 6086002;
      EMDimMgt@1160040000 : Codeunit 6086318;
    BEGIN
      DeleteOldDefaultDim;

      IF ContiniaUserSetup.GET("Continia User ID") THEN BEGIN
        IF ContiniaUserSetup.GetSalesPurchCode <> '' THEN
          EMDimMgt.InsertDefaultDimExpHeader(DATABASE::"Salesperson/Purchaser",ContiniaUserSetup.GetSalesPurchCode,Rec);

        IF ContiniaUserSetup."Vendor No." <> '' THEN
          EMDimMgt.InsertDefaultDimExpHeader(DATABASE::Vendor,ContiniaUserSetup."Vendor No.",Rec);
      END;

      IF "Job No." <> '' THEN
        EMDimMgt.InsertDefaultDimExpHeader(DATABASE::Job,"Job No.",Rec);

      CASE ValidatedFieldNo OF
        FIELDNO("Continia User ID"):
          IF ContiniaUserSetup.GET("Continia User ID") THEN BEGIN
            IF ContiniaUserSetup.GetSalesPurchCode <> '' THEN
              EMDimMgt.InsertDefaultDimExpHeader(DATABASE::"Salesperson/Purchaser",ContiniaUserSetup.GetSalesPurchCode,Rec);

            IF ContiniaUserSetup."Vendor No." <> '' THEN
              EMDimMgt.InsertDefaultDimExpHeader(DATABASE::Vendor,ContiniaUserSetup."Vendor No.",Rec);
          END;

        FIELDNO("Job No."):
          IF "Job No." <> '' THEN
            EMDimMgt.InsertDefaultDimExpHeader(DATABASE::Job,"Job No.",Rec);
      END;
    END;

    LOCAL PROCEDURE DeleteOldDefaultDim@1160040026();
    VAR
      ContiniaUser@1160040001 : Record 6086002;
      EMDimMgt@1160040000 : Codeunit 6086318;
    BEGIN
      IF ContiniaUser.GET(xRec."Continia User ID") THEN BEGIN
        IF ContiniaUser.GetSalesPurchCode <> '' THEN
          EMDimMgt.DeleteDefaultDimExpHeader(DATABASE::"Salesperson/Purchaser",ContiniaUser.GetSalesPurchCode,Rec);

        IF ContiniaUser."Vendor No." <> '' THEN
          EMDimMgt.DeleteDefaultDimExpHeader(DATABASE::Vendor,ContiniaUser."Vendor No.",Rec);
      END;

      IF xRec."Job No." <> '' THEN
        EMDimMgt.DeleteDefaultDimExpHeader(DATABASE::Job,xRec."Job No.",Rec);
    END;

    PROCEDURE GetAmountLCY@1160040003() : Decimal;
    BEGIN
      CALCFIELDS("Expense Total (LCY)","Mileage Total (LCY)");
      EXIT("Expense Total (LCY)" + "Mileage Total (LCY)");
    END;

    PROCEDURE GetDataCaption@1160040004() : Text[250];
    BEGIN
      EXIT(FORMAT("Document Type") + ' ' + "No.");
    END;

    PROCEDURE ExistsInInbox@1160040020() : Boolean;
    VAR
      ExpHeaderInbox@1160040000 : Record 6086341;
      ExpenseInbox@1160040001 : Record 6086323;
      MileageInbox@1160040002 : Record 6086353;
      EmptyGUID@1000 : GUID;
    BEGIN
      IF "Exp. Header GUID" = EmptyGUID THEN
        EXIT;

      ExpHeaderInbox.SETCURRENTKEY("Exp. Header GUID");
      ExpHeaderInbox.SETRANGE("Exp. Header GUID","Exp. Header GUID");
      ExpHeaderInbox.SETFILTER(Status,'%1|%2',ExpHeaderInbox.Status::Pending,ExpHeaderInbox.Status::Error);
      IF NOT ExpHeaderInbox.ISEMPTY THEN
        EXIT(TRUE);

      ExpenseInbox.SETCURRENTKEY("Expense Header GUID");
      ExpenseInbox.SETRANGE("Expense Header GUID","Exp. Header GUID");
      ExpenseInbox.SETFILTER(Status,'%1|%2',ExpenseInbox.Status::Pending,ExpenseInbox.Status::Error);
      IF NOT ExpenseInbox.ISEMPTY THEN
        EXIT(TRUE);

      MileageInbox.SETCURRENTKEY("Expense Header GUID");
      MileageInbox.SETRANGE("Expense Header GUID","Exp. Header GUID");
      MileageInbox.SETFILTER(Status,'%1|%2',MileageInbox.Status::Pending,MileageInbox.Status::Error);
      IF NOT MileageInbox.ISEMPTY THEN
        EXIT(TRUE);
    END;

    PROCEDURE CheckInboxAndThrowError@1();
    BEGIN
      IF NOT SuspendInboxCheck THEN
        IF ExistsInInbox THEN
          ThrowInboxError;
    END;

    PROCEDURE ThrowInboxError@3();
    VAR
      ExpHeaderInbox@1000 : Record 6086339;
    BEGIN
      ERROR(EMInboxFoundErr,TABLECAPTION,"No.",ExpHeaderInbox.TABLECAPTION);
    END;

    PROCEDURE Navigate@1160040005();
    VAR
      NavigateSettlement@1160040000 : Codeunit 6086326;
    BEGIN
      NavigateSettlement.NavigateSettlements(Rec);
    END;

    PROCEDURE HasApprovalComment@1160040022() : Boolean;
    VAR
      ApprovalCmtLine@1160040000 : Record 455;
      Expense@1160040001 : Record 6086320;
      Mileage@1160040002 : Record 6086338;
    BEGIN
      ApprovalCmtLine.SETCURRENTKEY("Table ID","Document Type","Document No.");
      ApprovalCmtLine.SETRANGE("Table ID",DATABASE::"Expense Header");
      ApprovalCmtLine.SETRANGE("Document Type",ApprovalCmtLine."Document Type"::Invoice);
      ApprovalCmtLine.SETRANGE("Document No.","No.");
      IF NOT ApprovalCmtLine.ISEMPTY THEN
        EXIT(TRUE);

      Expense.SETCURRENTKEY("Settlement No.");
      Expense.SETRANGE("Settlement No.","No.");
      IF Expense.FINDSET THEN
        REPEAT
          IF Expense.HasApprovalComment THEN
            EXIT(TRUE);
        UNTIL Expense.NEXT = 0;

      Mileage.SETCURRENTKEY("Settlement No.");
      Mileage.SETRANGE("Settlement No.","No.");
      IF Mileage.FINDSET THEN
        REPEAT
          IF Mileage.HasApprovalComment THEN
            EXIT(TRUE);
        UNTIL Mileage.NEXT = 0;
    END;

    PROCEDURE HasSettlementComment@1160040006() : Boolean;
    VAR
      Expense@1160040001 : Record 6086320;
      Mileage@1160040000 : Record 6086338;
    BEGIN
      CALCFIELDS(Comment);
      IF Comment THEN
        EXIT(TRUE);

      Expense.SETCURRENTKEY("Settlement No.");
      Expense.SETRANGE("Settlement No.","No.");
      IF Expense.FINDSET THEN
        REPEAT
          IF Expense.HasExpenseComment THEN
            EXIT(TRUE);
        UNTIL Expense.NEXT = 0;

      Mileage.SETCURRENTKEY("Settlement No.");
      Mileage.SETRANGE("Settlement No.","No.");
      IF Mileage.FINDSET THEN
        REPEAT
          IF Mileage.HasMileageComment THEN
            EXIT(TRUE);
        UNTIL Mileage.NEXT = 0;
    END;

    PROCEDURE HasErrorComment@1160040017(ShowFirstError@1000 : Boolean;RunValidationChecks@6086300 : Boolean) : Boolean;
    VAR
      EMCmtMgt@1160040001 : Codeunit 6086323;
    BEGIN
      EXIT(EMCmtMgt.HasErrorComments(DATABASE::"Expense Header","Document Type","No.",0,ShowFirstError,RunValidationChecks));
    END;

    PROCEDURE HasWarningComment@1160040008(ShowFirstError@1000 : Boolean) : Boolean;
    VAR
      EMCmtMgt@1160040001 : Codeunit 6086323;
    BEGIN
      EXIT(EMCmtMgt.HasWarningComments(DATABASE::"Expense Header","Document Type","No.",0,ShowFirstError,TRUE));
    END;

    PROCEDURE CheckUnProcessedInbox@1160040007();
    VAR
      ExpHeaderInbox@1160040000 : Record 6086341;
      UserResp@1160040001 : Record 6086379;
      TextMessage@1160040002 : Text[1024];
    BEGIN
      IF UserResp.GetResponsibilityFilter <> '' THEN
        EXIT;

      ExpHeaderInbox.SETFILTER(Status,'<>%1',ExpHeaderInbox.Status::Accepted);
      IF NOT ExpHeaderInbox.ISEMPTY THEN
        TextMessage := STRSUBSTNO(OneOrMoreInboxError,ExpHeaderInbox.TABLECAPTION);

      IF TextMessage <> '' THEN
        MESSAGE(TextMessage + ProcessInboxAsapTxt);
    END;

    PROCEDURE UpdatePostingDate@1160040009(PostingDatePolicy@1160040000 : 'FirstDate,LastDate');
    VAR
      Expense@1160040001 : Record 6086320;
      Mileage@1160040002 : Record 6086338;
      NewPostingDate@1160040003 : Date;
    BEGIN
      NewPostingDate := "Posting Date";

      Expense.SETCURRENTKEY("Settlement No.");
      Expense.SETRANGE("Settlement No.","No.");
      IF Expense.FINDFIRST THEN
        REPEAT
          IF PostingDatePolicy = PostingDatePolicy::FirstDate THEN
            IF (NewPostingDate = 0D) OR (NewPostingDate > Expense."Document Date") THEN
              NewPostingDate := Expense."Document Date";

          IF PostingDatePolicy = PostingDatePolicy::LastDate THEN
            IF (NewPostingDate = 0D) OR (NewPostingDate < Expense."Document Date") THEN
              NewPostingDate := Expense."Document Date";
        UNTIL Expense.NEXT = 0;

      Mileage.SETCURRENTKEY("Settlement No.");
      Mileage.SETRANGE("Settlement No.","No.");
      IF Mileage.FINDFIRST THEN
        REPEAT
          IF PostingDatePolicy = PostingDatePolicy::FirstDate THEN
            IF (NewPostingDate = 0D) OR (NewPostingDate > Mileage."Registration Date") THEN
              NewPostingDate := Mileage."Registration Date";

          IF PostingDatePolicy = PostingDatePolicy::LastDate THEN
            IF (NewPostingDate = 0D) OR (NewPostingDate < Mileage."Registration Date") THEN
              NewPostingDate := Mileage."Registration Date";
        UNTIL Mileage.NEXT = 0;

      IF "Posting Date" <> NewPostingDate THEN BEGIN
        "Posting Date" := NewPostingDate;
        MODIFY;
      END;
    END;

    PROCEDURE AttachExpensesToSettlement@1160040040();
    VAR
      Expense@1160040000 : Record 6086320;
      Expense2@1160040004 : Record 6086320;
      ExpenseForm@1160040002 : Page 6086317;
    BEGIN
      TestStatusOpenOrPendingExpUser;

      Expense.SETCURRENTKEY(Posted,"Continia User ID",Status,"Document Date");
      Expense.FILTERGROUP(4);
      Expense.SETRANGE("Continia User ID","Continia User ID");
      Expense.SETRANGE(Posted,FALSE);
      Expense.SETRANGE("Settlement No.",'');
      Expense.FILTERGROUP(0);

      ExpenseForm.LOOKUPMODE := TRUE;
      ExpenseForm.SETTABLEVIEW(Expense);
      IF ExpenseForm.RUNMODAL = ACTION::LookupOK THEN BEGIN
        ExpenseForm.GetSelectionFilter(Expense);

        IF Expense.FINDSET THEN
          REPEAT
            Expense2.GET(Expense."Entry No.");

            IF (Status = Status::Open) AND
               (Expense2.Status = Expense2.Status::"Pending Expense User")
            THEN
              CODEUNIT.RUN(CODEUNIT::"Expense - Reopen",Expense2);

            Expense2.GET(Expense."Entry No.");
            Expense2.VALIDATE("Settlement No.","No.");
            Expense2.MODIFY(TRUE);
          UNTIL Expense.NEXT = 0;
      END;
    END;

    PROCEDURE AttachMileageToSettlement@1160040010();
    VAR
      Mileage@1160040000 : Record 6086338;
      Mileage2@1160040004 : Record 6086338;
      MileageForm@1160040002 : Page 6086378;
    BEGIN
      TestStatusOpenOrPendingExpUser;

      Mileage.SETCURRENTKEY(Posted,"Continia User ID",Status,"Registration Date");
      Mileage.FILTERGROUP(4);
      Mileage.SETRANGE("Continia User ID","Continia User ID");
      Mileage.SETRANGE(Posted,FALSE);
      Mileage.SETRANGE("Settlement No.",'');
      Mileage.FILTERGROUP(0);

      MileageForm.LOOKUPMODE := TRUE;
      MileageForm.SETTABLEVIEW(Mileage);
      IF MileageForm.RUNMODAL = ACTION::LookupOK THEN BEGIN
        MileageForm.GetSelectionFilter(Mileage);

        IF Mileage.FINDSET THEN
          REPEAT
            Mileage2.GET(Mileage."Entry No.");

            IF (Status = Status::Open) AND
               (Mileage2.Status = Mileage2.Status::"Pending Expense User")
            THEN
              CODEUNIT.RUN(CODEUNIT::"Mileage-Reopen",Mileage2);

            Mileage2.VALIDATE("Settlement No.","No.");
            Mileage2.MODIFY(TRUE);
          UNTIL Mileage.NEXT = 0;
      END;
    END;

    PROCEDURE SetHideUI@1160040011();
    BEGIN
      HideUI := TRUE;
    END;

    PROCEDURE TestStatusOpenOrPendingExpUser@1160040013();
    BEGIN
      IF NOT (Status IN [Status::Open,Status::"Pending Expense User"]) THEN
        ERROR(StatusNotAllowed,"Document Type","No.");
    END;

    PROCEDURE NextReminderDate@1160040018() : Date;
    VAR
      EMReminder@1160040000 : Record 6086364;
    BEGIN
      EXIT(
        EMReminder.NextReminderDate(
          "Continia User ID",DATABASE::"Expense Header","Document Type"::Settlement,"No.",0,FirstDocumentDate));
    END;

    PROCEDURE SetSuspendInboxCheck@1160040016(NewSuspend@1160040000 : Boolean);
    BEGIN
      SuspendInboxCheck := NewSuspend;
    END;

    LOCAL PROCEDURE CheckLinesExist@1160040015();
    BEGIN
      IF LinesExist THEN
        ERROR(CannotModifyField,FIELDCAPTION("Continia User ID"));
    END;

    LOCAL PROCEDURE FirstDocumentDate@1160040019() FirstDate : Date;
    VAR
      Expense@1160040000 : Record 6086320;
      Mileage@1160040001 : Record 6086338;
      DocEarliestDate@1160040002 : Date;
    BEGIN
      FirstDate := "Date Created";

      Expense.SETCURRENTKEY("Settlement No.");
      Expense.SETRANGE("Settlement No.","No.");
      IF Expense.FINDSET THEN
        REPEAT
          DocEarliestDate := Expense.GetEarliestDate;
          IF (DocEarliestDate <> 0D) AND (DocEarliestDate < FirstDate) THEN
            FirstDate := DocEarliestDate;
        UNTIL Expense.NEXT = 0;

      Mileage.SETCURRENTKEY("Settlement No.");
      Mileage.SETRANGE("Settlement No.","No.");
      IF Mileage.FINDSET THEN
        REPEAT
          DocEarliestDate := Mileage.GetEarliestDate;
          IF (DocEarliestDate <> 0D) AND (DocEarliestDate < FirstDate) THEN
            FirstDate := DocEarliestDate;
        UNTIL Mileage.NEXT = 0;
    END;

    PROCEDURE GetTableCaptionPlural@1160040044() : Text[250];
    BEGIN
      EXIT(SettlementPlural);
    END;

    PROCEDURE AllocationExists@1160040001() : Boolean;
    VAR
      Expense@1160040000 : Record 6086320;
      Mileage@1160040001 : Record 6086338;
    BEGIN
      Expense.SETRANGE("Settlement No.","No.");
      IF Expense.FINDSET THEN
        REPEAT
          IF Expense.AllocationExists THEN
            EXIT(TRUE);
        UNTIL Expense.NEXT = 0;

      Mileage.SETRANGE("Settlement No.","No.");
      IF Mileage.FINDSET THEN
        REPEAT
          IF Mileage.AllocationExists THEN
            EXIT(TRUE);
        UNTIL Mileage.NEXT = 0;
    END;

    BEGIN
    {
      4PS, Objects renumbered
    }
    END.
  }
}

