OBJECT Table 11130020 IoT Service Object Threshold
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=ENU=IoT Service Object Threshold;
  }
  FIELDS
  {
    { 10  ;   ;Device ID           ;Text50        ;TableRelation="IoT Device";
                                                   CaptionML=ENU=Device ID }
    { 20  ;   ;Event ID            ;Text50        ;OnValidate=BEGIN
                                                                CALCFIELDS("Value Type");
                                                              END;

                                                   OnLookup=VAR
                                                              IoTDevice@1100528801 : Record 11130015;
                                                              IoTDeviceGroupEvent@1100528802 : Record 11130016;
                                                            BEGIN
                                                              IoTDevice.GET("Device ID");
                                                              IoTDeviceGroupEvent.SETRANGE("Device Group", IoTDevice."Device Group");
                                                              IF PAGE.RUNMODAL(PAGE::"IoT Device Group Events", IoTDeviceGroupEvent) = ACTION::LookupOK THEN
                                                                "Event ID" := IoTDeviceGroupEvent."Event ID";
                                                            END;

                                                   CaptionML=ENU=Event ID }
    { 30  ;   ;Service Object      ;Code20        ;TableRelation=Service-Object;
                                                   CaptionML=[ENU=Service Object;
                                                              NOR=Serviceobjekt;
                                                              SVE=Serviceobjekt] }
    { 40  ;   ;Action Code         ;Code20        ;TableRelation="IoT Action";
                                                   CaptionML=[ENU=Action Code;
                                                              SVE=ètgÑrdskod] }
    { 50  ;   ;IoT Action          ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("IoT Action"."IoT Action" WHERE (Code=FIELD(Action Code)));
                                                   CaptionML=[ENU=Action;
                                                              NOR=Handling;
                                                              SVE=ètgÑrd];
                                                   OptionCaptionML=ENU=" ,,,,Create Service Call,,,,Change Service Object Status";
                                                   OptionString=[ ,,,,Call,,,,ServiceObjectStatus];
                                                   Editable=No }
    { 60  ;   ;Action Description  ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("IoT Action".Description WHERE (Code=FIELD(Action Code)));
                                                   CaptionML=[ENU=Action Description;
                                                              NOR=Beskrivelse, handling;
                                                              SVE=ètgÑrdsbeskrivning];
                                                   Editable=No }
    { 100 ;   ;Lower Threshold Value;Text50       ;OnValidate=VAR
                                                                IoTManagement@1100528800 : Codeunit 11130041;
                                                              BEGIN
                                                                "Lower Threshold Value" := IoTManagement.GetEvaluateValueByValueType("Device ID", "Event ID", "Lower Threshold Value");
                                                              END;

                                                   CaptionML=ENU=Lower Threshold Value }
    { 110 ;   ;Upper Threshold Value;Text50       ;OnValidate=VAR
                                                                IoTManagement@1100528800 : Codeunit 11130041;
                                                              BEGIN
                                                                IF "Value Type" = "Value Type"::boolean THEN
                                                                  ERROR(
                                                                    FieldNotApplicable,
                                                                    FIELDCAPTION("Upper Threshold Value"),
                                                                    FIELDCAPTION("Value Type"),
                                                                    "Value Type"::boolean);
                                                                "Upper Threshold Value" := IoTManagement.GetEvaluateValueByValueType("Device ID", "Event ID", "Upper Threshold Value");
                                                              END;

                                                   CaptionML=ENU=Upper Threshold Value }
    { 200 ;   ;Value Type          ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("IoT Device Group Event"."Value Type" WHERE (Event ID=FIELD(Event ID)));
                                                   CaptionML=[ENU=Value Type;
                                                              NOR=Verditype;
                                                              SVE=VÑrdetyp];
                                                   OptionCaptionML=ENU=" ,boolean,date,datetime,decimal,integer,text,time";
                                                   OptionString=[ ,boolean,date,datetime,decimal,integer,text,time];
                                                   Editable=No }
    { 300 ;   ;In Progress         ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("IoT Action Log" WHERE (Device ID=FIELD(Device ID),
                                                                                             Event ID=FIELD(Event ID),
                                                                                             Service Object=FIELD(Service Object),
                                                                                             Action Code=FIELD(Action Code),
                                                                                             Closed=CONST(No)));
                                                   CaptionML=[ENU=In Progress;
                                                              NOR=I arbeid;
                                                              SVE=PÜgÜr];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Device ID,Event ID,Service Object,Action Code;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      FieldNotApplicable@1100528800 : TextConst 'ENU=%1 Not Applicable with %2 %3';

    PROCEDURE GetNoOfActionLogEntries@1100528800() : Integer;
    VAR
      IoTActionLog@1100528800 : Record 11130022;
    BEGIN
      IoTActionLog.SETRANGE("Device ID", "Device ID");
      IoTActionLog.SETRANGE("Event ID", "Event ID");
      IoTActionLog.SETRANGE("Service Object", "Service Object");
      IoTActionLog.SETRANGE("Action Code", "Action Code");
      EXIT(IoTActionLog.COUNT);
    END;

    PROCEDURE ShowActionLogEntries@1100528801();
    VAR
      IoTActionLog@1100528800 : Record 11130022;
      IoTActionLogPAGE@1100528801 : Page 11130767;
    BEGIN
      IoTActionLog.SETRANGE("Device ID", "Device ID");
      IoTActionLog.SETRANGE("Event ID", "Event ID");
      IoTActionLog.SETRANGE("Service Object", "Service Object");
      IoTActionLog.SETRANGE("Action Code", "Action Code");
      IoTActionLogPAGE.SETTABLEVIEW(IoTActionLog);
      IoTActionLogPAGE.RUNMODAL;
    END;

    BEGIN
    END.
  }
}

