OBJECT Codeunit 11125619 EstimateRecipeLine-Explode BOM
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    TableNo=11072075;
    OnRun=VAR
            EstimateRecipeLine@1100525003 : Record 11072075;
            GenericItem@1100525000 : Record 11125615;
            ExplodeGenericItem@1100525005 : Report 11125617;
          BEGIN
            EstimateRecipeLine := Rec;
            EstimateRecipeLine.SETRECFILTER;
            EstimateRecipeLine.SETRANGE("Line No.");

            IF "Item No." <> '' THEN BEGIN
              OrigLineNo := "Line No.";
              OrigQuantity := Quantity;
              ExplodeItem(EstimateRecipeLine);
            END ELSE BEGIN
              GenericItem.SETRANGE(Status,GenericItem.Status::Certified);
              IF PAGE.RUNMODAL(0,GenericItem) = ACTION::LookupOK THEN BEGIN
                GenericItem.TESTFIELD(Status,GenericItem.Status::Certified);
                ExplodeGenericItem.RUNMODAL;
                ExplodeGenItem(EstimateRecipeLine,GenericItem,ExplodeGenericItem.GetQuantity);
              END;
            END;
          END;

  }
  CODE
  {
    VAR
      Text001@1100525007 : TextConst 'DEU=Artikel %1 ist keine StÅckliste.;ENU=Item %1 is not a bill of materials.;NLD=Artikel %1 is geen stuklijst.;NOR=Artikkel %1 er ikke en stykkliste;SVE=Artikel %1 Ñr inte en struktur.';
      Text003@1100525005 : TextConst 'DEU=Es ist nicht genÅgend Platz, um die StÅckliste zu entfalten.;ENU=There is not enough space to explode the BOM.;NLD=Er is niet voldoende ruimte om de stuklijst weer te geven.;NOR=Det er ikke nok plass til Ü utfolde stykklisten.;SVE=Det finns inte tillrÑckligt med utrymme fîr att expandera strukturen.';
      OrigLineNo@1100525001 : Integer;
      OrigQuantity@1100525000 : Decimal;
      OrigItem@1100525003 : Code[20];
      LevelBOM@1100525002 : Integer;

    PROCEDURE ExplodeGenItem@1100525000(VAR EstimateRecipeLine@1100525000 : Record 11072075;GenericItem@1100525006 : Record 11125615;QuantityGenItem@1100525007 : Decimal);
    VAR
      ToEstimateRecipeLine@1100525001 : Record 11072075;
      GenericBOMLine@1100525005 : Record 11125616;
      NoOfBOMComp@1100525003 : Integer;
      NextLineNo@1100525004 : Integer;
      LineSpacing@1100525002 : Integer;
    BEGIN
      //explode for generic item
      WITH EstimateRecipeLine DO BEGIN
        GenericBOMLine.SETRANGE("Generic Item No.",GenericItem."No.");
        NoOfBOMComp := GenericBOMLine.COUNT;
        IF NoOfBOMComp = 0 THEN
          ERROR(Text001,GenericItem."No.");

        IF GenericBOMLine.FINDSET THEN BEGIN
          LineSpacing := GetLineSpacing(EstimateRecipeLine, NoOfBOMComp);
          NextLineNo := "Line No.";
          REPEAT
            NextLineNo := NextLineNo + LineSpacing;
            ToEstimateRecipeLine := EstimateRecipeLine;
            ToEstimateRecipeLine.INIT;
            ToEstimateRecipeLine."Line No." := NextLineNo;

            ToEstimateRecipeLine.VALIDATE("Item No.",GenericBOMLine."Item No.");
            ToEstimateRecipeLine.VALIDATE(Quantity,(GenericBOMLine.Quantity * QuantityGenItem));
            ToEstimateRecipeLine."Unit of Measure" := GenericBOMLine."Unit of Measure Code";
            ToEstimateRecipeLine.Description := GenericBOMLine.Description;
            ToEstimateRecipeLine."Part of Line" := OrigLineNo;
            ToEstimateRecipeLine.INSERT;
          UNTIL GenericBOMLine.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE ExplodeItem@1100525001(VAR EstimateRecipeLine@1100525000 : Record 11072075);
    VAR
      ToEstimateRecipeLine@1100525005 : Record 11072075;
      NoOfBOMComp@1100525003 : Integer;
      LineSpacing@1100525002 : Integer;
      NextLineNo@1100525001 : Integer;
      FromBOMComp@1100525008 : Record 90;
      BomComp@1100525007 : Record 90;
      TempEstimateRecipeLine@1100525006 : TEMPORARY Record 11072075;
    BEGIN
      //explode for item
      WITH EstimateRecipeLine DO BEGIN
        OrigLineNo := "Line No.";
        OrigQuantity := Quantity;
        IF OrigItem = '' THEN
          OrigItem := "Item No.";

        ToEstimateRecipeLine := EstimateRecipeLine;
        ToEstimateRecipeLine.INIT;
        ToEstimateRecipeLine.Description := Description;
        ToEstimateRecipeLine."Original Quantity" := OrigQuantity;
        ToEstimateRecipeLine."Part of Line" := "Part of Line";
        ToEstimateRecipeLine."Level BOM" := "Level BOM";
        ToEstimateRecipeLine."BOM Item No." := OrigItem;
        ToEstimateRecipeLine.MODIFY;
        LevelBOM := "Level BOM" + 1;

        FromBOMComp.SETRANGE("Parent Item No.","Item No.");
        FromBOMComp.SETFILTER(Type, '<>%1', FromBOMComp.Type::Resource);
        NoOfBOMComp := FromBOMComp.COUNT;
        IF NoOfBOMComp = 0 THEN BEGIN
          ExplodeItemProdBOM(EstimateRecipeLine);
        END;

        IF FromBOMComp.FINDSET THEN BEGIN
          LineSpacing := GetLineSpacing(EstimateRecipeLine, NoOfBOMComp);
          NextLineNo := "Line No.";
          REPEAT
            NextLineNo := NextLineNo + LineSpacing;
            ToEstimateRecipeLine := EstimateRecipeLine;
            ToEstimateRecipeLine.INIT;
            ToEstimateRecipeLine."Line No." := NextLineNo;

            IF FromBOMComp.Type = FromBOMComp.Type::Item THEN BEGIN
              FromBOMComp.TESTFIELD("No.");
              ToEstimateRecipeLine.VALIDATE("Item No.",FromBOMComp."No.");
              //DP02339.sn
              ToEstimateRecipeLine."Original Quantity" := FromBOMComp."Quantity per";
              ToEstimateRecipeLine.VALIDATE(Quantity,Quantity * ToEstimateRecipeLine."Original Quantity");
              //DP02339.en
            END;
            ToEstimateRecipeLine."Unit of Measure" := FromBOMComp."Unit of Measure Code";
            ToEstimateRecipeLine.Description := FromBOMComp.Description;
            ToEstimateRecipeLine."Part of Line" := OrigLineNo;
            ToEstimateRecipeLine."Level BOM" := LevelBOM;
            ToEstimateRecipeLine."BOM Item No." := OrigItem;
            ToEstimateRecipeLine.INSERT;

            IF FromBOMComp.Type = FromBOMComp.Type::Item THEN BEGIN
              BomComp.SETRANGE("Parent Item No.", ToEstimateRecipeLine."Item No.");
              IF NOT BomComp.ISEMPTY THEN BEGIN
                TempEstimateRecipeLine := ToEstimateRecipeLine;
                TempEstimateRecipeLine.INSERT;
              END;
            END;
          UNTIL FromBOMComp.NEXT = 0;
        END;
      END;

      IF TempEstimateRecipeLine.FINDSET THEN BEGIN
        //Explode BOMS
        COMMIT;
        REPEAT
          ExplodeItem(TempEstimateRecipeLine);
        UNTIL TempEstimateRecipeLine.NEXT = 0;
      END;
    END;

    PROCEDURE ExplodeItemProdBOM@1100525002(VAR EstimateRecipeLine@1100525000 : Record 11072075);
    VAR
      Item@1100525006 : Record 27;
      ToEstimateRecipeLine@1100525005 : Record 11072075;
      ProductionBOMLine@1100525004 : Record 99000772;
      NoOfBOMComp@1100525003 : Integer;
      LineSpacing@1100525002 : Integer;
      NextLineNo@1100525001 : Integer;
    BEGIN
      //explode for item with prod bom
      WITH EstimateRecipeLine DO BEGIN
        Item.GET("Item No.");
        ProductionBOMLine.SETRANGE("Production BOM No.",Item."Production BOM No.");
        ProductionBOMLine.SETRANGE(Type,ProductionBOMLine.Type::Item);
        NoOfBOMComp := ProductionBOMLine.COUNT;
        IF NoOfBOMComp = 0 THEN
          ERROR(Text001,"Item No.");

        IF ProductionBOMLine.FINDSET THEN BEGIN
          LineSpacing := GetLineSpacing(EstimateRecipeLine, NoOfBOMComp);
          NextLineNo := "Line No.";
          REPEAT
            NextLineNo := NextLineNo + LineSpacing;
            ToEstimateRecipeLine := EstimateRecipeLine;
            ToEstimateRecipeLine.INIT;
            ToEstimateRecipeLine."Line No." := NextLineNo;

            IF ProductionBOMLine.Type = ProductionBOMLine.Type::Item THEN BEGIN
              ProductionBOMLine.TESTFIELD("No.");
              ToEstimateRecipeLine.VALIDATE("Item No.",ProductionBOMLine."No.");
              //DP02339.sn
              ToEstimateRecipeLine."Original Quantity" := ProductionBOMLine."Quantity per";
              ToEstimateRecipeLine.VALIDATE(Quantity,Quantity * ToEstimateRecipeLine."Original Quantity");
              //DP02339.en
            END;
            ToEstimateRecipeLine."Unit of Measure" := ProductionBOMLine."Unit of Measure Code";
            ToEstimateRecipeLine.Description := ProductionBOMLine.Description;
            ToEstimateRecipeLine."Part of Line" := OrigLineNo;
            ToEstimateRecipeLine."Level BOM" := LevelBOM;
            ToEstimateRecipeLine."BOM Item No." := OrigItem;
            ToEstimateRecipeLine.INSERT;
          UNTIL ProductionBOMLine.NEXT = 0;
        END;
      END;
    END;

    LOCAL PROCEDURE GetLineSpacing@1100525003(VAR EstimateRecipeLine@1100525000 : Record 11072075;NoOfBOMComp@1100525002 : Integer) LineSpacing : Decimal;
    VAR
      ToEstimateRecipeLine@1100525001 : Record 11072075;
    BEGIN
      LineSpacing := 10000;

      ToEstimateRecipeLine := EstimateRecipeLine;
      ToEstimateRecipeLine.SETRECFILTER;
      ToEstimateRecipeLine.SETRANGE("Line No.");
      IF ToEstimateRecipeLine.FIND('>') THEN BEGIN
        LineSpacing := (ToEstimateRecipeLine."Line No." - EstimateRecipeLine."Line No.") DIV (1 + NoOfBOMComp);
        IF LineSpacing = 0 THEN
          ERROR(Text003);
      END;

      EXIT(LineSpacing);
    END;

    BEGIN
    {
      db, 04-02-16: C028603 (use same procedure for exploding different type of items)
      - calculate line spacing and determine next linenumber
      - use init instead of copy
      - validate item and quantity
    }
    END.
  }
}

