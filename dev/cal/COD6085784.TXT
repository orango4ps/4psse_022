OBJECT Codeunit 6085784 Continia Comp. Setup Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW13.70.00.5.00.02;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      DCDocumentCategory@6085573 : Record 6085575;
      CompanyCodeNotBlankErr@6085577 : TextConst 'DEU=%1 muss einen Wert haben in %2, wenn %3 %4 in %5 ist.;ENU="%1 must have a value in %2 when %3 is %4 in %5. ";NLD="%1 moet een waarde hebben in %2 wanneer %3 %4 in %5 is. "';
      CompanyActivatedMsg@6085576 : TextConst 'DEU=%1 ist bereits aktiviert. Wenn Sie %1 Ñndern, mÅssen Sie das Unternehmen erneut aktivieren.;ENU=%1 has already been activated. When you change %1 you have to reactivate the company.;NLD=%1 is al geactiveerd. Wanneer u %1 wijzigt, moet u het bedrijf opnieuw activeren.';
      DocumentsExsistMsg@6085575 : TextConst 'DEU="%2 %3 hat mindestens einen Beleg fertig zur Verarbeitung (%4 = %5 und %6 = %7). Wenn Sie %1 Ñndern, verlieren Sie den Zugriff auf diese Belege.\\Mîchten Sie fortfahren?";ENU="%2 %3 has at least one document ready for processing (%4 = %5 and %6 = %7). If you change %1 you will lose access to these documents.\\Are you sure you want to continue?";NLD="%2 %3 heeft ten minste ÇÇn document gereed voor verwerking (%4 = %5 en %6 = %7). Als u %1 wijzigt, raakt u de toegang tot deze documenten kwijt.\\Weet u zeker dat u wilt doorgaan?"';
      ExportConfFilesAndActivateMsg@6085580 : TextConst 'DEU=Wenn Sie %1 Ñndern, mÅssen Sie:\ - Document Capture erneut aktivieren\ - OCR-Konfigurationsdateien exportieren;ENU=When you change %1 you must:\ - Reactivate Document Capture\ - Export OCR Configuration Files;NLD=Wanneer u %1 wijzigt, moet u:\ - Document Capture opnieuw activeren\ - OCR-configuratiebestanden exporteren';
      ManuallyMoveFilesTxt@6085574 : TextConst 'DEU=\ - Verschiebe Archivdateien manuell;ENU=\ - Manually move archive files;NLD=\ - Verplaats archiefbestanden handmatig';
      ExportContiniaUsersTxt@6085581 : TextConst 'DEU=\ - Continia-Benutzer in Webgenehmigungsportal exportieren;ENU=\ - Export Continia Users to Web Approval Portal;NLD=\ - Continia-gebruikers exporteren naar webgoedkeuringsportal';
      ClientIDUsedErr@6085579 : TextConst 'DEU="%1 mit ''%2'' = %3 kann nicht gelîscht werden, weil es in %4 gebraucht wird.";ENU="%1 with ''%2'' = %3 cannot be deleted as it is used in %4.";NLD="%1 met ''%2'' = %3 kan niet worden verwijderd omdat het wordt gebruikt in %4."';
      ClientIDUsedInCompanyErr@6085578 : TextConst 'DEU="%1 mit ''%2'' = %3 kann nicht gelîscht werden, weil es in %4 in Unternehmen %5 gebraucht wird.";ENU="%1 with ''%2'' = %3 cannot be deleted as it is used in %4 in Company %5.";NLD="%1 met ''%2'' = %3 kan niet worden verwijderd omdat het wordt gebruikt in %4 in bedrijf %5."';

    PROCEDURE ContiniaCompanySetupOnBeforeValidateCompanyCode@6085573(VAR Rec@6085573 : Record 6192773;VAR xRec@6085574 : Record 6192773;CurrFieldNo@6085575 : Integer);
    VAR
      DCSetup@6085576 : Record 6085573;
      COLicenseMgt@6085579 : Codeunit 6192773;
      AboutDC@6085578 : Codeunit 6085589;
      ActivationState@6085577 : 'NeverActivated,NeedReactivation,Activated,OfflineStarted';
    BEGIN
      IF Rec."Company Code" = xRec."Company Code" THEN
        EXIT;

      IF NOT AboutDC.IsDCUsedInCompany(COMPANYNAME) THEN
        EXIT;

      IF xRec."Client ID" <> '' THEN
        IF COLicenseMgt.GetProductActivationState(AboutDC.ProductCode,COMPANYNAME) <> ActivationState::Activated THEN
          EXIT;

      DCSetup.GET;
      IF DCSetup."Use Cloud OCR" THEN
        IF Rec."Company Code" = '' THEN
          ERROR(CompanyCodeNotBlankErr,Rec.FIELDCAPTION("Company Code"),Rec.TABLECAPTION,DCSetup.FIELDCAPTION("Use Cloud OCR"),
            DCSetup."Use Cloud OCR",DCSetup.TABLECAPTION);

      MessageIfDocumentsToImportOrOCRExsist(Rec.FIELDCAPTION("Company Code"));
      MESSAGE(MakeTempTxt(Rec,Rec.FIELDNO("Company Code"),Rec.FIELDCAPTION("Company Code")));
    END;

    PROCEDURE ContiniaCompanySetupOnBeforeValidateClientID@6085574(VAR Rec@6085573 : Record 6192773;VAR xRec@6085574 : Record 6192773;CurrFieldNo@6085575 : Integer);
    VAR
      COLicenseMgt@6085576 : Codeunit 6192773;
      AboutDC@6085578 : Codeunit 6085589;
      ActivationState@6085577 : 'NeverActivated,NeedReactivation,Activated,OfflineStarted';
    BEGIN
      IF Rec."Client ID" = xRec."Client ID" THEN
        EXIT;

      IF NOT AboutDC.IsDCUsedInCompany(COMPANYNAME) THEN
        EXIT;

      IF xRec."Client ID" <> '' THEN
        IF COLicenseMgt.GetProductActivationState(AboutDC.ProductCode,COMPANYNAME) <> ActivationState::Activated THEN
          EXIT;

      MessageIfDocumentsToImportOrOCRExsist(Rec.FIELDCAPTION("Client ID"));
      MESSAGE(MakeTempTxt(Rec,Rec.FIELDNO("Client ID"),Rec.FIELDCAPTION("Client ID")));
    END;

    PROCEDURE ContiniaClientCredentialsOnBeforeDelete@6085575(VAR Rec@6085573 : Record 6192771;RunTrigger@6085574 : Boolean);
    VAR
      COLicenseMgt@6085576 : Codeunit 6192773;
      ActivationState@6085575 : 'NeverActivated,NeedReactivation,Activated,OfflineStarted';
      ContiniaCompanySetup@6085577 : Record 6192773;
      Company@6085578 : Record 2000000006;
    BEGIN
      IF ContiniaCompanySetup.GET THEN
        IF Rec."Client ID" = ContiniaCompanySetup."Client ID" THEN
          ERROR(ClientIDUsedErr,Rec.TABLECAPTION,Rec.FIELDCAPTION("Client ID"),Rec."Client ID",
            ContiniaCompanySetup.TABLECAPTION);

      Company.SETFILTER(Name,'<>%1',COMPANYNAME);
      IF Company.FIND('-') THEN
        REPEAT
          ContiniaCompanySetup.CHANGECOMPANY(Company.Name);
          IF ContiniaCompanySetup.GET THEN
            IF Rec."Client ID" = ContiniaCompanySetup."Client ID" THEN
              ERROR(ClientIDUsedInCompanyErr,Rec.TABLECAPTION,Rec.FIELDCAPTION("Client ID"),Rec."Client ID",
                ContiniaCompanySetup.TABLECAPTION,Company.Name);
        UNTIL Company.NEXT = 0;
    END;

    LOCAL PROCEDURE MessageIfDocumentsToImportOrOCRExsist@6085576(ChangedField@6085573 : Text[80]);
    BEGIN
      IF DCDocumentCategory.FIND('-') THEN
        REPEAT
          DCDocumentCategory.UpdateCounters;
          IF (DCDocumentCategory."Documents for OCR" <> 0) OR (DCDocumentCategory."Documents for Import" <> 0) THEN
            IF NOT CONFIRM(DocumentsExsistMsg,FALSE,ChangedField,DCDocumentCategory.TABLECAPTION,DCDocumentCategory.Code,
              DCDocumentCategory.FIELDCAPTION("Documents for OCR"),DCDocumentCategory."Documents for OCR",
              DCDocumentCategory.FIELDCAPTION("Documents for Import"),DCDocumentCategory."Documents for Import")
            THEN
              ERROR('');
        UNTIL DCDocumentCategory.NEXT = 0;
    END;

    LOCAL PROCEDURE MakeTempTxt@6085581(ContiniaCompanySetup@6085573 : Record 6192773;FieldNumber@6085575 : Integer;FieldCap@6085577 : Text[80]) : Text[1024];
    VAR
      DCSetup@6085574 : Record 6085573;
      TempTxt@6085576 : Text[1024];
    BEGIN
      DCSetup.GET;
      TempTxt := STRSUBSTNO(ExportConfFilesAndActivateMsg,FieldCap);

      IF FieldNumber = ContiniaCompanySetup.FIELDNO("Company Code") THEN
        IF (DCSetup."Document Storage Type" = DCSetup."Document Storage Type"::"File System") AND
          DCSetup."Company Code in Archive"
        THEN
          TempTxt := TempTxt + ManuallyMoveFilesTxt;

      IF ContiniaCompanySetup."Web Portal Code" <> '' THEN
        TempTxt := TempTxt + ExportContiniaUsersTxt;

      EXIT(TempTxt);
    END;

    BEGIN
    END.
  }
}

