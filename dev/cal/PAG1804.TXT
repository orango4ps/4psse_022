OBJECT Page 1804 Approval Workflow Setup Wizard
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Approval Workflow Setup;
               NOR=Oppsett av arbeidsflyt for godkjenning;
               SVE=Konfiguration av arbetsflîde fîr godkÑnnande];
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table1804;
    PageType=NavigatePage;
    SourceTableTemporary=Yes;
    ShowFilter=No;
    OnInit=BEGIN
             IF NOT GET THEN BEGIN
               INIT;
               "Use Exist. Approval User Setup" := TRUE;
               SetDefaultValues;
               INSERT;
             END;
             LoadTopBanners;
           END;

    OnOpenPage=VAR
                 ApprovalUserSetup@1000 : Page 663;
               BEGIN
                 ShowIntroStep;
                 ApprovalUserSetupLabel := STRSUBSTNO(OpenPageTxt,ApprovalUserSetup.CAPTION);
               END;

    OnQueryClosePage=VAR
                       AssistedSetup@1001 : Record 1803;
                     BEGIN
                       IF CloseAction = ACTION::OK THEN
                         IF AssistedSetup.GetStatus(PAGE::"Approval Workflow Setup Wizard") = AssistedSetup.Status::"Not Completed" THEN
                           IF NOT CONFIRM(NAVNotSetUpQst,FALSE) THEN
                             ERROR('');
                     END;

    ActionList=ACTIONS
    {
      { 8       ;    ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 15      ;1   ;Action    ;
                      Name=Back;
                      CaptionML=[ENU=Back;
                                 NOR=Tilbake;
                                 SVE=BakÜt];
                      ApplicationArea=#Suite;
                      Enabled=BackEnabled;
                      InFooterBar=Yes;
                      Image=PreviousRecord;
                      OnAction=BEGIN
                                 NextStep(TRUE);
                               END;
                                }
      { 14      ;1   ;Action    ;
                      Name=Next;
                      CaptionML=[ENU=Next;
                                 NOR=Neste;
                                 SVE=NÑsta];
                      ApplicationArea=#Suite;
                      Enabled=NextEnabled;
                      InFooterBar=Yes;
                      Image=NextRecord;
                      OnAction=BEGIN
                                 IF Step = Step::"Approval User Setup" THEN
                                   ValidateApproverUserSetup("Approver ID");
                                 NextStep(FALSE);
                               END;
                                }
      { 9       ;1   ;Action    ;
                      Name=Finish;
                      CaptionML=[ENU=Finish;
                                 NOR=Fullfõr;
                                 SVE=Slutfîr];
                      ApplicationArea=#Suite;
                      Enabled=FinishEnabled;
                      InFooterBar=Yes;
                      Image=Approve;
                      OnAction=VAR
                                 AssistedSetup@1004 : Record 1803;
                                 ApprovalWorkflowSetupMgt@1001 : Codeunit 1804;
                               BEGIN
                                 ApprovalWorkflowSetupMgt.ApplyInitialWizardUserInput(Rec);
                                 AssistedSetup.SetStatus(PAGE::"Approval Workflow Setup Wizard",AssistedSetup.Status::Completed);

                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 96  ;1   ;Group     ;
                Visible=TopBannerVisible AND NOT DoneVisible;
                Editable=FALSE;
                GroupType=Group }

    { 97  ;2   ;Field     ;
                ApplicationArea=#Suite;
                SourceExpr=MediaResourcesStandard."Media Reference";
                Editable=FALSE;
                ShowCaption=No }

    { 98  ;1   ;Group     ;
                Visible=TopBannerVisible AND DoneVisible;
                Editable=FALSE;
                GroupType=Group }

    { 99  ;2   ;Field     ;
                ApplicationArea=#Suite;
                SourceExpr=MediaResourcesDone."Media Reference";
                Editable=FALSE;
                ShowCaption=No }

    { 11  ;1   ;Group     ;
                Name=Step1;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                Visible=IntroVisible;
                GroupType=Group }

    { 18  ;2   ;Group     ;
                Name=Para1.1;
                CaptionML=[ENU=Welcome to Approval Workflow Setup;
                           NOR=Velkommen til oppsettet av arbeidsflyt for godkjenning;
                           SVE=VÑlkommen till InstÑllningar fîr arbetsflîde fîr godkÑnnande];
                GroupType=Group }

    { 19  ;3   ;Group     ;
                Name=Para1.1.1;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                GroupType=Group;
                InstructionalTextML=[ENU=You can create approval workflows that automatically notify an approver when a user tries to create or change certain values on documents, journal lines, or cards, such as an amount above a specified limit.;
                                     NOR=Du kan opprette arbeidsflyter for godkjenning som automatisk varsler en godkjenner nÜr en bruker prõver Ü opprette eller endre visse verdier for dokumenter, kladdelinjer eller kort, for eksempel belõp over en bestemt grense.;
                                     SVE=Du kan skapa arbetsflîden fîr godkÑnnande som automatiskt meddelar en godkÑnnare nÑr en anvÑndare fîrsîker att skapa eller Ñndra vissa vÑrden i dokument, journalrader eller kort, till exempel ett belopp îver en angiven grÑns.] }

    { 20  ;3   ;Group     ;
                Name=Para1.1.2;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                GroupType=Group;
                InstructionalTextML=[ENU=To enable typical approval workflows, such as for amounts on purchase invoices, you must specify basic settings, such as the approver, the amount limit and when the approval is due. More advanced settings are preset on the related approval workflow, which you can modify later.;
                                     NOR=Hvis du vil aktivere vanlige arbeidsflyter for godkjenning, for eksempel for belõp pÜ kjõpsfakturaer, mÜ du angi noen grunnleggende innstillinger, for eksempel godkjenner, belõpsgrense og forfallsdato for godkjenning. Mer avanserte innstillinger er forhÜndsdefinert for den relaterte godkjenningsarbeidsflyten, som du kan endre senere.;
                                     SVE=Fîr att kunna aktivera typiska arbetsflîden fîr godkÑnnande, till exempel fîr belopp pÜ inkîpsfakturor, mÜste du ange basinstÑllningar som godkÑnnare, beloppsgrÑns och nÑr godkÑnnandet fîrfaller. Mer avancerade instÑllningar har fîrinstÑllts i det relaterade arbetsflîdet fîr godkÑnnande. Dessa kan du Ñndra senare.] }

    { 21  ;2   ;Group     ;
                Name=Para1.2;
                CaptionML=[ENU=Let's go!;
                           NOR=La oss sette i gang!;
                           SVE=SÑtt igÜng!];
                GroupType=Group;
                InstructionalTextML=[ENU=Choose Next to select from a list of typical approval workflows and then start specifying basic settings for each workflow.;
                                     NOR=Velg Neste for Ü velge fra en liste over vanlige arbeidsflyter for godkjenning, og angi deretter grunnleggende innstillinger for hver arbeidsflyt.;
                                     SVE=VÑlj NÑsta om du vill vÑlja frÜn en lista med typiska arbetsflîden fîr godkÑnnande och sedan bîrja ange basinstÑllningar fîr respektive arbetsflîde.] }

    { 2   ;1   ;Group     ;
                Name=Step2;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                Visible=ApprovalDocumentTypesVisible;
                GroupType=Group }

    { 22  ;2   ;Group     ;
                Name=Para2.1;
                CaptionML=[ENU=Which approval workflow do you want to set up?;
                           NOR=Hvilken arbeidsflyt for godkjenning vil du definere?;
                           SVE=Vilket arbetsflîde fîr godkÑnnande vill du konfigurera?];
                GroupType=Group }

    { 3   ;3   ;Field     ;
                Name=Purchase Invoice Approval;
                CaptionML=[ENU=Purchase Invoice Approval;
                           NOR=Kjõpsfakturagodkjenning;
                           SVE=GodkÑnnande av inkîpsfaktura];
                ApplicationArea=#Suite;
                SourceExpr="Purch Invoice App. Workflow";
                OnValidate=BEGIN
                             NextEnabled := "Purch Invoice App. Workflow" OR "Sales Invoice App. Workflow"
                           END;
                            }

    { 16  ;3   ;Field     ;
                Name=Sales Invoice Approval;
                CaptionML=[ENU=Sales Invoice Approval;
                           NOR=Salgsfakturagodkjenning;
                           SVE=GodkÑnnande av fîrsÑljningsfaktura];
                ApplicationArea=#Suite;
                SourceExpr="Sales Invoice App. Workflow";
                OnValidate=BEGIN
                             NextEnabled := "Purch Invoice App. Workflow" OR "Sales Invoice App. Workflow"
                           END;
                            }

    { 32  ;1   ;Group     ;
                Name=Step3;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                Visible=UseExistingApprovalSetupVisible;
                GroupType=Group }

    { 33  ;2   ;Group     ;
                Name=Para3.1;
                CaptionML=[ENU=An approval user setup already exists.;
                           NOR=Det finnes allerede et oppsett for godkjenningsbruker.;
                           SVE=Det finns redan en instÑllning av anvÑndare fîr godkÑnnande.];
                GroupType=Group }

    { 36  ;3   ;Field     ;
                Name=Use Exist. Approval User Setup;
                CaptionML=[ENU=Use the existing setup;
                           NOR=Bruk det eksisterende oppsettet;
                           SVE=AnvÑnd de befintliga instÑllningarna];
                ApplicationArea=#Suite;
                SourceExpr="Use Exist. Approval User Setup" }

    { 34  ;3   ;Field     ;
                Name=ApprovalUserSetupLabel;
                ApplicationArea=#Suite;
                SourceExpr=ApprovalUserSetupLabel;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              PAGE.RUNMODAL(PAGE::"Approval User Setup");
                            END;

                ShowCaption=No }

    { 4   ;1   ;Group     ;
                Name=Step4;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                Visible=ApprovalUserSetupVisible;
                GroupType=Group }

    { 12  ;2   ;Group     ;
                Name=Para4.1;
                CaptionML=[ENU=To set up the approval users, answer the following questions.;
                           NOR=Svar pÜ fõlgende spõrsmÜl hvis du vil konfigurere godkjenningsbrukerne.;
                           SVE=Du stÑller in anvÑndare fîr godkÑnnanden genom att svara pÜ fîljande frÜgor:];
                Visible=PurchInvoiceApprovalDetailsVisible OR SalesInvoiceApprovalDetailsVisible;
                GroupType=Group }

    { 37  ;3   ;Field     ;
                Name=Who is the approver?;
                CaptionML=[ENU=Who is the approver?;
                           NOR=Hvem er godkjenneren?;
                           SVE=Vem Ñr godkÑnnare?];
                ApplicationArea=#Suite;
                SourceExpr="Approver ID";
                TableRelation=User."User Name";
                Enabled="Purch Invoice App. Workflow" OR "Sales Invoice App. Workflow" }

    { 23  ;2   ;Group     ;
                Name=Para4.2;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                Visible=PurchInvoiceApprovalDetailsVisible;
                GroupType=Group;
                InstructionalTextML=[ENU=Which amount can the user enter on purchase invoices before the invoice requires approval?;
                                     NOR=Hvor stort belõp kan brukeren angi pÜ kjõpsfakturaer fõr fakturaen krever godkjenning?;
                                     SVE=Vilket belopp kan anvÑndaren ange pÜ inkîpsfakturor innan fakturan mÜste godkÑnnas?] }

    { 5   ;3   ;Field     ;
                Name=Purch Amount Approval Limit;
                CaptionML=[ENU=Amount Limit;
                           NOR=Belõpsgrense;
                           SVE=BeloppsgrÑns];
                ApplicationArea=#Suite;
                SourceExpr="Purch Amount Approval Limit" }

    { 24  ;2   ;Group     ;
                Name=Para4.3;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                Visible=SalesInvoiceApprovalDetailsVisible;
                GroupType=Group;
                InstructionalTextML=[ENU=Which amount can the user enter on sales invoices before the invoice requires approval?;
                                     NOR=Hvor stort belõp kan brukeren angi pÜ salgsfakturaer fõr fakturaen krever godkjenning?;
                                     SVE=Vilket belopp kan anvÑndaren ange pÜ fîrsÑljningsfakturor innan fakturan mÜste godkÑnnas?] }

    { 13  ;3   ;Field     ;
                Name=Sales Amount Approval Limit;
                CaptionML=[ENU=Amount Limit;
                           NOR=Belõpsgrense;
                           SVE=BeloppsgrÑns];
                ApplicationArea=#Suite;
                SourceExpr="Sales Amount Approval Limit" }

    { 10  ;1   ;Group     ;
                Name=Step5;
                CaptionML=[ENU="";
                           NOR="";
                           SVE=""];
                Visible=DoneVisible;
                GroupType=Group }

    { 25  ;2   ;Group     ;
                Name=Para5.1;
                CaptionML=[ENU=That's it!;
                           NOR=Det var det!;
                           SVE=Klart!];
                GroupType=Group;
                InstructionalTextML=[ENU=Choose Finish to enable the selected approval workflows with the specified settings.;
                                     NOR=Velg Fullfõr for Ü aktivere de valgte arbeidsflytene for godkjenning med de angitte innstillingene.;
                                     SVE=VÑlj Slutfîr om du vill aktivera de angivna instÑllningarna fîr valda arbetsflîden fîr godkÑnnande.] }

  }
  CODE
  {
    VAR
      MediaRepositoryStandard@1040 : Record 9400;
      MediaRepositoryDone@1041 : Record 9400;
      MediaResourcesStandard@1002 : Record 2000000182;
      MediaResourcesDone@1000 : Record 2000000182;
      ClientTypeManagement@1007 : Codeunit 4030;
      Step@1015 : 'Intro,Approval Document Types,Use Existing Approval User Setup,Approval User Setup,Done';
      BackEnabled@1014 : Boolean;
      NextEnabled@1013 : Boolean;
      FinishEnabled@1010 : Boolean;
      TopBannerVisible@1042 : Boolean;
      IntroVisible@1009 : Boolean;
      ApprovalDocumentTypesVisible@1008 : Boolean;
      UseExistingApprovalSetupVisible@1017 : Boolean;
      ApprovalUserSetupVisible@1011 : Boolean;
      SalesInvoiceApprovalDetailsVisible@1003 : Boolean;
      PurchInvoiceApprovalDetailsVisible@1006 : Boolean;
      DoneVisible@1004 : Boolean;
      NAVNotSetUpQst@1005 : TextConst 'ENU=Document Approval has not been set up.\\Are you sure that you want to exit?;NOR=Dokumentgodkjenning er ikke konfigurert.\\Er du sikker pÜ at du vil avslutte?;SVE=DokumentgodkÑnnande har inte konfigurerats.\\Vill du avsluta?';
      NoUnlimitedApproverErr@1001 : TextConst 'ENU=Select a user that has unlimited approval rights.;NOR=Velg en bruker som har ubegrensede godkjenningsrettigheter.;SVE=VÑlj en anvÑndare som har obegrÑnsad godkÑnnandebehîrighet.';
      OpenPageTxt@1012 : TextConst '@@@=%1 is the page that will be opened when clicking the control;ENU=Open %1;NOR=èpne %1;SVE=ôppna %1';
      ApprovalUserSetupLabel@1016 : Text;

    LOCAL PROCEDURE NextStep@3(Backwards@1000 : Boolean);
    BEGIN
      IF Backwards THEN
        Step := Step - 1
      ELSE
        Step := Step + 1;

      CASE Step OF
        Step::Intro:
          ShowIntroStep;
        Step::"Approval Document Types":
          ShowApprovalDocumentTypesStep;
        Step::"Use Existing Approval User Setup":
          BEGIN
            ShowUseExistingApprovalSetupQstStep;
            IF NOT UseExistingApprovalSetupVisible THEN
              NextStep(Backwards)
          END;
        Step::"Approval User Setup":
          IF NOT "Use Exist. Approval User Setup" THEN
            ShowApprovalUserSetupDetailsStep
          ELSE
            NextStep(Backwards);
        Step::Done:
          ShowDoneStep;
      END;
      CurrPage.UPDATE(TRUE);
    END;

    LOCAL PROCEDURE ShowIntroStep@1();
    BEGIN
      ResetWizardControls;
      IntroVisible := TRUE;
      BackEnabled := FALSE;
    END;

    LOCAL PROCEDURE ShowApprovalDocumentTypesStep@2();
    BEGIN
      ResetWizardControls;
      ApprovalDocumentTypesVisible := TRUE;
      NextEnabled := "Purch Invoice App. Workflow" OR "Sales Invoice App. Workflow";
    END;

    LOCAL PROCEDURE ShowApprovalUserSetupDetailsStep@9();
    BEGIN
      ResetWizardControls;
      ApprovalUserSetupVisible := NOT "Use Exist. Approval User Setup";
      SalesInvoiceApprovalDetailsVisible := NOT "Use Exist. Approval User Setup" AND "Sales Invoice App. Workflow";
      PurchInvoiceApprovalDetailsVisible := NOT "Use Exist. Approval User Setup" AND "Purch Invoice App. Workflow";
    END;

    LOCAL PROCEDURE ShowUseExistingApprovalSetupQstStep@33();
    VAR
      ApprovalUserSetup@1000 : Record 91;
    BEGIN
      ResetWizardControls;
      UseExistingApprovalSetupVisible := NOT ApprovalUserSetup.ISEMPTY;
      IF NOT UseExistingApprovalSetupVisible THEN
        "Use Exist. Approval User Setup" := FALSE;
    END;

    LOCAL PROCEDURE ShowDoneStep@6();
    BEGIN
      ResetWizardControls;
      DoneVisible := TRUE;
      NextEnabled := FALSE;
      FinishEnabled := TRUE;
    END;

    LOCAL PROCEDURE ResetWizardControls@10();
    BEGIN
      // Buttons
      BackEnabled := TRUE;
      NextEnabled := TRUE;
      FinishEnabled := FALSE;

      // Tabs
      IntroVisible := FALSE;
      ApprovalDocumentTypesVisible := FALSE;
      UseExistingApprovalSetupVisible := FALSE;
      ApprovalUserSetupVisible := FALSE;
      PurchInvoiceApprovalDetailsVisible := FALSE;
      SalesInvoiceApprovalDetailsVisible := FALSE;
      DoneVisible := FALSE;
    END;

    LOCAL PROCEDURE ValidateApproverUserSetup@5(UserName@1001 : Code[50]);
    BEGIN
      IF UserName = '' THEN
        ERROR(NoUnlimitedApproverErr);
    END;

    LOCAL PROCEDURE SetDefaultValues@8();
    VAR
      ApprovalUserSetup@1003 : Record 91;
      Workflow@1000 : Record 1501;
      WorkflowSetup@1002 : Codeunit 1502;
      WorkflowCode@1001 : Code[20];
    BEGIN
      // Specific Purchase Invoice Approval Workflow: WZ-PIAPW
      WorkflowCode := WorkflowSetup.GetWorkflowWizardCode(WorkflowSetup.PurchaseInvoiceApprovalWorkflowCode);
      IF Workflow.GET(WorkflowCode) THEN
        "Purch Invoice App. Workflow" := TRUE;

      // Specific Sales Invoice Approval Workflow: WZ-SIAPW
      WorkflowCode := WorkflowSetup.GetWorkflowWizardCode(WorkflowSetup.SalesInvoiceApprovalWorkflowCode);
      IF Workflow.GET(WorkflowCode) THEN
        "Sales Invoice App. Workflow" := TRUE;

      ApprovalUserSetup.SETRANGE("Unlimited Sales Approval",TRUE);
      ApprovalUserSetup.SETRANGE("Unlimited Purchase Approval",TRUE);
      IF ApprovalUserSetup.FINDFIRST THEN
        "Approver ID" := ApprovalUserSetup."User ID";

      "Purch Amount Approval Limit" := ApprovalUserSetup.GetDefaultPurchaseAmountApprovalLimit;
      "Sales Amount Approval Limit" := ApprovalUserSetup.GetDefaultSalesAmountApprovalLimit;
    END;

    LOCAL PROCEDURE LoadTopBanners@40();
    BEGIN
      IF MediaRepositoryStandard.GET('AssistedSetup-NoText-400px.png',FORMAT(ClientTypeManagement.GetCurrentClientType)) AND
         MediaRepositoryDone.GET('AssistedSetupDone-NoText-400px.png',FORMAT(ClientTypeManagement.GetCurrentClientType))
      THEN
        IF MediaResourcesStandard.GET(MediaRepositoryStandard."Media Resources Ref") AND
           MediaResourcesDone.GET(MediaRepositoryDone."Media Resources Ref")
        THEN
          TopBannerVisible := MediaResourcesDone."Media Reference".HASVALUE;
    END;

    BEGIN
    END.
  }
}

