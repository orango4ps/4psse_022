OBJECT Table 11012856 Reference Norm
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               LicenceCu.CheckLicenceDeadline;
             END;

    OnModify=BEGIN
               LicenceCu.CheckLicenceDeadline;
             END;

    CaptionML=[ENU=Reference Limit;
               NOR=Referansegrense;
               SVE=Referensgr„ns];
    LookupPageID=Page11020488;
  }
  FIELDS
  {
    { 10  ;   ;Service Contract No.;Code20        ;TableRelation=Service-Contract;
                                                   OnValidate=BEGIN
                                                                IF "Service Contract No." <> '' THEN
                                                                  TESTFIELD("Customer No.", '');
                                                              END;

                                                   CaptionML=[ENU=Service Contract No.;
                                                              NOR=Servicekontraktnr;
                                                              SVE=Servicekontraktnr] }
    { 20  ;   ;Service Location No.;Code20        ;TableRelation="Service Location";
                                                   OnValidate=VAR
                                                                ServLocRec@1100525002 : Record 11012801;
                                                                ServObjectRec@1100525000 : Record 11071691;
                                                              BEGIN
                                                                IF ServLocRec.GET("Service Location No.") THEN BEGIN  //db, 23-12-13: C010904
                                                                  IF "Object No." <> '' THEN BEGIN
                                                                    ServObjectRec.GET("Object No.");
                                                                    TESTFIELD("Service Location No.", ServObjectRec."Service Location No.");
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Service Location No.;
                                                              NOR=Servicelokasjon Nr.;
                                                              SVE=Serviceplatsnr] }
    { 21  ;   ;Service Location Description;Text50;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location".Description WHERE (No.=FIELD(Service Location No.)));
                                                   CaptionML=[ENU=Service Location Description;
                                                              NOR=Servicelokasjonbeskrivelse;
                                                              SVE=Serviceplatsbeskrivning];
                                                   Editable=No }
    { 50  ;   ;Discipline          ;Code10        ;TableRelation=Discipline.Code;
                                                   OnValidate=VAR
                                                                ServiceContractDiscipline@1100528600 : Record 11012881;
                                                              BEGIN
                                                                IF ("Service Contract No." <> '') AND (Discipline <> '') THEN BEGIN
                                                                  ServiceContractDiscipline.SETRANGE("Service Contract No.", "Service Contract No.");
                                                                  IF NOT ServiceContractDiscipline.ISEMPTY THEN BEGIN
                                                                    ServiceContractDiscipline.SETRANGE(Discipline, Discipline);
                                                                    ServiceContractDiscipline.FINDFIRST;
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Discipline;
                                                              NOR=Disiplin;
                                                              SVE=Disciplin] }
    { 51  ;   ;Discipline Description;Text30      ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Discipline.Description WHERE (Code=FIELD(Discipline)));
                                                   CaptionML=[ENU=Discipline Description;
                                                              NOR=Disiplinbeskrivelse;
                                                              SVE=Disciplinbeskrivning];
                                                   Editable=No }
    { 55  ;   ;Trade Speciality    ;Code10        ;TableRelation="Trade Speciality".Code WHERE (Discipline=FIELD(Discipline));
                                                   OnValidate=VAR
                                                                ServiceContractDiscipline@1100528600 : Record 11012881;
                                                              BEGIN
                                                                IF ("Service Contract No." <> '') AND ("Trade Speciality" <> '') THEN BEGIN
                                                                  ServiceContractDiscipline.SETRANGE("Service Contract No.", "Service Contract No.");
                                                                  IF NOT ServiceContractDiscipline.ISEMPTY THEN BEGIN
                                                                    ServiceContractDiscipline.SETRANGE("Trade Speciality", "Trade Speciality");
                                                                    ServiceContractDiscipline.FINDFIRST;
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Trade Speciality;
                                                              NOR=SNI-nummer;
                                                              SVE=SNI-nummer] }
    { 56  ;   ;Trade Speciality Description;Text30;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Trade Speciality".Description WHERE (Discipline=FIELD(Discipline),
                                                                                                            Code=FIELD(Trade Speciality)));
                                                   CaptionML=[ENU=Trade Speciality Description;
                                                              NOR=Handelspesialitet, beskrivelse;
                                                              SVE=SNI-nummer, beskrivning];
                                                   Editable=No }
    { 60  ;   ;Priority            ;Code10        ;TableRelation=IF (Service Contract No.=FILTER(''),
                                                                     Customer No.=FILTER('')) "Service Priority".Code
                                                                     ELSE IF (Service Contract No.=FILTER(<>'')) "Service Contract Priority".Priority WHERE (Service Contract No.=FIELD(Service Contract No.))
                                                                     ELSE IF (Customer No.=FILTER(<>'')) "Customer Service Priority".Priority WHERE (Customer No.=FIELD(Customer No.));
                                                   CaptionML=[ENU=Priority;
                                                              NOR=Prioritet;
                                                              SVE=Prioritet] }
    { 61  ;   ;Priority Description;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Priority".Description WHERE (Code=FIELD(Priority)));
                                                   CaptionML=[ENU=Priority Description;
                                                              NOR=Prioritet, beskrivelse;
                                                              SVE=Prioritet, beskrivning];
                                                   Editable=No }
    { 70  ;   ;Reference Period    ;Code10        ;TableRelation="Reference Period".Code WHERE (Type=CONST(Common),
                                                                                                Apply=FILTER(ServCall|ServOrder));
                                                   OnValidate=BEGIN
                                                                RefPeriodRec.SETRANGE(Type, RefPeriodRec.Type::Common);
                                                                RefPeriodRec.SETRANGE(Code, "Reference Period");
                                                                RefPeriodRec.FINDFIRST;
                                                                "Description Reference Period" := RefPeriodRec.Description;
                                                              END;

                                                   CaptionML=[ENU=Reference Period;
                                                              NOR=Referanseperiode;
                                                              SVE=Referensperiod] }
    { 71  ;   ;Description Reference Period;Text50;CaptionML=[ENU=Description Reference Period;
                                                              NOR=Beskrivelse av Referanseperiode;
                                                              SVE=Beskrivning av referensperiod] }
    { 80  ;   ;Norm                ;Decimal       ;CaptionML=[ENU=Norm (Hours);
                                                              NOR=Norm (Timer);
                                                              SVE=Norm (timmar)];
                                                   DecimalPlaces=0:5;
                                                   BlankZero=Yes }
    { 90  ;   ;Comment             ;Text250       ;CaptionML=[ENU=Comment;
                                                              NOR=Merknad;
                                                              SVE=Kommentar] }
    { 100 ;   ;Type Hours          ;Option        ;CaptionML=[ENU=Type Hours;
                                                              NOR=Typetimer;
                                                              SVE=Typtimmar];
                                                   OptionCaptionML=[ENU=Clock Time,Office Time;
                                                                    NOR=Klokketid,Kontortid;
                                                                    SVE=Klocktid,Kontorstid];
                                                   OptionString=ClockTime,OfficeTime }
    { 110 ;   ;Customer No.        ;Code20        ;TableRelation=Customer;
                                                   OnValidate=BEGIN
                                                                IF "Customer No." <> '' THEN
                                                                  TESTFIELD("Service Contract No.", '');
                                                              END;

                                                   CaptionML=[ENU=Customer No.;
                                                              NOR=Kundenr;
                                                              SVE=Kundnr] }
    { 111 ;   ;Customer Description;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Customer.Name WHERE (No.=FIELD(Customer No.)));
                                                   CaptionML=ENU=Customer Description;
                                                   Editable=No }
    { 120 ;   ;Object No.          ;Code20        ;TableRelation=Service-Object;
                                                   OnValidate=VAR
                                                                ServiceContract@1100525000 : Record 11012812;
                                                                ServObjectRec@1100525001 : Record 11071691;
                                                              BEGIN
                                                                IF ("Service Contract No." <> '') AND ("Object No." <> '') THEN BEGIN
                                                                  ServiceContract.GET("Service Contract No.");
                                                                  ServiceContract.CheckObjectPartOfContract("Object No.", TODAY);
                                                                END;

                                                                IF ServObjectRec.GET("Object No.") THEN
                                                                  "Service Location No." := ServObjectRec."Service Location No.";  //db, 23-12-13: C010904
                                                              END;

                                                   OnLookup=VAR
                                                              ContractObject@1100525000 : Record 11071702;
                                                              ServiceLocation@1100525001 : Record 11012801;
                                                              ServiceObject@1100525002 : Record 11071691;
                                                            BEGIN
                                                              IF "Service Contract No." <> '' THEN BEGIN
                                                                ContractObject.SETRANGE("Service Contract No.", "Service Contract No.");
                                                                IF ContractObject.FINDSET THEN
                                                                  REPEAT
                                                                    ServiceObject."No." := ContractObject."Service Object No.";
                                                                    ServiceObject.MARK(TRUE);
                                                                  UNTIL ContractObject.NEXT = 0;
                                                                ServiceObject.MARKEDONLY(TRUE);
                                                              END ELSE
                                                                IF "Service Location No." <> '' THEN BEGIN
                                                                  ServiceLocation.GET("Service Location No.");
                                                                  ServiceLocation.GetRelatedObjects(ServiceObject);
                                                                END;
                                                              IF "Object No." <> '' THEN
                                                                IF ServiceObject.GET("Object No.") THEN;
                                                              IF PAGE.RUNMODAL(0, ServiceObject) = ACTION::LookupOK THEN
                                                                VALIDATE("Object No.", ServiceObject."No.");
                                                            END;

                                                   CaptionML=[ENU=Object No.;
                                                              NOR=Objektnr.;
                                                              SVE=Objektnr] }
    { 121 ;   ;Object Description  ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object.Description WHERE (No.=FIELD(Object No.)));
                                                   CaptionML=[ENU=Object Description;
                                                              NOR=Objektbeskrivelse;
                                                              SVE=Objektbeskrivning];
                                                   Editable=No }
    { 122 ;   ;Object Description 2;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Service-Object."Description 2" WHERE (No.=FIELD(Object No.)));
                                                   CaptionML=ENU=Object Description 2;
                                                   Editable=No }
    { 123 ;   ;Standard Object No. ;Code20        ;TableRelation="Standard Object";
                                                   CaptionML=[ENU=Standard Object No.;
                                                              NOR=Standardobjektnr;
                                                              SVE=Standardobjektsnr] }
    { 130 ;   ;Standard Object Description;Text100;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Standard Object".Description WHERE (No.=FIELD(Standard Object No.)));
                                                   CaptionML=[ENU=Standard Object Description;
                                                              NOR=Standard objektsbeskrivelse;
                                                              SVE=Standardobjektsbeskrivning];
                                                   Editable=No }
    { 140 ;   ;Standard Object Description 2;Text50;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Standard Object"."Description 2" WHERE (No.=FIELD(Standard Object No.)));
                                                   CaptionML=[ENU=Standard Object Description 2;
                                                              SVE=Standardobjektsbeskrivning 2];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Service Contract No.,Service Location No.,Standard Object No.,Object No.,Customer No.,Discipline,Trade Speciality,Priority,Reference Period;
                                                   Clustered=Yes }
    {    ;Service Location No.                     }
    {    ;Customer No.                             }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      RefPeriodRec@1100485000 : Record 11020272;
      LicenceCu@1100485001 : Codeunit 11012137;

    PROCEDURE CopyReferenceToServiceContract@1100485000(lvContract@1100485001 : Code[20]);
    VAR
      RefNormRec@1100485000 : Record 11012856;
      RefNormRec2@1100485002 : Record 11012856;
      RefPeriodRec@1100485003 : Record 11020272;
      ContractObject@1100528601 : Record 11071702;
    BEGIN
      IF lvContract = '' THEN BEGIN
        RefPeriodRec.SETRANGE(Type, RefPeriodRec.Type::Common);
        IF RefPeriodRec.FINDSET THEN BEGIN
          REPEAT
            RefNormRec2.INIT;
            RefNormRec2."Reference Period" := RefPeriodRec.Code;
            RefNormRec2."Description Reference Period" := RefPeriodRec.Description;
            RefNormRec2.Norm := RefPeriodRec.Norm;
            RefNormRec2."Type Hours" := RefPeriodRec."Type Hours";
            IF RefNormRec2.INSERT THEN;
          UNTIL RefPeriodRec.NEXT = 0;
        END;
      END ELSE BEGIN
        RefNormRec.SETRANGE("Service Contract No.", '');
        RefNormRec.SETRANGE("Service Location No.", '');
        RefNormRec.SETRANGE("Customer No.", '');
        RefNormRec.SETRANGE("Standard Object No.", '');
        RefNormRec.SETRANGE("Object No.", '');
        IF RefNormRec.FINDSET THEN BEGIN
          REPEAT
            RefNormRec2 := RefNormRec;
            RefNormRec2."Service Contract No." := lvContract;
            IF RefNormRec2.INSERT THEN;
          UNTIL RefNormRec.NEXT = 0;
        END;

        ContractObject.SETRANGE("Service Contract No.", lvContract);
        IF ContractObject.FINDSET THEN
          REPEAT
            ContractObject.CALCFIELDS("Standard Object No.");
            RefNormRec.SETRANGE("Standard Object No.", ContractObject."Standard Object No.");
            IF RefNormRec.FINDSET THEN BEGIN
              REPEAT
                RefNormRec2 := RefNormRec;
                RefNormRec2."Service Contract No." := lvContract;
                RefNormRec2."Object No." := ContractObject."Service Object No.";
                RefNormRec2."Standard Object No." := '';
                IF RefNormRec2.INSERT THEN;
              UNTIL RefNormRec.NEXT = 0;
            END;
          UNTIL ContractObject.NEXT = 0;
      END;
    END;

    PROCEDURE CopyReferenceToCustomer@1100409000(ICustomerNo@1100485001 : Code[20]);
    VAR
      RefNormRec@1100485000 : Record 11012856;
      RefNormRec2@1100485002 : Record 11012856;
    BEGIN
      IF ICustomerNo = '' THEN BEGIN
        CopyReferenceToServiceContract('');
      END ELSE BEGIN
        RefNormRec.SETFILTER("Service Contract No.", '%1', '');
        RefNormRec.SETFILTER("Service Location No.", '%1', '');
        RefNormRec.SETFILTER("Customer No.", '%1', '');
        RefNormRec.SETFILTER("Standard Object No.", '%1', '');
        RefNormRec.SETFILTER("Object No.", '%1', '');
        IF RefNormRec.FIND('-') THEN BEGIN
          REPEAT
            RefNormRec2 := RefNormRec;
            RefNormRec2."Customer No." := ICustomerNo;
            IF RefNormRec2.INSERT THEN;
          UNTIL RefNormRec.NEXT = 0;
        END;
      END;
    END;

    PROCEDURE LookupDiscipline@1210190000();
    VAR
      ContrDisciplineRec@1210190000 : Record 11012881;
      DisciplineRec@1210190001 : Record 11020204;
      TradeSpecRec@1210190002 : Record 11020285;
    BEGIN
      //db, 23-01-12: M30861
      ContrDisciplineRec.SETFILTER("Service Contract No.", "Service Contract No.");
      IF ContrDisciplineRec.FINDSET THEN BEGIN
        REPEAT
          DisciplineRec.Code := ContrDisciplineRec.Discipline;
          DisciplineRec.MARK(TRUE);
        UNTIL ContrDisciplineRec.NEXT = 0;
        DisciplineRec.MARKEDONLY(TRUE);
      END;
      IF PAGE.RUNMODAL(0, DisciplineRec) = ACTION::LookupOK THEN BEGIN
        Discipline := DisciplineRec.Code;
        IF NOT TradeSpecRec.GET (Discipline, "Trade Speciality") THEN
          "Trade Speciality" := '';
      END;
    END;

    PROCEDURE LookupTradeSpec@1210190001();
    VAR
      ContrDisciplineRec@1210190002 : Record 11012881;
      TradeSpecRec@1210190000 : Record 11020285;
    BEGIN
      //db, 23-01-12: M30861
      ContrDisciplineRec.SETFILTER("Service Contract No.", "Service Contract No.");
      ContrDisciplineRec.SETFILTER(Discipline, Discipline);
      IF ContrDisciplineRec.FINDSET THEN BEGIN
        REPEAT
          TradeSpecRec.Discipline := ContrDisciplineRec.Discipline;
          TradeSpecRec.Code := ContrDisciplineRec."Trade Speciality";
          TradeSpecRec.MARK(TRUE);
        UNTIL ContrDisciplineRec.NEXT = 0;
        TradeSpecRec.MARKEDONLY(TRUE);
      END;
      TradeSpecRec.SETRANGE(Discipline, Discipline);
      IF PAGE.RUNMODAL(0, TradeSpecRec) = ACTION::LookupOK THEN BEGIN
        Discipline := TradeSpecRec.Discipline;
        "Trade Speciality" := TradeSpecRec.Code;
      END;
    END;

    BEGIN
    END.
  }
}

