OBJECT Codeunit 11071687 Dispatch Auto Send Documents
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    TableNo=11012746;
    OnRun=BEGIN
            IF NOT SendSingleDocument THEN
              SendDispatchDocuments(Rec)
            ELSE
              SendSingleDispatchDocument(Rec);
          END;

  }
  CODE
  {
    VAR
      TmpErrorMessage@1100528601 : TEMPORARY Record 700;
      SendSingleDocument@1100528600 : Boolean;
      Text000@1100528602 : TextConst 'DEU=Einer oder mehrere Fehler wurden entdeckt. Diese anzeigen?;ENU=One or more errors were found. Show these?;NLD=Een of meer fouten gevonden, wilt u deze zien?';

    LOCAL PROCEDURE SendDispatchDocuments@1100528600(VAR IDocumentProperties@1100528602 : Record 11012746);
    VAR
      DocumentProperties@1100528600 : Record 11012746;
      DispatchAutoSendDocuments@1100528601 : Codeunit 11071687;
    BEGIN
      DocumentProperties.COPY(IDocumentProperties);
      DocumentProperties.SETCURRENTKEY("External Document");
      DocumentProperties.SETRANGE("External Document", TRUE);
      DocumentProperties.SETRANGE("Send to Dispatch", TRUE);
      DocumentProperties.SETRANGE("Sent to Dispatch on", 0DT);
      IF NOT DocumentProperties.FINDSET THEN
        EXIT;
      DispatchAutoSendDocuments.SetSendSingleDocument(TRUE);
      REPEAT
        IF CREATEDATETIME(DocumentProperties."Creation Date", DocumentProperties."Creation Time") <= CURRENTDATETIME - 5 * 60 * 1000 THEN
          IF NOT DispatchAutoSendDocuments.RUN(DocumentProperties) THEN BEGIN
            IF GUIALLOWED THEN
              TmpErrorMessage.LogMessage(DocumentProperties.RECORDID, 0, TmpErrorMessage."Message Type"::Error, GETLASTERRORTEXT);
            CLEARLASTERROR;
          END;
      UNTIL DocumentProperties.NEXT = 0;
      IF GUIALLOWED THEN
        IF TmpErrorMessage.HasErrors(FALSE) THEN
          IF CONFIRM(Text000) THEN
            TmpErrorMessage.ShowErrorMessages(FALSE);
    END;

    LOCAL PROCEDURE SendSingleDispatchDocument@1100528606(IDocumentProperties@1100528600 : Record 11012746);
    VAR
      DispatchInterface@1100528603 : Codeunit 11071685;
      ErrorMessage@1100528602 : Text;
      Success@1100528601 : Boolean;
    BEGIN
      IF IDocumentProperties.IsDocumentPicture THEN
        Success := DispatchInterface.AddPhotoToFile(IDocumentProperties, ErrorMessage)
      ELSE
        Success := DispatchInterface.AddAttachmentToFile(IDocumentProperties, ErrorMessage);
      IF Success THEN
        COMMIT
      ELSE
        IF ErrorMessage <> '' THEN
          ERROR(ErrorMessage);
    END;

    PROCEDURE SetSendSingleDocument@1100528605(ISendSingleDocument@1100528600 : Boolean);
    BEGIN
      SendSingleDocument := ISendSingleDocument;
    END;

    BEGIN
    END.
  }
}

