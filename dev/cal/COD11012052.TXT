OBJECT Codeunit 11012052 Create Invoice Call Del. Obj.
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    TableNo=11012450;
    OnRun=BEGIN
            ComplaintSetupRec.GET();
            SalesHeaderRec.INIT;
            IF "Inquiry Costs" < 0 THEN BEGIN
              SalesHeaderRec."Document Type" := SalesHeaderRec."Document Type"::"Credit Memo";
              ComplaintSetupRec.TESTFIELD("Credit Memo Nos.");
              ComplaintSetupRec.TESTFIELD("Posted Credit Memo Nos.");
              SalesHeaderRec."No. Series" := ComplaintSetupRec."Credit Memo Nos.";
              SalesHeaderRec."Posting No. Series" := ComplaintSetupRec."Posted Credit Memo Nos.";
            END ELSE BEGIN
              SalesHeaderRec."Document Type" := SalesHeaderRec."Document Type"::Invoice;
              ComplaintSetupRec.TESTFIELD("Invoice Nos.");
              ComplaintSetupRec.TESTFIELD("Posted Invoice Nos.");
              SalesHeaderRec."No. Series" := ComplaintSetupRec."Invoice Nos.";
              SalesHeaderRec."Posting No. Series" := ComplaintSetupRec."Posted Invoice Nos.";
            END;
            SalesHeaderRec."No." := NoSeriesMngmntCU.GetNextNo(SalesHeaderRec."No. Series",WORKDATE,TRUE);

            ContBusRelRec.INIT;
            ContBusRelRec.SETRANGE("Link to Table", ContBusRelRec."Link to Table"::Customer);
            ContBusRelRec.SETRANGE("Contact No.", "Bill to Contact");
            ContBusRelRec.FIND('-');

            SalesHeaderRec.INSERT(TRUE);
            SalesHeaderRec.VALIDATE("Sell-to Customer No.",ContBusRelRec."No.");
            SalesHeaderRec.MODIFY(FALSE);

            SalesLineNo := 10000;
            InsertSalesLine(Text001,"Inquiry Costs");

            IF "Inquiry Costs" >= 0 THEN BEGIN
              "Sales Invoice" := SalesHeaderRec."No.";
              IF "Sales Invoice" <> '' THEN
                MESSAGE(Text002,"Sales Invoice");
            END ELSE BEGIN
              "Credit Memo" := SalesHeaderRec."No.";
              IF "Credit Memo" <> '' THEN
                MESSAGE(Text003,"Credit Memo");
            END;
            MODIFY;
          END;

  }
  CODE
  {
    VAR
      ComplaintSetupRec@1210190003 : Record 11012108;
      SalesHeaderRec@1210190000 : Record 36;
      SalesLineRec@1210190001 : Record 37;
      ContBusRelRec@1210190006 : Record 5054;
      NoSeriesMngmntCU@1210190004 : Codeunit 396;
      SalesLineNo@1210190002 : Integer;
      Text001@1210190005 : TextConst 'DEU=Untersuchungskosten;ENU=Inquiry Costs;NLD=Onderzoekskosten;NOR=Kostnader for Henvendelse;SVE=Kostnader f”r f”rfr†gan';
      Text002@1100485000 : TextConst 'DEU=Rechnung: %1 erstellt.;ENU=Invoice: %1 generated.;NLD=Factuur: %1 aangemaakt.;NOR=Faktura: %1 generert;SVE=Faktura: %1 har genererats.';
      Text003@1100485001 : TextConst 'DEU=Gutschrift: %1 erstellt.;ENU=Credit Memo: %1 generated.;NLD=Creditnota: %1 aangemaakt.;NOR=Kreditnota: %1 generert;SVE=Kreditnota: %1 har genererats.';

    PROCEDURE InsertSalesLine@5(Descr@11012000 : Text[100];UnitPriceLCY@11012002 : Decimal);
    BEGIN
      SalesLineRec.INIT;
      SalesLineRec."Document Type" := SalesHeaderRec."Document Type";
      SalesLineRec."Document No." := SalesHeaderRec."No.";
      SalesLineRec."Line No." := SalesLineNo;
      SalesLineRec.Type := SalesLineRec.Type::"G/L Account";
      // SalesLineRec."System-Created Entry" := TRUE;
      SalesLineRec."Project Invoice" := FALSE;
      SalesLineRec."Installment Invoice" := FALSE;
      SalesLineRec."Installment Motivation" := 0D;
      SalesLineRec.VALIDATE("Sell-to Customer No.", ContBusRelRec."No.");
      SalesLineRec.VALIDATE("No.", ComplaintSetupRec."General Ledger No.");
      SalesLineRec.VALIDATE(Quantity, 1);
      IF SalesHeaderRec."Document Type" = SalesHeaderRec."Document Type"::"Credit Memo" THEN
        SalesLineRec.VALIDATE("Unit Price",  -1 * UnitPriceLCY)
      ELSE
        SalesLineRec.VALIDATE("Unit Price", UnitPriceLCY);

      SalesLineRec."Amount (LCY)" := UnitPriceLCY * SalesLineRec.Quantity;
      SalesLineRec.Description := Descr;
      SalesLineRec."Description 2" := '';
      SalesLineRec.INSERT;
      SalesLineNo := SalesLineNo + 10000;
    END;

    BEGIN
    END.
  }
}

