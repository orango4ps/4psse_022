OBJECT Page 6085638 Continia User Permissions
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=DCW16.00.00.5.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Continia-Benutzerrechte;
               ENU=User Approval Permissions;
               NLD=Continia gebruikersrechten];
    LinksAllowed=No;
    SourceTable=Table6085634;
    DataCaptionExpr=GetDataCaptionExpr;
    PageType=List;
    OnOpenPage=BEGIN
                 CHANGECOMPANY(GetCurrCompanyName);
               END;

    OnAfterGetRecord=BEGIN
                       NewRecord := FALSE;
                       AssigningPermission := "Assigning Permission";
                       ApprovalPermission := "Approval Permission";

                       SetControlAppearance;
                     END;

    OnNewRecord=BEGIN
                  NewRecord := TRUE;

                  "Approval User Group Code" := '';
                  AssigningPermission := AssigningPermission::" ";
                  ApprovalPermission := ApprovalPermission::" ";
                END;

    OnInsertRecord=BEGIN
                     NewRecord := FALSE;

                     IF GetCurrCompanyName <> COMPANYNAME THEN
                       SetCurrCompanyNameTbl(GetCurrCompanyName);

                     CurrPage.UPDATE(FALSE);
                   END;

    OnModifyRecord=VAR
                     ContiniaUserPermission@1160040000 : Record 6085634;
                   BEGIN
                     ContiniaUserPermission.CHANGECOMPANY(GetCurrCompanyName);
                     ContiniaUserPermission.SetCurrCompanyNameTbl(GetCurrCompanyName);
                     ContiniaUserPermission.GET("Continia User ID",Type,"Dimension Code","Approval User Group Code");
                     ContiniaUserPermission."Assigning Permission" := "Assigning Permission";
                     ContiniaUserPermission."Approval Permission" := "Approval Permission";
                     ContiniaUserPermission."Assigning Filter" := "Assigning Filter";
                     ContiniaUserPermission."Approval Filter" := "Approval Filter";
                     ContiniaUserPermission.MODIFY;

                     EXIT(FALSE);
                   END;

    OnDeleteRecord=BEGIN
                     SetCurrCompanyNameTbl(GetCurrCompanyName);
                   END;

    ActionList=ACTIONS
    {
      { 6085575 ;    ;ActionContainer;
                      Name=ActionItems;
                      CaptionML=;
                      ActionContainerType=ActionItems }
      { 6085574 ;1   ;Action    ;
                      Name=SelectAssigningPermission;
                      CaptionML=[DEU=Zuordnungsberechtigungen ausw„hlen;
                                 ENU=Select Assigning Permissions;
                                 NLD=Selecteer toekenningsrechten];
                      Promoted=Yes;
                      Enabled=EnableAssignPermission;
                      PromotedIsBig=Yes;
                      Image=GetLines;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 DrillDownSelection(GetCurrCompanyName,PermissionType::"Assigning Permission");
                                 CurrPage.UPDATE;
                               END;
                                }
      { 6085573 ;1   ;Action    ;
                      Name=SelectApprovalsPermission;
                      CaptionML=[DEU=Genehmigungsberechtigungen ausw„hlen;
                                 ENU=Select Approvals Permissions;
                                 NLD=Selecteer goedkeuringsrechten];
                      Promoted=Yes;
                      Enabled=EnableApprovalPermission;
                      PromotedIsBig=Yes;
                      Image=GetLines;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 DrillDownSelection(GetCurrCompanyName,PermissionType::"Approval Permission");
                                 CurrPage.UPDATE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1160040000;;Container;
                Name=ContentArea;
                ContainerType=ContentArea }

    { 1160040001;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1160040002;2;Field  ;
                Name=Type;
                SourceExpr=Type;
                OnValidate=BEGIN
                             IF NOT NewRecord THEN
                               ERROR(RenameErr,TABLECAPTION);
                           END;
                            }

    { 1160040007;2;Field  ;
                Name=Dimension Code;
                SourceExpr="Dimension Code";
                OnValidate=BEGIN
                             IF NOT NewRecord THEN
                               ERROR(RenameErr,TABLECAPTION);
                           END;

                OnLookup=BEGIN
                           EXIT(LookUpDimCode(GetCurrCompanyName,Text));
                         END;
                          }

    { 1160040011;2;Field  ;
                Name=Approval User Group Code;
                Lookup=Yes;
                DrillDown=Yes;
                SourceExpr="Approval User Group Code";
                Editable=FALSE;
                OnLookup=BEGIN
                           EXIT(LookUpAppUserGrpCode(GetCurrCompanyName,Text,FALSE));
                         END;
                          }

    { 1160040005;2;Field  ;
                Name=Assigning Permission;
                CaptionML=[DEU=Berechtigungen zuweisen;
                           ENU=Assigning Permission;
                           NLD=Rechten toekennen];
                OptionCaptionML=[DEU=" ,Alle,Inklusiv Selektierte,Exklusiv Selektierte,Filter";
                                 ENU=" ,All,Include Selected,Exclude Selected,Filter";
                                 NLD=" ,Alle,Inclusief geselecteerde,Exclusief geselecteerde,Filter"];
                SourceExpr=AssigningPermission;
                OnValidate=VAR
                             ContiniaUserPermission@1160040000 : Record 6085634;
                           BEGIN
                             "Assigning Permission" := AssigningPermission;

                             ContiniaUserPermission.CHANGECOMPANY(GetCurrCompanyName);
                             ContiniaUserPermission.SetCurrCompanyNameTbl(GetCurrCompanyName);
                             ContiniaUserPermission.GET("Continia User ID",Type,"Dimension Code","Approval User Group Code");
                             ContiniaUserPermission."Approval Permission" := "Approval Permission";
                             ContiniaUserPermission."Assigning Filter" := "Assigning Filter";
                             ContiniaUserPermission."Approval Filter" := "Approval Filter";
                             ContiniaUserPermission.VALIDATE("Assigning Permission",AssigningPermission);
                             ContiniaUserPermission.MODIFY;
                             CurrPage.UPDATE(FALSE);

                             SetControlAppearance;
                           END;
                            }

    { 1160040012;2;Field  ;
                Name=Approval Permission;
                CaptionML=[DEU=Genehmigungsberechtigung;
                           ENU=Approval Permission;
                           NLD=Goedkeuringstoestemming];
                OptionCaptionML=[DEU=" ,Alle,Inklusiv Selektierte,Exklusiv Selektierte,Filter,Gleich wie Zuweisung";
                                 ENU=" ,All,Include Selected,Exclude Selected,Filter,Same as Assigning";
                                 NLD=" ,Alle,Inclusief geselecteerde,Exclusief geselecteerde,Filter,Zelfde als toekenning"];
                SourceExpr=ApprovalPermission;
                OnValidate=VAR
                             ContiniaUserPermission@1160040000 : Record 6085634;
                           BEGIN
                             "Approval Permission" := ApprovalPermission;

                             ContiniaUserPermission.CHANGECOMPANY(GetCurrCompanyName);
                             ContiniaUserPermission.SetCurrCompanyNameTbl(GetCurrCompanyName);
                             ContiniaUserPermission.GET("Continia User ID",Type,"Dimension Code","Approval User Group Code");
                             ContiniaUserPermission."Assigning Permission" := "Assigning Permission";
                             ContiniaUserPermission."Assigning Filter" := "Assigning Filter";
                             ContiniaUserPermission."Approval Filter" := "Approval Filter";
                             ContiniaUserPermission.VALIDATE("Approval Permission",ApprovalPermission);
                             ContiniaUserPermission.MODIFY;
                             CurrPage.UPDATE(FALSE);

                             SetControlAppearance;
                           END;
                            }

    { 1160040006;2;Field  ;
                Name=Assigning Filter;
                SourceExpr="Assigning Filter";
                OnLookup=BEGIN
                           EXIT(LookUpPmsFilter(GetCurrCompanyName,PermissionType::"Assigning Permission",Text));
                         END;
                          }

    { 1160040008;2;Field  ;
                Name=Approval Filter;
                SourceExpr="Approval Filter";
                OnLookup=BEGIN
                           EXIT(LookUpPmsFilter(GetCurrCompanyName,PermissionType::"Approval Permission",Text));
                         END;
                          }

    { 1160040009;2;Field  ;
                Name=No. of Assigning Selections;
                SourceExpr="No. of Assigning Selections";
                OnDrillDown=BEGIN
                              DrillDownSelection(GetCurrCompanyName,PermissionType::"Assigning Permission");
                              CurrPage.UPDATE;
                            END;
                             }

    { 1160040010;2;Field  ;
                Name=No. of Approval Selections;
                SourceExpr="No. of Approval Selections";
                OnDrillDown=BEGIN
                              DrillDownSelection(GetCurrCompanyName,PermissionType::"Approval Permission");
                              CurrPage.UPDATE;
                            END;
                             }

  }
  CODE
  {
    VAR
      PermissionType@1160040000 : 'Assigning Permission,Approval Permission';
      CurrCompanyName@1160040001 : Text[30];
      ApprovalPermission@1160040003 : ' ,All,Include Selected,Exclude Selected,Filter,Same as Assigning';
      AssigningPermission@1160040004 : ' ,All,Include Selected,Exclude Selected,Filter';
      SkipPageModifyTrigger@1160040005 : Boolean;
      NewRecord@1160040006 : Boolean;
      RenameErr@1160040007 : TextConst 'DEU=Sie k”nnen eine %1 nicht umbenennen.;ENU=You cannot rename a %1.;NLD=U kunt een %1 niet hernoemen.';
      EnableApprovalPermission@1160040002 : Boolean INDATASET;
      EnableAssignPermission@6085573 : Boolean INDATASET;

    PROCEDURE SetCurrCompanyName@1160040019(NewCompanyName@1160040000 : Text[30]);
    BEGIN
      CurrCompanyName := NewCompanyName;
    END;

    PROCEDURE GetCurrCompanyName@1160040024() : Text[30];
    BEGIN
      IF CurrCompanyName = '' THEN
        EXIT(COMPANYNAME)
      ELSE
        EXIT(CurrCompanyName);
    END;

    PROCEDURE GetDataCaptionExpr@1160040003() : Text[150];
    VAR
      PermissionSelectionMgt@1160040000 : Codeunit 6085631;
    BEGIN
      EXIT(PermissionSelectionMgt.GetUserCaptionExpr(GetCurrCompanyName,"Continia User ID"));
    END;

    LOCAL PROCEDURE SetControlAppearance@6085573();
    BEGIN
      EnableAssignPermission := FALSE;
      EnableApprovalPermission := FALSE;
      IF "Assigning Permission" IN ["Assigning Permission"::"Exclude Selected","Assigning Permission"::"Include Selected"] THEN
        EnableAssignPermission := TRUE;

      IF "Approval Permission" IN ["Approval Permission"::"Exclude Selected","Approval Permission"::"Include Selected"] THEN
        EnableApprovalPermission := TRUE;
    END;

    BEGIN
    END.
  }
}

