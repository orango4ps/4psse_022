OBJECT Report 7315 Calculate Whse. Adjustment
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.05;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Ausgleich berechnen;
               ENU=Calculate Warehouse Adjustment;
               NLD=Magazijnherwaardering berekenen;
               NOR=Beregn lagerjustering];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 8129;    ;DataItem;                    ;
               DataItemTable=Table27;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=VAR
                               ItemJnlTemplate@1000 : Record 82;
                               ItemJnlBatch@1001 : Record 233;
                             BEGIN
                               IF PostingDate = 0D THEN
                                 ERROR(Text000);

                               ItemJnlTemplate.GET(ItemJnlLine."Journal Template Name");
                               ItemJnlBatch.GET(ItemJnlLine."Journal Template Name",ItemJnlLine."Journal Batch Name");
                               IF NextDocNo = '' THEN BEGIN
                                 IF ItemJnlBatch."No. Series" <> '' THEN BEGIN
                                   ItemJnlLine.SETRANGE("Journal Template Name",ItemJnlLine."Journal Template Name");
                                   ItemJnlLine.SETRANGE("Journal Batch Name",ItemJnlLine."Journal Batch Name");
                                   IF NOT ItemJnlLine.FIND('-') THEN
                                     NextDocNo := NoSeriesMgt.GetNextNo(ItemJnlBatch."No. Series",PostingDate,FALSE);
                                   ItemJnlLine.INIT;
                                 END;
                                 IF NextDocNo = '' THEN
                                   ERROR(Text001);
                               END;

                               NextLineNo := 0;

                               IF NOT HideValidationDialog THEN
                                 Window.OPEN(Text002,"No.");
                             END;

               OnAfterGetRecord=BEGIN
                                  IF NOT HideValidationDialog THEN
                                    Window.UPDATE;
                                END;

               OnPostDataItem=BEGIN
                                IF NOT HideValidationDialog THEN
                                  Window.CLOSE;
                              END;

               ReqFilterFields=No.,Location Filter,Variant Filter }

    { 5444;1   ;DataItem;                    ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number)
                                 WHERE(Number=CONST(1));
               OnPreDataItem=BEGIN
                               CLEAR(Location);
                               WhseEntry.RESET;
                               WhseEntry.SETCURRENTKEY("Item No.","Bin Code","Location Code","Variant Code");
                               WhseEntry.SETRANGE("Item No.",Item."No.");
                               Item.COPYFILTER("Variant Filter",WhseEntry."Variant Code");
                               Item.COPYFILTER("Lot No. Filter",WhseEntry."Lot No.");
                               Item.COPYFILTER("Serial No. Filter",WhseEntry."Serial No.");

                               IF WhseEntry.ISEMPTY THEN
                                 CurrReport.BREAK;

                               FillProspectReservationEntryBuffer(Item,ItemJnlLine."Journal Template Name",ItemJnlLine."Journal Batch Name");

                               AdjmtBinQuantityBuffer.RESET;
                               AdjmtBinQuantityBuffer.DELETEALL;
                             END;

               OnAfterGetRecord=VAR
                                  ReservationEntry@1000 : Record 337;
                                  SNLotNumbersByBin@1006 : Query 7300;
                                BEGIN
                                  WITH AdjmtBinQuantityBuffer DO BEGIN
                                    Location.RESET;
                                    Item.COPYFILTER("Location Filter",Location.Code);
                                    Location.SETRANGE("Directed Put-away and Pick",TRUE);
                                    IF Location.FINDSET THEN
                                      REPEAT
                                        SNLotNumbersByBin.SETRANGE(Location_Code,Location.Code);
                                        SNLotNumbersByBin.SETRANGE(Bin_Code,Location."Adjustment Bin Code");
                                        SNLotNumbersByBin.SETRANGE(Item_No,Item."No.");
                                        SNLotNumbersByBin.SETFILTER(Variant_Code,Item.GETFILTER("Variant Filter"));
                                        SNLotNumbersByBin.SETFILTER(Lot_No,Item.GETFILTER("Lot No. Filter"));
                                        SNLotNumbersByBin.SETFILTER(Serial_No,Item.GETFILTER("Serial No. Filter"));
                                        SNLotNumbersByBin.OPEN;

                                        WHILE SNLotNumbersByBin.READ DO BEGIN
                                          INIT;
                                          "Item No." := SNLotNumbersByBin.Item_No;
                                          "Variant Code" := SNLotNumbersByBin.Variant_Code;
                                          "Location Code" := SNLotNumbersByBin.Location_Code;
                                          "Bin Code" := SNLotNumbersByBin.Bin_Code;
                                          "Unit of Measure Code" := SNLotNumbersByBin.Unit_of_Measure_Code;
                                          "Base Unit of Measure" := Item."Base Unit of Measure";
                                          "Lot No." := SNLotNumbersByBin.Lot_No;
                                          "Serial No." := SNLotNumbersByBin.Serial_No;
                                          "Qty. to Handle (Base)" := SNLotNumbersByBin.Sum_Qty_Base;
                                          OnBeforeAdjmtBinQuantityBufferInsert(AdjmtBinQuantityBuffer,WhseEntry);
                                          INSERT;
                                        END;
                                      UNTIL Location.NEXT = 0;

                                    RESET;
                                    ReservationEntry.RESET;
                                    ReservationEntry.SETCURRENTKEY("Source ID");
                                    ItemJnlLine.RESET;
                                    ItemJnlLine.SETCURRENTKEY("Item No.");
                                    IF FINDSET THEN BEGIN
                                      REPEAT
                                        ItemJnlLine.RESET;
                                        ItemJnlLine.SETCURRENTKEY("Item No.");
                                        ItemJnlLine.SETRANGE("Journal Template Name",ItemJnlLine."Journal Template Name");
                                        ItemJnlLine.SETRANGE("Journal Batch Name",ItemJnlLine."Journal Batch Name");
                                        ItemJnlLine.SETRANGE("Item No.","Item No.");
                                        ItemJnlLine.SETRANGE("Location Code","Location Code");
                                        ItemJnlLine.SETRANGE("Unit of Measure Code","Unit of Measure Code");
                                        ItemJnlLine.SETRANGE("Warehouse Adjustment",TRUE);
                                        IF ItemJnlLine.FINDSET THEN
                                          REPEAT
                                            ReservationEntry.SETRANGE("Source Type",DATABASE::"Item Journal Line");
                                            ReservationEntry.SETRANGE("Source ID",ItemJnlLine."Journal Template Name");
                                            ReservationEntry.SETRANGE("Source Batch Name",ItemJnlLine."Journal Batch Name");
                                            ReservationEntry.SETRANGE("Source Ref. No.",ItemJnlLine."Line No.");
                                            IF "Lot No." <> '' THEN
                                              ReservationEntry.SETRANGE("Lot No.","Lot No.");
                                            IF "Serial No." <> '' THEN
                                              ReservationEntry.SETRANGE("Serial No.","Serial No.");
                                            ReservationEntry.CALCSUMS("Qty. to Handle (Base)");
                                            IF ReservationEntry."Qty. to Handle (Base)" <> 0 THEN BEGIN
                                              "Qty. to Handle (Base)" += ReservationEntry."Qty. to Handle (Base)";
                                              MODIFY;
                                            END;
                                          UNTIL ItemJnlLine.NEXT = 0;
                                      UNTIL NEXT = 0;
                                    END;
                                  END;
                                END;

               OnPostDataItem=VAR
                                QtyInUOM@1000 : Decimal;
                              BEGIN
                                WITH AdjmtBinQuantityBuffer DO BEGIN
                                  RESET;
                                  IF FINDSET THEN
                                    REPEAT
                                      SETRANGE("Location Code","Location Code");
                                      SETRANGE("Variant Code","Variant Code");
                                      SETRANGE("Unit of Measure Code","Unit of Measure Code");

                                      SETFILTER("Qty. to Handle (Base)",'>0');
                                      CALCSUMS("Qty. to Handle (Base)");
                                      QtyInUOM := UOMMgt.CalcQtyFromBase(-"Qty. to Handle (Base)",UOMMgt.GetQtyPerUnitOfMeasure(Item,"Unit of Measure Code"));
                                      IF QtyInUOM <> 0 THEN
                                        InsertItemJnlLine(AdjmtBinQuantityBuffer,QtyInUOM,-"Qty. to Handle (Base)","Unit of Measure Code",1);

                                      SETFILTER("Qty. to Handle (Base)",'<0');
                                      CALCSUMS("Qty. to Handle (Base)");
                                      QtyInUOM := UOMMgt.CalcQtyFromBase(-"Qty. to Handle (Base)",UOMMgt.GetQtyPerUnitOfMeasure(Item,"Unit of Measure Code"));
                                      IF QtyInUOM <> 0 THEN
                                        InsertItemJnlLine(AdjmtBinQuantityBuffer,QtyInUOM,-"Qty. to Handle (Base)","Unit of Measure Code",0);

                                      // rounding residue
                                      SETRANGE("Qty. to Handle (Base)");
                                      CALCSUMS("Qty. to Handle (Base)");
                                      QtyInUOM := UOMMgt.CalcQtyFromBase(-"Qty. to Handle (Base)",UOMMgt.GetQtyPerUnitOfMeasure(Item,"Unit of Measure Code"));
                                      IF (QtyInUOM = 0) AND ("Qty. to Handle (Base)" > 0) THEN
                                        InsertItemJnlLine(AdjmtBinQuantityBuffer,-"Qty. to Handle (Base)",-"Qty. to Handle (Base)","Base Unit of Measure",1);

                                      FINDLAST;
                                      SETRANGE("Location Code");
                                      SETRANGE("Variant Code");
                                      SETRANGE("Unit of Measure Code");
                                    UNTIL NEXT = 0;
                                  RESET;
                                  DELETEALL;
                                END;
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      CaptionML=[DEU=Lagerbestand berechnen;
                 ENU=Calculate Inventory;
                 NLD=Voorraad berekenen;
                 NOR=Beregn beholdning];
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF PostingDate = 0D THEN
                     PostingDate := WORKDATE;
                   ValidatePostingDate;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 1   ;2   ;Field     ;
                  CaptionML=[DEU=Buchungsdatum;
                             ENU=Posting Date;
                             NLD=Boekingsdatum;
                             NOR=Bokf›ringsdato;
                             SVE=Bokf”ringsdatum];
                  ToolTipML=[DEU=Gibt das Datum der Buchung dieser Stapelverarbeitung an. Das Programm tr„gt automatisch das Arbeitsdatum in dieses Feld ein, Sie k”nnen es jedoch „ndern.;
                             ENU=Specifies the date for the posting of this batch job. The program automatically enters the work date in this field, but you can change it.;
                             NLD=Hiermee wordt de boekingsdatum voor deze batchtaak opgegeven. In dit veld wordt automatisch de werkdatum ingevuld, maar deze kan desgewenst worden gewijzigd.;
                             NOR=Angir datoen for bokf›ring av denne kj›rselen. Programmet angir automatisk arbeidsdatoen i dette feltet, men du kan endre den.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=PostingDate;
                  OnValidate=BEGIN
                               ValidatePostingDate;
                             END;
                              }

      { 9   ;2   ;Field     ;
                  CaptionML=[DEU=Belegnr.;
                             ENU=Document No.;
                             NLD=Documentnr.;
                             NOR=Bilagsnr.;
                             SVE=Dokumentnr];
                  ToolTipML=[DEU=Gibt eine manuell eingegebene Belegnummer an, die in das Feld "Belegnr." der von der Stapelverarbeitung erzeugten Buch.-Blattzeilen eingetragen wird.;
                             ENU=Specifies a manually entered document number that will be entered in the Document No. field, on the journal lines created by the batch job.;
                             NLD=Hiermee wordt een handmatig ingevoerd documentnummer opgegeven dat in het veld Documentnr. wordt ingevoerd op de dagboekregels die tijdens de batchverwerking worden gemaakt.;
                             NOR=Angir et manuelt angitt dokumentnummer som skal angis i feltet Dokumentnr. p† kladdelinjene som ble opprettet av kj›rselen.];
                  ApplicationArea=#Warehouse;
                  SourceExpr=NextDocNo }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Geben Sie das Buchungsdatum ein.;ENU=Enter the posting date.;NLD=Voer de boekingdatum opnieuw in.;NOR=Angi bokf›ringsdatoen.;SVE=Ange bokf”ringsdatum.';
      Text001@1001 : TextConst 'DEU=Geben Sie die Belegnummer ein.;ENU=Enter the document no.;NLD=Voer een documentnummer in.;NOR=Angi bilagsnummer.;SVE=Ange dokumentnr';
      Text002@1002 : TextConst 'DEU=Artikel werden durchgelesen  #1##########;ENU=Processing items    #1##########;NLD=Verwerken art.      #1##########;NOR=Behandler varer     #1##########;SVE=Behandlar artiklar  #1##########';
      ItemJnlBatch@1004 : Record 233;
      ItemJnlLine@1005 : Record 83;
      WhseEntry@1006 : Record 7312;
      Location@1031 : Record 14;
      SourceCodeSetup@1008 : Record 242;
      AdjmtBinQuantityBuffer@1003 : TEMPORARY Record 7330;
      TempReservationEntryBuffer@1011 : TEMPORARY Record 337;
      NoSeriesMgt@1017 : Codeunit 396;
      UOMMgt@1010 : Codeunit 5402;
      Window@1019 : Dialog;
      PostingDate@1020 : Date;
      NextDocNo@1021 : Code[20];
      NextLineNo@1023 : Integer;
      HideValidationDialog@1028 : Boolean;

    [External]
    PROCEDURE SetItemJnlLine@1(VAR NewItemJnlLine@1000 : Record 83);
    BEGIN
      ItemJnlLine := NewItemJnlLine;
    END;

    LOCAL PROCEDURE ValidatePostingDate@4();
    BEGIN
      ItemJnlBatch.GET(ItemJnlLine."Journal Template Name",ItemJnlLine."Journal Batch Name");
      IF ItemJnlBatch."No. Series" = '' THEN
        NextDocNo := ''
      ELSE BEGIN
        NextDocNo := NoSeriesMgt.GetNextNo(ItemJnlBatch."No. Series",PostingDate,FALSE);
        CLEAR(NoSeriesMgt);
      END;
    END;

    LOCAL PROCEDURE InsertItemJnlLine@6(VAR TempBinContentBuffer@1004 : TEMPORARY Record 7330;Quantity2@1002 : Decimal;QuantityBase2@1005 : Decimal;UOM2@1009 : Code[10];EntryType2@1011 : 'Negative Adjmt.,Positive Adjmt.');
    BEGIN
      OnBeforeFunctionInsertItemJnlLine(
        TempBinContentBuffer."Item No.",TempBinContentBuffer."Variant Code",TempBinContentBuffer."Location Code",
        Quantity2,QuantityBase2,UOM2,EntryType2);

      WITH ItemJnlLine DO BEGIN
        IF NextLineNo = 0 THEN BEGIN
          LOCKTABLE;
          RESET;
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          IF FIND('+') THEN
            NextLineNo := "Line No.";

          SourceCodeSetup.GET;
        END;
        NextLineNo := NextLineNo + 10000;

        IF QuantityBase2 <> 0 THEN BEGIN
          INIT;
          "Line No." := NextLineNo;
          VALIDATE("Posting Date",PostingDate);
          IF QuantityBase2 > 0 THEN
            VALIDATE("Entry Type","Entry Type"::"Positive Adjmt.")
          ELSE BEGIN
            VALIDATE("Entry Type","Entry Type"::"Negative Adjmt.");
            Quantity2 := -Quantity2;
            QuantityBase2 := -QuantityBase2;
          END;
          VALIDATE("Document No.",NextDocNo);
          VALIDATE("Item No.",TempBinContentBuffer."Item No.");
          VALIDATE("Variant Code",TempBinContentBuffer."Variant Code");
          VALIDATE("Location Code",TempBinContentBuffer."Location Code");
          VALIDATE("Source Code",SourceCodeSetup."Item Journal");
          VALIDATE("Unit of Measure Code",UOM2);
          "Posting No. Series" := ItemJnlBatch."Posting No. Series";

          VALIDATE(Quantity,Quantity2);
          "Quantity (Base)" := QuantityBase2;
          "Invoiced Qty. (Base)" := QuantityBase2;
          "Warehouse Adjustment" := TRUE;
          INSERT(TRUE);
          OnAfterInsertItemJnlLine(ItemJnlLine);

          CreateReservationEntry(ItemJnlLine,TempBinContentBuffer,EntryType2,UOM2);
        END;
      END;

      OnAfterFunctionInsertItemJnlLine(
        TempBinContentBuffer."Item No.",TempBinContentBuffer."Variant Code",TempBinContentBuffer."Location Code",
        Quantity2,QuantityBase2,UOM2,EntryType2,ItemJnlLine);
    END;

    [External]
    PROCEDURE InitializeRequest@2(NewPostingDate@1000 : Date;DocNo@1001 : Code[20]);
    BEGIN
      PostingDate := NewPostingDate;
      NextDocNo := DocNo;
    END;

    [External]
    PROCEDURE SetHideValidationDialog@7380(NewHideValidationDialog@1000 : Boolean);
    BEGIN
      HideValidationDialog := NewHideValidationDialog;
    END;

    LOCAL PROCEDURE FillProspectReservationEntryBuffer@25(VAR Item@1003 : Record 27;JournalTemplateName@1000 : Code[10];JournalBatchName@1001 : Code[10]);
    VAR
      ReservationEntry@1002 : Record 337;
    BEGIN
      TempReservationEntryBuffer.RESET;
      TempReservationEntryBuffer.DELETEALL;
      ReservationEntry.RESET;
      ReservationEntry.SETRANGE("Source Type",DATABASE::"Item Journal Line");
      ReservationEntry.SETRANGE("Source ID",JournalTemplateName);
      ReservationEntry.SETRANGE("Source Batch Name",JournalBatchName);
      ReservationEntry.SETRANGE("Reservation Status",ReservationEntry."Reservation Status"::Prospect);
      ReservationEntry.SETRANGE("Item No.",Item."No.");
      ReservationEntry.SETFILTER("Variant Code",Item."Variant Filter");

      IF ReservationEntry.FINDSET THEN
        REPEAT
          TempReservationEntryBuffer := ReservationEntry;
          TempReservationEntryBuffer.INSERT;
        UNTIL ReservationEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE CreateReservationEntry@35(VAR ItemJournalLine@1000 : Record 83;VAR TempBinContentBuffer@1001 : TEMPORARY Record 7330;EntryType@1003 : 'Negative Adjmt.,Positive Adjmt.';UOMCode@1006 : Code[10]);
    VAR
      WarehouseEntry@1005 : Record 7312;
      WarehouseEntry2@1007 : Record 7312;
      CreateReservEntry@1009 : Codeunit 99000830;
      OrderLineNo@1008 : Integer;
    BEGIN
      TempBinContentBuffer.FINDSET;
      REPEAT
        WarehouseEntry.SETCURRENTKEY(
          "Item No.","Bin Code","Location Code","Variant Code","Unit of Measure Code","Lot No.","Serial No.","Entry Type");
        WarehouseEntry.SETRANGE("Item No.",TempBinContentBuffer."Item No.");
        WarehouseEntry.SETRANGE("Bin Code",TempBinContentBuffer."Bin Code");
        WarehouseEntry.SETRANGE("Location Code",TempBinContentBuffer."Location Code");
        WarehouseEntry.SETRANGE("Variant Code",TempBinContentBuffer."Variant Code");
        WarehouseEntry.SETRANGE("Unit of Measure Code",UOMCode);
        WarehouseEntry.SETRANGE("Lot No.",TempBinContentBuffer."Lot No.");
        WarehouseEntry.SETRANGE("Serial No.",TempBinContentBuffer."Serial No.");
        WarehouseEntry.SETFILTER("Entry Type",'%1|%2',EntryType,WarehouseEntry."Entry Type"::Movement);
        IF NOT WarehouseEntry.FINDFIRST THEN
          EXIT;

        TempReservationEntryBuffer.RESET;
        WarehouseEntry.CALCSUMS("Qty. (Base)",Quantity);
        UpdateWarehouseEntryQtyByReservationEntryBuffer(
          WarehouseEntry,WarehouseEntry."Lot No.",WarehouseEntry."Serial No.");

        WarehouseEntry2.COPYFILTERS(WarehouseEntry);
        CASE EntryType OF
          EntryType::"Positive Adjmt.":
            WarehouseEntry2.SETRANGE("Entry Type",WarehouseEntry2."Entry Type"::"Negative Adjmt.");
          EntryType::"Negative Adjmt.":
            WarehouseEntry2.SETRANGE("Entry Type",WarehouseEntry2."Entry Type"::"Positive Adjmt.");
        END;
        WarehouseEntry2.CALCSUMS("Qty. (Base)",Quantity);
        UpdateWarehouseEntryQtyByReservationEntryBuffer(
          WarehouseEntry2,WarehouseEntry."Lot No.",WarehouseEntry."Serial No.");

        IF ABS(WarehouseEntry2."Qty. (Base)") > ABS(WarehouseEntry."Qty. (Base)") THEN BEGIN
          WarehouseEntry."Qty. (Base)" := 0;
          WarehouseEntry.Quantity := 0;
        END ELSE BEGIN
          WarehouseEntry."Qty. (Base)" += WarehouseEntry2."Qty. (Base)";
          WarehouseEntry.Quantity += WarehouseEntry2.Quantity;
        END;

        IF WarehouseEntry."Qty. (Base)" <> 0 THEN BEGIN
          IF ItemJournalLine."Order Type" = ItemJournalLine."Order Type"::Production THEN
            OrderLineNo := ItemJournalLine."Order Line No.";

          OnBeforeCreateReservEntryFor(ItemJournalLine);

          CreateReservEntry.CreateReservEntryFor(
            DATABASE::"Item Journal Line",ItemJournalLine."Entry Type",ItemJournalLine."Journal Template Name",
            ItemJournalLine."Journal Batch Name",OrderLineNo,ItemJournalLine."Line No.",ItemJournalLine."Qty. per Unit of Measure",
            ABS(WarehouseEntry.Quantity),ABS(WarehouseEntry."Qty. (Base)"),
            WarehouseEntry."Serial No.",WarehouseEntry."Lot No.");

          IF WarehouseEntry."Qty. (Base)" < 0 THEN
            CreateReservEntry.SetDates(WarehouseEntry."Warranty Date",WarehouseEntry."Expiration Date");

          CreateReservEntry.CreateEntry(
            ItemJournalLine."Item No.",ItemJournalLine."Variant Code",ItemJournalLine."Location Code",ItemJournalLine.Description,
            0D,0D,0,TempReservationEntryBuffer."Reservation Status"::Prospect);
        END;
      UNTIL TempBinContentBuffer.NEXT = 0;
    END;

    LOCAL PROCEDURE UpdateWarehouseEntryQtyByReservationEntryBuffer@45(VAR WarehouseEntry@1000 : Record 7312;LotNo@1001 : Code[50];SerialNo@1002 : Code[50]);
    BEGIN
      IF WarehouseEntry."Qty. (Base)" = 0 THEN
        EXIT;

      IF LotNo <> '' THEN
        TempReservationEntryBuffer.SETRANGE("Lot No.",LotNo);
      IF SerialNo <> '' THEN
        TempReservationEntryBuffer.SETRANGE("Serial No.",SerialNo);

      TempReservationEntryBuffer.SETRANGE(Positive,WarehouseEntry."Qty. (Base)" < 0);
      TempReservationEntryBuffer.CALCSUMS("Quantity (Base)",Quantity);

      WarehouseEntry."Qty. (Base)" += TempReservationEntryBuffer."Quantity (Base)";
      WarehouseEntry.Quantity += TempReservationEntryBuffer.Quantity;
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeFunctionInsertItemJnlLine@7(ItemNo@1007 : Code[20];VariantCode2@1006 : Code[10];LocationCode2@1005 : Code[10];Quantity2@1004 : Decimal;QuantityBase2@1003 : Decimal;UOM2@1002 : Code[10];EntryType2@1001 : 'Negative Adjmt.,Positive Adjmt.');
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInsertItemJnlLine@1001(VAR ItemJournalLine@1000 : Record 83);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterFunctionInsertItemJnlLine@5(ItemNo@1007 : Code[20];VariantCode2@1006 : Code[10];LocationCode2@1005 : Code[10];Quantity2@1004 : Decimal;QuantityBase2@1003 : Decimal;UOM2@1002 : Code[10];EntryType2@1001 : 'Negative Adjmt.,Positive Adjmt.';VAR ItemJournalLine@1000 : Record 83);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeAdjmtBinQuantityBufferInsert@3(VAR BinContentBuffer@1000 : Record 7330;WarehouseEntry@1001 : Record 7312);
    BEGIN
    END;

    [Integration]
    LOCAL PROCEDURE OnBeforeCreateReservEntryFor@8(VAR ItemJournalLine@1000 : Record 83);
    BEGIN
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

