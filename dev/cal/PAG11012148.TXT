OBJECT Page 11012148 Apply Budget Difference
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    Permissions=TableData 11012263=r;
    CaptionML=[DEU=Ausgleichen Budgetunterschied;
               ENU=Apply Budget Difference;
               NLD=Vereffen begrotingsverschil;
               NOR=Bruk budsjettdifferanse;
               SVE=TillÑmpa budgetdifferens];
    PageType=NavigatePage;
    OnInit=BEGIN
             ElementEditable := TRUE;
             RateFieldEditable := TRUE;
             NoOfLinesToAddEditable := TRUE;
             AmntToApplyEditable := TRUE;
             CompressTypeEditable := TRUE;
           END;

    OnOpenPage=BEGIN
                 gBudgAdjRec.CALCFIELDS("Project Description");
                 NoOfLinesToAdd := 1;
                 IF CostObjectRec.Code <> '' THEN
                   ValidateCostObject;

                 // This MARKEDONLY(TRUE) is needed in page to prevent that all budget lines in the database are updated
                 // when apply button is pressed before SetMarkedBudgetLines is called.
                 MarkedBudgetLine.MARKEDONLY(TRUE); //*C-029097.n Must also be done if the functie SetMarkedBudgetLines is not executed!
                 SetEditable();  //*C-029097.n
               END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1210190020;1 ;Action    ;
                      CaptionML=[DEU=&Anwenden;
                                 ENU=&Apply;
                                 NLD=&Vereffenen;
                                 NOR=&Bruk;
                                 SVE=&TillÑmpa];
                      InFooterBar=Yes;
                      Image=ApplyTemplate;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 ApplyBudgetDifference;  //DP00883
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100525001;1;Field  ;
                CaptionML=[DEU=Ausgleichen Budgetunterschied;
                           ENU=Apply Budget Difference;
                           NLD=Vereffen begrotingsverschil;
                           NOR=Bruk budsjettdifferanse;
                           SVE=TillÑmpa budgetdifferens];
                Style=Strong;
                StyleExpr=TRUE }

    { 1210190002;1;Field  ;
                CaptionML=[DEU=Projektnr.;
                           ENU=Project No.;
                           NLD=Projectnr.;
                           NOR=Prosjektnr;
                           SVE=Projektnr];
                SourceExpr=gBudgAdjRec."Project No.";
                Editable=FALSE }

    { 1210190000;1;Field  ;
                CaptionML=[DEU=Projektbeschreibung;
                           ENU=Project Description;
                           NLD=Projectomschrijving;
                           NOR=Prosjektbeskrivelse;
                           SVE=Projektbeskrivning];
                SourceExpr=gBudgAdjRec."Project Description";
                Visible=false;
                Editable=FALSE }

    { 1210190004;1;Field  ;
                CaptionML=[DEU=Regulierung;
                           ENU=Adjustment;
                           NLD=Correctie;
                           NOR=Justering;
                           SVE=Justering];
                SourceExpr=gBudgAdjRec."No.";
                Editable=FALSE }

    { 1210190001;1;Field  ;
                CaptionML=[DEU=Korrekturbeschreibung;
                           ENU=Adjustment Description;
                           NLD=Correctieomschrijving;
                           NOR=Justering Beskrivelse;
                           SVE=Justering beskrivning];
                SourceExpr=gBudgAdjRec.Description;
                Visible=false;
                Editable=FALSE }

    { 1210190006;1;Field  ;
                Name=AmntToApply;
                CaptionML=[DEU=Auszugleichender Betrag (LCY);
                           ENU=Amount to Apply (LCY);
                           NLD=Te vereffenen bedrag (LV)];
                SourceExpr=AmntToApplyLCY;
                Editable=AmntToApplyEditable;
                OnValidate=BEGIN
                             IF ((MaxAmntToApply >= 0) AND (AmntToApplyLCY > MaxAmntToApply)) OR
                                ((MaxAmntToApply < 0) AND (AmntToApplyLCY < MaxAmntToApply)) THEN
                               ERROR(Text002, MaxAmntToApply);
                             //DP00883
                             IF CurrencyCode = '' THEN
                               AmntToApplyFCY := AmntToApplyLCY
                             ELSE
                               CalculateAmountByCurrency(1);
                           END;
                            }

    { 1100525002;1;Field  ;
                CaptionML=[DEU=Ausgleichsbetrag;
                           ENU=Amount to Apply;
                           NLD=Te vereffenen bedrag;
                           NOR=Belõp som skal utlignes;
                           SVE=Belopp att koppla];
                SourceExpr=AmntToApplyFCY;
                Editable=AmntToApplyEditable;
                OnValidate=BEGIN
                             //DP00883
                             IF CurrencyCode = '' THEN
                               AmntToApplyLCY := AmntToApplyFCY
                             ELSE
                               CalculateAmountByCurrency(0);
                           END;
                            }

    { 1100525003;1;Field  ;
                CaptionML=[DEU=WÑhrungscode;
                           ENU=Currency Code;
                           NLD=Valutacode;
                           NOR=Valutakode;
                           SVE=Valutakod];
                SourceExpr=CurrencyCode;
                OnValidate=BEGIN
                             //DP00883
                             IF CurrencyCode = '' THEN
                               AmntToApplyFCY := AmntToApplyLCY
                             ELSE
                               CalculateAmountByCurrency(1);
                           END;
                            }

    { 1210190023;1;Field  ;
                CaptionML=[DEU=Verdichten;
                           ENU=Compress;
                           NLD=Verdichten;
                           NOR=Komprimer;
                           SVE=Komprimera];
                OptionCaptionML=[DEU=Ja,Nein,Baubeschreibungsposten / Teilposten;
                                 ENU=Yes,No,Tender Paragraph / Part;
                                 NLD=Ja,Nee,Bestekpost / deelpost;
                                 NOR=Ja,Nei,Tilbudsavsnitt/Del;
                                 SVE=Ja,Nej,Anbudsstycke/Del];
                SourceExpr=CompressType;
                Editable=CompressTypeEditable;
                OnValidate=BEGIN
                             //*C-029097
                             ValidateCompressType();
                             SetEditable();
                           END;
                            }

    { 1210190008;1;Field  ;
                Name=CostObject;
                CaptionML=[DEU=KostentrÑger;
                           ENU=Cost Object;
                           NLD=Kostendrager;
                           NOR=Kostnadsobjekt;
                           SVE=Kostnadsobjekt];
                SourceExpr=CostObjectRec.Code;
                OnValidate=VAR
                             lCostObjectRec@1210190001 : Record 349;
                           BEGIN
                             IF CostObjectRec.Code <> '' THEN BEGIN
                               lCostObjectRec.RESET;
                               lCostObjectRec.SETRANGE("Global Dimension No.",2);
                               lCostObjectRec.SETRANGE(Code,CostObjectRec.Code);
                               lCostObjectRec.FIND('-');
                             END;
                             CheckChangeDescription(CostObjectRec.Name, lCostObjectRec.Name);
                             CostObjectRec := lCostObjectRec;
                             ValidateCostObject;
                               CostObjectRecCodeOnAfterValida;
                           END;

                OnLookup=VAR
                           lCostObjectRec@1100525002 : Record 349;
                           CostObjectLookupFrm@1210190000 : Page 11012021;
                         BEGIN
                           lCostObjectRec := CostObjectRec;
                           CLEAR(CostObjectLookupFrm);
                           CostObjectLookupFrm.LOOKUPMODE(TRUE);
                           CostObjectLookupFrm.SETRECORD(CostObjectRec);
                           IF CostObjectLookupFrm.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             CostObjectLookupFrm.GETRECORD(CostObjectRec);
                             CheckChangeDescription(lCostObjectRec.Name, CostObjectRec.Name);
                           END;
                           ValidateCostObject;
                           CurrPage.UPDATE(TRUE);
                         END;
                          }

    { 1210190010;1;Field  ;
                CaptionML=[DEU=Name KostentrÑger;
                           ENU=Cost Object Name;
                           NLD=Kostendragernaam;
                           NOR=Kostobjekt, navn;
                           SVE=Kostnadsobjekt, namn];
                SourceExpr=CostObjectRec.Name;
                Visible=false;
                Editable=FALSE }

    { 1210190011;1;Field  ;
                CaptionML=[DEU=Kostenart;
                           ENU=Cost Type;
                           NLD=Kostensoort;
                           NOR=Kosttype;
                           SVE=Kostnadstyp];
                OptionCaptionML=[DEU=Arbeit,Material,Subunternehmung,Werkzeug,öbriges;
                                 ENU=Labor,Material,Subcontracting,Plant,Sundry;
                                 NLD=Arbeid,Materiaal,Onderaanneming,Materieel,Overig;
                                 NOR=Arbeidskraft,Materiale,Underentrepenõr,Maskin,Diverse;
                                 SVE=Arbetskraft,Material,Underentreprenad,Maskin,Diverse];
                SourceExpr=CostObjectRec."Cost Type";
                Editable=FALSE }

    { 1100525000;1;Field  ;
                Name=DescriptionNewBudgetLine;
                CaptionML=[DEU=Beschreibung;
                           ENU=Description;
                           NLD=Omschrijving;
                           NOR=Beskrivelse;
                           SVE=Beskrivning];
                SourceExpr=DescriptionNewBudgetLine }

    { 1210190013;1;Field  ;
                Name=Element;
                CaptionML=[DEU=Element;
                           ENU=Element;
                           NLD=Element;
                           NOR=Element;
                           SVE=Element];
                SourceExpr=ProjElemRec.Element;
                Editable=ElementEditable;
                OnValidate=VAR
                             lProjElemRec@1210190000 : Record 11012010;
                           BEGIN
                             IF ProjElemRec.Element <> '' THEN BEGIN
                               lProjElemRec.RESET;
                               lProjElemRec.SETRANGE("Project No.",gBudgAdjRec."Project No.");
                               lProjElemRec.SETRANGE(Element,ProjElemRec.Element);
                               lProjElemRec.FIND('-');
                               ProjElemRec := lProjElemRec;
                             END;
                           END;

                OnLookup=VAR
                           ProjElemLookupFrm@1210190000 : Page 11012029;
                         BEGIN
                           IF NOT ElementEditable THEN  //*33411.n
                             EXIT;
                           CLEAR(ProjElemLookupFrm);
                           ProjElemLookupFrm.LOOKUPMODE(TRUE);
                           ProjElemRec.SETRANGE("Project No.",gBudgAdjRec."Project No.");
                           ProjElemLookupFrm.SETTABLEVIEW(ProjElemRec);
                           IF ProjElemLookupFrm.RUNMODAL = ACTION::LookupOK THEN
                             ProjElemLookupFrm.GETRECORD(ProjElemRec);
                           CurrPage.UPDATE(TRUE);
                         END;
                          }

    { 1210190015;1;Field  ;
                CaptionML=[DEU=Elementbeschreibung;
                           ENU=Element Description;
                           NLD=Elementomschrijving;
                           NOR=Elementbeskrivelse;
                           SVE=Elementbeskrivning];
                SourceExpr=ProjElemRec.Description;
                Visible=false;
                Editable=FALSE }

    { 1210190016;1;Field  ;
                Name=RateField;
                CaptionML=[DEU=Tarifcode;
                           ENU=Rate Code;
                           NLD=Tariefcode;
                           NOR=Avgiftskode;
                           SVE=Avgiftskod];
                SourceExpr=BudgetRateRec.Code;
                Editable=RateFieldEditable;
                OnValidate=BEGIN
                             CalcHours;
                               BudgetRateRecCodeOnAfterValida;
                           END;

                OnLookup=VAR
                           RateLookupFrm@1210190000 : Page 11020265;
                         BEGIN
                           IF NOT RateFieldEditable THEN  //*33411.n
                             EXIT;
                           CLEAR(RateLookupFrm);
                           RateLookupFrm.LOOKUPMODE(TRUE);

                           BudgetRateRec.RESET;
                           BudgetRateRec.SETFILTER("Project Filter", '%1', gBudgAdjRec."Project No.");
                           RateLookupFrm.SETTABLEVIEW(BudgetRateRec);
                           IF RateLookupFrm.RUNMODAL = ACTION::LookupOK THEN
                             RateLookupFrm.GETRECORD(BudgetRateRec);
                           CurrPage.UPDATE(TRUE);
                         END;
                          }

    { 1210190018;1;Field  ;
                CaptionML=[DEU=Tarifbeschreibung;
                           ENU=Rate Description;
                           NLD=Tariefomschrijving;
                           NOR=Beskrivelse av sats;
                           SVE=Beskrivning av taxa];
                BlankZero=Yes;
                SourceExpr=GetBudgetRate();
                Visible=false;
                Editable=FALSE }

    { 1210190021;1;Field  ;
                Name=HoursToAddField;
                CaptionML=[DEU=Anzahl Stunden;
                           ENU=Hours to add;
                           NLD=Aantal uren;
                           NOR=Timer Ü legge til;
                           SVE=Timmar att lÑgga till];
                BlankZero=Yes;
                SourceExpr=HoursToAdd;
                Editable=FALSE }

    { 1100485000;1;Field  ;
                Name=NoOfLinesToAdd;
                CaptionML=[DEU=Anzahl Zeilen hinzufÅgen;
                           ENU=No. of Lines to Add;
                           NLD=Aantal regels toevoegen;
                           NOR=Antall rader som skal legges til;
                           SVE=Antal rader som ska lÑggas till];
                SourceExpr=NoOfLinesToAdd;
                MinValue=1;
                Editable=NoOfLinesToAddEditable;
                OnValidate=BEGIN
                             ValidateNoOfLinesToAdd();  //*C-029097.n
                           END;
                            }

  }
  CODE
  {
    VAR
      gBudgAdjRec@1210190000 : Record 11012003;
      ProjElemRec@1210190004 : Record 11012010;
      BudgetRateRec@1210190003 : Record 11012000;
      CostObjectRec@1100525000 : Record 349;
      MarkedBudgetLine@1210190008 : Record 11012001;
      gPurchActionCde@1100525001 : Code[20];
      DescriptionNewBudgetLine@1100525002 : Text[100];
      AmntToApplyLCY@1210190001 : Decimal;
      MaxAmntToApply@1210190002 : Decimal;
      HoursToAdd@1210190005 : Decimal;
      NoOfLinesToAdd@1100485000 : Integer;
      Text001@1210190006 : TextConst 'DEU=WÑhlen Sie %1 aus;ENU=Select %1;NLD=Selecteer %1;NOR=Velg %1;SVE=VÑlj %1';
      Text002@1210190007 : TextConst 'DEU=Der maximal auszugleichende Betrag ist %1.;ENU=Maximum Amount to Apply is %1.;NLD=Het maximum te vereffenen bedrag is %1.;NOR=Maksimalt belõp er %1.;SVE=Maximalt belopp att tillÑmpa Ñr %1.';
      AmntToApplyEditable@1210190009 : Boolean INDATASET;
      CompressType@1210190010 : 'Yes,No,Tender Paragraph / Part';
      Text003@1210190011 : TextConst 'DEU=Keine Berechtigung fÅr %1;ENU=No permission for %1;NLD=Geen permissie voor %1;NOR=Ingen tillatelse for %1;SVE=Ingen behîrighet fîr %1';
      Text004@1210190012 : TextConst 'DEU=Verdichten ''Nein'' nur zulÑssig, wenn zuerst Budgetzeilen markiert worden sind.;ENU="Compress ''No'' only allowed if first budget lines are marked. ";NLD=Verdichten ''Nee'' alleen toegestaan als er eerst werkbegrotingsregels gemarkeerd zijn.;NOR=Nei'' til komprimering er kun tillatt om fõrste budsjettradene er markerte.;SVE="Nej'' till komprimering Ñr endast tillÜtet om fîrsta budgetraderna Ñr markerade. "';
      NoOfLinesToAddEditable@19062939 : Boolean INDATASET;
      ElementEditable@19042443 : Boolean INDATASET;
      RateFieldEditable@1210190013 : Boolean INDATASET;
      AmntToApplyFCY@1100525003 : Decimal;
      CurrencyCode@1100525004 : Code[20];
      ProjectNo@1100525005 : Code[20];
      CompressTypeEditable@1100525006 : Boolean;

    PROCEDURE SetDefaults@1210190000(lBudgAdjRec@1210190001 : Record 11012003;lAmntToApplyDec@1210190000 : Decimal;lCostObjectCde@1100525000 : Code[20];lPurchActionCde@1100525001 : Code[20]);
    VAR
      ProjRec@1100525002 : Record 11072003;
    BEGIN
      gBudgAdjRec := lBudgAdjRec;
      AmntToApplyLCY := lAmntToApplyDec;
      MaxAmntToApply := lAmntToApplyDec;
      gPurchActionCde := lPurchActionCde;
      CostObjectRec.SETRANGE(Code,lCostObjectCde);
      CostObjectRec.SETRANGE("Global Dimension No.",2);
      IF CostObjectRec.FINDFIRST THEN;
      DescriptionNewBudgetLine := CostObjectRec.Name;
      //DP00883.sn
      ProjectNo := gBudgAdjRec."Project No.";
      ProjRec.GET(ProjectNo);
      CurrencyCode := ProjRec."Currency Code";
      IF CurrencyCode = '' THEN
        AmntToApplyFCY := AmntToApplyLCY
      ELSE
        CalculateAmountByCurrency(1);
      //DP00883.en
    END;

    PROCEDURE SetDefaultNewDescription@1100525001(DefaultDescription@1100525000 : Text[100]);
    BEGIN
      DescriptionNewBudgetLine := DefaultDescription;
    END;

    PROCEDURE SetMarkedBudgetLines@1210190002(VAR BudgetLine@1210190001 : Record 11012001;AmountToApplyEditable@1210190000 : Boolean);
    BEGIN
      MarkedBudgetLine.COPY(BudgetLine);
      //AmntToApplyEditable := TRUE;  //*C-029097.o
      AmntToApplyEditable := AmountToApplyEditable;  //*C-029097.n

      MarkedBudgetLine.FILTERGROUP(9);
      CompressTypeEditable := TRUE;
      MarkedBudgetLine.SETFILTER(Option, '<>%1', '');
      IF NOT MarkedBudgetLine.ISEMPTY THEN
        CompressTypeEditable := FALSE
      ELSE BEGIN
        MarkedBudgetLine.SETRANGE(Option);
        MarkedBudgetLine.SETFILTER("Settlement Quantity Code", '<>%1', '');
        IF NOT MarkedBudgetLine.ISEMPTY THEN
          CompressTypeEditable := FALSE;
      END;
      IF NOT CompressTypeEditable THEN
        CompressType := CompressType::No;

      MarkedBudgetLine.SETRANGE(Option);
      MarkedBudgetLine.SETRANGE("Settlement Quantity Code");

      MarkedBudgetLine.FILTERGROUP(0);
    END;

    PROCEDURE CalcHours@1210190001();
    VAR
      lvRate@1100485000 : Decimal;
    BEGIN
      HoursToAdd := 0;
      lvRate := GetBudgetRate();
      IF lvRate <> 0 THEN
        HoursToAdd := (AmntToApplyLCY * -1)/lvRate;
    END;

    PROCEDURE GetBudgetRate@1100485000() : Decimal;
    VAR
      Job@1100528600 : Record 11072003;
      BudgetRateManagement@1100528601 : Codeunit 11012358;
      lvRate@1100485001 : Decimal;
      BudgetRateFoundAt@1100528602 : Text[250];
    BEGIN
      IF NOT Job.GET(gBudgAdjRec."Project No.") THEN
        Job.INIT;
      lvRate := BudgetRateManagement.GetBudgetRate(
        0, gBudgAdjRec."Project No.", '', '', Job."Global Dimension 1 Code", BudgetRateRec.Code, gBudgAdjRec."Version Date",
        BudgetRateFoundAt);

      EXIT(lvRate);
    END;

    PROCEDURE ValidateCostObject@1100525000();
    BEGIN
      BudgetRateRec.RESET;
      BudgetRateRec.SETRANGE(Code, CostObjectRec."Rate Code");
      BudgetRateRec.SETRANGE("Starting Date", 0D, gBudgAdjRec."Version Date");
      BudgetRateRec.SETFILTER("Ending Date",'%1|>=%2',0D, gBudgAdjRec."Version Date");
      IF BudgetRateRec.FINDLAST THEN
        CalcHours;
      SetEditable();  //*C-029097.n
    END;

    PROCEDURE CheckChangeDescription@1100525002(OldDescription@1100525000 : Text[50];NewDescription@1100525001 : Text[50]);
    BEGIN
      IF  (DescriptionNewBudgetLine = OldDescription) OR (DescriptionNewBudgetLine = '') THEN
        DescriptionNewBudgetLine := NewDescription;
    END;

    PROCEDURE ValidateCompressType@1210190003();
    VAR
      ProjTenderPar@1210190000 : Record 11012263;
      FPSLicenseManagement@1100527400 : Codeunit 11229289;
    BEGIN
      //*C-029097
      IF CompressType = CompressType::"Tender Paragraph / Part" THEN BEGIN
        IF NOT FPSLicenseManagement.LicenseAndReadPermissionForTable(ProjTenderPar) THEN  // Tender Par. in granule 11012390 (GWW)
          ERROR(Text003, ProjTenderPar.TABLECAPTION);
      END;

      IF CompressType <> CompressType::Yes THEN
        NoOfLinesToAdd := 1;

      IF CompressType = CompressType::No THEN BEGIN
        IF MarkedBudgetLine.ISEMPTY THEN
          ERROR(Text004);
        ProjElemRec.Element := '';
        BudgetRateRec.Code := '';
      END;
    END;

    PROCEDURE ValidateNoOfLinesToAdd@1210190004();
    BEGIN
      //*C-029097
      IF NoOfLinesToAdd <> 1 THEN BEGIN
        IF CompressType <> CompressType::Yes THEN
          NoOfLinesToAdd := 1;  //* Must be 1 if not compressed!  Also if by Tender Par then 1 line per Tender Par.
      END;
    END;

    PROCEDURE SetEditable@1210190005();
    BEGIN
      //*C-029097
      ElementEditable := CompressType <> CompressType::No;
      RateFieldEditable :=
        (CompressType <> CompressType::No) AND (CostObjectRec."Cost Type" = CostObjectRec."Cost Type"::Labor);
      NoOfLinesToAddEditable := CompressType = CompressType::Yes;
    END;

    PROCEDURE CalcTotalAmountForCompressType@1210190006(VAR BudgetLine@1210190000 : Record 11012001;VAR RemainingAmountToApply@1210190002 : Decimal) : Decimal;
    VAR
      BudgetLine2@1210190001 : Record 11012001;
    BEGIN
      //*C-029097
      // In this function only the (flow)field "Total Amount" may be filled/calculated
      CASE CompressType OF
        CompressType::Yes:
          BEGIN
            BudgetLine.CALCFIELDS("Total Amount");
            RemainingAmountToApply := BudgetLine."Total Amount" - BudgetLine.SummaryAmount;
          END;
        CompressType::No:
          RemainingAmountToApply := BudgetLine."Amount (LCY)";
        CompressType::"Tender Paragraph / Part":
          BEGIN
            RemainingAmountToApply := 0;
            BudgetLine2.SETRANGE("Project No.", BudgetLine."Project No.");
            BudgetLine2.SETRANGE(Adjustment, BudgetLine.Adjustment);
            BudgetLine2.SETRANGE("Extension Contract", BudgetLine."Extension Contract");
            BudgetLine2.SETRANGE("Tender Paragraph", BudgetLine."Tender Paragraph");
            BudgetLine2.SETRANGE("Tender Paragraph Part", BudgetLine."Tender Paragraph Part");
            BudgetLine2.SETRANGE("Budget Summary Line", FALSE);
            IF BudgetLine2.FINDSET THEN BEGIN
              REPEAT
                RemainingAmountToApply := RemainingAmountToApply + BudgetLine2."Amount (LCY)";
              UNTIL BudgetLine2.NEXT =0;
            END;
          END;
      END;

      //call 30022
      IF AmntToApplyLCY > 0 THEN BEGIN
        IF RemainingAmountToApply > AmntToApplyLCY THEN
          RemainingAmountToApply := AmntToApplyLCY;
      END ELSE
        IF RemainingAmountToApply < AmntToApplyLCY THEN
          RemainingAmountToApply := AmntToApplyLCY;
      //call 30022
    END;

    LOCAL PROCEDURE CostObjectRecCodeOnAfterValida@19059534();
    BEGIN
      CurrPage.UPDATE(TRUE);
    END;

    LOCAL PROCEDURE BudgetRateRecCodeOnAfterValida@19063496();
    BEGIN
      IF NOT BudgetRateRec.GET(BudgetRateRec.Code) THEN
        CLEAR(BudgetRateRec);
    END;

    LOCAL PROCEDURE CalculateAmountByCurrency@1100525004(iAction@1100525001 : Integer);
    VAR
      ProjectCurrencyExchangeRate@1100525000 : Record 11020628;
    BEGIN
      //DP00883
      IF iAction = 1 THEN
        AmntToApplyFCY := ProjectCurrencyExchangeRate.ExchangeAmtLCYToFCY(AmntToApplyLCY, FALSE, FALSE, CurrencyCode, ProjectNo, '', 0D)
      ELSE
        AmntToApplyLCY := ProjectCurrencyExchangeRate.ExchangeAmtFCYToLCY(AmntToApplyFCY, FALSE, FALSE, CurrencyCode, ProjectNo, '', 0D);
    END;

    LOCAL PROCEDURE ApplyBudgetDifference@1100525003();
    VAR
      BudgetRec@1100525007 : Record 11012001;
      BudgetRec2@1100525006 : Record 11012001;
      TmpTenderParPart@1100525005 : TEMPORARY Record 11012264;
      SkipTenderParPart@1100525004 : Boolean;
      LineNoInt@1100525003 : Integer;
      Counter@1100525002 : Integer;
      ApplyPercentage@1100525001 : Decimal;
      RemainingAmountToApply@1100525000 : Decimal;
    BEGIN
      //DP00883: moved from page-action to function
      IF CompressType <> CompressType::No THEN BEGIN
        IF CostObjectRec.Code = '' THEN
          ERROR(Text001, BudgetRec.FIELDCAPTION("Cost Object"));

        IF (CostObjectRec."Cost Type" = CostObjectRec."Cost Type"::Labor) AND
           (BudgetRateRec.Code = '')
        THEN
          IF NOT BudgetRateRec.GET(BudgetRateRec.Code) THEN
            ERROR(Text001, BudgetRateRec.TABLECAPTION);
      END;
      ValidateCompressType();
      ValidateNoOfLinesToAdd();

      IF AmntToApplyEditable AND (AmntToApplyLCY <> MaxAmntToApply) THEN BEGIN
        ApplyPercentage := AmntToApplyLCY / MaxAmntToApply;
        IF MarkedBudgetLine.FINDSET(TRUE, FALSE) THEN
          REPEAT
            IF MarkedBudgetLine."Cost Type" = MarkedBudgetLine."Cost Type"::Labor THEN
              MarkedBudgetLine.VALIDATE(Quantity, MarkedBudgetLine.Quantity * ApplyPercentage)
            ELSE
              MarkedBudgetLine.VALIDATE("Price (LCY)", MarkedBudgetLine."Price (LCY)" * ApplyPercentage);
            MarkedBudgetLine.MODIFY;
          UNTIL MarkedBudgetLine.NEXT = 0;
      END;

      //Determine Last Line No of Adjustment
      BudgetRec.SETCURRENTKEY("Project No.", Adjustment);
      BudgetRec.SETRANGE("Project No.",gBudgAdjRec."Project No.");
      BudgetRec.SETRANGE(Adjustment,gBudgAdjRec."No.");
      IF BudgetRec.FINDSET THEN
        REPEAT
          IF LineNoInt < BudgetRec."Line No." THEN
            LineNoInt := BudgetRec."Line No.";
        UNTIL BudgetRec.NEXT = 0
      ELSE
        EXIT;  //DP00883 instead of CurrPage.CLOSE;

      IF CompressType = CompressType::No THEN BEGIN
        //* Only marked budget lines may be applied.
        //* Note: Mark the budget lines of BudgetRec after the last lineno is determined.
        MarkedBudgetLine.FINDSET;
        REPEAT
          IF BudgetRec.GET(
            MarkedBudgetLine."Project No.", MarkedBudgetLine.Adjustment,
            MarkedBudgetLine."Extension Contract", MarkedBudgetLine.Option, MarkedBudgetLine."Line No.")
          THEN
            BudgetRec.MARK(TRUE);
        UNTIL MarkedBudgetLine.NEXT = 0;
        BudgetRec.MARKEDONLY(TRUE);
      END;

      BudgetRec.SETFILTER("Line No.", '..%1', LineNoInt);
      LineNoInt := LineNoInt + 10000;
      BudgetRec.SETRANGE("Budget Summary Line", FALSE);
      IF BudgetRec.FINDSET(FALSE, FALSE) THEN BEGIN
        REPEAT
          SkipTenderParPart := FALSE;
          IF CompressType = CompressType::"Tender Paragraph / Part" THEN BEGIN
            IF TmpTenderParPart.GET(BudgetRec."Project No.", BudgetRec."Tender Paragraph", BudgetRec."Tender Paragraph Part") THEN
              SkipTenderParPart := TRUE
            ELSE BEGIN
              TmpTenderParPart."Project No." := BudgetRec."Project No.";
              TmpTenderParPart."Tender Paragraph" := BudgetRec."Tender Paragraph";
              TmpTenderParPart."Tender Paragraph Part" := BudgetRec."Tender Paragraph Part";
              TmpTenderParPart.INSERT;
            END;
          END;
          IF (NOT SkipTenderParPart) THEN BEGIN
            BudgetRec.SETRANGE("Adjustment Filter", gBudgAdjRec."No.");
            BudgetRec.SETRANGE("Extension Filter", BudgetRec."Extension Contract");
            CalcTotalAmountForCompressType(BudgetRec, RemainingAmountToApply);
          END;
          IF (ABS(RemainingAmountToApply) >= 0.01) AND (NOT SkipTenderParPart) THEN BEGIN
            CLEAR(BudgetRec2);
            BudgetRec2."Project No." := gBudgAdjRec."Project No.";
            BudgetRec2.Adjustment := gBudgAdjRec."No.";
            BudgetRec2."Cost Type" := CostObjectRec."Cost Type";
            BudgetRec2."Extension Contract" := BudgetRec."Extension Contract";
            BudgetRec2."Version Date" := gBudgAdjRec."Version Date";
            IF CompressType = CompressType::"Tender Paragraph / Part" THEN BEGIN
              BudgetRec2."Tender Paragraph" := BudgetRec."Tender Paragraph";
              BudgetRec2."Tender Paragraph Part" := BudgetRec."Tender Paragraph Part";
            END;
            //DP00883.sn
            BudgetRec2."Currency Code" := CurrencyCode;
            BudgetRec2.CheckCurrencyAgreement;
            //DP00883.en
            IF CompressType = CompressType::No THEN BEGIN
              BudgetRec2.VALIDATE(Element, BudgetRec.Element);
              BudgetRec2.VALIDATE("Cost Object",CostObjectRec.Code);
              IF (DescriptionNewBudgetLine <> '') AND (BudgetRec2.Description <> DescriptionNewBudgetLine) THEN
                BudgetRec2.Description := DescriptionNewBudgetLine
              ELSE
                BudgetRec2.Description := BudgetRec.Description;
              BudgetRec2."Description 2" := BudgetRec."Description 2";
              BudgetRec2."Unit of Measure" := BudgetRec."Unit of Measure";
              BudgetRec2."Unit of Time" := BudgetRec."Unit of Time";
              BudgetRec2."Time Quantity" := BudgetRec."Time Quantity";
              BudgetRec2."Item No." := BudgetRec."Item No.";
              BudgetRec2.Manufacturer := BudgetRec.Manufacturer;
              BudgetRec2."Basic Item" := BudgetRec."Basic Item";
              BudgetRec2."Vendor (Trade Item)" := BudgetRec."Vendor (Trade Item)";
              BudgetRec2."Trade Item" := BudgetRec."Trade Item";
              BudgetRec2."Plot No." := BudgetRec."Plot No.";
              BudgetRec2."House Model" := BudgetRec."House Model";
              BudgetRec2.Option := BudgetRec.Option;
              BudgetRec2."Main Group" := BudgetRec."Main Group";
              BudgetRec2.Group := BudgetRec.Group;
              BudgetRec2."Sub Group" := BudgetRec."Sub Group";
              BudgetRec2."Settlement Quantity Code" := BudgetRec."Settlement Quantity Code";
              BudgetRec2."Tender Paragraph" := BudgetRec."Tender Paragraph";
              BudgetRec2."Tender Paragraph Part" := BudgetRec."Tender Paragraph Part";
              BudgetRec2."Cost Component" := BudgetRec."Cost Component";
              BudgetRec2."Variant Code" := BudgetRec."Variant Code";
              BudgetRec2."Plant Company" := BudgetRec."Plant Company";
              BudgetRec2."Plant Location" := BudgetRec."Plant Location";
              BudgetRec2."Plant Type" := BudgetRec."Plant Type";
              IF BudgetRec2."Cost Type" = BudgetRec2."Cost Type"::Labor THEN BEGIN
                BudgetRec2."Rate Code" := BudgetRec."Rate Code";
                BudgetRec2."Production Rate" := BudgetRec."Production Rate";
                IF BudgetRec."Cost Type" = BudgetRec."Cost Type"::Labor THEN BEGIN
                  BudgetRec2."Rate (LCY)" := BudgetRec."Rate (LCY)";
                  BudgetRec2."Rate (FCY)" := BudgetRec."Rate (FCY)";  //DP00883
                  BudgetRec2.Norm := BudgetRec.Norm;
                  BudgetRec2."Production Rate" := BudgetRec."Production Rate";
                END ELSE BEGIN
                  BudgetRec2."Rate (LCY)" := BudgetRec."Price (LCY)";
                  BudgetRec2."Rate (FCY)" := BudgetRec."Price (FCY)";
                  BudgetRec2.Norm := 1;
                END;
                BudgetRec2.VALIDATE(Quantity, -BudgetRec.Quantity);
              END ELSE BEGIN
                BudgetRec2."Discount %" := BudgetRec."Discount %";
                BudgetRec2."Gross Price (LCY)" := BudgetRec."Gross Price (LCY)";
                BudgetRec2."Gross Price (FCY)" := BudgetRec."Gross Price (FCY)";  //DP00883
                BudgetRec2."Surcharge %" := BudgetRec."Surcharge %";
                BudgetRec2."Surcharge Price (LCY)" := BudgetRec."Surcharge Price (LCY)";
                BudgetRec2."Surcharge Price (FCY)" := BudgetRec."Surcharge Price (FCY)";  //DP00883
                //C040847.sn
                IF BudgetRec2."Cost Type" = BudgetRec2."Cost Type"::Plant THEN BEGIN
                  BudgetRec2."Plant Rates in Hours" := BudgetRec."Plant Rates in Hours";
                  BudgetRec2.Norm := BudgetRec.Norm;
                  BudgetRec2."Production Rate" := BudgetRec."Production Rate";
                END;
                //C040847.en
                IF BudgetRec."Cost Type" <> BudgetRec."Cost Type"::Labor THEN BEGIN
                  BudgetRec2."Price (FCY)" := BudgetRec."Price (FCY)";
                  BudgetRec2."Price (LCY)" := BudgetRec."Price (LCY)";
                  BudgetRec2.VALIDATE(Quantity, -BudgetRec.Quantity);
                END ELSE BEGIN
                  BudgetRec2."Price (FCY)" := BudgetRec."Rate (FCY)";
                  BudgetRec2."Price (LCY)" := BudgetRec."Rate (LCY)";
                  BudgetRec2.VALIDATE(Quantity, -BudgetRec.Quantity * BudgetRec.Norm);
                END;
              END;
            END ELSE BEGIN
              BudgetRec2.VALIDATE("Cost Object",CostObjectRec.Code);
              IF CostObjectRec."Cost Type" = CostObjectRec."Cost Type"::Labor THEN BEGIN
                BudgetRec2.VALIDATE("Rate Code",BudgetRateRec.Code);
                BudgetRec2.TESTFIELD("Rate (LCY)");
                BudgetRec2.Norm := 1;
                BudgetRec2."Production Rate" := 1;
                BudgetRec2.VALIDATE(Quantity,((RemainingAmountToApply * -1)/ (BudgetRec2."Rate (LCY)" * NoOfLinesToAdd)));
              END ELSE BEGIN
                BudgetRec2.Quantity := 1;
                BudgetRec2.VALIDATE("Price (LCY)",RemainingAmountToApply/NoOfLinesToAdd * -1);
              END;
              IF ProjElemRec.Element <> '' THEN
                BudgetRec2.VALIDATE(Element, ProjElemRec.Element);
              IF (DescriptionNewBudgetLine <> '') AND (BudgetRec2.Description <> DescriptionNewBudgetLine) THEN
                BudgetRec2.Description := DescriptionNewBudgetLine;
            END;
            FOR Counter := 1 TO NoOfLinesToAdd DO BEGIN
              IF (NoOfLinesToAdd > 1) AND (Counter = NoOfLinesToAdd) AND (CompressType <> CompressType::No) THEN BEGIN
                //last line holds rounding
                BudgetRec2.SETRANGE("Adjustment Filter", gBudgAdjRec."No.");
                BudgetRec2.SETRANGE("Extension Filter", BudgetRec."Extension Contract");
                CalcTotalAmountForCompressType(BudgetRec2, RemainingAmountToApply);
                IF CostObjectRec."Cost Type" = CostObjectRec."Cost Type"::Labor THEN
                  BudgetRec2.VALIDATE(Quantity,((RemainingAmountToApply * -1)/ (BudgetRec2."Rate (LCY)")))
                ELSE
                  BudgetRec2.VALIDATE("Price (LCY)",RemainingAmountToApply * -1);
              END;
              BudgetRec2."Line No." := LineNoInt;
              BudgetRec2.INSERT(TRUE);
              BudgetRec2."Currency Code" := CurrencyCode;  //DP00883: reset on insert to initial value by project
              BudgetRec2.VALIDATE("Purchase Action",gPurchActionCde);
              BudgetRec2.MODIFY(TRUE);
              BudgetRec2.ProcessSummarySheet(BudgetRec2, FALSE); //C026999.n
              LineNoInt := LineNoInt + 10000;
            END;
          END;
        UNTIL BudgetRec.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}

