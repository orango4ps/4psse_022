OBJECT Table 11072344 DPT Subsite ServLoc
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               DPTDefaultSubsite@1100528300 : Record 11229941;
             BEGIN
               GetDescription;
             END;

    OnModify=BEGIN
               IF ("Subsite Structure" <> xRec."Subsite Structure") THEN
                 CheckRecordChangeAllowed;
             END;

    OnDelete=VAR
               DPTSubsiteDocTypeServLoc@1100528300 : Record 11072345;
               DPTHeaderText@1100528303 : Record 11229943;
             BEGIN
               CheckRecordChangeAllowed;
               DPTSubsiteDocTypeServLoc.SETRANGE("Service Location No.", "Service Location No.");
               DPTSubsiteDocTypeServLoc.SETRANGE("Subsite Code", Code);
               DPTSubsiteDocTypeServLoc.DELETEALL(TRUE);

               DPTHeaderText.SETRANGE("Table No.", DATABASE::"DPT Subsite ServLoc");
               DPTHeaderText.SETRANGE("Field No.", FIELDNO(Code));
               DPTHeaderText.SETRANGE("Source No.", "Service Location No.");
               DPTHeaderText.SETRANGE("Subsite Code", Code);
               DPTHeaderText.DELETEALL;
             END;

    OnRename=BEGIN
               ERROR(Text000);
             END;

    CaptionML=ENU=DPT Subsite (Service);
    LookupPageID=Page11130628;
    DrillDownPageID=Page11130628;
  }
  FIELDS
  {
    { 10  ;   ;Service Location No.;Code20        ;TableRelation="Service Location".No.;
                                                   CaptionML=[ENU=Service Location No.;
                                                              NOR=Servicelokasjon Nr.;
                                                              SVE=Serviceplatsnr];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 11  ;   ;Service Location Description;Text50;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Service Location".Description WHERE (No.=FIELD(Service Location No.)));
                                                   CaptionML=[ENU=Service Location Description;
                                                              NOR=Servicelokasjonbeskrivelse;
                                                              SVE=Serviceplatsbeskrivning];
                                                   Editable=No }
    { 20  ;   ;Code                ;Code20        ;OnValidate=BEGIN
                                                                GetDescription;
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              NOR=Kode;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 30  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 40  ;   ;Subsite Structure   ;Option        ;OnValidate=BEGIN
                                                                CheckRecordChangeAllowed;
                                                              END;

                                                   CaptionML=ENU=Subsite Structure;
                                                   OptionCaptionML=ENU=1 Document Library,Document Library per Document Type,Permission per Document Type;
                                                   OptionString=OneDocLibrary,DocLibraryPerDocType,PermissionPerDocType }
    { 60  ;   ;Synchronized On     ;DateTime      ;CaptionML=ENU=Synchronized On;
                                                   Editable=No }
    { 70  ;   ;Template            ;Text50        ;TableRelation=IF (Template Type=CONST(Redirect)) "DPT Subsite Doc Type ServLoc"."Document Type" WHERE (Service Location No.=FIELD(Service Location No.),
                                                                                                                                                          Subsite Code=FIELD(Code));
                                                   CaptionML=[ENU=Template;
                                                              NOR=Mal;
                                                              SVE=Mall] }
    { 80  ;   ;Template Type       ;Option        ;CaptionML=[ENU=Template Type;
                                                              NOR=Maltype;
                                                              SVE=Malltyp];
                                                   OptionCaptionML=ENU=Default,Page,Redirect;
                                                   OptionString=Default,Page,Redirect }
    { 90  ;   ;Use Document CheckIn/-Out;Boolean  ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                CheckRecordChangeAllowed;
                                                                SetUseDocumentVersionDocType;
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Manual Check-In/Check-Out }
    { 100 ;   ;Use Doc CheckIn/-Out Comp List;Boolean;
                                                   InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                CheckRecordChangeAllowed;
                                                              END;

                                                   DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Manual Check-In/Check-Out in Company Lists }
  }
  KEYS
  {
    {    ;Service Location No.,Code               ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100528300 : TextConst 'ENU=Rename is not allowed.;NOR=Gi nytt navn ikke tillatt;SVE=Att ge nytt namn „r inte till†tet.';

    PROCEDURE CreateAllSubsites@1100528302(ServiceLocationNo@1100528300 : Code[20]);
    VAR
      ServiceLocation@1100528304 : Record 11012801;
      DPTDefaultSubsite@1100528303 : Record 11229941;
      DPTSubsiteServLoc@1100528302 : Record 11072344;
      DPTHeaderText@1100528301 : Record 11229943;
    BEGIN
      DPTDefaultSubsite.SETRANGE("Source Type", DPTDefaultSubsite."Source Type"::ServiceLocation);
      IF (NOT DPTDefaultSubsite.FINDSET) THEN
        EXIT;
      ServiceLocation.GET(ServiceLocationNo);

      REPEAT
        IF (NOT DPTSubsiteServLoc.GET(ServiceLocationNo, DPTDefaultSubsite.Code)) THEN BEGIN
          DPTSubsiteServLoc.INIT;
          DPTSubsiteServLoc.Code := DPTDefaultSubsite.Code;
          DPTSubsiteServLoc."Service Location No." := ServiceLocationNo;
          DPTSubsiteServLoc."Subsite Structure" := DPTDefaultSubsite."Subsite Structure";
          DPTSubsiteServLoc.Template := DPTDefaultSubsite.Template;
          DPTSubsiteServLoc."Template Type" := DPTDefaultSubsite."Template Type";
          DPTSubsiteServLoc."Use Doc CheckIn/-Out Comp List" := DPTDefaultSubsite."Use Doc CheckIn/-Out Comp List";
          DPTSubsiteServLoc."Use Document CheckIn/-Out" := DPTDefaultSubsite."Use Document CheckIn/-Out";
          DPTSubsiteServLoc.INSERT(TRUE);
          DPTHeaderText.CopySubsiteText(DATABASE::"DPT Subsite ServLoc", DPTSubsiteServLoc.FIELDNO(Code),
            DPTSubsiteServLoc."Service Location No.", DPTSubsiteServLoc.Code);
        END;
      UNTIL (DPTDefaultSubsite.NEXT = 0);
    END;

    LOCAL PROCEDURE "---Locals---"@1100528300();
    BEGIN
    END;

    LOCAL PROCEDURE CheckRecordChangeAllowed@1100528301();
    VAR
      DPTInterface@1100528302 : Codeunit 11012451;
    BEGIN
      TESTFIELD("Synchronized On", 0DT);
      DPTInterface.SetDPTType(2);
      DPTInterface.CheckDocumentsLinkedToSubsiteDocumentType("Service Location No.", Code, '');
    END;

    LOCAL PROCEDURE GetDescription@1100528304();
    VAR
      DPTDefaultSubsite@1100528300 : Record 11229941;
    BEGIN
      IF (NOT DPTDefaultSubsite.GET(DPTDefaultSubsite."Source Type"::ServiceLocation, Code)) THEN
        DPTDefaultSubsite.INIT;
      IF (Description = '') THEN
        Description := DPTDefaultSubsite.Description;
    END;

    LOCAL PROCEDURE SetUseDocumentVersionDocType@1100528303();
    VAR
      DPTSubsiteDocTypeServLoc@1100528300 : Record 11072345;
    BEGIN
      DPTSubsiteDocTypeServLoc.SETRANGE("Service Location No.", "Service Location No.");
      DPTSubsiteDocTypeServLoc.SETRANGE("Subsite Code", Code);
      DPTSubsiteDocTypeServLoc.MODIFYALL("Use Document CheckIn/-Out", "Use Document CheckIn/-Out", TRUE);
    END;

    BEGIN
    END.
  }
}

