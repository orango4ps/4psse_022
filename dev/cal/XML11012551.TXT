OBJECT XMLport 11012551 Import Plant Invoices Central
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Import Plant Invoices Main;
               SVE=Importera huvudMaskinfakturor];
    Direction=Import;
    PreserveWhiteSpace=Yes;
    TextEncoding=WINDOWS;
    OnInitXMLport=BEGIN
                    ProjSetUp.GET;
                    ProjSetUp.TESTFIELD("Plant Invoices Journal Templ.");
                    ProjSetUp.TESTFIELD("Plant Invoices Batch Name");
                  END;

    OnPreXMLport=BEGIN
                   PostRec.LOCKTABLE;
                   PostRec.SETRANGE("Journal Template Name",ProjSetUp."Plant Invoices Journal Templ.");
                   PostRec.SETRANGE("Journal Batch Name",ProjSetUp."Plant Invoices Batch Name");
                   IF PostRec.FIND('+') THEN
                     LineCounter := PostRec."Line No."
                   ELSE
                     LineCounter := 0;
                 END;

    Format=Variable Text;
    UseRequestPage=No;
  }
  ELEMENTS
  {
    { [{102CE9E5-CDE7-4DC0-BC4E-4F130C14116E}];  ;Root                ;Element ;Text     }

    { [{F00DD5EA-EAF3-4FC9-82C6-F68B8BEB000B}];1 ;GenJournalLine      ;Element ;Table   ;
                                                  SourceTable=Table81;
                                                  SourceTableView=SORTING(Field1,Field51,Field2);
                                                  Import::OnAfterInitRecord=BEGIN
                                                                              "Gen. Journal Line".INIT;
                                                                            END;

                                                  Import::OnBeforeInsertRecord=BEGIN
                                                                                 WITH "Gen. Journal Line" DO BEGIN
                                                                                   CostObjectLine := "Shortcut Dimension 2 Code";
                                                                                   DescriptionLine := Description;
                                                                                   FinishedProject := '';
                                                                                   "Journal Template Name" := ProjSetUp."Plant Invoices Journal Templ.";
                                                                                   "Journal Batch Name" := ProjSetUp."Plant Invoices Batch Name";
                                                                                   "Line No." := LineCounter + 10000;
                                                                                   LineCounter := LineCounter + 10000;
                                                                                   "Account Type" := "Account Type"::"G/L Account";
                                                                                   JnlTemplRec.GET(ProjSetUp."Plant Invoices Journal Templ.");
                                                                                   JnlTemplRec.TESTFIELD("No. Series");
                                                                                   "Document No." := NoSeriesMgt.GetNextNo(JnlTemplRec."No. Series",0D,FALSE);

                                                                                   "Document Date" := "Posting Date";
                                                                                   DimMgt.GetDimValueRec(2,"Shortcut Dimension 2 Code",DimValRec,FALSE,'');

                                                                                   RecRef.GETTABLE("Gen. Journal Line");
                                                                                   XMLportStatusDialogMgt.ShowImportStatus(RecRef);

                                                                                   IF "Job No." <> '' THEN BEGIN
                                                                                     ProjRec.GET("Job No.");

                                                                                     IF (ProjSetUp."Provisions at Closure") THEN BEGIN
                                                                                       IF ProjRec."Project Status" >= ProjRec."Project Status"::Archive THEN BEGIN
                                                                                         DetermineFinishedProjectCode;
                                                                                       END;
                                                                                     END ELSE BEGIN
                                                                                       IF ProjRec."Project Status" >= ProjRec."Project Status"::Finished THEN BEGIN
                                                                                         DetermineFinishedProjectCode;
                                                                                       END;
                                                                                     END;
                                                                                     //VALIDATE("Job No.");
                                                                                     "Job No." := '';  //* Force execute OnValidate (first check 'if same as in xRec then Exit')
                                                                                     VALIDATE("Job No.", ProjRec."No.");  //* So first make empty then restore
                                                                                   END;

                                                                                   IF "Shortcut Dimension 2 Code" <> CostObjectLine THEN  //* Can be modified in validate Job
                                                                                     "Shortcut Dimension 2 Code" := CostObjectLine;
                                                                                   VALIDATE("Shortcut Dimension 2 Code");
                                                                                   VALIDATE(Amount);
                                                                                   VALIDATE("VAT Prod. Posting Group",'');
                                                                                   IF (Description <> DescriptionLine) AND (DescriptionLine <> '') THEN  //* Can be modified in validates
                                                                                     Description := DescriptionLine;
                                                                                   "Source Code" := JnlTemplRec."Source Code";
                                                                                   "Reason Code" := JnlTemplRec."Reason Code";
                                                                                   "Unit of Measure Code" := DimValRec."Unit of Measure";
                                                                                   IF FinishedProject <> '' THEN
                                                                                     Description := COPYSTR(STRSUBSTNO('%1 Project %2',Description,FinishedProject), 1, MAXSTRLEN(Description));
                                                                                   IF ("Job No." <> '') AND (Element = '') THEN
                                                                                     Element := GetStandardProjElement();
                                                                                 END;
                                                                               END;
                                                                                }

    { [{8876DF8E-21C8-4299-855B-C051E9209F25}];2 ;PostingDate         ;Element ;Field   ;
                                                  DataType=Date;
                                                  SourceField=Gen. Journal Line::Posting Date;
                                                  MinOccurs=Zero }

    { [{07A77B94-3039-4A55-8395-0F519323D49D}];2 ;DocumentNo          ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=Gen. Journal Line::Document No.;
                                                  MinOccurs=Zero }

    { [{F953774C-42FB-4A02-B32A-4C2BC0B20E77}];2 ;JobNo               ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=Gen. Journal Line::Job No.;
                                                  MinOccurs=Zero }

    { [{8D20D99F-C043-4014-99F9-2D8168CE5AFA}];2 ;ShortcutDimension2Code;Element;Field  ;
                                                  DataType=Code;
                                                  SourceField=Gen. Journal Line::Shortcut Dimension 2 Code;
                                                  MinOccurs=Zero }

    { [{91348C4B-EF62-43D5-80A5-5D3C0FDE10AD}];2 ;BalAccountNo        ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=Gen. Journal Line::Bal. Account No.;
                                                  MinOccurs=Zero }

    { [{1618ADCD-E539-4434-96F4-73AB67A302B5}];2 ;Description         ;Element ;Field   ;
                                                  DataType=Text;
                                                  SourceField=Gen. Journal Line::Description;
                                                  MinOccurs=Zero }

    { [{4BCB0AF3-9535-4769-BEC3-B7FBE5DA573B}];2 ;Amount              ;Element ;Field   ;
                                                  DataType=Decimal;
                                                  SourceField=Gen. Journal Line::Amount;
                                                  MinOccurs=Zero }

    { [{5B970EED-1785-41ED-A8F2-67DEA04E26FB}];2 ;Element             ;Element ;Field   ;
                                                  DataType=Code;
                                                  SourceField=Gen. Journal Line::Element;
                                                  MinOccurs=Zero }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      ProjSetUp@1100525011 : Record 315;
      PostRec@1100525010 : Record 81;
      JnlTemplRec@1100525009 : Record 80;
      ProjRec@1100525008 : Record 11072003;
      PtypRec@1100525007 : Record 11012009;
      DimValRec@1100525006 : Record 349;
      RecRef@1100525012 : RecordRef;
      XMLportStatusDialogMgt@1100525013 : Codeunit 11130006;
      DimMgt@1100525005 : Codeunit 408;
      NoSeriesMgt@1100525004 : Codeunit 396;
      LineCounter@1100525003 : Integer;
      FinishedProject@1100525002 : Code[20];
      CostObjectLine@1100525001 : Code[20];
      DescriptionLine@1100525000 : Text[100];

    PROCEDURE GetStandardProjElement@1210190002() RetElement@1210190002 : Code[20];
    VAR
      lvProjElementRec@1210190000 : Record 11012010;
      lvText001@1210190001 : TextConst 'ENU=Standard element of project %1 not present;SVE=Standardelement f”r projektet %1 finns inte';
      lvProjRec@1100485000 : Record 11072003;
    BEGIN
      RetElement := '';
      lvProjRec.GET("Gen. Journal Line"."Job No.");   //**4PS01.n
      IF NOT lvProjRec."Posting Element Mandatory" THEN
        EXIT;

      lvProjElementRec.SETRANGE("Project No.", "Gen. Journal Line"."Job No.");
      lvProjElementRec.SETRANGE(Level, 0);
      IF lvProjElementRec.FIND('-') THEN
        RetElement := lvProjElementRec.Element;

      IF RetElement = '' THEN
        ERROR(lvText001, "Gen. Journal Line"."Job No.");
    END;

    PROCEDURE DetermineFinishedProjectCode@1210190000();
    BEGIN
      FinishedProject := "Gen. Journal Line"."Job No.";
      ProjRec.TESTFIELD("Project Type");
      PtypRec.GET(ProjRec."Project Type");
      PtypRec.TESTFIELD("Project No.After Closure Costs");
      "Gen. Journal Line"."Job No." := PtypRec."Project No.After Closure Costs";
      ProjRec.GET("Gen. Journal Line"."Job No.");
    END;

    BEGIN
    {
      CC: Dataport 11012551

      4PS01 JD, 8 aug 2007, Call 5990, "Posting Element Mandatory" also on project card
    }
    END.
  }
}

