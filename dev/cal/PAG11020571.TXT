OBJECT Page 11020571 Department Authoriz. Objects
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Kostenstellenbearbeitung Objekte;
               ENU=Department Authorization Objects;
               NLD=Kostenplaatsautorisatie objecten;
               NOR=Avdeling - godkjenning, objekter;
               SVE=Avdelningsbehîrighet, objekt];
    SourceTable=Table11012466;
    PageType=List;
    UsageCategory=Tasks;
    OnInit=BEGIN
             "FlowFilter Auth. FromVisible" := TRUE;
             TypeVisible := TRUE;
           END;

    OnOpenPage=VAR
                 UpdateExtTableProperties@1100529200 : Codeunit 11020226;
               BEGIN
                 CurrPage.EDITABLE(NOT CurrPage.LOOKUPMODE);
                 TypeVisible := (NOT CurrPage.LOOKUPMODE);
                 "FlowFilter Auth. FromVisible" := (NOT CurrPage.LOOKUPMODE);
                 UpdateExtTableProperties.RUN;
               END;

    OnNewRecord=BEGIN
                  Type := xRec.Type;
                END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1100485009;1 ;ActionGroup;
                      CaptionML=[DEU=F&unktion;
                                 ENU=F&unctions;
                                 NLD=F&uncties;
                                 NOR=Fu&nksjon;
                                 SVE=Fu&nktion] }
      { 1100485010;2 ;Action    ;
                      ShortCutKey=F9;
                      CaptionML=[DEU=Initialisieren;
                                 ENU=Initialize;
                                 NLD=Initiâren;
                                 NOR=Initierˇ...\;
                                 SVE=Initieraˇ...\];
                      Promoted=Yes;
                      Image=Start;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 Initialize;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100485000;1;Group  ;
                GroupType=Repeater }

    { 1100485001;2;Field  ;
                SourceExpr=Type;
                Visible=TypeVisible }

    { 1100485003;2;Field  ;
                SourceExpr=ID }

    { 1100485005;2;Field  ;
                DrillDown=No;
                SourceExpr=Name;
                Visible=FALSE }

    { 1100485007;2;Field  ;
                DrillDown=No;
                SourceExpr=Caption }

    { 1100485016;2;Field  ;
                SourceExpr="Dimension 1 Code Filter" }

    { 1100525000;2;Field  ;
                SourceExpr="Allow Empty Code" }

    { 1100525002;2;Field  ;
                SourceExpr="FlowFilter Auth. From";
                Visible="FlowFilter Auth. FromVisible" }

  }
  CODE
  {
    VAR
      SourceTableTmpRec@1100485000 : TEMPORARY Record 2000000001;
      WindowDlg@1100485001 : Dialog;
      Text000@1100485007 : TextConst 'DEU=Initiieren...\;ENU=Initialize...\;NLD=Initiâren ...\;NOR=Initierˇ...\;SVE=Initieraˇ...\';
      Text001@1100485002 : TextConst 'DEU=HinzufÅgren Tabellen @1@@@@@@@@@\;ENU=Add Tables @1@@@@@@@@@\;NLD=Toevoegen tabellen @1@@@@@@@@@\;NOR=Legg til tabeller @1@@@@@@@@@\;SVE=LÑgg till tabellerna @1@@@@@@@@@\';
      Text002@1100485003 : TextConst 'DEU=Seiten vorbereiten @2@@@@@@@@@\;ENU=Prepare Pages @2@@@@@@@@@\;NLD=Voorbereiden pages @2@@@@@@@@@\;SVE=Fîrbered formulÑr @2@@@@@@@@@\';
      Text003@1100485004 : TextConst 'DEU=Seiten hinzufÅgen @3@@@@@@@@@\;ENU=Add Pages    @3@@@@@@@@@\;NLD=Toevoegen pages    @3@@@@@@@@@;SVE=LÑgg till formulÑr    @3@@@@@@@@@\';
      TotalNr@1100485005 : Integer;
      CurrNr@1100485006 : Integer;
      TypeVisible@19063733 : Boolean INDATASET;
      "FlowFilter Auth. FromVisible"@19051094 : Boolean INDATASET;

    PROCEDURE Initialize@1100485000();
    VAR
      ExtTableProp@1100485000 : Record 11020573;
      DepartmentAuthorizObjectRec@1100485001 : Record 11012466;
      DepartmentAuthorizObjectRec2@1100485002 : Record 11012466;
    BEGIN
      WindowDlg.OPEN(Text000+Text001+Text002+Text003);
      ConvertFormTypeToPageType;
      //*26863.sn   Based on Table 'Extended Table Property'
      WITH ExtTableProp DO BEGIN
        TotalNr := COUNT;
        CurrNr := 0;
        IF FINDFIRST THEN BEGIN
          REPEAT
            CurrNr += 1;
            WindowDlg.UPDATE(1,ROUND(10000 * CurrNr/TotalNr,1));
            IF (ExtTableProp."DPA Field No." <> 0) OR (ExtTableProp."DPA Filter Field No." <> 0) THEN BEGIN
              DepartmentAuthorizObjectRec.Type := DepartmentAuthorizObjectRec.Type::Table;
              DepartmentAuthorizObjectRec.ID := "Table ID";
              DepartmentAuthorizObjectRec."Dimension 1 Code Filter" := DepartmentAuthorizObjectRec."Dimension 1 Code Filter"::None;
              IF DepartmentAuthorizObjectRec.INSERT THEN;
            END;
          UNTIL NEXT = 0;
        END;
      END;
      //*26863.en

      FillTempTable;

      DepartmentAuthorizObjectRec.SETRANGE(Type,DepartmentAuthorizObjectRec.Type::Table);

      TotalNr := DepartmentAuthorizObjectRec.COUNT;
      CurrNr := 0;

      IF DepartmentAuthorizObjectRec.FIND('-') THEN
        REPEAT
          CurrNr += 1;
          WindowDlg.UPDATE(3,ROUND(10000 * CurrNr/TotalNr,1));

          SourceTableTmpRec.SETRANGE(Type,SourceTableTmpRec.Type::Page);
          SourceTableTmpRec.SETRANGE("Company Name",FORMAT(DepartmentAuthorizObjectRec.ID));
          IF SourceTableTmpRec.FIND('-') THEN
            REPEAT
              DepartmentAuthorizObjectRec2.Type := DepartmentAuthorizObjectRec.Type::Page;
              DepartmentAuthorizObjectRec2.ID := SourceTableTmpRec.ID;
              DepartmentAuthorizObjectRec2."Dimension 1 Code Filter" := DepartmentAuthorizObjectRec."Dimension 1 Code Filter"::None;
              IF DepartmentAuthorizObjectRec2.INSERT THEN;
            UNTIL SourceTableTmpRec.NEXT = 0;
        UNTIL DepartmentAuthorizObjectRec.NEXT = 0;

      WindowDlg.CLOSE;
    END;

    PROCEDURE FillTempTable@1100485001() : Integer;
    VAR
      ObjectRec@1100485000 : Record 2000000001;
    BEGIN
      SourceTableTmpRec.RESET;
      SourceTableTmpRec.DELETEALL;

      WITH ObjectRec DO BEGIN
        SETRANGE(Type,Type::Page);

        TotalNr := COUNT;
        CurrNr := 0;

        IF FIND('-') THEN
          REPEAT
            CurrNr += 1;
            WindowDlg.UPDATE(2,ROUND(10000 * CurrNr/TotalNr,1));

            SourceTableTmpRec.Type := SourceTableTmpRec.Type::Page;
            SourceTableTmpRec."Company Name" := FORMAT(GetTableNo(ObjectRec));
            SourceTableTmpRec.ID := ID;
            SourceTableTmpRec.INSERT;
          UNTIL NEXT = 0;
      END;
    END;

    PROCEDURE GetTableNo@1100485002(IObjectRec@1100485000 : Record 2000000001) NextInt : Integer;
    VAR
      InStr@1100485002 : InStream;
    BEGIN
      WITH IObjectRec DO BEGIN
        CALCFIELDS("BLOB Reference");
        "BLOB Reference".CREATEINSTREAM(InStr);
        WHILE NOT InStr.EOS DO BEGIN
          InStr.READ(NextInt);
          IF (NextInt = 283173) AND (NOT InStr.EOS) THEN BEGIN
            InStr.READ(NextInt);
            EXIT(NextInt);
          END;
        END;
      END;
    END;

    LOCAL PROCEDURE ConvertFormTypeToPageType@1100525002();
    VAR
      DepartmentAuthorizObject@1100525000 : Record 11012466;
      DepartmentAuthorizObjectNew@1100525001 : Record 11012466;
    BEGIN
      DepartmentAuthorizObject.SETRANGE(Type, DepartmentAuthorizObject.Type::Form);
      IF DepartmentAuthorizObject.FINDSET THEN
        REPEAT
          DepartmentAuthorizObjectNew := DepartmentAuthorizObject;
          DepartmentAuthorizObjectNew.Type := DepartmentAuthorizObjectNew.Type::Page;
          IF DepartmentAuthorizObjectNew.INSERT THEN;
        UNTIL DepartmentAuthorizObject.NEXT = 0;

      DepartmentAuthorizObject.DELETEALL;
    END;

    BEGIN
    END.
  }
}

