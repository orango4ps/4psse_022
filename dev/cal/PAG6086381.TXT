OBJECT Page 6086381 EM Default Dimensions-Multiple
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EMW16.00.10.3.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Vorgabedimensionen - Mehrfach;
               ENU=Default Dimensions-Multiple;
               NLD=Standaarddimensies - Multi];
    SourceTable=Table6086368;
    DataCaptionExpr=GetCaption;
    PageType=List;
    SourceTableTemporary=Yes;
    OnInit=BEGIN
             CurrPage.LOOKUPMODE := TRUE;
           END;

    OnOpenPage=BEGIN
                 GetDefaultDim;
               END;

    OnAfterGetRecord=BEGIN
                       DimensionValueCodeOnFormat(FORMAT("Dimension Value Code"));
                       ValuePostingOnFormat(FORMAT("Value Posting"));
                     END;

    OnInsertRecord=BEGIN
                     SETRANGE("Dimension Code","Dimension Code");
                     IF NOT FINDFIRST AND ("Dimension Code" <> '') THEN BEGIN
                       "Multi Selection Action" := "Multi Selection Action"::Change;
                       INSERT;
                     END;
                     SETRANGE("Dimension Code");
                     EXIT(FALSE);
                   END;

    OnModifyRecord=BEGIN
                     "Multi Selection Action" := "Multi Selection Action"::Change;
                     MODIFY;
                     EXIT(FALSE);
                   END;

    OnDeleteRecord=BEGIN
                     "Multi Selection Action" := "Multi Selection Action"::Delete;
                     MODIFY;
                     EXIT(FALSE);
                   END;

    OnQueryClosePage=BEGIN
                       IF CloseAction = ACTION::LookupOK THEN
                         LookupOKOnPush;
                     END;

  }
  CONTROLS
  {
    { 1900000001;;Container;
                Name=ContentArea;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                Name=Dimension Code;
                SourceExpr="Dimension Code";
                OnValidate=BEGIN
                             IF (xRec."Dimension Code" <> '') AND (xRec."Dimension Code" <> "Dimension Code") THEN
                               ERROR(Text000,TABLECAPTION);
                             DimensionCodeOnAfterValidate;
                           END;
                            }

    { 4   ;2   ;Field     ;
                Name=Dimension Value Code;
                SourceExpr="Dimension Value Code";
                OnValidate=BEGIN
                             DimensionValueCodeOnAfterValid;
                           END;
                            }

    { 6   ;2   ;Field     ;
                Name=Value Posting;
                SourceExpr="Value Posting";
                OnValidate=BEGIN
                             ValuePostingOnAfterValidate;
                           END;
                            }

  }
  CODE
  {
    VAR
      TempDefaultDim2@1003 : TEMPORARY Record 6086368;
      TempDefaultDim3@1004 : TEMPORARY Record 6086368;
      TotalRecNo@1005 : Integer;
      Text000@1000 : TextConst 'DEU=Sie k”nnen eine %1 nicht umbenennen.;ENU=You cannot rename a %1.;NLD=U kunt een %1 niet hernoemen.';
      Text001@1001 : TextConst 'DEU=(Konflikt);ENU=(Conflict);NLD=(Conflict)';

    PROCEDURE SetMultiExpType@1160040000(VAR ExpenseType@1000 : Record 6086307);
    BEGIN
      TempDefaultDim2.DELETEALL;
      WITH ExpenseType DO
        IF FINDFIRST THEN
          REPEAT
            CopyDefaultDimToDefaultDim(DATABASE::"Expense Type",Code);
          UNTIL NEXT = 0;
    END;

    LOCAL PROCEDURE SetCommonDefaultDim@2();
    VAR
      EMDefaultDim@1000 : Record 6086368;
    BEGIN
      SETRANGE("Multi Selection Action","Multi Selection Action"::Delete);
      IF FINDFIRST THEN
        REPEAT
          IF TempDefaultDim3.FINDFIRST THEN
            REPEAT
              IF EMDefaultDim.GET(TempDefaultDim3."Table ID",TempDefaultDim3."No.","Dimension Code") THEN
                EMDefaultDim.DELETE(TRUE);
            UNTIL TempDefaultDim3.NEXT = 0;
        UNTIL NEXT = 0;

      SETRANGE("Multi Selection Action","Multi Selection Action"::Change);
      IF FINDFIRST THEN
        REPEAT
          IF TempDefaultDim3.FINDFIRST THEN
            REPEAT
              IF EMDefaultDim.GET(TempDefaultDim3."Table ID",TempDefaultDim3."No.","Dimension Code") THEN BEGIN
                EMDefaultDim."Dimension Code" := "Dimension Code";
                EMDefaultDim."Dimension Value Code" := "Dimension Value Code";
                EMDefaultDim."Value Posting" := "Value Posting";
                EMDefaultDim.MODIFY(TRUE);
              END ELSE BEGIN
                EMDefaultDim.INIT;
                EMDefaultDim."Table ID" := TempDefaultDim3."Table ID";
                EMDefaultDim."No." := TempDefaultDim3."No.";
                EMDefaultDim."Dimension Code" := "Dimension Code";
                EMDefaultDim."Dimension Value Code" := "Dimension Value Code";
                EMDefaultDim."Value Posting" := "Value Posting";
                EMDefaultDim.INSERT(TRUE);
              END;
            UNTIL TempDefaultDim3.NEXT = 0;
        UNTIL NEXT = 0;
    END;

    LOCAL PROCEDURE CopyDefaultDimToDefaultDim@14(TableID@1000 : Integer;No@1001 : Code[20]);
    VAR
      DefaultDim@1002 : Record 6086368;
    BEGIN
      TotalRecNo := TotalRecNo + 1;
      TempDefaultDim3."Table ID" := TableID;
      TempDefaultDim3."No." := No;
      TempDefaultDim3.INSERT;

      DefaultDim.SETRANGE("Table ID",TableID);
      DefaultDim.SETRANGE("No.",No);
      IF DefaultDim.FINDFIRST THEN
        REPEAT
          TempDefaultDim2 := DefaultDim;
          TempDefaultDim2.INSERT;
        UNTIL DefaultDim.NEXT = 0;
    END;

    LOCAL PROCEDURE GetDefaultDim@15();
    VAR
      Dim@1000 : Record 348;
      RecNo@1001 : Integer;
    BEGIN
      DELETEALL;
      IF Dim.FINDFIRST THEN
        REPEAT
          RecNo := 0;
          TempDefaultDim2.SETRANGE("Dimension Code",Dim.Code);
          SETRANGE("Dimension Code",Dim.Code);
          IF TempDefaultDim2.FINDFIRST THEN
            REPEAT
              IF FINDFIRST THEN BEGIN
                IF "Dimension Value Code" <> TempDefaultDim2."Dimension Value Code" THEN
                  IF ("Multi Selection Action" <> 10) AND
                     ("Multi Selection Action" <> 21)
                  THEN BEGIN
                    "Multi Selection Action" := "Multi Selection Action" + 10;
                    "Dimension Value Code" := '';
                  END;
                IF "Value Posting" <> TempDefaultDim2."Value Posting" THEN
                  IF ("Multi Selection Action" <> 11) AND
                     ("Multi Selection Action" <> 21)
                  THEN BEGIN
                    "Multi Selection Action" := "Multi Selection Action" + 11;
                    "Value Posting" := "Value Posting"::" ";
                  END;
                MODIFY;
                RecNo := RecNo + 1;
              END ELSE BEGIN
                Rec := TempDefaultDim2;
                INSERT;
                RecNo := RecNo + 1;
              END;
            UNTIL TempDefaultDim2.NEXT = 0;
          IF FINDFIRST AND (RecNo <> TotalRecNo) THEN
            IF ("Multi Selection Action" <> 10) AND
               ("Multi Selection Action" <> 21)
            THEN BEGIN
              "Multi Selection Action" := "Multi Selection Action" + 10;
              "Dimension Value Code" := '';
              MODIFY;
            END;
        UNTIL Dim.NEXT = 0;
      RESET;
      SETCURRENTKEY("Dimension Code");
      SETCURRENTKEY("Dimension Code");
      SETFILTER("Multi Selection Action",'<>%1',"Multi Selection Action"::Delete);
    END;

    LOCAL PROCEDURE DimensionCodeOnAfterValidate@19005427();
    BEGIN
      IF NOT ISSERVICETIER THEN
        CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE DimensionValueCodeOnAfterValid@19017072();
    BEGIN
      IF NOT ISSERVICETIER THEN
        CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE ValuePostingOnAfterValidate@19043761();
    BEGIN
      IF NOT ISSERVICETIER THEN
        CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE LookupOKOnPush@19031339();
    BEGIN
      SetCommonDefaultDim;
    END;

    LOCAL PROCEDURE DimensionValueCodeOnFormat@19000225(Text@19073275 : Text[1024]);
    BEGIN
      IF "Dimension Code" <> '' THEN
        IF ("Multi Selection Action" = 10) OR
           ("Multi Selection Action" = 21)
        THEN
          Text := Text001;
    END;

    LOCAL PROCEDURE ValuePostingOnFormat@19024282(Text@19070135 : Text[1024]);
    BEGIN
      IF "Dimension Code" <> '' THEN
        IF ("Multi Selection Action" = 11) OR
           ("Multi Selection Action" = 21)
        THEN
          Text := Text001;
    END;

    BEGIN
    END.
  }
}

