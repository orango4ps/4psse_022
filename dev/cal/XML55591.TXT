OBJECT XMLport 55591 dOM Changed Object List
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=t.om;
  }
  PROPERTIES
  {
    CaptionML=ENU=Changed Object List;
    Direction=Import;
    OnInitXMLport=BEGIN
                    Changed := TRUE;
                    New := TRUE;
                    Deleted := FALSE;
                  END;

    Format=Fixed Text;
  }
  ELEMENTS
  {
    { [{AFF90B98-5A38-472C-81C0-8891F97724E7}];  ;root                ;Element ;Text     }

    { [{DCEF7385-CBCC-4A33-9A7D-171F9AFA1E2D}];1 ;object              ;Element ;Table   ;
                                                  SourceTable=Table2000000001;
                                                  SourceTableView=SORTING(Field1,Field2,Field3)
                                                                  WHERE(Field1=FILTER(1..));
                                                  AutoSave=No }

    { [{379B7E87-F18A-49F5-97A4-9AC3AE8C5EA3}];2 ;TextField           ;Element ;Text    ;
                                                  Import::OnAfterAssignVariable=VAR
                                                                                  ObjectNo@1100409000 : Integer;
                                                                                  Position@1000 : Integer;
                                                                                  Length@1001 : Integer;
                                                                                BEGIN
                                                                                  Position := STRPOS(TextField,':');

                                                                                  ActionType := COPYSTR(TextField,1,Position -1);
                                                                                  IF (ActionType = 'changed') AND NOT Changed THEN
                                                                                    EXIT;

                                                                                  IF (ActionType = 'new') AND NOT New THEN
                                                                                    EXIT;

                                                                                  IF (ActionType = 'deleted') AND NOT Deleted THEN
                                                                                    EXIT;

                                                                                  Length := STRPOS(TextField,'.txt') - (Position + 5);
                                                                                  EVALUATE(ObjectNo,COPYSTR(TextField,Position + 5,Length));

                                                                                  IF MarkedObject.GET(ConvertObjectType(COPYSTR(TextField,Position + 2,3)), '', ObjectNo) THEN
                                                                                    MarkedObject.MARK(TRUE);
                                                                                END;

                                                  Export::OnBeforePassVariable=BEGIN
                                                                                 //TextField := STRSUBSTNO('%1%2.txt',FORMAT(Object.Type, 3), Object.ID);
                                                                                 //TextField := STRSUBSTNO('%1%2.txt - %3',FORMAT(Object.Type, 3), Object.ID, Object."Version List");
                                                                                 TextField := STRSUBSTNO('%1: %2%3.txt', ActionType, FORMAT(Object.Type, 3), Object.ID);
                                                                               END;

                                                  Width=1024 }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1   ;    ;Container ;
                  Name=wfw;
                  ContainerType=ContentArea }

      { 2   ;1   ;Field     ;
                  CaptionML=ENU=New Objects;
                  SourceExpr=New }

      { 3   ;1   ;Field     ;
                  CaptionML=ENU=Changed Objects;
                  SourceExpr=Changed }

      { 4   ;1   ;Field     ;
                  CaptionML=ENU=Deleted Objects;
                  SourceExpr=Deleted }

    }
  }
  CODE
  {
    VAR
      MarkedObject@1100409000 : Record 2000000001;
      ActionType@1000 : Text[20];
      Changed@1001 : Boolean;
      New@1002 : Boolean;
      Deleted@1003 : Boolean;

    PROCEDURE CopyObjectTableMarks@1100409000(VAR Object2@1100409000 : Record 2000000001);
    BEGIN
      Object2.CLEARMARKS;

      MarkedObject.MARKEDONLY(TRUE);
      IF NOT MarkedObject.FINDSET THEN
        EXIT;

      REPEAT
        Object2.GET(MarkedObject.Type, MarkedObject."Company Name", MarkedObject.ID);
        Object2.MARK(TRUE);
      UNTIL MarkedObject.NEXT = 0;
    END;

    LOCAL PROCEDURE ConvertObjectType@1100409001(TypeTxt@1100409000 : Text[3]) : Integer;
    VAR
      Object2@1100409001 : Record 2000000001;
    BEGIN
      CASE LOWERCASE(TypeTxt) OF
        'tab': EXIT(Object2.Type::Table);
        'rep': EXIT(Object2.Type::Report);
        'cod': EXIT(Object2.Type::Codeunit);
        'xml': EXIT(Object2.Type::XMLport);
        'men': EXIT(Object2.Type::MenuSuite);
        'pag': EXIT(Object2.Type::Page);
        'que': EXIT(Object2.Type::Query);
      END;
    END;

    BEGIN
    END.
  }
}

