OBJECT Table 11012905 Project CF Companies Cluster
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    DataPerCompany=No;
    OnDelete=VAR
               ProjectCFCompany@1100527700 : Record 11012903;
             BEGIN
               ProjectCFCompany.SETRANGE("Cluster Code",Code);
               ProjectCFCompany.DELETEALL(TRUE);
             END;

    CaptionML=ENU=Project CF Companies Cluster;
  }
  FIELDS
  {
    { 10  ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              SVE=Kod];
                                                   NotBlank=Yes }
    { 20  ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              SVE=Beskrivning] }
    { 30  ;   ;Reference Date      ;Date          ;DataClassification=ToBeClassified;
                                                   CaptionML=[ENU=Reference Date;
                                                              SVE=Referensdatum] }
    { 40  ;   ;Expected Outcome    ;Option        ;DataClassification=ToBeClassified;
                                                   CaptionML=ENU=Expected Outcome;
                                                   OptionCaptionML=ENU=Normal,Best Case,Worst Case;
                                                   OptionString=Normal,Best Case,Worst Case }
    { 50  ;   ;Last Updated        ;DateTime      ;CaptionML=[ENU=Last Updated;
                                                              SVE=Senast uppdaterad];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE BuildMatrix@1100527700();
    VAR
      ProjectCFProjectsDetail@1100527709 : Record 11012643;
      ProjectCFCompaniesDetail@1100527708 : Record 11012904;
      ProjectCFCompanies@1100527705 : Record 11012903;
      ProjectCFProjects@1100527704 : Record 11012642;
      ProjectCFCompaniesNormalTotal@1100527700 : Record 11012903;
      ProjectCFCompaniesBestTotal@1100527702 : Record 11012903;
      ProjectCFCompaniesWorstTotal@1100527703 : Record 11012903;
      ProjectCFProjectsSetup@1100527701 : Record 11012639;
    BEGIN
      ProjectCFCompaniesDetail.SETRANGE("Cluster Code",Code);
      ProjectCFCompaniesDetail.DELETEALL;

      ProjectCFCompanies.SETRANGE("Cluster Code",Code);
      ProjectCFCompanies.SETRANGE(Type,ProjectCFCompanies.Type::Total);
      ProjectCFCompanies.DELETEALL;

      ProjectCFCompanies.SETRANGE(Type,ProjectCFCompanies.Type::Company);
      IF ProjectCFCompanies.FINDSET THEN
        REPEAT
          ProjectCFCompanies.INIT;
          ProjectCFProjects.CHANGECOMPANY(ProjectCFCompanies."Company Name");
          ProjectCFProjects.SETRANGE("Selected Expected Outcome",ProjectCFCompanies."Selected Expected Outcome");
          ProjectCFProjects.SETRANGE(Type,ProjectCFProjects.Type::Total);
          IF ProjectCFProjects.FINDFIRST THEN BEGIN
            AddAmountsToTotalLine(ProjectCFProjects,ProjectCFCompanies."Selected Expected Outcome",ProjectCFCompanies);

            CASE ProjectCFCompanies."Selected Expected Outcome" OF
              ProjectCFCompanies."Selected Expected Outcome"::Normal:
                AddAmountsToTotalLine(ProjectCFProjects,ProjectCFCompanies."Selected Expected Outcome",ProjectCFCompaniesNormalTotal);
              ProjectCFCompanies."Selected Expected Outcome"::"Best Case":
                AddAmountsToTotalLine(ProjectCFProjects,ProjectCFCompanies."Selected Expected Outcome",ProjectCFCompaniesBestTotal);
              ProjectCFCompanies."Selected Expected Outcome"::"Worst Case":
                AddAmountsToTotalLine(ProjectCFProjects,ProjectCFCompanies."Selected Expected Outcome",ProjectCFCompaniesWorstTotal);
            END;
          END;

          ProjectCFProjectsSetup.CHANGECOMPANY(ProjectCFCompanies."Company Name");
          IF NOT ProjectCFProjectsSetup.GET THEN
            ProjectCFProjectsSetup.INIT;

          ProjectCFCompanies."Last Updated" := ProjectCFProjectsSetup."Last Updated";
          ProjectCFCompanies.MODIFY;

          ProjectCFProjectsDetail.CHANGECOMPANY(ProjectCFCompanies."Company Name");
          ProjectCFProjectsDetail.SETRANGE("Selected Expected Outcome",ProjectCFCompanies."Selected Expected Outcome");
          IF ProjectCFProjectsDetail.FINDSET THEN
            REPEAT
              ProjectCFCompaniesDetail.INIT;
              ProjectCFCompaniesDetail."Cluster Code" := ProjectCFCompanies."Cluster Code";
              ProjectCFCompaniesDetail."Selected Expected Outcome" := ProjectCFCompanies."Selected Expected Outcome";
              ProjectCFCompaniesDetail."Company Name" := ProjectCFCompanies."Company Name";
              ProjectCFCompaniesDetail."Project No." := ProjectCFProjectsDetail."Project No.";
              ProjectCFCompaniesDetail."Cash Flow" := ProjectCFProjectsDetail."Cash Flow";
              ProjectCFCompaniesDetail.Rubric := ProjectCFProjectsDetail.Rubric;
              ProjectCFCompaniesDetail."Line No." := ProjectCFProjectsDetail."Line No.";
              ProjectCFCompaniesDetail.Date := ProjectCFProjectsDetail.Date;
              ProjectCFCompaniesDetail.Amount := ProjectCFProjectsDetail.Amount;
              ProjectCFCompaniesDetail.INSERT;
            UNTIL ProjectCFProjectsDetail.NEXT = 0;
        UNTIL ProjectCFCompanies.NEXT = 0;

      //Totals
      InsertTotalLine(ProjectCFCompaniesNormalTotal);
      InsertTotalLine(ProjectCFCompaniesBestTotal);
      InsertTotalLine(ProjectCFCompaniesWorstTotal);

      "Last Updated" := CURRENTDATETIME;
      MODIFY;
    END;

    LOCAL PROCEDURE AddAmountsToTotalLine@1100527704(ProjectCFProjects@1100527700 : Record 11012642;ExpectedOutcome@1100527701 : Integer;VAR ProjectCFCompaniesTotal@1100527702 : Record 11012903);
    BEGIN
      ProjectCFCompaniesTotal."Selected Expected Outcome" := ExpectedOutcome;
      ProjectCFCompaniesTotal."Actual Amount" += ProjectCFProjects."Actual Amount";
      ProjectCFCompaniesTotal."Open(Purchase)" += ProjectCFProjects."Open(Purchase)";
      ProjectCFCompaniesTotal."Prognosis Amount" += ProjectCFProjects."Prognosis Amount";
      ProjectCFCompaniesTotal."Prognosis Total Cost" += ProjectCFProjects."Prognosis Total Cost";
      ProjectCFCompaniesTotal."Total Cost" += ProjectCFProjects."Total Cost";
      ProjectCFCompaniesTotal.Remaining += ProjectCFProjects.Remaining;
    END;

    LOCAL PROCEDURE InsertTotalLine@1100527706(VAR ProjectCFCompaniesTotal@1100527700 : Record 11012903);
    BEGIN
      ProjectCFCompaniesTotal.Type := ProjectCFCompaniesTotal.Type::Total;
      ProjectCFCompaniesTotal."Cluster Code" := Code;
      ProjectCFCompaniesTotal.INSERT;
    END;

    BEGIN
    END.
  }
}

