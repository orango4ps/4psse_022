OBJECT Table 11229332 Vendor by Purchase Requisition
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               PurchaseRequisition.GET("Purchase Requisition No.");
               "Document Template" := PurchaseRequisition."Document Template";
             END;

    CaptionML=ENU=Vendor by Purchase Requisition;
    LookupPageID=Page11126655;
  }
  FIELDS
  {
    { 5   ;   ;Purchase Requisition No.;Code20    ;TableRelation="Purchase Requisition";
                                                   CaptionML=[ENU=Purchase Requisition No.;
                                                              SVE=Ink”psrekvisition nr] }
    { 20  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF ("Vendor No." <> '') THEN
                                                                  VALIDATE("Vendor No.");
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              NOR=Radtype;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=[ENU=Vendor,Contact;
                                                                    NOR=Leverand›r,Kontakt;
                                                                    SVE=Leverant”rs,Kontakt];
                                                   OptionString=Vendor,Contact }
    { 30  ;   ;Vendor No.          ;Code20        ;TableRelation=IF (Type=CONST(Vendor)) Vendor
                                                                 ELSE IF (Type=CONST(Contact)) Contact WHERE (Person No.=FILTER(''));
                                                   OnValidate=VAR
                                                                StrategicPurchMgt@1100529800 : Codeunit 11020211;
                                                              BEGIN
                                                                IF "Vendor No." <> xRec."Vendor No." THEN BEGIN
                                                                  "Contact Person No." := '';
                                                                  Contact := '';
                                                                END;

                                                                IF (Type = Type::Vendor) AND ("Vendor No." <> '') THEN BEGIN
                                                                  IF VendorRec.GET("Vendor No.") THEN BEGIN
                                                                    IF StrategicPurchMgt.IsLimitedPurchaser(DocType::Order) AND
                                                                      NOT StrategicPurchMgt.VendorHasContract(VendorRec)
                                                                    THEN
                                                                      ERROR(STRSUBSTNO(Text003, "Vendor No."));

                                                                    IF VendorRec."Block Purchase Quote" THEN
                                                                      VendorRec.TESTFIELD("Block Purchase Quote", FALSE);
                                                                    IF (VendorRec.Blocked = VendorRec.Blocked::All) THEN
                                                                      MESSAGE(Text001, "Vendor No.");
                                                                    IF (VendorRec."Block Purchase Order" = TRUE) OR
                                                                        (VendorRec.Blocked = VendorRec.Blocked::Payment) THEN
                                                                      MESSAGE(Text002, "Vendor No.");
                                                                END;
                                                                  "Contact Person No." := VendorRec."Primary Contact No.";
                                                                  VALIDATE(Contact, VendorRec.Contact);
                                                                END;

                                                                CalcFieldsVendor();
                                                              END;

                                                   CaptionML=[ENU=Vendor No.;
                                                              NOR=Leverand›rnr.;
                                                              SVE=Leverant”rsnr];
                                                   NotBlank=Yes;
                                                   Description=Vendor or Contact, depending on Type }
    { 31  ;   ;Preferred Vendor    ;Boolean       ;OnValidate=BEGIN
                                                                CheckOtherVendors;  //DP01314
                                                              END;

                                                   CaptionML=[ENU=Preferred Vendor;
                                                              NOR=Foretrukket leverand›r;
                                                              SVE=F”rvald leverant”r] }
    { 40  ;   ;Vendor Name         ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor.Name WHERE (No.=FIELD(Vendor No.)));
                                                   CaptionML=[ENU=Name;
                                                              NOR=Navn;
                                                              SVE=Namn];
                                                   Editable=No }
    { 41  ;   ;Contact Name        ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Vendor No.)));
                                                   CaptionML=[ENU=Name (Contact);
                                                              NOR=Navn (kontakt);
                                                              SVE=Namn (kontakt)];
                                                   Editable=No }
    { 50  ;   ;Address             ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor.Address WHERE (No.=FIELD(Vendor No.)));
                                                   CaptionML=[ENU=Address;
                                                              NOR=Adresse;
                                                              SVE=Adress];
                                                   Editable=No }
    { 51  ;   ;Address Contact     ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Address WHERE (No.=FIELD(Vendor No.)));
                                                   CaptionML=[ENU=Address (Contact);
                                                              NOR=Adresse (kontakt);
                                                              SVE=Adress (kontakt)];
                                                   Editable=No }
    { 60  ;   ;City                ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor.City WHERE (No.=FIELD(Vendor No.)));
                                                   CaptionML=[ENU=City;
                                                              NOR=Poststed;
                                                              SVE=Ort];
                                                   Editable=No }
    { 61  ;   ;City Contact        ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.City WHERE (No.=FIELD(Vendor No.)));
                                                   CaptionML=[ENU=City (Contact);
                                                              NOR=City (kontakt);
                                                              SVE=Ort (kontakt)];
                                                   Editable=No }
    { 70  ;   ;Phone No.           ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor."Phone No." WHERE (No.=FIELD(Vendor No.)));
                                                   ExtendedDatatype=Phone No.;
                                                   CaptionML=[ENU=Phone No.;
                                                              NOR=Telefonnr.;
                                                              SVE=Telefonnr];
                                                   Editable=No }
    { 71  ;   ;Phone No. Contact   ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact."Phone No." WHERE (No.=FIELD(Vendor No.)));
                                                   ExtendedDatatype=Phone No.;
                                                   CaptionML=[ENU=Phone No. (Contact);
                                                              NOR=Telefonnr, (kontakt);
                                                              SVE=Telefonnr, (kontakt)];
                                                   Editable=No }
    { 72  ;   ;E-mail              ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor.E-Mail WHERE (No.=FIELD(Vendor No.)));
                                                   ExtendedDatatype=E-Mail;
                                                   CaptionML=[ENU=Email;
                                                              NOR=E-post;
                                                              SVE=E-post];
                                                   Editable=No }
    { 73  ;   ;E-mail Contact      ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.E-Mail WHERE (No.=FIELD(Vendor No.)));
                                                   ExtendedDatatype=E-Mail;
                                                   CaptionML=ENU=Email Contact;
                                                   Editable=No }
    { 80  ;   ;Post Code           ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor."Post Code" WHERE (No.=FIELD(Vendor No.)));
                                                   TableRelation="Post Code";
                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Post Code;
                                                              NOR=Postnr;
                                                              SVE=Postnr];
                                                   Editable=No }
    { 81  ;   ;Post Code Contact   ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact."Post Code" WHERE (No.=FIELD(Vendor No.)));
                                                   TableRelation="Post Code";
                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Post Code (Contact);
                                                              NOR=Postnummer, kontakt;
                                                              SVE=Postnr (kontakt)];
                                                   Editable=No }
    { 85  ;   ;Contract Type       ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor."Blanket Contract Type" WHERE (No.=FIELD(Vendor No.)));
                                                   CaptionML=[ENU=Contract Type;
                                                              NOR=Kontraktstype;
                                                              SVE=Kontraktstyp];
                                                   OptionCaptionML=[ENU=No Contract,Contract,Contract with Discount Terms;
                                                                    NOR=Ingen kontrakt,Kontrakt,Kontrakt med rabattvilk†r;
                                                                    SVE=Inget kontrakt,Kontrakt,Kontrakt med rabattvillkor];
                                                   OptionString=No Contract,Contract,Contract with Discount Terms;
                                                   Editable=No }
    { 87  ;   ;Blocked             ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor.Blocked WHERE (No.=FIELD(Vendor No.)));
                                                   CaptionML=[ENU=Blocked;
                                                              NOR=Blokkert;
                                                              SVE=Sp„rrad];
                                                   OptionCaptionML=[ENU=" ,Payment,All";
                                                                    NOR=" ,Betaling,Alt";
                                                                    SVE=" ,Betalning,Alla"];
                                                   OptionString=[ ,Payment,All];
                                                   Editable=No }
    { 88  ;   ;Block Purchase Order;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor."Block Purchase Order" WHERE (No.=FIELD(Vendor No.)));
                                                   CaptionML=[ENU=Blocked for Purchase Orders;
                                                              SVE=Ink”psorder sp„rrader];
                                                   Editable=No }
    { 89  ;   ;Block Purchase Quote;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Vendor."Block Purchase Quote" WHERE (No.=FIELD(Vendor No.)));
                                                   CaptionML=[ENU=Blocked for Purchase Quotes;
                                                              SVE=Ink”psofferter sp„rrade] }
    { 90  ;   ;Contact             ;Text100       ;OnValidate=VAR
                                                                Contact@1100528700 : Record 5050;
                                                                IsValidContactPersonNo@1100528701 : Boolean;
                                                              BEGIN
                                                                IsValidContactPersonNo := FALSE;

                                                                IF "Contact Person No." <> '' THEN
                                                                  IF Contact.GET("Contact Person No.") THEN
                                                                    IF Contact.Name = Rec.Contact THEN
                                                                      IsValidContactPersonNo := TRUE;

                                                                IF NOT IsValidContactPersonNo THEN
                                                                  "Contact Person No." := '';
                                                              END;

                                                   CaptionML=[ENU=Contact;
                                                              NOR=Kontakt;
                                                              SVE=Kontaktperson] }
    { 100 ;   ;Document Template   ;Code20        ;TableRelation="Document Template"."Template Code" WHERE (Type=CONST(Purchase Quote));
                                                   CaptionML=[ENU=Document Template;
                                                              NOR=Dokumentmal;
                                                              SVE=Dokumentmall] }
    { 110 ;   ;Contact Person No.  ;Code20        ;TableRelation=Contact.No.;
                                                   CaptionML=[ENU=Contact Person No.;
                                                              NOR=Kontaktperson;
                                                              SVE=Kontaktperson];
                                                   Editable=No }
    { 120 ;   ;Last Generated Document No.;Code20 ;CaptionML=[ENU=Last Generated Document No.;
                                                              NOR=Siste genererte dokumentnr.;
                                                              SVE=Senaste genererade dokumentnr];
                                                   Editable=No }
    { 130 ;   ;Order Address Code  ;Code10        ;TableRelation="Order Address".Code WHERE (Vendor No.=FIELD(Vendor No.));
                                                   CaptionML=[ENU=Order Address Code;
                                                              SVE=Ink”psadresskod] }
    { 999 ;   ;Activate for        ;Option        ;CaptionML=[ENU=Activate for;
                                                              NOR=Aktiver for;
                                                              SVE=Aktivera f”r];
                                                   OptionCaptionML=ENU=" ,Purchase Quote,Purchase Order";
                                                   OptionString=[ ,Quote,Order];
                                                   Description=DP01314 }
  }
  KEYS
  {
    {    ;Purchase Requisition No.,Type,Vendor No.;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      VendorRec@1210190000 : Record 23;
      Text001@1100525000 : TextConst 'ENU=Vendor %1 is blocked.;NOR=Leverand›r %1 er blokkert.;SVE=Leverant”r %1 har sp„rrats.';
      Text002@1100525001 : TextConst 'ENU=Vendor %1 is Blocked for Purchase.;NOR=Leverand›r %1 er blokkert for innkj›p.;SVE=Leverant”r %1 har sp„rrats f”r ink”p.';
      PurchaseRequisition@1100529400 : Record 11229330;
      Text003@1100529800 : TextConst 'ENU=Vendor %1 has no valid Contract.;SVE=Leverant”r %1 har inget giltigt kontrakt.';
      DocType@1100529801 : 'Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order';

    PROCEDURE CalcFieldsVendor@1100485000();
    VAR
      Contact@1100525000 : Record 5050;
    BEGIN
      CASE Type OF
        Type::Vendor:
          CALCFIELDS("Vendor Name", Address, City, "Post Code", "Phone No.", "E-mail","Contract Type");
        Type::Contact:
          BEGIN
            CALCFIELDS("Contact Name", "Address Contact", "City Contact", "Post Code Contact", "Phone No. Contact", "E-mail Contact");
            "Vendor Name" := "Contact Name";
            Address := "Address Contact";
            City := "City Contact";
            "Post Code" := "Post Code Contact";
            "Phone No." := "Phone No. Contact";
            "E-mail" := "E-mail Contact";
            "Contract Type" := "Contract Type"::"No Contract";
          END;
      END;
      IF "Contact Person No." <> '' THEN
        IF Contact.GET("Contact Person No.") THEN BEGIN
          IF Contact."Phone No." <> '' THEN
            "Phone No." := Contact."Phone No.";
          IF Contact."E-Mail" <> '' THEN
            "E-mail" := Contact."E-Mail";
        END;
    END;

    PROCEDURE CalcSumQuoteComparison@1100525000(ReturnValueType@1100525001 : ',Amount,TotalAmount,Quantity,TotalQuantity';CostObject@1100525002 : Code[20];UnitOfMeasure@1100525003 : Code[10];LineDescription@1100525004 : Text[50]) RetValue : Decimal;
    VAR
      QuoteLineRec@1100525000 : Record 39;
    BEGIN
      //* ReturnValueType: 1=Amount, 2=TotalAmount, 3=Quantity, 4=TotalQuantity
      RetValue := 0;
      QuoteLineRec.SETCURRENTKEY("Document Type", "Buy-from Vendor No.");
      QuoteLineRec.SETRANGE("Document Type", QuoteLineRec."Document Type"::Quote);
      IF (Type <> Type::Contact) THEN
        QuoteLineRec.SETRANGE("Buy-from Vendor No.", "Vendor No.")
      ELSE
        QuoteLineRec.SETRANGE("Buy-from Contact No.", "Vendor No.");
      QuoteLineRec.SETRANGE("Purchase Requisition No.", "Purchase Requisition No.");
      IF ReturnValueType IN [ReturnValueType::Amount, ReturnValueType::Quantity] THEN BEGIN
        QuoteLineRec.SETRANGE("Shortcut Dimension 2 Code", CostObject);
        QuoteLineRec.SETRANGE(Description, LineDescription);
        QuoteLineRec.SETRANGE("Unit of Measure Code", UnitOfMeasure);
      END;

      IF QuoteLineRec.FINDSET(FALSE,FALSE) THEN BEGIN
        REPEAT
          CASE ReturnValueType OF
            ReturnValueType::Amount,
            ReturnValueType::TotalAmount:
              RetValue := RetValue + QuoteLineRec."Amount (LCY)";
            ReturnValueType::Quantity,
            ReturnValueType::TotalQuantity:
              RetValue := RetValue + QuoteLineRec.Quantity;
          END;
        UNTIL QuoteLineRec.NEXT = 0;
      END;
    END;

    PROCEDURE LookupContact@1100528700();
    VAR
      Contact@1100528701 : Record 5050;
      ContactList@1100528702 : Page 5052;
    BEGIN
      IF Type = Type::Vendor THEN BEGIN
        Contact.GetContactByVendor("Vendor No.", Contact, TRUE);
        Contact.SETRANGE("Company No.", Contact."Company No.");
      END ELSE
        Contact.SETRANGE("Company No.", "Vendor No.");
      Contact.SETRANGE(Type, Contact.Type::Person);

      ContactList.SETTABLEVIEW(Contact);
      ContactList.LOOKUPMODE(TRUE);
      IF ContactList.RUNMODAL = ACTION::LookupOK THEN BEGIN
        ContactList.GETRECORD(Contact);
        "Contact Person No." := Contact."No.";
        VALIDATE(Rec.Contact, Contact.Name);
      END;
    END;

    PROCEDURE PreferredVendorPresent@1100525001(RequisitionNo@1100525000 : Code[20]) : Boolean;
    VAR
      PreferredVendor@1100528900 : Code[20];
    BEGIN
      SETRANGE("Purchase Requisition No.", RequisitionNo);
      SETRANGE("Preferred Vendor", TRUE);
      IF FINDFIRST THEN BEGIN
        PreferredVendor := "Vendor No.";
      END ELSE BEGIN
        SETRANGE("Preferred Vendor", FALSE);
        IF FINDFIRST THEN
          PreferredVendor := "Vendor No.";
      END;
      SETRANGE("Preferred Vendor");
      EXIT(PreferredVendor<>'');
    END;

    LOCAL PROCEDURE CheckOtherVendors@1100529500();
    VAR
      VendorbyPurchaseRequisition@1100529500 : Record 11229332;
      VendorbyPurchaseRequisition2@1100529501 : Record 11229332;
    BEGIN
      //DP01314
      VendorbyPurchaseRequisition.SETRANGE("Purchase Requisition No.", "Purchase Requisition No.");
      VendorbyPurchaseRequisition.SETFILTER("Vendor No.", '<>%1', "Vendor No.");
      VendorbyPurchaseRequisition.SETRANGE("Preferred Vendor", TRUE);
      IF VendorbyPurchaseRequisition.FINDSET THEN BEGIN
        REPEAT
          VendorbyPurchaseRequisition2 := VendorbyPurchaseRequisition;
          VendorbyPurchaseRequisition2."Preferred Vendor" := FALSE;
          VendorbyPurchaseRequisition2.MODIFY;
        UNTIL VendorbyPurchaseRequisition.NEXT = 0;
      END;
    END;

    BEGIN
    {
      DB01137: copy from Vendor by Purchase Action
    }
    END.
  }
}

