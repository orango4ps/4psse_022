OBJECT Codeunit 11229301 Generic Web Service Job Queue
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    TableNo=472;
    OnRun=VAR
            ParameterString@1100528301 : Text;
            Pos@1100528302 : Integer;
          BEGIN
            ParameterString := "Parameter String";

            REPEAT
              Pos := STRPOS(ParameterString, ',');
              IF (Pos > 0) THEN
                Parameter := UPPERCASE(COPYSTR(ParameterString, 1, Pos - 1))
              ELSE
                Parameter := UPPERCASE(ParameterString);
              JobQueueHandler;
              ParameterString := COPYSTR(ParameterString, Pos + 1);
            UNTIL (Pos = 0);
          END;

  }
  CODE
  {
    VAR
      Parameter@1100528301 : Text;

    LOCAL PROCEDURE JobQueueHandler@1100525010();
    VAR
      GenericWebServiceFunctions@1100528300 : Codeunit 11126711;
    BEGIN
      ProcessOpenTasks;
      GenericWebServiceFunctions.DeleteCompletedTasks;
    END;

    LOCAL PROCEDURE ProcessOpenTasks@1100525006();
    VAR
      GenericTempTable@1100528301 : TEMPORARY Record 11020579;
      GenericWebServiceMethod@1100528302 : Record 11126716;
      GenericWebServiceSetup@1100525003 : Record 11126728;
      GenericWebserviceQueue@1100525000 : Record 11126715;
      GenericWebServiceFunctions@1100528300 : Codeunit 11126711;
    BEGIN
      IF (Parameter = '') THEN
        EXIT;
      GenericWebServiceSetup.GET;
      GenericTempTable.DELETEALL;
      GenericWebserviceQueue.SETFILTER(Status, '%1|%2', GenericWebserviceQueue.Status::Failed,
        GenericWebserviceQueue.Status::Open);
      GenericWebserviceQueue.SETRANGE("Company Name", COMPANYNAME);
      GenericWebserviceQueue.SETRANGE("Method Type", Parameter);
      IF (GenericWebServiceSetup."Max No. Of Retries" > 0) THEN
        GenericWebserviceQueue.SETRANGE("Try No.", 0, GenericWebServiceSetup."Max No. Of Retries" - 1);
      IF (NOT GenericWebserviceQueue.FINDSET) THEN
        EXIT;

      REPEAT
        GenericWebserviceQueue.CALCFIELDS(Priority);
        GenericWebServiceMethod.GET(GenericWebserviceQueue."Method Name");
        IF (NOT GenericWebServiceMethod.Manual) THEN BEGIN
          CLEAR(GenericTempTable);
          GenericTempTable."Key Code1" := FORMAT(GenericWebserviceQueue.Priority);
          GenericTempTable."Key Code2" := FORMAT(GenericWebserviceQueue."Entry No.");
          GenericTempTable.BigInt1 := GenericWebserviceQueue."Entry No.";
          GenericTempTable.INSERT;
        END;
      UNTIL (GenericWebserviceQueue.NEXT= 0);

      IF (GenericTempTable.FINDSET) THEN
        REPEAT
          GenericWebServiceFunctions.ProcessQueueEntry(GenericTempTable.BigInt1);
        UNTIL (GenericTempTable.NEXT= 0);
    END;

    BEGIN
    END.
  }
}

