OBJECT Page 11012239 Copy Summary
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Kopieren Endblatt;
               ENU=Copy Summary;
               NLD=Kopieer staartblad;
               NOR=Kopier sammendrag;
               SVE=Kopiera sammandrag];
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    PageType=StandardDialog;
    OnOpenPage=BEGIN
                 SheetToTxtEditable := TRUE;
                 SheetToEditable := TRUE;
                 EstToEditable := TRUE;
               END;

    OnQueryClosePage=BEGIN
                       CloseActionOK := CloseAction IN [ACTION::OK,ACTION::LookupOK];
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100525002;1;Group  ;
                CaptionML=[DEU=Kopieren von;
                           ENU=Copy from;
                           NLD=Kopiâren van;
                           NOR=Kopier fra;
                           SVE=Kopiera frÜn];
                GroupType=Group }

    { 1   ;2   ;Field     ;
                CaptionML=[DEU=Kalkulation;
                           ENU=Estimate;
                           NLD=Calculatie;
                           NOR=Kalkyle;
                           SVE=Kalkyl];
                NotBlank=Yes;
                SourceExpr=EstFrom;
                OnValidate=BEGIN
                             EstRec.GET(EstFrom);
                             EstFromTxt := EstRec.Description;
                           END;

                OnLookup=BEGIN
                           EstRec.RESET;
                           IF PAGE.RUNMODAL(PAGE::"Estimate List",EstRec) = ACTION::LookupOK THEN BEGIN
                              EstFrom := EstRec."No.";
                              EstFromTxt := EstRec.Description;
                           END;
                           CurrPage.UPDATE(TRUE);
                         END;
                          }

    { 11  ;2   ;Field     ;
                CaptionML=[DEU=Beschreibung;
                           ENU=Description;
                           NLD=Omschrijving;
                           NOR=Beskrivelse;
                           SVE=Beskrivning];
                SourceExpr=EstFromTxt;
                Editable=FALSE }

    { 3   ;2   ;Field     ;
                CaptionML=[DEU=Endblatt;
                           ENU=Summary;
                           NLD=Staartblad;
                           NOR=Sammendrag;
                           SVE=Sammanfattning];
                SourceExpr=SheetFrom;
                OnValidate=BEGIN
                             IF SheetFrom = '' THEN BEGIN
                               SheetFromTxt := '';
                               SheetTo := '';
                               SheetToTxt := '';
                             END ELSE BEGIN
                               SheetRec.GET(EstFrom,SheetFrom);
                               SheetFromTxt := SheetRec.Description;
                             END;

                             SheetToTxtSetEditable;
                           END;

                OnLookup=BEGIN
                           SheetRec.RESET;
                           SheetRec.SETRANGE("Estimate No.",EstFrom);
                           IF PAGE.RUNMODAL(PAGE::"Summary Sheet List",SheetRec) = ACTION::LookupOK THEN BEGIN
                              EstRec.GET(SheetRec."Estimate No.");
                              EstFrom := EstRec."No.";
                              EstFromTxt := EstRec.Description;
                              SheetFrom := SheetRec.Summary;
                              SheetFromTxt := SheetRec.Description;
                           END;
                           CurrPage.UPDATE(TRUE);

                           SheetToTxtSetEditable;
                         END;
                          }

    { 12  ;2   ;Field     ;
                CaptionML=[DEU=Beschreibung;
                           ENU=Description;
                           NLD=Omschrijving;
                           NOR=Beskrivelse;
                           SVE=Beskrivning];
                SourceExpr=SheetFromTxt;
                Editable=FALSE }

    { 1100525003;1;Group  ;
                CaptionML=[DEU=Kopieren in;
                           ENU=Copy to;
                           NLD=Kopiâren naar;
                           NOR=Kopier til;
                           SVE=Kopiera till];
                GroupType=Group }

    { 7   ;2   ;Field     ;
                Name=EstTo;
                CaptionML=[DEU=Kalkulation;
                           ENU=Estimate;
                           NLD=Calculatie;
                           NOR=Kalkyle;
                           SVE=Kalkyl];
                NotBlank=Yes;
                SourceExpr=EstTo;
                Editable=EstToEditable;
                OnValidate=BEGIN
                             EstRec.GET(EstTo);
                             EstToTxt := EstRec.Description;
                           END;

                OnLookup=BEGIN
                           EstRec.RESET;
                           IF PAGE.RUNMODAL(PAGE::"Estimate List",EstRec) = ACTION::LookupOK THEN BEGIN
                              EstTo := EstRec."No.";
                              EstToTxt := EstRec.Description;
                           END;
                           CurrPage.UPDATE(TRUE);
                         END;
                          }

    { 13  ;2   ;Field     ;
                CaptionML=[DEU=Beschreibung;
                           ENU=Description;
                           NLD=Omschrijving;
                           NOR=Beskrivelse;
                           SVE=Beskrivning];
                SourceExpr=EstToTxt;
                Editable=FALSE }

    { 8   ;2   ;Field     ;
                Name=SheetTo;
                Lookup=No;
                DrillDown=No;
                CaptionML=[DEU=Endblatt;
                           ENU=Summary;
                           NLD=Staartblad;
                           NOR=Sammendrag;
                           SVE=Sammanfattning];
                SourceExpr=SheetTo;
                Editable=SheetToTxtEditable;
                OnValidate=BEGIN
                             IF SheetFrom <> SheetTo THEN
                               SheetRec.GET(EstFrom,SheetFrom);
                             IF SheetRec.GET(EstTo,SheetTo) THEN
                               ERROR(Text002);
                           END;

                OnLookup=BEGIN
                           SheetRec.SETRANGE("Estimate No.",EstTo);
                           PAGE.RUNMODAL(PAGE::"Summary Sheet List",SheetRec);
                         END;
                          }

    { 14  ;2   ;Field     ;
                Name=SheetToTxt;
                CaptionML=[DEU=Beschreibung;
                           ENU=Description;
                           NLD=Omschrijving;
                           NOR=Beskrivelse;
                           SVE=Beskrivning];
                SourceExpr=SheetToTxt;
                Editable=SheetToTxtEditable }

    { 1100525004;1;Group  ;
                CaptionML=[DEU=Ausgleichen;
                           ENU=Apply;
                           NLD=Toepassen;
                           NOR=Koble;
                           SVE=VerkstÑll];
                GroupType=Group }

    { 1100485000;2;Field  ;
                CaptionML=[DEU=Endblatt pro Teilkalkulation erstellen;
                           ENU=Generate Summary By Sub Estimate;
                           NLD=Genereer staartbladen per deelcalculatie;
                           NOR=Generer sammendrag etter delkalkyle;
                           SVE=Generera sammandrag efter delkalkyl];
                SourceExpr=GenerateBl;
                OnValidate=BEGIN
                             EstToSetEditable;
                           END;
                            }

  }
  CODE
  {
    VAR
      Text002@11012012 : TextConst 'DEU=Endblatt bereits vorhanden;ENU=Summary Sheet already exists;NLD=Staartblad bestaat al;NOR=Sammendragsarket finnes allerede;SVE=Summeringsarket finns redan';
      EstFrom@11012000 : Code[20];
      SubEstFrom@1210190000 : Code[20];
      SheetFrom@11012001 : Code[10];
      EstTo@11012002 : Code[20];
      SheetTo@11012003 : Code[10];
      EstFromTxt@11012004 : Text[50];
      SheetFromTxt@11012005 : Text[50];
      EstToTxt@11012006 : Text[50];
      SheetToTxt@11012007 : Text[50];
      EstRec@11012008 : Record 11012151;
      SheetRec@11012009 : Record 11012172;
      GenerateBl@1100485000 : Boolean;
      Text003@1100485002 : TextConst 'DEU=Kein Endblatt pro Teilkalkulation erstellt;ENU=No Summary per Sub-Estimate created;NLD=Geen staartblad per deelcalculatie aangemaakt';
      EstToEditable@1100525007 : Boolean INDATASET;
      SheetToEditable@1100525008 : Boolean INDATASET;
      SheetToTxtEditable@1100525009 : Boolean INDATASET;
      CloseActionOK@1100528400 : Boolean;
      CopyMgt@1100409000 : Codeunit 11012227;

    PROCEDURE FillDefaults@1(lEstFrom@11012000 : Code[20];lSheetFrom@11012001 : Code[20]);
    VAR
      xSheetFrom@1100485000 : Code[20];
    BEGIN
      EstFrom   := lEstFrom;
      EstTo     := EstFrom;
      SheetFrom := lSheetFrom;

      IF EstRec.GET(lEstFrom) THEN BEGIN
        EstFromTxt := EstRec.Description;
        EstToTxt   := EstRec.Description;
        xSheetFrom := EstRec."Summary Source (Cost)";

        IF EstRec."Estimate Source" <> '' THEN
          IF EstRec.GET(EstRec."Estimate Source") THEN BEGIN
            lEstFrom := EstRec."No.";
            EstFrom := EstRec."No.";
            EstFromTxt := EstRec.Description;
          END;

        IF xSheetFrom <> '' THEN BEGIN
          IF SheetRec.GET(lEstFrom,xSheetFrom) THEN BEGIN
            lSheetFrom:= SheetRec.Summary;
            SheetFrom := SheetRec.Summary;
          END;
        END;
      END;

      IF SheetRec.GET(lEstFrom,lSheetFrom) THEN BEGIN
        SheetFromTxt := SheetRec.Description;
        SheetToTxt := SheetRec.Description;
      END;

      SheetToTxtSetEditable;
    END;

    PROCEDURE FillDefaultsSource@1100485000(lEstFrom@11012000 : Code[20];lSubEstFrom@1210190000 : Code[20];lSheetFrom@11012001 : Code[20];lEstTo@1100485001 : Code[20];lSheetTo@1100485000 : Code[20]);
    BEGIN
      EstFrom := lEstFrom;
      SheetFrom := lSheetFrom;

      EstTo := lEstTo;
      SubEstFrom := lSubEstFrom;
      SheetTo := lSheetTo;

      IF EstRec.GET(lEstFrom) THEN
        EstFromTxt := EstRec.Description;

      IF SheetRec.GET(lEstFrom,lSheetFrom) THEN
        SheetFromTxt := SheetRec.Description;

      IF EstRec.GET(lEstTo) THEN
        EstToTxt := EstRec.Description;

      IF SheetRec.GET(lEstTo,lSheetTo) THEN
        SheetToTxt := SheetRec.Description;

      SheetToTxtSetEditable;
    END;

    PROCEDURE CopySummary@1100485001();
    VAR
      SumLineRec@1100485000 : Record 11012171;
    BEGIN
      EstRec.GET(EstTo);
      EstRec.TESTFIELD(Fixed, FALSE);
      CopyMgt.CopySummarySheet(EstFrom,'',SheetFrom,EstTo,SheetTo,SheetToTxt,0);
      IF SheetTo = '' THEN BEGIN
        SheetRec.SETRANGE("Estimate No.", EstTo);
        IF SheetRec.FINDFIRST THEN
          SheetTo := SheetRec.Summary;
      END;
      IF SheetRec.GET(EstTo, SheetTo) THEN BEGIN
        SheetRec.Fixed := FALSE;
        SheetRec.MODIFY;
        //call 25809.sn
        SumLineRec.SETRANGE("Estimate No.", EstTo);
        SumLineRec.SETRANGE(Summary, SheetTo);
        IF SumLineRec.FINDFIRST THEN
          SumLineRec.UpdateLogbook;
        //call 25809.en
        SumLineRec.UpdateSummarySheet(EstTo, SheetTo, FALSE); //call 25809
      END;
      IF (EstRec."Summary (Cost)" = '') AND (SheetRec."Summary Type" = SheetRec."Summary Type"::Cost)  THEN  //DP00381
        EstRec."Summary (Cost)" := SheetTo;
      IF (EstRec."Summary (Sales)" = '') AND (SheetRec."Summary Type" = SheetRec."Summary Type"::Sales)  THEN  //DP00381
        EstRec."Summary (Sales)" := SheetTo;
      EstRec.MODIFY;
    END;

    PROCEDURE GenerateSummary@1100485002();
    VAR
      SubEstRecNEW@1100528900 : Record 11072074;
      SumLineRec@1100485000 : Record 11012171;
      SheetNew@1100525001 : Text[100];
      SheetNewTxt@1100525000 : Text[100];
      Numb@1100525002 : Integer;
      lvText000@1100525003 : TextConst 'DEU=Leider ist die LÑnge des Endblattcodes nicht geeignet fÅr diese Funktion;ENU=Reduce Length of Summary Sheet Code for this function;NLD=Helaas is lengte van staartbladcode niet geschikt voor deze functie;NOR=Reduser lengden pÜ sammendragsarkets kode for denne funksjonen;SVE=Minska lÑngden pÜ sammandragsarkets kod fîr den hÑr funktionen';
      lvText001@1100525004 : TextConst 'DEU=Leider ist die LÑnge der Endblattbeschreibung nicht geeignet fÅr diese Funktion;ENU=Reduce Length of Summary Sheet Description for this function;NLD=Helaas is lengte van staartbladomschrijving niet geschikt voor deze functie;NOR=Reduser lengden pÜ sammendragsarkets Beskrivelse for denne funksjonen;SVE=Minska lÑngden pÜ sammandragsarkets beskrivning fîr den hÑr funktionen';
      SummarySheet@1100525006 : Record 11012172;
      LastSeqNo@1100525007 : Integer;
      PosSep@1100525008 : Integer;
      TxtLen@1100525009 : Integer;
      SeqNoTxt@1100525010 : Text[30];
      SeqNoInt@1100525011 : Integer;
      AddSheet@1100525012 : Boolean;
    BEGIN
      EstRec.GET(EstTo);
      EstRec.TESTFIELD(Fixed, FALSE);
      SubEstRecNEW.RESET;
      SubEstRecNEW.SETRANGE("Estimate No.",EstTo);
      IF SubEstFrom <> '' THEN
        SubEstRecNEW.SETRANGE("Sub-Estimate No.", SubEstFrom);
      SubEstRecNEW.SETRANGE(Level,0);
      IF SubEstRecNEW.FINDSET(TRUE,FALSE) THEN BEGIN
        REPEAT
          //C024909.sn
          AddSheet := FALSE;
          LastSeqNo := 0;
          SummarySheet.RESET;
          SummarySheet.SETRANGE("Estimate No.", SubEstRecNEW."Estimate No.");
          SummarySheet.SETRANGE("Sub-Estimate No.", SubEstRecNEW."Sub-Estimate No.");
          SummarySheet.SETFILTER(Summary, SheetFrom + '-*');
          IF NOT SummarySheet.FINDFIRST THEN BEGIN
            SummarySheet.SETRANGE("Sub-Estimate No.");
            IF SummarySheet.FINDSET THEN BEGIN
              REPEAT
                PosSep := STRPOS(SummarySheet.Summary,'-');
                TxtLen := STRLEN(SummarySheet.Summary);
                IF (PosSep <> 0) AND (TxtLen>PosSep) THEN BEGIN
                  SeqNoTxt := COPYSTR(SummarySheet.Summary, PosSep+1, TxtLen-PosSep);
                  EVALUATE(SeqNoInt, SeqNoTxt);
                  IF SeqNoInt > LastSeqNo THEN BEGIN
                    LastSeqNo := SeqNoInt;
                    AddSheet := TRUE;
                  END;
                END;
              UNTIL SummarySheet.NEXT = 0;
            END ELSE BEGIN
              AddSheet := TRUE;
            END;
          END;
          //C024909.en
          IF (AddSheet = TRUE) THEN BEGIN
            Numb += 1;
            //C024909.sn
            //SheetNew := STRSUBSTNO(SheetFrom + '-%1',Numb);
            SheetNew := STRSUBSTNO(SheetFrom + '-%1',LastSeqNo+1);
            //C024909.en
            IF STRLEN(SheetNew) > MAXSTRLEN(SheetRec.Summary) THEN
              ERROR(lvText000);
            SheetNewTxt := SheetRec.Description;
            IF STRLEN(SheetNewTxt) > MAXSTRLEN(SheetRec.Description) THEN
              ERROR(lvText001);
            //
            CopyMgt.CopySummarySheet(EstFrom,SubEstFrom,SheetFrom,EstTo,SheetNew,SheetNewTxt,0);
            //
            SheetRec.GET(EstTo, SheetNew);
            SheetRec.Fixed := FALSE;
            SheetRec.VALIDATE("Sub-Estimate No.", SubEstRecNEW."Sub-Estimate No.");
            SheetRec.MODIFY;
            SheetRec.ResetFilterSubEstimate;
            //
            SumLineRec.SETRANGE("Estimate No.", EstTo);
            SumLineRec.SETRANGE(Summary, SheetTo);
            IF SumLineRec.FINDFIRST THEN
              SumLineRec.UpdateLogbook;
            SumLineRec.UpdateSummarySheet(EstTo, SheetNew, FALSE);
            IF SheetRec."Summary Type" = SheetRec."Summary Type"::Cost THEN BEGIN
              IF SubEstRecNEW."Summary (Cost)" = '' THEN BEGIN
                SubEstRecNEW."Summary (Cost)" := SheetNew;
                SubEstRecNEW.MODIFY;
              END;
            END ELSE BEGIN
              IF SubEstRecNEW."Summary (Sales)" = '' THEN BEGIN
                SubEstRecNEW."Summary (Sales)" := SheetNew;
                SubEstRecNEW.MODIFY;
              END;
            END;
          END;
        UNTIL SubEstRecNEW.NEXT = 0;
      END;
      IF Numb = 0 THEN
        ERROR(Text003);
    END;

    LOCAL PROCEDURE EstToSetEditable@19030444();
    BEGIN
      EstToEditable := (GenerateBl=FALSE);
      SheetToEditable := (GenerateBl=FALSE);
    END;

    LOCAL PROCEDURE SheetToTxtSetEditable@19034915();
    BEGIN
      SheetToTxtEditable := (SheetFrom <> '');
    END;

    LOCAL PROCEDURE StdDialogConfirm@1100528400() : Boolean;
    BEGIN
      CurrPage.RUNMODAL;
      EXIT(CloseActionOK);
    END;

    LOCAL PROCEDURE StdDialogDoAction@1100528401();
    BEGIN
      IF GenerateBl THEN
        GenerateSummary
      ELSE
        CopySummary;
    END;

    PROCEDURE StdDialogRunModal@1100528404();
    BEGIN
      IF StdDialogConfirm THEN
        StdDialogDoAction;
    END;

    PROCEDURE SetSelectionSummarySubEstimate@1100525000(lEstFrom@1100525001 : Code[20];lSheetFrom@1100525000 : Code[20]);
    BEGIN
      //C024158
      EstFrom   := lEstFrom;
      EstTo     := EstFrom;
      SheetFrom := lSheetFrom;
      IF NOT SheetRec.GET(EstFrom, SheetFrom) THEN SheetRec.INIT;  //C050358
    END;

    BEGIN
    END.
  }
}

