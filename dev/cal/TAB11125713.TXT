OBJECT Table 11125713 Project Planning Period Column
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ERROR('');
             END;

    OnModify=BEGIN
               ERROR('');
             END;

    OnDelete=BEGIN
               ERROR('');
             END;

    OnRename=BEGIN
               ERROR('');
             END;

    CaptionML=[ENU=Project Planning Period Column;
               NOR=Kolonne for Prosjektplanleggingsperiode;
               SVE=Projektplanering, periodkolumn];
  }
  FIELDS
  {
    { 10  ;   ;Period Type         ;Option        ;CaptionML=[ENU=Period Type;
                                                              NOR=Periodetype;
                                                              SVE=Periodtyp];
                                                   OptionCaptionML=[ENU=Date,Week,Month,Quarter,Year;
                                                                    NOR=Dato,Uke,MÜned,Kvartal,èr;
                                                                    SVE=Datum,Vecka,MÜnad,Kvartal,èr];
                                                   OptionString=Date,Week,Month,Quarter,Year;
                                                   Editable=No }
    { 20  ;   ;Start Date          ;Date          ;CaptionML=[ENU=Start Date;
                                                              NOR=Startdato;
                                                              SVE=Startdatum];
                                                   Editable=No }
    { 30  ;   ;Sub Column No.      ;Integer       ;CaptionML=[ENU=Sub Column No.;
                                                              NOR=Delkolonne nr.;
                                                              SVE=Subkolumnnr];
                                                   Editable=No }
    { 40  ;   ;End Date            ;Date          ;CaptionML=[ENU=End Date;
                                                              NOR=Sluttdato;
                                                              SVE=Slutdatum];
                                                   Editable=No }
    { 50  ;   ;Period No.          ;Integer       ;CaptionML=[ENU=Period No.;
                                                              NOR=Periodenr.;
                                                              SVE=Periodnr];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Period Type,Start Date,Sub Column No.   ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE PeriodControl@1100525000(SubColumns@1100525000 : Integer);
    VAR
      DateRec@1100525001 : Record 2000000007;
      PeriodColRec@1100525002 : Record 11125713;
      PeriodType@1100525003 : Integer;
      CurrYear@1100525004 : Integer;
      MinYear@1100525007 : Integer;
      MaxYear@1100525008 : Integer;
      YearNo@1100525006 : Integer;
      SubColNo@1100525005 : Integer;
    BEGIN
      //* Generate period table by sub column, only if not present. The record with 'PeriodType=Year' is the
      //* indication if the records of a year/subcolumn are present.
      CurrYear := DATE2DMY(TODAY, 3);
      MinYear := CurrYear - 1;
      MaxYear := CurrYear + 5;

      // Check/insert records for year/subcolumn
      FOR YearNo := MinYear TO MaxYear DO BEGIN
        FOR SubColNo := 1 TO SubColumns DO BEGIN
          IF NOT PeriodColRec.GET(PeriodColRec."Period Type"::Year, DMY2DATE(1, 1, YearNo), SubColNo) THEN BEGIN
            FOR PeriodType := 0 TO 4 DO BEGIN  //* Date/Week/Month/Quarter/Year
              DateRec.SETRANGE("Period Type", PeriodType);
              DateRec.SETRANGE("Period Start", DMY2DATE(1, 1, YearNo), DMY2DATE(31, 12, YearNo));
              IF DateRec.FINDSET(FALSE,FALSE) THEN BEGIN
                REPEAT
                  PeriodColRec.INIT;
                  PeriodColRec."Period Type" := DateRec."Period Type";
                  PeriodColRec."Start Date" := DateRec."Period Start";
                  PeriodColRec."Sub Column No." := SubColNo;
                  PeriodColRec."End Date" := NORMALDATE(DateRec."Period End");
                  PeriodColRec."Period No." := DateRec."Period No.";
                  IF PeriodColRec.INSERT THEN;
                UNTIL DateRec.NEXT = 0;
              END;
            END;
          END;
        END;
      END;

      //* Delete before and after generated years
      FOR PeriodType := 0 TO 4 DO BEGIN  //* Date/Week/Month/Quarter/Year
        PeriodColRec.RESET;
        PeriodColRec.SETRANGE("Period Type", PeriodType);
        PeriodColRec.SETFILTER("Start Date", '..%1', DMY2DATE(31, 12, MinYear-1));
        PeriodColRec.SETFILTER("End Date", '..%1', DMY2DATE(31, 12, MinYear-1));
        PeriodColRec.DELETEALL;

        PeriodColRec.RESET;
        PeriodColRec.SETRANGE("Period Type", PeriodType);
        PeriodColRec.SETFILTER("Start Date", '%1..', DMY2DATE(1, 1, MaxYear+1));
        PeriodColRec.DELETEALL;
      END;
    END;

    BEGIN
    {
      //* It is only allowed to insert records in this tabel with the function 'PeriodControl',
      //* manual update of this table is not allowed.
    }
    END.
  }
}

