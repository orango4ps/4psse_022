OBJECT Table 11125994 Deviation - Selected Option
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CheckIfDeviationIsNotFixed;
             END;

    OnModify=BEGIN
               CheckIfDeviationIsNotFixed;
             END;

    OnDelete=BEGIN
               CheckIfDeviationIsNotFixed;
             END;

    OnRename=BEGIN
               CheckIfDeviationIsNotFixed;
             END;

    CaptionML=[ENU=Deviation - Selected Option;
               NOR=Avvik - Valgt alternativ;
               SVE=Avvikelse Í valt alternativ];
  }
  FIELDS
  {
    { 10  ;   ;Deviation No.       ;Code20        ;TableRelation="QA Deviation";
                                                   CaptionML=[ENU=Deviation No.;
                                                              NOR=Avviksnummer;
                                                              SVE=Avvikelsenr] }
    { 20  ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              NOR=Type;
                                                              SVE=Radtyp];
                                                   OptionCaptionML=[ENU=Subjects,Consequences,Process Phases;
                                                                    NOR=Emner,konsekvenser,prosessfaser;
                                                                    SVE=Žmnen,F”ljder,Processfaser];
                                                   OptionString=Subjects,Consequences,ProcessPhases;
                                                   Description=NOTE: Options must be the same as in T11125993 }
    { 30  ;   ;Option Code         ;Code10        ;TableRelation="Deviation - Multi Option Value".Code WHERE (Type=FIELD(Type));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS(Description);
                                                              END;

                                                   CaptionML=[ENU=Option Code;
                                                              NOR=Alternativkode;
                                                              SVE=Alternativkod] }
    { 40  ;   ;Description         ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Deviation - Multi Option Value"."Option Value" WHERE (Type=FIELD(Type),
                                                                                                                             Code=FIELD(Option Code)));
                                                   OnValidate=BEGIN
                                                                LookupOptionCode(FALSE);
                                                              END;

                                                   CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Deviation No.,Type,Option Code          ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1100525000 : TextConst 'ENU=First select line(s).;NOR=Velg rad(er) f›rst.;SVE=V„lj f”rst rad(er).';

    PROCEDURE RunFormSelectOptions@1100525006(DeviationNo@1100525003 : Code[20];OptionType@1100525000 : Option);
    VAR
      DevSelectedOption@1100525005 : Record 11125994;
    BEGIN
      IF DeviationNo = '' THEN
        EXIT;
      DevSelectedOption.FILTERGROUP(2);
      DevSelectedOption.SETRANGE("Deviation No.", DeviationNo);
      DevSelectedOption.SETRANGE(Type, OptionType);
      DevSelectedOption.FILTERGROUP(0);
      PAGE.RUNMODAL(PAGE::"Deviation - Selected Options", DevSelectedOption);
    END;

    PROCEDURE RunFormMultiSelectOptions@1100525002(DeviationNo@1100525003 : Code[20];OptionType@1100525000 : Option);
    VAR
      Deviation@1100525004 : Record 11125986;
      MultiOptionSelection@1100525001 : Page 11125998;
      EditableMode@1100525002 : Boolean;
    BEGIN
      IF DeviationNo = '' THEN
        EXIT;
      IF Deviation.GET(DeviationNo) THEN BEGIN
        IF NOT Deviation.Fixed THEN
          EditableMode := TRUE;
      END;
      MultiOptionSelection.SetSelections(DeviationNo,OptionType,EditableMode);
      MultiOptionSelection.RUNMODAL;
    END;

    PROCEDURE GetSelectedOptionsText@1100525005(DeviationNo@1100525000 : Code[20];OptionType@1100525002 : Option;MaxLen@1100525003 : Integer) SelectedOptions : Text[1024];
    VAR
      DevSelectedOption@1100525004 : Record 11125994;
      Separator@1100525005 : Text[3];
    BEGIN
      IF DeviationNo = '' THEN
        EXIT('');
      SelectedOptions := '';
      Separator := '';
      IF (MaxLen = 0) OR (MaxLen > MAXSTRLEN(SelectedOptions)) THEN
        MaxLen := MAXSTRLEN(SelectedOptions);

      DevSelectedOption.SETRANGE("Deviation No.", DeviationNo);
      DevSelectedOption.SETRANGE(Type, OptionType);
      IF DevSelectedOption.FINDSET THEN BEGIN
        REPEAT
          DevSelectedOption.CALCFIELDS(Description);
          IF STRLEN(SelectedOptions) + STRLEN(Separator) + STRLEN(DevSelectedOption.Description) <= MaxLen THEN
            SelectedOptions := SelectedOptions + Separator + DevSelectedOption.Description
          ELSE BEGIN
            IF STRLEN(SelectedOptions) <= (MaxLen - 3) THEN
              SelectedOptions := SelectedOptions + '...'
            ELSE
              SelectedOptions := COPYSTR(SelectedOptions, 1, MaxLen - 3) + '...';
            EXIT;
          END;
          Separator := ' / ';
        UNTIL DevSelectedOption.NEXT = 0;
      END;
    END;

    PROCEDURE LookupOptionCode@1100525000(MultiCopy@1100525000 : Boolean);
    VAR
      Deviation@1100525003 : Record 11125986;
      MultiOptionValue@1100525001 : Record 11125993;
      MultiOptionValueList@1100525002 : Page 11125996;
      EditableMode@1100525004 : Boolean;
    BEGIN
      IF ("Deviation No." <> '') AND Deviation.GET("Deviation No.") THEN BEGIN
        IF NOT Deviation.Fixed THEN
          EditableMode := TRUE;
      END;

      MultiOptionValue.FILTERGROUP(2);
      MultiOptionValue.SETRANGE(Type, Type);
      MultiOptionValue.FILTERGROUP(0);
      MultiOptionValue.Type := Type;
      MultiOptionValue.Code := "Option Code";
      MultiOptionValueList.SETTABLEVIEW(MultiOptionValue);
      MultiOptionValueList.SETRECORD(MultiOptionValue);
      IF MultiCopy AND EditableMode THEN
        MultiOptionValueList.SetMultiCopy("Deviation No.");
      IF NOT EditableMode THEN
        MultiOptionValueList.RUNMODAL
      ELSE BEGIN
        MultiOptionValueList.LOOKUPMODE(TRUE);
        IF MultiOptionValueList.RUNMODAL = ACTION::LookupOK THEN BEGIN
          MultiOptionValueList.GETRECORD(MultiOptionValue);
          IF MultiOptionValue.Code <> '' THEN
            VALIDATE("Option Code", MultiOptionValue.Code);
        END;
      END;
    END;

    PROCEDURE CopyMarkedOptionsToDeviation@1100525001(VAR MultiOptionValue@1100525001 : Record 11125993;DeviationNo@1100525000 : Code[20]) : Boolean;
    VAR
      MarkedMultiOptionValue@1100525002 : Record 11125993;
      DevSelectedOption@1100525003 : Record 11125994;
    BEGIN
      MarkedMultiOptionValue.COPY(MultiOptionValue);

      IF NOT MarkedMultiOptionValue.FINDSET THEN BEGIN
        MESSAGE(Text000);
        EXIT(FALSE);
      END ELSE BEGIN
        REPEAT
          IF NOT DevSelectedOption.GET(DeviationNo, MarkedMultiOptionValue.Type, MarkedMultiOptionValue.Code) THEN BEGIN
            DevSelectedOption.INIT;
            DevSelectedOption."Deviation No." := DeviationNo;
            DevSelectedOption.Type := MarkedMultiOptionValue.Type;
            DevSelectedOption."Option Code" := MarkedMultiOptionValue.Code;
            DevSelectedOption.INSERT;
          END;
        UNTIL MarkedMultiOptionValue.NEXT = 0;
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE CheckIfDeviationIsNotFixed@1210190000();
    VAR
      QADeviation@1210190000 : Record 11125986;
    BEGIN
      IF NOT QADeviation.GET("Deviation No.") THEN
        EXIT;
      QADeviation.TESTFIELD(Fixed, FALSE);
    END;

    BEGIN
    END.
  }
}

