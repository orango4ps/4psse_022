OBJECT Report 5899 Calculate Inventory Value
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Lagerwert berechnen;
               ENU=Calculate Inventory Value;
               NLD=Voorraadwaarde berekenen;
               NOR=Beregn lagerverdi;
               SVE=BerÑkna lagervÑrde];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   InventorySetup.GET; //**4PS
                 END;

  }
  DATASET
  {
    { 8129;    ;DataItem;                    ;
               DataItemTable=Table27;
               DataItemTableView=SORTING(No.)
                                 WHERE(Type=CONST(Inventory));
               OnPreDataItem=VAR
                               TempErrorBuf@1002 : TEMPORARY Record 5890;
                               lvItemRec@1100485000 : Record 27;
                             BEGIN
                               ItemJnlTemplate.GET(ItemJnlLine."Journal Template Name");
                               ItemJnlTemplate.TESTFIELD(Type,ItemJnlTemplate.Type::Revaluation);

                               ItemJnlBatch.GET(ItemJnlLine."Journal Template Name",ItemJnlLine."Journal Batch Name");
                               IF NextDocNo = '' THEN BEGIN
                                 IF ItemJnlBatch."No. Series" <> '' THEN BEGIN
                                   ItemJnlLine.SETRANGE("Journal Template Name",ItemJnlLine."Journal Template Name");
                                   ItemJnlLine.SETRANGE("Journal Batch Name",ItemJnlLine."Journal Batch Name");
                                   IF NOT ItemJnlLine.FINDFIRST THEN
                                     NextDocNo := NoSeriesMgt.GetNextNo(ItemJnlBatch."No. Series",PostingDate,FALSE);
                                   ItemJnlLine.INIT;
                                 END;
                                 IF NextDocNo = '' THEN
                                   ERROR(Text003);
                               END;

                               CalcInvtValCheck.SetProperties(PostingDate,CalculatePer,ByLocation,ByVariant,ShowDialog,FALSE);
                               CalcInvtValCheck.RunCheck(Item,TempErrorBuf);

                               ItemLedgEntry.SETCURRENTKEY("Item No.",Positive,"Location Code","Variant Code");

                               NextLineNo := 0;

                               IF ShowDialog THEN
                                 Window.OPEN(Text010,"No.");

                               GLSetup.GET;
                               SourceCodeSetup.GET;

                               IF CalcBase IN [CalcBase::"Standard Cost - Assembly List",CalcBase::"Standard Cost - Manufacturing"] THEN BEGIN
                                 CalculateStdCost.SetProperties(PostingDate,TRUE,CalcBase = CalcBase::"Standard Cost - Assembly List",FALSE,'',TRUE);
                                 CalculateStdCost.CalcItems(Item,NewStdCostItem);
                                 CLEAR(CalculateStdCost);
                               //**4PS.sn: M10617
                               END ELSE BEGIN
                                 lvItemRec.COPY(Item);
                                 lvItemRec.SETRANGE("Replenishment System", lvItemRec."Replenishment System"::"Prod. Order");
                                 CalculateStdCost.SetProperties(PostingDate,TRUE,CalcBase = CalcBase::"Standard Cost - Manufacturing",FALSE,'',TRUE);
                                 CalculateStdCost.CalcItems(lvItemRec,NewStdCostItem);
                                 CLEAR(CalculateStdCost);
                               //**4PS.en
                               END;
                             END;

               OnAfterGetRecord=BEGIN
                                  IF ShowDialog THEN
                                    Window.UPDATE;

                                  IF (CalculatePer = CalculatePer::Item) AND ("Costing Method" = "Costing Method"::Average) THEN BEGIN
                                    CalendarPeriod."Period Start" := PostingDate;
                                    AvgCostAdjmtPoint."Valuation Date" := PostingDate;
                                    AvgCostAdjmtPoint.GetValuationPeriod(CalendarPeriod);
                                    IF PostingDate <> CalendarPeriod."Period End" THEN
                                      ERROR(Text011,"No.",PostingDate,CalendarPeriod."Period End");
                                  END;

                                  ValJnlBuffer.RESET;
                                  ValJnlBuffer.DELETEALL;
                                  IncludeExpectedCost := ("Costing Method" = "Costing Method"::Standard) AND (CalculatePer = CalculatePer::Item);
                                  ItemLedgEntry.SETRANGE("Item No.","No.");
                                  ItemLedgEntry.SETRANGE(Positive,TRUE);
                                  COPYFILTER("Location Filter",ItemLedgEntry."Location Code");
                                  COPYFILTER("Variant Filter",ItemLedgEntry."Variant Code");
                                  IF ItemLedgEntry.FIND('-') THEN
                                    REPEAT
                                      IF IncludeEntryInCalc(ItemLedgEntry,PostingDate,IncludeExpectedCost) THEN BEGIN
                                        RemQty := ItemLedgEntry.CalculateRemQuantity(ItemLedgEntry."Entry No.",PostingDate);
                                        CASE CalculatePer OF
                                          CalculatePer::"Item Ledger Entry":
                                            InsertItemJnlLine(
                                              ItemLedgEntry."Entry Type",ItemLedgEntry."Item No.",
                                              ItemLedgEntry."Variant Code",ItemLedgEntry."Location Code",RemQty,
                                              ItemLedgEntry.CalculateRemInventoryValue(ItemLedgEntry."Entry No.",ItemLedgEntry.Quantity,RemQty,FALSE,PostingDate),
                                              ItemLedgEntry."Entry No.",0);
                                          CalculatePer::Item:
                                            InsertValJnlBuffer(
                                              ItemLedgEntry."Item No.",ItemLedgEntry."Variant Code",
                                              ItemLedgEntry."Location Code",RemQty,
                                              ItemLedgEntry.CalculateRemInventoryValue(ItemLedgEntry."Entry No.",ItemLedgEntry.Quantity,RemQty,
                                                IncludeExpectedCost AND NOT ItemLedgEntry."Completely Invoiced",PostingDate));
                                        END;
                                      END;
                                    UNTIL ItemLedgEntry.NEXT = 0;

                                  IF CalculatePer = CalculatePer::Item THEN
                                    IF (GETFILTER("Location Filter") <> '') OR ByLocation THEN BEGIN
                                      ByLocation2 := TRUE;
                                      COPYFILTER("Location Filter",Location.Code);
                                      IF Location.FIND('-') THEN BEGIN
                                        CLEAR(ValJnlBuffer);
                                        ValJnlBuffer.SETCURRENTKEY("Item No.","Location Code","Variant Code");
                                        ValJnlBuffer.SETRANGE("Item No.","No.");
                                        REPEAT
                                          ValJnlBuffer.SETRANGE("Location Code",Location.Code);
                                          IF (GETFILTER("Variant Filter") <> '') OR ByVariant THEN BEGIN
                                            ByVariant2 := TRUE;
                                            ItemVariant.SETRANGE("Item No.","No.");
                                            COPYFILTER("Variant Filter",ItemVariant.Code);
                                            IF ItemVariant.FIND('-') THEN
                                              REPEAT
                                                ValJnlBuffer.SETRANGE("Variant Code",ItemVariant.Code);
                                                Calculate(Item,ItemVariant.Code,Location.Code);
                                              UNTIL ItemVariant.NEXT = 0;
                                            ValJnlBuffer.SETRANGE("Variant Code",'');
                                            Calculate(Item,'',Location.Code);
                                          END ELSE
                                            Calculate(Item,'',Location.Code);
                                        UNTIL Location.NEXT = 0;
                                      END;
                                      ValJnlBuffer.SETRANGE("Location Code",'');
                                      IF ByVariant THEN BEGIN
                                        ItemVariant.SETRANGE("Item No.","No.");
                                        COPYFILTER("Variant Filter",ItemVariant.Code);
                                        IF ItemVariant.FIND('-') THEN
                                          REPEAT
                                            ValJnlBuffer.SETRANGE("Variant Code",ItemVariant.Code);
                                            Calculate(Item,ItemVariant.Code,'');
                                          UNTIL ItemVariant.NEXT = 0;
                                        ValJnlBuffer.SETRANGE("Variant Code",'');
                                        Calculate(Item,'','');
                                      END ELSE
                                        Calculate(Item,'','');
                                    END ELSE
                                      IF (GETFILTER("Variant Filter") <> '') OR ByVariant THEN BEGIN
                                        ByVariant2 := TRUE;
                                        ItemVariant.SETRANGE("Item No.","No.");
                                        COPYFILTER("Variant Filter",ItemVariant.Code);
                                        IF ItemVariant.FIND('-') THEN BEGIN
                                          ValJnlBuffer.RESET;
                                          ValJnlBuffer.SETCURRENTKEY("Item No.","Variant Code");
                                          ValJnlBuffer.SETRANGE("Item No.","No.");
                                          REPEAT
                                            ValJnlBuffer.SETRANGE("Variant Code",ItemVariant.Code);
                                            Calculate(Item,ItemVariant.Code,'');
                                          UNTIL ItemVariant.NEXT = 0;
                                        END;
                                        ValJnlBuffer.SETRANGE("Location Code");
                                        ValJnlBuffer.SETRANGE("Variant Code",'');
                                        Calculate(Item,'','');
                                      END ELSE BEGIN
                                        ValJnlBuffer.RESET;
                                        ValJnlBuffer.SETCURRENTKEY("Item No.");
                                        ValJnlBuffer.SETRANGE("Item No.","No.");
                                        Calculate(Item,'','');
                                      END;
                                END;

               OnPostDataItem=VAR
                                SKU@1001 : Record 5700;
                                ItemCostMgt@1002 : Codeunit 5804;
                              BEGIN
                                IF NOT UpdStdCost THEN
                                  EXIT;

                                IF ByLocation THEN
                                  COPYFILTER("Location Filter",SKU."Location Code");
                                IF ByVariant THEN
                                  COPYFILTER("Variant Filter",SKU."Variant Code");

                                NewStdCostItem.COPYFILTERS(Item);
                                IF NewStdCostItem.FIND('-') THEN
                                  REPEAT
                                    IF NOT UpdatedStdCostSKU.GET('',NewStdCostItem."No.",'') THEN
                                      ItemCostMgt.UpdateStdCostShares(NewStdCostItem);

                                    SKU.SETRANGE("Item No.",NewStdCostItem."No.");
                                    IF SKU.FIND('-') THEN
                                      REPEAT
                                        IF NOT UpdatedStdCostSKU.GET(SKU."Location Code",NewStdCostItem."No.",SKU."Variant Code") THEN BEGIN
                                          //SKU.VALIDATE("Standard Cost",NewStdCostItem."Standard Cost"); //**4PS.o
                                          //db, 25-04-07: bijwerken SKU-prijs gebeurt na doorboeken herwaardering als daarin kostprijs op artikel aangepast wordt.
                                          SKU.MODIFY;
                                        END;
                                      UNTIL SKU.NEXT = 0;
                                  UNTIL NewStdCostItem.NEXT = 0;
                              END;

               ReqFilterFields=No.,Costing Method,Replenishment System,Source Revaluation }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnInit=BEGIN
               UpdStdCostEnable := TRUE;
               CalcBaseEnable := TRUE;
               ByVariantEnable := TRUE;
               ByLocationEnable := TRUE;
             END;

      OnOpenPage=BEGIN
                   SetDefaultsRequestPage; //**4PS.n
                   IF PostingDate = 0D THEN
                     PostingDate := WORKDATE;
                   ValidatePostingDate;

                   ValidateCalcLevel;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ] }

      { 15  ;2   ;Field     ;
                  CaptionML=[DEU=Buchungsdatum;
                             ENU=Posting Date;
                             NLD=Boekingsdatum;
                             NOR=Bokfõringsdato;
                             SVE=Bokfîringsdatum];
                  ToolTipML=[DEU=Legt das Datum fÅr die Buchung dieser Stapelverarbeitung fest. StandardmÑ·ig wird das Arbeitsdatum eingegeben, Sie kînnen es allerdings Ñndern.;
                             ENU=Specifies the date for the posting of this batch job. By default, the working date is entered, but you can change it.;
                             NLD=Hiermee wordt de datum opgegeven voor de boeking van deze batchverwerking. Standaard wordt de werkdatum ingevuld in dit veld, maar u kunt deze wijzigen.;
                             NOR=Angir datoen for bokfõring av denne kjõrselen. Som standard oppgis arbeidsdatoen, men du kan endre den.;
                             SVE=Anger datumet fîr bokfîringen av detta batch-jobb. Arbetsdatumet anges standard, men du kan Ñndra det.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=PostingDate }

      { 1   ;2   ;Field     ;
                  CaptionML=[DEU=Belegnr.;
                             ENU=Document No.;
                             NLD=Documentnr.;
                             NOR=Bilagsnr.;
                             SVE=Dokumentnr];
                  ToolTipML=[DEU=Gibt die Nummer des Dokuments an, das vom Bericht oder der Stapelverarbeitung verarbeitet wird.;
                             ENU=Specifies the number of the document that is processed by the report or batch job.;
                             NLD=Hiermee wordt het nummer opgegeven van het document dat wordt verwerkt door het rapport of de batchverwerking.;
                             NOR=Angir nummeret for dokumentet som behandles av rapporten eller kjõrselen.;
                             SVE=Anger antalet dokument som rapporten eller buntjobbet bearbetar.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=NextDocNo }

      { 3   ;2   ;Field     ;
                  CaptionML=[DEU=Berechnung pro;
                             ENU=Calculate Per;
                             NLD=Berekenen per;
                             NOR=Beregn per];
                  ToolTipML=[DEU=Legt fest, ob der Lagerwert pro Artikelposten oder pro Artikel summiert werden soll.;
                             ENU=Specifies if you want to sum up the inventory value per item ledger entry or per item.;
                             NLD=Hiermee wordt opgegeven of u de voorraadwaarde per artikelpost of per artikel wilt berekenen.;
                             NOR=Angir om du vil summere lagerverdien per varepost eller per vare.];
                  OptionCaptionML=[DEU=Artikelposten,Artikel;
                                   ENU=Item Ledger Entry,Item;
                                   NLD=Artikelpost,Artikel;
                                   NOR=Varepost,Vare];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=CalculatePer;
                  OnValidate=BEGIN
                               IF CalculatePer = CalculatePer::Item THEN
                                 ItemCalculatePerOnValidate;
                               IF CalculatePer = CalculatePer::"Item Ledger Entry" THEN
                                 ItemLedgerEntryCalculatePerOnV;
                             END;
                              }

      { 8   ;2   ;Field     ;
                  Name=By Location;
                  CaptionML=[DEU=Nach Lagerort;
                             ENU=By Location;
                             NLD=Per vestiging;
                             NOR=Per lokasjon];
                  ToolTipML=[DEU=Gibt an, ob der Lagerbestand nach Ort berechnet werden soll.;
                             ENU=Specifies whether to calculate inventory by location.;
                             NLD=Hiermee wordt opgegeven of voorraad per locatie moet worden berekend.;
                             NOR=Angir om du vil beregne beholdning etter lokasjon.];
                  ApplicationArea=#Location;
                  SourceExpr=ByLocation;
                  Enabled=ByLocationEnable }

      { 12  ;2   ;Field     ;
                  Name=By Variant;
                  CaptionML=[DEU=Nach Variante;
                             ENU=By Variant;
                             NLD=Per variant;
                             NOR=Per variant];
                  ToolTipML=[DEU=Legt die Artikelvarianten fest, die die Stapelverarbeitung berÅcksichtigen soll.;
                             ENU=Specifies the item variants that you want the batch job to consider.;
                             NLD=Hiermee worden de artikelvarianten opgegeven die door de batchverwerking moeten worden meegenomen.;
                             NOR=Angir varevariantene du vil at kjõrselen skal vurdere.];
                  ApplicationArea=#Planning;
                  SourceExpr=ByVariant;
                  Enabled=ByVariantEnable }

      { 14  ;2   ;Field     ;
                  Name=UpdStdCost;
                  CaptionML=[DEU=Einst.-Preis (fest) aktual.;
                             ENU=Update Standard Cost;
                             NLD=Vaste verrekenprijs aanpassen;
                             NOR=Oppdater standardkost;
                             SVE=Uppdatera standardkostnad];
                  ToolTipML=[DEU=Legt fest, ob der Einstandspreis der Artikel gemÑ· dem berechneten Lagerwert aktualisiert werden soll.;
                             ENU=Specifies if you want the items' standard cost to be updated according to the calculated inventory value.;
                             NLD=Hiermee wordt opgegeven of u wilt dat de vaste verrekenprijs van het artikel wordt bijgewerkt aan de hand van de berekende voorraadwaarde.;
                             NOR=Angir om du vil at varers standardkostnad skal oppdateres i henhold til den beregnede lagerverdien.];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=UpdStdCost;
                  Enabled=UpdStdCostEnable }

      { 5   ;2   ;Field     ;
                  Name=CalcBase;
                  CaptionML=[DEU=Berechnungsgrundlage;
                             ENU=Calculation Base;
                             NLD=Berekeningsbasis;
                             NOR=Beregningsgrunnlag;
                             SVE=BerÑkningsbas];
                  ToolTipML=[DEU=Legt fest, ob das Neubewertungs Buch.-Blatt einen neuen Wert fÅr den Inhalt des Feldes "Einstandspreis (neu bewertet)" vorschlÑgt.;
                             ENU=Specifies if the revaluation journal will suggest a new value for the Unit Cost (Revalued) field.;
                             NLD=Hiermee wordt opgegeven of een nieuwe waarde voor het veld Kostprijs (Geherwaardeerd) wordt voorgesteld in het herwaarderingsdagboek.;
                             NOR=Angir om revalueringskladden skal foreslÜ en ny verdi for feltet Enhetskost (revaluert).];
                  OptionCaptionML=[DEU=" ,Letzter EK-Preis,Einst.-Preis - Montageliste,Einst.-Preis - Produktion";
                                   ENU=" ,Last Purchase Price,Standard Cost - Assembly List,Standard Cost - Manufacturing";
                                   NLD=" ,Laatste inkoopprijs,Vaste verrekenprijs - Assemblageoverzicht,Vaste verrekenprijs - Productie";
                                   NOR=" ,Siste direkte enhetskost,Standardkost - stykklisteoversikt,Standardkost - produksjon"];
                  ApplicationArea=#Basic,#Suite;
                  SourceExpr=CalcBase;
                  Enabled=CalcBaseEnable }

      { 1100525000;2;Field  ;
                  CaptionML=[DEU="öberschlagen Artikel miet Einstandspreis (berechnet=reguliert)";
                             ENU="Skip Item with Unit Cost (Calculated=Revalued)";
                             NLD="Overslaan artikel met kostprijs (berekend=geherwaardeerd)"];
                  SourceExpr=SkipItemWithSameCostPrice }

      { 1100525001;2;Field  ;
                  CaptionML=[DEU=Informationen;
                             ENU=Information;
                             NLD=Informatie;
                             SVE=Information];
                  SourceExpr=MessTxt;
                  Editable=FALSE;
                  MultiLine=Yes }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text003@1003 : TextConst 'DEU=Sie mÅssen eine Belegnr. eingeben.;ENU=You must enter a document number.;NLD=U moet een documentnummer invullen.;NOR=Du mÜ angi et dokumentnummer.;SVE=Du mÜste ange ett dokumentnr.';
      Text010@1010 : TextConst 'DEU=Artikel werden durchgelesen #1##########;ENU=Processing items #1##########;NLD=Verwerken artikelen #1##########;NOR=Behandler varer  #1##########;SVE=Behandlar artiklar #1##########';
      NewStdCostItem@1019 : TEMPORARY Record 27;
      UpdatedStdCostSKU@1020 : TEMPORARY Record 5700;
      ItemJnlBatch@1021 : Record 233;
      ItemJnlLine@1022 : Record 83;
      ValJnlBuffer@1024 : TEMPORARY Record 5803;
      ItemJnlTemplate@1025 : Record 82;
      ItemLedgEntry@1026 : Record 32;
      GLSetup@1027 : Record 98;
      SourceCodeSetup@1028 : Record 242;
      Location@1029 : Record 14;
      ItemVariant@1030 : Record 5401;
      AvgCostAdjmtPoint@1002 : Record 5804;
      CalendarPeriod@1005 : Record 2000000007;
      CalcInvtValCheck@1004 : Codeunit 5899;
      NoSeriesMgt@1031 : Codeunit 396;
      CalculateStdCost@1032 : Codeunit 5812;
      Window@1033 : Dialog;
      CalculatePer@1034 : 'Item Ledger Entry,Item';
      CalcBase@1035 : ' ,Last Direct Unit Cost,Standard Cost - Assembly List,Standard Cost - Manufacturing';
      PostingDate@1036 : Date;
      NextDocNo@1037 : Code[20];
      AverageUnitCostLCY@1038 : Decimal;
      RemQty@1040 : Decimal;
      NextLineNo@1041 : Integer;
      NextLineNo2@1042 : Integer;
      ByLocation@1043 : Boolean;
      ByVariant@1044 : Boolean;
      ByLocation2@1045 : Boolean;
      ByVariant2@1046 : Boolean;
      UpdStdCost@1048 : Boolean;
      ShowDialog@1000 : Boolean;
      IncludeExpectedCost@1001 : Boolean;
      Text011@1006 : TextConst 'DEU=Sie kînnen keine Neubewertung mithilfe von ''Berechnung pro Artikel'' fÅr Artikel %1 zum Buchungsdatum %2 ausfÅhren. FÅr diese Periode kann nur das Buchungsdatum %3 verwendet werden.;ENU=You cannot revalue by Calculate Per Item for item %1 using posting date %2. You can only use the posting date %3 for this period.;NLD=U kunt niet herwaarderen met Berekenen per Artikel voor artikel %1 met boekingsdatum %2. U kunt voor deze periode alleen boekingsdatum %3 gebruiken.;NOR=Du kan ikke revaluere etter Beregn per vare for varen %1 med bokfõringsdatoen %2. Du kan bare bruke bokfõringsdatoen %3 for denne perioden.';
      ByLocationEnable@19076268 : Boolean INDATASET;
      ByVariantEnable@19005255 : Boolean INDATASET;
      CalcBaseEnable@19032955 : Boolean INDATASET;
      UpdStdCostEnable@19076759 : Boolean INDATASET;
      DuplWarningQst@1008 : TextConst 'DEU=Doppelte Neubewertungs Buch.-BlÑtter werden generiert.\Mîchten Sie fortfahren?;ENU=Duplicate Revaluation Journals will be generated.\Do you want to continue?;NLD=Er worden dubbele herwaarderingsdagboeken gegenereerd.\Wilt u doorgaan?;NOR=Det vil bli generert dupliserte revalueringskladder.\Vil du fortsette?';
      HideDuplWarning@1009 : Boolean;
      InventorySetup@1100485000 : Record 313;
      SkipItemWithSameCostPrice@1100485002 : Boolean;
      MessTxt@1100485001 : Text[250];

    LOCAL PROCEDURE IncludeEntryInCalc@9(ItemLedgEntry@1000 : Record 32;PostingDate@1001 : Date;IncludeExpectedCost@1002 : Boolean) : Boolean;
    BEGIN
      WITH ItemLedgEntry DO BEGIN
        IF IncludeExpectedCost THEN
          EXIT("Posting Date" IN [0D..PostingDate]);
        EXIT("Completely Invoiced" AND ("Last Invoice Date" IN [0D..PostingDate]));
      END;
    END;

    [External]
    PROCEDURE SetItemJnlLine@1(VAR NewItemJnlLine@1000 : Record 83);
    BEGIN
      ItemJnlLine := NewItemJnlLine;
    END;

    LOCAL PROCEDURE ValidatePostingDate@4();
    BEGIN
      ItemJnlBatch.GET(ItemJnlLine."Journal Template Name",ItemJnlLine."Journal Batch Name");
      IF ItemJnlBatch."No. Series" = '' THEN
        NextDocNo := ''
      ELSE BEGIN
        NextDocNo := NoSeriesMgt.GetNextNo(ItemJnlBatch."No. Series",PostingDate,FALSE);
        CLEAR(NoSeriesMgt);
      END;
    END;

    LOCAL PROCEDURE ValidateCalcLevel@3();
    BEGIN
      PageValidateCalcLevel;
      EXIT;
    END;

    LOCAL PROCEDURE InsertValJnlBuffer@2(ItemNo2@1000 : Code[20];VariantCode2@1001 : Code[10];LocationCode2@1002 : Code[10];Quantity2@1003 : Decimal;Amount2@1004 : Decimal);
    VAR
      lvSKU@1210190000 : Record 5700;
    BEGIN
      //**4PS.sn M23305
      IF lvSKU.GET(LocationCode2,ItemNo2,VariantCode2) THEN
        IF lvSKU.Consignment THEN
          EXIT;
      //**4PS.en M23305
      ValJnlBuffer.RESET;
      ValJnlBuffer.SETCURRENTKEY("Item No.","Location Code","Variant Code");
      ValJnlBuffer.SETRANGE("Item No.",ItemNo2);
      ValJnlBuffer.SETRANGE("Location Code",LocationCode2);
      ValJnlBuffer.SETRANGE("Variant Code",VariantCode2);
      IF ValJnlBuffer.FINDFIRST THEN BEGIN
        ValJnlBuffer.Quantity := ValJnlBuffer.Quantity + Quantity2;
        ValJnlBuffer."Inventory Value (Calculated)" :=
          ValJnlBuffer."Inventory Value (Calculated)" + Amount2;
        ValJnlBuffer.MODIFY;
      END ELSE
        IF Quantity2 <> 0 THEN BEGIN
          NextLineNo2 := NextLineNo2 + 10000;
          ValJnlBuffer.INIT;
          ValJnlBuffer."Line No." := NextLineNo2;
          ValJnlBuffer."Item No." := ItemNo2;
          ValJnlBuffer."Variant Code" := VariantCode2;
          ValJnlBuffer."Location Code" := LocationCode2;
          ValJnlBuffer.Quantity := Quantity2;
          ValJnlBuffer."Inventory Value (Calculated)" := Amount2;
          ValJnlBuffer.INSERT;
        END;
    END;

    LOCAL PROCEDURE CalcAverageUnitCost@8(BufferQty@1000 : Decimal;VAR InvtValueCalc@1001 : Decimal;VAR AppliedAmount@1002 : Decimal);
    VAR
      ValueEntry@1003 : Record 5802;
      AverageQty@1004 : Decimal;
      AverageCost@1005 : Decimal;
      NotComplInvQty@1007 : Decimal;
      NotComplInvValue@1008 : Decimal;
    BEGIN
      WITH ValueEntry DO BEGIN
        "Item No." := Item."No.";
        "Valuation Date" := PostingDate;
        IF ValJnlBuffer.GETFILTER("Location Code") <> '' THEN
          "Location Code" := ValJnlBuffer.GETRANGEMIN("Location Code");
        IF ValJnlBuffer.GETFILTER("Variant Code") <> '' THEN
          "Variant Code" := ValJnlBuffer.GETRANGEMIN("Variant Code");
        SumCostsTillValuationDate(ValueEntry);
        AverageQty := "Invoiced Quantity";
        AverageCost := "Cost Amount (Actual)";

        CalcNotComplInvcdTransfer(NotComplInvQty,NotComplInvValue);
        AverageQty -= NotComplInvQty;
        AverageCost -= NotComplInvValue;

        RESET;
        SETRANGE("Item No.",Item."No.");
        SETRANGE("Valuation Date",0D,PostingDate);
        SETFILTER("Location Code",ValJnlBuffer.GETFILTER("Location Code"));
        SETFILTER("Variant Code",ValJnlBuffer.GETFILTER("Variant Code"));
        SETRANGE(Inventoriable,TRUE);
        SETRANGE("Item Charge No.",'');
        SETFILTER("Posting Date",'>%1',PostingDate);
        SETFILTER("Entry Type",'<>%1',"Entry Type"::Revaluation);
        CALCSUMS("Invoiced Quantity","Cost Amount (Actual)");
        AverageQty -= "Invoiced Quantity";
        AverageCost -= "Cost Amount (Actual)";
      END;

      IF AverageQty <> 0 THEN BEGIN
        AverageUnitCostLCY := AverageCost / AverageQty;
        IF AverageUnitCostLCY < 0 THEN
          AverageUnitCostLCY := 0;
      END ELSE
        AverageUnitCostLCY := 0;

      AppliedAmount := InvtValueCalc;
      InvtValueCalc := BufferQty * AverageUnitCostLCY;
    END;

    LOCAL PROCEDURE CalcNotComplInvcdTransfer@7(VAR NotComplInvQty@1001 : Decimal;VAR NotComplInvValue@1002 : Decimal);
    VAR
      ItemLedgEntry@1000 : Record 32;
      RemQty@1003 : Decimal;
      RemInvValue@1004 : Decimal;
      i@1005 : Integer;
    BEGIN
      FOR i := 1 TO 2 DO
        WITH ItemLedgEntry DO BEGIN
          SETCURRENTKEY("Item No.",Positive,"Location Code","Variant Code");
          SETRANGE("Item No.",Item."No.");
          SETRANGE(Positive,i = 1);
          SETFILTER("Location Code",ValJnlBuffer.GETFILTER("Location Code"));
          SETFILTER("Variant Code",ValJnlBuffer.GETFILTER("Variant Code"));
          IF FIND('-') THEN
            REPEAT
              IF (Quantity = "Invoiced Quantity") AND
                 NOT "Completely Invoiced" AND
                 ("Last Invoice Date" IN [0D..PostingDate]) AND
                 ("Invoiced Quantity" <> 0)
              THEN BEGIN
                RemQty := Quantity;
                RemInvValue := CalcItemLedgEntryActualCostTillPostingDate("Entry No.",PostingDate);
                NotComplInvQty := NotComplInvQty + RemQty;
                NotComplInvValue := NotComplInvValue + RemInvValue;
              END;
            UNTIL NEXT = 0;
        END;
    END;

    LOCAL PROCEDURE InsertItemJnlLine@6(EntryType2@1000 : Option;ItemNo2@1001 : Code[20];VariantCode2@1002 : Code[10];LocationCode2@1003 : Code[10];Quantity2@1005 : Decimal;Amount2@1006 : Decimal;ApplyToEntry2@1007 : Integer;AppliedAmount@1008 : Decimal);
    VAR
      Item@1009 : Record 27;
      SKU@1010 : Record 5700;
      lvCalcBase@1100485000 : ' ,Last Direct Unit Cost,Standard Cost - Assembly List,Standard Cost - Manufacturing';
    BEGIN
      IF Quantity2 = 0 THEN
        EXIT;

      WITH ItemJnlLine DO BEGIN
        IF NOT HideDuplWarning THEN
          IF ItemJnlLineExists(ItemJnlLine,ItemNo2,VariantCode2,LocationCode2,ApplyToEntry2) THEN
            IF CONFIRM(DuplWarningQst) THEN
              HideDuplWarning := TRUE
            ELSE
              ERROR('');
        IF NextLineNo = 0 THEN BEGIN
          LOCKTABLE;
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          IF FINDLAST THEN
            NextLineNo := "Line No.";
        END;

        NextLineNo := NextLineNo + 10000;
        INIT;
        "Line No." := NextLineNo;
        "Value Entry Type" := "Value Entry Type"::Revaluation;
        VALIDATE("Posting Date",PostingDate);
        VALIDATE("Entry Type",EntryType2);
        VALIDATE("Document No.",NextDocNo);
        VALIDATE("Item No.",ItemNo2);
        "Reason Code" := ItemJnlBatch."Reason Code";
        "Variant Code" := VariantCode2;
      //  "Location Code" := LocationCode2; //**4PS.o
        VALIDATE("Location Code", LocationCode2); //**4PS.o
        "Source Code" := SourceCodeSetup."Revaluation Journal";
        VALIDATE("Unit Amount",0);
        IF ApplyToEntry2 <> 0 THEN
          "Inventory Value Per" := "Inventory Value Per"::" "
        ELSE
          IF ByLocation2 AND ByVariant2 THEN
            "Inventory Value Per" := "Inventory Value Per"::"Location and Variant"
          ELSE
            IF ByLocation2 THEN
              "Inventory Value Per" := "Inventory Value Per"::Location
            ELSE
              IF ByVariant2 THEN
                "Inventory Value Per" := "Inventory Value Per"::Variant
              ELSE
                "Inventory Value Per" := "Inventory Value Per"::Item;
        IF CalculatePer = CalculatePer::"Item Ledger Entry" THEN BEGIN
          "Applies-to Entry" := ApplyToEntry2;
          CopyDim(ItemLedgEntry."Dimension Set ID");
        END;
        VALIDATE(Quantity,Quantity2);
        VALIDATE("Inventory Value (Calculated)",ROUND(Amount2,GLSetup."Amount Rounding Precision"));
        //**4PS.sn: M10617
        lvCalcBase := CalcBase;
        IF Item.GET("Item No.") THEN
          IF Item."Replenishment System" = Item."Replenishment System"::"Prod. Order" THEN
            CalcBase := CalcBase::"Standard Cost - Manufacturing";
        //**4PS.en
        CASE CalcBase OF
          CalcBase::" ":
            VALIDATE("Inventory Value (Revalued)","Inventory Value (Calculated)");
          CalcBase::"Last Direct Unit Cost":
            IF SKU.GET("Location Code","Item No.","Variant Code") THEN
              //VALIDATE("Unit Cost (Revalued)",SKU."Last Direct Cost") //**4PS.o
              VALIDATE("Unit Cost (Revalued)",SKU."Last Direct Cost" + SKU."Surcharge SKU") //**4PS.n call 25128
            ELSE BEGIN
              Item.GET("Item No.");
              VALIDATE("Unit Cost (Revalued)",Item."Last Direct Cost");
            END;
          CalcBase::"Standard Cost - Assembly List",
          CalcBase::"Standard Cost - Manufacturing":
            BEGIN
              IF NewStdCostItem.GET(ItemNo2) THEN BEGIN
                VALIDATE("Unit Cost (Revalued)",NewStdCostItem."Standard Cost");
                "Single-Level Material Cost" := NewStdCostItem."Single-Level Material Cost";
                "Single-Level Capacity Cost" := NewStdCostItem."Single-Level Capacity Cost";
                "Single-Level Subcontrd. Cost" := NewStdCostItem."Single-Level Subcontrd. Cost";
                "Single-Level Cap. Ovhd Cost" := NewStdCostItem."Single-Level Cap. Ovhd Cost";
                "Single-Level Mfg. Ovhd Cost" := NewStdCostItem."Single-Level Mfg. Ovhd Cost";
                "Rolled-up Material Cost" := NewStdCostItem."Rolled-up Material Cost";
                "Rolled-up Capacity Cost" := NewStdCostItem."Rolled-up Capacity Cost";
                "Rolled-up Subcontracted Cost" := NewStdCostItem."Rolled-up Subcontracted Cost";
                "Rolled-up Mfg. Ovhd Cost" := NewStdCostItem."Rolled-up Mfg. Ovhd Cost";
                "Rolled-up Cap. Overhead Cost" := NewStdCostItem."Rolled-up Cap. Overhead Cost";
                UpdatedStdCostSKU."Item No." := ItemNo2;
                UpdatedStdCostSKU."Location Code" := LocationCode2;
                UpdatedStdCostSKU."Variant Code" := VariantCode2;
                IF UpdatedStdCostSKU.INSERT THEN;
              END ELSE
                VALIDATE("Inventory Value (Revalued)","Inventory Value (Calculated)");
            END;
        END;
        "Update Standard Cost" := UpdStdCost;
        "Partial Revaluation" := TRUE;
        "Applied Amount" := AppliedAmount;
        //**4PS.sn
        CalcBase := lvCalcBase;
        IF InventorySetup."Product Posting Group (Reval.)" <> '' THEN
          "Gen. Prod. Posting Group" := InventorySetup."Product Posting Group (Reval.)";  //db, 27-03-07
        IF SkipItemWithSameCostPrice AND ("Unit Cost (Revalued)" = "Unit Cost (Calculated)") THEN
          EXIT;  //db, 10-04-07 (testmelding 1698/1749: alleen toevoegen bij prijsverschil)
        //**4PS.en
        INSERT;
        OnAfterInsertItemJnlLine(ItemJnlLine);
      END;
    END;

    [External]
    PROCEDURE InitializeRequest@5(NewPostingDate@1000 : Date;NewDocNo@1004 : Code[20];NewHideDuplWarning@1008 : Boolean;NewCalculatePer@1001 : Option;NewByLocation@1002 : Boolean;NewByVariant@1003 : Boolean;NewUpdStdCost@1005 : Boolean;NewCalcBase@1006 : Option;NewShowDialog@1007 : Boolean);
    BEGIN
      PostingDate := NewPostingDate;
      NextDocNo := NewDocNo;
      CalculatePer := NewCalculatePer;
      ByLocation := NewByLocation;
      ByVariant := NewByVariant;
      UpdStdCost := NewUpdStdCost;
      CalcBase := NewCalcBase;
      ShowDialog := NewShowDialog;
      HideDuplWarning := NewHideDuplWarning;
    END;

    LOCAL PROCEDURE PageValidateCalcLevel@19046229();
    BEGIN
      IF CalculatePer = CalculatePer::"Item Ledger Entry" THEN BEGIN
        ByLocation := FALSE;
        ByVariant := FALSE;
        CalcBase := CalcBase::" ";
        UpdStdCost := FALSE;
      //**4PS.sn
      END ELSE BEGIN
        ByLocation := FALSE;
        ByVariant := FALSE;
        CalcBase := CalcBase::"Last Direct Unit Cost";
        UpdStdCost := TRUE;
      //**4PS.en
      END;
    END;

    LOCAL PROCEDURE ItemLedgerEntryCalculatePerOnV@19059582();
    BEGIN
      ValidateCalcLevel;
    END;

    LOCAL PROCEDURE ItemCalculatePerOnValidate@19037447();
    BEGIN
      ValidateCalcLevel;
    END;

    LOCAL PROCEDURE ItemJnlLineExists@12(ItemJnlLine@1007 : Record 83;ItemNo@1001 : Code[20];VariantCode@1002 : Code[10];LocationCode@1003 : Code[10];ApplyToEntry@1004 : Integer) : Boolean;
    BEGIN
      WITH ItemJnlLine DO BEGIN
        SETRANGE("Journal Template Name","Journal Template Name");
        SETRANGE("Journal Batch Name","Journal Batch Name");
        SETRANGE("Item No.",ItemNo);
        SETRANGE("Variant Code",VariantCode);
        SETRANGE("Location Code",LocationCode);
        SETRANGE("Applies-to Entry",ApplyToEntry);
        EXIT(NOT ISEMPTY);
      END;
    END;

    LOCAL PROCEDURE CalcItemLedgEntryActualCostTillPostingDate@98(ItemLedgEntryNo@1001 : Integer;PostingDate@1002 : Date) : Decimal;
    VAR
      ValueEntry@1000 : Record 5802;
    BEGIN
      WITH ValueEntry DO BEGIN
        SETCURRENTKEY("Item Ledger Entry No.");
        SETRANGE("Item Ledger Entry No.",ItemLedgEntryNo);
        SETFILTER("Posting Date",'<=%1',PostingDate);
        SETFILTER("Entry Type",'<>%1',"Entry Type"::Rounding);
        SETRANGE("Expected Cost",FALSE);
        CALCSUMS("Cost Amount (Actual)");
        EXIT("Cost Amount (Actual)");
      END;
    END;

    LOCAL PROCEDURE Calculate@10(Item@1001 : Record 27;VariantCode@1002 : Code[10];LocationCode@1003 : Code[10]);
    VAR
      AppliedAmount@1000 : Decimal;
    BEGIN
      ValJnlBuffer.CALCSUMS(Quantity,"Inventory Value (Calculated)");
      IF ValJnlBuffer.Quantity <> 0 THEN BEGIN
        AppliedAmount := 0;
        IF Item."Costing Method" = Item."Costing Method"::Average THEN
          CalcAverageUnitCost(
            ValJnlBuffer.Quantity,ValJnlBuffer."Inventory Value (Calculated)",AppliedAmount);
        InsertItemJnlLine(ItemJnlLine."Entry Type"::"Positive Adjmt.",
          Item."No.",VariantCode,LocationCode,ValJnlBuffer.Quantity,ValJnlBuffer."Inventory Value (Calculated)",
          0,AppliedAmount);
      END;
    END;

    [Integration]
    LOCAL PROCEDURE OnAfterInsertItemJnlLine@1001(VAR ItemJournalLine@1000 : Record 83);
    BEGIN
    END;

    PROCEDURE SetDefaultsRequestPage@1100485000();
    VAR
      lvText000@1100485000 : TextConst 'DEU=Wenn Sie dafÅr sorgen mîchten, dass alle Artikel aktualisiert worden sind, bevor Sie die Neubewertung starten, mÅssen Sie zuerst die Stapelbuchung %1 ausfÅhren.;ENU=To make sure that all items are adjusted before you start the revaluation you should run the %1 batch job first.;NLD=Als u ervoor wilt zorgen dat alle artikelen zijn bijgewerkt voordat u de herwaardering start, moet u eerst de batchverwerking %1 uitvoeren.;SVE=Om du vill se till att alla artiklar Ñr justerade innan du startar omvÑrderIngen ska du kîra batchjobbet %1 fîrst.';
      ObjTransl@1100485001 : Record 377;
    BEGIN
      //**4PS
      CalculatePer := CalculatePer::Item;
      MessTxt := STRSUBSTNO(lvText000,
        ObjTransl.TranslateObject(ObjTransl."Object Type"::Report,REPORT::"Adjust Cost - Item Entries"));
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

