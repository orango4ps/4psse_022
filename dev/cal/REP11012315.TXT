OBJECT Report 11012315 Remove Price History
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Preishistorie l”schen;
               ENU=Remove Price History;
               NLD=Prijshistorie verwijderen;
               NOR=Fjerne Prishistorikk;
               SVE=Ta bort prishistorik];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   GetItemSetup;
                 END;

    OnPostReport=BEGIN
                   StoreItemSetup;
                 END;

    UsageCategory=Tasks;
  }
  DATASET
  {
    { 4881;    ;DataItem;                    ;
               DataItemTable=Table11012317;
               OnPreDataItem=BEGIN
                               Status.OPEN(Text001);
                               NoOfLines := COUNT;
                               StatusCounter := 0;
                             END;

               OnAfterGetRecord=BEGIN
                                  StatusCounter := StatusCounter+1;
                                  Status.UPDATE(1,ROUND(StatusCounter/NoOfLines*10000,1));
                                  HistLimitDate := ItemSetup."Price History From";

                                  HistRec.SETRANGE("Item Code", "Item Code");
                                  HistRec.SETRANGE(Vendor, Vendor);
                                  HistRec.SETFILTER("Starting Date", '%1..', HistLimitDate);
                                  IF HistRec.COUNT = 0 THEN BEGIN
                                    HistRec.SETRANGE("Starting Date");
                                    IF HistRec.FINDLAST THEN
                                      HistLimitDate := HistRec."Starting Date";
                                  END;
                                  HistRec.SETFILTER("Starting Date", '<%1', HistLimitDate);
                                  //db,sn, 04-07-14: C010570
                                  IF HistRec.FINDLAST THEN BEGIN
                                    HistLimitDate := HistRec."Starting Date";
                                    HistRec.SETFILTER("Starting Date", '<%1', HistLimitDate);
                                  END;
                                  //db,en, 04-07-14: C010570

                                  HistRec.DELETEALL(TRUE);
                                END;

               OnPostDataItem=BEGIN
                                Status.CLOSE;
                              END;

               ReqFilterFields=Vendor,Item Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1210190000;;Container;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  ContainerType=ContentArea }

      { 1210190001;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100525000;2;Field  ;
                  CaptionML=[DEU=Preishistorieperiode;
                             ENU=Price History Period;
                             NLD=Prijshistorieperiode;
                             NOR=Prishistorikkperiode;
                             SVE=Prishistorikperiod];
                  SourceExpr=ItemSetup."Price History Period";
                  Editable=FALSE;
                  OnValidate=BEGIN
                               GetHistoryFilter;
                             END;
                              }

      { 1100525001;2;Field  ;
                  CaptionML=[DEU=Preishistorie ab;
                             ENU=Price History From;
                             NLD=Prijshistorie vanaf;
                             NOR=Prishistorikk fra;
                             SVE=Prishistorik fr†n];
                  SourceExpr=ItemSetup."Price History From";
                  Editable=FALSE }

      { 1100525002;2;Field  ;
                  CaptionML=[DEU=Preishistorie ab (Vorschlag);
                             ENU=Price History From (Proposal);
                             NLD=Prijshistorie vanaf (voorstel);
                             NOR=Prishistorikk fra (forslag);
                             SVE=Prishistorik fr†n (f”rslag)];
                  SourceExpr=PriceHistoryNew;
                  OnValidate=BEGIN
                               GetHistoryFilter();
                             END;
                              }

      { 1210190002;2;Field  ;
                  CaptionML=[DEU=Preishistorie l”schen;
                             ENU=Clear Price History;
                             NLD=Opschonen prijshistorie;
                             NOR=Fjern prishistorikk;
                             SVE=Rensa prishistorik];
                  SourceExpr=HistFilter;
                  Editable=FALSE }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      HistRec@1100485000 : Record 11012315;
      ItemSetup@1100485001 : Record 11012300;
      HistLimitDate@1100485002 : Date;
      HistFilter@1100485008 : Text[30];
      PriceHistoryNew@1100525000 : Date;
      Status@1100485005 : Dialog;
      NoOfLines@1100485004 : Integer;
      StatusCounter@1100485003 : Integer;
      Text001@1100485006 : TextConst 'DEU=Aktualisieren Preishistorie @1@@@@@@@@@@@@@@@@@@;ENU=Update Price History @1@@@@@@@@@@@@@@@@@@;NLD=Bijwerken prijshistorie @1@@@@@@@@@@@@@@@@@@;NOR=Oppdater prishistorikk @1@@@@@@@@@@@@@@@@@@;SVE=Uppdatera prishistorik @1@@@@@@@@@@@@@@@@@@';

    PROCEDURE GetHistoryFilter@1100525000();
    BEGIN
      HistFilter := STRSUBSTNO('..%1', PriceHistoryNew - 1);
    END;

    PROCEDURE GetItemSetup@1100525001();
    VAR
      PeriodTxt@1100525000 : Text[30];
      YearNo@1100525001 : Integer;
      RefDate@1100525002 : Date;
    BEGIN
      ItemSetup.GET;
      ItemSetup.TESTFIELD("Price History Period");
      PeriodTxt := '-' + FORMAT(ItemSetup."Price History Period");
      YearNo := DATE2DMY(TODAY,3);
      RefDate := DMY2DATE(1, 1, YearNo);
      PriceHistoryNew := CALCDATE(PeriodTxt,RefDate);
      GetHistoryFilter;
    END;

    PROCEDURE StoreItemSetup@1100525002();
    BEGIN
      ItemSetup.GET;
      ItemSetup."Price History From" := PriceHistoryNew;
      ItemSetup.MODIFY;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

