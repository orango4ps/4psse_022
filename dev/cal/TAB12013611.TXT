OBJECT Table 12013611 EX Document Line Dimension
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=EXF510000;
  }
  PROPERTIES
  {
    OnInsert=VAR
               ExDocLineDim@1100285000 : Record 12013611;
             BEGIN
               ExDocLineDim.RESET;
               ExDocLineDim.SETCURRENTKEY("Document No.");
               ExDocLineDim.SETRANGE("Document Type","Document Type");
               ExDocLineDim.SETRANGE("Document No.","Document No.");
               ExDocLineDim.SETRANGE("Line No.","Line No.");
               ExDocLineDim.SETRANGE("Dimension Code","Dimension Code");
               IF ExDocLineDim.FINDFIRST THEN
                 ERROR(STRSUBSTNO(EXF48,"Dimension Code"));

               Changeable;

               "Change Date" := CREATEDATETIME(TODAY,TIME);
               "Changed By" := USERID;
             END;

    OnModify=BEGIN
               Changeable;

               "Change Date" := CREATEDATETIME(TODAY,TIME);
               "Changed By" := USERID;
             END;

    OnDelete=VAR
               ExDoc@1100285000 : Record 12013608;
             BEGIN
               Changeable;

               IF ExDoc.GET("Document ID") THEN BEGIN
                 ExDoc."Change Date" := CREATEDATETIME(TODAY,TIME);
                 ExDoc."Changed By" := USERID;
                 ExDoc.MODIFY;
               END;
             END;

    CaptionML=[ENU=Document Line Dimension;
               SVE=Dokumentraddimension];
  }
  FIELDS
  {
    { 2   ;   ;Document Type       ;Option        ;CaptionML=[ENU=Document Type;
                                                              SVE=Dokumenttyp];
                                                   OptionCaptionML=[ENU=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order,,,Requisition;
                                                                    SVE=Offert,Order,Faktura,Kreditnota,Avropsorder,Returorder,,,Rekvisition];
                                                   OptionString=Quote,Order,Invoice,Credit Memo,Blanket Order,Return Order,,,Requisition }
    { 3   ;   ;Document No.        ;Code20        ;CaptionML=[ENU=Document No.;
                                                              SVE=Dokumentnr] }
    { 4   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              SVE=Radnr] }
    { 5   ;   ;Dimension Code      ;Code20        ;OnValidate=VAR
                                                                Column@1100285000 : Record 12013661;
                                                                DocLine@1100285002 : Record 12013609;
                                                                ColumnID@1100285001 : Integer;
                                                              BEGIN
                                                                ColumnID := ExFlow.GetColumnID("Dimension Code");

                                                                IF ColumnID = 0 THEN
                                                                  ERROR(STRSUBSTNO(EXF45,"Dimension Code"));

                                                                IF Column.GET(ColumnID) THEN
                                                                  IF Column."Dimension Code" <> '' THEN
                                                                    IF NOT DimMgt.CheckDim("Dimension Code") THEN
                                                                      ERROR(DimMgt.GetDimErr);

                                                                "Column ID" := ColumnID;

                                                                DocLine.RetrieveRecord(DocLine,"Document Type","Document No.","Line No.");
                                                                "Document Line ID" := DocLine.ID;
                                                                "Document ID" := DocLine."Document ID";
                                                              END;

                                                   CaptionML=[ENU=Dimension Code;
                                                              SVE=Dimensionskod] }
    { 10  ;   ;Dimension Value     ;Code20        ;TableRelation="Dimension Value".Code WHERE (Dimension Code=FIELD(Dimension Code));
                                                   OnValidate=VAR
                                                                Column@1100285000 : Record 12013661;
                                                                DocLine@1100285002 : Record 12013609;
                                                                ColumnID@1100285001 : Integer;
                                                              BEGIN
                                                                ColumnID := ExFlow.GetColumnID("Dimension Code");

                                                                IF ColumnID = 0 THEN
                                                                  ERROR(STRSUBSTNO(EXF45,"Dimension Code"));

                                                                IF Column.GET(ColumnID) THEN
                                                                  IF Column."Dimension Code" <> '' THEN
                                                                    IF NOT DimMgt.CheckDimValue("Dimension Code","Dimension Value") THEN
                                                                      ERROR(DimMgt.GetDimErr);

                                                                DocLine.RetrieveRecord(DocLine,"Document Type","Document No.","Line No.");
                                                                "Document Line ID" := DocLine.ID;
                                                                "Document ID" := DocLine."Document ID";
                                                              END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Dimension Value;
                                                              SVE=Dimensionsv„rde] }
    { 11  ;   ;Column ID           ;BigInteger    ;CaptionML=[ENU=Column ID;
                                                              SVE=Kolumn-ID] }
    { 12  ;   ;Document Line ID    ;BigInteger    ;CaptionML=[ENU=Document Line ID;
                                                              SVE=Dokumentrads ID] }
    { 13  ;   ;ID                  ;BigInteger    ;AutoIncrement=Yes;
                                                   CaptionML=[ENU=ID;
                                                              SVE=ID] }
    { 14  ;   ;Dimension Desc.     ;Text200       ;CaptionML=[ENU=Description;
                                                              SVE=Beskrivning] }
    { 31  ;   ;Document ID         ;BigInteger    ;CaptionML=[ENU=Document ID;
                                                              SVE=Dokument-ID] }
    { 600 ;   ;Changed By          ;Code50        ;CaptionML=[ENU=Changed By;
                                                              SVE=Žndrad av];
                                                   Editable=No }
    { 601 ;   ;Change Date         ;DateTime      ;CaptionML=[ENU=Change Date;
                                                              SVE=Žndrad datum];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
    {    ;Document No.                             }
    {    ;Document Type,Document No.,Line No.,Dimension Code }
    {    ;Document Line ID                         }
    {    ;Document ID                              }
    {    ;Column ID,Dimension Value                }
    {    ;Dimension Code                           }
    {    ;Document Line ID,Change Date             }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ExFlow@1100285002 : Codeunit 12013601;
      DimMgt@1100285000 : Codeunit 408;
      EXF02@1100285007 : TextConst 'ENU=Dimensions can not be changed for an approved invoice!;SVE=Dimensioner f†r inte „ndras f”r en attesterad faktura!';
      EXF41@1100285006 : TextConst 'ENU=Warning! The line has been approved!\\Do you still want to update line?;SVE=Varning! Raden har attesterats! \\Vill du „nd† uppdatera raden?';
      EXF42@1100285004 : TextConst 'ENU=The Line is Ready for Posting and needs to be restarted before it can be changed!;SVE=Raden „r klar f”r bokf”ring och beh”ver †terstartas f”r att kunna „ndras!';
      EXF45@1100285005 : TextConst 'ENU=ExFlow do not use Dimension %1!;SVE=Dimensionen %1 anv„nds inte av ExFlow!';
      EXF47@1100285008 : TextConst 'ENU=Operation aborted.;SVE=Funktionen avbryten.';
      EXF48@1100285010 : TextConst 'ENU=Dimension %1 already exists.;SVE=Dimension %1 finns redan.';

    LOCAL PROCEDURE Changeable@1100285001();
    VAR
      DocHead@1100285001 : Record 12013608;
      AppSetup@1100285002 : Record 12013601;
      ExFlow@1100285000 : Codeunit 12013601;
    BEGIN
      IF DocHead.RetrieveRecord(DocHead,"Document Type","Document No.") THEN BEGIN
        IF DocHead.Status = DocHead.Status::"Ready for posting" THEN
          ERROR(EXF42);

        IF DocHead.Status > DocHead.Status::Active THEN BEGIN
          IF NOT ExFlow.IsUserSuperior(USERID) THEN
            ERROR(EXF02);

          AppSetup.GET;
          IF NOT AppSetup."Suppress Approved Message" THEN
            IF NOT CONFIRM(EXF41) THEN
              ERROR(EXF47);
        END;
      END;
    END;

    PROCEDURE RetrieveRecord@1100285000(VAR ExDocLineDim@1100285005 : Record 12013611;DocType@1100285001 : Integer;DocNo@1100285002 : Code[20];LineNo@1100285003 : Integer;DimCode@1100285004 : Code[20]) : Boolean;
    BEGIN
      WITH ExDocLineDim DO BEGIN
        RESET;
        CLEAR(ExDocLineDim);
        SETCURRENTKEY("Document No.");
        SETRANGE("Document Type", DocType);
        SETRANGE("Document No.", DocNo);
        SETRANGE("Line No.", LineNo);
        SETRANGE("Dimension Code", DimCode);
        EXIT(FINDFIRST);
      END;
    END;

    PROCEDURE ClearEmptyRecords@1100285002();
    VAR
      ExDocLineDim@1100285000 : Record 12013611;
    BEGIN
      ExDocLineDim.SETCURRENTKEY("Document Line ID");
      ExDocLineDim.SETRANGE("Document Line ID", "Document Line ID");
      ExDocLineDim.SETRANGE("Dimension Value", '');
      IF NOT ExDocLineDim.ISEMPTY THEN
        ExDocLineDim.DELETEALL;
    END;

    BEGIN
    END.
  }
}

