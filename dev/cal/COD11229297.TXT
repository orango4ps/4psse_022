OBJECT Codeunit 11229297 Impl. Activity Graph Functions
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      TempRec@1100525000 : TEMPORARY Record 6529 SECURITYFILTERING(Ignored);

    LOCAL PROCEDURE InsertTempRec@1100525077(Index@1100525000 : Integer;Name@1100525001 : Text;Key@1100525002 : Code[20]);
    BEGIN
      WITH TempRec DO BEGIN
        INIT;
        "Entry No." := Index;
        "Table Name" := Name;
        "Search Record ID" := Key;
        INSERT;
      END;
    END;

    LOCAL PROCEDURE GetTempRec@1100525081(Index@1100525001 : Integer;Name@1100525000 : Text) Key : Code[20];
    BEGIN
      WITH TempRec DO BEGIN
        SETRANGE("Entry No.",Index);
        SETFILTER("Table Name",Name);
        FINDFIRST;
        Key := "Search Record ID";
        EXIT;
      END;
    END;

    LOCAL PROCEDURE OpenImplActivityPage@1100525001(SelectedKey@1100525000 : Code[20];SelectedField@1100525003 : 'WorkGroup,Subject';SelectedStatus@1100525004 : Boolean);
    VAR
      ImplementationActivities@1100525002 : Page 11125690;
      ImplActivity@1100525001 : Record 11229390;
    BEGIN
      IF SelectedField = SelectedField::WorkGroup THEN
        ImplActivity.SETRANGE("Work Group",SelectedKey);
      IF SelectedField = SelectedField::Subject THEN
        ImplActivity.SETRANGE(Subject,SelectedKey);
      IF SelectedStatus THEN
        ImplActivity.SETRANGE(Open,TRUE)
      ELSE
        ImplActivity.SETRANGE(Open,FALSE);

      ImplementationActivities.SETTABLEVIEW(ImplActivity);
      ImplementationActivities.RUNMODAL();
    END;

    PROCEDURE DrilldownOnGraph@1100525004(Index@1100525000 : Integer;SelectedField@1100525002 : 'WorkGroup,Subject';SelectedStatus@1100525001 : Boolean;Name@1100525004 : Text);
    VAR
      SelectedKey@1100525003 : Code[20];
    BEGIN
      SelectedKey:=GetTempRec(Index,Name);
      OpenImplActivityPage(SelectedKey,SelectedField,SelectedStatus);
    END;

    PROCEDURE GenerateChartDataForSubject@1100525000(VAR BusChartBuf@1100525000 : Record 485;SelectedStatus@1100525007 : Boolean;Name@1100525006 : Text);
    VAR
      I@1100525001 : Integer;
      ImplActivitySubject@1100525002 : Record 11229396;
      ImplActivity@1100525003 : Record 11229390;
      NumberOfRecords@1100525004 : Integer;
    BEGIN
      WITH BusChartBuf DO BEGIN
        Initialize;
        AddMeasure(Name,1,"Data Type"::Decimal,"Chart Type"::Column);
        SetXAxis(ImplActivitySubject.TABLECAPTION,"Data Type"::String);
        ImplActivitySubject.SETRANGE(ImplActivitySubject.Type,ImplActivitySubject.Type::Subject);
        I:=0;
        IF ImplActivitySubject.FINDFIRST THEN
        REPEAT
          ImplActivity.SETRANGE(Subject, ImplActivitySubject.Code);
          ImplActivity.SETRANGE(Open, SelectedStatus);
          NumberOfRecords:=ImplActivity.COUNT;
          IF NumberOfRecords > 0 THEN BEGIN
            AddColumn(ImplActivitySubject.Description);
            SetValueByIndex(0,I,NumberOfRecords);
            InsertTempRec(I,Name,ImplActivitySubject.Code);
            I:=I+1;
          END;
        UNTIL ImplActivitySubject.NEXT = 0
      END;
    END;

    PROCEDURE GenerateChartDataForWorkGroup@1100525002(VAR BusChartBuf@1100525000 : Record 485;SelectedStatus@1100525005 : Boolean;Name@1100525004 : Text);
    VAR
      I@1100525001 : Integer;
      ImplActivityWorkGroup@1100525002 : Record 11229394;
      NumberOfActivities@1100525003 : Integer;
    BEGIN
      WITH BusChartBuf DO BEGIN
        Initialize;
        AddMeasure(Name,1,"Data Type"::Decimal,"Chart Type"::Column);
        SetXAxis(ImplActivityWorkGroup.TABLECAPTION,"Data Type"::String);
        I:=0;
        IF ImplActivityWorkGroup.FINDFIRST THEN
        REPEAT
          ImplActivityWorkGroup.CALCFIELDS("No. Of Open Activities");
          ImplActivityWorkGroup.CALCFIELDS("No. Of Activities");
          IF SelectedStatus THEN
            NumberOfActivities:=ImplActivityWorkGroup."No. Of Open Activities"
          ELSE
            NumberOfActivities:=ImplActivityWorkGroup."No. Of Activities" - ImplActivityWorkGroup."No. Of Open Activities";

          IF NumberOfActivities > 0 THEN BEGIN
            AddColumn(ImplActivityWorkGroup.Description);
            SetValueByIndex(0,I,NumberOfActivities);
            InsertTempRec(I,Name,ImplActivityWorkGroup.Code);
            I:=I+1;
          END;
        UNTIL ImplActivityWorkGroup.NEXT = 0
      END;
    END;

    BEGIN
    END.
  }
}

