OBJECT Table 5196 To-do Interaction Language
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVW114.00,4PS14.00;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               RemoveAttachment(FALSE);
             END;

    CaptionML=[ENU=Task Interaction Language;
               NOR=Spr†k for oppgavesamhandling];
    LookupPageID=Page5196;
  }
  FIELDS
  {
    { 1   ;   ;To-do No.           ;Code20        ;TableRelation=To-do;
                                                   CaptionML=[ENU=Task No.;
                                                              NOR=Oppgavenr.;
                                                              SVE=Uppgiftsnr.] }
    { 2   ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   CaptionML=[ENU=Language Code;
                                                              NOR=Spr†kkode;
                                                              SVE=Spr†kkod] }
    { 3   ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              NOR=Beskrivelse;
                                                              SVE=Beskrivning] }
    { 4   ;   ;Attachment No.      ;Integer       ;TableRelation=Attachment;
                                                   CaptionML=[ENU=Attachment No.;
                                                              NOR=Vedleggsnr.;
                                                              SVE=Bilagenr] }
  }
  KEYS
  {
    {    ;To-do No.,Language Code                 ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1002 : TextConst 'ENU=You have canceled the create process.;NOR=Du har avbrutt opprettingen.';
      Text001@1001 : TextConst 'ENU=Replace existing attachment?;NOR=Erstatte eksisterende vedlegg?';
      Text002@1000 : TextConst 'ENU=You have canceled the import process.;NOR=Du har avbrutt importen.';
      Text003@1003 : TextConst 'ENU=You cannot create attachments here.;NOR=Du kan ikke opprette vedlegg her.';
      TemplateCode@1100528800 : Code[10];

    [Internal]
    PROCEDURE CreateAttachment@1(PageNotEditable@1004 : Boolean) : Boolean;
    VAR
      Attachment@1000 : Record 11072030;
      WordManagement@1002 : Codeunit 11072023;
      NewAttachNo@1003 : Integer;
    BEGIN
      IF PageNotEditable THEN
        ERROR(Text003);
      IF "Attachment No." <> 0 THEN BEGIN
        IF Attachment.GET("Attachment No.") THEN
          Attachment.TESTFIELD("Read Only",FALSE);
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
      END;

      NewAttachNo := WordManagement.CreateWordAttachment("To-do No." + ' ' + Description,"Language Code");
      IF NewAttachNo <> 0 THEN BEGIN
        IF "Attachment No." <> 0 THEN
          RemoveAttachment(FALSE);
        "Attachment No." := NewAttachNo;
        MODIFY;
        EXIT(TRUE);
      END;
      ERROR(Text000);
    END;

    [Internal]
    PROCEDURE OpenAttachment@2(PageNotEditable@1001 : Boolean);
    VAR
      Attachment@1000 : Record 11072030;
    BEGIN
      IF "Attachment No." = 0 THEN
        EXIT;
      Attachment.GET("Attachment No.");
      IF PageNotEditable THEN
        Attachment."Read Only" := TRUE;
      Attachment.SetTemplateCode(TemplateCode); //**4PS.n
      Attachment.OpenAttachment("To-do No." + ' ' + Description,FALSE,"Language Code");
    END;

    [Internal]
    PROCEDURE CopyFromAttachment@3();
    VAR
      TaskInteractionLanguage@1000 : Record 5196;
      Attachment@1001 : Record 11072030;
      AttachmentManagement@1002 : Codeunit 11072021;
      NewAttachNo@1003 : Integer;
    BEGIN
      IF Attachment.GET("Attachment No.") THEN
        Attachment.TESTFIELD("Read Only",FALSE);

      IF "Attachment No." <> 0 THEN BEGIN
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
        RemoveAttachment(FALSE);
        "Attachment No." := 0;
        MODIFY;
        COMMIT;
      END;

      TaskInteractionLanguage.SETFILTER("Attachment No.",'<>%1',0);
      IF PAGE.RUNMODAL(0,TaskInteractionLanguage) = ACTION::LookupOK THEN BEGIN
        NewAttachNo := AttachmentManagement.InsertAttachment(TaskInteractionLanguage."Attachment No.");
        IF NewAttachNo <> 0 THEN BEGIN
          "Attachment No." := NewAttachNo;
          MODIFY;
        END;
      END;
    END;

    [Internal]
    PROCEDURE ImportAttachment@5();
    VAR
      Attachment@1000 : Record 11072030;
      TempAttachment@1001 : TEMPORARY Record 11072030;
      MarketingSetup@1003 : Record 5079;
      AttachmentManagement@1002 : Codeunit 11072021;
      FileName@1004 : Text;
    BEGIN
      IF "Attachment No." <> 0 THEN BEGIN
        IF Attachment.GET("Attachment No.") THEN
          Attachment.TESTFIELD("Read Only",FALSE);
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
      END;

      CLEAR(TempAttachment);
      IF TempAttachment.ImportAttachmentFromClientFile('',TRUE,FALSE) THEN BEGIN
        IF "Attachment No." = 0 THEN
          Attachment.GET(AttachmentManagement.InsertAttachment(0))
        ELSE
          Attachment.GET("Attachment No.");
        TempAttachment."No." := Attachment."No.";
        TempAttachment."Storage Pointer" := Attachment."Storage Pointer";
        TempAttachment.WizSaveAttachment;
        MarketingSetup.GET;
        IF MarketingSetup."Attachment Storage Type" = MarketingSetup."Attachment Storage Type"::"Disk File" THEN
          IF TempAttachment."No." <> 0 THEN BEGIN
            FileName := TempAttachment.ConstDiskFileName;
            IF FileName <> '' THEN
              Attachment.ExportAttachmentToServerFile(FileName);
          END;

        Attachment."Storage Type" := TempAttachment."Storage Type";
        Attachment."Storage Pointer" := TempAttachment."Storage Pointer";
        Attachment."Attachment File" := TempAttachment."Attachment File";
        Attachment."File Extension" := TempAttachment."File Extension";
        Attachment.MODIFY;
        "Attachment No." := Attachment."No.";
        MODIFY;
      END ELSE
        ERROR(Text002);
    END;

    [Internal]
    PROCEDURE ExportAttachment@6();
    VAR
      Attachment@1000 : Record 11072030;
      FileName@1001 : Text[1024];
    BEGIN
      IF Attachment.GET("Attachment No.") THEN
        Attachment.ExportAttachmentToClientFile(FileName);
    END;

    [Internal]
    PROCEDURE RemoveAttachment@7(Prompt@1000 : Boolean) : Boolean;
    VAR
      Attachment@1001 : Record 11072030;
    BEGIN
      IF Attachment.GET("Attachment No.") THEN
        IF Attachment.RemoveAttachment(Prompt) THEN BEGIN
          "Attachment No." := 0;
          MODIFY;
          EXIT(TRUE);
        END;
    END;

    PROCEDURE SetTemplateCode@1100528800(lTemplateCode@1100528800 : Code[10]);
    BEGIN
      //**4PS
      TemplateCode := lTemplateCode;
    END;

    BEGIN
    END.
  }
}

