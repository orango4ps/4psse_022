OBJECT Page 11127228 Service Orders WHA WS
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=;
    SourceTable=Table11012823;
    PageType=List;
    SourceTableTemporary=Yes;
  }
  CONTROLS
  {
    { 1100528000;;Container;
                Name=Container;
                ContainerType=ContentArea }

    { 1100528001;1;Field  ;
                Name=ResourceNo;
                SourceExpr=ResourceNo }

    { 1100528007;1;Field  ;
                Name=FromDate;
                SourceExpr=FromDate;
                OnValidate=BEGIN
                             FillSourceTable();
                           END;
                            }

    { 1100528002;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1100528003;2;Field  ;
                Name=CompanyName;
                SourceExpr=CURRENTCOMPANY }

    { 1100528004;2;Field  ;
                Name=No;
                SourceExpr="No." }

    { 1100528005;2;Field  ;
                Name=Description;
                SourceExpr=Description }

    { 1100528006;2;Field  ;
                Name=Deleted;
                SourceExpr=GetDeleted }

  }
  CODE
  {
    VAR
      ResourceNo@1100528000 : Code[20];
      FromDate@1100528002 : DateTime;
      GuidDeleted@1100528001 : GUID;
      GetAll@1100528700 : Boolean;

    LOCAL PROCEDURE FillSourceTable@1100527003();
    VAR
      EmpolySetup@1100528003 : Record 11126692;
      EmpolyWarehouseMgt@1100528002 : Codeunit 11126681;
      FilterString@1100528001 : Text;
      ServiceOrder@1100527000 : Record 11012823;
      DeletedDataApps@1100526000 : Record 11229871;
    BEGIN
      Rec.RESET;
      Rec.DELETEALL;

      GetAll := DATE2DMY(DT2DATE(FromDate), 3) <= 1950;

      EmpolySetup.GET;
      FilterString := EmpolyWarehouseMgt.SetSOFilter;
      IF (FilterString <> '') THEN BEGIN
        IF (EmpolySetup."SO Status Check" = EmpolySetup."SO Status Check"::RefPoint) THEN
          ServiceOrder.SETFILTER("Actual Reference Point", FilterString)
        ELSE
          ServiceOrder.SETFILTER(Status, FilterString);
      END;

      IF NOT GetAll THEN
        ServiceOrder.SETFILTER("Modified on", '%1..', DT2DATE(FromDate) - 1);
      IF ServiceOrder.FINDSET THEN
        REPEAT
          Rec.TRANSFERFIELDS(ServiceOrder);
          IF Rec.INSERT THEN;
        UNTIL ServiceOrder.NEXT = 0;

      GuidDeleted := CREATEGUID;

      DeletedDataApps.SETRANGE("Table ID", DATABASE::"Service Order");
      DeletedDataApps.SETFILTER(Date, '%1..', DT2DATE(FromDate) - 1);
      IF DeletedDataApps.FINDSET THEN
        REPEAT
          IF NOT ServiceOrder.GET(DeletedDataApps."Code 1") THEN BEGIN
            Rec.INIT;
            Rec."No." := DeletedDataApps."Code 1";
            Rec.Description := FORMAT(GuidDeleted);
            IF Rec.INSERT THEN;
          END;
        UNTIL DeletedDataApps.NEXT = 0;
    END;

    LOCAL PROCEDURE GetDeleted@1100526001() : Boolean;
    BEGIN
      EXIT(Description = FORMAT(GuidDeleted));
    END;

    BEGIN
    END.
  }
}

