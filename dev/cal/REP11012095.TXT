OBJECT Report 11012095 Create Cycle Blanket Scheme
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Zyklus Abrufschema erstellen;
               ENU=Create Cycle Blanket Scheme;
               NLD=Cyclus afroepschema aanmaken;
               NOR=Opprett blankettskjema (syklisk);
               SVE=Skapa cykliskt avropsschema];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF (StartingDate = 0D) OR (Cycle = EmptyCycle) THEN
                    ERROR(Text002);

                  ValidateFirstDate();
                  ValidateEndingDate();
                END;

  }
  DATASET
  {
    { 6547;    ;DataItem;                    ;
               DataItemTable=Table39;
               DataItemTableView=SORTING(Document Type,Document No.,Line No.);
               OnPreDataItem=BEGIN
                               COPY(gPurchLineRec);
                             END;

               OnAfterGetRecord=VAR
                                  FPSLicenseManagement@1100527400 : Codeunit 11229289;
                                BEGIN
                                  CALCFIELDS("Total Quantity Blanket Scheme");
                                  QuantityToDo := Quantity - "Total Quantity Blanket Scheme";
                                  IF (QuantityToDo <= 0) THEN
                                    CurrReport.SKIP;

                                  BlanketSchemeRec.RESET;
                                  BlanketSchemeRec.SETCURRENTKEY("Document No." ,"Document Line No.", "Blanket Date");
                                  BlanketSchemeRec2.RESET;
                                  BlanketSchemeRec2.SETCURRENTKEY("Document No." ,"Document Line No.", "Blanket Date");
                                  BlanketSchemeFound := TRUE;
                                  NextDate := FirstDate;

                                  WHILE (QuantityToDo > 0) AND ((NextDate <= EndingDate) OR (EndingDate = 0D)) AND (BlanketSchemeFound) DO BEGIN
                                    BlanketSchemeFound := FALSE;
                                    BlanketSchemeRec.SETCURRENTKEY("Document No." ,"Document Line No.", "Blanket Date");
                                    BlanketSchemeRec.SETRANGE("Document No.", "Document No.");
                                    BlanketSchemeRec.SETRANGE("Document Line No.", "Line No.");
                                    BlanketSchemeRec.SETRANGE("Blanket Date", StartingDate);
                                    BlanketSchemeRec.SETFILTER(Quantity, '>0');
                                    IF BlanketSchemeRec.FIND('-') THEN BEGIN
                                      REPEAT
                                        BlanketSchemeFound := TRUE;
                                        BlanketSchemeRec2.SETRANGE("Document No.", BlanketSchemeRec."Document No.");
                                        BlanketSchemeRec2.SETRANGE("Document Line No.", BlanketSchemeRec."Document Line No.");
                                        BlanketSchemeRec2.SETRANGE("Blanket Date", NextDate);
                                        BlanketSchemeRec2.SETRANGE(Deliver, BlanketSchemeRec.Deliver);
                                        BlanketSchemeRec2.SETRANGE("Ship To Location", BlanketSchemeRec."Ship To Location");
                                        BlanketSchemeRec2.SETRANGE("Project No.", BlanketSchemeRec."Project No.");  //DP00416.n
                                        BlanketSchemeRec2.SETRANGE("Plot No.", BlanketSchemeRec."Plot No.");  //DP00416.n
                                        IF NOT BlanketSchemeRec2.FIND('-') THEN BEGIN
                                          IF (QuantityToDo >= BlanketSchemeRec.Quantity) THEN BEGIN
                                            BlanketQuantity := BlanketSchemeRec.Quantity;
                                            QuantityToDo := QuantityToDo - BlanketQuantity;
                                          END ELSE BEGIN
                                            BlanketQuantity := QuantityToDo;
                                            QuantityToDo := 0;
                                          END;
                                          BlanketSchemeRec2.INIT;
                                          BlanketSchemeRec2."Document No." := BlanketSchemeRec."Document No.";
                                          BlanketSchemeRec2."Document Line No." := BlanketSchemeRec."Document Line No.";
                                          BlanketSchemeRec2."Line No." := 0;
                                          BlanketSchemeRec2."Blanket Date" := NextDate;
                                          BlanketSchemeRec2.Quantity := BlanketQuantity;
                                          BlanketSchemeRec2.Deliver := BlanketSchemeRec.Deliver;
                                          BlanketSchemeRec2."Ship To Location" := BlanketSchemeRec."Ship To Location";
                                          BlanketSchemeRec2."Project No." := BlanketSchemeRec."Project No.";  //DP00416.n
                                          BlanketSchemeRec2."Plot No." := BlanketSchemeRec."Plot No.";  //DP00416.n
                                          BlanketSchemeRec2.INSERT(TRUE);
                                        END;
                                      UNTIL (BlanketSchemeRec.NEXT = 0) OR (QuantityToDo <= 0)
                                    END;

                                    NextDate := CalculateNextDate(NextDate);

                                    IF FPSLicenseManagement.LicenseAndReadPermissionForTable(BaseCalRec) THEN BEGIN  //db.sn, 29-12-06
                                      CompInfoRec.GET;
                                      REPEAT
                                        NonWorkDate := CalendarMgt.CheckDateStatus(CompInfoRec."Base Calendar Code", NextDate, dummyDesc);
                                        IF NonWorkDate = TRUE THEN
                                          NextDate := CalculateNextDate(NextDate);
                                      UNTIL (NonWorkDate = FALSE) OR (NextDate > EndingDate);
                                    END;                                     //db.en, 29-12-06
                                  END;
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   StartingDate := gStartingDate;
                   IF (StartingDate = 0D) THEN
                     StartingDate := TODAY;

                   FirstDate := CalculateNextDate(StartingDate);
                 END;

    }
    CONTROLS
    {
      { 1100525000;;Container;
                  ContainerType=ContentArea }

      { 1100525001;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options;
                             NLD=Opties;
                             NOR=Alternativer;
                             SVE=Alternativ];
                  GroupType=Group }

      { 1100525002;2;Field  ;
                  CaptionML=[DEU=Startdatum;
                             ENU=Starting Date;
                             NLD=Begindatum;
                             NOR=Startdato;
                             SVE=Startdatum];
                  SourceExpr=StartingDate;
                  OnValidate=BEGIN
                               IF (StartingDate = 0D) OR (Cycle = EmptyCycle) THEN
                                 EXIT;

                               ValidateFirstDate();
                             END;
                              }

      { 1100525003;2;Field  ;
                  CaptionML=[DEU=Zyklus;
                             ENU=Cycle;
                             NLD=Cyclus;
                             NOR=Syklus;
                             SVE=Cyklisk];
                  SourceExpr=Cycle;
                  OnValidate=BEGIN
                               IF (StartingDate = 0D) OR (Cycle = EmptyCycle) THEN
                                 EXIT;

                               ValidateFirstDate();
                             END;
                              }

      { 1100525004;2;Field  ;
                  CaptionML=[DEU=Erzeugen ab;
                             ENU=Generate from;
                             NLD=Genereren vanaf;
                             NOR=Generer fra;
                             SVE=Generera fr†n];
                  SourceExpr=FirstDate;
                  Editable=FALSE }

      { 1100525005;2;Field  ;
                  CaptionML=[DEU=Erzeugen bis einschl.;
                             ENU=Generate upto;
                             NLD=Genereren t/m;
                             NOR=Generer opp til;
                             SVE=Generera upp till];
                  SourceExpr=EndingDate;
                  OnValidate=BEGIN
                               ValidateEndingDate();
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      gPurchLineRec@1210190000 : Record 39;
      Text000@1210190001 : TextConst 'DEU=Datum ''Erzeugen ab'' muss gr”sser als das Startdatum sein.;ENU=Date ''Generate from'' must be greater than the Starting Date.;NLD=Datum ''Genereren vanaf'' moet groter zijn dan de begindatum.;NOR=''Generer fra''-datoen m† inntreffe senere enn startdatoen.;SVE=''Generera fr†n''-datumet m†ste infalla senare „n startdatumet.';
      BlanketSchemeRec@1210190010 : Record 11012066;
      BlanketSchemeRec2@1210190013 : Record 11012066;
      CompInfoRec@1100485001 : Record 79;
      BaseCalRec@1100485000 : Record 11072014;
      Cycle@1210190002 : DateFormula;
      EmptyCycle@1210190007 : DateFormula;
      gStartingDate@1210190008 : Date;
      StartingDate@1210190003 : Date;
      EndingDate@1210190004 : Date;
      FirstDate@1210190005 : Date;
      Text001@1210190006 : TextConst 'DEU=Datum ''Erzeugen ab'' darf nicht kleiner als das heutige Datum sein.;ENU=Date ''Generate from'' may not be less than current date.;NLD=Datum ''Genereren vanaf'' mag niet kleiner zijn dan de huidige datum.;NOR=''Generer fra''-datoen kan ikke inntreffe tidligere enn dagens dato.;SVE=''Generera fr†n''-datumet kan inte infalla tidigare „n dagens datum.';
      Text002@1210190009 : TextConst 'DEU=Startdatum und/oder Zyklus nicht eingegeben.;ENU=Starting Date and/or Cycle not filled.;NLD=Begindatum en/of cyclus niet gevuld.;NOR=Startdato og/eller syklus er ikke fylt ut.;SVE=Startdatum och/eller cykel har inte fyllts i.';
      NextDate@1210190011 : Date;
      BlanketSchemeFound@1210190016 : Boolean;
      QuantityToDo@1210190012 : Decimal;
      Text003@1210190014 : TextConst 'DEU=Datum ''Erzeugen bis einschl.'' darf nicht kleiner als das Datum ''Genenieren ab'' sein.;ENU=Date ''Generate upto'' may not be less than date ''Generate from''.;NLD=Datum ''Genereren t/m'' mag niet kleiner zijn dan datum ''Genereren vanaf''.;NOR=''Generer opp til''-datoen kan ikke inntreffe tidligere enn ''Generer fra''-datoen.;SVE=''Generera upp till''-datumet kan inte infalla tidigare „n ''Generera fr†n''-datumet.';
      BlanketQuantity@1210190015 : Decimal;
      NonWorkDate@1100485002 : Boolean;
      CalendarMgt@1100485003 : Codeunit 7600;
      dummyDesc@1100485004 : Text[30];

    PROCEDURE InitSelections@1210190000(VAR IPurchLineRec@1210190000 : Record 39;IStartingDate@1210190001 : Date);
    BEGIN
      gPurchLineRec.COPY(IPurchLineRec);
      gStartingDate := IStartingDate;
    END;

    PROCEDURE ValidateFirstDate@1210190002();
    BEGIN
      FirstDate := CalculateNextDate(StartingDate);
      IF (FirstDate <= StartingDate) THEN
        ERROR(Text000);

      IF (FirstDate < TODAY) THEN
        ERROR(Text001);
    END;

    PROCEDURE ValidateEndingDate@1210190005();
    BEGIN
      IF (EndingDate =0D) THEN
        EXIT;

      IF (EndingDate < FirstDate) THEN
        ERROR(Text003);
    END;

    PROCEDURE CalculateNextDate@1210190001(IDate@1210190000 : Date) : Date;
    BEGIN
      IF (IDate = 0D) OR (Cycle = EmptyCycle) THEN
        EXIT(IDate);

      EXIT(CALCDATE(Cycle, IDate));
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

