OBJECT Table 11012440 Project Planning Team Resource
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=4PS14.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               TeamResPeriodRec@1100525000 : Record 11125714;
             BEGIN
               IF "Resource Type" = "Resource Type"::Plant THEN
                 TESTFIELD("Plant No.");

               TeamResPeriodRec.INIT;
               TeamResPeriodRec."Team Code" := "Team Code";
               TeamResPeriodRec."Resource Type" := "Resource Type";
               TeamResPeriodRec."Resource Code" := "Resource Code";
               TeamResPeriodRec."Plant No." := "Plant No.";
               TeamResPeriodRec."Date in Team" := TODAY;
               TeamResPeriodRec.INSERT(TRUE);
             END;

    OnModify=BEGIN
               IF "Resource Type" = "Resource Type"::Plant THEN
                 TESTFIELD("Plant No.");
             END;

    OnDelete=VAR
               TeamResPeriodRec@1100525000 : Record 11125714;
             BEGIN
               TeamResPeriodRec.SETRANGE("Team Code", "Team Code");
               TeamResPeriodRec.SETRANGE("Resource Type", "Resource Type");
               TeamResPeriodRec.SETRANGE("Resource Code", "Resource Code");
               TeamResPeriodRec.SETRANGE("Plant No.", "Plant No.");
               TeamResPeriodRec.DELETEALL;
             END;

    CaptionML=[ENU=Project Planning Team Resource;
               NOR=Prosjektplanlegging, teamressurs;
               SVE=Resurs i projektplaneringsteam];
  }
  FIELDS
  {
    { 10  ;   ;Team Code           ;Code20        ;TableRelation="Project Planning Team";
                                                   CaptionML=[ENU=Team Code;
                                                              NOR=Prosjektgruppekode;
                                                              SVE=Lagkkod] }
    { 20  ;   ;Resource Type       ;Option        ;OnValidate=BEGIN
                                                                IF "Resource Type" <> xRec."Resource Type" THEN BEGIN
                                                                  "Resource Code" := '';
                                                                  "Plant No." := '';
                                                                  INIT;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Resource Type;
                                                              NOR=Ressurstype;
                                                              SVE=Resurstyp];
                                                   OptionCaptionML=[ENU=Employee,Plant;
                                                                    NOR=Ansatt,Maskin;
                                                                    SVE=Anst„lld,Maskin];
                                                   OptionString=Employee,Plant }
    { 30  ;   ;Resource Code       ;Code20        ;TableRelation=IF (Resource Type=CONST(Employee)) Employee
                                                                 ELSE IF (Resource Type=CONST(Plant)) "Plant Type";
                                                   OnValidate=VAR
                                                                EmplRec@1100525002 : Record 5200;
                                                                PlantTypeRec@1100525001 : Record 11012551;
                                                                EmplTimeTableRec@1100525000 : Record 11012923;
                                                              BEGIN
                                                                IF "Resource Code" <> xRec."Resource Code" THEN BEGIN
                                                                  "Plant No." := '';
                                                                  Discipline := '';
                                                                END;

                                                                IF "Resource Code" = '' THEN
                                                                  EXIT;

                                                                CASE "Resource Type" OF
                                                                  "Resource Type"::Employee:
                                                                    BEGIN
                                                                      EmplRec.GET("Resource Code");
                                                                      //IF NOT (EmplRec.Schedule IN [EmplRec.Schedule::Project, EmplRec.Schedule::Both]) THEN
                                                                      //  EmplRec.FIELDERROR(Schedule);
                                                                      EmplTimeTableRec.SETRANGE("Employee No.", "Resource Code");
                                                                      EmplTimeTableRec.FINDFIRST;  //* Employee Time Table must be present (for Gantt Chart)
                                                                      IF Discipline = '' THEN
                                                                        Discipline := EmplRec.Discipline;
                                                                    END;
                                                                  "Resource Type"::Plant:
                                                                    BEGIN
                                                                      PlantTypeRec.GET("Resource Code");
                                                                      PlantTypeRec.TESTFIELD(Bulk, FALSE);
                                                                    END;
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupResourceCode(TRUE);
                                                            END;

                                                   CaptionML=[ENU=Resource Code;
                                                              NOR=Ressurskode;
                                                              SVE=Resurskod];
                                                   NotBlank=Yes }
    { 40  ;   ;Plant No.           ;Code10        ;TableRelation=IF (Resource Type=CONST(Plant)) "Plant Number".No. WHERE (Plant Type=FIELD(Resource Code));
                                                   OnValidate=BEGIN
                                                                IF "Plant No." <> '' THEN BEGIN
                                                                  TESTFIELD("Resource Type", "Resource Type"::Plant);
                                                                  TESTFIELD("Resource Code");
                                                                END ELSE BEGIN
                                                                  IF "Resource Type" = "Resource Type"::Plant THEN
                                                                    TESTFIELD("Plant No.");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Plant No.;
                                                              NOR=Maskinnr.;
                                                              SVE=Maskinnr] }
    { 50  ;   ;Percentage          ;Decimal       ;InitValue=100;
                                                   CaptionML=[ENU=Percentage;
                                                              NOR=Prosent;
                                                              SVE=Procent];
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 80  ;   ;Discipline          ;Code10        ;TableRelation=Discipline;
                                                   OnValidate=BEGIN
                                                                IF Discipline <> '' THEN
                                                                  TESTFIELD("Resource Type", "Resource Type"::Employee);
                                                              END;

                                                   CaptionML=[ENU=Discipline;
                                                              NOR=Disiplin;
                                                              SVE=Disciplin] }
  }
  KEYS
  {
    {    ;Team Code,Resource Type,Resource Code,Plant No.;
                                                   Clustered=Yes }
    {    ;Resource Code,Plant No.                  }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ProjActCapComMgt@1100525001 : Codeunit 11125706;

    PROCEDURE GetResourceDescription@1100525000() : Text[100];
    BEGIN
      EXIT(ProjActCapComMgt.GetResourceDescription(
        DATABASE::"Project Planning Team Resource", "Resource Type", "Resource Code", "Plant No."));
    END;

    PROCEDURE GetNoOfPeriods@1100525001() : Integer;
    VAR
      TeamResPeriodRec@1100525000 : Record 11125714;
    BEGIN
      TeamResPeriodRec.SETRANGE("Team Code", "Team Code");
      TeamResPeriodRec.SETRANGE("Resource Type", "Resource Type");
      TeamResPeriodRec.SETRANGE("Resource Code", "Resource Code");
      TeamResPeriodRec.SETRANGE("Plant No.", "Plant No.");
      EXIT(TeamResPeriodRec.COUNT);
    END;

    PROCEDURE LookupResourceCode@1100525002(EditableMode@1100525000 : Boolean);
    VAR
      EmplRec@1100525001 : Record 5200;
      PlantTypeRec@1100525002 : Record 11012551;
    BEGIN
      CASE "Resource Type" OF
        "Resource Type"::Employee:
          BEGIN
            EmplRec.FILTERGROUP(6);
            //EmplRec.SETFILTER(Schedule, '%1|%2', EmplRec.Schedule::Project, EmplRec.Schedule::Both);
            EmplRec.FILTERGROUP(0);
            EmplRec."No." := "Resource Code";
            IF NOT EditableMode THEN
              PAGE.RUNMODAL(0, EmplRec)
            ELSE BEGIN
              IF PAGE.RUNMODAL(0, EmplRec) = ACTION::LookupOK THEN
                VALIDATE("Resource Code", EmplRec."No.");
            END;
          END;
        "Resource Type"::Plant:
          BEGIN
            PlantTypeRec.FILTERGROUP(6);
            PlantTypeRec.SETRANGE(Bulk, FALSE);
            PlantTypeRec.FILTERGROUP(0);
            PlantTypeRec.Code := "Resource Code";
            IF NOT EditableMode THEN
              PAGE.RUNMODAL(0, PlantTypeRec)
            ELSE BEGIN
              IF PAGE.RUNMODAL(0, PlantTypeRec) = ACTION::LookupOK THEN
                VALIDATE("Resource Code", PlantTypeRec.Code);
            END;
          END;
      END;
    END;

    BEGIN
    END.
  }
}

